#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьФорматВыгрузки(Организация) Экспорт 
	Если Не ПравоДоступа("Чтение", Метаданные.Константы.НастройкиФорматаВыгрузкиОтчетовСтатистики) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ТаблицаФорматов = Константы.НастройкиФорматаВыгрузкиОтчетовСтатистики.Получить().Получить();
	Если ТипЗнч(ТаблицаФорматов) <> Тип("ТаблицаЗначений") Тогда 
		Возврат Неопределено;
	КонецЕсли;
	Форматы = ТаблицаФорматов.НайтиСтроки(Новый Структура("Организация", Организация));
	Если Форматы.Количество() = 1 Тогда 
		Возврат Форматы[0].Формат;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Процедура УстановитьФорматВыгрузки(Организация, Формат) Экспорт 
	Если Формат <> 1 И Формат <> 2 Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Изменение", Метаданные.Константы.НастройкиФорматаВыгрузкиОтчетовСтатистики) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаФорматов = Константы.НастройкиФорматаВыгрузкиОтчетовСтатистики.Получить().Получить();
	Если ТипЗнч(ТаблицаФорматов) <> Тип("ТаблицаЗначений") Тогда 
		ТаблицаФорматов = ПолучитьПустуюТаблицуФорматовВыгрузки();
	КонецЕсли;
	
	Форматы = ТаблицаФорматов.НайтиСтроки(Новый Структура("Организация", Организация));
	Если Форматы.Количество() = 0 Тогда 
		СтрокаФормата = ТаблицаФорматов.Добавить();
	Иначе
		СтрокаФормата = Форматы[0];
	КонецЕсли;
	
	СтрокаФормата.Организация = Организация;
	СтрокаФормата.Формат = Формат;
	Константы.НастройкиФорматаВыгрузкиОтчетовСтатистики.Установить(Новый ХранилищеЗначения(ТаблицаФорматов));
КонецПроцедуры

Функция ПолучитьПустуюТаблицуФорматовВыгрузки()
	ТаблицаФорматовВыгрузки = Новый ТаблицаЗначений;
	ТаблицаФорматовВыгрузки.Колонки.Добавить("Организация");
	ТаблицаФорматовВыгрузки.Колонки.Добавить("Формат");
	Возврат ТаблицаФорматовВыгрузки;
КонецФункции

#КонецОбласти

#КонецЕсли