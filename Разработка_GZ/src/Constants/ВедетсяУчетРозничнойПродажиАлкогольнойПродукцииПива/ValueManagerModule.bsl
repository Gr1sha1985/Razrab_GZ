
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначения.РазделениеВключено() Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("Метаданные", "ОбработкаОтветовЕГАИС");
		
		УстановитьПривилегированныйРежим(Истина);
		Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
		
		Если Задания.Количество() <> 1 Тогда
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Интеграция с ЕГАИС'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегламентныеЗадания.ОбработкаОтветовЕГАИС,,
				НСтр("ru='Не найдено регламентное задание обработки ответов ЕГАИС'"));
			Возврат;
		КонецЕсли;
		
		Задание = Задания[0];
		
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Использование", Значение);
		
		РегламентныеЗаданияСервер.ИзменитьЗадание(Задание.УникальныйИдентификатор, ПараметрыЗадания);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
