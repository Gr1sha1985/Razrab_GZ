#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);	
	Если Не ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	
	ДатаСнятияСУчета 	= ТекущаяДатаСеанса();
	КПП					= РегистрацияВНалоговомОргане.КПП;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьТекстПояснения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДатаСнятияСУчетаПриИзменении(Элемент)
	
	УстановитьТекстПояснения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура СнятьСУчета(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;	
	
	Если СнятьСУчетаНаСервере() Тогда
		Закрыть(ДатаСнятияСУчета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьТекстПояснения()
	
	ТекстПояснения = НСтр("ru = 'Деятельность организации по текущему месту нахождения прекращается.
						|Организация снимается с учета %1 по КПП %2.'");
	ТекстПояснения = СтрШаблон(ТекстПояснения, Формат(ДатаСнятияСУчета, "ДФ=dd.MM.yyyy; ДП="), КПП);
	
	Пояснение = Новый ФорматированнаяСтрока(ТекстПояснения,, ОбщегоНазначенияКлиент.ЦветСтиля("НадписьПоясняющаяГиперссылку"));
	
КонецПроцедуры

&НаСервере
Функция СнятьСУчетаНаСервере()
	
	Попытка
		РегистрацияОбъект = РегистрацияВНалоговомОргане.ПолучитьОбъект();
		РегистрацияОбъект.Заблокировать();
		РегистрацияОбъект.ДатаСнятияСУчета = ДатаСнятияСУчета;
		РегистрацияОбъект.Записать();
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Возникла ошибка при записи даты снятия с учета
		|'") + ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;	
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти