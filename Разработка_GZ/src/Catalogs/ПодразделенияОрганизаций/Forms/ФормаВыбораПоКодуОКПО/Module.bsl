////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	 Если Параметры.Свойство("АвтоТест") Тогда
	 	Возврат;
	 КонецЕсли;	
	  
	 Заголовок = НСтр("ru = 'Подразделения, зарегистрированные в Росстате'");
	
	Если Параметры.Отбор.Свойство("Организация") Тогда
		ОтборОрганизация = Параметры.Отбор.Организация;
		Параметры.Отбор.Удалить("Организация");
	КонецЕсли;
	
	Если Не Справочники.Организации.ИспользуетсяНесколькоОрганизаций() 
		И Не ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ОтборОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		Элементы.ГруппаОтборОрганизация.Доступность = Ложь;
	КонецЕсли;
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	
	ЗаполнитьСписокПодразделений();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ЗаполнитьСписокПодразделений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ЗаполнитьСписокПодразделений();

КонецПроцедуры

&НаКлиенте
Процедура СписокПодразделенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Закрыть(Элемент.ТекущиеДанные.Подразделение);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьСписокПодразделений()
		
	Элементы.Организация.Видимость = НЕ ЗначениеЗаполнено(ОтборОрганизация);	
	СписокПодразделений.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Владелец КАК Организация,
		|	ПодразделенияОрганизаций.КодПоОКПО КАК КодПоОКПО,
		|	ПодразделенияОрганизаций.Наименование КАК Наименование,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.Родитель.Родитель.КодПоОКПО, """") = ПодразделенияОрганизаций.КодПоОКПО
		|				ТОГДА ПодразделенияОрганизаций.Родитель.Родитель.Родитель
		|			КОГДА ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.Родитель.КодПоОКПО, """") = ПодразделенияОрганизаций.КодПоОКПО
		|				ТОГДА ПодразделенияОрганизаций.Родитель.Родитель
		|			КОГДА ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.КодПоОКПО, """") = ПодразделенияОрганизаций.КодПоОКПО
		|				ТОГДА ПодразделенияОрганизаций.Родитель.Наименование
		|			ИНАЧЕ ПодразделенияОрганизаций.Ссылка
		|		КОНЕЦ КАК Справочник.ПодразделенияОрганизаций) КАК Подразделение
		|ПОМЕСТИТЬ ОбособленныеПодразделения
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	(&ВсеОрганизации
		|			ИЛИ ПодразделенияОрганизаций.Владелец = &Организация)
		|	И ПодразделенияОрганизаций.ОбособленноеПодразделение
		|	И ПодразделенияОрганизаций.КодПоОКПО <> """"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КодПоОКПО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГруппировкаПоОКПО.Организация КАК Организация,
		|	ГруппировкаПоОКПО.КодПоОКПО КАК КодПоОКПО,
		|	ГруппировкаПоОКПО.Подразделение КАК Подразделение,
		|	ЕСТЬNULL(ОбособленныеПодразделения.Наименование, """") КАК НаименованиеПодчиненного
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОбособленныеПодразделения.Организация КАК Организация,
		|		ОбособленныеПодразделения.КодПоОКПО КАК КодПоОКПО,
		|		МАКСИМУМ(ОбособленныеПодразделения.Подразделение) КАК Подразделение
		|	ИЗ
		|		ОбособленныеПодразделения КАК ОбособленныеПодразделения
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ОбособленныеПодразделения.Организация,
		|		ОбособленныеПодразделения.КодПоОКПО) КАК ГруппировкаПоОКПО
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОбособленныеПодразделения КАК ОбособленныеПодразделения
		|		ПО ГруппировкаПоОКПО.КодПоОКПО = ОбособленныеПодразделения.КодПоОКПО
		|			И ГруппировкаПоОКПО.Подразделение <> ОбособленныеПодразделения.Подразделение
		|ИТОГИ
		|	МАКСИМУМ(Подразделение)
		|ПО
		|	Организация,
		|	КодПоОКПО";
	
	Запрос.УстановитьПараметр("ВсеОрганизации", НЕ ОтборОрганизацияИспользование);
	Запрос.УстановитьПараметр("Организация", ОтборОрганизация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОрганизация = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаОрганизация.Следующий() Цикл
	
		ВыборкаКодПоОКПО = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ВыборкаКодПоОКПО.Следующий() Цикл

			НоваяСтрока = СписокПодразделений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаКодПоОКПО);
			
			// Заполним список подразделений, входящих в состав группы по ОКПО
			ВыборкаДетальныеЗаписи = ВыборкаКодПоОКПО.Выбрать();
						
			МассивНаименований = Новый Массив;
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Если НЕ ПустаяСтрока(ВыборкаДетальныеЗаписи.НаименованиеПодчиненного) Тогда
					МассивНаименований.Добавить(ВыборкаДетальныеЗаписи.НаименованиеПодчиненного);
				КонецЕсли;	
			КонецЦикла;
			
			НоваяСтрока.СоставПодразделения = СтрСоединить(МассивНаименований, "; ");
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры	
