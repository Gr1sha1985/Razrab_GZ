
&НаКлиенте 
Перем ТекущийТекстНомераСчета; // Текст, набранный в поле ввода номера счета

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ОткрытИзПлатежногоПоручения Тогда
		Элементы.КодГосударственногоОргана.Видимость        = Ложь;
		Элементы.ГосударственныйОрганНаименование.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("КодГосударственногоОргана")
		И ЗначениеЗаполнено(Параметры.КодГосударственногоОргана)
		И Параметры.Свойство("ВидГосударственногоОргана")
		И ЗначениеЗаполнено(Параметры.ВидГосударственногоОргана) Тогда
		КодГосударственногоОргана = Параметры.КодГосударственногоОргана;
		ВидГосударственногоОргана = Параметры.ВидГосударственногоОргана;
	ИначеЕсли Параметры.Свойство("РегистрацияВНалоговомОргане")
		И ЗначениеЗаполнено(Параметры.РегистрацияВНалоговомОргане) Тогда
		КодГосударственногоОргана = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.РегистрацияВНалоговомОргане, "Код");
		ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган;
	Иначе
		ВызватьИсключение НСтр("ru = 'Не указаны реквизиты государственного органа'");
	КонецЕсли;
	
	Если Параметры.ОткрытИзПлатежногоПоручения Тогда
		Заголовок = ЗаголовокДляПлатежногоПоручения(ВидГосударственногоОргана);
	Иначе
		Заголовок = ЗаголовокДляОрганизации(ВидГосударственногоОргана);
	КонецЕсли;
	
	Данные = ДанныеГосударственныхОрганов.ГосударственныйОрган(ВидГосударственногоОргана, КодГосударственногоОргана);
	
	ГосударственныйОрганБанк = Данные.ПлатежныеРеквизиты.Банк;
	ГосударственныйОрганБИК  = Данные.ПлатежныеРеквизиты.БИК;
	ГосударственныйОрганИНН  = Данные.ИНН;
	ГосударственныйОрганКПП  = Данные.КПП;
	ГосударственныйОрганНаименование    = Данные.ПолноеНаименование;
	ГосударственныйОрганРасчетныйСчет   = Данные.ПлатежныеРеквизиты.РасчетныйСчет;
	ГосударственныйОрганТекстПолучателя = Данные.ПлатежныеРеквизиты.ПолучательПлатежа;
	ДеятельностьБанкаПрекращена = ДеятельностьБанкаПрекращена(ГосударственныйОрганБИК);
	
	Если Параметры.Свойство("НаименованиеГосударственногоОргана")
		И НЕ ЗначениеЗаполнено(ГосударственныйОрганНаименование) Тогда
		ГосударственныйОрганНаименование = Параметры.НаименованиеГосударственногоОргана;
	КонецЕсли;
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	Если НЕ ПравоДоступа("Изменение", Метаданные.Справочники.Контрагенты) Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ЦветВыделенияНекорректногоЗначение = ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой;
	
	ПодсказкаБанк = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляБанка(ДеятельностьБанкаПрекращена);
	Элементы.ПодсказкаБанк.Видимость = ЗначениеЗаполнено(ПодсказкаБанк);
	ПодсказкаНомерСчета = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляНомерСчета(
		ГосударственныйОрганРасчетныйСчет, ГосударственныйОрганБИК, Истина, ЦветВыделенияНекорректногоЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
	
		ЗаписатьДанныеОПлатежныхРеквизитах();
		
		Закрыть(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГосударственныйОрганБанкПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ГосударственныйОрганБанк) Тогда
		ГосударственныйОрганБИК = "";
		ДеятельностьБанкаПрекращена = Ложь;
	КонецЕсли;
	
	ПодсказкаБанк = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляБанка(ДеятельностьБанкаПрекращена);
	Элементы.ПодсказкаБанк.Видимость = ЗначениеЗаполнено(ПодсказкаБанк);
	
	Если ЗначениеЗаполнено(ГосударственныйОрганРасчетныйСчет) Тогда
		ПодсказкаНомерСчета = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляНомерСчета(
			ГосударственныйОрганРасчетныйСчет, ГосударственныйОрганБИК, Истина, ЦветВыделенияНекорректногоЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГосударственныйОрганБанкОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РеквизитыБанка = БанковскиеСчетаФормыКлиент.ПолучитьДанныеБанка(ВыбранноеЗначение);
	ВыбранноеЗначение = РеквизитыБанка.Ссылка;
	
	Если ЗначениеЗаполнено (РеквизитыБанка.Ссылка) Тогда
		ГосударственныйОрганБИК = РеквизитыБанка.Код;
		ДеятельностьБанкаПрекращена = РеквизитыБанка.ДеятельностьПрекращена;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ГосударственныйОрганБанкАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	БанковскиеСчетаФормыКлиент.БанкАвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка, ПараметрыПолученияДанных);

КонецПроцедуры

&НаКлиенте
Процедура НалоговыйОрганРасчетныйСчетПриИзменении(Элемент)

	ГосударственныйОрганРасчетныйСчет = СтрЗаменить(ГосударственныйОрганРасчетныйСчет," ","");
	
	ПодсказкаНомерСчета = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляНомерСчета(
		ГосударственныйОрганРасчетныйСчет, ГосударственныйОрганБИК, Истина, ЦветВыделенияНекорректногоЗначение)
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыйОрганРасчетныйСчетИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ТекущийТекстНомераСчета = СтрЗаменить(Текст," ","");
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПодсказкуНомераСчета", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьДанныеОПлатежныхРеквизитах();
	
	ГосударственныйОрган = ДанныеГосударственныхОрганов.ГосударственныйОрган(ВидГосударственногоОргана, КодГосударственногоОргана);
	ГосударственныйОрган.Вид = ВидГосударственногоОргана;
	ГосударственныйОрган.Код = КодГосударственногоОргана;
	ГосударственныйОрган.ПолноеНаименование = ГосударственныйОрганНаименование;
	ГосударственныйОрган.ИНН = ГосударственныйОрганИНН;
	ГосударственныйОрган.КПП = ГосударственныйОрганКПП;
	Если НЕ ЗначениеЗаполнено(ГосударственныйОрган.Наименование) Тогда
		ГосударственныйОрган.Наименование = ГосударственныйОрганНаименование;
	КонецЕсли;
	ГосударственныйОрган.ПлатежныеРеквизиты.ПолучательПлатежа = ГосударственныйОрганТекстПолучателя;
	ГосударственныйОрган.ПлатежныеРеквизиты.РасчетныйСчет = ГосударственныйОрганРасчетныйСчет;
	ГосударственныйОрган.ПлатежныеРеквизиты.КоррСчет = "";
	ГосударственныйОрган.ПлатежныеРеквизиты.БИК = ГосударственныйОрганБИК;
	
	ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(ГосударственныйОрган);
	
КонецПроцедуры

Функция ДеятельностьБанкаПрекращена(БИК)
	
	Результат = Ложь;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КлассификаторБанков.ДеятельностьПрекращена
	|ИЗ
	|	Справочник.КлассификаторБанков КАК КлассификаторБанков
	|ГДЕ
	|	КлассификаторБанков.Код = &БИК";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("БИК", БИК);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.ДеятельностьПрекращена;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_УстановитьПодсказкуНомераСчета()
	
	Если Не БанковскиеПравила.ПроверитьДлинуНомераСчета(ТекущийТекстНомераСчета) Тогда
		ПодсказкаНомерСчета = БанковскиеСчетаФормыКлиент.ПодсказкаВводаПоляНомерСчета(ТекущийТекстНомераСчета, ГосударственныйОрганБИК);
	Иначе
		ПодсказкаНомерСчета = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляНомерСчета(
			ТекущийТекстНомераСчета, ГосударственныйОрганБИК, Истина, ЦветВыделенияНекорректногоЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаголовокДляПлатежногоПоручения(ВидГосударственногоОргана)
	
	Если ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган Тогда
		Заголовок = НСтр("ru = 'Получатель платежа (налоговая инспекция)'");
	ИначеЕсли ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганПФР Тогда
		Заголовок = НСтр("ru = 'Получатель платежа (отделение ПФР)'");
	ИначеЕсли ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганФСС Тогда
		Заголовок = НСтр("ru = 'Получатель платежа (отделение ФСС)'");
	Иначе
		Заголовок = НСтр("ru = 'Получатель платежа'");
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаголовокДляОрганизации(ВидГосударственногоОргана)
	
	Если ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган Тогда
		Заголовок = НСтр("ru = 'Платежные реквизиты налоговой инспекции'");
	ИначеЕсли ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганПФР Тогда
		Заголовок = НСтр("ru = 'Платежные реквизиты отделения ПФР'");
	ИначеЕсли ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганФСС Тогда
		Заголовок = НСтр("ru = 'Платежные реквизиты отделения ФСС'");
	Иначе
		Заголовок = НСтр("ru = 'Платежные реквизиты'");
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции




#КонецОбласти