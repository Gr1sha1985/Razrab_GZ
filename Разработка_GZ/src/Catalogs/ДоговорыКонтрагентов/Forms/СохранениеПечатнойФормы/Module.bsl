#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// заполнение списка форматов
	Справочники.ДоговорыКонтрагентов.ЗаполнитьПоддерживаемыеФорматыСохранения(ВыбранныеФорматыСохранения);
	
	ДоговорКонтрагента = Параметры.ОбъектПечати;
	
	// заполнение списка выбора для присоединения файлов к объекту
	Элементы.ВыбранныйОбъект.СписокВыбора.Добавить(ДоговорКонтрагента);

	// место сохранения по умолчанию
	Если ПравоДоступа("Добавление", Метаданные.Справочники.ДоговорыКонтрагентовПрисоединенныеФайлы) Тогда
		ВариантСохранения = "Присоединить";
	Иначе
		ВариантСохранения = "СохранитьВПапку";
		Элементы.ВариантСохранения.Доступность = Ложь;
	КонецЕсли;
	
	// объект для присоединения по умолчанию
	Если Элементы.ВыбранныйОбъект.СписокВыбора.Количество() > 0 Тогда
		ВыбранныйОбъект = Элементы.ВыбранныйОбъект.СписокВыбора[0].Значение;
	Иначе
		Элементы.ВариантСохранения.Видимость = Ложь;
		Элементы.ПапкаДляСохраненияФайлов.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	КонецЕсли;
	
	Элементы.ВыбранныйОбъект.ТолькоПросмотр = Элементы.ВыбранныйОбъект.СписокВыбора.Количество() = 1;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	ФорматыСохраненияИзНастроек = Настройки["ВыбранныеФорматыСохранения"];
	Если ФорматыСохраненияИзНастроек <> Неопределено Тогда
		Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл 
			ФорматИзНастроек = ФорматыСохраненияИзНастроек.НайтиПоЗначению(ВыбранныйФормат.Значение);
			Если ФорматИзНастроек <> Неопределено Тогда
				ВыбранныйФормат.Пометка = ФорматИзНастроек.Пометка;
			КонецЕсли;
		КонецЦикла;
		Настройки.Удалить("ВыбранныеФорматыСохранения");
	КонецЕсли;
	
	Если Элементы.ВыбранныйОбъект.СписокВыбора.Количество() = 0 Тогда
		НастройкаВариантСохранения = Настройки["ВариантСохранения"];
		Если НастройкаВариантСохранения <> Неопределено Тогда
			Настройки.Удалить("ВариантСохранения");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьМестаСохранения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантСохраненияПриИзменении(Элемент)
	УстановитьВидимостьМестаСохранения();
	ОчиститьСообщения();
КонецПроцедуры

&НаКлиенте
Процедура ПапкаДляСохраненияФайловНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораПапки = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Если Не ПустаяСтрока(ВыбраннаяПапка) Тогда
		ДиалогВыбораПапки.Каталог = ВыбраннаяПапка;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПапкаДляСохраненияФайловНачалоВыбораЗавершение", ЭтотОбъект);
	ДиалогВыбораПапки.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаДляСохраненияФайловНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено
		И ВыбранныеФайлы.Количество()>0 Тогда
		
		ВыбраннаяПапка = ВыбранныеФайлы.Получить(0);
		ОчиститьСообщения();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныйОбъектОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	#Если Не ВебКлиент Тогда
	Если ВариантСохранения = "СохранитьВПапку" И ПустаяСтрока(ВыбраннаяПапка) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Необходимо указать папку.'"),,"ВыбраннаяПапка");
		Возврат;
	КонецЕсли;
	#КонецЕсли
		
	ФорматыСохранения = Новый Массив;
	
	Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл
		Если ВыбранныйФормат.Пометка Тогда
			ФорматыСохранения.Добавить(ВыбранныйФормат.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ФорматыСохранения.Количество() = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Необходимо указать как минимум один из предложенных форматов.'"));
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ВариантСохранения", ВариантСохранения);
	РезультатВыбора.Вставить("ПапкаДляСохранения", ВыбраннаяПапка);
	РезультатВыбора.Вставить("ОбъектДляПрикрепления", ВыбранныйОбъект);
	РезультатВыбора.Вставить("ФорматыСохранения", ФорматыСохранения);
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьМестаСохранения()
	
	ПрисоединитьВыбранныйФайл = (ВариантСохранения = "Присоединить");
	
	#Если ВебКлиент Тогда
	Элементы.ПапкаДляСохраненияФайлов.Видимость = Ложь;
	Элементы.ВыбранныйОбъект.Видимость          = Истина;
	Элементы.ВыбранныйОбъект.Доступность        = ПрисоединитьВыбранныйФайл;
	#Иначе
	Элементы.ВыбранныйОбъект.Видимость          = ПрисоединитьВыбранныйФайл;
	Элементы.ПапкаДляСохраненияФайлов.Видимость = НЕ ПрисоединитьВыбранныйФайл;
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти
