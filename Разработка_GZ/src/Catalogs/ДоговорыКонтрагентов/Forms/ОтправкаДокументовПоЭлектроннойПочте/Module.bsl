
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметры.ОбъектОтправки) Тогда
		ВызватьИсключение НСтр("ru='Непосредственное открытие этой формы не предусмотрено.'");
	КонецЕсли;
	
	Список.Добавить("Договор", НСтр("ru = 'Договор'"), Истина);
	Если ТипЗнч(Параметры.ОбъектОтправки) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		Список.Добавить("ПриложениеКДоговору", НСтр("ru = 'Приложение к договору'"), Истина);
	КонецЕсли;
	
	Справочники.ДоговорыКонтрагентов.ЗаполнитьПоддерживаемыеФорматыСохранения(ФорматыСохранения);
	
	НастройкаСпискаФорм = ХранилищеОбщихНастроек.Загрузить("КомандыОтправкиДоговорыКонтрагентов");
	Если НастройкаСпискаФорм <> Неопределено Тогда
		Для Каждого ВыбранныйКоманда Из НастройкаСпискаФорм Цикл 
			Команда = Список.НайтиПоЗначению(ВыбранныйКоманда.Значение);
			Если Команда <> Неопределено
				И Команда.Представление = ВыбранныйКоманда.Представление Тогда
				Команда.Пометка = ВыбранныйКоманда.Пометка;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Список.Количество() = 1 Тогда
		СценарийОтправкиОднойПечатнойФормы = Истина;
		Список[0].Пометка = Истина;
		ТекущаяГруппа = "ГруппаФорматы";
	Иначе
		СценарийОтправкиОднойПечатнойФормы = Ложь;
		ТекущаяГруппа = "ГруппаВыборПечатнойФормы";
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВыборФормата();
	СформироватьПредставлениеВыбранныхФорматов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьКомандыОтправки(Список);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФорматВложенийНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Для Каждого ФорматСохранения Из ФорматыСохранения Цикл
		ВыбранныйФормат = ВыбранныеФорматыСохранения.НайтиПоЗначению(ФорматСохранения.Значение);
		Если ВыбранныйФормат <> Неопределено Тогда
			ФорматСохранения.Пометка = Истина;
		Иначе
			ФорматСохранения.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ТекущаяГруппа = "ГруппаФорматы";
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ТекущаяГруппа = "ГруппаФорматы" Тогда
		ВыбранныеФорматыСохранения.Очистить();
		Для Каждого ФорматСохранения Из ФорматыСохранения Цикл
			Если ФорматСохранения.Пометка Тогда
				ВыбранныеФорматыСохранения.Добавить(ФорматСохранения.Значение, ФорматСохранения.Представление, Истина);
			КонецЕсли;
		КонецЦикла;
		
		Если ВыбранныеФорматыСохранения.Количество() = 0 Тогда
			ВыбранныеФорматыСохранения.Добавить(ФорматыСохранения[0].Значение, ФорматыСохранения[0].Представление, Истина);
		КонецЕсли;
		
		Если НЕ СценарийОтправкиОднойПечатнойФормы Тогда
			ТекущаяГруппа = "ГруппаВыборПечатнойФормы";
			СформироватьПредставлениеВыбранныхФорматов();
			УправлениеФормой(ЭтотОбъект);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ОтмеченныеОбъектыОтправки = Новый Массив;
	Для каждого ЭлементСписка Из Список Цикл
		Если ЭлементСписка.Пометка Тогда
			ОтмеченныеОбъектыОтправки.Добавить(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ОтмеченныеОбъектыОтправки.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='Необходимо выбрать, как минимум, одну печатную форму.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОтмеченныеФорматыСохранения = Новый Массив;
	
	Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл
		Если ВыбранныйФормат.Пометка Тогда
			ОтмеченныеФорматыСохранения.Добавить(ВыбранныйФормат.Значение);
		КонецЕсли;
	КонецЦикла;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ОбъектыОтправки",   ОтмеченныеОбъектыОтправки);
	РезультатВыбора.Вставить("ФорматыСохранения", ОтмеченныеФорматыСохранения);
	РезультатВыбора.Вставить("УпаковатьВАрхив",   УпаковатьВАрхив);
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Если ТекущаяГруппа = "ГруппаФорматы" И НЕ СценарийОтправкиОднойПечатнойФормы Тогда
		ТекущаяГруппа = "ГруппаВыборПечатнойФормы";
		УправлениеФормой(ЭтотОбъект);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВыборФормата()
	
	ЕстьВыбранныйФормат = Ложь;
	Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл
		Если ВыбранныйФормат.Пометка Тогда
			ЕстьВыбранныйФормат = Истина;
			ФорматСохранения = ФорматыСохранения.НайтиПоЗначению(ВыбранныйФормат.Значение);
			Если ФорматСохранения <> Неопределено Тогда
				ФорматСохранения.Пометка = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьВыбранныйФормат Тогда
		ФорматСохранения = ФорматыСохранения[0];
		ВыбранныеФорматыСохранения.Добавить(ФорматСохранения.Значение, ФорматСохранения.Представление, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредставлениеВыбранныхФорматов()
	
	ФорматВложений = "";
	Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл
		Если ВыбранныйФормат.Пометка Тогда
			Если Не ПустаяСтрока(ФорматВложений) Тогда
				ФорматВложений = ФорматВложений + ", ";
			КонецЕсли;
			ФорматВложений = ФорматВложений + ВыбранныйФормат.Представление;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьКомандыОтправки(СписокКоманд)
	
	ХранилищеОбщихНастроек.Сохранить("КомандыОтправкиДоговорыКонтрагентов", , СписокКоманд);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Для Каждого ГруппаСУправляемойВидимостью Из ГруппыСУправляемойВидимостью() Цикл
		Элементы[ГруппаСУправляемойВидимостью].Видимость = Ложь;
	КонецЦикла;
	
	Элементы[Форма.ТекущаяГруппа].Видимость = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ГруппыСУправляемойВидимостью()
	
	Результат = Новый Массив;
	
	Результат.Добавить("ГруппаФорматы");
	Результат.Добавить("ГруппаВыборПечатнойФормы");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
