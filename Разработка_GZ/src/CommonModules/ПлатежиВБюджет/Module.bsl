#Область ПрограммныйИнтерфейс

Функция ПроверитьДублиУИН(ИдентификаторПлатежа, Ссылка) Экспорт
	
	Результат = Новый Массив; // См. НовыйОписаниеОшибки()
	
	Дубли = ПлатежиВБюджетПереопределяемый.ДублиУИН(ИдентификаторПлатежа, Ссылка);
	Если Дубли.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	ШаблонТекста = НСтр("ru = 'Возможно, уникальный идентификатор платежа указан ошибочно.
        |
        |[ГдеЕщеУказанЭтотИдентификатор]
        |
        |Одинаковые идентификаторы следует указывать только при частичной оплате.'");
	
	
	Если Дубли.Количество() = 1 Тогда
		
		ГдеЕщеУказанЭтотИдентификатор = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Этот же идентификатор платежа указан в другом документе:
				|%1.'"),
			Дубли[0]);
		
	ИначеЕсли Дубли.Количество() = 2 Тогда
		
		ГдеЕщеУказанЭтотИдентификатор = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Этот же идентификатор платежа указан в других документах:
				|- %1
				|- %2.'"),
			Дубли[0],
			Дубли[1]);
		
	ИначеЕсли Дубли.Количество() = 3 Тогда
		
		ГдеЕщеУказанЭтотИдентификатор = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Этот же идентификатор платежа указан в других документах:
				|- %1
				|- %2
				|- %3.'"),
			Дубли[0],
			Дубли[1],
			Дубли[2]);
		
	Иначе
		
		ТекстЕщеДокументов = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 более ранний документ;;%1 более ранних документа;%1 более ранних документов;%1 более ранних документа'"),
			Дубли.Количество() - 2);
		
		ГдеЕщеУказанЭтотИдентификатор = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Этот же идентификатор платежа указан в других документах:
				|- %1
				|- %2
				|  и еще %3.'"),
			Дубли[0],
			Дубли[1],
			ТекстЕщеДокументов);
		
	КонецЕсли;
	
	ПараметрыТекста = Новый Структура;
	ПараметрыТекста.Вставить("ГдеЕщеУказанЭтотИдентификатор", ГдеЕщеУказанЭтотИдентификатор);
	
	ТекстОшибки = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ПараметрыТекста);
	
	ОписаниеОшибки = ПлатежиВБюджетКлиентСервер.НовыйОписаниеОшибки(ТекстОшибки, "ИдентификаторПлатежа");
	Результат.Добавить(ОписаниеОшибки);
	
	Возврат Результат;
	
КонецФункции

Функция КодыЭлементаДоходаПоУровнюБюджета(УровеньБюджета) Экспорт
	
	Результат = Новый Массив;
	Если УровеньБюджета = Перечисления.УровниБюджетов.ФедеральныйБюджет Тогда
		Результат.Добавить(ПлатежиВБюджетКлиентСервер.КодЭлементаДоходаФедеральногоБюджета());
	ИначеЕсли УровеньБюджета = Перечисления.УровниБюджетов.РегиональныйБюджет Тогда
		Результат.Добавить(ПлатежиВБюджетКлиентСервер.КодЭлементаДоходаРегиональногоБюджета());
	Иначе
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			Результат,
			СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
				ПлатежиВБюджетКлиентСервер.КодыЭлементаДоходаМестногоБюджета()));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
