#Область ПрограммныйИнтерфейс

// Возвращает список параметров для вызова метода взаимодействия по его имени.
//
// Параметры:
//   МетодВзаимодействия - имя метода из списка методов ИнтеграцияСПлатформойСамозанятые.МетодыВзаимодействия()
//
// Возвращаемое значение:
//  Структура - ключом является имя параметра, значение - пустым значением нужного типа
//
Функция НовыеПараметрыМетодаВзаимодействия(МетодВзаимодействия) Экспорт
	
	// Подключение и отключение сервиса, получение списка разрешений
	
	Если МетодВзаимодействия = "КодПодтверждения" Тогда
		
		Возврат НовыеПараметры_КодПодтверждения(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "ЗапросНаПривязку" Тогда
		
		Возврат НовыеПараметры_ЗапросНаПривязку(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "СтатусЗапросаНаПривязку" Тогда
		
		Возврат НовыеПараметры_СтатусЗапроса(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "ЗапросНаОтвязку" Тогда
		
		Возврат НовыеПараметры(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "СписокРазрешений" Тогда
		
		Возврат НовыеПараметры(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "ЗапросНаИзменениеРазрешений" Тогда
		
		Возврат НовыеПараметры_ЗапросНаИзменениеРазрешений(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "СтатусЗапросаНаИзменениеРазрешений" Тогда
		
		Возврат НовыеПараметры_СтатусЗапроса(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "ДетальныйСтатусНалогоплательщика" Тогда
		
		Возврат НовыеПараметры(МетодВзаимодействия);
		
	// Регистрация и сторнирование доходов
		
	ИначеЕсли МетодВзаимодействия = "РегистрацияДохода" Тогда
		
		Возврат НовыеПараметры_РегистрацияДохода(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "СторнированиеДохода" Тогда
		
		Возврат НовыеПараметры_СторнированиеДохода(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "СтатусыЧеков" Тогда
		
		Возврат НовыеПараметры_СтатусыЧеков(МетодВзаимодействия);
		
	// Состояние лицевого счета, информация о зарегистрированных доходах за период
	
	ИначеЕсли МетодВзаимодействия = "СостояниеЛицевогоСчета" Тогда
		
		Возврат НовыеПараметры(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "СписокДоходов" Тогда
		
		Возврат НовыеПараметры_СписокДоходов(МетодВзаимодействия);
		
	// Начисленный налог, получение реквизитов квитанции на уплату
	
	ИначеЕсли МетодВзаимодействия = "ИнформацияОНачислениях" Тогда
		
		Возврат НовыеПараметры_ИнформацияОНачислениях(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "ИнформацияОНалоговыхНачислениях" Тогда
		
		Возврат НовыеПараметры(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "СписокКвитанцийНаУплатуНалога" Тогда
		
		Возврат НовыеПараметры_СписокКвитанцийНаУплатуНалога(МетодВзаимодействия);
		
	// Справки
	
	ИначеЕсли МетодВзаимодействия = "СправкаОПостановкеНаУчет" Тогда
		
		Возврат НовыеПараметры_Справки(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "СправкаОДоходах" Тогда
		
		Возврат НовыеПараметры_Справки(МетодВзаимодействия);
		
	// Оповещения
	
	ИначеЕсли МетодВзаимодействия = "КоличествоНепрочитанныхОповещений" Тогда
		
		Возврат НовыеПараметры(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "СписокОповещений" Тогда
		
		Возврат НовыеПараметры_СписокОповещений(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "ПометитьОповещениеПрочитанным" Тогда
		
		Возврат НовыеПараметры_ИзменениеСтатусаВыбранныхОповещений(МетодВзаимодействия);
		
	ИначеЕсли МетодВзаимодействия = "ПоместитьОповещениеВАрхив" Тогда
		
		Возврат НовыеПараметры_ИзменениеСтатусаВыбранныхОповещений(МетодВзаимодействия);
		
	Иначе
		
		ВызватьИсключение СтрШаблон("ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия():
			|Метод взаимодействия %1 не определен", МетодВзаимодействия);
		
	КонецЕсли;
	
КонецФункции

// Возвращает перечисление статусов выполнения методов взаимодействия.
//
// Возвращаемое значение:
//  Структура
//
Функция СтатусыЗапросов() Экспорт
	
	СтатусыЗапросов = Новый Структура;
	СтатусыЗапросов.Вставить("Выполняется", "Выполняется");
	СтатусыЗапросов.Вставить("Выполнено", "Выполнено");
	СтатусыЗапросов.Вставить("Ошибка", "Ошибка");
	СтатусыЗапросов.Вставить("Отменено", "Отменено");
	
	Возврат СтатусыЗапросов;
	
КонецФункции

// Возвращает перечисление статусов оповещений сервиса Мой налог.
//
// Возвращаемое значение:
//  Структура
//
Функция СтатусыОповещений() Экспорт
	
	СтатусыСообщений = Новый Структура;
	СтатусыСообщений.Вставить("Новое", "Новое");
	СтатусыСообщений.Вставить("Прочитано", "Прочитано");
	СтатусыСообщений.Вставить("ВАрхиве", "ВАрхиве");
	
	Возврат СтатусыСообщений;
	
КонецФункции

// Возвращает перечисление типов оповещений сервиса Мой налог.
//
// Возвращаемое значение:
//  Структура
//
Функция ТипыОповещений() Экспорт
	
	ТипыОповещений = Новый Структура;
	ТипыОповещений.Вставить("ОплатаНалога", "ОплатаНалога");
	ТипыОповещений.Вставить("ЗапросНаПодключение", "ЗапросНаПодключение");
	ТипыОповещений.Вставить("ИзменениеРазрешений", "ИзменениеРазрешений");
	ТипыОповещений.Вставить("ОтвязкаОтПартнера", "ОтвязкаОтПартнера");
	ТипыОповещений.Вставить("Информация", "Информация");
	
	Возврат ТипыОповещений;
	
КонецФункции

// Возвращает перечисление статусов чеков НПД
//
// Возвращаемое значение:
//   Структура
//
Функция СтатусыЧеков() Экспорт
	
	СтатусыЧеков = Новый Структура;
	СтатусыЧеков.Вставить("Зарегистрирован", "Зарегистрирован");
	СтатусыЧеков.Вставить("ВыполняетсяРегистрация", "ВыполняетсяРегистрация");
	СтатусыЧеков.Вставить("ОшибкаПриРегистрации", "ОшибкаПриРегистрации");
	СтатусыЧеков.Вставить("ОжидаетОтправкиВФНС", "ОжидаетОтправкиВФНС");
	СтатусыЧеков.Вставить("Аннулирован", "Аннулирован");
	СтатусыЧеков.Вставить("ВыполняетсяАннулирование", "ВыполняетсяАннулирование");
	СтатусыЧеков.Вставить("ОшибкаПриАннулировании", "ОшибкаПриАннулировании");
	
	Возврат СтатусыЧеков;
	
КонецФункции

// Возвращает перечень кодов разрешений на действия партнера от имени налогоплательщика.
//
// Возвращаемое значение:
//   Структура
//
Функция ВидыРазрешенийНаДействияПартнера() Экспорт
	
	Разрешения = Новый Структура;
	Разрешения.Вставить("ОтражениеДохода", "ОтражениеДохода");
	Разрешения.Вставить("ПолучениеИнформацииПоНалоговымНачислениям", "ПолучениеИнформацииПоНалоговымНачислениям");
	Разрешения.Вставить("ОплатаНалоговыхНачислений", "ОплатаНалоговыхНачислений");
	Разрешения.Вставить("ПолучениеИнформацииПоДоходам", "ПолучениеИнформацииПоДоходам");
	Разрешения.Вставить("ПолучениеCводнойИнформацииОДоходе", "ПолучениеCводнойИнформацииОДоходе");
	Разрешения.Вставить("КорректировкаСведенийОДоходахПоданныхПартнером", "КорректировкаСведенийОДоходахПоданныхПартнером");
	Разрешения.Вставить("КорректировкаСведенийОДоходахБезОграничений", "КорректировкаСведенийОДоходахБезОграничений");
	
	Возврат Разрешения;
	
КонецФункции

// Возвращает имя события для записей событий в журнал регистрации
//
// Возвращаемое значение:
//   Строка - имя события
//
Функция ИмяСобытияЖурналаРегистрации() Экспорт
	
	Возврат НСтр("ru = 'Интеграция с платформой Самозанятые'", 
		ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
КонецФункции

// Возвращает адрес сервиса
//
// Возвращаемое значение:
//   Строка - адрес страницы
//
Функция АдресСервиса() Экспорт
	
	Возврат "https://lknpd.nalog.ru";
	
КонецФункции

// Возвращает адрес начальной страницы сервиса
//
// Возвращаемое значение:
//   Строка - адрес страницы
//
Функция АдресСтраницыАутентификацииСервиса() Экспорт
	
	Возврат АдресСервиса() + "/auth/login";
	
КонецФункции

// Возвращает адрес страницы с настройками сервиса
//
// Возвращаемое значение:
//   Строка - адрес страницы
//
Функция АдресСтраницыНастройкиСервиса() Экспорт
	
	Возврат АдресСервиса() + "/settings";
	
КонецФункции

// Возвращает адрес страницы со списком партнеров сервиса
//
// Возвращаемое значение:
//   Строка - адрес страницы
//
Функция АдресСтраницыПартнеры() Экспорт
	
	Возврат АдресСервиса() + "/settings/partners";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеПараметры_КодПодтверждения(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("НомерТелефона", "");
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_ЗапросНаПривязку(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("НомерТелефона", "");
	ПараметрыМетода.Вставить("ИдентификаторКодаПодтверждения", "");
	ПараметрыМетода.Вставить("КодПодтверждения", "");
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_СтатусЗапроса(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("Идентификатор", "");
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_РегистрацияДохода(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("УИДЧека", ""); // уникальный номер документа в программе
	ПараметрыМетода.Вставить("ДатаРасчета", Дата(1, 1, 1));
	ПараметрыМетода.Вставить("ДатаФормирования", Дата(1, 1, 1));
	ПараметрыМетода.Вставить("Контрагент", ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
	ПараметрыМетода.Вставить("Услуги", Неопределено);
	ПараметрыМетода.Вставить("СуммаДокумента", 0);
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_СторнированиеДохода(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("УИДЧека", ""); // уникальный номер документа в программе
	ПараметрыМетода.Вставить("ИдентификаторЧека", "");
	ПараметрыМетода.Вставить("ПричинаОтменыЧека", "");
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_СтатусыЧеков(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("Чеки", Новый Массив); // Массив номеров чеков.
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_СписокДоходов(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("НачалоПериода", Дата(1, 1, 1));
	ПараметрыМетода.Вставить("КонецПериода", Дата(1, 1, 1));
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_ИнформацияОНачислениях(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("Период", Дата(1, 1, 1));
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_СписокКвитанцийНаУплатуНалога(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("НачалоПериода", Дата(1, 1, 1));
	ПараметрыМетода.Вставить("КонецПериода", Дата(1, 1, 1));
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_Справки(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("Период", Дата(1, 1, 1));
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_СписокОповещений(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("ПолучатьПрочитанные", Ложь);
	ПараметрыМетода.Вставить("ПолучатьАрхивные", Ложь);
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_ИзменениеСтатусаВыбранныхОповещений(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("Оповещения", Неопределено); // Массив идентификаторов оповещений
	
	Возврат ПараметрыМетода;
	
КонецФункции

Функция НовыеПараметры_ЗапросНаИзменениеРазрешений(МетодВзаимодействия)
	
	ПараметрыМетода = НовыеПараметры(МетодВзаимодействия);
	ПараметрыМетода.Вставить("Разрешения", Новый Массив); // Массив из ВидыРазрешенийНаДействияПартнера
	
	Возврат ПараметрыМетода;
	
КонецФункции

// Возвращает список параметров по умолчанию
//
// Возвращаемое значение:
//  Структура
//
Функция НовыеПараметры(МетодВзаимодействия)
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("МетодВзаимодействия", МетодВзаимодействия);
	ПараметрыМетода.Вставить("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	
	Возврат ПараметрыМетода;
	
КонецФункции

#КонецОбласти
