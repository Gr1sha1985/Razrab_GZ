
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для справочника Организации
//
// Клиент-серверные методы форм справочника Организации
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает сокращенное наименование по ФИО предпринимателя.
//
// Параметры:
//   Фамилия  - Строка - Фамилия индивидуального предпринимателя.
//   Имя      - Строка - Имя индивидуального предпринимателя.
//   Отчество - Строка - Отчество индивидуального предпринимателя.
//
// Возвращаемое значение: 
//   Строка - Сокращенное наименование организации, полученное из ФИО индивидуального предпринимателя.
//
Функция СокращенноеНаименованиеИндивидуальногоПредпринимателя(Фамилия, Имя, Отчество) Экспорт
	
	Если ПустаяСтрока(Фамилия) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'ИП %1%2%3'"),
			СокрЛП(Фамилия),
			?(ПустаяСтрока(СокрЛП(Имя)),      "", " " + Лев(СокрЛП(Имя), 1) + "."),
			?(ПустаяСтрока(СокрЛП(Отчество)), "", " " + Лев(СокрЛП(Отчество), 1) + "."));
	
КонецФункции

// Возвращает полное наименование по ФИО предпринимателя.
//
// Параметры:
//   Фамилия  - Строка - Фамилия индивидуального предпринимателя.
//   Имя      - Строка - Имя индивидуального предпринимателя.
//   Отчество - Строка - Отчество индивидуального предпринимателя.
//
// Возвращаемое значение: 
//   Строка - Полное наименование организации, полученное из ФИО индивидуального предпринимателя.
//
Функция ПолноеНаименованиеИндивидуальногоПредпринимателя(Фамилия, Имя, Отчество) Экспорт
	
	Если ПустаяСтрока(Фамилия) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Индивидуальный предприниматель %1%2%3'"),
			СокрЛП(Фамилия),
			?(ПустаяСтрока(СокрЛП(Имя)),      "", " " + СокрЛП(Имя)),
			?(ПустаяСтрока(СокрЛП(Отчество)), "", " " + СокрЛП(Отчество)));
	
КонецФункции

// Возвращает наименование по ФИО предпринимателя.
//
// Параметры:
//   Фамилия  - Строка - Фамилия индивидуального предпринимателя.
//   Имя      - Строка - Имя индивидуального предпринимателя.
//   Отчество - Строка - Отчество индивидуального предпринимателя.
//
// Возвращаемое значение: 
//   Строка - Наименование организации, полученное из ФИО индивидуального предпринимателя.
//
Функция НаименованиеИндивидуальногоПредпринимателя(Фамилия, Имя, Отчество) Экспорт
	
	Если ПустаяСтрока(Фамилия) И ПустаяСтрока(Имя) И ПустаяСтрока(Отчество) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1%2%3ИП'"),
			?(ПустаяСтрока(СокрЛП(Фамилия)),  "", СокрЛП(Фамилия) + " "),
			?(ПустаяСтрока(СокрЛП(Имя)),      "", Лев(СокрЛП(Имя), 1) + ". "),
			?(ПустаяСтрока(СокрЛП(Отчество)), "", Лев(СокрЛП(Отчество), 1) + ". "));
			
КонецФункции

// Проверяет, что полное наименование организации соответствует сокращенному и не изменялось пользователем.
//
// Параметры:
//   СокращенноеНаименование - Строка - Сокращенное наименование организации.
//   ПолноеНаименование - Строка - Полное наименование организации.
//
// Возвращаемое значение: 
//   Булево - результат сравнения сокращенного и полного наименования.
//
Функция ПолноеНаименованиеСоответствуетСокращенномуНаименованию(СокращенноеНаименование, ПолноеНаименование) Экспорт
	
	ПолноеНаименованиеПоСокращенному = ПолноеНаименованиеПоСокращенномуНаименованию(СокращенноеНаименование);
	Возврат СокрЛП(ПолноеНаименованиеПоСокращенному) = СокрЛП(ПолноеНаименование);
	
КонецФункции

// Возвращает полное наименование организации по сокращенному.
//
// Параметры:
//   СокращенноеНаименование - Строка - Сокращенное наименование организации.
//
// Возвращаемое значение: 
//   НаименованиеПолное - Строка - Полное наименование организации, полученное из сокращенного.
//
Функция ПолноеНаименованиеПоСокращенномуНаименованию(СокращенноеНаименование) Экспорт
	
	СтруктураНаименования = РазложитьСтрокуНаименования(СокращенноеНаименование);
	
	Если НЕ ЗначениеЗаполнено(СтруктураНаименования.ОрганизационноПравоваяФорма) Тогда
		НаименованиеПолное = СтруктураНаименования.Наименование;
	ИначеЕсли СтруктураНаименования.ВыводитьКавычки Тогда
		НаименованиеПолное = СтрШаблон(НСтр("ru = '%1 ""%2""'"), 
							СтруктураНаименования.ОрганизационноПравоваяФорма, СтруктураНаименования.Наименование);
	Иначе
		НаименованиеПолное = СтрШаблон(НСтр("ru = '%1 %2'"),
							СтруктураНаименования.ОрганизационноПравоваяФорма, СтруктураНаименования.Наименование);
	КонецЕсли;
	
	Возврат НаименованиеПолное;
	
КонецФункции

// Проверяет, что наименование организации соответствует сокращенному и не изменялось пользователем.
//
// Параметры:
//   СокращенноеНаименование - Строка - Сокращенное наименование организации.
//   Наименование - Строка - Наименование организации.
//
// Возвращаемое значение: 
//   Булево - результат сравнения наименования и сокращенного наименования.
//
Функция НаименованиеСоответствуетСокращенномуНаименованию(СокращенноеНаименование, Наименование) Экспорт
	
	НаименованиеПоСокращенному = НаименованиеПоСокращенномуНаименованию(СокращенноеНаименование);
	Возврат СокрЛП(НаименованиеПоСокращенному) = СокрЛП(Наименование);
	
КонецФункции

// Возвращает наименование организации по сокращенному.
//
// Параметры:
//   СокращенноеНаименование - Строка - Сокращенное наименование организации.
//
// Возвращаемое значение: 
//   Наименование - Строка - Наименование организации, полученное из сокращенного.
//
Функция НаименованиеПоСокращенномуНаименованию(СокращенноеНаименование) Экспорт
	
	СтруктураНаименования = РазложитьСтрокуНаименования(СокращенноеНаименование);
	
	Возврат СтруктураНаименования.Наименование
			+ ?(ПустаяСтрока(СтруктураНаименования.ОрганизационноПравоваяФормаСокращенно), "", " ")
			+ СтруктураНаименования.ОрганизационноПравоваяФормаСокращенно;
	
КонецФункции

// Возвращает структуру ФИО индивидуального предпринимателя по сокращенному или полному наименованию.
//
// Параметры:
//   Наименование - Строка - Сокращенное или полное наименование индивидуального предпринимателя.
//
// Возвращаемое значение: 
//   Структура или Неопределено - Структура ФИО, если разложить не удалось, возвращается Неопределено.
//
Функция ФамилияИмяОтчествоПоНаименованиюИП(Знач Наименование) Экспорт
	
	СтруктураНаименования = РазложитьСтрокуНаименования(Наименование);
	Если ЗначениеЗаполнено(СтруктураНаименования.ОрганизационноПравоваяФорма) Тогда
		Наименование = СтруктураНаименования.Наименование;
	КонецЕсли;
	
	МассивСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(Наименование);
	Если МассивСлов.Количество() > 1
		И МассивСлов.Количество() < 4 Тогда
		Фамилия  = МассивСлов[0];
		Имя      = ?(МассивСлов.Количество() > 1,  МассивСлов[1], "");
		Отчество = ?(МассивСлов.Количество() > 2,  МассивСлов[2], "");
		Возврат Новый Структура("Фамилия, Имя, Отчество", Фамилия, Имя, Отчество);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ВладелецРегистрации(Форма) Экспорт
	
	Объект            = Форма.Объект;
	ОрганизацияСсылка = Форма.ОрганизацияСсылка;
	Возврат ?(Объект.ОбособленноеПодразделение, Объект.ГоловнаяОрганизация, ОрганизацияСсылка);
	
КонецФункции

// Возвращает представление выбранной системы налогообложения.
//
// Параметры:
//   ИсточникУчетнойПолитики - Структура, СправочникСсылка.Организации - описание структуры в НастройкиУчета.ОписаниеСтруктурыУчетнойПолитики()
//
// Возвращаемое значение:
//   Строка
//
Функция ПредставлениеСистемыНалогообложения(Знач ИсточникУчетнойПолитики) Экспорт
	
	Если ТипЗнч(ИсточникУчетнойПолитики) = Тип("СправочникСсылка.Организации") Тогда
		// Преобразуем ссылку на организацию в структуру параметров учетной политики.
		ИсточникУчетнойПолитики = ОрганизацииФормыВызовСервера.ПараметрыСистемыНалогообложенияПоОрганизации(ИсточникУчетнойПолитики);
	КонецЕсли;
	
	СистемаНалогообложенияОписание = "";
	Если Не ИсточникУчетнойПолитики.ПрименяетсяУСН И Не ИсточникУчетнойПолитики.ПлательщикНДС Тогда
		
		Если ИсточникУчетнойПолитики.ПрименяетсяУСНПатент Тогда
			
			СистемаНалогообложенияОписание = НСтр("ru = 'Патентная'");
			
			Если ИсточникУчетнойПолитики.ПлательщикЕНВД Тогда
				СистемаНалогообложенияОписание = СистемаНалогообложенияОписание + ", " + НСтр("ru = 'ЕНВД (до конца 2020 года)'");
			КонецЕсли;
			
		ИначеЕсли ИсточникУчетнойПолитики.ПлательщикЕНВД Тогда
			
			СистемаНалогообложенияОписание = НСтр("ru = 'ЕНВД (до конца 2020 года)'");
			
		ИначеЕсли ИсточникУчетнойПолитики.ПрименяетсяНалогНаПрофессиональныйДоход Тогда
			
			СистемаНалогообложенияОписание = НСтр("ru = 'Налог на профессиональный доход (""самозанятые"")'");
			
		КонецЕсли;
		
	Иначе
	
		Если ИсточникУчетнойПолитики.ПрименяетсяУСН Тогда
			ПредставлениеОбъектаНалогообложения = ?(ИсточникУчетнойПолитики.ПрименяетсяУСНДоходыМинусРасходы,
				НСтр("ru = 'доходы минус расходы'"), НСтр("ru = 'доходы'"));
			СистемаНалогообложенияОписание = НСтр("ru = 'Упрощенная'") + " (" + ПредставлениеОбъектаНалогообложения + ")";
		Иначе
			СистемаНалогообложенияОписание = НСтр("ru = 'Общая'");
		КонецЕсли;
		Если ИсточникУчетнойПолитики.ПрименяетсяУСНПатент Тогда
			СистемаНалогообложенияОписание = СистемаНалогообложенияОписание + ", " + НСтр("ru = 'Патент'");
		КонецЕсли;
		Если ИсточникУчетнойПолитики.ПлательщикЕНВД Тогда
			СистемаНалогообложенияОписание = СистемаНалогообложенияОписание + ", " + НСтр("ru = 'ЕНВД (до конца 2020 года)'");
		КонецЕсли;
		Если ИсточникУчетнойПолитики.ПлательщикТорговогоСбора Тогда
			СистемаНалогообложенияОписание = СистемаНалогообложенияОписание + ", " + НСтр("ru = 'Торговый сбор'");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СистемаНалогообложенияОписание;
	
КонецФункции

Функция ТекстВыбораЗначенияПоГиперссылке() Экспорт
	
	Возврат НСтр("ru = 'Выбрать'");
	
КонецФункции

Функция ТелефонСоответствуетТребованиям(Знач Телефон) Экспорт
	
	Телефон = ТолькоЦифры(СокрЛП(Телефон));
	
	КодСтраны = Лев(Телефон, 1);
	КодРегиона = Сред(Телефон, 2, 3);
	Номер = Сред(Телефон, 5);
	
	Если ПустаяСтрока(КодСтраны) Или ПустаяСтрока(КодРегиона) Или ПустаяСтрока(Номер) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Если СтрДлина(Телефон) > 15 Тогда
		// Номер телефона слишком длинный
		Возврат Ложь;
	КонецЕсли;
	
	Если КодСтраны = "7" Тогда
		Если СтрДлина(Номер) > 7 Тогда
			// В России номер телефона не может быть больше 7 цифр
			Возврат Ложь;
		КонецЕсли;
		Если КодРегионаЗарезервирован(КодРегиона) Тогда
			Возврат Ложь;
		КонецЕсли;
		Если НеГеографическийКодРоссии(КодРегиона) И СтрДлина(КодРегиона) <> 3 Тогда
			// В России номера мобильных телефонов должны содержать 3 цифры
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ИндивидуальныйПредпринимательЗарегистрированВФСС(Реквизиты) Экспорт
	
	Если Реквизиты = Неопределено Или Реквизиты.ЮридическоеФизическоеЛицо <> ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьРегистрационныйНомерФСС = ЗначениеЗаполнено(Реквизиты.РегистрацияВФСС)
		И ЗначениеЗаполнено(Реквизиты.РегистрацияВФСС.РегистрационныйНомерФСС);
	
	Возврат ЕстьРегистрационныйНомерФСС;
	
КонецФункции

// Заполняет актуальные наименования организации после завершения редактирования истории в отдельной форме.
// 
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - Форма, в которой редактируется организация.
//	Объект - СправочникОбъект.Организации, ДанныеФормыСтруктура - Организация, наименования которой изменяются.
//	НаборЗаписей - ТаблицаЗначений, ДанныеФормыКоллекция - История наименований организации.
//
Процедура УстановитьНаименованиеПослеРедактированияИстории(Форма, Объект, НаборЗаписей) Экспорт
	
	Форма.Модифицированность = Истина;

	НаборЗаписей.Сортировать("Период");
	
	Объект.ИсторияНаименований.Очистить();
	Если НаборЗаписей.Количество() > 1 Тогда
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
			ЗаписьИстории = Объект.ИсторияНаименований.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьИстории, ЗаписьНабора);
		КонецЦикла;
	КонецЕсли;
	
	АктуальнаяЗапись = НаборЗаписей[НаборЗаписей.Количество()-1];
	Объект.НаименованиеСокращенное = АктуальнаяЗапись.НаименованиеСокращенное;
	Объект.НаименованиеПолное      = АктуальнаяЗапись.НаименованиеПолное;
	Объект.ФамилияИП               = АктуальнаяЗапись.ФамилияИП;
	Объект.ИмяИП                   = АктуальнаяЗапись.ИмяИП;
	Объект.ОтчествоИП              = АктуальнаяЗапись.ОтчествоИП;
	
	Если ПустаяСтрока(Объект.Наименование)
		ИЛИ НаименованиеСоответствуетСокращенномуНаименованию(Форма.НаименованиеСокращенноеДоИзменения, Объект.Наименование) Тогда
		Объект.Наименование = НаименованиеПоСокращенномуНаименованию(Объект.НаименованиеСокращенное);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазложитьСтрокуНаименования(Знач Наименование)
	
	Результат = Новый Структура("Наименование,ОрганизационноПравоваяФорма,ОрганизационноПравоваяФормаСокращенно,ВыводитьКавычки");
	
	Наименование    = СокрЛП(Наименование);
	ВыводитьКавычки = Истина;
	
	Если УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='ООО'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Общество с ограниченной ответственностью'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='ООО'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='ПАО'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Публичное акционерное общество'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='ПАО'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='АО'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Акционерное общество'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='АО'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='ОАО'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Открытое акционерное общество'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='ОАО'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='ЗАО'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Закрытое акционерное общество'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='ЗАО'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='ИП'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Индивидуальный предприниматель'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='ИП'");
		ВыводитьКавычки = Ложь;
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='Общество с ограниченной ответственностью'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Общество с ограниченной ответственностью'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='ООО'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='Публичное акционерное общество'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Публичное акционерное общество'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='ПАО'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='Акционерное общество'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Акционерное общество'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='АО'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='Открытое акционерное общество'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Открытое акционерное общество'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='ОАО'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='Закрытое акционерное общество'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Закрытое акционерное общество'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='ЗАО'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru='Индивидуальный предприниматель'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru='Индивидуальный предприниматель'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru='ИП'");
		ВыводитьКавычки = Ложь;
	Иначе
		ОрганизационноПравоваяФорма				= "";
		ОрганизационноПравоваяФормаСокращенно	= "";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ОрганизационноПравоваяФорма) Тогда
		КоличествоКавычек	= СтрЧислоВхождений(Наименование, """");
		Если КоличествоКавычек > 1 Тогда
			// Наименование организации внутри внешних кавычек
			ПозицияПервойКавычки	= СтрНайти(Наименование, """");
			ПозицияПоследнейКавычки	= 0;
			
			ВремНаименование	= Наименование;
			
			ПозицияКавычки		= ПозицияПервойКавычки;
			Пока ПозицияКавычки > 0 Цикл
				ПозицияПоследнейКавычки	= ПозицияПоследнейКавычки + ПозицияКавычки;
				ВремНаименование	= Сред(ВремНаименование, ПозицияКавычки + 1);
				ПозицияКавычки		= СтрНайти(ВремНаименование, """");
			КонецЦикла;
			
			Наименование	= Сред(Наименование, ПозицияПервойКавычки + 1, ПозицияПоследнейКавычки - ПозицияПервойКавычки - 1);
			Если НЕ КоличествоКавычек%2 = 0 Тогда
				Наименование	= Наименование + """";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Результат.Наименование							= СокрЛП(Наименование);
	Результат.ОрганизационноПравоваяФорма			= ОрганизационноПравоваяФорма;
	Результат.ОрганизационноПравоваяФормаСокращенно	= ОрганизационноПравоваяФормаСокращенно;
	Результат.ВыводитьКавычки						= ВыводитьКавычки;
	
	Возврат Результат;
	
КонецФункции

Функция УбратьИзНаименованияОрганизационнуюФорму(Наименование, ИмяОрганизационнойФормы)

	Результат	= Ложь;
	
	Наименование	= СокрЛП(Наименование);
	
	ДлинаНаименования			= СтрДлина(Наименование);
	ДлинаОрганизационнойФормы	= СтрДлина(ИмяОрганизационнойФормы);
	
	Если Лев(Наименование, ДлинаОрганизационнойФормы) = ИмяОрганизационнойФормы Тогда
		Наименование	= СокрЛ(Сред(Наименование, ДлинаОрганизационнойФормы + 1));
		Результат		= Истина;
	КонецЕсли;	
	
	Если Прав(Наименование, ДлинаОрганизационнойФормы) = ИмяОрганизационнойФормы Тогда
		Наименование	= СокрП(Лев(Наименование, ДлинаНаименования - ДлинаОрганизационнойФормы - 1));
		Результат		= Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ГеографическийКодРоссии(КодРегиона) Экспорт
	
	Возврат СтрНачинаетсяС(КодРегиона, "3") Или СтрНачинаетсяС(КодРегиона, "4") Или СтрНачинаетсяС(КодРегиона, "8");
	
КонецФункции

Функция НеГеографическийКодРоссии(КодРегиона) Экспорт
	
	Возврат СтрНачинаетсяС(КодРегиона, "9");
	
КонецФункции

Функция КодРегионаЗарезервирован(КодРегиона) Экспорт
	
	Возврат СтрНачинаетсяС(КодРегиона, "0") Или СтрНачинаетсяС(КодРегиона, "1") Или СтрНачинаетсяС(КодРегиона, "2") Или СтрНачинаетсяС(КодРегиона, "5");
	
КонецФункции

Функция ТолькоЦифры(Строка) Экспорт
	
	ОбработаннаяСтрока = "";
	
	Для НомерСимвола = 1 По СтрДлина(Строка) Цикл
		Символ = Сред(Строка, НомерСимвола, 1);
		Если Символ >= "0" И Символ <= "9" Тогда
			ОбработаннаяСтрока = ОбработаннаяСтрока + Символ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбработаннаяСтрока;
	
КонецФункции

Процедура ОпределитьПолПоОтчеству(Пол, Знач Отчество) Экспорт
	
	Если Прав(Отчество, 2) = "ич" Тогда
		Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской");
	ИначеЕсли Прав(Отчество, 2) = "на" Тогда
		Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский");
	КонецЕсли;
	
КонецПроцедуры

Процедура ТелефонПриИзменении(Форма, Элемент) Экспорт
	
	Строки = Форма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("ИмяРеквизита", Элемент.Имя));
	Если Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ДанныеСтроки = Строки[0];
	
	ВведенноеЗначение = Форма[Элемент.Имя];
	Если ПустаяСтрока(ВведенноеЗначение) Тогда
		ДанныеСтроки.Представление = "";
		ДанныеСтроки.Значение = Неопределено;
	Иначе
		Телефон = ТелефонДляЗаявления(ВведенноеЗначение);
		Форма[Элемент.Имя] = Телефон;
		ДанныеСтроки.Представление =Телефон;
		ДанныеСтроки.Значение = ОрганизацииФормыВызовСервера.КонтактнаяИнформацияПоПредставлению(Телефон, ДанныеСтроки.Вид);
	КонецЕсли;
	
КонецПроцедуры

Функция ТелефонДляЗаявления(Знач Телефон) Экспорт
	
	Телефон = ОрганизацииФормыКлиентСервер.ТолькоЦифры(СокрЛП(Телефон));
	
	КодСтраны = Лев(Телефон, 1);
	КодРегиона = Сред(Телефон, 2, 3);
	Номер = Сред(Телефон, 5);
	
	Если КодСтраны <> "7" И КодСтраны <> "8" Тогда
		Возврат Телефон;
	КонецЕсли;
	
	Если ОрганизацииФормыКлиентСервер.ГеографическийКодРоссии(КодРегиона) Тогда
		ШаблонТелефона = "8(%1)%2";
	ИначеЕсли ОрганизацииФормыКлиентСервер.НеГеографическийКодРоссии(КодРегиона) Тогда
		ШаблонТелефона = "+7(%1)%2";
	Иначе
		Возврат Телефон;
	КонецЕсли;
	
	Возврат СтрШаблон(ШаблонТелефона, КодРегиона, Номер);
	
КонецФункции

// Функция определяет, заполнено ли представление адреса в указанном реквизите формы
//
// Параметры:
//     Форма                - ФормаКлиентскогоПриложения - Форма владельца контактной информации (организации или подразделения).
//     ИмяРеквизитаАдреса   - Строка - Имя реквизита формы, содержащего представление адреса.
//
Функция АдресЗаполен(Форма, ИмяРеквизитаАдреса) Экспорт
	
	Возврат НЕ ПустаяСтрока(Форма[ИмяРеквизитаАдреса])
		И Форма[ИмяРеквизитаАдреса] <> УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки();
	
КонецФункции	

Функция НастроитьПояснениеКПП(Форма, ЭтоФизЛицо = Ложь, ОбособленноеПодразделение = Истина) Экспорт 
	
	Элементы = Форма.Элементы;
		
	ЕстьКнопкаСнятьСУчета = (Элементы.Найти("СнятьСУчета") <> Неопределено);
	ПоказыватьКомандуСнятьСУчета = Истина;
	
	Если ЕстьКнопкаСнятьСУчета Тогда
		ПоказыватьКнопкуСнятьСУчета = НЕ ЭтоФизЛицо И ОбособленноеПодразделение;
	КонецЕсли;	
	
	//Если Форма.РегистрацияВНалоговомОргане.Ссылка.Пустая() Тогда
	//	
	//	// Некорректный формат КПП - выведем пояснение
	//	Элементы.НадписьПоясненияНекорректногоКПП.Видимость = ЗначениеЗаполнено(Форма.НадписьПоясненияНекорректногоКПП);
	//	Элементы.НадписьПоясненияСнятоСУчета.Видимость 		= Ложь;
	//	Если ЕстьКнопкаСнятьСУчета Тогда
	//		Элементы.СнятьСУчета.Видимость = Ложь;
	//	КонецЕсли;	
	//			
	//ИначеЕсли ЗначениеЗаполнено(Форма.РегистрацияВНалоговомОргане.ДатаСнятияСУчета) Тогда
	//	
	//	// Выбранный КПП закрыт
	//	ТекстСообщения = НСтр("ru = ' Подразделение снято с учета %1 '");
	//	ТекстСообщения = СтрШаблон(ТекстСообщения, Формат(Форма.РегистрацияВНалоговомОргане.ДатаСнятияСУчета, "ДЛФ=D"));
	//	
	//	МассивСтрок = Новый Массив;
	//	// Пояснение выводим форматированной строкой, чтобы указать меньший размер шрифта, чем в поле ввода.
	//	// В самом поле ввода шрифт указан больше (см. форму), чтобы фон выделения поля окружал текст пояснения с запасом.
	//	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ТекстСообщения, Новый Шрифт(, 10)));
	//	Форма.НадписьПоясненияСнятоСУчета = Новый ФорматированнаяСтрока(МассивСтрок);
	//					
	//	Элементы.НадписьПоясненияНекорректногоКПП.Видимость = Ложь;
	//	Элементы.НадписьПоясненияСнятоСУчета.Видимость 		= Истина;
	//	Если ЕстьКнопкаСнятьСУчета Тогда
	//		Элементы.СнятьСУчета.Видимость = Ложь;
	//	КонецЕсли;	
	//	
	//ИначеЕсли ЗначениеЗаполнено(Форма.НадписьПоясненияНекорректногоКПП) Тогда
	//	
	//	// КПП не закрыт, но, возможно, есть ошибка в формате - выводим пояснение
	//	Элементы.НадписьПоясненияНекорректногоКПП.Видимость = Истина;
	//	Элементы.НадписьПоясненияСнятоСУчета.Видимость 		= Ложь;
	//	Если ЕстьКнопкаСнятьСУчета Тогда
	//		Элементы.СнятьСУчета.Видимость = ПоказыватьКнопкуСнятьСУчета;
	//	КонецЕсли;	
	//	
	//Иначе
	//	
	//	// КПП соответствует требованиям и не закрыт - скрываем все пояснения 
	//	Элементы.НадписьПоясненияНекорректногоКПП.Видимость = Ложь;
	//	Элементы.НадписьПоясненияСнятоСУчета.Видимость 		= Ложь;
	//	Если ЕстьКнопкаСнятьСУчета Тогда
	//		Элементы.СнятьСУчета.Видимость = ПоказыватьКнопкуСнятьСУчета;
	//	КонецЕсли;	
	//			
	//КонецЕсли;	
	
КонецФункции

Процедура НастроитьСвойстваЭлементаДатаРегистрации(Элемент, ЭтоФизЛицо) Экспорт

	Если ЭтоФизЛицо Тогда
		Элемент.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(НСтр("ru='Дата государственной регистрации, указывается в ""Свидетельстве о государственной регистрации индивидуального предпринимателя"".
			|'")),
			Новый ФорматированнаяСтрока(НСтр("ru='Подробнее'"),,,, "http://its.1c.ru/bmk/bp30/fillingentrepreneur#entregdate"));

	Иначе
		Элемент.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(НСтр("ru='Дата государственной регистрации, указывается в ""Свидетельстве о государственной регистрации юридического лица"".
			|'")),
			Новый ФорматированнаяСтрока(НСтр("ru='Подробнее'"),,,, "http://its.1c.ru/bmk/bp30/fillingorganisation#orgregdate"));

	КонецЕсли;

КонецПроцедуры

Процедура НастроитьСвойстваЭлементаЮрАдресОрганизации(Элемент, ЭтоФизЛицо) Экспорт

	Если ЭтоФизЛицо Тогда

		Элемент.Заголовок = ЗаголовокАдресаФизЛица();
		Элемент.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(
			НСтр("ru='Адрес индивидуального предпринимателя по месту постоянной регистрации.'"));
	
	Иначе

		Элемент.Заголовок = НСтр("ru = 'Юридический адрес'");
		Элемент.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(
			НСтр("ru='Адрес, указанный в учредительных документах (Уставе) и в Выписке из ЕГРЮЛ. По этому адресу организация поставлена на налоговый учет.'"));
	
	КонецЕсли;

КонецПроцедуры

Функция ЗаголовокАдресаФизЛица() Экспорт
	
	Возврат НСтр("ru = 'Адрес места жительства'");
	
КонецФункции

#КонецОбласти

#Область ОКВЭД2

Функция ПрочитатьОКВЭД2(КодОКВЭД2, СообщениеОбОшибке = "") Экспорт
	
	СообщениеОбОшибке = "";
	НаименованиеОКВЭД2 = Неопределено;
	
	Если Не ЗначениеЗаполнено(КодОКВЭД2) тогда
		Возврат НаименованиеОКВЭД2;
	КонецЕсли;
	
	ОКВЭДКорректен = ОКВЭДСоответствуетТребованиям(СообщениеОбОшибке, КодОКВЭД2);
	
	Если ОКВЭДКорректен Тогда
		
		ОКВЭД2 = ОбщегоНазначенияБПВызовСервера.ПолучитьКлассификатор("ОКВЭД2");
		НаименованиеОКВЭД2 = ОКВЭД2.Получить(КодОКВЭД2);
		Если НаименованиеОКВЭД2 = Неопределено Тогда
			СообщениеОбОшибке  = Нстр("ru = 'Код ОКВЭД не найден в классификаторе. Возможно указан неправильный код'");
			НаименованиеОКВЭД2 = "";
		КонецЕсли;
	КонецЕсли;
	
	Возврат НаименованиеОКВЭД2;
	
КонецФункции

Функция ОКВЭДСоответствуетТребованиям(СообщениеОбОшибке, КодОКВЭД) Экспорт
	
	СообщениеОбОшибке = "";
	
	ОКВЭДКорректен = РегламентированнаяОтчетностьКлиентСервер.ОКВЭДСоответствуетТребованиям(КодОКВЭД);
	
	Если Не ОКВЭДКорректен Тогда
		ПримерОКВЭД = ПримерПравильногоОКВЭД(КодОКВЭД);
		Если ПустаяСтрока(ПримерОКВЭД) Тогда
			СообщениеОбОшибке = "В коде ОКВЭД разрешены только цифры и символ "".""";
		Иначе
			ШаблонОшибки = Нстр("ru = 'Код ОКВЭД не соответствует формату. Пример правильного кода: %1'");
			СообщениеОбОшибке = СтрШаблон(ШаблонОшибки, ПримерОКВЭД);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОКВЭДКорректен;
	
КонецФункции

Функция ПримерПравильногоОКВЭД(ИсходнаяСтрока) Экспорт
	
	Результат = "";
	
	Длина = СтрДлина(ИсходнаяСтрока);
	СимволыОКВЭД = Новый Массив();
	Для НомерСимвола = 1 По Длина Цикл
		
		Если СимволыОКВЭД.Количество() >= ДлинаОКВЭД() Тогда
			Прервать;
		КонецЕсли;
		
		КодСимвола = КодСимвола(ИсходнаяСтрока, НомерСимвола);
		
		Если КодСимвола > 47 И КодСимвола < 58 Тогда // Число.
			Символ = Сред(ИсходнаяСтрока, НомерСимвола, 1);
			СимволыОКВЭД.Добавить(Символ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СимволыОКВЭД.Количество() = 1 Тогда
		// ОКВЭД минимум содержит 2 знака.
		СимволыОКВЭД.Добавить("0")
	КонецЕсли;
	
	Для НомерСимвола = 1 По СимволыОКВЭД.Количество() Цикл
		Если НомерСимвола = СимволыОКВЭД.Количество() Или НомерСимвола % 2 <> 0 Тогда
			Результат = Результат + СимволыОКВЭД[НомерСимвола - 1];
		Иначе
			Результат = Результат + СимволыОКВЭД[НомерСимвола - 1] + ".";
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ДлинаОКВЭД()
	
	Возврат 6;
	
КонецФункции

#КонецОбласти
