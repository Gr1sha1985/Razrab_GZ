// Содержимое модуля отличается в версиях ПРОФ и КОРП.

#Область ПрограммныйИнтерфейс

// Начинает загрузку данных из формы, отображающей эти данные.
//
// Параметры:
//  Форма                       - УправляемаяФорма - форма, отображающая загруженные данные
//  ОбработчикСледующейЗагрузки - ОписаниеОповещения - содержит описание процедуры,
//                    обеспечивающей старт очередной (следующей) загрузки. Параметры процедуры:
//    * Результат - интервал в секундах, через который следует начать очередную загрузку
//    * ДополнительныеПараметры - значение, которое было указано при создании объекта оповещения
//
Процедура НачатьЗагрузку(Форма, ОбработчикСледующейЗагрузки) Экспорт
	
	РезультатНачалаЗагрузки = ОбменДаннымиСВнешнимиСистемамиБПВызовСервера.НачатьЗагрузку(Форма.УникальныйИдентификатор);
	
	Если РезультатНачалаЗагрузки.КодРезультата = "НачатьПозже" Тогда
		// Загрузка данных недавно была инициирована из другого списка
		// Отложим повторную попытку
		ВыполнитьОбработкуОповещения(ОбработчикСледующейЗагрузки, РезультатНачалаЗагрузки.Интервал);
	КонецЕсли;
	
	Если РезультатНачалаЗагрузки.КодРезультата <> "ДлительнаяОперация" Тогда
		Возврат;
	КонецЕсли;
	
	ОбработчикЗавершения = Новый ОписаниеОповещения("ЗавершитьЗагрузку", ЭтотОбъект, ОбработчикСледующейЗагрузки);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатНачалаЗагрузки.ДлительнаяОперация, ОбработчикЗавершения, ПараметрыОжидания);
	
	// По окончании ожидания - см. ЗавершитьЗагрузку
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗавершитьЗагрузку(РезультатДлительнойОперации, ОбработчикСледующейЗагрузки) Экспорт // Обработчик оповещения
	
	// Вне зависимости от результата загрузки запланируем следующую
	ВыполнитьОбработкуОповещения(
		ОбработчикСледующейЗагрузки,
		ОбменДаннымиСВнешнимиСистемамиБПКлиентСервер.ИнтервалОперативнойЗагрузкиДанных());
	
	Если РезультатДлительнойОперации = Неопределено Тогда  // отменено пользователем
		Возврат;
	КонецЕсли;
	
	Если РезультатДлительнойОперации.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
		
	РезультатЗагрузки = ПолучитьИзВременногоХранилища(РезультатДлительнойОперации.АдресРезультата);// См. Справочники.НастройкиЗагрузкиДанныхВнешнихСистем.НовыйРезультатЗагрузки
	Если ТипЗнч(РезультатЗагрузки) = Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТипОбъектов Из РезультатЗагрузки.ТипыЗагруженныхОбъектов Цикл
		ОповеститьОбИзменении(ТипОбъектов);
	КонецЦикла;
			
КонецПроцедуры

#КонецОбласти
