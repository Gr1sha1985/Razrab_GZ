#Область СлужебныйПрограммныйИнтерфейс

#Область СохранениеПолучениеРазмераАвансаПоУмолчанию

// Заполняет ранее сохраненный способ расчета аванса
//
// Параметры:
//		СпособРасчетаАванса	- реквизит объекта или управляемой формы, который заполняется
//								ранее сохраненным способом расчета аванса.
//
Процедура УстановитьСпособРасчетаАвансаПоУмолчанию(СпособРасчетаАванса) Экспорт
	
	СпособРасчета = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"СпособРасчетаАванса", "СпособРасчета");
		
	Если СпособРасчета <> Неопределено Тогда
		СпособРасчетаАванса = СпособРасчета;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает размер аванса в процентах по умолчанию, сохраненный в настройках пользователя.
//
// Параметры:
//		Организация			- СправочникСсылка.Организации
//
// Возвращаемое значение:
//		Число
//
Функция РазмерАвансаВПроцентахПоУмолчанию(Организация) Экспорт
	
	Возврат РазмерАвансаПоУмолчанию(Организация, Перечисления.СпособыРасчетаАванса.ПроцентомОтТарифа);
	
КонецФункции

// Возвращает размер аванса по умолчанию, сохраненный в настройках пользователя.
//
// Параметры:
//		Организация			- СправочникСсылка.Организации
//		СпособРасчетаАванса	- ПеречислениеСсылка.СпособыРасчетаАванса
//
// Возвращаемое значение:
//		Число
//
Функция РазмерАвансаПоУмолчанию(Организация, СпособРасчетаАванса) Экспорт
	
	РазмерАванса = 0;
	
	Если СпособРасчетаАванса = Перечисления.СпособыРасчетаАванса.ПроцентомОтТарифа Тогда
		
		РазмерАванса = Неопределено;
			
		СохраненнаяНастройка = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"СпособРасчетаАванса", "ПроцентомОтТарифа");
			
		Если СохраненнаяНастройка <> Неопределено Тогда
			
			СохраненныйРазмерАванса = СохраненнаяНастройка.Получить(Организация);
			Если СохраненныйРазмерАванса <> Неопределено Тогда
				РазмерАванса = СохраненныйРазмерАванса;
			КонецЕсли; 
			
		КонецЕсли;
		
		Если РазмерАванса = Неопределено Тогда
			РазмерАванса = 40;
		КонецЕсли; 
		
	КонецЕсли; 
	
	Возврат РазмерАванса;
	
КонецФункции

// Сохраняет в настройках пользователя размер аванса в процентах.
//
// Параметры:
//		РазмерАванса		- Число
//		Организация			- СправочникСсылка.Организации
//		СпособРасчетаАванса	- ПеречислениеСсылка.СпособыРасчетаАванса
//
Процедура ЗапомнитьРазмерАвансаПоУмолчанию(РазмерАванса, Организация, СпособРасчетаАванса) Экспорт
	
	Если ЗначениеЗаполнено(СпособРасчетаАванса) Тогда
			
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("СпособРасчетаАванса", "СпособРасчета", СпособРасчетаАванса);
		
		Если СпособРасчетаАванса = Перечисления.СпособыРасчетаАванса.ПроцентомОтТарифа Тогда
			
			НастройкаРазмераАванса = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
				"СпособРасчетаАванса", "ПроцентомОтТарифа");
				
			Если НастройкаРазмераАванса = Неопределено Тогда
				НастройкаРазмераАванса = Новый Соответствие;
			КонецЕсли;
			
			НастройкаРазмераАванса.Вставить(Организация, РазмерАванса);
			
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("СпособРасчетаАванса", "ПроцентомОтТарифа", НастройкаРазмераАванса);
			
		КонецЕсли; 
	
	КонецЕсли; 

КонецПроцедуры

#КонецОбласти

Процедура ФормаПодробнееОРасчетеНДФЛПриЗаполнении(Форма) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"КорректировкаВыплаты",
		"Видимость",
		Форма.КорректировкиВыплаты.Количество() > 0);
	
	ИспользоватьСтатьиФинансирования = ЗарплатаКадры.ИспользоватьСтатьиФинансированияЗарплата();
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"КорректировкаВыплатыКомандаРедактированияРаспределения",
		"Видимость",
		ИспользоватьСтатьиФинансирования);
	
	Форма.КорректировкаВыплаты = Форма.КорректировкиВыплаты.Выгрузить(,"КорректировкаВыплаты").Итог("КорректировкаВыплаты");
	
КонецПроцедуры

Функция РезультатРаспределенияКорректировкиВыплаты(РаспределениеРезультатовУдержаний, ИдентификаторСтроки) Экспорт
	Возврат РасчетЗарплатыВнутренний.РезультатРаспределенияКорректировкиВыплаты(РаспределениеРезультатовУдержаний, ИдентификаторСтроки);
КонецФункции

#КонецОбласти
