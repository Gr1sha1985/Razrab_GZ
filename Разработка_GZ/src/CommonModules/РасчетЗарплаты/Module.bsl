
#Область ПрограммныйИнтерфейс

// Возвращает ссылку на заполненный документ начисления зарплаты.
// Если для заданного месяца, организации и подразделения существует несколько 
// документов, возвращается хронологически первый.
// Если необходимого документа нет, он создается и записывается.
// В любом случае выполняется заполнение документа.
//
// Параметры:
//	МесяцНачисления
//	Организация
//	Подразделение (необязательный)
//
// Возвращаемое значение - ссылка на документ.
//
Функция ДокументНачисленияЗарплаты(МесяцНачисления, Организация, Подразделение = Неопределено) Экспорт
	
	ДокументСсылка = Неопределено;
	
	РасчетЗарплатыВнутренний.ПодобратьДокументНачисленияЗарплаты(ДокументСсылка, МесяцНачисления, Организация, Подразделение);
	
	Возврат ДокументСсылка;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.РегистрыСведений.ПлановыеАвансы, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ПлановыеАвансыИнтервальный, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ПлановыеНачисления, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ПлановыеНачисленияИнтервальный, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ПлановыеУдержания, Истина);
	
КонецПроцедуры

#КонецОбласти

// Возвращает таблицу значений с данными для распределения удержаний физлица 
// пропорционально начислениям по всем местам работы.
//
// Параметры:
//		ФизическиеЛица   - Массив.
//  	МесяцНачисления  - Дата.
//		Организация      - СправочникСсылка.Организации.
//
// Возвращаемое значение:
//		ТаблицаЗначений - ТаблицаЗначений - см НоваяТаблицаБазаУдержанийПоУмолчанию().
//
Функция БазаУдержанийПоУмолчанию(ФизическиеЛица, МесяцНачисления, Организация) Экспорт
	Возврат РасчетЗарплатыВнутренний.БазаУдержанийПоУмолчанию(ФизическиеЛица, МесяцНачисления, Организация); 
КонецФункции

// Формирует план видов расчета по текущему состоянию функциональных опций.
//
Процедура СформироватьПланВидовРасчетаПоНастройкам() Экспорт
	РасчетЗарплатыВнутренний.СформироватьПланВидовРасчетаПоНастройкам();
КонецПроцедуры

// Устанавливает соответствие видов расчета РайонныйКоэффициент и СевернаяНадбавка 
// в плане видов расчета Начисления текущим настройкам системы.
// Если виды расчета требуются, они создаются.
// Если виды расчета не требуются, они помечаются на удаление.
Процедура СформироватьВидыРасчетаРКиСН() Экспорт
	РасчетЗарплатыВнутренний.СформироватьВидыРасчетаРКиСН();
КонецПроцедуры

// Конструирует объект для хранения данных для проведения.
// Структура может содержать
//		НачисленияПоСотрудникам - таблица значений
//			ФизическоеЛицо.
//			Сотрудник
//			Подразделение
//			Начисление
//			Сумма
//			ОтработаноДней
//			ОтработаноЧасов
//
//		УдержанияПоСотрудникам - таблица значений
//			ФизическоеЛицо.
//			Удержание
//			Сумма
//
//		ИсчисленныйНДФЛ - таблица значений.
//
//		ИсчисленныеВзносы - таблица значений.
//
//		МенеджерВременныхТаблиц - менеджер временных таблиц на котором могут 
//		удерживаться таблицы
//			ВТНачисления (данные о начисленных суммах).
//				Сотрудник
//				ПериодДействия
//				ДатаНачала
//				Начисление
//				СуммаДохода
//				СуммаВычетаНДФЛ
//				СуммаВычетаВзносы
//				КодВычетаНДФЛ
//				Подразделение
//			ВТФизическиеЛица (список людей по которым выполняется расчет)
//				ФизическоеЛицо.
//
Функция СоздатьДанныеДляПроведенияНачисленияЗарплаты() Экспорт
	
	Возврат РасчетЗарплатыВнутренний.СоздатьДанныеДляПроведенияНачисленияЗарплаты();
	
КонецФункции

// Заполняет движения плановыми начислениями
//		ПлановыеНачисления
//		ЗначенияПериодическихПоказателейРасчетаЗарплатыСотрудников.
//
// Параметры:
//  РегистраторОбъект
//	Движения - коллекция движений, в которой необходимо заполнить движения.
//	СтруктураДанных - Структура
//		Ключи:
//			ДанныеОПлановыхНачислениях (необязательный) - таблица значений с полями:
//				ДатаСобытия
//				Сотрудник
//				Начисление
//				Размер
//
Процедура СформироватьДвиженияПлановыхНачислений(РегистраторОбъект, Движения, СтруктураДанных, ФормироватьЗаписиТолькоДляИзменений = Истина, ЗаполнятьНаборЗаписей = Истина) Экспорт
	
	РасчетЗарплатыВнутренний.СформироватьДвиженияПлановыхНачислений(РегистраторОбъект, Движения, СтруктураДанных, ФормироватьЗаписиТолькоДляИзменений, ЗаполнятьНаборЗаписей);
	
КонецПроцедуры

// Заполняет движения плановыми удержаниями (см. процедуру
// РасчетЗарплатыВнутренний.СформироватьДвиженияПлановыхУдержаний).
//
Процедура СформироватьДвиженияПлановыхУдержаний(Движения, СтруктураДанных) Экспорт

	РасчетЗарплатыВнутренний.СформироватьДвиженияПлановыхУдержаний(Движения, СтруктураДанных);
	
КонецПроцедуры

// Заполняет движения плановыми выплатами (авансы) (см. процедуру
// РасчетЗарплатыВнутренний.СформироватьДвиженияПлановыхВыплат).
//
Процедура СформироватьДвиженияПлановыхВыплат(Движения, ДанныеОПлановыхВыплатах) Экспорт

	РасчетЗарплатыВнутренний.СформироватьДвиженияПлановыхВыплат(Движения, ДанныеОПлановыхВыплатах);
	
КонецПроцедуры

// Формирует движения по регистрам подсистемы.
// Параметры:
//		Движения - коллекция движений регистратора.
//		Отказ - признак отказа от заполнения движений.
//		Организация
//		ПериодРегистрации
//		Начисления - таблица значений с колонками (необязательный).
//			Сотрудник
//			Договор
//
//		Допустимо присутствие других колонок в передаваемой таблице значений.
//
Процедура СформироватьДвиженияНачисленийПоДоговорам(Движения, Отказ, Организация, ПериодРегистрации, Начисления) Экспорт
	
	Для Каждого Строка Из Начисления Цикл
		
		Если Строка.СпособОплаты = Перечисления.СпособыОплатыПоДоговоруГПХ.ВКонцеСрокаСАвансовымиПлатежами Тогда
			
			НоваяСтрокаНачисленияПоДоговорамГПХ = Движения.НачисленияПоДоговорамГПХ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаНачисленияПоДоговорамГПХ, Строка);
			НоваяСтрокаНачисленияПоДоговорамГПХ.Период = ПериодРегистрации;
			НоваяСтрокаНачисленияПоДоговорамГПХ.Организация = Организация;
						
		Иначе
			
			НоваяСтрока = Движения.ОплаченныеДоговоры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ДоговорАкт = Строка.ДокументОснование;
			НоваяСтрока.МесяцНачисления = ПериодРегистрации;
			НоваяСтрока.Организация = Организация;
						
		КонецЕсли;
		
	КонецЦикла;   
	
	Если Движения.НачисленияПоДоговорамГПХ.Количество() > 0 Тогда
		Движения.НачисленияПоДоговорамГПХ.Записывать = Истина;
	КонецЕсли;
	
	Если Движения.ОплаченныеДоговоры.Количество() > 0 Тогда 
		Движения.ОплаченныеДоговоры.Записывать = Истина;
	КонецЕсли;		
		
КонецПроцедуры

// Заполняет данные для проведения начислениями и 
// временной таблицей ВТНачисления.
//	
// Параметры:	
// 		ДанныеДляПроведенияНачисленияЗарплаты.
//		Документ
//		ТаблицаНачислений - имя (имена через запятую) табличной части с начислениями, не обязательно, по умолчанию -
//		                    "Начисления".
//		ПолеДатыДействия - поле запроса для получения даты действия, по умолчанию дата действия - месяц 
// 				начисления первичного документа, т.е. "Ссылка.МесяцНачисления".
//
Процедура ЗаполнитьНачисления(ДанныеДляПроведенияНачисленияЗарплаты, Документ, ТаблицаНачислений = "Начисления", ПолеДатыДействия = "Ссылка.МесяцНачисления", ПолеВидаНачисления = Неопределено, ПолеВидаНачисленияВШапке = Неопределено, ФизическиеЛица = Неопределено) Экспорт
	РасчетЗарплатыВнутренний.ЗаполнитьНачисления(ДанныеДляПроведенияНачисленияЗарплаты, Документ, ТаблицаНачислений, ПолеДатыДействия, ПолеВидаНачисления, ПолеВидаНачисленияВШапке, ФизическиеЛица);
КонецПроцедуры

// Дополняет структуру данных для проведения таблицей НДФЛ.
//
Процедура ЗаполнитьДанныеНДФЛ(ДанныеДляПроведения, ДокументСсылка, ФизическиеЛица = Неопределено) Экспорт
	РасчетЗарплатыВнутренний.ЗаполнитьДанныеНДФЛ(ДанныеДляПроведения, ДокументСсылка, ФизическиеЛица);
КонецПроцедуры

// Дополняет структуру данных для проведения таблицей КорректировкиВыплаты.
//
Процедура ЗаполнитьДанныеКорректировкиВыплаты(ДанныеДляПроведения, ДокументСсылка, ФизическиеЛица = Неопределено) Экспорт
	РасчетЗарплатыВнутренний.ЗаполнитьДанныеКорректировкиВыплаты(ДанныеДляПроведения, ДокументСсылка, ФизическиеЛица);
КонецПроцедуры

// Дополняет структуру данных для проведения таблицей страховых взносов.
//
Процедура ЗаполнитьДанныеСтраховыхВзносов(ДанныеДляПроведения, ДокументСсылка, ФизическиеЛица = Неопределено) Экспорт
	РасчетЗарплатыВнутренний.ЗаполнитьДанныеСтраховыхВзносов(ДанныеДляПроведения, ДокументСсылка, ФизическиеЛица);
КонецПроцедуры

// Заполняет данные для проведения удержаниями.
//	
// Параметры:	
// 		ДанныеДляПроведенияНачисленияЗарплаты.
//		Документ
//		ТаблицаУдержаний - имя табличной части с удержаниями, не обязательно, по умолчанию - "Удержания".
//
Процедура ЗаполнитьУдержания(ДанныеДляПроведенияНачисленияЗарплаты, Документ, ТаблицаУдержаний = "Удержания", ФизическиеЛица = Неопределено) Экспорт
	
	РасчетЗарплатыВнутренний.ЗаполнитьУдержания(ДанныеДляПроведенияНачисленияЗарплаты, Документ, ТаблицаУдержаний, ФизическиеЛица);
	
КонецПроцедуры

// Формирует временную таблицу со списком физических лиц ВТФизическиеЛица, которые рассчитываются при 
// проведении документа.
//
// Параметры:
// 		ДанныеДляПроведенияНачисленияЗарплаты.
//		Документ
//		ИсправленныйДокумент
//		СписокФизическихЛиц.
//
Процедура ЗаполнитьСписокФизическихЛиц(ДанныеДляПроведенияНачисленияЗарплаты, Документ, ИсправленныйДокумент = Неопределено, СписокФизическихЛиц = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоставДокументовЗарплатаКадры.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	РегистрСведений.СоставДокументовЗарплатаКадры КАК СоставДокументовЗарплатаКадры
		|ГДЕ
		|	СоставДокументовЗарплатаКадры.ДокументФизическогоЛица В(&МассивДокументов)
		|	И СоставДокументовЗарплатаКадры.Сотрудник <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
		|	И &СписокФизическихЛиц";
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить(Документ);
	Если ЗначениеЗаполнено(ИсправленныйДокумент) Тогда
		МассивДокументов.Добавить(ИсправленныйДокумент);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Если СписокФизическихЛиц <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СписокФизическихЛиц", "СоставДокументовЗарплатаКадры.ФизическоеЛицо В (&СписокФизическихЛиц)");
		Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СписокФизическихЛиц", "ИСТИНА");
	КонецЕсли; 
	Запрос.МенеджерВременныхТаблиц = ДанныеДляПроведенияНачисленияЗарплаты.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

// Метод позволяет определить применение районного коэффициента и северной надбавки.
// 
Функция ВостребованностьРКиСН() Экспорт
	
	ВостребованностьРКиСН = Новый Структура("СевернаяНадбавка, РайонныйКоэффициент");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(НастройкиОрганизаций.ПрименятьСевернуюНадбавку), ЛОЖЬ) КАК СевернаяНадбавка,
	|	ЕСТЬNULL(МАКСИМУМ(НастройкиОрганизаций.ПрименятьРайонныйКоэффициент), ЛОЖЬ) КАК РайонныйКоэффициент
	|ИЗ
	|	РегистрСведений.НастройкиЗарплатаКадры КАК НастройкиОрганизаций");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ВостребованностьРКиСН.СевернаяНадбавка = Выборка.СевернаяНадбавка;
	ВостребованностьРКиСН.РайонныйКоэффициент = Выборка.РайонныйКоэффициент;
	
	Возврат ВостребованностьРКиСН;
	
КонецФункции

// Возвращает массив удержаний, соответствующие параметрам.
//
// Параметры:
//		КатегорияУдержания - Перечисление.КатегорииУдержаний
// 		Отбор - Структура, содержащая в качестве ключа наименование одного из реквизитов ПланаВидовРасчета.Удержания. 
//				Если в качестве ключа передано "КатегорияУдержания" и значение отбора отличается от значения первого параметра,
//				то вернется пустой массив.
//
// Возвращаемое значение:
//		Удержания - массив удержаний, соответствующих роли и отборам.
//
Функция УдержанияПоКатегории(КатегорияУдержания, Отбор = Неопределено) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Удержания.Ссылка
	|ИЗ
	|	ПланВидовРасчета.Удержания КАК Удержания
	|ГДЕ
	|	Удержания.КатегорияУдержания = &КатегорияУдержания
	|	&УСЛОВИЕ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КатегорияУдержания", КатегорияУдержания);
	
	Условие = "";
	СтрокаЗамены = "&УСЛОВИЕ";
	Если Отбор <> Неопределено Тогда
		Для Каждого КлючИЗначение Из Отбор Цикл
			Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			Если ТипЗнч(КлючИЗначение.Значение) = Тип("Массив") Тогда
				Условие = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 И Удержания.%2 В (&%2)", Условие, КлючИЗначение.Ключ);
			Иначе
				Условие = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 И Удержания.%2 = &%2", Условие, КлючИЗначение.Ключ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Условие = ?(ЗначениеЗаполнено(Условие), Условие, "И Истина");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, СтрокаЗамены, Условие);
	
	Удержания = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Удержания.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Удержания;
	
КонецФункции

// Вычисляет рекомендуемые корректировки выплаты на основании исчисленного налога и 
// сформированных ранее корректировок выплаты.
// Корректировка выплаты - разница между суммой долга перед сотрудником и 
// суммой, положенной к выплате сотруднику. 
// Разница может быть обусловлена невозможностью выплаты излишне удержанного ранее НДФЛ.
//
// Параметры:
// 	Налоги - ТаблицаЗначений - таблица значений, в которой есть колонки: 
//		* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица;
//		* Налог - Число.
// 	Должна содержать, как минимум, одну строку.
//
//	ВозвратНДФЛПрошлыхЛет - ТаблицаЗначений - Сведения о возвратах НДФЛ за прошлый год.
//		* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица;
//		* Сумма - Число.
//
//	ТекущиеКорректировкиВыплаты - ТаблицаЗначений - таблица значений, в которой есть колонки: 
//		* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица;
//		* Сотрудник - СправочникСсылка.Сотрудники;
//		* Подразделение - СправочникСсылка.ПодразделенияОрганизаций - не обязательно к заполнению;
//		* СтатьяФинансирования - СправочникСсылка.СтатьиФинансированияЗарплата - не обязательно к заполнению;
//		* СтатьяРасходов - СправочникСсылка.СтатьиРасходовЗарплата - не обязательно к заполнению;
//		* СуммаВзаиморасчетов - Число - не обязательно к заполнению.
//		* СуммаКорректировки  - Число - не обязательно к заполнению.
// 	Может содержать строки не по всем физическим лицам, которые переданы в параметре Налоги.
//  Это означает, что текущих корректировок нет.
//
//  ГодНалоговогоПериода - Число - год налогового периода
//
// Возвращаемое значение (см. ТаблицаКорректировкиВыплаты()):
//	ТаблицаЗначений - таблица значений с колонками: 
//		* ФизическоеЛицо - СправочникСсылка.ФизическиеЛица;
//		* Сотрудник - СправочникСсылка.Сотрудники;
//		* Подразделение - СправочникСсылка.ПодразделенияОрганизаций - не обязательно к заполнению;
//		* СтатьяФинансирования - СправочникСсылка.СтатьиФинансированияЗарплата - не обязательно к заполнению;
//		* СтатьяРасходов - СправочникСсылка.СтатьиРасходовЗарплата - не обязательно к заполнению;
//		* КорректировкаВыплаты - Число - разница между суммой долга перед сотрудником и суммой, 
//									положенной к выплате. Может быть отрицательной.
//		* Первичная  - булево. Если Истина, значит корректировка вносится в учет (например, из-за 
//  					появления отрицательного НДФЛ. Если Ложь, значит это "обратная" 
//						корректировка, восстанавливающая взаиморасчеты после выполнения первичной корректировки
//	Содержит строки со всеми физлицами, переданными в таблице Налоги
//	Если в переданных таблицах есть дополнительные колонки, они игнорируются в возвращаемой таблице.
// 
Функция РекомендуемыеКорректировкиВыплаты(Налоги, ВозвратНДФЛПрошлыхЛет, ТекущиеКорректировкиВыплаты, ГодНалоговогоПериода = НеОпределено) Экспорт
	Если ГодНалоговогоПериода = НеОпределено Тогда
		Возврат РекомендуемыеКорректировкиВыплатыУстар(Налоги, ТекущиеКорректировкиВыплаты);
	КонецЕсли;
	КорректировкиВыплаты = ТаблицаКорректировкиВыплаты();
	
	ПараметрыОтбораСтрокКорректировки = Новый Структура("ФизическоеЛицо");
	Для Каждого СтрокаНалога Из Налоги Цикл
		ПараметрыОтбораСтрокКорректировки.ФизическоеЛицо = СтрокаНалога.ФизическоеЛицо;
		ТекущаяКорректировка = 0;
		ОстатокВзаиморасчетов = 0;
		СтрокиКорректировок = ТекущиеКорректировкиВыплаты.НайтиСтроки(ПараметрыОтбораСтрокКорректировки);
		Если СтрокаНалога.НалоговыйПериод = ГодНалоговогоПериода И СтрокиКорректировок.Количество() > 0 Тогда
			Для Каждого СтрокаКорректировки Из СтрокиКорректировок Цикл
				ТекущаяКорректировка = ТекущаяКорректировка + СтрокаКорректировки.СуммаКорректировки;
				ОстатокВзаиморасчетов = ОстатокВзаиморасчетов + СтрокаКорректировки.СуммаВзаиморасчетов;
			КонецЦикла;
			ВозвратНДФЛ = ВозвратНДФЛПрошлыхЛет.НайтиСтроки(ПараметрыОтбораСтрокКорректировки);
			Для Каждого СтрокаВозврата Из ВозвратНДФЛ Цикл
				ТекущаяКорректировка = ТекущаяКорректировка + СтрокаВозврата.Сумма;
			КонецЦикла;
		КонецЕсли;
		
		Если СтрокаНалога.ИсчисленоНалога < 0 ИЛИ ТекущаяКорректировка < 0 Тогда
			// Если текущая корректировка меньше нуля (уже переплатили) или налог меньше нуля (сейчас переплатим) 
			НоваяСтрока = КорректировкиВыплаты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНалога);
			// налог, который нельзя выплатить, но с учетом долга перед сотрудником
			НевыплачиваемыйНалог = СтрокаНалога.ИсчисленоНалога + Мин(Макс(ОстатокВзаиморасчетов, 0), -СтрокаНалога.ИсчисленоНалога);
			// Выбираем для корректировки наибольшую по модулю 
			НоваяСтрока.КорректировкаВыплаты = Мин(ТекущаяКорректировка, НевыплачиваемыйНалог + Макс(СтрокаНалога.НеУдержаноНалога, 0));
			// Если корректируем из-за отрицательного налога, то это - "первичная" корректировка
			Если ТекущаяКорректировка > НевыплачиваемыйНалог Тогда
				НоваяСтрока.Первичная = Истина;
			КонецЕсли;
		ИначеЕсли СтрокаНалога.ИсчисленоНалога < ТекущаяКорректировка Тогда
			// Если налог не позволяет полностью компенсировать ("занулить") текущую корректировку
			// компенсируем текущую корректировку пропорционально.
			КорректировкаВыплаты = СтрокаНалога.ИсчисленоНалога;
			Коэффициент = КорректировкаВыплаты/ТекущаяКорректировка;
			Распределено = 0;
			МаксимальныйВесСтроки = 0;
			Для Каждого СтрокаКорректировки Из СтрокиКорректировок Цикл
				НоваяСтрока = КорректировкиВыплаты.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКорректировки);
				НоваяСтрока.КорректировкаВыплаты = СтрокаКорректировки.СуммаКорректировки * Коэффициент;
				Распределено = Распределено + НоваяСтрока.КорректировкаВыплаты;
				ВесСтроки = Макс(НоваяСтрока.КорректировкаВыплаты, -НоваяСтрока.КорректировкаВыплаты);
				Если МаксимальныйВесСтроки <= ВесСтроки Тогда
					МаксимальныйВесСтроки = ВесСтроки;
					СамаяТяжелаяСтрока = НоваяСтрока;
				КонецЕсли;
			КонецЦикла;
			// Отнесем копейки остатка на самую "тяжелую" строку.
			СамаяТяжелаяСтрока.КорректировкаВыплаты = СамаяТяжелаяСтрока.КорректировкаВыплаты - (КорректировкаВыплаты - Распределено);
		ИначеЕсли ТекущаяКорректировка = 0 Тогда
			Продолжить;
		Иначе
			// Если налог позволяет - просто "зануляем" текущую корректировку.
			Для Каждого СтрокаКорректировки Из СтрокиКорректировок Цикл
				НоваяСтрока = КорректировкиВыплаты.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКорректировки);
				НоваяСтрока.КорректировкаВыплаты = СтрокаКорректировки.СуммаКорректировки;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтборНулевыхСтрок = Новый Структура("КорректировкаВыплаты", 0);
	НулевыеСтроки = КорректировкиВыплаты.НайтиСтроки(ОтборНулевыхСтрок);
	Для Каждого НулеваяСтрока Из НулевыеСтроки Цикл
		КорректировкиВыплаты.Удалить(НулеваяСтрока);
	КонецЦикла;
	
	Возврат КорректировкиВыплаты;
	
КонецФункции

Функция СведенияОКорректировкахВыплаты(Форма, ПутьКДаннымАдресДанныеОбНДФЛ, ФизическоеЛицо = Неопределено) Экспорт
	
	ДанныеОбНДФЛ = ПолучитьИзВременногоХранилища(ПутьКДаннымАдресДанныеОбНДФЛ);
	
	Если ФизическоеЛицо = Неопределено Тогда
		КоллекцияСтрокКорректировкиВыплаты = Форма.Объект.КорректировкиВыплаты.Выгрузить();
	Иначе
		
		СтруктураОтбора = Новый Структура("ФизическоеЛицо", ФизическоеЛицо);
		КоллекцияСтрокКорректировкиВыплаты = Форма.Объект.КорректировкиВыплаты.Выгрузить(СтруктураОтбора);
		
	КонецЕсли;
	
	ДанныеОбНДФЛ.Вставить("КорректировкиВыплаты", КоллекцияСтрокКорректировкиВыплаты);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеОбНДФЛ, Форма.УникальныйИдентификатор);
	
КонецФункции

// Определяет, являются ли переданные основания теми документами, по которым НДФЛ уплачивается не сразу, а до конца месяца. 
//
// Параметры:
//	Основания - массив - содержит ссылки - основания для начисления НДФЛ.
//
// Возвращаемое значение - соответствие, ключом является переданное основание, значение - да/нет.
//
Функция ОснованиеИсчисленияНалогаСОтсроченнойУплатой(Основания) Экспорт

	Возврат РасчетЗарплатыВнутренний.ОснованиеИсчисленияНалогаСОтсроченнойУплатой(Основания)	

КонецФункции 

// Таблица для хранения данных табличных частей КорректировкиВыплаты документов, начисляющих зарплату.
Функция ТаблицаКорректировкиВыплатыДокумента() Экспорт
	ТаблицаКорректировкиВыплаты = Новый ТаблицаЗначений;
	ТаблицаКорректировкиВыплаты.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаКорректировкиВыплаты.Колонки.Добавить("КорректировкаВыплаты", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаКорректировкиВыплаты.Колонки.Добавить("ФиксРасчет", Новый ОписаниеТипов("Булево"));
	ТаблицаКорректировкиВыплаты.Колонки.Добавить("ФиксСтрока", Новый ОписаниеТипов("Булево"));
	ТаблицаКорректировкиВыплаты.Колонки.Добавить("ИдентификаторСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(7));
	ТаблицаКорректировкиВыплаты.Колонки.Добавить("РезультатРаспределения", Новый ОписаниеТипов());
	ТаблицаКорректировкиВыплаты.Колонки.Добавить("КомандаРедактированияРаспределения", Новый ОписаниеТипов("Строка"));
	ТаблицаКорректировкиВыплаты.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	ТаблицаКорректировкиВыплаты.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Возврат ТаблицаКорректировкиВыплаты;
	
КонецФункции

Функция ЗначениеПоказателяПоИдентификатору(Показатели, Идентификатор) Экспорт
	
	Возврат РасчетЗарплатыВнутренний.ЗначениеПоказателяПоИдентификатору(Показатели, Идентификатор);
	
КонецФункции

// Помещает в переданный МенеджерВременныхТаблиц таблицу 
// 	ВТДополнительныеСвойстваНачислений с полями
//		Начисление - ПланВидовРасчетаСсылка.Начисления,
//		ЯвляетсяДенежнымСодержанием - Булево,
//		ЯвляетсяДенежнымДовольствием - Булево.
//		ЯвляетсяДоходомВНатуральнойФорме - Булево.
//		НачисляетсяВЦеломЗаМесяц - Булево.
//
// Параметры:
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - 
//
Процедура СоздатьВТДополнительныеСвойстваНачислений(МенеджерВременныхТаблиц) Экспорт

	РасчетЗарплатыВнутренний.СоздатьВТДополнительныеСвойстваНачислений(МенеджерВременныхТаблиц)	

КонецПроцедуры

#Область Аванс

// Функция возвращает таблицу значений с информацией о плановых авансах сотрудников.
//
// Параметры:
//	ТаблицаСотрудников - таблица значений с колонками.
//		- Сотрудник; 
//		- Период
//	ДокументСсылка - если указать, то зарегистрированные этим документом авансы будут игнорироваться;
//	ВсеЗаписи - булево значение. 
//		Если Истина, то в результирующей временной таблице
//		будет столько же записей, сколько во входной временной таблице сотрудников.
//		Если Ложь, то в результирующей временной таблице 
//		будут записи только для тех сотрудников, для которых задан плановый аванс.
//		По умолчанию - Ложь.
//
// Возвращаемое значение - таблица значений с колонками.
//		- Сотрудник
//		- СпособРасчетаАванса
//		- Аванс.
//
Функция АвансыСотрудников(ТаблицаСотрудников, ДокументСсылка = Неопределено, ВсеЗаписи  = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТаблицаСотрудников", ТаблицаСотрудников);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
		ПараметрыПостроения.ВсеЗаписи = ВсеЗаписи;
		ПараметрыПостроения.ФормироватьСПериодичностьДень = Ложь;
		
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Регистратор", "<>", ДокументСсылка);

		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
			"ПлановыеАвансы",
			Запрос.МенеджерВременныхТаблиц,
			Истина,
			ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ТаблицаСотрудников),
			ПараметрыПостроения);

		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПлановыеАвансыСрезПоследних.Сотрудник,
			|	ПлановыеАвансыСрезПоследних.СпособРасчетаАванса КАК СпособРасчетаАванса,
			|	ПлановыеАвансыСрезПоследних.Аванс КАК Аванс
			|ИЗ
			|	ВТПлановыеАвансыСрезПоследних КАК ПлановыеАвансыСрезПоследних";
		
	Иначе
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТаблицаСотрудников.Сотрудник,
			|	ТаблицаСотрудников.Период
			|ПОМЕСТИТЬ ВТСотрудники
			|ИЗ
			|	&ТаблицаСотрудников КАК ТаблицаСотрудников";
			
		Запрос.Выполнить();
		
		ОписательТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
			Запрос.МенеджерВременныхТаблиц, "ВТСотрудники");
		
		КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательТаблиц, Истина, "ТекущийСпособРасчетаАванса,ТекущийАванс");
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	КадровыеДанныеСотрудников.Сотрудник,
			|	КадровыеДанныеСотрудников.ТекущийСпособРасчетаАванса КАК СпособРасчетаАванса,
			|	КадровыеДанныеСотрудников.ТекущийАванс КАК Аванс
			|ИЗ
			|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников";
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции	

// Функция возвращает перечень типов документов, являющихся начислениями за первую половину месяца.
// Тип документа содержится в ключе элемента соответствия, в качестве значения указывается Истина.
//
// Параметры:
//	ТипРезультата - строка - определяет тип возвращаемого значения,
//                           допустимые значения "Соответствие" (по умолчанию) и "Массив".
//
// Возвращаемое значение - Соответствие или Массив в зависимости от параметра ТипРезультата. 
//	Если передано недопустимое значение параметра, функция вернет Неопределено.
//
Функция ТипыДокументовНачисленияАванса(ТипРезультата = "Соответствие") Экспорт

	Возврат РасчетЗарплатыВнутренний.ТипыДокументовНачисленияАванса(ТипРезультата)	

КонецФункции 

#КонецОбласти

#Область КорректировкиВыплаты

Функция ТаблицаКорректировкиВыплаты()
	
	КорректировкиВыплаты = Новый ТаблицаЗначений;
	КорректировкиВыплаты.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	КорректировкиВыплаты.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	КорректировкиВыплаты.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	КорректировкиВыплаты.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	КорректировкиВыплаты.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	КорректировкиВыплаты.Колонки.Добавить("КорректировкаВыплаты", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	КорректировкиВыплаты.Колонки.Добавить("Первичная", Новый ОписаниеТипов("Булево"));
	КорректировкиВыплаты.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат КорректировкиВыплаты;
	
КонецФункции

// Добавляет в МенеджерВременныхТаблиц временную таблицу с данными КорректировкиВыплаты по документу.
//
Процедура СоздатьВТДанныеКорректировкиВыплатыПоДокументу(МенеджерВременныхТаблиц, ДокументСсылка, ОписаниеТаблицДокумента = Неопределено, ФизическиеЛица = Неопределено) Экспорт
	
	Если ФизическиеЛица = Неопределено Тогда
		ОтборПоФизическимЛицам = Ложь;
	Иначе
		ОтборПоФизическимЛицам = Истина;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаКорректировкиВыплаты.Ссылка,
	|	ТаблицаКорректировкиВыплаты.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаКорректировкиВыплаты.НомерСтроки,
	|	ТаблицаКорректировкиВыплаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТаблицаКорректировкиВыплаты.КорректировкаВыплаты
	|ПОМЕСТИТЬ ВТДанныеКорректировкиВыплаты
	|ИЗ
	|	#ТаблицаКорректировкиВыплаты КАК ТаблицаКорректировкиВыплаты
	|ГДЕ
	|	ТаблицаКорректировкиВыплаты.Ссылка = &Ссылка
	|	И ТаблицаКорректировкиВыплаты.ФизическоеЛицо В(&ФизическиеЛица)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаКорректировкиВыплаты.Ссылка,
	|	ТаблицаКорректировкиВыплаты.НомерСтроки,
	|	ТаблицаКорректировкиВыплаты.ИдентификаторСтроки,
	|	ТаблицаКорректировкиВыплаты.ФизическоеЛицо,
	|	ТаблицаКорректировкиВыплаты.КорректировкаВыплаты";
	
	Если ОписаниеТаблицДокумента = Неопределено Тогда
		ПолноеИмяДокумента = ДокументСсылка.Метаданные().ПолноеИмя();
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаКорректировкиВыплаты", ПолноеИмяДокумента + ".КорректировкиВыплаты");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаКорректировкиВыплаты", ОписаниеТаблицДокумента.ИмяТаблицыКорректировкиВыплаты);
	КонецЕсли;
	
	// Создаем временную таблицу
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Если ОтборПоФизическимЛицам Тогда
		Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ТаблицаКорректировкиВыплаты.ФизическоеЛицо В(&ФизическиеЛица)", "");
	КонецЕсли; 
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ФормаПодробнееОРасчетеНДФЛПриЗаполнении(Форма) Экспорт
	РасчетЗарплатыВнутренний.ФормаПодробнееОРасчетеНДФЛПриЗаполнении(Форма);
КонецПроцедуры

Функция КонтролируемыеПоляКорректировкиВыплатыДляФиксацииРезультатов() Экспорт
	
	Возврат РасчетЗарплатыВнутренний.КонтролируемыеПоляКорректировкиВыплатыДляФиксацииРезультатов();
	
КонецФункции

#КонецОбласти

#Область Свойства

// См. УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств.
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbf50-9802-11e9-80cd-4cedfb43b11a", Метаданные.ПланыВидовРасчета.Начисления);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbfee-9802-11e9-80cd-4cedfb43b11a", Метаданные.ПланыВидовРасчета.Удержания);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет данные начисления зарплаты за месяц.
// Параметры:
//	ДанныеНачисленияЗарплаты - объект, описывающий документ начисления зарплаты и 
//  содержащий следующий минимальный набор полей.
//		Ссылка
//		МесяцНачисления
//		Организация
//		Подразделение
//		Начисления - коллекция со списком начисления, со следующим 
//		минимальным набором полей:
//			Сотрудник
//			Подразделение
//			Начисление
//			Результат
//			ОтработаноДней
//			ОтработаноЧасов
//		Удержания и иные коллекции, содержащие данные начисления зарплаты, 
//		которые могут быть разными в зависимости от реализации документа начисления.
//		
Процедура ЗаполнитьДокументНачисленияЗарплаты(ДанныеНачисленияЗарплаты) Экспорт
	РасчетЗарплатыВнутренний.ЗаполнитьДокументНачисленияЗарплаты(ДанныеНачисленияЗарплаты);
КонецПроцедуры	

// Возвращает таблицу значений, содержащую данные начисления и расчета зарплаты.
//
// Параметры:
//	Организация
//	МесяцНачисления,
//	Документ - документ, для которого получаются данные для начисления.
//	Подразделение - не обязательный.
//	Сотрудники - не обязательный.
//
// Возвращаемое значение:
//	Таблица значений, которая содержит, как минимум, колонки.
//		Сотрудник
//		Подразделение
//		Начисление
//		Результат
//
Функция РезультатНачисленияРасчетаЗарплаты(Организация, МесяцНачисления, Документ, Подразделение = Неопределено, Сотрудники = Неопределено) Экспорт
	Возврат РасчетЗарплатыВнутренний.РезультатНачисленияРасчетаЗарплаты(Организация, МесяцНачисления, Документ, Подразделение, Сотрудники);
КонецФункции

Функция НачисленияТарифнойСтавки() Экспорт
	
	Возврат РасчетЗарплатыВнутренний.НачисленияТарифнойСтавки();
	
КонецФункции

Функция КатегорииСдельнойОплатыТруда() Экспорт
	
	Возврат РасчетЗарплатыВнутренний.КатегорииСдельнойОплатыТруда();
	
КонецФункции

Функция КатегорииНачисленийКомпенсационныхВыплат() Экспорт
	
	Возврат РасчетЗарплатыВнутренний.КатегорииНачисленийКомпенсационныхВыплат();
	
КонецФункции

// Заменяет строки таблицы ЗаменяемыеДанные строками таблица ЗаменяющиеДанные.
// Сопоставление строк выполняется по колонке КлючеваяКолонка для массива значений Значения.
// Если требуется - вставляет новые строки.
// Если требуется - удаляет неиспользованные старые строки.
//
// Параметры:
//	Значения
//	КлючеваяКолонка - имена колонок через запятую, по которым выполняется сопоставление заменяемой коллекции.
//	ЗаменяемыеДанные - коллекция строк, в которой необходимо выполнить замену.
//	ЗаменяющиеДанные - коллекция строк, которые нужно использовать для замены строк в коллекции ЗаменяемыеДанные.
//	УсловиеЗамены - структура, если указано, замена производится только в случае, 
//		если значения полей структуры по всем заменяемых строкам соблюдается.
//	ОтборСтрок - структура, если указано, в коллекциях обрабатываются только строки, удовлетворяющие отбору, 
//		в качестве значения может быть указан массив значений.
//
Процедура ЗаменитьСтрокиНаНовыеДанные(ЗаменяемыеДанные, ЗаменяющиеДанные, КлючевыеКолонки, УсловияЗамены = Неопределено, ОтборСтрок = Неопределено) Экспорт
	
	МассивКлючевыхКолонок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КлючевыеКолонки);
	
	// Собираем строки коллекции для обработки, 
	// если задан отбор строк, то добавляем только те, которые ему соответствуют.
	КоллекцииСтрок = Новый Массив;
	КоллекцииСтрок.Добавить(ЗаменяемыеДанные);
	КоллекцииСтрок.Добавить(ЗаменяющиеДанные);
	
	НайденныеСтроки = Новый Массив;
	Для Каждого КоллекцияСтрок Из КоллекцииСтрок Цикл
		Для Каждого СтрокаКоллекции Из КоллекцияСтрок Цикл
			СоответствуетУсловиям = Истина;
			Если ОтборСтрок <> Неопределено Тогда
				Для Каждого КлючИЗначение Из ОтборСтрок Цикл
					ИмяОтбора = КлючИЗначение.Ключ;
					ЗначениеОтбора = КлючИЗначение.Значение;
					Если ТипЗнч(ЗначениеОтбора) = Тип("Массив") Тогда
						СоответствуетУсловиям = ЗначениеОтбора.Найти(СтрокаКоллекции[ИмяОтбора]) <> Неопределено;
					Иначе
						СоответствуетУсловиям = СтрокаКоллекции[ИмяОтбора] = ЗначениеОтбора;
					КонецЕсли;
					Если Не СоответствуетУсловиям Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Если СоответствуетУсловиям Тогда
				НайденныеСтроки.Добавить(СтрокаКоллекции);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	СочетанияКлючевых = Новый ТаблицаЗначений;
	Для Каждого ИмяКолонки Из МассивКлючевыхКолонок Цикл
		СочетанияКлючевых.Колонки.Добавить(ИмяКолонки);
	КонецЦикла;
	
	// Используем ключ поиска для того, чтобы найти все строки 
	// и выбрать из них все возможные сочетания значений ключевых полей по обеим коллекциям.
	КлючОтбора = Новый Структура(КлючевыеКолонки);
	Для Каждого СтрокаКоллекции Из НайденныеСтроки Цикл
		ЗаполнитьЗначенияСвойств(КлючОтбора, СтрокаКоллекции);
		Если СочетанияКлючевых.НайтиСтроки(КлючОтбора).Количество() = 0 Тогда
			ЗаполнитьЗначенияСвойств(СочетанияКлючевых.Добавить(), СтрокаКоллекции);
		КонецЕсли;
	КонецЦикла;
		
	// Для всех возможных сочетаний выполняем замену строк.
	Для Каждого СочетаниеКлючевых Из СочетанияКлючевых Цикл
		ЗаполнитьЗначенияСвойств(КлючОтбора, СочетаниеКлючевых); 
		СтарыеСтроки = ЗаменяемыеДанные.НайтиСтроки(КлючОтбора);
		СоответствуетУсловиям = Истина;
		Если УсловияЗамены <> Неопределено Тогда
			// Если коллекция старых строк, имеет хотя бы одну строку, 
			// не соответствующую условиям замены - не обновляем.
			Для Каждого СтараяСтрока Из СтарыеСтроки Цикл
				Для Каждого КлючИЗначение Из УсловияЗамены Цикл
					Если СтараяСтрока[КлючИЗначение.Ключ] <> КлючИЗначение.Значение Тогда
						СоответствуетУсловиям = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если Не СоответствуетУсловиям Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Если Не СоответствуетУсловиям Тогда
			Продолжить;
		КонецЕсли;
		НовыеСтроки = ЗаменяющиеДанные.НайтиСтроки(КлючОтбора);
		ИндексСтроки = 0;
		ИндексДляВставки = 0;
		Для Каждого Строка Из НовыеСтроки Цикл
			Если СтарыеСтроки.ВГраница() < ИндексСтроки Тогда
				СтрокаДляЗаполнения = ЗаменяемыеДанные.Вставить(ИндексДляВставки);
			Иначе
				СтрокаДляЗаполнения = СтарыеСтроки[ИндексСтроки];
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтрокаДляЗаполнения, Строка);
			ИндексДляВставки = ЗаменяемыеДанные.Индекс(СтрокаДляЗаполнения) + 1;
			ИндексСтроки = ИндексСтроки + 1;
		КонецЦикла;
		Для Счетчик = ИндексСтроки По СтарыеСтроки.ВГраница() Цикл
			ЗаменяемыеДанные.Удалить(ЗаменяемыеДанные.Индекс(СтарыеСтроки[Счетчик]));
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает строку с измерениями расчета интервального регистра Плановых начислений.
//
Функция ИзмеренияРасчетаПлановыхНачислений() Экспорт
	
	Возврат РасчетЗарплатыВнутренний.ИзмеренияРасчетаПлановыхНачислений();
	
КонецФункции

#Область БлокФункцийПервоначальногоЗаполненияИОбновленияИБ
//

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РасчетЗарплаты.ЗаполнитьПланВидовРасчета";
	Обработчик.НачальноеЗаполнение = Истина;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РасчетЗарплаты.УстановитьРежимРасчетаДокументовПриРедактировании";
	Обработчик.НачальноеЗаполнение = Истина;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.8.3";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("2be9c11e-788b-4a0d-8c82-73d69f0cf008");
	Обработчик.Процедура = "Документы.Отпуск.ЗаполнитьДатыЗапрета";
	Обработчик.Комментарий = НСтр("ru = 'Заполняет даты запрета редактирования документов ""Отпуск"".'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.9.2";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("aa691371-3704-4445-bbee-7e35030f3c22");
	Обработчик.Процедура = "РасчетЗарплаты.СоздатьНачислениеОтпускБезОплаты";
	Обработчик.Комментарий = НСтр("ru='Создает начисление ""Отпуск без оплаты""'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.11.26";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("aa79122e-503c-4710-abb5-1386db6da48c");
	Обработчик.Процедура       = "РасчетЗарплаты.УстановитьПорядокОпределенияМесяцаДоходаВВидеОплатыТрудаПоУмолчанию";
	Обработчик.Комментарий     = НСтр("ru = 'Установить порядок определения месяца дохода в виде оплаты труда по умолчанию'");
	
КонецПроцедуры

Процедура ЗаполнитьПланВидовРасчета() Экспорт
	РасчетЗарплатыВнутренний.СформироватьПланВидовРасчетаПоНастройкам(Истина);
КонецПроцедуры

Процедура УстановитьРежимРасчетаДокументовПриРедактировании() Экспорт
	
	Если Не Константы.РассчитыватьДокументыПриРедактировании.Получить()
		И Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		Константы.РассчитыватьДокументыПриРедактировании.Установить(Истина);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура СоздатьНачислениеОтпускБезОплаты(ПараметрыОбновления = Неопределено) Экспорт
	
	РасчетЗарплатыВнутренний.СоздатьНачислениеОтпускБезОплаты(ПараметрыОбновления);
	
КонецПроцедуры

Процедура УстановитьПорядокОпределенияМесяцаДоходаВВидеОплатыТрудаПоУмолчанию(ПараметрыОбновления = Неопределено) Экспорт 
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.Ссылка КАК Ссылка
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПорядокОпределенияМесяцаДоходаВВидеОплатыТруда = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.ПорядокОпределенияМесяцаДоходаВВидеОплатыТруда.ПустаяСсылка)");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ВидРасчетаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВидРасчетаОбъект.ПорядокОпределенияМесяцаДоходаВВидеОплатыТруда = Перечисления.ПорядокОпределенияМесяцаДоходаВВидеОплатыТруда.ПоПериодуНачисления;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ВидРасчетаОбъект);
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	
КонецПроцедуры

#КонецОбласти

Функция РекомендуемыеКорректировкиВыплатыУстар(Налоги, ТекущиеКорректировкиВыплаты)
	
	КорректировкиВыплаты = ТаблицаКорректировкиВыплаты();
	
	ПараметрыОтбораСтрокКорректировки = Новый Структура("ФизическоеЛицо");
	Для Каждого СтрокаНалога Из Налоги Цикл
		ПараметрыОтбораСтрокКорректировки.ФизическоеЛицо = СтрокаНалога.ФизическоеЛицо;
		СтрокиКорректировок = ТекущиеКорректировкиВыплаты.НайтиСтроки(ПараметрыОтбораСтрокКорректировки);
		ТекущаяКорректировка = 0;
		ОстатокВзаиморасчетов = 0;
		Для Каждого СтрокаКорректировки Из СтрокиКорректировок Цикл
			ТекущаяКорректировка = ТекущаяКорректировка + СтрокаКорректировки.СуммаКорректировки;
			ОстатокВзаиморасчетов = ОстатокВзаиморасчетов + СтрокаКорректировки.СуммаВзаиморасчетов;
		КонецЦикла;
		
		Если СтрокаНалога.Налог < 0 ИЛИ ТекущаяКорректировка < 0 Тогда
			// Если текущая корректировка меньше нуля (уже переплатили) или налог меньше нуля (сейчас переплатим) 
			НоваяСтрока = КорректировкиВыплаты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНалога);
			// налог, который нельзя выплатить, но с учетом долга перед сотрудником
			НевыплачиваемыйНалог = СтрокаНалога.Налог + Мин(Макс(ОстатокВзаиморасчетов, 0), -СтрокаНалога.Налог);
			// Выбираем для корректировки наибольшую по модулю 
			НоваяСтрока.КорректировкаВыплаты = Мин(ТекущаяКорректировка, НевыплачиваемыйНалог);
			// Если корректируем из-за отрицательного налога, то это - "первичная" корректировка
			Если ТекущаяКорректировка > НевыплачиваемыйНалог Тогда
				НоваяСтрока.Первичная = Истина;
			КонецЕсли;
		ИначеЕсли СтрокаНалога.Налог < ТекущаяКорректировка Тогда
			// Если налог не позволяет полностью компенсировать ("занулить") текущую корректировку
			// компенсируем текущую корректировку пропорционально.
			КорректировкаВыплаты = СтрокаНалога.Налог;
			Коэффициент = КорректировкаВыплаты/ТекущаяКорректировка;
			Распределено = 0;
			МаксимальныйВесСтроки = 0;
			Для Каждого СтрокаКорректировки Из СтрокиКорректировок Цикл
				НоваяСтрока = КорректировкиВыплаты.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКорректировки);
				НоваяСтрока.КорректировкаВыплаты = СтрокаКорректировки.СуммаКорректировки * Коэффициент;
				Распределено = Распределено + НоваяСтрока.КорректировкаВыплаты;
				ВесСтроки = Макс(НоваяСтрока.КорректировкаВыплаты, -НоваяСтрока.КорректировкаВыплаты);
				Если МаксимальныйВесСтроки <= ВесСтроки Тогда
					МаксимальныйВесСтроки = ВесСтроки;
					СамаяТяжелаяСтрока = НоваяСтрока;
				КонецЕсли;
			КонецЦикла;
			// отнесем копейки остатка на самую "тяжелую" строку.
			СамаяТяжелаяСтрока.КорректировкаВыплаты = СамаяТяжелаяСтрока.КорректировкаВыплаты - (КорректировкаВыплаты - Распределено);
		Иначе
			// Если налог позволяет - просто "зануляем" текущую корректировку.
			Для Каждого СтрокаКорректировки Из СтрокиКорректировок Цикл
				НоваяСтрока = КорректировкиВыплаты.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКорректировки);
				НоваяСтрока.КорректировкаВыплаты = СтрокаКорректировки.СуммаКорректировки;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтборНулевыхСтрок = Новый Структура("КорректировкаВыплаты", 0);
	НулевыеСтроки = КорректировкиВыплаты.НайтиСтроки(ОтборНулевыхСтрок);
	Для Каждого НулеваяСтрока Из НулевыеСтроки Цикл
		КорректировкиВыплаты.Удалить(НулеваяСтрока);
	КонецЦикла;
	
	Возврат КорректировкиВыплаты;
	
КонецФункции

Функция НоваяТаблицаБазаУдержанийПоУмолчанию() Экспорт

	БазаПоУмолчанию = Новый ТаблицаЗначений();
	БазаПоУмолчанию.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	БазаПоУмолчанию.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	БазаПоУмолчанию.Колонки.Добавить("ГоловнойСотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	БазаПоУмолчанию.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	БазаПоУмолчанию.Колонки.Добавить("ТерриторияВыполненияРаботВОрганизации", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	БазаПоУмолчанию.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	
	Возврат БазаПоУмолчанию;

КонецФункции

#КонецОбласти


