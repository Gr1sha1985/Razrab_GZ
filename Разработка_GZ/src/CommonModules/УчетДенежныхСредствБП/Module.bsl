
///////////////////////////////////////////////////////////////////////////////
// РАБОТА С БАНКОВСКИМИ ДОКУМЕНТАМИ

// Форматирует сумму банковского платежного документа
//
// Параметры:
//  Сумма - число - реквизит, который надо отформатировать
//  ВыводитьСуммуБезКопеек - булево - флаг представления суммы без копеек
//
// Возвращаемое значение
//  Отформатированная строка
//
Функция ФорматироватьСуммуПлатежногоДокумента(Сумма, ВыводитьСуммуБезКопеек = Ложь) Экспорт
	
	Результат  = Сумма;
	ЦелаяЧасть = Цел(Сумма);
	
	Если Результат = ЦелаяЧасть Тогда
		Если ВыводитьСуммуБезКопеек Тогда
			Результат = Формат(Результат, "ЧДЦ=2; ЧРД='='; ЧГ=0");
			Результат = Лев(Результат, СтрНайти(Результат, "="));
		Иначе
			Результат = Формат(Результат, "ЧДЦ=2; ЧРД='-'; ЧГ=0");
		КонецЕсли;
	Иначе
		Результат = Формат(Результат, "ЧДЦ=2; ЧРД='-'; ЧГ=0");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ФорматироватьСуммуПлатежногоДокумента()

// Форматирует сумму прописью банковского платежного документа
//
// Параметры:
//  Сумма - число - реквизит, который надо представить прописью 
//  Валюта - СправочникСсылка.Валюты - валюта, в которой нужно представить сумму
//  ВыводитьСуммуБезКопеек - булево - флаг представления суммы без копеек
//
// Возвращаемое значение
//  Отформатированная строка
//
Функция ФорматироватьСуммуПрописьюПлатежногоДокумента(Сумма, Валюта, ВыводитьСуммуБезКопеек = Ложь) Экспорт
	
	Результат     = Сумма;
	ЦелаяЧасть    = Цел(Сумма);
	ФорматСтрока  = "Л=ru_RU; ДП=Ложь";
	ПарамПредмета = Валюта.ПараметрыПрописи;
	
	Если Результат = ЦелаяЧасть Тогда
		Если ВыводитьСуммуБезКопеек Тогда
			Результат = ЧислоПрописью(Результат, ФорматСтрока, ПарамПредмета);
			Результат = Лев(Результат, СтрНайти(Результат, "0") - 1);
		Иначе
			Результат = ЧислоПрописью(Результат, ФорматСтрока, ПарамПредмета);
		КонецЕсли;
	Иначе
		Результат = ЧислоПрописью(Результат, ФорматСтрока, ПарамПредмета);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ФорматироватьСуммуПрописьюПлатежногоДокумента()

Функция НаименованиеДляПечатныхФормИП(ФИОИП, НаименованиеПолное)
	
	Если ПустаяСтрока(ФИОИП.Фамилия) Тогда
		Возврат НаименованиеПолное;
	КонецЕсли;
	
	НаименованиеДляПечатныхФормИП = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'ИП %1 %2 %3'"),
		СокрЛП(ФИОИП.Фамилия),
		СокрЛП(ФИОИП.Имя),
		СокрЛП(ФИОИП.Отчество));
	
	Возврат НаименованиеДляПечатныхФормИП;
	
КонецФункции

Функция СвойстваВладельцаСчета(ВладелецСчета, Период = Неопределено)
	
	СвойстваВладельца = Новый Структура;
	СвойстваВладельца.Вставить("Наименование", "");
	СвойстваВладельца.Вставить("НаименованиеПолное", "");
	СвойстваВладельца.Вставить("НаименованиеДляПечатныхФорм", "");
	СвойстваВладельца.Вставить("НаименованиеПлательщикаПриПеречисленииВБюджет", "");
	
	Если ЗначениеЗаполнено(ВладелецСчета) Тогда
		Если ТипЗнч(ВладелецСчета) = Тип("СправочникСсылка.Организации") Тогда
			НаименованияОрганизации = Справочники.Организации.НаименованияНаДату(ВладелецСчета, Период);
			НаименованиеПлательщикаПриПеречисленииВБюджет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
															ВладелецСчета, "НаименованиеПлательщикаПриПеречисленииВБюджет");
			
			СвойстваВладельца.Наименование                = НаименованияОрганизации.СокращенноеНаименование;
			СвойстваВладельца.НаименованиеПолное          = НаименованияОрганизации.ПолноеНаименование;
			СвойстваВладельца.НаименованиеДляПечатныхФорм = НаименованияОрганизации.НаименованиеДляПечатныхФорм;
			СвойстваВладельца.НаименованиеПлательщикаПриПеречисленииВБюджет = НаименованиеПлательщикаПриПеречисленииВБюджет;
			Если Не Справочники.Организации.ЭтоЮрЛицо(ВладелецСчета) Тогда
				СвойстваВладельца.НаименованиеДляПечатныхФорм =
					НаименованиеДляПечатныхФормИП(НаименованияОрганизации.ФИО, СвойстваВладельца.НаименованиеПолное);
			КонецЕсли;
		ИначеЕсли ТипЗнч(ВладелецСчета) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			РеквизитыВладельца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВладелецСчета,
					"Наименование, ФИО");
			СвойстваВладельца.Наименование                = РеквизитыВладельца.Наименование;
			СвойстваВладельца.НаименованиеДляПечатныхФорм = РеквизитыВладельца.ФИО;
		ИначеЕсли ТипЗнч(ВладелецСчета) = Тип("СправочникСсылка.Контрагенты") Тогда
			НаименованияКонтрагента = Справочники.Контрагенты.НаименованияНаДату(ВладелецСчета, Период);
			СвойстваВладельца.Наименование                = НаименованияКонтрагента.СокращенноеНаименование;
			СвойстваВладельца.НаименованиеДляПечатныхФорм = НаименованияКонтрагента.НаименованиеДляПечатныхФорм;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СвойстваВладельца;
	
КонецФункции

Функция СвойстваБанковскогоСчета(БанковскийСчет)
	
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БанковскийСчет,
			"Банк, НомерСчета, БанкДляРасчетов, ТекстКорреспондента");
	Иначе
		Возврат Новый Структура("Банк, НомерСчета, БанкДляРасчетов, ТекстКорреспондента",
			БанковскийСчет.Банк, "", БанковскийСчет.БанкДляРасчетов, "");
	КонецЕсли;
	
КонецФункции

Функция НаименованиеПлательщикаПолучателяПоУмолчанию(ВладелецСчета, БанковскийСчет, ПеречислениеВБюджет = Ложь, Период = Неопределено, Знач СвойстваВладельца = Неопределено, Знач СвойстваБанковскогоСчета = Неопределено) Экспорт
	
	ЭтоОрганизация = ТипЗнч(ВладелецСчета) = Тип("СправочникСсылка.Организации");
	Если СвойстваВладельца = Неопределено Тогда
		СвойстваВладельца = СвойстваВладельцаСчета(ВладелецСчета, Период);
	КонецЕсли;
	
	Если СвойстваБанковскогоСчета = Неопределено Тогда
		СвойстваБанковскогоСчета = СвойстваБанковскогоСчета(БанковскийСчет);
	КонецЕсли;
	
	Если ЭтоОрганизация И Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(ВладелецСчета) Тогда
		Если ПеречислениеВБюджет Тогда
			// 383-П:
			// Ф.И.О.  и  правовой  статус [//Адрес регистрации//] [счет в банке-корреспонденте]
			СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВладелецСчета, Период);
			
			Наименование = ПлатежиВБюджетКлиентСервер.НаименованиеПлательщикаИндивидуальногоПредпринимателя(
				Справочники.Организации.ФамилияИмяОтчествоПредпринимателя(ВладелецСчета, Период),
				СведенияОбОрганизации.ЮридическийАдрес,
				Период);
		Иначе
			// 383-П:
			// для физических лиц - полностью фамилия, имя, отчество (если иное не вытекает из закона или национального обычая) (далее - Ф.И.О.);
			// для индивидуальных предпринимателей - Ф.И.О. и правовой статус;
			Наименование = СвойстваВладельца.НаименованиеДляПечатныхФорм;
		КонецЕсли;
	Иначе
		Наименование = СокрЛП(СвойстваВладельца.НаименованиеДляПечатныхФорм);
	КонецЕсли;

	Если ПустаяСтрока(Наименование) Тогда
		Наименование = СокрЛП(СвойстваВладельца.Наименование);
	КонецЕсли;

	Если ЗначениеЗаполнено(СвойстваБанковскогоСчета.БанкДляРасчетов)
			И ЗначениеЗаполнено(СвойстваБанковскогоСчета.Банк) Тогда
			
		СвойстваБанка  = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СвойстваБанковскогоСчета.Банк, "Наименование, Город");
		
		Наименование = СокрЛП(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 р/с %2 в %3 %4'"),
			Наименование, СвойстваБанковскогоСчета.НомерСчета, СокрЛП(СвойстваБанка.Наименование), СвойстваБанка.Город));
			
	КонецЕсли;
		
	Возврат Наименование;
	
КонецФункции

// Формируется текст плательщика или получателя для печатной формы платежного документа
//
// Параметры
//  ТекстНаименования  	– <строка> – значение реквизита документа, если реквизит заполнен, он и выводится на печать
//  ВладелецСчета  		– <СправочникСсылка.Организации>/<СправочникСсылка.Контрагенты> – владелец банковского счета
//  БанковскийСчет		– <СправочникСсылка.БанковскиеСчета> – банковский счет плательщика или получателя
//  ПеречислениеВБюджет	– <Булево> – флаг перечисления в бюджет
//
// Возвращаемое значение:
//   <Строка>			– наименование плательщика или получателя, которое будет выводиться в печатной форме платежного документа
//
Функция СформироватьТекстНаименованияПлательщикаПолучателя(ТекстНаименования, ВладелецСчета, БанковскийСчет, ПеречислениеВБюджет = Ложь, Период = Неопределено) Экспорт
	
	ТекстРезультат = ТекстНаименования;
	Если ПустаяСтрока(ТекстРезультат) Тогда
		
		ЭтоОрганизация = ТипЗнч(ВладелецСчета) = Тип("СправочникСсылка.Организации");
		
		СвойстваВладельца = СвойстваВладельцаСчета(ВладелецСчета, Период);
		
		СвойстваБанковскогоСчета = СвойстваБанковскогоСчета(БанковскийСчет);
		
		Если ЭтоОрганизация И ПеречислениеВБюджет
			И НЕ ПустаяСтрока(СвойстваВладельца.НаименованиеПлательщикаПриПеречисленииВБюджет) Тогда
			
			ТекстРезультат = СокрЛП(СвойстваВладельца.НаименованиеПлательщикаПриПеречисленииВБюджет);
			
		ИначеЕсли ПустаяСтрока(СвойстваБанковскогоСчета.ТекстКорреспондента) Тогда
			
			ТекстРезультат = НаименованиеПлательщикаПолучателяПоУмолчанию(
				ВладелецСчета,
				БанковскийСчет,
				ПеречислениеВБюджет,
				Период,
				СвойстваВладельца,
				СвойстваБанковскогоСчета);
			
		Иначе
			
			ТекстРезультат = СокрЛП(СвойстваБанковскогоСчета.ТекстКорреспондента);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстРезультат;
	
КонецФункции // СформироватьТекстНаименованияПлательщикаПолучателя

// Формирует значения по умолчанию реквизитов плательщика и получателя для банковских платежных документов
//
// Параметры
//  Плательщик  		– <СправочникСсылка.Организации>/<СправочникСсылка.Контрагенты> – плательщик, владелец банковского счета
//  СчетПлательщика		– <СправочникСсылка.БанковскиеСчета> – банковский счет плательщика
//  Получатель  		– <СправочникСсылка.Организации>/<СправочникСсылка.Контрагенты> – получатель, владелец банковского счета
//  СчетПолучателя		– <СправочникСсылка.БанковскиеСчета> – банковский счет получателя
//  ПеречислениеВБюджет	– <Булево> – флаг перечисления в бюджет
//
// Возвращаемое значение:
//   <Структура>		– структура строковых реквизитов плательщика и получателя
//						  ключи структуры: 
//							ТекстПлательщика, ИННПлательщика, КПППлательщика, 
//							ТекстПолучателя, ИННПолучателя, КПППолучателя
//							НаименованиеБанкаПлательщика, НомерСчетаПлательщика, БикБанкаПлательщика, СчетБанкаПлательщика 
//							НаименованиеБанкаПолучателя, НомерСчетаПолучателя, БикБанкаПолучателя, СчетБанкаПолучателя
//
Функция СформироватьАвтоЗначенияРеквизитовПлательщикаПолучателя(Плательщик, СчетПлательщика, Получатель, СчетПолучателя, ПеречислениеВБюджет = Ложь, Период = Неопределено, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	ЗначенияРеквизитов = Новый Структура;
	
	Если ЗначениеЗаполнено(Плательщик) Тогда
		СвойстваПлательщика = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Плательщик,
			"ЮридическоеФизическоеЛицо, ИНН");
		Если ТипЗнч(Плательщик) = Тип("СправочникСсылка.Контрагенты") Тогда
			СвойстваПлательщика.Вставить("КПП", Справочники.Контрагенты.КППНаДату(Плательщик, Период));
		ИначеЕсли ТипЗнч(Плательщик) = Тип("СправочникСсылка.Организации") Тогда
			СвойстваПлательщика.Вставить("КПП", Справочники.Организации.КППНаДату(Плательщик, Период));
		Иначе
			СвойстваПлательщика.Вставить("КПП", ""); // физ.лицо или не поддерживаемый тип
		КонецЕсли;
	Иначе
		СвойстваПлательщика = Новый Структура("ЮридическоеФизическоеЛицо, ИНН, КПП",
			Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо, "", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		СвойстваПлательщика.Вставить("КПП", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РегистрацияВНалоговомОргане, "КПП"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетПлательщика) Тогда
		СвойстваСчетаПлательщика = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетПлательщика,
			"Владелец, Банк, НомерСчета, БанкДляРасчетов, ВсегдаУказыватьКПП");
	Иначе
		СвойстваСчетаПлательщика = Новый Структура("Владелец, Банк, НомерСчета, БанкДляРасчетов, ВсегдаУказыватьКПП",
			Неопределено, СчетПлательщика.Банк, "", СчетПлательщика.БанкДляРасчетов, Ложь);
	КонецЕсли;
	
	ПолучательНерезидент = Ложь;
	Если ЗначениеЗаполнено(Получатель) Тогда
		Если ТипЗнч(Получатель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			СвойстваПолучателя = Новый Структура("ЮридическоеФизическоеЛицо, ИНН, КПП",
				Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо, Получатель.ИНН, "");
		Иначе
			СвойстваПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Получатель,
				"ЮридическоеФизическоеЛицо, ИНН");
			Если ТипЗнч(Получатель) = Тип("СправочникСсылка.Контрагенты") Тогда
				СвойстваПолучателя.Вставить("КПП", Справочники.Контрагенты.КППНаДату(Получатель, Период));
				СтранаРегистрации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Получатель, "СтранаРегистрации");
				ПолучательНерезидент = СтранаРегистрации <> Справочники.СтраныМира.Россия;
			Иначе
				СвойстваПолучателя.Вставить("КПП", Справочники.Организации.КППНаДату(Получатель, Период));
			КонецЕсли;
		КонецЕсли;
	Иначе
		СвойстваПолучателя = Новый Структура("ЮридическоеФизическоеЛицо, ИНН, КПП",
			Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо, "", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетПолучателя) Тогда
		СвойстваСчетаПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетПолучателя,
			"Владелец, Банк, НомерСчета, БанкДляРасчетов, ВсегдаУказыватьКПП, ТекстНазначения");
	Иначе
		СвойстваСчетаПолучателя = Новый Структура("Владелец, Банк, НомерСчета, БанкДляРасчетов, ВсегдаУказыватьКПП, ТекстНазначения",
			Неопределено, СчетПолучателя.Банк, "", СчетПолучателя.БанкДляРасчетов, Ложь, "");
	КонецЕсли;
	
	УплатаНалогаЗаТретьихЛиц = ПеречислениеВБюджет И ТипЗнч(Плательщик) = Тип("СправочникСсылка.Контрагенты");
	
	ОрганизацияПлательщик = Плательщик;
	Если УплатаНалогаЗаТретьихЛиц Тогда
		ПлательщикВБюджетИП = СвойстваПлательщика.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		ОрганизацияПлательщик = СвойстваСчетаПлательщика.Владелец;
	Иначе
		ПлательщикВБюджетИП = ТипЗнч(Плательщик) = Тип("СправочникСсылка.Организации") И
			СвойстваПлательщика.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	КонецЕсли;
	
	ЗначенияРеквизитов.Вставить("ТекстПлательщика",
		СформироватьТекстНаименованияПлательщикаПолучателя(
			"",
			ОрганизацияПлательщик,
			СчетПлательщика,
			ПеречислениеВБюджет,
			Период));
	
	Если ПеречислениеВБюджет И ПлательщикВБюджетИП И ПустаяСтрока(СвойстваПлательщика.ИНН) Тогда
		// Если платеж в бюджет, то согласно Приказу Минфина РФ от 12.11.2013 N 107н если у ИП нет ИНН, проставляется "0"
		ЗначенияРеквизитов.Вставить("ИННПлательщика", "0");
	Иначе
		ЗначенияРеквизитов.Вставить("ИННПлательщика", СвойстваПлательщика.ИНН);
	КонецЕсли;
	
	ВсегдаУказыватьКПППлательщика = ПеречислениеВБюджет ИЛИ СвойстваСчетаПлательщика.ВсегдаУказыватьКПП И НЕ ПлательщикВБюджетИП;
	ЗначенияРеквизитов.Вставить("ВсегдаУказыватьКПППлательщика", ВсегдаУказыватьКПППлательщика);
	ЗначенияРеквизитов.Вставить("КПППлательщика",
		?(ВсегдаУказыватьКПППлательщика,
		// Если платеж в бюджет, то согласно Приказу Минфина РФ от 12.11.2013 N 107н если это ИП, то в поле КПП проставляется "0"
		?(ПлательщикВБюджетИП, "0", СвойстваПлательщика.КПП),
		""));
	
	ПереводНаДругойСчет = ЗначениеЗаполнено(СчетПлательщика)
		И (СвойстваСчетаПолучателя.Владелец = СвойстваСчетаПлательщика.Владелец);
	Если ПереводНаДругойСчет Тогда
		ВладелецСчетаПолучателя = Плательщик;
		СвойстваВладельцаСчетаПолучателя = СвойстваПлательщика;
	Иначе
		ВладелецСчетаПолучателя = Получатель;
		СвойстваВладельцаСчетаПолучателя = СвойстваПолучателя;
	КонецЕсли;
	
	ЗначенияРеквизитов.Вставить("ТекстПолучателя",
		СформироватьТекстНаименованияПлательщикаПолучателя(
			"",
			ВладелецСчетаПолучателя,
			СчетПолучателя,
			ПеречислениеВБюджет,
			Период));
	
	ПолучательФизЛицо = СвойстваВладельцаСчетаПолучателя.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
	Если ПолучательФизЛицо И ПустаяСтрока(СвойстваВладельцаСчетаПолучателя.ИНН) Тогда
		// У получателя - физического лица (или ИП) может не быть ИНН,
		// в этом случае действуем так, как рекомендуется поступать в Приказу Минфина РФ от 12.11.2013 N 107н
		// когда у ИП отсутствует ИНН. Проставляем "0" - в этом случае проверка на заполенность ИНН не будет срабатывать.
		ЗначенияРеквизитов.Вставить("ИННПолучателя", "0");
	ИначеЕсли ПолучательНерезидент И ПустаяСтрока(СвойстваВладельцаСчетаПолучателя.ИНН) Тогда
		ЗначенияРеквизитов.Вставить("ИННПолучателя", "0");
	Иначе
		ЗначенияРеквизитов.Вставить("ИННПолучателя", СвойстваВладельцаСчетаПолучателя.ИНН);
	КонецЕсли;
	
	ВсегдаУказыватьКПППолучателя = ПеречислениеВБюджет ИЛИ СвойстваСчетаПолучателя.ВсегдаУказыватьКПП;
	ЗначенияРеквизитов.Вставить("ВсегдаУказыватьКПППолучателя", ВсегдаУказыватьКПППолучателя);
	ЗначенияРеквизитов.Вставить("КПППолучателя",
		?(ВсегдаУказыватьКПППолучателя,
		?(НЕ ПустаяСтрока(СвойстваВладельцаСчетаПолучателя.КПП), СвойстваВладельцаСчетаПолучателя.КПП, "0"),
		""));
	
	НепрямыеРасчетыУПлательщика = ЗначениеЗаполнено(СвойстваСчетаПлательщика.БанкДляРасчетов);
	БанкПлательщика = ?(НепрямыеРасчетыУПлательщика, СвойстваСчетаПлательщика.БанкДляРасчетов, СвойстваСчетаПлательщика.Банк);
	Если ЗначениеЗаполнено(БанкПлательщика) Тогда
		ЗначенияРеквизитов.Вставить("НаименованиеБанкаПлательщика",
			СокрЛП(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 %2'"),
				БанкПлательщика.Наименование, БанкПлательщика.Город)));
		ЗначенияРеквизитов.Вставить("НомерСчетаПлательщика",
			?(НепрямыеРасчетыУПлательщика, СвойстваСчетаПлательщика.Банк.КоррСчет, СвойстваСчетаПлательщика.НомерСчета));
		ЗначенияРеквизитов.Вставить("БикБанкаПлательщика",  БанкПлательщика.Код);
		ЗначенияРеквизитов.Вставить("СчетБанкаПлательщика", БанкПлательщика.КоррСчет);
	Иначе
		ЗначенияРеквизитов.Вставить("НаименованиеБанкаПлательщика", "");
		ЗначенияРеквизитов.Вставить("НомерСчетаПлательщика",        "");
		ЗначенияРеквизитов.Вставить("БикБанкаПлательщика",          "");
		ЗначенияРеквизитов.Вставить("СчетБанкаПлательщика",         "");
	КонецЕсли;
	
	НепрямыеРасчетыУПолучателя = ЗначениеЗаполнено(СвойстваСчетаПолучателя.БанкДляРасчетов);
	БанкПолучателя = ?(НепрямыеРасчетыУПолучателя, СвойстваСчетаПолучателя.БанкДляРасчетов, СвойстваСчетаПолучателя.Банк);
	
	Если ЗначениеЗаполнено(БанкПолучателя) Тогда
		СвойстваБанкаПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БанкПолучателя,
			"Код, Наименование, КоррСчет, Город");
	Иначе
		СвойстваБанкаПолучателя = Новый Структура("Код, Наименование, КоррСчет, Город", "", "", "", "");
	КонецЕсли;
	
	ЗначенияРеквизитов.Вставить("НаименованиеБанкаПолучателя",
		СокрЛП(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 %2'"),
			СвойстваБанкаПолучателя.Наименование, СвойстваБанкаПолучателя.Город)));
	ЗначенияРеквизитов.Вставить("НомерСчетаПолучателя",
		?(НепрямыеРасчетыУПолучателя, ?(ЗначениеЗаполнено(СвойстваСчетаПолучателя.Банк),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СвойстваСчетаПолучателя.Банк, "КоррСчет"), ""),
			СвойстваСчетаПолучателя.НомерСчета));
	ЗначенияРеквизитов.Вставить("БикБанкаПолучателя",     СвойстваБанкаПолучателя.Код);
	ЗначенияРеквизитов.Вставить("СчетБанкаПолучателя",    СвойстваБанкаПолучателя.КоррСчет);
	ЗначенияРеквизитов.Вставить("ТекстНазначенияПлатежа", СвойстваСчетаПолучателя.ТекстНазначения);
	
	Возврат ЗначенияРеквизитов;
	
КонецФункции //СформироватьАвтоЗначенияРеквизитовПлательщикаПолучателя

// Устанавливает банковский счет по умолчанию. Возвращает состояние установлен/не установлен
//
// Параметры:
//  Счет             - СправочникСсылка.БанковскиеСчета - текущее значение счета.
//  ВладелецСчета    – СправочникСсылка.Контрагенты, СправочникСсылка.Организации, СправочникСсылка.ФизическиеЛица - Контрагент (Организация, Физлицо),
//                     счет которого нужно получить.
//  Валюта           – СправочникСсылка.Валюты - валюта счета.
//  СовпадениеВалюты – Булево - признак совпадения нужной валюты с указанной, либо исключения ее из поиска.
//                     По умолчанию ищем счет с указанной валютой.
//  УчитыватьВалюту –  Булево - признак необходмости учитывать валюту при поиске счета.
//                     По умолчанию при поиске учитываем валюту.
//
// ВозвращаемоеЗначение:
//  Булево – установлен / не установлен счет по умолчанию
//
Функция УстановитьБанковскийСчет(Счет, ВладелецСчета, Валюта, СовпадениеВалюты = Истина, УчитыватьВалюту = Истина) Экспорт
	
	Если ТипЗнч(Счет) <> Тип("СправочникСсылка.БанковскиеСчета") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НовыйСчет = Справочники.БанковскиеСчета.ПустаяСсылка();
	Если НЕ ЗначениеЗаполнено(ВладелецСчета) Тогда
		ПолучитьНовыйСчет = Счет <> НовыйСчет;
		Счет = НовыйСчет;
		Возврат ПолучитьНовыйСчет;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	БанковскиеСчета.Ссылка,
	|	ВЫБОР
	|		КОГДА СправочникВладелец.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникВладелец
	|		ПО БанковскиеСчета.Владелец = СправочникВладелец.Ссылка
	|			И БанковскиеСчета.Ссылка = СправочникВладелец.ОсновнойБанковскийСчет
	|ГДЕ
	|	БанковскиеСчета.Владелец = &ВладелецСчета
	|	И БанковскиеСчета.ПометкаУдаления = ЛОЖЬ
	|	И (&УчитыватьВалюту = ЛОЖЬ
	|			ИЛИ (БанковскиеСчета.ВалютаДенежныхСредств = &Валюта
	|					И &СовпадениеВалюты = ИСТИНА
	|				ИЛИ НЕ БанковскиеСчета.ВалютаДенежныхСредств = &Валюта
	|					И &СовпадениеВалюты = ЛОЖЬ))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Запрос.УстановитьПараметр("ВладелецСчета",    ВладелецСчета);
	Запрос.УстановитьПараметр("Валюта",           Валюта);
	Запрос.УстановитьПараметр("СовпадениеВалюты", СовпадениеВалюты);
	Запрос.УстановитьПараметр("УчитыватьВалюту",  УчитыватьВалюту);
	
	Если ТипЗнч(ВладелецСчета) = Тип("СправочникСсылка.Контрагенты") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Справочник.Организации", "Справочник.Контрагенты");
	ИначеЕсли ТипЗнч(ВладелецСчета) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Справочник.Организации", "Справочник.ФизическиеЛица");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Результат    = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		НайденОсновнойСчет = Выборка.Приоритет = 1;
		НайденОдинСчет     = Выборка.Количество() = 1;
		
		Если НайденОсновнойСчет ИЛИ НайденОдинСчет Тогда
			НовыйСчет = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	ПолучитьНовыйСчет = Счет <> НовыйСчет;
	Если ПолучитьНовыйСчет Тогда
		Если НЕ ЗначениеЗаполнено(Счет) Тогда
			Счет = НовыйСчет;
		Иначе
			СвойствоСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Счет, "Владелец, ВалютаДенежныхСредств");
			Если СвойствоСчета.Владелец <> ВладелецСчета
				ИЛИ СовпадениеВалюты И СвойствоСчета.ВалютаДенежныхСредств <> Валюта Тогда
				Счет = НовыйСчет;
			Иначе
				ПолучитьНовыйСчет = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПолучитьНовыйСчет;
	
КонецФункции // УстановитьБанковскийСчет

Функция ОпределитьРегистрациюВНалоговомОргане(УчетныеДанные, Организация) Экспорт
	
	ТипРегистрацияВНалоговомОргане = Тип("СправочникСсылка.РегистрацииВНалоговомОргане");
	РегистрацияВНалоговомОргане = Организация.РегистрацияВНалоговомОргане;
	Для НомерСубконто = 1 По 3 Цикл
		ЗначениеСубконто = УчетныеДанные["Субконто" + НомерСубконто];
		Если ТипЗнч(ЗначениеСубконто) = ТипРегистрацияВНалоговомОргане Тогда
			РегистрацияВНалоговомОргане = ЗначениеСубконто;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат РегистрацияВНалоговомОргане;
	
КонецФункции

// Возвращает список программ типа "Клиент банка", имеющих сертификат 1С:Совместимо
//
// Параметры
//  Нет
//
// Возвращаемое значение:
//   <СписокЗначений>   - список наименований программ
//
Функция СписокСовместимыхПрограммКлиентовБанка(ВернутьВМассиве = Истина) Экспорт

	Если ВернутьВМассиве Тогда
		СписокКБ = Новый Массив;
	Иначе
		СписокКБ = Новый СписокЗначений;
	КонецЕсли; 
	
	СписокКБ.Добавить("DiasoftCLIENT 4x4 for Windows ЗАО ""Диасофт""");
	СписокКБ.Добавить("iBank 2 компании ""БИФИТ""");
	СписокКБ.Добавить("isFront - Система дистанционного управления финансами");
	СписокКБ.Добавить("LanVisit DOS 4.90 Ланит");
	СписокКБ.Добавить("On-Soft Клиент-Банк КБ ""ЛОКО-Банк""");
	СписокКБ.Добавить("WinClient ЗАО ""МПИ-Банк""");
	СписокКБ.Добавить("Yugo-Vostok Online ОАО БАНК ""ЮГО-ВОСТОК""");
	СписокКБ.Добавить("АРМ ""Клиент"" АС ""Клиент-Сбербанк"" Сбербанка России");
	СписокКБ.Добавить("АРМ ""Электронный клиент"" от НОМОС-БАНК-Сибирь");
	СписокКБ.Добавить("ИНИСТ Банк-Клиент ЗАО ""ИНИСТ""");
	СписокКБ.Добавить("Клиент банка InterBank v.5.1 R-Style Software Lab");
	СписокКБ.Добавить("Клиент-банк ""BARS"" фирмы ""Оникс Капитал""");
	СписокКБ.Добавить("Клиент-банк АКБ ""Лефко-Банк""");
	СписокКБ.Добавить("Клиент-банк ""МИБ"" АКБ ""Московский Индустриальный банк""");
	СписокКБ.Добавить("Клиент-Банк ОАО ""Прио-Внешторгбанк""");	
	СписокКБ.Добавить("Клиент-Банк РФК");
	СписокКБ.Добавить("Клиент-ТЕЛЕБАНК ЗАО ""Степ Ап""");
	СписокКБ.Добавить("Комплекс обмена платежными документами ""Курьер"". ЗАО ""АстраСофт""");
	СписокКБ.Добавить("Система ""MailBank"" фирмы ""Системные технологии""");
	СписокКБ.Добавить("Система ""PSB On-Line"" ОАО ""ПРОМСВЯЗЬБАНК""");
	СписокКБ.Добавить("Система ""ВЕДАНА"" фирмы ""Анива""");
	СписокКБ.Добавить("Система ""ДБО BS-Client"" ООО ""Банк Софт Системс""");
	СписокКБ.Добавить("Система ""Клиент-Банк Плюс"" ОАО ""Уралвнешторгбанка""");
	СписокКБ.Добавить("Система ""Клиент-Банк"" ЗАО ""Банк ""Новый Символ""");
	СписокКБ.Добавить("Система ""Электронный Офис"" ЗАО ""Райффайзенбанк""");
	СписокКБ.Добавить("Система ""Электронный Офис"" ОАО ""ИМПЭКСБАНК""");
	СписокКБ.Добавить("Система Банк-Клиент АКБ ""София""");
	СписокКБ.Добавить("Система Клиент-Банк ""BClient""");
	СписокКБ.Добавить("Система клиент-банк ""TIVAL""");
	СписокКБ.Добавить("Система электронных расчетов QuickPay ЗАО ""АО Кворум""");
	СписокКБ.Добавить("ЦФТ - Интернет-банк (Faktura.ru) фирмы ""Центр финансовых технологий""");
	СписокКБ.Добавить("Электронный клиент АКБ ""Автобанк""");
	
	Возврат СписокКБ;
	
КонецФункции 

Функция ПолучитьСтавкуНДСПоДоговору(Знач Период, Знач ДоговорКонтрагента) Экспорт
	
	Возврат Справочники.ДоговорыКонтрагентов.СтавкаНДСПоДоговору(Период, ДоговорКонтрагента);
	
КонецФункции

// Заполняет реквизиты платежного документа значениями по умолчанию
//
Процедура ЗаполнитьРеквизитыПлатежногоДокумента(ДокументОбъект) Экспорт
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	
	ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
	
	Если НЕ ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
		ДокументОбъект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	Если ОбщегоНазначенияБП.ЕстьНезаполненныйРеквизитДокумента("СтавкаНДС", ДокументОбъект, МетаданныеДокумента) Тогда
		ДокументОбъект.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПоУмолчанию(ДокументОбъект.Дата);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументОбъект.ВалютаДокумента) Тогда
		ДокументОбъект.ВалютаДокумента = Константы.ВалютаРегламентированногоУчета.Получить();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументОбъект.СчетОрганизации)
	   И ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
		УстановитьБанковскийСчет(ДокументОбъект.СчетОрганизации, ДокументОбъект.Организация, ДокументОбъект.ВалютаДокумента);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументОбъект.СчетКонтрагента)
	   И ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
		УстановитьБанковскийСчет(ДокументОбъект.СчетКонтрагента, ДокументОбъект.Контрагент, ДокументОбъект.ВалютаДокумента);
	КонецЕсли;
	
КонецПроцедуры 

Функция ПолучитьДоговорКонтрагента(Знач ПараметрыПлатежа) Экспорт
	
	НовыйДоговор  = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	ВидыДоговоров = УчетДенежныхСредствКлиентСервер.ОпределитьВидДоговораСКонтрагентом(ПараметрыПлатежа.ВидОперации);
	
	Если ПараметрыПлатежа.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПриобретениеИностраннойВалюты Тогда
		ОплатаВВалюте = Ложь;
	ИначеЕсли ПараметрыПлатежа.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажиИностраннойВалюты Тогда
		ОплатаВВалюте = Истина;
	Иначе
		ОплатаВВалюте = ПараметрыПлатежа.ОплатаВВалюте;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ОплатаВВалюте", Новый Структура("ЗначениеОтбора", ОплатаВВалюте));
	Если ОплатаВВалюте
		И ПараметрыПлатежа.ВидОперации <> Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажиИностраннойВалюты Тогда
		Отбор.Вставить("ВалютаВзаиморасчетов", Новый Структура("ЗначениеОтбора", ПараметрыПлатежа.ВалютаДокумента));
	КонецЕсли;
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
		НовыйДоговор, ПараметрыПлатежа.Контрагент, ПараметрыПлатежа.Организация, ВидыДоговоров, Отбор);
	
	Возврат НовыйДоговор;
	
КонецФункции

Функция ЗаполнитьДоговорКонтрагента(Документ, ДанныеЗаполнения, ОплатаВВалюте) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ.Контрагент)
		Или Не ЗначениеЗаполнено(Документ.РасшифровкаПлатежа) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	СтрокаРасшифровки = Документ.РасшифровкаПлатежа[0];
	
	Если ЗначениеЗаполнено(Документ.РасшифровкаПлатежа[0].ДоговорКонтрагента) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СвойстваДоговора = Новый Структура;
	СвойстваДоговора.Вставить("ВидОперации");
	СвойстваДоговора.Вставить("Организация");
	СвойстваДоговора.Вставить("Контрагент");
	СвойстваДоговора.Вставить("ДоговорКонтрагента");
	СвойстваДоговора.Вставить("ОплатаВВалюте");
	СвойстваДоговора.Вставить("ВалютаДокумента");
	ЗаполнитьЗначенияСвойств(СвойстваДоговора, Документ);
	ЗаполнитьЗначенияСвойств(СвойстваДоговора, ДанныеЗаполнения);
	
	СвойстваДоговора.ОплатаВВалюте = ОплатаВВалюте;
	
	СтрокаРасшифровки.ДоговорКонтрагента = ПолучитьДоговорКонтрагента(СвойстваДоговора);
	
	Возврат ЗначениеЗаполнено(СтрокаРасшифровки.ДоговорКонтрагента);
			
КонецФункции

Функция ПлатежГосОргану(ВидОперации, Контрагент) Экспорт
	
	Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога
		ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц Тогда
		Возврат Истина;
	Иначе
		Если УчетДенежныхСредствКлиентСервер.РасчетыСФизическимиЛицами(ВидОперации)
			ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю
			ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет
			ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП Тогда
			
			Возврат Ложь;
			
		ИначеЕсли ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(Контрагент) Тогда
			Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ГоловнойКонтрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ГоловнойКонтрагент");
			Если ЗначениеЗаполнено(ГоловнойКонтрагент) Тогда
				ГосударственныйОрган = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ГоловнойКонтрагент, "ГосударственныйОрган");
			Иначе
				ГосударственныйОрган = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ГосударственныйОрган");
			КонецЕсли;
			
			Возврат ГосударственныйОрган;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТЧИКОВ ОБНОВЛЕНИЯ НА НОВУЮ ВЕРСИЮ

// Банки

// Очистим справочник Банки от неиспользуемых элементов
// 
Процедура ОчиститьБанкиОтНеиспользуемыхЭлементов() Экспорт
	
	// удалим элементы, на которые нет ссылок
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Банк КАК Банк
	|ПОМЕСТИТЬ ВТ_АктуальныеБанки
	|ИЗ
	|	(ВЫБРАТЬ
	|		БанковскиеСчета.Банк КАК Банк
	|	ИЗ
	|		Справочник.БанковскиеСчета КАК БанковскиеСчета
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		БанковскиеСчета.БанкДляРасчетов
	|	ИЗ
	|		Справочник.БанковскиеСчета КАК БанковскиеСчета
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ВЫРАЗИТЬ(УдалитьСоответствияОбъектовИнформационныхБаз.УникальныйИдентификаторИсточника КАК Справочник.Банки)
	|	ИЗ
	|		РегистрСведений.УдалитьСоответствияОбъектовИнформационныхБаз КАК УдалитьСоответствияОбъектовИнформационныхБаз
	|	ГДЕ
	|		УдалитьСоответствияОбъектовИнформационныхБаз.ТипИсточника = ""СправочникСсылка.Банки""
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		УдалитьЛицевыеСчетаРаботниковОрганизации.Банк
	|	ИЗ
	|		РегистрСведений.УдалитьЛицевыеСчетаРаботниковОрганизации КАК УдалитьЛицевыеСчетаРаботниковОрганизации) КАК ВложенныйЗапрос
	|ГДЕ
	|	ЕСТЬNULL(ВложенныйЗапрос.Банк, НЕОПРЕДЕЛЕНО) <> НЕОПРЕДЕЛЕНО
	|	И ВложенныйЗапрос.Банк <> ЗНАЧЕНИЕ(Справочник.Банки.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Банк
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Банки.Ссылка КАК Банк
	|ИЗ
	|	Справочник.Банки КАК Банки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АктуальныеБанки КАК ВТ_АктуальныеБанки
	|		ПО Банки.Ссылка = ВТ_АктуальныеБанки.Банк
	|ГДЕ
	|	НЕ Банки.Ссылка.ЭтоГруппа
	|	И ВТ_АктуальныеБанки.Банк ЕСТЬ NULL ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	МассивБанков = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Банк");
	Для инд = 0 По МассивБанков.ВГраница() Цикл
		БанкОбъект = МассивБанков[инд].ПолучитьОбъект();
		БанкОбъект.ОбменДанными.Загрузка = Истина;
		БанкОБъект.Удалить();
	КонецЦикла;
	
	// удалим пусты группы
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Банки.Родитель КАК Родитель
	|ПОМЕСТИТЬ ВТ_АктуальныеРегионы
	|ИЗ
	|	Справочник.Банки КАК Банки
	|ГДЕ
	|	НЕ Банки.ЭтоГруппа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Родитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Банки.Ссылка КАК Банк
	|ИЗ
	|	Справочник.Банки КАК Банки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АктуальныеРегионы КАК ВТ_АктуальныеРегионы
	|		ПО Банки.Ссылка = ВТ_АктуальныеРегионы.Родитель
	|ГДЕ
	|	Банки.ЭтоГруппа
	|	И ВТ_АктуальныеРегионы.Родитель ЕСТЬ NULL ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	МассивГруппБанков = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Банк");
	Для инд = 0 По МассивГруппБанков.ВГраница() Цикл
		РегионОбъект = МассивГруппБанков[инд].ПолучитьОбъект();
		РегионОбъект.ОбменДанными.Загрузка = Истина;
		РегионОбъект.Удалить();
	КонецЦикла;
	
КонецПроцедуры

// Документы

// Процедура устанавливает Статью ДДС в банковски и кассовых документах
// из ТЧ РасшифровкаПлатежа в шапку документа, для "прочих" операций
// в которых ТЧ РасшифровкаПлатежа не используется
//
Процедура УстановитьВДокументахСтатьюДДСИзРасшифровкаПлатежа() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ЛОЖЬ КАК ОчищатьРасшифровку,
	|	МАКСИМУМ(ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств) КАК СтатьяДвиженияДенежныхСредств
	|ИЗ
	|	Документ.ПоступлениеНаРасчетныйСчет.РасшифровкаПлатежа КАК ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа
	|ГДЕ
	|	ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеДенежныхСредств.ПрочееПоступление)
	|	И ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств ЕСТЬ НЕ NULL 
	|	И ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств <> ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|	И ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка.СтатьяДвиженияДенежныхСредств = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеНаРасчетныйСчетРасшифровкаПлатежа.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка,
	|	ИСТИНА,
	|	МАКСИМУМ(СписаниеСРасчетногоСчетаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств)
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета.РасшифровкаПлатежа КАК СписаниеСРасчетногоСчетаРасшифровкаПлатежа
	|ГДЕ
	|	(СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПрочееСписание)
	|			ИЛИ СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПодотчетномуЛицу)
	|			ИЛИ СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет)
	|			ИЛИ СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП)
	|			ИЛИ СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога))
	|	И СписаниеСРасчетногоСчетаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств ЕСТЬ НЕ NULL 
	|	И СписаниеСРасчетногоСчетаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств <> ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|	И СписаниеСРасчетногоСчетаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств <> СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.СтатьяДвиженияДенежныхСредств
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
	|	ИСТИНА,
	|	МАКСИМУМ(ПриходныйКассовыйОрдерРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств)
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПриходныйКассовыйОрдерРасшифровкаПлатежа
	|ГДЕ
	|	(ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ПрочийПриход)
	|			ИЛИ ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ВозвратОтПодотчетногоЛица)
	|			ИЛИ ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ПолучениеНаличныхВБанке))
	|	И ПриходныйКассовыйОрдерРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств ЕСТЬ НЕ NULL 
	|	И ПриходныйКассовыйОрдерРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств <> ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|	И ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.СтатьяДвиженияДенежныхСредств = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка,
	|	ИСТИНА,
	|	МАКСИМУМ(РасходныйКассовыйОрдерРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств)
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасходныйКассовыйОрдерРасшифровкаПлатежа
	|ГДЕ
	|	(РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ПрочийРасход)
	|			ИЛИ РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыдачаПодотчетномуЛицу)
	|			ИЛИ РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыплатаЗаработнойПлатыПоВедомостям)
	|			ИЛИ РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыплатаЗаработнойПлатыРаботнику)
	|			ИЛИ РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВзносНаличнымиВБанк)
	|			ИЛИ РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыплатаДепонентов)
	|			ИЛИ РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.УплатаНалога))
	|	И РасходныйКассовыйОрдерРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств ЕСТЬ НЕ NULL 
	|	И РасходныйКассовыйОрдерРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств <> ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|	И РасходныйКассовыйОрдерРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств <> РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.СтатьяДвиженияДенежныхСредств
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка";
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	НачатьТранзакцию();
	Попытка
		
		Пока ВыборкаДокументов.Следующий() Цикл
			
			ДокументОбъект = ВыборкаДокументов.Ссылка.ПолучитьОбъект();
			ДокументОбъект.СтатьяДвиженияДенежныхСредств = ВыборкаДокументов.СтатьяДвиженияДенежныхСредств;
			ДокументОбъект.РасшифровкаПлатежа.Очистить();
			
			Если НЕ ВыборкаДокументов.ОчищатьРасшифровку Тогда
				НоваяСтрока = ДокументОбъект.РасшифровкаПлатежа.Добавить();
				НоваяСтрока.СуммаПлатежа = ДокументОбъект.СуммаДокумента;
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, Истина);
			
		КонецЦикла;
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ШаблонСообщения = НСтр("ru = 'Не удалось обработать документ
                                |%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			,
			ВыборкаДокументов.Ссылка,
			ТекстСообщения);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

//Возвращает значение Статьи движения денежных средств по умолчанию, в зависимости от контекста операции
//
// Параметры
//  КонтекстОперации    – <Строка>/<ПеречисленияСсылка> – Вид операции документа или строковая константа, однозначно идентифицирующая вид операции
//
// Возвращаемое значение:
//   <СправочникСсылка.СтатьиДвиженияДенежныхСредств>		– Ссылка на предопределенный элемент справочника или пустая ссылка
Функция СтатьяДДСПоУмолчанию(Знач КонтекстОперации) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиДвиженияДенежныхСредств")
		ИЛИ НЕ ЗначениеЗаполнено(КонтекстОперации) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтатьяДДС = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
	
	МенеджерСправочника = "Справочник.СтатьиДвиженияДенежныхСредств.";
	
	Если ТипЗнч(КонтекстОперации) = Тип("Строка") Тогда
		
		Если КонтекстОперации = "КомиссияБанка" Тогда
			СтатьяДДС = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(МенеджерСправочника + "УслугиБанков");
		ИначеЕсли КонтекстОперации = "НалогНаПрибыль" Тогда
			СтатьяДДС = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(МенеджерСправочника + "НалогНаПрибыль");
		ИначеЕсли КонтекстОперации = "УплатаПроцентов" Тогда
			СтатьяДДС = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(МенеджерСправочника + "ВозвратПроцентовЗаймаПолученного");
		ИначеЕсли КонтекстОперации = "ПогашениеДолга" Тогда
			СтатьяДДС = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(МенеджерСправочника + "ВозвратЗаймаПолученного");
		КонецЕсли;
		
	ИначеЕсли Типзнч(КонтекстОперации) = Тип("ПеречислениеСсылка.ВидыОперацийОтчетОРозничныхПродажах") Тогда
		
		СтатьяДДС = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(МенеджерСправочника + "РозничнаяВыручка");
		
	Иначе
		
		СтатьяДДС = УчетДенежныхСредствПовтИсп.ПолучитьСтатьюДДСПоВидуОперации(КонтекстОперации);
		
	КонецЕсли;

	Возврат СтатьяДДС;

КонецФункции

// Процедура очищает в банковски документах 
// ТЧ РасшифровкаПлатежа для отдельных операций
//
Процедура ОчиститьВДокументахРасшифровкуПлатежа() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета.РасшифровкаПлатежа КАК СписаниеСРасчетногоСчетаРасшифровкаПлатежа
	|ГДЕ
	|	СписаниеСРасчетногоСчетаРасшифровкаПлатежа.Ссылка.ВидОперации В(&ВидыОпераций)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасходныйКассовыйОрдерРасшифровкаПлатежа
	|ГДЕ
	|	РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации В(&ВидыОпераций)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПриходныйКассовыйОрдерРасшифровкаПлатежа
	|ГДЕ
	|	ПриходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.ВидОперации В(&ВидыОпераций)";
	
	ВидыОпераций = Новый Массив;
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПодотчетномуЛицу);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПрочееСписание);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийПКО.ПрочийПриход);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийПКО.ВозвратОтПодотчетногоЛица);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийПКО.ПолучениеНаличныхВБанке);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийРКО.ПрочийРасход);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийРКО.ВыдачаПодотчетномуЛицу);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыПоВедомостям);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийРКО.ВыплатаДепонентов);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийРКО.ВзносНаличнымиВБанк);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийРКО.УплатаНалога);
	
	Запрос.УстановитьПараметр("ВидыОпераций", ВидыОпераций);
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	НачатьТранзакцию();
	Попытка
		
		Пока ВыборкаДокументов.Следующий() Цикл
			
			ДокументОбъект = ВыборкаДокументов.Ссылка.ПолучитьОбъект();
			ДокументОбъект.РасшифровкаПлатежа.Очистить();
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, Истина);
			
		КонецЦикла;
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ШаблонСообщения = НСтр("ru = 'Не удалось обработать документ.
                                |%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			ВыборкаДокументов.Ссылка, 
			ТекстСообщения);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

// Процедура копирует значение из реквизита объекта СуммаУслуг
// в реквизит ТЧ РасшифровкаПлатежа СуммаУслуг документа ПоступлениеНаРасчетныйСчет
//
Процедура УстановитьСуммуУслугВТЧРасшифровкаПлатежаВДокументахПоступлениеНаРС() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеНаРасчетныйСчет.Ссылка КАК Ссылка,
	|	ПоступлениеНаРасчетныйСчет.СуммаУслуг КАК СуммаУслуг
	|ИЗ
	|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
	|ГДЕ
	|	ПоступлениеНаРасчетныйСчет.СуммаУслуг > 0
	|	И ПоступлениеНаРасчетныйСчет.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажПоПлатежнымКартамИБанковскимКредитам)";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	НачатьТранзакцию();
	Попытка
		
		Пока ВыборкаДокументов.Следующий() Цикл
			
			ДокументОбъект = ВыборкаДокументов.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект.РасшифровкаПлатежа.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ДокументОбъект.РасшифровкаПлатежа[0].СуммаУслуг = ВыборкаДокументов.СуммаУслуг;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
			
		КонецЦикла;
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ШаблонСообщения = НСтр("ru = 'Не удалось обработать документ.
                                |%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ПоступлениеНаРасчетныйСчет,
			ВыборкаДокументов.Ссылка, 
			ТекстСообщения);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

// Процедура копирует значение из реквизита объекта КурсНаДатуПриобретенияРеализацииВалюты
// в реквизит ТЧ РасшифровкаПлатежа КурсНаДатуПриобретенияРеализацииВалюты документа ПоступлениеНаРасчетныйСчет
//
Процедура УстановитьКурсНаДатуПриобретенияРеализацииВалютыВТЧРасшифровкаПлатежаВДокументахПоступлениеНаРС() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеНаРасчетныйСчет.Ссылка КАК Ссылка,
	|	ПоступлениеНаРасчетныйСчет.КурсНаДатуПриобретенияРеализацииВалюты КАК КурсНаДатуПриобретенияРеализацииВалюты
	|ИЗ
	|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
	|ГДЕ
	|	ПоступлениеНаРасчетныйСчет.КурсНаДатуПриобретенияРеализацииВалюты <> 0
	|	И ПоступлениеНаРасчетныйСчет.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажиИностраннойВалюты)";
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	НачатьТранзакцию();
	Попытка
		
		Пока ВыборкаДокументов.Следующий() Цикл
			
			ДокументОбъект = ВыборкаДокументов.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект.РасшифровкаПлатежа.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Для каждого СтрокаРасшифровкиПлатежа Из ДокументОбъект.РасшифровкаПлатежа Цикл
				СтрокаРасшифровкиПлатежа.КурсНаДатуПриобретенияРеализацииВалюты
					= ВыборкаДокументов.КурсНаДатуПриобретенияРеализацииВалюты;
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
			
		КонецЦикла;
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ШаблонСообщения = НСтр("ru = 'Не удалось обработать документ.
                                |%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ПоступлениеНаРасчетныйСчет,
			ВыборкаДокументов.Ссылка, 
			ТекстСообщения);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

Процедура ОбновитьРКОПоВыплатеДепонентов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходныйКассовыйОрдерВыплатаДепонентов.Ссылка КАК Ссылка,
	|	РасходныйКассовыйОрдерВыплатаДепонентов.ФизЛицо КАК ФизическоеЛицо
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер.ВыплатаДепонентов КАК РасходныйКассовыйОрдерВыплатаДепонентов
	|ГДЕ
	|	РасходныйКассовыйОрдерВыплатаДепонентов.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВыплатаДепонентов)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходныйКассовыйОрдерВыплатаДепонентов.Ссылка,
	|	РасходныйКассовыйОрдерВыплатаДепонентов.ФизЛицо";
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ДокументОбъект = ВыборкаДокументов.Ссылка.ПолучитьОбъект();
		ДокументОбъект.Контрагент = ВыборкаДокументов.ФизическоеЛицо;
		Если НЕ ЗначениеЗаполнено(ВыборкаДокументов.ФизическоеЛицо) Тогда
			ДокументОбъект.РучнаяКорректировка = Истина;
		КонецЕсли;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ИсправитьОчередностьПлатежаВРеквизитахДляУплатыВБюджет() Экспорт
	
	НаборЗаписей = РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	Для каждого Запись Из НаборЗаписей Цикл
		
		Если Запись.ОчередностьПлатежа = 3 ИЛИ Запись.ОчередностьПлатежа = 4 ИЛИ Запись.ОчередностьПлатежа = 6 Тогда
			
			Запись.ОчередностьПлатежа = 5;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СПРАВКИ О БАНКОВСКИХ СЧЕТАХ

Процедура ЗаполнитьСведенияОрублевыхСчетах(ПараметрыОтчета, Контейнер) Экспорт
	
	СведенияОСчетах = СведенияОСчетах(ПараметрыОтчета);
	
	Контейнер.ДанныеДопСтрок.Очистить();
	
	Индекс = 0;
	Для каждого Счет Из СведенияОСчетах Цикл
		
		Если ПустаяСтрока(Счет.НомерСчета) Тогда
			Продолжить;
		КонецЕсли;
		
		Контейнер.ДанныеДопСтрок.Добавить();
		
		НомерСтроки = Индекс + 1;
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101001"] = Формат(НомерСтроки, "");
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101002"] = Счет.ВидСчета;
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101003"] = Счет.НомерСчета;
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101004"] = Счет.БанкНаименование;
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101005"] = АдресБанка(Счет);
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101006"] = Счет.ДатаОткрытия;
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101007"] = Счет.ДатаЗакрытия;
		
		Индекс = Индекс + 1;
		
	КонецЦикла
	
КонецПроцедуры

Процедура ЗаполнитьСведенияОСчетахвВиностраннойВалюте(ПараметрыОтчета, Контейнер) Экспорт
	
	СведенияОСчетах = СведенияОСчетах(ПараметрыОтчета, Истина);
	
	Контейнер.ДанныеДопСтрок.Очистить();
	
	Индекс = 0;
	Для каждого Счет Из СведенияОСчетах Цикл
		
		Если ПустаяСтрока(Счет.НомерСчета) Тогда
			Продолжить;
		КонецЕсли;
		
		Контейнер.ДанныеДопСтрок.Добавить();
		
		НомерСтроки = Индекс + 1;
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101001"] = Формат(НомерСтроки, "");
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101002"] = Счет.НомерСчета;
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101003"] = Счет.БанкНаименование;
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101004"] = АдресБанка(Счет, Истина);
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101005"] = Счет.НомерИДатаРазрешения;
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101006"] = Счет.ДатаОткрытия;
		Контейнер.ДанныеДопСтрок[Индекс]["П010000101007"] = Счет.ДатаЗакрытия;
		
		Индекс = Индекс + 1;
		
	КонецЦикла
	
КонецПроцедуры

Функция АдресБанка(Счет, Валютный = Ложь)
	
	Адрес = Новый Массив;
	
	Если Валютный И НЕ ПустаяСтрока(Счет.БанкСтрана) Тогда
		Адрес.Добавить(Счет.БанкСтрана);
	КонецЕсли;
	
	Если ПустаяСтрока(Счет.БанкАдрес) ИЛИ СокрЛП(Счет.БанкАдрес) = "," Тогда
		Если НЕ ПустаяСтрока(Счет.БанкГород) Тогда
			Адрес.Добавить(Счет.БанкГород);
		КонецЕсли;
	Иначе
		Адрес.Добавить(Счет.БанкАдрес);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Счет.БанкТелефоны) Тогда
		Адрес.Добавить(Счет.БанкТелефоны);
	КонецЕсли;
	
	Возврат СтрСоединить(Адрес, ", ");
	
КонецФункции

Функция СведенияОСчетах(ПараметрыОтчета, Валютный = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",  ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("ПериодОтчета", КонецДня(ПараметрыОтчета.мДатаКонцаПериодаОтчета + 1)); // Из отчета период приходит на конец предыдущего дня.
	Запрос.УстановитьПараметр("Валютный",     Валютный);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	БанковскиеСчета.ВидСчета КАК ВидСчета,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	ЕСТЬNULL(БанковскиеСчета.Банк.Наименование, """") КАК БанкНаименование,
	|	ЕСТЬNULL(БанковскиеСчета.Банк.Страна.Наименование, """") КАК БанкСтрана,
	|	ЕСТЬNULL(БанковскиеСчета.Банк.Адрес, """") КАК БанкАдрес,
	|	ЕСТЬNULL(БанковскиеСчета.Банк.Телефоны, """") КАК БанкТелефоны,
	|	ЕСТЬNULL(БанковскиеСчета.Банк.Город, """") КАК БанкГород,
	|	БанковскиеСчета.НомерИДатаРазрешения КАК НомерИДатаРазрешения,
	|	БанковскиеСчета.ДатаОткрытия КАК ДатаОткрытия,
	|	БанковскиеСчета.ДатаЗакрытия КАК ДатаЗакрытия
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Владелец = &Организация
	|	И БанковскиеСчета.Валютный = &Валютный
	|	И НЕ БанковскиеСчета.ПометкаУдаления
	|	И (БанковскиеСчета.ДатаОткрытия = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ БанковскиеСчета.ДатаОткрытия <= &ПериодОтчета)
	|	И (БанковскиеСчета.ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ БанковскиеСчета.ДатаЗакрытия > &ПериодОтчета)";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСведенияОПоказателяхСправкиОРублевыхСчетах(ПоказателиОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101001", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101002", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101003", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101004", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101005", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101006", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101007", Истина, Ложь);
	
	Возврат ПоказателиОтчета;
	
КонецФункции

Функция ПолучитьСведенияОПоказателяхСправкиОСчетахвВиностраннойВалюте(ПоказателиОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101001", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101002", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101003", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101004", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101005", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101006", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П010000101007", Истина, Ложь);
	
	Возврат ПоказателиОтчета;
	
КонецФункции
