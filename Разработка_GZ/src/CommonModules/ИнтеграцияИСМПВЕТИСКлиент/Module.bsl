/////////////////////////////////////////////////////////////////////////////
// Совместная работа подсистем ВетИС и ИСМП.
//   * Если подсистема ВетИС отсутствует, изменений не требуется.
//

#Область ПрограммныйИнтерфейс

#Область ФормаУточненияДанныхИС

// Обработчик выбора произвольного идентификатора происхождения (не из списка выбора)
// 
// Параметры:
//   Форма                - ФормаКлиентскогоПриложения -  ОбщаяФорма.ФормаУточненияДанныхИС
//   СтандартнаяОбработка - Булево - признак стандартной обработки события
//
Процедура ИдентификаторПроисхожденияВЕТИСОбработкаВыбора(Форма, СтандартнаяОбработка) Экспорт
	
	ИнтеграцияИСМПВЕТИСКлиентПереопределяемый.ИдентификаторПроисхожденияВЕТИСОбработкаВыбора(Форма, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("ГосИС.ВетИС") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Отказ = Ложь;
	Если Форма.ХарактеристикиИспользуются И Не ЗначениеЗаполнено(Форма.Характеристика) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Характеристика"" не заполнено.'"),, "Характеристика",, Отказ);
	КонецЕсли;
	Если Форма.ТребуетсяУказаниеСерии И ЗначениеЗаполнено(Форма.Склад) И Не ЗначениеЗаполнено(Форма.Серия) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Серия"" не заполнено.'"),, "Серия",, Отказ);
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МодульВЕТИС = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияВЕТИСВызовСервера");
	Продукция = МодульВЕТИС.СопоставленнаяПродукция(Форма.Номенклатура, Форма.Характеристика, Форма.Серия);
	
	Отбор = Новый Структура;
	Отбор.Вставить("Продукция", Продукция);
	Если Форма.ЭтоКодМаркировкиСоСрокомГодности Тогда
		Отбор.Вставить("СкоропортящаясяПродукция", Форма.Скоропортящаяся);
	КонецЕсли;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	МодульВЕТИС = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияВЕТИСКлиент");
	ИмяФормыВыбора = МодульВЕТИС.ИмяФормыВыбораПоТипу(Форма.ПараметрыСканирования.ТипИдентификатораПроисхожденияВЕТИС);
	
	ОповещениеВыбора = Новый ОписаниеОповещения("ВыборИдентификатораПроисхожденияВЕТИСЗавершение", Форма);
	ОткрытьФорму(ИмяФормыВыбора, ПараметрыФормы,,,,, ОповещениеВыбора);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти