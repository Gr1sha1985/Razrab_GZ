////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиСлужебныйКлиент: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает расширение файла.
//
// Параметры:
//  ПутьКФайлу - Строка - полный путь к файлу.
// 
// Возвращаемое значение:
// Строка - расширение файла.
//
Функция РасширениеФайла(ПутьКФайлу) Экспорт
	
	Позиция = СтрНайти(ПутьКФайлу, ".", НаправлениеПоиска.СКонца);
	
	Если Позиция > 0 Тогда
		Возврат Сред(ПутьКФайлу, Позиция + 1);
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

#Область ОбработкаЭлектронныхДокументов

// Выполняет указанные действия с электронным документом, привязанным к владельцу
// 
// Параметры:
//  МассивСсылокНаОбъект - Массив из ОпределяемыйТип.ВладелецОбменСБанками, ДокументСсылка.СообщениеОбменСБанками - 
//                         ссылки на владельцев электронных документов или на сообщения обмена.
//  Действия - Строка - действия, которые необходимо выполнить
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - обрабатываемый электронный документ.
//
Процедура ОбработатьЭлектронныеДокументы(МассивСсылокНаОбъект, Действия, СообщениеОбмена = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивСсылокНаОбъект", МассивСсылокНаОбъект);
	ДополнительныеПараметры.Вставить("Действия", Действия);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	
	MAC = ЗначениеИзКеша("Фрод");
	Если ЗначениеЗаполнено(MAC) Тогда
		ДополнительныеПараметры.Вставить("Фрод", Новый Структура("MAC", ЗначениеИзКеша("Фрод")));
	Иначе
		ДополнительныеПараметры.Вставить("Фрод", Новый Структура);
	КонецЕсли;
	
	ПользовательОповещенОПлатформе8_3_18 = ЗначениеИзКеша("ПользовательОповещенОПлатформе8_3_18");
	
	Если ПользовательОповещенОПлатформе8_3_18 <> Неопределено Тогда
		ДополнительныеПараметры.Фрод.Вставить(
			"ПользовательОповещенОПлатформе8_3_18", ПользовательОповещенОПлатформе8_3_18);
	КонецЕсли;
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПоОбработкеЭлектронныхДокументов(
		ДополнительныеПараметры);
	
	Если Результат.ТребуетсяКриптография Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПолученияОтпечатковОбработатьЭД", ЭтотОбъект, ДополнительныеПараметры);
		ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина);
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	Оповещение = Новый ОписаниеОповещения("ПослеОбработкиДокументов", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат.Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

// Добавляет подписи в электронный документ.
//
// Параметры:
//   Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//      * РезультатВызова - Булево - Истина - подписи добавлены и валидны, Ложь - произошла ошибка или невалидна подпись.
//      * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//   СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - ссылка на сообщение обмена;
//   ДанныеПодписей - Массив из ДвоичныеДанные - данные подписей.
//
Процедура ДобавитьПодписиИОпределитьСтатусы(Оповещение, СообщениеОбмена, ДанныеПодписей) Экспорт
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("СообщениеОбмена", СообщениеОбмена);
	ПараметрыОбработки.Вставить("ДанныеПодписей", ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ДанныеПодписей));
	ПараметрыОбработки.Вставить("ОповещениеПослеДобавленияИПроверкиПодписей", Оповещение);
		
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьСертификатыИзПодписиПослеПолученияМенеджераКриптографии", ЭтотОбъект, ПараметрыОбработки);
		
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(Оповещение, "ПолучениеСертификатов");
	
КонецПроцедуры

// Обработчик асинхронного вызова
Процедура ПриОткрытииЭлектронногоДокумента(Результат, Параметры) Экспорт
	
	ОткрытьЭДДляПросмотра(Параметры, , , Истина);
	
КонецПроцедуры

#КонецОбласти

#Область Аутентификация

// Процедура интерактивно запрашивает логин и пароль у пользователя.
//
// Параметры:
//  ОписаниеОповещенияОЗакрытии - ОписаниеОповещения - обработчик, вызываемый при завершении процедуры со следующими параметрами:
//    * Результат - Структура - данные аутентификации, содержит поля:
//                    * Логин - Строка - логин пользователя
//                    * Пароль - Строка - пароль пользователя.
//              - Неопределено - пользователь отказался вводить данные аутентификации
//    * ДополнительныеПараметры - Произвольный - параметры, указанные при создании оповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена;
//
Процедура ПолучитьДанныеАутентификации(ОписаниеОповещенияОЗакрытии, НастройкаОбмена) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ВидОперации", НСтр("ru = 'Аутентификация на сервере банка'"));
	ПараметрыФормы.Вставить("НастройкаОбмена", НастройкаОбмена);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросПароляКСертификату",
		ПараметрыФормы, , , , , ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

// Функция проверяет, получен ли ранее пароль для авторизации на сервере банка.
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//  ДанныеАвторизации - Структура - содержит следующие записи:
//    * Логин - строка - имя пользователя;
//    * Пароль - Строка - пароль пользователя.
//
// Возвращаемое значение:
//  Булево - Истина - если пароль для сертификата ЭП получен ранее, иначе - Ложь.
//
Функция ПолученыДанныеАвторизации(НастройкаОбмена, ДанныеАвторизации) Экспорт
	
	Если Не ЗначениеЗаполнено(НастройкаОбмена) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеАутентификации = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками.ПаролиСеанса"];
	Если ТипЗнч(ДанныеАутентификации) = Тип("ФиксированноеСоответствие") Тогда
		ДанныеАвторизации = ДанныеАутентификации.Получить(НастройкаОбмена);
		Если ЗначениеЗаполнено(ДанныеАвторизации) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Удаляет пароль, сохраненный на время сеанса.
//
// Параметры:
//  ОбъектПривязки - СправочникСсылка.НастройкиОбменСБанками - ссылка на объект, к которому относится пароль.
//
Процедура УдалитьПарольИзСеанса(ОбъектПривязки) Экспорт
	
	СоответствиеСертификатаИПароля = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками.ПаролиСеанса"];
	Если ТипЗнч(СоответствиеСертификатаИПароля) = Тип("ФиксированноеСоответствие")
		И СоответствиеСертификатаИПароля.Получить(ОбъектПривязки) <> Неопределено Тогда
		Соответствие = Новый Соответствие;
		Для Каждого Элемент Из СоответствиеСертификатаИПароля Цикл
			Если Элемент.Ключ <> ОбъектПривязки Тогда
				Соответствие.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЕсли;
		КонецЦикла;
		СоответствиеПаролей = Новый ФиксированноеСоответствие(Соответствие);
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками.ПаролиСеанса", СоответствиеПаролей);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Выписка

// Обработчик события из ОбменСБанкамиКлиент.ВключитьАвтоматическоеПолучениеВыписки
//
Процедура ПослеВключенияАвтоматическогоПолученияВыписки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПрикладногоРешения, Истина);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПрикладногоРешения, Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму разбора банковской выписки.
//
// Параметры:
//    СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение с выпиской банка.
//
Процедура РазобратьВыпискуБанка(СообщениеОбмена) Экспорт
	
	Если ОбменСБанкамиСлужебныйВызовСервера.УстановленныеПодписиВалидны(СообщениеОбмена) Тогда
		ОбменСБанкамиКлиентПереопределяемый.РазобратьВыпискуБанка(СообщениеОбмена);
	Иначе
		ШаблонТекста = НСтр("ru = 'Разбор документа %1.
							|Документ не обработан, так как содержит невалидные подписи.'");
		Текст = СтрШаблон(ШаблонТекста, СообщениеОбмена);
		ОбщегоНазначенияКлиент.СообщитьПользователю(Текст);
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму банковских выписок.
//
Процедура ОткрытьБанковскиеВыписки(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОткрытьФорму("Документ.СообщениеОбменСБанками.Форма.ВыпискиБанка");
	
КонецПроцедуры

#КонецОбласти

#Область Синхронизация

// Показывает результат синхронизации
//
// Параметры:
//  Отправлено - Число - количество отправленных пакетов.
//  Получено - Число - количество полученных пакетов.
//
Процедура ПоказатьРезультатСинхронизации(Отправлено, Получено = 0) Экспорт
	
	ЗаголовокОповещения = НСтр("ru = 'Обмен 1С:ДиректБанк'");
	ШаблонОповещения = НСтр("ru = 'Отправлено: (%1), получено: (%2).'");
	ТекстОповещения = СтрШаблон(ШаблонОповещения, Отправлено, Получено);
	ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	
КонецПроцедуры

// Выполняет синхронизацию с банком по одной настройке обмена .
//
// Параметры:
//   Оповещение - ОписаниеОповещения - обработчик, вызываемый после выполнения операции
//     * Успех - Булево - если Ложь, то произошла ошибка
//     * ДополнительныеПараметры - Структура - параметры.
//   НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком
//   ВыводитьОкноОжидания - Булево - признак необходимости вывода окна ожидания
//   ОповещениеЗапускаДлительнойОперации - ОписаниеОповещения - обработчик, вызываемый после запуска длительной операции.
//                                                              Используется для прерывания процесса по требованию пользователя
//     * ИдентификаторЗадания - УникальныйИдентификатор - идентификатор длительной операции
//     * ДополнительныеПараметры - Структура - дополнительные параметры.
//
Процедура СинхронизироватьСБанком(
	Оповещение,
	НастройкаОбмена,
	ВыводитьОкноОжидания = Ложь,
	ОповещениеЗапускаДлительнойОперации = Неопределено) Экспорт
	
	ТекстСообщения = НСтр("ru = 'Синхронизация документов с банком. Подождите...'");
	Состояние(НСтр("ru = 'Отправка документов.'"), , ТекстСообщения);
	
	ПараметрыАвторизации = Новый Соответствие;
	Параметры = Новый Структура;
	Параметры.Вставить("ПараметрыАвторизации", ПараметрыАвторизации);
	Параметры.Вставить("ОповещениеПослеСинхронизации", Оповещение);
	Параметры.Вставить("ОповещениеЗапускаДлительнойОперации", ОповещениеЗапускаДлительнойОперации);
	Параметры.Вставить("ВыводитьОкноОжидания", ВыводитьОкноОжидания);
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("ТребуетсяАвторизация", Истина);
	
	РеквизитыНастройкиОбмена = Новый Структура("ПрограммаБанка, АутентификацияПоСертификату, ИспользуетсяКриптография,
		|Недействительна, ИмяВнешнегоМодуля, АдресСервера, ИдентификаторОрганизации, ВерсияФормата,
		|ДополнительнаяОбработка, ПравоВыполненияОбмена, ПодписываетсяЗапросВыписки, Организация, Банк");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
		
	Если Не ВозможноВыполнитьСинхронизацию(НастройкаОбмена, РеквизитыНастройкиОбмена, Оповещение) Тогда
		Возврат;
	КонецЕсли;
		
	Параметры.Вставить("РеквизитыНастройкиОбмена", РеквизитыНастройкиОбмена);
		
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		СинхронизироватьСбербанк(Параметры);
	ИначеЕсли РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		СинхронизироватьАсинхронныйОбмен(Параметры);
	ИначеЕсли РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.FintechIntegration") Тогда
		// Только получение статусов, т.к. при отправке не формируется пакет
		ПолучитьДокументыЧерезFintech(Параметры);
	Иначе // АльфаБанк, обработка и компонента.
		Параметры.Вставить("ТребуетсяАвторизация", Ложь);
		ОтправитьДокументыВБанк(Неопределено, Параметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Сбербанк

// Выполняет кеширование данных авторизации на сервере сбербанка.
//
// Параметры:
//  Название - Строка - название кэшируемого параметра;
//  Значение - Произвольный - значение кэшируемого параметра.
//
Процедура ЗакешироватьПараметрСбербанка(Название, Значение) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) <> Тип("Соответствие") Тогда
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками", Новый Соответствие);
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	КонецЕсли;
	
	ПараметрыОбменаСбербанк = ПараметрыПодсистемыОбменСБанками.Получить("Сбербанк");
	
	Если ПараметрыОбменаСбербанк = Неопределено Тогда
		ПараметрыОбменаСбербанк = Новый Структура;
	КонецЕсли;
	
	ПараметрыОбменаСбербанк.Вставить(Название, Значение);
	ПараметрыПодсистемыОбменСБанками.Вставить("Сбербанк", ПараметрыОбменаСбербанк);
	
КонецПроцедуры

// Получает двоичные данные сертификата подписи с токена.

//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                    которая будет вызвана после завершения вызова метода со следующими параметрами:
//    Результат - ДвоичныеДанные - данные сертификата;
//              - Неопределено - произошла ошибка.
//    ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта Оповещение.
//  ПодключаемыйМодуль - ComОбъект - внешняя компонента сбербанка;
//  ИдентификаторСертификата  - Строка - уникальный идентификатор сертификата на токене.
//
Процедура ПолучитьДвоичныеДанныеСертификатаСбербанк(Оповещение, ПодключаемыйМодуль, ИдентификаторСертификата) Экспорт
	
	СертификатBase64 = "";
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияДанныхСертификата", Оповещение);
	
	ОповещениеПослеПолученияДанныхСертификата = Новый ОписаниеОповещения("ПослеПолученияДанныхСертификатаСТокенаСбербанк",
		ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПослеПолученияДанныхСертификатаСТокенаСбербанк", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовПолучитьСертификатVPNKeyTLS(
		ОповещениеПослеПолученияДанныхСертификата, ИдентификаторСертификата, СертификатBase64);
		
КонецПроцедуры

// Производит аутентификацию на сервере банк по сертификату
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение после выполнения процедуры
//    * Результат - Булево - если Истина, то аутентификация выполнена успешно, иначе Ложь.
//  ИдентификаторВК - Строка - идентификатор внешней компоненты.
//  ДанныеСертификата - Структура - данные сертификата, содержит поля:
//     * ИдентификаторСертификата - Строка - идентификатор сертификата на токене
//     * ДвоичныеДанныеСертификата - ДвоичныеДанные - двоичные данные сертификата.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - используется для журналирования.
//  ЭтоПолучениеНастроек - Булево - аутентификации производится для получения настроек обмена.
//
Процедура АутентификацияПоСертификатуСбербанк(
	Оповещение,
	ИдентификаторВК,
	ДанныеСертификата,
	НастройкаОбмена,
	ЭтоПолучениеНастроек = Ложь) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификации", Оповещение);
	ДополнительныеПараметры.Вставить("ИдентификаторВК", ИдентификаторВК);
	ДополнительныеПараметры.Вставить("ДанныеСертификата", ДанныеСертификата);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеОпределенияСертификата = Новый ОписаниеОповещения(
		"ПослеПолученияДанныхСертификатаСбербанка", ЭтотОбъект, ДополнительныеПараметры);
	
	ОпределитьСертификатПодписиСбербанк(
		ОповещениеПослеОпределенияСертификата, ИдентификаторВК, НастройкаОбмена, , ЭтоПолучениеНастроек);
	
КонецПроцедуры

// Определяет возможна ли работа с указанной версией внешней компоненты.
//
// Параметры:
//  Версия - Строка - версия загружаемой внешней компоненты.
// 
// Возвращаемое значение:
//  Булево - Истина, если указанная версия поддерживается, иначе Ложь.
//
Функция ПоддерживаетсяВерсияКомпонентыСбербанк(Версия) Экспорт

	МинимальнаяВерсия = "1.2.2.0";
	
	Если Версия < МинимальнаяВерсия Тогда
		ШаблонСообщения = НСтр("ru = 'Работа с внешним модулем не поддерживается. Необходимо загрузить более новую версию.
								|Текущая версия: %1.
								|Требуется версия: %2 или выше.'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Версия, МинимальнаяВерсия);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
	
КонецФункции

// Открывает канал на токене Сбербанка. Аутентификация на токене уже должна быть выполнена.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//               * РезультатВызова - Булево - Если Истина, то канал создан, иначе Ложь;
//               * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ИдентификаторВК - Строка - идентификатор внешней компоненты.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена.
//
Процедура УстановитьКаналНаТокенеСбербанк(Оповещение, ИдентификаторВК, НастройкаОбмена = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановкиВиртуальногоКанала", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"УстановитьКаналПослеПодключенияВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, ИдентификаторВК);
	
КонецПроцедуры

// Отправляет тикеты в банк и получает ответы.
//
// Параметры:
//    Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//     * РезультатВызова - Строка - ответ банка по переданному тикету
//     * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//    ИдентификаторОрганизации - Строка - идентификатор организации на сервере банка;
//    Тикет - Строка - тикет, по которым требуется получение ответа;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена.
//
Процедура ПолучитьСтатусЗапросаСбербанк(Оповещение, ИдентификаторОрганизации, Тикет, НастройкаОбмена = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	ДополнительныеПараметры.Вставить("Тикет", Тикет);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения("ПолучитьСтатусЗапросаПослеПодключенияВК1ССбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьВнешнююКомпоненту1СДляСбербанка(ОповещениеПослеПодключенияВК);
	
КонецПроцедуры

// Отправляет запрос новых документов в Сбербанк
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое по результату выполнения:
//       * Результат - Структура - данные подписи:
//            ** Успех - Булево - признак успешности проведенной операции, если Истина - операция выполнена, иначе Ложь;
//            ** Тикет - Строка - ответный тикет банка, присутствует только если Успех = Истина;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//  СертификатПодписи - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - ссылка на сертификат подписи;
//  ИдентификаторСертификата - Строка - идентификатор сертификата подписи на токене.
//
Процедура ОтправитьЗапросНовыхДокументовСбербанк(
	Оповещение,
	НастройкаОбмена,
	СертификатПодписи,
	ИдентификаторСертификата) Экспорт
	
	РеквизитыНастройкиОбмена = Новый Структура("ИдентификаторОрганизации, ИмяВнешнегоМодуля");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
	СтрокаПодписи = "ATTRIBUTES" + Символ(10) + "OrgId=" + РеквизитыНастройкиОбмена.ИдентификаторОрганизации + Символ(10)
		+ "RequestId=" + ИдентификаторЗапроса;

	СтрокаПодписиBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(СтрокаПодписи);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтрокаПодписиBase64", СтрокаПодписиBase64);
	ДополнительныеПараметры.Вставить("ИдентификаторЗапроса", ИдентификаторЗапроса);
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиЗапроса", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ИдентификаторОрганизации", РеквизитыНастройкиОбмена.ИдентификаторОрганизации);
	ДополнительныеПараметры.Вставить("СертификатПодписи", СертификатПодписи);
	
	РеквизитыСертификата = Новый Структура("ДанныеСертификата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(СертификатПодписи, РеквизитыСертификата);
	
	Представление = НСтр("ru = 'Запрос новых документов'");
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОтправитьЗапросНовыхДокументовПослеПодписиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ПодписатьДанныеСбербанк(ОписаниеОповещения, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля, СтрокаПодписиBase64,
		ИдентификаторСертификата, Представление, РеквизитыСертификата.ДанныеСертификата);

КонецПроцедуры

// Производит процесс аутентификации на токене Сбербанка.
// 
// Параметры:
//    ОповещениеОВыполнении - ОписаниеОповещения - оповещение после выполнения аутентификации на токене:
//          Результат - Булево - Истина, если аутентификация выполнена успешно, иначе Ложь;
//          ДополнительныеПараметры - Структура - параметры описания оповещения;
//    ИдентификаторВК - Строка - идентификатор внешней компоненты;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена с банком;
//    ПринудительнаяАутентификация - Булево - если Истина, то аутентификация будет производится даже если она была выполнена ранее;
//    ЭтоТест - Булево - если Истина, то будет недоступно сохранение пароля.
//
Процедура АутентифицироватьсяНаТокенеСбербанка(
	ОповещениеОВыполнении,
	ИдентификаторВК,
	НастройкаОбмена = Неопределено,
	ПринудительнаяАутентификация = Ложь,
	ЭтоТест = Ложь) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификацииНаТокене", ОповещениеОВыполнении);
	ДополнительныеПараметры.Вставить("ПринудительнаяАутентификация", ПринудительнаяАутентификация);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ЭтоТест", ЭтоТест);
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ТихийСтартПослеПодключенияВКСбербанка", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, ИдентификаторВК);
	
КонецПроцедуры

// Определяет валидность установленных подписей и сохраняет результат в ЭД.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после проверки подписи электронных документов;
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена со Сбербанком;
//  МассивСообщенийОбмена - Массив из ДокументСсылка.СообщениеОбменСБанками - проверяемые сообщения обмена с банком.
//
Процедура ОпределитьСтатусыПодписейСбербанк(Оповещение, НастройкаОбмена, МассивСообщенийОбмена) Экспорт

	Если МассивСообщенийОбмена.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
		Возврат;
	КонецЕсли;
	
	КопияМассиваСообщенийОбмена = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(МассивСообщенийОбмена);
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеПослеПроверкиПодписи", Оповещение);
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("МассивСообщенийОбмена", КопияМассиваСообщенийОбмена);
	
	ОписаниеОбработчика = Новый ОписаниеОповещения(
		"ПослеАутентификацииНаТокенеОпределитьСтатусыПодписейСбербанк", ЭтотОбъект, Параметры);

	РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
		
	АутентифицироватьсяНаТокенеСбербанка(
		ОписаниеОбработчика, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля, НастройкаОбмена);
	
КонецПроцедуры

// Возвращает закешированный параметр обмена со сбербанком.
//
// Параметры:
//  НазваниеПараметра  - Строка - название параметра.
//
// Возвращаемое значение:
// Произвольный - значение параметра.
//
Функция ЗначениеИзКешаСбербанк(НазваниеПараметра) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие") Тогда
		ПараметрыОбменаСбербанк = ПараметрыПодсистемыОбменСБанками.Получить("Сбербанк");
		Если ПараметрыОбменаСбербанк <> Неопределено И ПараметрыОбменаСбербанк.Свойство(НазваниеПараметра) Тогда
			Возврат ПараметрыОбменаСбербанк[НазваниеПараметра];
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Выполняет очистку закэшированных параметров обмена со Сбербанком
//
Процедура ОчиститьДанныеАвторизацииСбербанк() Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие") Тогда
		ПараметрыПодсистемыОбменСБанками.Удалить("Сбербанк");
	КонецЕсли;
	
КонецПроцедуры

// обработчик асинхронного вызова из ОбменСБанкамиКлиент.ПроверитьПодписьСбербанк
Процедура ЗавершитьТестПодписиСертификатаСбербанк(ПодписьВерна, Параметры) Экспорт
	
	Если ПодписьВерна = Неопределено Тогда
		Параметры.Контекст.ОписаниеОшибки = НСтр("ru = 'При проверке подписи произошла ошибка'");
	ИначеЕсли НЕ ПодписьВерна Тогда
		Параметры.Контекст.ОписаниеОшибки = НСтр("ru = 'Установленная подпись неверна'");
	КонецЕсли;

	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаПодписиСертификата, Параметры.Контекст);
	
КонецПроцедуры

// Осуществляет проверку подписи на токене сбербанка.
//
// Параметры:
//  Оповещение- ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//       * ПодписьВерна - Булево, Неопределено - признак валидности подписи, Неопределено в случае возникновения ошибки.
//       * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ПодписанныеДанные - Строка - подписанные данные в формате Base64.
//  Подпись - Строка - двоичные данные подписи в формате Base64.
//  ДвоичныеДанныеСертификата - Строка - сертификат в формате Base64;
//  ИдентификаторВК - Строка - идентификатор внешней компоненты Сбербанка.
//
Процедура ПроверитьПодписьНаТокенеСбербанк(Оповещение, ПодписанныеДанные, Подпись, ДвоичныеДанныеСертификата, ИдентификаторВК) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиПодписи", Оповещение);
	ДополнительныеПараметры.Вставить("ПодписанныеДанные", ПодписанныеДанные);
	ДополнительныеПараметры.Вставить("Подпись", Подпись);
	ДополнительныеПараметры.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ПроверитьПодписьПослеПодключенияВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, ИдентификаторВК);
	
КонецПроцедуры

// Осуществляет подпись данных на токене Сбербанка.
//
// Параметры:
//    ОписаниеОповещения - ОписаниеОповещения - оповещение, вызываемое по результату выполнения:
//       * Результат - Структура - данные подписи:
//            ** Успех - Булево - признак успешности проведенной операции, если Истина - операция выполнена, иначе Ложь;
//            ** ЭП - Строка - данные подписи в Base64;
//            ** ТекстОшибки - Строка - текст ошибки. Элемент присутствует только если возникла ошибка;
//    ИдентификаторВК - Строка - идентификатор внешней компоненты банка;
//    СтрокаПодписи - Строка - подписываемые данные;
//    ИдентификаторСертификата - Строка - идентификатор сертификата на токене банка;
//    Представление - Строка, ДокументСсылка - отображается при подписании на сенсорном или экранном токене.
//    ДанныеСертификата - ДвоичныеДанные - двоичные данные сертификата
//
Процедура ПодписатьДанныеСбербанк(ОписаниеОповещения, ИдентификаторВК, СтрокаПодписи, ИдентификаторСертификата, Представление, ДанныеСертификата) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодписи", ОписаниеОповещения);
	ДополнительныеПараметры.Вставить("СтрокаПодписи", СтрокаПодписи);
	ДополнительныеПараметры.Вставить("ИдентификаторСертификата", ИдентификаторСертификата);
	ДополнительныеПараметры.Вставить("Представление", Представление);
	ДополнительныеПараметры.Вставить("ИдентификаторВК", ИдентификаторВК);
	ДополнительныеПараметры.Вставить("ДанныеСертификата", ДанныеСертификата);
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ПодписатьДанныеПослеПодключенияВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, ИдентификаторВК);
	
КонецПроцедуры

// Осуществляет подбор сертификата подписи на основе установленного соединения.
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры:
//       * Результат - Структура, Неопределено, КодВозвратаДиалога - возвращаемое значение
//           - Структура - данные сертификата:
//                ** ИдентификаторСертификата - Строка - идентификатор сертификата на токене;
//                ** СертификатСсылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - найденный сертификат;
//                ** ДвоичныеДанныеСертификата - ДвоичныеДанные - двоичные данные сертификата.
//           - Неопределено - произошла ошибка;
//           - КодВозвратаДиалога - если КодВозвратаДиалога.Отмена, то пользователь отказался от выбора сертификата;
//       * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения;
//    ИдентификаторВК - Строка - идентификатор внешней компоненты банка;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//    ОграничениеПоСертификатам - Массив - сертификаты, которые доступны для использования в текущей операции.
//    ЭтоПолучениеНастроек - Булево - при получении настроек не нужно проверять наличие сертификата в существующей настройке обмена.
//
Процедура ОпределитьСертификатПодписиСбербанк(Оповещение, ИдентификаторВК, НастройкаОбмена, ОграничениеПоСертификатам = Неопределено, ЭтоПолучениеНастроек = Ложь) Экспорт

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОпределенияСертификата", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ЭтоПолучениеНастроек", ЭтоПолучениеНастроек);
	Если ОграничениеПоСертификатам <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("ОграничениеПоСертификатам", ОграничениеПоСертификатам);
	КонецЕсли;
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ОпределитьСертификатПодписиПослеПодключенияВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, ИдентификаторВК);
	
КонецПроцедуры

// Получает идентификаторы сертификатов с токена Сбербанка.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//   которая будет вызвана после завершения вызова метода со следующими параметрами:
//      * Результат - Структура - результат выполнения метода:
//           ** Успех - Булево - признак успешности выполнения процедуры;
//           ** ТекстОшибки - Строка - содержит текст ошибки, если Успех = Ложь;
//           ** СоответствиеСертификатов - Соответствие - содержит идентификаторы сертификатов с токена:
//                 *** Ключ - Строка - идентификатор сертификата токена;
//                 *** Значение - ДвоичныеДанные - двоичные данные сертификата;
//  ПодключаемыйМодуль - COMОбъект - внешняя компонента банка.
//
Процедура ПолучитьДанныеСертификатовСТокенаСбербанк(Оповещение, ПодключаемыйМодуль) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияСпискаСертификатов", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеПолученияСпискаИдентификаторов = Новый ОписаниеОповещения(
		"ПослеПолученияСпискаСертификатовСТокенаСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияСпискаИдентификаторовСертификатов", ЭтотОбъект);
		
	ИдентификаторыСертификатов = "";

	ПодключаемыйМодуль.НачатьВызовПолучитьСписокИдентСертификатовVPNKeyTLS(
		ОповещениеПослеПолученияСпискаИдентификаторов, "0", ИдентификаторыСертификатов);
	
КонецПроцедуры

Процедура ПослеПолученияСпискаБизнесСистемСТокенаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Произошла ошибка при получении списка бизнес систем.'");
		ТекстОшибки = НСтр("ru = 'Внешняя компонента VpnKey-TLS при получении списка бизнес систем вернула код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		Операция = НСтр("ru = 'Получение списка бизнес систем.'");
		ОбработатьОшибку(Операция, ТекстОшибки);
		ОчиститьДанныеАвторизацииСбербанк();
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	Иначе
		Результат = Новый Структура("Успех, БизнесСистемы", Истина, ПараметрыВызова.Получить(0));
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаБизнесСистем, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСпискаБизнесСистемНаТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Операция = НСтр("ru = 'Получение списка бизнес систем на аппаратном устройстве.'");
	ТекстСообщения = НСтр("ru = 'Не удалось получить список бизнес систем с аппаратного устройства'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ОбработатьОшибку(Операция, ТекстОшибки);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаБизнесСистем, Результат);

КонецПроцедуры

// Обработчик асинхронного метода ОбменСБанкамиКлиент.ПроверитьУстановкуПодписиСбербанк.
//
// Параметры:
//    Успех - Булево - признак аутентификации, если Истина, то аутентификация была успешно выполнена.
//    Параметры - Структура - данные для проверки подписи:
//       * Контекст - Структура - контекст вызова;
//       * ОповещениеПослеПодписания - ОписаниеОповещения - оповещение после проверки подписи;
//       * Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат установленной подписи.
//
Процедура ПроверитьУстановкуПодписиПослеАутентификацииНаТокенеСбербанк(Успех, Параметры) Экспорт
	
	Если Не Успех Тогда
		ОписаниеОшибки = НСтр("ru = 'Не удалось аутентифицироваться на банковском ключе'");
		Параметры.Контекст.ОписаниеОшибки = ОписаниеОшибки;
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Параметры.Контекст);
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения("ПолучитьДанныеСертификатовПослеПодключенияВКСбербанк", ЭтотОбъект, Параметры);
	
	ПодключитьКомпоненту(Оповещение, Параметры.ИмяВнешнегоМодуля);
	
КонецПроцедуры

// Выполняет подтверждение электронных документов с помощью SMS
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения подтверждения
//    * Результат - Число - количество подтвержденных документов
//    * ДополнительныеПараметры - Произвольный - дополнительный параметр оповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком
//  МассивСообщенийОбмена - Массив - сообщения, которые нужно подтвердить
//    * ДокументСсылка.СообщениеОбменСБанками - сообщение обмена с электронным документом.
//
Процедура ВыполнитьПодтверждениеДокументовСбербанк(Оповещение, НастройкаОбмена, МассивСообщенийОбмена) Экспорт
	
	ПодтвердитьСледующийДокументСбербанк(Оповещение, НастройкаОбмена, МассивСообщенийОбмена);
	
КонецПроцедуры

// Добавляет подпись для электронных документов.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Оповещение, вызываемое после подписания:
//      * Результат - Структура - результат выполнения процедуры:
//           * Успех - Булево - если Истина, то все электронные документы были подписаны;
//           * Количество - Число - количество подписанных электронных документов;
//      * ДополнительныеПараметры - Структура - контекст выполнения процедуры;
//  НастройкаОбмена  - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//  МассивСообщенийОбмена  - Массив из ДокументСсылка.СообщениеОбменСБанками - подписываемые электронные документы
//  ОграничениеПоСертификатам  - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификаты, которыми можно подписать ЭД.
//  ПринудительнаяАутентификация - Булево - если Истина, то необходима принудительная аутентификация на токене.
//
Процедура ПодписатьЭДСбербанк(
	Оповещение,
	НастройкаОбмена,
	МассивСообщенийОбмена,
	ОграничениеПоСертификатам = Неопределено,
	ПринудительнаяАутентификация = Ложь) Экспорт
	
	Параметры = Новый Структура;
	
	СообщенияКПодписи = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(МассивСообщенийОбмена);
	
	РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("СообщенияОбменаСбербанк", СообщенияКПодписи);
	Параметры.Вставить("ОповещениеПослеПодписания", Оповещение);
	Параметры.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	Если ОграничениеПоСертификатам <> Неопределено Тогда
		Параметры.Вставить("ОграничениеПоСертификатам", ОграничениеПоСертификатам);
	КонецЕсли;
	
	ОповещениеПослеАутентификацииНаТокене = Новый ОписаниеОповещения(
		"ОпределитьСертификатПослеАутентификацииНаТокенеСбербанк", ЭтотОбъект, Параметры);
	
	АутентифицироватьсяНаТокенеСбербанка(
		ОповещениеПослеАутентификацииНаТокене, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля, НастройкаОбмена, ПринудительнаяАутентификация);
	
КонецПроцедуры

// Выводит сообщение с ошибкой, которая произошла во внешней компоненте 1С для Сбербанка
//
// Параметры:
//  КодОшибки - Строка - код ошибки, который вернула процедура.
//  ВидОперации - Строка - выполняемая операция, которая привела к ошибке.
//  ШаблонСообщения - Строка - начальный текст сообщения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - объект привязки сообщения.
//
Процедура ВывестиИнформацияОбОшибкеСбербанк(КодОшибки, ВидОперации, ШаблонСообщения, НастройкаОбмена = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КодОшибки", КодОшибки);
	ДополнительныеПараметры.Вставить("ВидОперации", ВидОперации);
	ДополнительныеПараметры.Вставить("ШаблонСообщения", ШаблонСообщения);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);

	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьТекстОшибкиПослеПодключенияВК1ССбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьВнешнююКомпоненту1СДляСбербанка(Оповещение);
	
КонецПроцедуры

// Осуществляет подключение внешней компоненты для обмена со Сбербанком.
//
// Параметры:
//    ОписаниеОповещения - ОписаниеОповещения - вызывается после выполнения процедуры
//       * Результат - AddIn, Неопределено - подключенная внешняя компонента.
//
Процедура ПодключитьВнешнююКомпоненту1СДляСбербанка(ОписаниеОповещения) Экспорт
	
	ДополнительныеПараметры = Новый Структура("ОповещениеПослеПодключенияВК1СДляСбербанка", ОписаниеОповещения);
	Оповещение = Новый ОписаниеОповещения("ПослеПодключенияВК1СДляСбербанка", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыПодключения = ОбщегоНазначенияКлиент.ПараметрыПодключенияКомпоненты();
	ПараметрыПодключения.ТекстПояснения = НСтр("ru = 'Для продолжения обмена с банком ""ПАО Сбербанк""
												|необходимо установить внешний модуль 1CSBRFServiceProxy (Версия 11.2).'");
	АдресВнешнейКомпоненты = "Обработка.ОбменСБанками.Макет.SBRFServiceProxy";
	ОбщегоНазначенияКлиент.ПодключитьКомпонентуИзМакета(
		Оповещение, "SBRFServiceProxy", АдресВнешнейКомпоненты, ПараметрыПодключения);
	
КонецПроцедуры

// Устанавливает соединение с сервером банка и производит процедуру аутентификации.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//          * РезультатВызова - Булево - процедура выполнена успешно(истина) или нет (Ложь).
//          * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//  ЭтоТест - Булево - если Истина, то будет недоступно сохранение пароля.
//
Процедура УстановитьСоединениеИАутентифицироватьсяНаСервереЧерезТокенСбербанк(Оповещение, НастройкаОбмена, ЭтоТест = Ложь) Экспорт
	
	РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификацииНаСервере", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ИмяМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	ОповещениеПослеАутентификацииНаАппаратномУстройстве = Новый ОписаниеОповещения(
		"УстановитьВиртуальныйКаналПослеАутентификацииНаАппаратномУстройствеСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	АутентифицироватьсяНаТокенеСбербанка(
		ОповещениеПослеАутентификацииНаАппаратномУстройстве, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля, НастройкаОбмена, , ЭтоТест);
	
КонецПроцедуры

// Производит процесс аутентификации на сервере банка по логину и паролю.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения метода
//   * Результат - Структура - результат аутентификации, содержит поля:
//       * Успех - Булево - результат аутентификации
//       * ТребуетсяТокен - Булево - возвращает Истина, если Успех = Ложь и требуется токен.
//   * ДополнительныеПараметры - Структура - контекст выполнения метода
//  ИмяВнешнегоМодуля - Строка - название внешней компоненты банка
//  КлючСессии - Произвольный - ключ сохранения сессии на сервере
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками, Неопределено - текущая настройка обмена с банком
//  ЛогинПароль - Структура - логин и пароль для аутентификации, содержит поля:
//    * Логин - Строка - имя пользователя
//    * Пароль - Строка - пароль пользователя.
//  ТекстПояснения - Строка - текст пояснения перед установкой внешней компоненты.
//  ВыводитьОкноОжидания - Булево - признак вывода окно при длительной операции.
//
Процедура ВыполнитьАутентификациюПоЛогинуСбербанк(Оповещение, ИмяВнешнегоМодуля, КлючСессии, НастройкаОбмена, ЛогинПароль = Неопределено, ТекстПояснения = "", ВыводитьОкноОжидания = Истина) Экспорт
	
	СтруктураВозврата = Новый Структура("Успех, ТребуетсяТокен", Ложь, Ложь);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеБазовойАутентификации", Оповещение);
	ДополнительныеПараметры.Вставить("ЛогинПароль", ЛогинПароль);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("СтруктураВозврата", СтруктураВозврата);
	ДополнительныеПараметры.Вставить("КлючСессии", КлючСессии);
	ДополнительныеПараметры.Вставить("ИмяВнешнегоМодуля", ИмяВнешнегоМодуля);
	ДополнительныеПараметры.Вставить("ВыводитьОкноОжидания", ВыводитьОкноОжидания);
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ПолучитьФродПараметрыПослеПодключенияВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, ИмяВнешнегоМодуля, ТекстПояснения);
	
КонецПроцедуры

#КонецОбласти

#Область Fintech

Процедура ПолучитьДокументыЧерезFintech(Параметры)

	Параметры.Вставить("ЕстьОшибка", Ложь);
	Параметры.Вставить("СчетаТребующиеАутентификацию", Новый Массив);
	Параметры.Вставить("КоличествоПолученных", 0);

	РезультатВыполнения = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПолученияДокументовFintech(
		Параметры.НастройкаОбмена);
	
	Если РезультатВыполнения.Свойство("ТребуетсяАутентификация") Тогда
		ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
			"ПолучитьДокументыПослеАутентификацииFintech", ЭтотОбъект, Параметры);
		ВыполнитьАутентификациюFintech(ОповещениеПослеАутентификации, Параметры.НастройкаОбмена,
			Параметры.РеквизитыНастройкиОбмена.Организация, Параметры.РеквизитыНастройкиОбмена.Банк, ,
			Параметры.ВыводитьОкноОжидания);
	Иначе
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Параметры.ВыводитьОкноОжидания;
		Оповещение = Новый ОписаниеОповещения("ПослеСинхронизацииFintech", ЭтотОбъект, Параметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, Оповещение, ПараметрыОжидания);
		ВыполнитьОбработкуОповещения(
			Параметры.ОповещениеЗапускаДлительнойОперации, РезультатВыполнения.ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДокументыПослеАутентификацииFintech(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		РезультатВыполнения = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПолученияДокументовFintech(
			ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.СчетаТребующиеАутентификацию);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ДополнительныеПараметры.ВыводитьОкноОжидания;
		Оповещение = Новый ОписаниеОповещения("ПослеСинхронизацииFintech", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, Оповещение, ПараметрыОжидания);
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеЗапускаДлительнойОперации, РезультатВыполнения.ИдентификаторЗадания);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеСинхронизацииFintech(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Синхронизация через сервис Fintech'");
		ОбработатьОшибку(ВидОперации, РезультатЗадания.ПодробноеПредставлениеОшибки,
			РезультатЗадания.КраткоеПредставлениеОшибки, ДополнительныеПараметры.НастройкаОбмена);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		ДополнительныеПараметры.КоличествоПолученных = ДополнительныеПараметры.КоличествоПолученных
														 + РезультатОперации.КоличествоПолученных;
		Если ЗначениеЗаполнено(РезультатОперации.СчетаТребующиеАутентификацию) Тогда
			ВыполнитьСинхронизациюПоСчетамFintech(
				РезультатОперации.СчетаТребующиеАутентификацию, ДополнительныеПараметры);
		Иначе
			ПоказатьРезультатСинхронизации(0, ДополнительныеПараметры.КоличествоПолученных);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьСинхронизациюПоСчетамFintech(МассивСчетов, ДополнительныеПараметры)
	
	Если ЗначениеЗаполнено(МассивСчетов) Тогда
		ДополнительныеПараметры.Вставить("СчетаТребующиеАутентификацию", МассивСчетов);
		НомерСчета = МассивСчетов.Получить(0);
		ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
			"ПолучитьДокументыПослеАутентификацииПоСчетуFintech", ЭтотОбъект, ДополнительныеПараметры);
		ВыполнитьАутентификациюFintech(ОповещениеПослеАутентификации, ДополнительныеПараметры.НастройкаОбмена,
			ДополнительныеПараметры.РеквизитыНастройкиОбмена.Организация,
			ДополнительныеПараметры.РеквизитыНастройкиОбмена.Банк, НомерСчета);
	Иначе
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеСинхронизации, ДополнительныеПараметры.ЕстьОшибка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДокументыПослеАутентификацииПоСчетуFintech(Успех, ДополнительныеПараметры) Экспорт
	
	Если Успех Тогда
		ПолучитьДокументыПослеАутентификацииFintech(Истина, ДополнительныеПараметры)
	Иначе
		ДополнительныеПараметры.ЕстьОшибка = Истина;
		ДополнительныеПараметры.СчетаТребующиеАутентификацию.Удалить(0);
		ВыполнитьСинхронизациюПоСчетамFintech(
			ДополнительныеПараметры.СчетаТребующиеАутентификацию, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Производит процесс аутентификации на сервисе Fintech Integration.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения метода.
//   * Успех - Булево - результат аутентификации. Истина - аутентификация выполнена успешно.
//   * ДополнительныеПараметры - Структура - контекст выполнения метода.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком
//  Организация - ОпределяемыйТип.Организация, Неопределено - ссылка на организацию
//  Банк - ОпределяемыйТип.БанкОбменСБанками, Неопределено - ссылка на банк
//  НомерСчета - Строка - номера счета, к которым необходим доступ.
//  Цель - Строк - возможные значения:
//   * "Подключение" - первичное подключение к сервису
//   * "Обмен" - для обмена документами
//   * "Тест" - проверка настроек обмена.
//  ВыводитьОкноОжидания - Булево - выводить ли окно ожидания длительной операции.
//
Процедура ВыполнитьАутентификациюFintech(
	Оповещение,
	НастройкаОбмена,
	Организация,
	Банк,
	НомерСчета=Неопределено,
	Цель="Обмен",
	ВыводитьОкноОжидания=Ложь) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификации", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("Организация", Организация);
	ДополнительныеПараметры.Вставить("Банк", Банк);
	ДополнительныеПараметры.Вставить("НомерСчета", НомерСчета);
	ДополнительныеПараметры.Вставить("Цель", Цель);
	ДополнительныеПараметры.Вставить("ВыводитьОкноОжидания", ВыводитьОкноОжидания);

	Если ОбменСБанкамиСлужебныйВызовСервера.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
		ПродолжитьПодключениеКFintechПослеПодключенияИППСбербанк(Истина, ДополнительныеПараметры);
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"ПродолжитьПодключениеКFintechПослеПодключенияИППСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры


// Получает выписку через сервис Fintech после аутентификации
// 
// Параметры:
// 	Результат - Булево - если Истина, то аутентификация была успешно выполнена
// 	ДополнительныеПараметры - Структура - контекст выполнения процедуры.
Процедура ПолучитьВыпискуПослеАутентификацииFintechСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		ПараметрыВыполнения = Новый Структура("НастройкаОбмена, НомерСчета, ДатаНачала, ДатаОкончания");
		ЗаполнитьЗначенияСвойств(ПараметрыВыполнения, ДополнительныеПараметры);
		РезультатВыполнения = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПолученияВыпискиFintechСбербанк(
			ПараметрыВыполнения);
			
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыпискиFintech", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, Оповещение, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбменЧерезДополнительнуюОбработку

// Получает внешний интерфейс дополнительной обработки.
//
// Параметры:
//  ДополнительнаяОбработка - СправочникСсылка.ДополнительныеОтчетыИОбработки - дополнительная обработка;
//  АдресКомпоненты - Строка, Неопределено - адрес внешней компоненты, которую требуется установить.
//
// Возвращаемое значение:
//  Форма - форма внешней обработки или Неопределено, если не удалось получить внешний интерфейс.
//
Функция ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(ДополнительнаяОбработка, АдресКомпоненты = Неопределено) Экспорт

	Перем АдресФайлаВнешнегоМодуля;
	
	ПодключаемыйМодуль = ФормаДополнительнойОбработкиИзКэш(ДополнительнаяОбработка);

	Если ПодключаемыйМодуль <> Неопределено Тогда
		Возврат ПодключаемыйМодуль;
	КонецЕсли;
	
	ИмяВнешнегоМодуля = Неопределено;
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		АдресФайлаВнешнегоМодуля = ОбменСБанкамиСлужебныйВызовСервера.АдресДополнительнойОбработки(ДополнительнаяОбработка);
	#Иначе
		ВнешняяОбработкаПодключена = ОбменСБанкамиСлужебныйВызовСервера.ПодключитьДополнительнуюОбработку(
			ДополнительнаяОбработка, ИмяВнешнегоМодуля);
		Если Не ВнешняяОбработкаПодключена Тогда
			Возврат Неопределено;
		КонецЕсли;
	#КонецЕсли
	
	ОбработкаИнициализирована = ИнициализироватьИнтерфейсДополнительнойОбработки(
		ДополнительнаяОбработка, АдресФайлаВнешнегоМодуля, ИмяВнешнегоМодуля, АдресКомпоненты);
	
	Если Не ОбработкаИнициализирована Тогда  // Требуется установка внешней компоненты
		Возврат Неопределено;
	КонецЕсли;
	
	ПодключаемыйМодуль = ФормаДополнительнойОбработкиИзКэш(ДополнительнаяОбработка);

	Возврат ПодключаемыйМодуль;
	
КонецФункции

// Выполняет смс-подтверждение платежей через дополнительную обработку
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры
//  МассивСообщенийОбмена - Массив - сообщения обмена, содержащие неподтвержденные платежи
//      * СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение обмена
//  ВнешнийПодключаемыйМодуль - ВнешняяОбработка - подключаемый модуль банка
//  СертификатXML - Строка - сертификат клиента.
//  ЕстьОшибка - Булево - признак ошибки.
//
Процедура ПодтвердитьПлатежиЧерезДополнительнуюОбработку(Оповещение, МассивСообщенийОбмена, ВнешнийПодключаемыйМодуль, СертификатXML, ЕстьОшибка = Ложь) Экспорт

	Если МассивСообщенийОбмена.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Оповещение, ЕстьОшибка);
		Возврат;
	КонецЕсли;
	
	СообщениеОбмена = МассивСообщенийОбмена.Получить(0);
	МассивСообщенийОбмена.Удалить(0);
	
	ЗначенияРеквизитовСообщенияОбмена = Новый Структура("ВнешнийИдентификатор");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСообщенияОбмена(СообщениеОбмена, ЗначенияРеквизитовСообщенияОбмена);
	
	ВнешниеИдентификаторы = Новый Массив;
	ВнешниеИдентификаторы.Добавить(ЗначенияРеквизитовСообщенияОбмена.ВнешнийИдентификатор);
	ПараметрыЗапроса = Новый Структура("ИдентификаторыДокументов", ВнешниеИдентификаторы);
	
	Попытка
		Результат = ВнешнийПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, 4, ПараметрыЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка инициализации сессии подтверждения.
							|Код ошибки: ДО-%1
							|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Инициализация сессии подтверждения'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, СообщениеОбмена);
		ПодтвердитьПлатежиЧерезДополнительнуюОбработку(
			Оповещение, МассивСообщенийОбмена, ВнешнийПодключаемыйМодуль, СертификатXML, Истина);
		Возврат;
	КонецПопытки;
	
	Сессия = Результат.Сессия;
	
	Попытка
		ВнешнийПодключаемыйМодуль.ОтправитьКодПодтвержденияПоSMS(СертификатXML, Сессия);
	Исключение
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонОшибки = НСтр("ru = 'Ошибка отправки кода подтверждения платежного поручения.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Отправка кода подтверждения платежного поручения'");
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, СообщениеОбмена);
		ПодтвердитьПлатежиЧерезДополнительнуюОбработку(
			Оповещение, МассивСообщенийОбмена, ВнешнийПодключаемыйМодуль, СертификатXML, Истина);
		Возврат;
	КонецПопытки;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИдентификаторСессии", Сессия.Идентификатор);
	ПараметрыФормы.Вставить("СообщениеОбмена", СообщениеОбмена);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Сессия", Сессия);
	ДополнительныеПараметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("СертификатXML", СертификатXML);
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
	ДополнительныеПараметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ЕстьОшибка", ЕстьОшибка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПодтвердитьПлатежЗавершитьЧерезДополнительнуюОбработку", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ПодтверждениеПлатежныхПорученийПоSMS", ПараметрыФормы, ЭтотОбъект, , , ,
		ОписаниеОповещения);
	
КонецПроцедуры

// Получает данные сертификата через дополнительную обработку
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//      * Результат - Структура, Неопределено - возможные варианты: 
//                  - Неопределено, если произошла ошибка или пользователь отказался от ввода пароля;
//                  - Структура - данные сертификата. Содержит следующие поля:
//                     * Отпечаток - Строка - отпечаток сертификата
//                     * СертификатXML - Строка - данные сертификата в Base64
//                     * Пароль - Строка - пароль к закрытой части сертификата.
//      * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль - ФормаКлиентскогоПриложения - внешний модуль обмена с банком;
//  ИдентификаторХранилища - Строка - серийный номер аппаратного устройства.
//
Процедура ПолучитьСертификатЧерезДополнительнуюОбработку(
	Оповещение,
	ПодключаемыйМодуль,
	ИдентификаторХранилища) Экспорт
	
	Попытка
		СертификатыНаУстройстве = ПодключаемыйМодуль.СертификатыВХранилище(ИдентификаторХранилища);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения банковских сертификатов.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Получение банковских сертификатов'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
		ВыполнитьОбработкуОповещения(Оповещение, Неопределено);
		Возврат;
	КонецПопытки;
	
	Если СертификатыНаУстройстве.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'На банковском ключе отсутствуют сертификаты.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(Оповещение, Неопределено);
		Возврат;
	КонецЕсли;

	ДанныеВыбора = Новый Соответствие;
		
	Для Каждого СертификатXML Из СертификатыНаУстройстве Цикл
		ДанныеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(ПодключаемыйМодуль, СертификатXML);
		Если ДанныеСертификата = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтруктураСертификата = Новый Структура;
		СтруктураСертификата.Вставить("СертификатBase64", СертификатXML);
		СтруктураСертификата.Вставить("Псевдоним", ДанныеСертификата.Псевдоним);
		СтруктураСертификата.Вставить("ТребуетсяПароль", Истина);
		ДанныеВыбора.Вставить(ДанныеСертификата.Отпечаток, СтруктураСертификата);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("ДанныеВыбора", ДанныеВыбора);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияСертификата", Оповещение);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеВыбораСертификатаЧерезДополнительнуюОбработку", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ВыборСертификатаДляДобавления", ПараметрыФормы, , , , , Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ПослеВыбораСертификатаЧерезДополнительнуюОбработку(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификата, Неопределено);
		Возврат;
	КонецЕсли;
	
	
	СертификатXML = ДанныеВыбора.СертификатBase64;
	
	ПарольУстановлен = УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
		ДополнительныеПараметры.ПодключаемыйМодуль, СертификатXML, ДанныеВыбора.Пароль);
		
	Если НЕ ПарольУстановлен Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификата, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("СертификатXML", СертификатXML);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеУстановкиСоединенияЧерезДополнительнуюОбработку", ЭтотОбъект, ДополнительныеПараметры);
	УстановитьСоединениеЧерезДополнительнуюОбработку(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, СертификатXML);
	
КонецПроцедуры

// Осуществляет проверку валидности подписей
//
// Параметры
//    ВнешнийПодключаемыйМодуль - внешний подключаемый модуль.
//    Параметры                 - Структура:
//       НастройкаОбмена                        - СправочникСсылка.НастройкиОбменСБанками - настройка обмена, по которой
//                                           выполняется подписание.
//
Процедура НачатьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Параметры) Экспорт
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		ОО = Новый ОписаниеОповещения("ПолучитьДоступноеХранилищеЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
		РеквизитыНастройкиОбмена = Новый Структура("ДополнительнаяОбработка");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			Параметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
		ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(ОО, РеквизитыНастройкиОбмена.ДополнительнаяОбработка);
	Иначе
		ПолучитьДоступноеХранилищеЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Подключает внешнюю обработку.
//
// Параметры:
//    Оповещение - ОписаниеОповещения - обработчик, который вызывается после получения внешнего модуля:
//        Результат - ФормаКлиентскогоПриложения или Неопределено - подключенный модуль и Неопределено в случае ошибки;
//    ДополнительнаяОбработка - СправочникСсылка.ДополнительныеОтчетыИОбработки - дополнительная обработка.
//
Процедура ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(Оповещение, ДополнительнаяОбработка) Экспорт

	АдресВК = Неопределено;
	ВнешнийПодключаемыйМодуль = ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(ДополнительнаяОбработка, АдресВК);
	Если ВнешнийПодключаемыйМодуль = Неопределено И ЗначениеЗаполнено(АдресВК) Тогда
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияВнешнегоМодуля", Оповещение);
		ДополнительныеПараметры.Вставить("ДополнительнаяОбработка", ДополнительнаяОбработка);
		ОповещениеПослеУстановкиКомпоненты = Новый ОписаниеОповещения(
			"ПослеУстановкиВнешнейКомпонентыДополнительнойОбработки", ЭтотОбъект, ДополнительныеПараметры);
		УстановитьВнешнююКомпонентуДляОбменаЧерезОбработку(ОповещениеПослеУстановкиКомпоненты, АдресВК);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, ВнешнийПодключаемыйМодуль);
	КонецЕсли;

КонецПроцедуры

Процедура ПослеУстановкиВнешнейКомпонентыДополнительнойОбработки(Результат, Параметры) Экспорт

	Если Результат Тогда
		ВнешнийПодключаемыйМодуль = ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(Параметры.ДополнительнаяОбработка);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияВнешнегоМодуля, ВнешнийПодключаемыйМодуль);
	Иначе
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияВнешнегоМодуля);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет необходимость установки пин-кода.
//
// Параметры:
//  ВнешнийПодключаемыйМодуль  - ФормаКлиентскогоПриложения - внешняя управляемая форма;
//  ИдентификаторХранилища  - Строка - идентификатор хранилища.
//
// Возвращаемое значение:
//   Булево, Неопределено - Истина - пин-код уже установлен или не требуется, Ложь - требуется установка пин кода,
//                          Неопределено - произошла ошибка при определении необходимости пин-кода.
//
Функция НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
	ВнешнийПодключаемыйМодуль,
	ИдентификаторХранилища) Экспорт
	
	Ошибка = Ложь;
	ТребуетсяПИН = ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, Ошибка);
	
	Если Ошибка Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТребуетсяПИН Тогда
		
		УстановленPINКодХранилища = УстановленPINКодХранилищаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, Ошибка);
	
		Если Ошибка Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Возврат НЕ УстановленPINКодХранилища;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Устанавливает PIN код для доступа к банковскому ключу.
//
// Параметры
//  ВнешнийПодключаемыйМодуль - ФормаКлиентскогоПриложения - внешний программный интерфейс;
//  ИдентификаторХранилища  - Строка - идентификатор хранилища;
//  ПинКод  - Строка - Пин код.
//
// Возвращаемое значение:
//  Булево -  пин код установлен успешно или нет.
//
Функция УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, ПинКод) Экспорт
	
	ПинКодУстановлен = Истина;
	
	Попытка
		ВнешнийПодключаемыйМодуль.УстановитьPINКодХранилища(ИдентификаторХранилища, ПинКод);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка установки PIN-кода.
		                          |Код ошибки: ДО-%1
		                          |%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка PIN-кода'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
		ПинКодУстановлен = Ложь;
	КонецПопытки;
	
	Возврат ПинКодУстановлен;
	
КонецФункции

// Получает внешний модуль для подписания ЭД через доп.обработку и переходит к получению выписки банка.
//
// Параметры:
//    ВнешнийПодключаемыйМодуль - ФормаКлиентскогоПриложения - внешний модуль.
//    Параметры                 - Структура - содержит поля:
//       * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком
//                    по которой выполняется получение выписки.
//       * НомерСчета   - Строка - номер банковского счета организации. Если не указан, то запрос по всем счетам.
//       * ДатаНачала    - Дата - дата начала периода
//       * ДатаОкончания - Дата - дата окончания периода
//
Процедура ПолучитьВыпискуЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Параметры) Экспорт
	
	Если ВнешнийПодключаемыйМодуль <> Неопределено Тогда
		НастройкаОбмена = Параметры.НастройкаОбмена;
		ДанныеСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатовБанка(НастройкаОбмена);
		
		Устройства = ПодключенныеХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль);
		
		Соответствие = Новый Соответствие;
		Если Устройства <> Неопределено И Устройства.Количество() > 0 Тогда
			Для Каждого ДанныеСертификата Из ДанныеСертификатов Цикл
				ПроверитьСрокДействияСертификатаБанка(ДанныеСертификата.Сертификат, ДанныеСертификата.ДействителенДО,
					ДанныеСертификата.ПользовательОповещенОСрокеДействия);
				ДанныеСертификатаЧерезДополнительнуюОбработку = ДанныеСертификатаЧерезДополнительнуюОбработку(
													ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
				Если Устройства.Найти(ДанныеСертификатаЧерезДополнительнуюОбработку.ИдентификаторХранилища) <> Неопределено Тогда
					Соответствие.Вставить(ДанныеСертификата.Сертификат, ДанныеСертификата);
				КонецЕсли;
			КонецЦикла
		Иначе
			ТекстСообщения = НСтр("ru = 'Для выполнения операции необходимо подключить банковский ключ к компьютеру'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат
		КонецЕсли;
		
		Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
		Параметры.Вставить("ДанныеСертификатов", ДанныеСертификатов);
		Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		
		ВидОперации = НСтр("ru = 'Аутентификация на ресурсе банка'");
		Если Соответствие.Количество() > 0 Тогда
			Если ЕстьСертификатССохраненнымПаролем(Соответствие, ВнешнийПодключаемыйМодуль) Тогда
				ВыбранныйСертификат = Неопределено;
				Если НЕ (ДанныеСертификата.Свойство("ВыбранныйСертификат")
					И ТипЗнч(ВыбранныйСертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования")) Тогда
				
					ДанныеСертификата.Вставить("ВыбранныйСертификат", ДанныеСертификата.Сертификат);
				КонецЕсли;
				ПродолжитьПолучениеВыпискиПослеВводаПароляСертификатаЧерезДополнительнуюОбработку(
					ДанныеСертификата, Параметры)
			Иначе
				Оповещение = Новый ОписаниеОповещения(
					"ПродолжитьПолучениеВыпискиПослеВводаПароляСертификатаЧерезДополнительнуюОбработку", ЭтотОбъект,
					Параметры);
				ПолучитьПарольКСертификату(Оповещение, Соответствие, ВидОперации);
				Возврат;
			КонецЕсли;
		Иначе
			ТекстСообщения = НСтр("ru = 'К компьютеру подключен не подходящий банковский ключ'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Получает внешний модуль для подписания ЭД через доп.обработку и переходит к получению выписки банка.
//
// Параметры:
//    Результат - Структура, Неопределено - если структура,
//              то процедура была вызвана после получения пароля к сертификату:
//       ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования.
//       Комментарий         - Строка.
//       ПарольСертификата  - Строка.

//    Параметры - Структура:
//       СоотвСертификатовИИхСтруктур - Соответствие.
//       ДанныеСертификатов           - Структура.
//       ВнешнийПодключаемыйМодуль    - Внешний подключаемый модуль.
//       НастройкаОбмена                 - СправочникСсылка.НастройкиОбменСБанками - настройка обмена
//                                    по которой выполняется получение выписки.
//       НомерСчета                   - Строка - номер банковского счета организации. Если не указан, то запрос по всем счетам.
//       Владелец                     - Форма или элемент формы - получатель оповещения о выборе элемента - выписки банка.
//       ДатаНачала                   - Дата.
//       ДатаОкончания                - Дата.
//
Процедура ПродолжитьПолучениеВыпискиПослеВводаПароляСертификатаЧерезДополнительнуюОбработку(Результат, Параметры) Экспорт
	
	ВыбранныйСертификат = Неопределено;
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("ВыбранныйСертификат", ВыбранныйСертификат)
		И ТипЗнч(ВыбранныйСертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		
		СоотвСертификатовИИхСтруктур = Параметры.СоотвСертификатовИИхСтруктур;
		НастройкаОбмена = Параметры.НастройкаОбмена;
		
		Если СоотвСертификатовИИхСтруктур.Количество() > 0 Тогда
			ДанныеСертификатов = Параметры.ДанныеСертификатов;
			ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
			
			Для Каждого ДанныеСертификата Из ДанныеСертификатов Цикл
				Если ДанныеСертификата.Сертификат = ВыбранныйСертификат Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			ДанныеСертификатаЧерезДополнительнуюОбработку = ДанныеСертификатаЧерезДополнительнуюОбработку(
											ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
			Если ДанныеСертификатаЧерезДополнительнуюОбработку <> Неопределено Тогда
				Параметры.Вставить("ИмяПроцедуры", "ПродолжитьПолучениеВыпискиПослеУстановкиСоединенияЧерезДопОбработку");
				Параметры.Вставить("Модуль", ЭтотОбъект);
				Параметры.Вставить("ДанныеСертификата", ДанныеСертификата);
				Параметры.Вставить("ПарольСертификата", Результат.ПарольСертификата);
				Параметры.Вставить("ИдентификаторХранилища", ДанныеСертификатаЧерезДополнительнуюОбработку.ИдентификаторХранилища);
				Параметры.Вставить("ВыбранныйСертификат", ВыбранныйСертификат);
				
				ТребуетсяУстановкаPINКода = НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
					ВнешнийПодключаемыйМодуль, ДанныеСертификатаЧерезДополнительнуюОбработку.ИдентификаторХранилища);
					
				Если ТребуетсяУстановкаPINКода = Ложь Тогда
					ПродолжитьПолучениеВыпискиЧерезДополнительнуюОбработку(Параметры);
				ИначеЕсли ТребуетсяУстановкаPINКода = Истина Тогда
					ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
						"ПродолжитьПолучениеВыпискиПослеВводаPINКодаЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
					НачатьУстановкуPINКодаХранилища(НастройкаОбмена,
						ДанныеСертификатаЧерезДополнительнуюОбработку.ИдентификаторХранилища, ОписаниеОповещенияОЗакрытии);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьЧерезДополнительнуюОбработку(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Число") Тогда
		Параметры.КоличествоОтправленных = Параметры.КоличествоОтправленных + Результат;
	КонецЕсли;
	
	ДанныеДляОтправки = Неопределено;
	Если НЕ Параметры.Свойство("ДанныеДляОтправки", ДанныеДляОтправки) ИЛИ ДанныеДляОтправки.Количество() = 0 Тогда
		
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеОтправкиПЭД, Параметры);
	Иначе
		Для Каждого КлючИЗначение Из ДанныеДляОтправки Цикл
			// СтруктураОтправки будет передана в процедуру
			// НачатьОтправкуПакетовЧерезДополнительнуюОбработку, после получения внешнего модуля.
			СтруктураОтправки = Новый Структура;
			СтруктураОтправки.Вставить("НастройкаОбмена", КлючИЗначение.Ключ);
			СтруктураОтправки.Вставить("ДанныеДляОтправки", КлючИЗначение.Значение);
			СтруктураОтправки.Вставить("КоличествоОтправленных", 0);
			СтруктураОтправки.Вставить("КоличествоПодготовленных", 0);
			
			Параметры.ДанныеДляОтправки.Удалить(КлючИЗначение.Ключ);
			// ОбработчикПродолжения - рекурсивный вызов текущей процедуры, для продолжения
			// отправки следующего элемента ДанныеДляОтправки.
			ОбработчикПродолжения = Новый ОписаниеОповещения("ОтправитьЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
			СтруктураОтправки.Вставить("ОповещениеПослеОтправкиДокументов", ОбработчикПродолжения);
			РеквизитыНастройкиОбмена = Новый Структура("ДополнительнаяОбработка");
			ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
				КлючИЗначение.Ключ, РеквизитыНастройкиОбмена);
			ОбработчикПослеПодключения = Новый ОписаниеОповещения(
				"НачатьОтправкуПакетовЧерезДополнительнуюОбработку", ЭтотОбъект, СтруктураОтправки);
			ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(
				ОбработчикПослеПодключения, РеквизитыНастройкиОбмена.ДополнительнаяОбработка);
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьОтправкуПакетовЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Параметры) Экспорт
	
	Если ВнешнийПодключаемыйМодуль <> Неопределено Тогда
		ДанныеОтправки = Параметры.ДанныеДляОтправки;
		
		Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		Устройства = ПодключенныеХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Ложь);
		
		СписокВыбораСертификата = Новый Массив;
		
		Если Устройства <> Неопределено И Устройства.Количество() > 0 Тогда
			
			Если ДанныеОтправки.Свойство("Сертификаты") Тогда
				ДанныеСертификатов = ДанныеОтправки.Сертификаты;
			Иначе
				ДанныеСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатовПоНастройкеОбмена(
					Параметры.НастройкаОбмена);
				ДанныеОтправки.Вставить("Сертификаты", ДанныеСертификатов);
			КонецЕсли;
			
			Для Каждого ДанныеСертификата Из ДанныеСертификатов Цикл
				ПарольУстановлен = УстановленПарольСертификатаЧерезДополнительнуюОбработку(
								ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
				Если ПарольУстановлен Тогда
					Соответствие = Новый Соответствие;
					СтруктураСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(
						ДанныеСертификата.СертификатСсылка);
					Соответствие.Вставить(ДанныеСертификата.СертификатСсылка, СтруктураСертификата);
					Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
					Прервать;
				КонецЕсли;
			
				СодержимоеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(
						ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
				Если Устройства.Найти(СодержимоеСертификата.ИдентификаторХранилища) <> Неопределено Тогда
					СписокВыбораСертификата.Добавить(ДанныеСертификата.СертификатСсылка);
				КонецЕсли;
			КонецЦикла;
			
			Параметры.Вставить("ДанныеСертификата", ДанныеСертификата);
			
			Если НЕ ПарольУстановлен Тогда
				Если СписокВыбораСертификата.Количество() = 0 Тогда
					Для Каждого ДанныеСертификата Из ДанныеСертификатов Цикл
						СписокВыбораСертификата.Добавить(ДанныеСертификата.СертификатСсылка);
					КонецЦикла;
				КонецЕсли;
				Соответствие = Новый Соответствие;
				Для Каждого Сертификат Из СписокВыбораСертификата Цикл
					СтруктураСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(Сертификат);
					Соответствие.Вставить(Сертификат, СтруктураСертификата);
				КонецЦикла;
				
				Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
				
				Если ЕстьСертификатССохраненнымПаролем(Соответствие, ВнешнийПодключаемыйМодуль) Тогда
					Для Каждого Элемент Из Соответствие Цикл
						ДанныеСертификата = Элемент.Значение;
						Прервать;
					КонецЦикла;
					ПарольУстановлен = Истина;
				Иначе
					ОООЗ = Новый ОписаниеОповещения(
						"ПродолжитьОтправкуПакетовПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
					ПолучитьПарольКСертификату(ОООЗ, Соответствие, НСтр("ru = 'Аутентификация на ресурсе банка'"));
					Возврат
				КонецЕсли;
			КонецЕсли;
			Если ПарольУстановлен Тогда
				Если Не ДанныеСертификата.Свойство("ВыбранныйСертификат")
					Или Не ЗначениеЗаполнено(ДанныеСертификата.ВыбранныйСертификат) Тогда
					
					ДанныеСертификата.Вставить("ВыбранныйСертификат", ДанныеСертификата.СертификатСсылка);
				КонецЕсли;
				ПродолжитьОтправкуПакетовПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(ДанныеСертификата, Параметры);
				Возврат
			КонецЕсли;
		Иначе
			ТекстСообщения = НСтр("ru = 'К компьютеру не подключен банковский ключ для отправки данных по настройке обмена: %1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Параметры.НастройкаОбмена);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	
	Результат = Новый Структура("Успех", Ложь);
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеОтправкиДокументов, Результат);
	
КонецПроцедуры

Процедура ПродолжитьОтправкуПакетовПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(Результат, Параметры) Экспорт
	
	ВыбранныйСертификат = Неопределено;
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("ВыбранныйСертификат", ВыбранныйСертификат)
		И ТипЗнч(ВыбранныйСертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		
		ПараметрыСертификата = Параметры.СоотвСертификатовИИхСтруктур[ВыбранныйСертификат];
		ПараметрыСертификата.Вставить("ПарольПолучен", Истина);
		ПараметрыСертификата.Вставить("ПарольСертификата", Результат.ПарольСертификата);
		ПараметрыСертификата.Вставить("ВыбранныйСертификат", ВыбранныйСертификат);
		ПараметрыСертификата.Вставить("Комментарий", "");
		Результат.Свойство("Комментарий", ПараметрыСертификата.Комментарий);
		Параметры.Вставить("СтруктураСертификата", ПараметрыСертификата);
		
		ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
		Сертификаты = Параметры.ДанныеДляОтправки.Сертификаты;
		ДанныеСертификата = Неопределено;
		Для Каждого СтруктураСертификата Из Сертификаты Цикл
			Если СтруктураСертификата.СертификатСсылка = ВыбранныйСертификат Тогда
				ДанныеСертификата = СтруктураСертификата;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ДанныеСертификата <> Неопределено Тогда
			Параметры.Вставить("ДанныеСертификата", ДанныеСертификата);
			ДанныеСертификатаЧерезДополнительнуюОбработку = ДанныеСертификатаЧерезДополнительнуюОбработку(
				ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
			ИдентификаторХранилища = ДанныеСертификатаЧерезДополнительнуюОбработку.ИдентификаторХранилища;
			
			Если ДанныеСертификатаЧерезДополнительнуюОбработку <> Неопределено Тогда
				Если Параметры.ДанныеДляОтправки.Свойство("ДанныеПакетов") Тогда
					Параметры.Вставить("ОтправляемыеПакетыЧерезДопОбработку", Параметры.ДанныеДляОтправки.ДанныеПакетов);
				КонецЕсли;
				Параметры.Вставить("ИмяПроцедуры", "ОтправитьПакетыЧерезДополнительнуюОбработку");
				Параметры.Вставить("Модуль", ЭтотОбъект);
				Параметры.Вставить("ПарольУстановлен", Истина);
				Параметры.Вставить("ПарольСертификата", Результат.ПарольСертификата);
				Параметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);

				НеобходимаУстановкаPINКодаХранилища = НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
																	ВнешнийПодключаемыйМодуль, ИдентификаторХранилища);
				Если НеобходимаУстановкаPINКодаХранилища = Истина Тогда
					ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
						"ПродолжитьОтправкуПакетаПослеВводаPINКодаЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
					НачатьУстановкуPINКодаХранилища(Параметры.НастройкаОбмена, ИдентификаторХранилища, ОписаниеОповещенияОЗакрытии);
					Возврат;
				ИначеЕсли НеобходимаУстановкаPINКодаХранилища = Ложь Тогда
					ПродолжитьОтправкуПакетаЧерезДополнительнуюОбработку(Параметры);
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура("Успех", Ложь);
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеОтправкиДокументов, Результат);
	
КонецПроцедуры

Процедура ПродолжитьОтправкуПакетовЧерезДополнительнуюОбработкуПослеПодключения(ВнешнийПодключаемыйМодуль, Параметры) Экспорт

	Если ВнешнийПодключаемыйМодуль <> Неопределено Тогда
		ДанныеОтправки = Параметры.ДанныеДляОтправки;
		
		Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		Устройства = ПодключенныеХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль);
		
		СписокВыбораСертификата = Новый Массив;
		
		Если Устройства <> Неопределено И Устройства.Количество() > 0 Тогда
			
			Если ДанныеОтправки.Свойство("Сертификаты") Тогда
				ДанныеСертификатов = ДанныеОтправки.Сертификаты;
			Иначе
				ДанныеСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатовПоНастройкеОбмена(
					Параметры.НастройкаОбмена);
				ДанныеОтправки.Вставить("Сертификаты", ДанныеСертификатов);
			КонецЕсли;
			
			Для Каждого ДанныеСертификата Из ДанныеСертификатов Цикл
				ПарольУстановлен = УстановленПарольСертификатаЧерезДополнительнуюОбработку(
								ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
				Если ПарольУстановлен Тогда
					Соответствие = Новый Соответствие;
					СтруктураСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(
						ДанныеСертификата.СертификатСсылка);
					Соответствие.Вставить(ДанныеСертификата.СертификатСсылка, СтруктураСертификата);
					Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
					Прервать;
				КонецЕсли;
			
				СодержимоеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(
						ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
				Если Устройства.Найти(СодержимоеСертификата.ИдентификаторХранилища) <> Неопределено Тогда
					СписокВыбораСертификата.Добавить(ДанныеСертификата.СертификатСсылка);
				КонецЕсли;
			КонецЦикла;
			
			Параметры.Вставить("ДанныеСертификата", ДанныеСертификата);
			
			Если НЕ ПарольУстановлен Тогда
				Если СписокВыбораСертификата.Количество() = 0 Тогда
					Для Каждого ДанныеСертификата Из ДанныеСертификатов Цикл
						СписокВыбораСертификата.Вставить(ДанныеСертификата.СертификатСсылка);
					КонецЦикла;
				КонецЕсли;
				Соответствие = Новый Соответствие;
				Для Каждого Сертификат Из СписокВыбораСертификата Цикл
					СтруктураСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(Сертификат);
					Соответствие.Вставить(Сертификат, СтруктураСертификата);
				КонецЦикла;
				
				Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
				
				Если ЕстьСертификатССохраненнымПаролем(Соответствие, ВнешнийПодключаемыйМодуль) Тогда
					Для Каждого Элемент Из Соответствие Цикл
						ДанныеСертификата = Элемент.Значение;
						Прервать;
					КонецЦикла;
					ПарольУстановлен = Истина;
				Иначе
					ОООЗ = Новый ОписаниеОповещения(
						"ПродолжитьОтправкуПакетовПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
					ПолучитьПарольКСертификату(ОООЗ, Соответствие, НСтр("ru = 'Аутентификация на ресурсе банка'"));
					Возврат;
				КонецЕсли;
			КонецЕсли;
			Если ПарольУстановлен Тогда
				Если Не ДанныеСертификата.Свойство("ВыбранныйСертификат")
					Или Не ЗначениеЗаполнено(ДанныеСертификата.ВыбранныйСертификат) Тогда
					
					ДанныеСертификата.Вставить("ВыбранныйСертификат", ДанныеСертификата.СертификатСсылка);
				КонецЕсли;
				ПродолжитьОтправкуПакетовПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(ДанныеСертификата, Параметры);
				Возврат;
			КонецЕсли;
		Иначе
			ТекстСообщения = НСтр("ru = 'К компьютеру не подключен банковский ключ для отправки данных по настройке обмена: %1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Параметры.НастройкаОбмена);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура("Успех", Ложь);
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеОтправкиДокументов, Результат);
		
КонецПроцедуры

// Дозаполняет недостающие данные, необходимые для подписания ЭД и переходит к подписанию ЭД.
//
// Параметры:
//    ВнешнийПодключаемыйМодуль - ФормаКлиентскогоПриложения - внешний модуль.
//    Параметры - Структура - содержит поля:
//       * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком.
//       * МассивСообщенийОбменаКПодписи - Массив - подписываемые сообщения обмена.
//       * СтруктураСертификата  - Структура - параметры сертификата, для которого "выше"
//                             выполнялся запрос пароля.
//       * ОбработчикПродолжения - ОписаниеОповещения - описание, которое надо выполнить после завершения
//                                                      отработки текущих ЭД (МассивСообщенийОбменаКПодписи).
//
Процедура ПроверитьНеобходимостьУстановкиПинКода(ВнешнийПодключаемыйМодуль, Параметры) Экспорт
	
	ПрерватьПодписание = Истина;
	Если ВнешнийПодключаемыйМодуль <> Неопределено Тогда
		Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		СертификатСсылка = Параметры.СтруктураСертификата.СертификатПодписи;
		Параметры.Вставить("ВыбранныйСертификат", СертификатСсылка);
		РеквизитыСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(СертификатСсылка);
		
		СертификатXML = РеквизитыСертификата.ДвоичныеДанныеСертификата;
		Параметры.Вставить("СертификатXMLЧерезДопОбработку", СертификатXML);
		
		ДанныеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, СертификатXML);
		
		Если ДанныеСертификата <> Неопределено Тогда
			Параметры.Вставить("ИдентификаторХранилища", ДанныеСертификата.ИдентификаторХранилища);
			
			ТребуетсяУстановкаPINКода = НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
				ВнешнийПодключаемыйМодуль, ДанныеСертификата.ИдентификаторХранилища);
			
			Если ТребуетсяУстановкаPINКода = Истина Тогда
				ОО = Новый ОписаниеОповещения(
					"ПродолжитьПодписаниеПослеВводаPINКодаЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
				НачатьУстановкуPINКодаХранилища(Параметры.НастройкаОбмена, ДанныеСертификата.ИдентификаторХранилища, ОО);
				ПрерватьПодписание = Ложь;
			ИначеЕсли ТребуетсяУстановкаPINКода = Ложь Тогда
				ПродолжитьПодписаниеЧерезДополнительнуюОбработку(Параметры);
				ПрерватьПодписание = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеПодписатьЭД = Неопределено;
	Если ПрерватьПодписание И Параметры.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД)
		И ТипЗнч(ОписаниеПодписатьЭД) = Тип("ОписаниеОповещения") Тогда
		
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

// Предлагает пользователю выбрать хранилище и возвращает результат выбора.
//
// Параметры:
//  НастройкаОбмена  - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена с банком;
//  ОО - ОписаниеОповещения - описание вызова процедуры программного модуля, который будет осуществлен после выбора хранилища;
//  Параметры  - структура - параметры обработки ЭД.
//
Процедура ВыбратьХранилищеЧерезДополнительнуюОбработку(НастройкаОбмена, ОО, Параметры) Экспорт
	
	ПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	Хранилища = ПодключенныеХранилищаЧерезДополнительнуюОбработку(ПодключаемыйМодуль);
	
	Если Хранилища = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОО);
	ИначеЕсли Хранилища.Количество() = 1
		И НЕ ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(ПодключаемыйМодуль, Хранилища[0]) Тогда
		ВыполнитьОбработкуОповещения(ОО, Хранилища[0]);
	ИначеЕсли Хранилища.Количество() > 0 Тогда
		СтруктураПараметров = Новый Структура("Хранилища, НастройкаОбмена", Хранилища, НастройкаОбмена);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ВыборХранилища", СтруктураПараметров, , , , , ОО);
	Иначе
		ТекстСообщения = НСтр("ru = 'Для выполнения операции необходимо подключить банковский ключ к компьютеру'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(ОО);
	КонецЕсли
	
КонецПроцедуры

// Вызывается из НачатьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку и,
// по обработке оповещения, из ВыбратьХранилищеЧерезДополнительнуюОбработку.
// Осуществляет проверку валидности подписей.
//
// Параметры
//    ДоступноеХранилище - хранилище сертификатов.
//    Параметры                 - Структура:
//       ВнешнийПодключаемыйМодуль           - внешний подключаемый модуль.
//       МассивСообщенийОбменаДляПроверкиЧерезДополнительнуюОбработку - Массив - ЭД в которых надо проверить статусы подписей.
//       ОбработчикПродолжения     - ОписаниеОповещения - (необязательный) описание, которое надо выполнить
//                                           после завершения отработки текущих ЭД (МассивСообщенийОбменаКПодписи).
//       ОповеститьОПроверкеЭП               - Строка - (необязательный) признак необходимости выполнить "Оповестить()".
//
Процедура ВыполнитьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку(ДоступноеХранилище, Параметры) Экспорт
	
	Если ДоступноеХранилище <> Неопределено Тогда
		ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
		МассивСообщенийОбмена = Параметры.МассивСообщенийОбменаДляПроверкиЧерезДополнительнуюОбработку;
		Для Каждого СообщениеОбмена Из МассивСообщенийОбмена Цикл
			СтруктураСодержимогоСообщенияОбмена = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСодержимогоСообщенияОбмена(
				СообщениеОбмена);
				
			Если СтруктураСодержимогоСообщенияОбмена = Неопределено Тогда
				Продолжить;
			КонецЕсли;
				
			РезультатПроверки = Новый Массив;
			Для Каждого СтрокаЭП Из СтруктураСодержимогоСообщенияОбмена.Подписи Цикл
				СтруктураЗаписи = Новый Структура();
				Попытка
					ДвоичныеДанныеЭП = СтрокаЭП.Подпись;
					СертификатXML = СтрокаЭП.Сертификат;
					ДопПараметры = Новый Структура("ИдентификаторХранилища", ДоступноеХранилище);
					ПодписьВалидна = ВнешнийПодключаемыйМодуль.ПроверитьПодпись(
						СертификатXML, СтруктураСодержимогоСообщенияОбмена.ДанныеЭД, ДвоичныеДанныеЭП, ДопПараметры);
					СтруктураЗаписи.Вставить("ПодписьВерна", ПодписьВалидна);
					СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
					РезультатПроверки.Добавить(СтруктураЗаписи);
				Исключение
					ШаблонОшибки = НСтр("ru = 'Ошибка проверки подписи.
											|Код ошибки: ДО-%1
											|%2'");
					ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
					ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
					Операция = НСтр("ru = 'Проверка подписи'");
					ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, СообщениеОбмена);
				КонецПопытки;
			КонецЦикла;
			Если РезультатПроверки.Количество() Тогда
				ОбменСБанкамиСлужебныйВызовСервера.СохранитьРезультатыПроверкиПодписей(СообщениеОбмена, РезультатПроверки);
			КонецЕсли;
		КонецЦикла;
		
		Если Параметры.Свойство("ОповеститьОПроверкеЭП") Тогда
			Оповестить("ПроведенаПроверкаЭП", МассивСообщенийОбмена);
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеОповещения = Неопределено;
	Если Параметры.Свойство("ОбработчикПродолжения", ОписаниеОповещения)
		И ТипЗнч(ОписаниеОповещения) = Тип("ОписаниеОповещения") Тогда
		
		Результат = Неопределено;
		Параметры.Свойство("РезультатПодписания", Результат);
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Результат);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает пароль для установки соединения с банком.
//
// Параметры:
//  ВнешнийПодключаемыйМодуль  - ФормаКлиентскогоПриложения - внешняя управляемая форма;
//  СертификатXML  - Строка - Содержит данные сертификата;
//  Пароль  - Строка - пароль сертификата.
//  ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - текущий сертификат подписи.
//
// Возвращаемое значение:
//   Булево   - пароль верный или нет.
//
Функция УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
	ВнешнийПодключаемыйМодуль,
	СертификатXML,
	Пароль,
	ВыбранныйСертификат = Неопределено) Экспорт
	
	Попытка
		ВнешнийПодключаемыйМодуль.УстановитьПарольСертификата(СертификатXML, Пароль);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка установки пароля сертификата.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка пароля сертификата'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Если ЗначениеЗаполнено(ВыбранныйСертификат) Тогда
			УдалитьПарольИзСеанса(ВыбранныйСертификат);
		КонецЕсли;
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Проверяет наличие уже установленного PIN код для хранилища.
//
// Параметры:
//  ВнешнийПодключаемыйМодуль  - ФормаКлиентскогоПриложения - внешняя управляемая форма;
//  ИдентификаторХранилища  - Строка - идентификатор хранилища.
//  ЕстьОшибка - Булево - если Истина, то при проверке произошла ошибка.
//
// Возвращаемое значение:
//   Булево, Неопределено - Истина - пин-код установлен установлен ранее, Ложь - пин код не установлен,
//                          Неопределено - произошла ошибка.
//
Функция УстановленPINКодХранилищаЧерезДополнительнуюОбработку(
	ВнешнийПодключаемыйМодуль,
	ИдентификаторХранилища,
	ЕстьОшибка = Ложь) Экспорт
	
	Попытка
		УстановленPINКодХранилища = ВнешнийПодключаемыйМодуль.УстановленPINКодХранилища(ИдентификаторХранилища);
	Исключение
		ЕстьОшибка = Истина;
		ШаблонОшибки = НСтр("ru = 'Ошибка проверки установленного PIN-кода.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Проверка наличия установленного PIN-кода'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат УстановленPINКодХранилища;
	
КонецФункции

// Открывает форму ввода пин-кода.
//
// Параметры:
//  НастройкаОбмена  - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена с банком;
//  ИдентификаторХранилища  - Строка - идентификатор хранилища;
//  ОписаниеОповещенияОЗакрытии - ОписаниеОповещения - возврат после ввода PIN-кода.
//
Процедура НачатьУстановкуPINКодаХранилища(НастройкаОбмена, ИдентификаторХранилища, ОписаниеОповещенияОЗакрытии) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыФормы.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросPINКода", ПараметрыФормы, , , , , ОписаниеОповещенияОЗакрытии);
	
КонецПроцедуры

// Отправляет запрос в банк.
//
// Параметры
//  ВнешнийПодключаемыйМодуль  - Управляемая Форма - внешняя управляемая форма;
//  СертификатXML  - строка - данные сертификата;
//  ТипЗапроса  - Число - тип запроса;
//  ДанныеОтправки  - Структура - данные для отправки.
//
// Возвращаемое значение:
//  Соответствие, Неопределено -  результат выполнения.
//
Функция ОтправитьЗапросЧерезДополнительнуюОбработку(
	ВнешнийПодключаемыйМодуль,
	СертификатXML,
	ТипЗапроса,
	ДанныеОтправки) Экспорт

	Попытка
		Результат = ВнешнийПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, ТипЗапроса, ДанныеОтправки);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка отправки данных.
		                          |Код ошибки: ДО-%1
		                          |%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Отправка данных'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
	КонецПопытки;
	
	Возврат Результат

КонецФункции 

// Только для внутреннего использования
Процедура НачатьТестНастройкиОбменаЧерезДополнительнуюОбработку(Результат, Параметры) Экспорт
	
	Если Результат = Ложь Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	ДоступныеСертификаты = Параметры.ДоступныеСертификаты;
	
	Если ДоступныеСертификаты.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Проверка проведена не полностью, т.к. в настройке обмена отсутствуют сертификаты подписи'");
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ВнешнийПодключаемыйМодуль = ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(Параметры.ДополнительнаяОбработка);
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("УстановленаВнешняяКомпонента") Тогда
		Параметры.Удалить("УстановленаВнешняяКомпонента");
	КонецЕсли;
	
	Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
	
	Соответствие = Новый Соответствие;
	Для Каждого Элемент Из ДоступныеСертификаты Цикл
		Соответствие.Вставить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
	Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПродолжитьТестНастройкиОбменаПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
	ПолучитьПарольКСертификату(Оповещение, Соответствие, НСтр("ru = 'Аутентификация на ресурсе банка'"));

КонецПроцедуры

// Получает массив идентификаторов хранилищ, подключенных к компьютеру.
//
// Параметры:
//  ВнешнийПодключаемыйМодуль  - ФормаКлиентскогоПриложения - внешняя управляемая форма.
//  ВыводитьСообщение - Булево - если Ложь, то при отсутствии подключенного аппаратного устройства будет выведено сообщение.
//
// Возвращаемое значение:
//  Массив - идентификаторы хранилищ или Неопределено в случае ошибки.
//
Функция ПодключенныеХранилищаЧерезДополнительнуюОбработку(
	ВнешнийПодключаемыйМодуль,
	ВыводитьСообщение = Истина) Экспорт
	
	Попытка
		Устройства = ВнешнийПодключаемыйМодуль.ХранилищаСертификатов();
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка при поиске подключенных аппаратных устройств.
		                          |Код ошибки: ДО-%1
		                          |%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Поиск аппаратных устройств.'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
		Возврат Неопределено;
	КонецПопытки;
	
	Если Устройства.Количество() = 0 И ВыводитьСообщение Тогда
		ТекстСообщения = НСтр("ru = 'Не найдено ни одного аппаратного устройства.
									|Убедитесь, что устройство подключено к компьютеру и повторите операцию'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат Устройства;
	
КонецФункции

// Устанавливает соединение с банком.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                    которая будет вызвана после завершения вызова метода со следующими параметрами: 
//                  * Результат - Булево - результат вызова метода, если Истина, то соединение установлено успешно.
//                  * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль  - ФормаКлиентскогоПриложения - внешняя управляемая форма;
//  СертификатXML  - Строка - Содержит данные сертификата;
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка с банком;
//
Процедура УстановитьСоединениеЧерезДополнительнуюОбработку(Оповещение, ПодключаемыйМодуль, СертификатXML, НастройкаОбмена = Неопределено) Экспорт
	
	Попытка
		
		Если НЕ ПодключаемыйМодуль.УстановитьСоединение(СертификатXML) Тогда
			ПараметрыРасширеннойАутентификации = Неопределено;
			ТребуетсяРасширеннаяАутентификация = ПодключаемыйМодуль.ТребуетсяРасширеннаяАутентификация(
															СертификатXML, ПараметрыРасширеннойАутентификации);
			Если ТребуетсяРасширеннаяАутентификация Тогда
				Если ПараметрыРасширеннойАутентификации.Способы.Количество() = 0 Тогда
					ВызватьИсключение НСтр("ru = 'Не определены способы расширенной аутентификации.'");
				КонецЕсли;
				Если НЕ ПараметрыРасширеннойАутентификации.Способы.Свойство("SMS") Тогда
					ВызватьИсключение НСтр("ru = 'Расширенная аутентификация по SMS не поддерживается.'");
				КонецЕсли;
				
				РасширеннаяАутентификацияЧерезДопОбработку(
					ПодключаемыйМодуль, СертификатXML, ПараметрыРасширеннойАутентификации.Сессия, Оповещение);
			Иначе
				ВызватьИсключение НСтр("ru = 'Ошибка установки соединения.'");
			КонецЕсли;
		Иначе
			ВыполнитьОбработкуОповещения(Оповещение, Истина);
		КонецЕсли;
		
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка установки соединения.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка соединения'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, НастройкаОбмена);
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
	КонецПопытки;
	
КонецПроцедуры

// Проверяет, нужно ли устанавливать пин-код.
//
// Параметры
//  ВнешнийПодключаемыйМодуль  - Управляемая Форма - внешняя управляемая форма;
//  ИдентификаторХранилища - Строка - идентификатор хранилища;
//  Ошибка - Булево - признак возникновения ошибки при выполнении функции;
//
// Возвращаемое значение:
//  Булево, Неопределено - нужно установить PIN или Неопределено при возникновении ошибки.
//
Функция ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
	ВнешнийПодключаемыйМодуль,
	ИдентификаторХранилища,
	Ошибка = Ложь) Экспорт
	
	Ошибка = Ложь;
	Попытка
		ТребуетсяПИН = ВнешнийПодключаемыйМодуль.ТребуетсяУстановкаPINКодаХранилища(ИдентификаторХранилища);
	Исключение
		ОчиститьСообщения();
		ШаблонОшибки = НСтр("ru = 'Ошибка проверки необходимости ввода PIN-кода.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Проверка необходимости ввода PIN-кода'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
		Ошибка = Истина;
		ТребуетсяПИН = Ложь;
	КонецПопытки;
	
	Возврат ТребуетсяПИН;
	
КонецФункции

Функция ДанныеСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, СертификатXML) Экспорт
	
	Попытка
		ДанныеСертификата = ВнешнийПодключаемыйМодуль.ДанныеСертификата(СертификатXML);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения данных сертификата.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Получение данных сертификата'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ДанныеСертификата;
	
КонецФункции

Функция УстановленПарольСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, СертификатXML) Экспорт
	
	Попытка
		УстановленПарольСертификата = ВнешнийПодключаемыйМодуль.УстановленПарольСертификата(СертификатXML);
	Исключение
		УстановленПарольСертификата = Ложь;
	КонецПопытки;

	Возврат УстановленПарольСертификата;
	
КонецФункции

Процедура ПродолжитьПолучениеВыпискиПослеВводаPINКодаЧерезДополнительнуюОбработку(PINКод, Параметры) Экспорт
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	
	Если PINКод = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановленPIN = УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, Параметры.ИдентификаторХранилища, PINКод);
		
	Если НЕ УстановленPIN Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	ДанныеСертификата = Параметры.ДанныеСертификата;
	ПарольСертификата = Параметры.ПарольСертификата;
	
	ПарольУстановлен = УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата, ПарольСертификата, Параметры.ВыбранныйСертификат);
	Если НЕ ПарольУстановлен Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПродолжитьПолучениеВыпискиПослеУстановкиСоединенияЧерезДопОбработку", ЭтотОбъект, Параметры);
	
	УстановитьСоединениеЧерезДополнительнуюОбработку(Оповещение, ВнешнийПодключаемыйМодуль,
		ДанныеСертификата.ДвоичныеДанныеСертификата, НастройкаОбмена);
		
КонецПроцедуры

Процедура ПродолжитьОтправкуПакетаПослеВводаPINКодаЧерезДополнительнуюОбработку(PINКод, Параметры) Экспорт
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	Если PINКод <> Неопределено Тогда
		УстановленPIN = УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, Параметры.ИдентификаторХранилища, PINКод);
		Если УстановленPIN Тогда
			ПродолжитьОтправкуПакетаЧерезДополнительнуюОбработку(Параметры);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура("Успех", Ложь);
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеОтправкиДокументов, Результат);
	
КонецПроцедуры

Процедура ОтправитьПакетыЧерезДополнительнуюОбработку(АутентификацияВыполнена, Параметры) Экспорт
	
	Если АутентификацияВыполнена = Истина Тогда
		ДокументыКОтправке = Новый Массив;
		СообщенияОбменаДляПодтверждения = Новый Массив;
		
		Если Параметры.Свойство("ОтправляемыеПакетыЧерезДопОбработку") Тогда
			Для Каждого Документ Из Параметры.ОтправляемыеПакетыЧерезДопОбработку Цикл
				СтруктураОтправки = Новый Структура;
				СтруктураОтправки.Вставить("Ключ", Документ.Значение.Ключ);
				СтруктураОтправки.Вставить("ЭлектронныйДокумент", Документ.Значение.ПлатежноеПоручение);
				СтруктураОтправки.Вставить("СхемаДанных", ПолучитьИзВременногоХранилища(Документ.Значение.СлужебныеДанные));
				СтруктураОтправки.Вставить("Подписи", Новый Массив);
				УдалитьИзВременногоХранилища(Документ.Значение.СлужебныеДанные);
			
				Для Каждого СтрокаДанныеПодписи Из Документ.Значение.Подписи Цикл
					ДанныеПодписи = Новый Структура(
						"Сертификат, Подпись", СтрокаДанныеПодписи.Сертификат, СтрокаДанныеПодписи.Подпись);
					СтруктураОтправки.Подписи.Добавить(ДанныеПодписи);
				КонецЦикла;
				ДокументыКОтправке.Добавить(СтруктураОтправки);
			КонецЦикла;
			
			СтруктураОтправки = Новый Структура();
			СтруктураОтправки.Вставить("Документы",         ДокументыКОтправке);
			СтруктураОтправки.Вставить("ВерсияСхемыДанных", ОбменСБанкамиКлиентСервер.ВерсияФорматаСинхронногоОбмена());

			Результат = ОтправитьЗапросЧерезДополнительнуюОбработку(
				Параметры.ВнешнийПодключаемыйМодуль, Параметры.ДанныеСертификата.ДвоичныеДанныеСертификата, 3, СтруктураОтправки);
				
			Если Результат = Неопределено Тогда
				Результат = Новый Структура("Успех", Ложь);
				ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеОтправкиДокументов, Результат);
				Возврат;
			Иначе
				КоличествоОтправленных = Результат.Количество();
				ОтправленныеДокументы = Новый Массив;
				ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветБанкаПослеОтправкиДокументовЧерезВнешнююОбработку(
					Параметры.НастройкаОбмена, Параметры.ОтправляемыеПакетыЧерезДопОбработку, Результат, ОтправленныеДокументы,
					СообщенияОбменаДляПодтверждения);
				Если ОтправленныеДокументы.Количество() Тогда
					Оповестить("ОтправленоDirectBank", ОтправленныеДокументы);
				КонецЕсли;
				Параметры.Вставить("КоличествоОтправленных", КоличествоОтправленных);
			КонецЕсли;
		Иначе
			Параметры.Вставить("КоличествоОтправленных", 0);
		КонецЕсли;
		Если Параметры.ДанныеДляОтправки.Свойство("ДокументыДляПодтверждения") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				СообщенияОбменаДляПодтверждения, Параметры.ДанныеДляОтправки.ДокументыДляПодтверждения);
		КонецЕсли;
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПодтвержденияПлатежейЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
		ПодтвердитьПлатежиЧерезДополнительнуюОбработку(Оповещение, СообщенияОбменаДляПодтверждения,
			Параметры.ВнешнийПодключаемыйМодуль, Параметры.ДанныеСертификата.ДвоичныеДанныеСертификата);
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура("Успех", Ложь);
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеОтправкиДокументов, Результат);
	
КонецПроцедуры

Процедура ПослеПодтвержденияПлатежейЧерезДополнительнуюОбработку(ЕстьОшибка, ДополнительныеПараметры) Экспорт
	
	Результат = Новый Структура("Успех, Отправлено", Не ЕстьОшибка, ДополнительныеПараметры.КоличествоОтправленных);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиДокументов, Результат);
	
КонецПроцедуры

Процедура ПродолжитьПодписаниеПослеВводаPINКодаЧерезДополнительнуюОбработку(PINКод, Параметры) Экспорт
	
	ПрерватьПодписание = Истина;
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	Если PINКод <> Неопределено Тогда
		УстановленPIN = УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, Параметры.ИдентификаторХранилища, PINКод);
			
		Если УстановленPIN Тогда
			ПрерватьПодписание = Ложь;
			ПродолжитьПодписаниеЧерезДополнительнуюОбработку(Параметры)
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеПодписатьЭД = Неопределено;
	Если ПрерватьПодписание И Параметры.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД)
		И ТипЗнч(ОписаниеПодписатьЭД) = Тип("ОписаниеОповещения") Тогда
		
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодписаниеЭДЧерезДополнительнуюОбработку(АутентификацияВыполнена, Параметры) Экспорт
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	СтруктураСертификата = Параметры.СтруктураСертификата;
	МассивСообщенийОбменаКПодписи = Параметры.МассивСообщенийОбменаКПодписи;
	ПрерватьПодписание = Ложь;
	Если АутентификацияВыполнена = Истина Тогда
		СертификатXML = Параметры.СертификатXMLЧерезДопОбработку;
		ДанныеОбработки = ОбменСБанкамиСлужебныйВызовСервера.ДанныеДляФормированияЭПЧерезДопОбработку(
			Параметры.МассивСообщенийОбменаКПодписи);

		Индекс = -1;
		МассивСообщенийНовыеСхемы = Новый Массив;
		МассивСхемДанных = Новый Массив;
		
		Для каждого ЭлементКоллекции Из ДанныеОбработки.МассивСообщенийБезСхем Цикл
			Индекс = Индекс + 1;
			МассивТекстовыхДанныхЭД = Новый Массив;
			МассивТекстовыхДанныхЭД.Добавить(ДанныеОбработки.МассивТекстовыхДанныхЭД.Получить(Индекс));
			Попытка
				МассивНовыхСхемДанных = ВнешнийПодключаемыйМодуль.СхемаДанных(СертификатXML, МассивТекстовыхДанныхЭД);
			Исключение
				ШаблонОшибки = НСтр("ru = 'Ошибка проверки документа в банке.
											|Код ошибки: ДО-%1
											|%2'");
				ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
				ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
				Операция = НСтр("ru = 'Получение схемы данных'");
				ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
				// В большинстве случаев требуется исправление реквизитов документов:
				СтатусОтклоненБанком = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.ОтклоненБанком");
				РеквизитыИзменения = Новый Структура;
				РеквизитыИзменения.Вставить("ПричинаОтклонения", ДеталиОшибки.Сообщение);
				РеквизитыИзменения.Вставить("Статус", СтатусОтклоненБанком);
				ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(
					МассивСообщенийОбменаКПодписи.Получить(0), РеквизитыИзменения);
				Продолжить;
			КонецПопытки;
			МассивСхемДанных.Добавить(МассивНовыхСхемДанных.Получить(0));
			МассивСообщенийНовыеСхемы.Добавить(ЭлементКоллекции);


		КонецЦикла;
		
		ОбменСБанкамиСлужебныйВызовСервера.СохранитьСхемыДанных(
			НастройкаОбмена, МассивСообщенийНовыеСхемы, МассивСхемДанных);
			
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДанныеОбработки.МассивДанныхСхем,  МассивСхемДанных);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДанныеОбработки.МассивСообщенийСоСхемами,  МассивСообщенийНовыеСхемы);
			
		Попытка
			МассивПодписей = ВнешнийПодключаемыйМодуль.Подписать(СертификатXML, ДанныеОбработки.МассивДанныхСхем);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка подписания документов.
										|Код ошибки: ДО-%1
										|%2'");
			ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Подписание документов'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, НастройкаОбмена);
			ПрерватьПодписание = Истина;
		КонецПопытки;
		Если НЕ ПрерватьПодписание Тогда
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьДанныеПодписей(
				ДанныеОбработки.МассивСообщенийСоСхемами, МассивПодписей, СтруктураСертификата.СертификатПодписи);
			Параметры.КоличествоПодписанных = Параметры.КоличествоПодписанных + МассивПодписей.Количество();
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеПодписатьЭД = Неопределено;
	Параметры.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД);
	Если ТипЗнч(ОписаниеПодписатьЭД) = Тип("ОписаниеОповещения") Тогда
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДоступноеХранилищеЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Параметры) Экспорт
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		ОписаниеОповещения = Неопределено;
		Если Параметры.Свойство("ОбработчикПродолжения", ОписаниеОповещения)
			И ТипЗнч(ОписаниеОповещения) = Тип("ОписаниеОповещения") Тогда
			
			Результат = Неопределено;
			Параметры.Свойство("РезультатПодписания", Результат);
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, Результат);
		КонецЕсли;
	Иначе
		Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		ДоступноеХранилище = ДоступноеХранилищеЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль);
		Если ЗначениеЗаполнено(ДоступноеХранилище) Тогда
			ВыполнитьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку(ДоступноеХранилище, Параметры);
		Иначе
			Оповещение = Новый ОписаниеОповещения(
				"ВыполнитьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
			ВыбратьХранилищеЧерезДополнительнуюОбработку(Параметры.НастройкаОбмена, Оповещение, Параметры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьТестНастройкиОбменаПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(ПараметрВозврата, Параметры) Экспорт
	
	Если ПараметрВозврата = Неопределено ИЛИ Параметры.СоотвСертификатовИИхСтруктур.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки);
		Возврат;
	КонецЕсли;
	
	ПараметрыСертификата = Параметры.СоотвСертификатовИИхСтруктур.Получить(ПараметрВозврата.ВыбранныйСертификат);
	ПараметрыСертификата.ПарольСертификата = ПараметрВозврата.ПарольСертификата;
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	СертификатXML = ПараметрыСертификата.ДанныеСертификата;
	
	// Блок чтения данных сертификата.
	ДанныеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, СертификатXML);
	Если ДанныеСертификата = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
		
	// Блок проверка наличия установленного PIN-кода.
	ЕстьОшибка = Ложь;
	ТребуетсяПИН = ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ДанныеСертификата.ИдентификаторХранилища, ЕстьОшибка);
	Если ЕстьОшибка Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
		
	Параметры.Вставить("СертификатXML", СертификатXML);
	Параметры.Вставить("ПарольСертификата", ПараметрВозврата.ПарольСертификата);
	Параметры.Вставить("ДанныеСертификата", ДанныеСертификата);
	Параметры.Вставить("ИмяПроцедуры", "ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработкуПослеАутентификации");
	Параметры.Вставить("Модуль", ЭтотОбъект);
	Параметры.Вставить("ПараметрыСертификата", ПараметрыСертификата);
	Параметры.Вставить("ИдентификаторХранилища", ДанныеСертификата.ИдентификаторХранилища);
	Параметры.Вставить("ВыбранныйСертификат", ПараметрВозврата.ВыбранныйСертификат);
	
	// Блок проверка установки PIN-кода.
	Если ТребуетсяПИН Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИдентификаторХранилища", ДанныеСертификата.ИдентификаторХранилища);
		ПараметрыФормы.Вставить("НастройкаОбмена", НастройкаОбмена);
		Параметры.Вставить("УстановленPINКод");
		ОООЗ = Новый ОписаниеОповещения(
			"ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработкуПослеВводаPINКода", ЭтотОбъект, Параметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросPINКода", ПараметрыФормы, , , , , ОООЗ);
		Возврат;
	КонецЕсли;
	ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработку(Параметры);
	
КонецПроцедуры

Процедура ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработкуПослеАутентификации(АутентификацияВыполнена, Параметры) Экспорт
	
	Если Не АутентификацияВыполнена = Истина Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;

	ПараметрыСертификата = Параметры.ПараметрыСертификата;
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	СертификатXML = Параметры.СертификатXML;
	ДанныеСертификата = Параметры.ДанныеСертификата;
	
	// Блок проверка установки подписи для данных.
	
	ОтпечатокBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(ПараметрыСертификата.Отпечаток);
	ДвоичныеДанные = Base64Значение(ОтпечатокBase64);
	МассивПодписи = Новый Массив;
	МассивПодписи.Добавить(ДвоичныеДанные);
	Попытка
		МассивПодписей = ВнешнийПодключаемыйМодуль.Подписать(СертификатXML, МассивПодписи);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка установки подписи.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка подписи'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки);
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, ТекстСообщения);
		Возврат;
	КонецПопытки;
	
	// Блок проверка подписи.
	Попытка
		ДопПараметры = Новый Структура("ИдентификаторХранилища", ДанныеСертификата.ИдентификаторХранилища);
		ПодписьВалидна = ВнешнийПодключаемыйМодуль.ПроверитьПодпись(
			СертификатXML, ДвоичныеДанные, МассивПодписей[0], ДопПараметры);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка проверки подписи.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Проверка подписи'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки);
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, ТекстСообщения);
		Возврат;
	КонецПопытки;
	Если НЕ ПодписьВалидна Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, НСтр("ru = 'Подпись не валидна'"));
		Возврат;
	КонецЕсли;
	
	// Блок проверки отправки тестового запроса.
	Попытка
		ВнешнийПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, 1);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка отправки тестового запроса.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Отправка тестового запроса'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, , Параметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, ТекстСообщения);
		Возврат;
	КонецПопытки;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, Истина);
	
КонецПроцедуры

Процедура ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработкуПослеВводаPINКода(PINКод, Параметры) Экспорт
	
	Если PINКод = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки);
		Возврат;
	КонецЕсли;
	
	ПинКодУстановлен = УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
		Параметры.ВнешнийПодключаемыйМодуль, Параметры.ИдентификаторХранилища, PINКод);
	
	Если Не ПинКодУстановлен Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработку(Параметры)
	
КонецПроцедуры

// Показывает вопрос и устанавливает внешнюю компоненту.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Оповещение, вызываемое после установки компоненты.
//    * Результат - Булево - если Истина, то пользователь начал установку внешней компоненты,
//                           если Ложь, то пользователь отказался от установки.
//  Местоположение - Строка - Строка, определяющая местоположение внешнего компонента.
//
Процедура УстановитьВнешнююКомпонентуДляОбменаЧерезОбработку(Оповещение, Местоположение) Экспорт
	
	ДополнительныеПараметры = Новый Структура("Оповещение, Местоположение", Оповещение, Местоположение);
	ОповещениеПослеВопроса = Новый ОписаниеОповещения(
		"ПослеВопросаОУстановкеКомпоненты", ЭтотОбъект, ДополнительныеПараметры);
	#Если ВебКлиент Тогда
		ТекстВопроса = НСтр("ru = 'Для обмена с банком необходимо установить расширение.'");
	#Иначе
		ТекстВопроса = НСтр("ru = 'Для обмена с банком необходимо установить компоненту.'");
	#КонецЕсли
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(Истина, НСтр("ru = 'Установить и продолжить'"));
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	
	ПоказатьВопрос(ОповещениеПослеВопроса, ТекстВопроса, Кнопки, , Истина);
	
КонецПроцедуры

Процедура ПродолжитьПолучениеВыпискиПослеУстановкиСоединенияЧерезДопОбработку(
	АутентификацияВыполнена,
	Параметры) Экспорт
	
	Если Не АутентификацияВыполнена Тогда
		Возврат
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
	ПараметрыФормы.Вставить("ДанныеСертификата", Параметры.ДанныеСертификата);
	ПараметрыФормы.Вставить("ДатаНачала", Параметры.ДатаНачала);
	ПараметрыФормы.Вставить("ДатаОкончания", Параметры.ДатаОкончания);
	ПараметрыФормы.Вставить("НомерСчета", Параметры.НомерСчета);
	ПараметрыФормы.Вставить("ВидОперации", "ПолучениеВыписки");
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписки", ЭтотОбъект, Параметры);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область Криптография

// Получает пароль к сертификату для нестандартного подписания.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                    которая будет вызвана после завершения вызова метода со следующими параметрами:
//   * Результат - Структура, Неопределено - возможные значения: 
//                - Структура - результат вызова метода. Содержит следующие поля:
//                  ** ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//                                         - выбранный пользователем сертификат подписи;
//                  ** ПарольСертификата - Строка - пароль к закрытой части сертификата;
//               - Неопределено - Пользователь отказался от ввода и закрыл форму.
//    * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  СоотвСертификатовИИхСтруктур - Соответствие - набор сертификатов для выбора:
//        * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат подписи;
//        * Значение - Структура - параметры сертификат подписи. Содержит следующие поля:
//             * ПарольПолучен - Булево - признак, что пароль сохранен в информационной базе
//  ВидОперации - Строка - выполняемая операция;
//  ОбъектыДляОбработки - ОпределяемыйТип.ВладелецОбменСБанками, Массив из ОпределяемыйТип.ВладелецОбменСБанками - 
//                        подписываемый объект;
//  ПрограммаБанка - ПеречислениеСсылка.ПрограммыБанка - программа обмена с банком.
//
Процедура ПолучитьПарольКСертификату(
	Оповещение,
	СоотвСертификатовИИхСтруктур,
	ВидОперации,
	ОбъектыДляОбработки = Неопределено,
	ПрограммаБанка = Неопределено) Экспорт
	
	Если ТипЗнч(СоотвСертификатовИИхСтруктур) = Тип("Соответствие")
		И СоотвСертификатовИИхСтруктур.Количество() > 0 Тогда
		
		СбербанкОнлайн = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
		
		Если СоотвСертификатовИИхСтруктур.Количество() = 1 ИЛИ ПрограммаБанка = СбербанкОнлайн Тогда
			Для Каждого Элемент Из СоотвСертификатовИИхСтруктур Цикл
				Если Элемент.Значение.ПарольПолучен = Истина ИЛИ ПрограммаБанка = СбербанкОнлайн Тогда
					Элемент.Значение.Вставить("ВыбранныйСертификат", Элемент.Ключ);
					ВыполнитьОбработкуОповещения(Оповещение, Элемент.Значение);
					Возврат;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОперации", ВидОперации);
		МассивСертификатов = Новый Массив;
		Для Каждого КлючЗначение Из СоотвСертификатовИИхСтруктур Цикл
			МассивСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла;
		ПараметрыФормы.Вставить("МассивСертификатов", МассивСертификатов);
		Если ОбъектыДляОбработки <> Неопределено Тогда
			Если ТипЗнч(ОбъектыДляОбработки) <> Тип("Массив") Тогда
				МассивОбъектов = Новый Массив;
				МассивОбъектов.Добавить(ОбъектыДляОбработки);
			Иначе
				МассивОбъектов = ОбъектыДляОбработки;
			КонецЕсли;
			ПараметрыФормы.Вставить("ОбъектыДляОбработки", МассивОбъектов);
		КонецЕсли;
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросПароляКСертификату", ПараметрыФормы, , , , , Оповещение,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьРезультатПослеПроверкиПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
	Если ТипЗнч(Результат) = Тип("Булево") Тогда
		СтруктураРезультата.Вставить("ПодписьВерна", Результат);
	ИначеЕсли ТипЗнч(Результат) = Тип("Строка") Тогда
		ТекстОшибки = НСтр("ru = 'Электронная подпись неверна.
								|Причина: %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Результат);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		СтруктураРезультата.Вставить("ПодписьВерна", Ложь);
	Иначе
		ТекстСообщения = НСтр("ru = 'Не удалось проверить подписи электронного документа.
									|Проверьте настройки криптографии или обратитесь к администратору.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.РезультатыПроверкиПодписей.Добавить(СтруктураРезультата);
	
	ПроверитьОчереднуюПодпись(ДополнительныеПараметры)
	
КонецПроцедуры

// Получает отпечатки сертификатов. Если в настройках криптографии не указана подпись на сервере, то принудительно устанавливается расширение.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения асинхронной процедуры
//
Процедура ПолучитьОтпечаткиСертификатов(Оповещение) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	СоздаватьЭлектронныеПодписиНаСервере = ЭлектроннаяПодписьКлиент.СоздаватьЭлектронныеПодписиНаСервере();
	ДополнительныеПараметры.Вставить("СоздаватьЭлектронныеПодписиНаСервере", СоздаватьЭлектронныеПодписиНаСервере);
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	Если ЭлектроннаяПодписьКлиент.СоздаватьЭлектронныеПодписиНаСервере() Тогда
		ПолучитьОтпечаткиПослеУстановкиРасширения(Истина, ДополнительныеПараметры);
	Иначе
		ОбработчикРезультата = Новый ОписаниеОповещения(
			"ПолучитьОтпечаткиПослеУстановкиРасширения", ЭтотОбъект, ДополнительныеПараметры);
		ЭлектроннаяПодписьКлиент.УстановитьРасширение(Ложь, ОбработчикРезультата);
	КонецЕсли;
	
КонецПроцедуры

// Подписывает сообщения обмена и проверяет их валидность
//
// Параметры:
//  ОписаниеОповещения - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры. см. ЭлектроннаяПодписьКлиент.Подписать.
//  МассивСообщенийОбмена - Массив - содержит ссылки на ДокументСсылка.СообщениеОбменСБанками
//  МассивСертификатов - Массив - список сертификатов для выбора пользователем. В элементах содержит СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования.
//
Процедура Подписать(ОписаниеОповещения, МассивСообщенийОбмена, МассивСертификатов) Экспорт
	
	ПрисоединенныеФайлыСообщенийОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПрисоединенныеФайлы(МассивСообщенийОбмена);
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	ОписаниеДанных.Вставить("БезПодтверждения", Истина);
	ОписаниеДанных.Вставить("ЗаголовокДанных", "");
	
	НаборДанных = Новый Массив;
	Если МассивСообщенийОбмена.Количество() = 1 Тогда
		Операция = НСтр("ru = 'Подписание электронного документа'");
		Данные = Новый Структура;
		ПараметрыДляПолученияДД = Новый Структура(
			"СообщениеОбмена, ОписаниеДанных", МассивСообщенийОбмена[0], ОписаниеДанных);
		СсылкаНаДД = Новый ОписаниеОповещения(
			"ПолучитьДвоичныеДанныеДляСообщенияОбмена", ЭтотОбъект, ПараметрыДляПолученияДД);
		Данные.Вставить("Данные", СсылкаНаДД);
		Данные.Вставить("Объект", ПрисоединенныеФайлыСообщенийОбмена.Получить(МассивСообщенийОбмена[0]));
		ОбработчикОткрытияЭД = Новый ОписаниеОповещения(
			"ПриОткрытииЭлектронногоДокумента", ЭтотОбъект, МассивСообщенийОбмена[0]);
		СтруктураПредставления = Новый Структура("Представление, Значение", МассивСообщенийОбмена[0], ОбработчикОткрытияЭД);
		Данные.Вставить("Представление", СтруктураПредставления);
		Данные.Вставить("СообщениеОбменСБанками", МассивСообщенийОбмена[0]);
		НаборДанных.Добавить(Данные);
	Иначе
		Операция = НСтр("ru = 'Подписание электронных документов'");
		ПредставлениеНабора = НСтр("ru = 'Электронные документы (%1)'");
		МассивПредставлений = Новый Массив;
		Для Каждого КлючЗначение Из ПрисоединенныеФайлыСообщенийОбмена Цикл
			ОбработчикОткрытияЭД = Новый ОписаниеОповещения("ПриОткрытииЭлектронногоДокумента", ЭтотОбъект, КлючЗначение.Ключ);
			СтруктураПредставления = Новый Структура("Представление, Значение", Строка(КлючЗначение.Ключ), ОбработчикОткрытияЭД);
			МассивПредставлений.Добавить(СтруктураПредставления);
			
			Данные = Новый Структура;
			ПараметрыДляПолучения = Новый Структура("СообщениеОбмена, ОписаниеДанных", КлючЗначение.Ключ, ОписаниеДанных);
			ПолучениеДвоичныхДанных = Новый ОписаниеОповещения(
				"ПолучитьДвоичныеДанныеДляСообщенияОбмена", ЭтотОбъект, ПараметрыДляПолучения);
			Данные.Вставить("Данные", ПолучениеДвоичныхДанных);
			Данные.Вставить("Объект", КлючЗначение.Значение);
			Данные.Вставить("СообщениеОбменСБанками", КлючЗначение.Ключ);
			НаборДанных.Добавить(Данные);
		КонецЦикла;
		ОписаниеДанных.Вставить("СписокПредставлений", МассивПредставлений);
		ОписаниеДанных.Вставить("ПредставлениеНабора", ПредставлениеНабора);
	КонецЕсли;
	
	ОписаниеДанных.Вставить("НаборДанных", НаборДанных);
	ОписаниеДанных.Вставить("Операция", Операция);
	ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, , ОписаниеОповещения);
	
КонецПроцедуры

// Проверяет подписи электронного документа.
//
// Параметры:
// Оповещение - ОписаниеОповещения - оповещение после проверки подписей:
//    * Результат - Булево - Истина, если не было ошибки и все подписи валидны;
//    * ДополнительныеПараметры - Структура - Содержит дополнительные параметры;
// СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение обмена электронного документа.
//
Процедура ПроверитьПодписиСообщенияОбмена(Оповещение, СообщениеОбмена) Экспорт
	
	СтруктураСодержимого = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСодержимогоСообщенияОбмена(СообщениеОбмена);
	
	МассивПодписей = СтруктураСодержимого.Подписи;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиПодписей", Оповещение);
	ДополнительныеПараметры.Вставить("МассивПодписей", МассивПодписей);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ДвоичныеДанныеЭД", СтруктураСодержимого.ДанныеЭД);
	ДополнительныеПараметры.Вставить("ИндексТекущейПодписи", -1);
	ДополнительныеПараметры.Вставить("РезультатыПроверкиПодписей", Новый Массив);
	
	ПроверитьОчереднуюПодпись(ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область ПодменюКоманд

// Команда открытия электронного документа.
Процедура ОткрытьАктуальныйЭД(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОбменСБанкамиКлиент.ОткрытьАктуальныйЭД(
		ПараметрКоманды, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды);
	
КонецПроцедуры

// Команда открытия формы списка электронных документов.
Процедура СписокЭД(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОбменСБанкамиКлиент.ОткрытьСписокЭД(ПараметрКоманды, ПараметрыВыполненияКоманды);
	
КонецПроцедуры

// Команда создания электронного документа.
Процедура СформироватьЭД(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОчиститьСообщения();
	Обработчик = Новый ОписаниеОповещения("СформироватьНовыйЭД", ОбменСБанкамиКлиент, Истина);
	ЭлектронноеВзаимодействиеСлужебныйКлиент.ВыполнитьПроверкуПроведенияДокументов(
		ПараметрКоманды, Обработчик, ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры

// Команда создания, подписания и отправки электронного документа.
Процедура СформироватьПодписатьОтправитьЭД(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОчиститьСообщения();
	Обработчик = Новый ОписаниеОповещения("СформироватьПодписатьОтправитьЭД", ОбменСБанкамиКлиент);
	ЭлектронноеВзаимодействиеСлужебныйКлиент.ВыполнитьПроверкуПроведенияДокументов(
		ПараметрКоманды, Обработчик, ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры

// Открывает форму списка документов переписки с банками
// 
Процедура ОткрытьПереписку(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	НавигационнаяСсылка = "e1cib/command/Документ.ПисьмоОбменСБанками.Команда.ПерепискаСБанками";
	ПерейтиПоНавигационнойСсылке(НавигационнаяСсылка);
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиОбмена

// Производит тестирование настройки обмена с банком.
//
// Параметры:
//  Обработчик - ОписаниеОповещения - обработчик, вызываемый после тестирования настройки:
//           * Результат - Структура - с полями:
//              ** Успех - Булево - результат тестирования настройки;
//              ** МассивСообщений - Массив - в элементах строки, содержащие текст сообщений об ошибках;
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - тестируемая настройка обмена с банком.
//  ВыводитьОкноОжидания - Булево - выводить окно ожидания длительной операции
//
Процедура ПровестиТестНастройки(Обработчик, НастройкаОбмена, ВыводитьОкноОжидания=Ложь) Экспорт
	
	ВидЭДЗапросЗонд = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросЗонд");
	ПараметрыОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаПоВидуЭД(НастройкаОбмена, ВидЭДЗапросЗонд);
	ПрограммаБанка = ПараметрыОбмена.ПрограммаБанка;
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		Параметры = Новый Структура;
		Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
		Параметры.Вставить("РеквизитыНастройкиОбмена", ПараметрыОбмена);
		Параметры.Вставить("ОбработчикПослеТестаНастройки", Обработчик);
		Если ПараметрыОбмена.АутентификацияПоСертификату ИЛИ ПараметрыОбмена.ТребуетсяПодпись Тогда
			Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтпечатковПроверитьСвязь", ЭтотОбъект, Параметры);
			ПолучитьОтпечаткиСертификатов(Оповещение);
		Иначе
			ПослеПолученияОтпечатковПроверитьСвязь(Новый Соответствие, Параметры);
		КонецЕсли;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		ПровестиТестНастройкиСбербанк(Обработчик, НастройкаОбмена, ПараметрыОбмена);
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.FintechIntegration") Тогда
		ПровестиТестНастройкиFintech(Обработчик, НастройкаОбмена, ПараметрыОбмена, ВыводитьОкноОжидания);
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		ТестНастройкиОбменаВК(Обработчик, НастройкаОбмена);
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ТестНастройкиОбменаЧерезДополнительнуюОбработку(Обработчик, НастройкаОбмена);
	КонецЕсли;
	
КонецПроцедуры

// Прерывает процесс получения документов через ВК.
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком.
//
Процедура ПрерватьПроцессыНаКлиенте(НастройкаОбмена) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) <> Тип("Соответствие") Тогда
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками", Новый Соответствие);
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	КонецЕсли;

	Если ПараметрыПодсистемыОбменСБанками.Получить("ПрерватьПроцессПоНастройкамОбмена") <> Неопределено Тогда
		МассивНастроекОбмена = ПараметрыПодсистемыОбменСБанками.Получить("ПрерватьПроцессПоНастройкамОбмена");
	Иначе
		МассивНастроекОбмена = Новый Соответствие;
	КонецЕсли;
	
	МассивНастроекОбмена.Вставить(НастройкаОбмена, Истина);
	
	ПараметрыПодсистемыОбменСБанками.Вставить("ПрерватьПроцессПоНастройкамОбмена", МассивНастроекОбмена);
	
КонецПроцедуры

// Определяет, отменил ли пользователь процесс запроса данных. При этом признак прерывания сбрасывается.
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка, по который идет процесс получения документов.
// 
// Возвращаемое значение:
// Булево - если Истина, значит пользователь нажал на кнопку Отмена.
//
Функция ПроцессПрерван(НастройкаОбмена) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие")
		И НЕ ПараметрыПодсистемыОбменСБанками.Получить("ПрерватьПроцессПоНастройкамОбмена") = Неопределено Тогда
		НастройкиОбменаДляПрерыванияПроцесса = ПараметрыПодсистемыОбменСБанками.Получить(
			"ПрерватьПроцессПоНастройкамОбмена");
		Если НастройкиОбменаДляПрерыванияПроцесса.Получить(НастройкаОбмена) <> Неопределено Тогда
			НастройкиОбменаДляПрерыванияПроцесса.Удалить(НастройкаОбмена);
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;

	Возврат Ложь;
	
КонецФункции

// Функция получает пароль пользователя к сертификатам ЭП. Если в СоотвСертификатовИИхСтруктур передано несколько
// сертификатов, то в этот параметр вместо списка помещается один найденный сертификат с паролем и его параметры.
//
// Параметры:
//  СоотвСертификатовИИхСтруктур - Соответствие - содержит соответствие сертификатов и их параметров:
//    * Ключ     - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат ЭП.
//    * Значение - Структура - содержит параметры сертификата.
//  ВнешнийПодключаемыйМодуль - Форма - внешний модуль.
//
// Возвращаемое значение:
//  Булево - Истина - если пароль для сертификата ЭП получен, иначе - Ложь.
//
Функция ЕстьСертификатССохраненнымПаролем(
	СоотвСертификатовИИхСтруктур,
	ВнешнийПодключаемыйМодуль = Неопределено) Экспорт
	
	ПарольПолучен = Ложь;
	
	Если ТипЗнч(СоотвСертификатовИИхСтруктур) = Тип("Соответствие") Тогда
		КоличествоСертификатов = СоотвСертификатовИИхСтруктур.Количество();
		Для Каждого КлючИЗначение Из СоотвСертификатовИИхСтруктур Цикл
			Сертификат = КлючИЗначение.Ключ;
			ПараметрыСертификата = КлючИЗначение.Значение;
			ПрограммаБанка = Неопределено;
			Если НЕ ЗначениеЗаполнено(ПараметрыСертификата) Тогда
				ПараметрыСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(Сертификат);
			КонецЕсли;
			Если ПараметрыСертификата.Свойство("ПрограммаБанка")
				И ПараметрыСертификата.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
				ПарольПолучен = Истина;
				Прервать;
			КонецЕсли;
			ПарольСертификата = Неопределено;
			Если (КоличествоСертификатов = 1) И (ПараметрыСертификата.Свойство("ПарольПолучен", ПарольПолучен) И ПарольПолучен
													ИЛИ ПарольКСертификатуПолучен(Сертификат, ПарольСертификата)) Тогда
				Если НЕ ПарольПолучен Тогда
					ПарольПолучен = Истина;
					ПараметрыСертификата.Вставить("ПарольПолучен", ПарольПолучен);
					ПараметрыСертификата.Вставить("ПарольСертификата", ПарольСертификата);
					ПараметрыСертификата.Вставить("ВыбранныйСертификат", Сертификат);
				КонецЕсли;
				Прервать;
			ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
					И НЕ ВнешнийПодключаемыйМодуль = Неопределено
					И АктуаленКэшПароляСертификатаЧерезДополнительнуюОбработку(ПараметрыСертификата, ВнешнийПодключаемыйМодуль) Тогда
				ПараметрыСертификата.Вставить("ВыбранныйСертификат", Сертификат);
				ПарольПолучен = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ПарольПолучен Тогда
			СоотвСертификатовИИхСтруктур = Новый Соответствие;
			СоотвСертификатовИИхСтруктур.Вставить(Сертификат, ПараметрыСертификата);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПарольПолучен;

КонецФункции

#КонецОбласти

#Область ОбменЧерезВК

// Подключает компоненту, выполненную по технологии Native API и COM асинхронном режиме.
// Для веб-клиента предлагается диалог, подсказывающий пользователю действия по установке.
// Выполняет проверку возможности исполнения компоненты на текущем клиенте пользователя.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Описание оповещения о подключении со следующими параметрами:
//      * Результат - Структура - результат подключения компоненты:
//          ** Подключено - Булево - признак подключения.
//          ** ПодключаемыйМодуль - Произвольный - экземпляр объекта внешней компоненты;
//                     - ФиксированноеСоответствие - экземпляры объектов внешней компоненты,
//                     указанные в ПараметрыПодключения.ИдентификаторыСозданияОбъектов,
//                     Ключ - Идентификатор, Значение - экземпляр объекта.
//          ** ОписаниеОшибки - Строка - краткое описание ошибки. При отмене пользователем пустая строка.
//      * ДополнительныеПараметры - Структура - значение, которое было указано при создании объекта ОписаниеОповещения.
//  Идентификатор - Строка - идентификатор объекта внешней компоненты.
//  ТекстПояснения - Строка - (необязательный) текст пояснения при установке компоненты.
//
Процедура ПодключитьКомпоненту(Оповещение, Идентификатор, ТекстПояснения = "") Экспорт
	
	ПараметрыПодключения = ВнешниеКомпонентыКлиент.ПараметрыПодключения();
	
	Если Не ЗначениеЗаполнено(ТекстПояснения) Тогда
		ШаблонПояснения = НСтр("ru = 'Для продолжения необходимо установить внешнюю компоненту %1.'");
		ТекстПояснения = СтрШаблон(ШаблонПояснения, Идентификатор);
	КонецЕсли;
	
	ПараметрыПодключения.ТекстПояснения = ТекстПояснения;
	
	ВнешниеКомпонентыКлиент.ПодключитьКомпоненту(Оповещение, Идентификатор, , ПараметрыПодключения);
	
КонецПроцедуры

// Асинхронный обработчик, вызывается из ОбменСБанкамиКлиент.ПроверитьПодписьЧерезВК.
//
// Параметры:
//  Результат - Строка, Булево - результат проверки подписи
//  ДополнительныеПараметры - Произвольный - контекст выполнения операции.
//
Процедура ЗавершитьТестСертификатаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ДополнительныеПараметры.Контекст.ОписаниеОшибки = Результат;
	ИначеЕсли НЕ Результат Тогда
		ДополнительныеПараметры.Контекст.ОписаниеОшибки = НСтр("ru = 'Установленная подпись неверна.'");
	КонецЕсли;

	ПараметрыПриложения.Удалить("ЭлектронноеВзаимодействие.ОбменСБанками.ДанныеПодписиВК");
	ПараметрыПриложения.Удалить("ЭлектронноеВзаимодействие.ОбменСБанками.ПодключаемыйМодуль");

	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеТестаПодписиЧерезВК, ДополнительныеПараметры.Контекст);
	
КонецПроцедуры

// Проверяет подпись с использованием внешней компоненты
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после проверки подписи:
//   * Результат - Строка - текст ошибки, возникшей при проверке
//               - Булево - результат проверки подписи, если Истина, то подпись валидна
//  ПодключаемыйМодуль - Произвольный - внешняя компонента
//  ДанныеBase64 - Строка - подписанные данные в формате Base64
//  СертификатBase64 - Строка - данные сертификата в формате Base64
//  ПодписьBase64 - Строка - данные подписи в формате Base64.
//
Процедура ПроверитьПодписьЧерезВК(
	Оповещение,
	ПодключаемыйМодуль,
	ДанныеBase64,
	СертификатBase64, ПодписьBase64) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиПодписи", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ОповещениеПослеВызоваПроверкиПодписи = Новый ОписаниеОповещения("ПослеПроверкиПодписиЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПроверкиПодписиЧерезВК", ЭтотОбъект);
		
	ПодключаемыйМодуль.НачатьВызовПроверитьПодпись(
		ОповещениеПослеВызоваПроверкиПодписи, СертификатBase64, ДанныеBase64, ПодписьBase64);
	
КонецПроцедуры

// Производит инициализацию внешней компоненты банка (асинхронно).
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры:
//       * Результат - Булево - Истина при успешной инициализации, в противном случае возвращается строка;
//                   - Строка - Содержит текст ошибки, если операцию не удалось выполнить.
//       * ДополнительныеПараметры - Произвольный - контекст выполнения.
//    ПодключаемыйМодуль - Произвольный - внешняя компонента банка;
//
Процедура ИнициализироватьВК(Оповещение, ПодключаемыйМодуль) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеИнициализацииВК", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеИнициализацииВК = Новый ОписаниеОповещения(
		"ПослеИнициализацииВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуИнициализацииВК", ЭтотОбъект);

	ИнформацияОПрограмме = ОбменСБанкамиСлужебныйВызовСервера.ИнформацияОПрограммеДляВК();
	
	ВерсияФормата = ОбменСБанкамиКлиентСервер.БазоваяВерсияФорматаАсинхронногоОбмена();
	
	Попытка
		ПодключаемыйМодуль.НачатьВызовИнициализировать(ОповещениеПослеИнициализацииВК, ИнформацияОПрограмме, ВерсияФормата);
	Исключение
		ТекстОшибки = НСтр("ru = 'При работе с внешним модулем произошла ошибка.
								|Выберите другой внешний модуль или обратитесь в техническую поддержку.'");
		ВыполнитьОбработкуОповещения(Оповещение, ТекстОшибки);
	КонецПопытки

КонецПроцедуры

// Формирует пакеты и отправляет в банк через внешнюю компоненту
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                    которая будет вызвана после завершения вызова метода со следующими параметрами:
//    * РезультатВызова - Структура - результат выполнения процедуры. Содержит следующие поля:
//                      ** КоличествоПодготовленных - Число - количество подготовленных пакетов;
//                      ** КоличествоОтправленных - Число - количество отправленных пакетов.
//    * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения
//  ДанныеОтправки - Соответствие - отправляемые данные:
//     * Ключ - СправочникСсылка.НастройкиОбменСБанками - настройка обмена;
//     * Значение - Структура - отправляемые данные. Содержит поля:
//         ** МассивСообщенийОбмена - Массив из ДокументСсылка.СообщениеОбменСБанками - 
//                                    электронные документы для отправки в банк
//         ** МассивСообщенийТребующихПодтверждение - Массив из ДокументСсылка.СообщениеОбменСБанками -
//                                                    электронные документы для подтверждения
//  ПолучитьНовыеДокументы - Булево - после отправки получить новые документы из банка.
//
Процедура ОтправитьДокументыЧерезВК(Оповещение, ДанныеОтправки, ПолучитьНовыеДокументы = Истина) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиСообщенийОбменаПоНастройкам", Оповещение);
	ДополнительныеПараметры.Вставить("ДанныеОтправки", ДанныеОтправки);
	ДополнительныеПараметры.Вставить("КоличествоПодготовленных", 0);
	ДополнительныеПараметры.Вставить("КоличествоОтправленных", 0);
	ДополнительныеПараметры.Вставить("ПолучитьНовыеДокументы", ПолучитьНовыеДокументы);
	
	ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

// Подписывает электронный документ с использованием внешней компоненты.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                    которая будет вызвана после завершения вызова метода со следующими параметрами:
//    * РезультатВызова - Булево - Истина, если документ успешно подписан, Ложь - операция не выполнена.
//                      - Строка - текст ошибки, которая произошла при подписи.
//                      - Неопределено - пользователь отказался от продолжения операции.
//    * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - подписываемый электронный документ;
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена;
//  СертификатСсылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат подписи
//  Пароль - Строка - пароль к закрытой части сертификата;
//  ПодключаемыйМодуль - Произвольный - подключенная внешняя компонента банка.
//  ЭтоТест - Булево - если Истина, то процедура вызывается в рамках теста.
//
Процедура ПодписатьЭДПоСертификатуЧерезВК(
	Оповещение,
	СообщениеОбмена,
	НастройкаОбмена,
	СертификатСсылка,
	Пароль,
	ПодключаемыйМодуль = Неопределено,
	ЭтоТест = Ложь) Экспорт

	//@skip-warning
	ДвоичныеДанныеЭД = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(СообщениеОбмена);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодписанияЧерезВК", Оповещение);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ДанныеЭДBase64", Base64Строка(ДвоичныеДанныеЭД));
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("СертификатСсылка", СертификатСсылка);
	ДополнительныеПараметры.Вставить("Пароль", Пароль);
	ДополнительныеПараметры.Вставить("ЭтоТест", ЭтоТест);
	РеквизитыСертификата = Новый Структура("ДанныеСертификата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(СертификатСсылка, РеквизитыСертификата);
	ДополнительныеПараметры.Вставить("СертификатBase64", РеквизитыСертификата.ДанныеСертификата);
	
	Если ПодключаемыйМодуль = Неопределено Тогда
		ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
			"АутентифицироватьсяНаТокенеПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
		
		ПодключитьИИнициализироватьВК(ОповещениеПослеПодключенияВК, НастройкаОбмена);
	Иначе // используется при тесте настроек, когда модуль выбран из файла
		АутентифицироватьсяНаТокенеПослеПодключенияВК(ПодключаемыйМодуль, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает соединение с банком на доступном сертификате.
// Если соединение уже установлено, то повторную установку соединения не инициирует.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                    которая будет вызвана после завершения вызова метода со следующими параметрами: 
//   * Результат - Булево - истина, если соединение успешно установлено.
//               - Строка - текст ошибки выполнения операции. Ее необходимо вывести пользователю.
//               - Неопределено - пользователь отказался от установки соединения.
//   * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//
Процедура УстановитьСоединениеЧерезВК(Оповещение, НастройкаОбмена) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановкиСоединенияВК", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеПолученияПодключаемогоМодуля = Новый ОписаниеОповещения(
		"ПослеПолученияПодключаемогоМодуляВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьИИнициализироватьВК(ОповещениеПослеПолученияПодключаемогоМодуля, НастройкаОбмена);
	
КонецПроцедуры

// Осуществляет подключение и инициализацию внешней компоненты.
//
// Параметры:
//    Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                      которая будет вызвана после завершения вызова метода со следующими параметрами:
//        * РезультатВызова - Произвольный, Строка, Неопределено - возможные значения
//                          - Произвольный - внешняя компонента банка;
//                          - Строка - текст ошибки;
//                          - Неопределено - внешняя компонента не подключена, текст ошибки выведен пользователю.
//        * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена.
//
Процедура ПодключитьИИнициализироватьВК(Оповещение, НастройкаОбмена) Экспорт
	
	РеквизитыНастройкиОбмена = Новый Структура(
		"ИмяВнешнегоМодуля, ИспользоватьЖурналирование, КаталогДляЖурналирования, ИдентификаторКлиента");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещенияПослеПодключенияИИнициализацииВК", Оповещение);
	ДополнительныеПараметры.Вставить("ИспользоватьЖурналирование", РеквизитыНастройкиОбмена.ИспользоватьЖурналирование);
	ДополнительныеПараметры.Вставить("КаталогДляЖурналирования", РеквизитыНастройкиОбмена.КаталогДляЖурналирования);
	ДополнительныеПараметры.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	ДополнительныеПараметры.Вставить("ИдентификаторКлиента", РеквизитыНастройкиОбмена.ИдентификаторКлиента);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ИнициализироватьПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ВнешниеКомпонентыКлиент.ПодключитьКомпоненту(ОповещениеПослеПодключенияВК, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
		
КонецПроцедуры

Процедура ПослеВключенияЖурналированияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ШаблонСообщения = НСтр("ru = 'При включении журналирования обмена произошла ошибка:
									|%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Результат);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, ТекстСообщения);
	Иначе
		ВыполнитьОбработкуОповещения(
				ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, ДополнительныеПараметры.ПодключаемыйМодуль);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВключенияЖурналирования(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеВключенияЖурналирования, Истина);
	
КонецПроцедуры

Процедура ПослеОшибкиВключенияЖурналированияВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При включении журналирования произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Включение журналирования.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеВключенияЖурналирования,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

// Получает новые документы из банка.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                    которая будет вызвана после завершения вызова метода со следующими параметрами:
//     * РезультатВызова - Структура - результат работы процедуры. Имеет следующие поля
//                            * Результат - Булево - Истина, если метод отработал корректно
//                                                   и новые документы были получены и загружены в базу;
//                                        - Строка - текст ошибки, которую необходимо вывести пользователю.
//                            * КоличествоПолученных - Число - количество полученных пакетов.
//     * ДополнительныеПараметры - Произвольный - значение,
//                                                которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль - Произвольный - внешняя компонента банка.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена.
//  СоздаватьОперацииВыписки - Булево - если Истина, то необходимо отражать выписку в учете.
//
Процедура ПолучитьНовыеДокументыВК(
	Оповещение,
	ПодключаемыйМодуль,
	НастройкаОбмена,
	СоздаватьОперацииВыписки = Истина) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияНовыхДокументов", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("КоличествоПолученных", 0);
	ДополнительныеПараметры.Вставить("СоздаватьОперацииВыписки", СоздаватьОперацииВыписки);
	
	ОповещениеПослеПолученияИдентификаторовПакетовЧерезВК = Новый ОписаниеОповещения(
		"ПолучитьПакетыПослеПолученияИдентификаторовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ВызватьПолучениеИдентификаторовНовыхПакетовЧерезВК(
		ОповещениеПослеПолученияИдентификаторовПакетовЧерезВК, НастройкаОбмена, ПодключаемыйМодуль);
	
КонецПроцедуры

// Получает идентификаторы подключенных ключей через ВК.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//       * РезультатВызова - Массив - содержит идентификаторы подключенных электронных ключей.
//                         - Строка - текст ошибки при исключении
//       * ДополнительныеПараметры - Произвольный - значение,
//                                                  которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль - Произвольный - внешняя компонента банка.
//
Процедура ПолучитьИдентификаторыПодключенныхКлючейЧерезВК(Оповещение, ПодключаемыйМодуль) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияИдентификаторовКлючей", Оповещение);
	ОповещениеПослеПолученияХранилищКлючей = Новый ОписаниеОповещения("ПослеПолученияХранилищКлючейЭПВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПолученияХранилищКлючейЭП", ЭтотОбъект);
	ПодключаемыйМодуль.НачатьВызовХранилищаКлючейЭП(ОповещениеПослеПолученияХранилищКлючей);
	
КонецПроцедуры

// Выполняет тестирование настройки обмена.
//
// Параметры:
//  Оповещение -  ОписаниеОповещения - Содержит описание процедуры,
//                                     которая будет вызвана после завершения вызова метода со следующими параметрами: 
//                  * Результат - Булево, Строка, Неопределено - возможные значения:
//                              - Булево - равен Истина, если тест пройден успешно,
//                                         Ложь - тест не выполнен с выводом сообщения об ошибке;
//                              - Строка - текст ошибки;
//                              - Неопределено - пользователь нажал Отмена.
//                  * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//  ПараметрыУстановленногоСоединения - Структура - если соединение устанавливалось при получении настроек, то оно и будет использоваться для теста:
//     * ИдентификаторХранилища - Строка - идентификатор электронного ключа, на котором уже установлено соединение;
//     * Отпечаток - Строка - отпечаток сертификата, по которому уже установлено соединение.
//     * СертификатBase64 - Строка - сертификат, на котором установлено соединение в формате Base64.
//
Процедура ТестНастройкиОбменаВК(Оповещение, НастройкаОбмена, ПараметрыУстановленногоСоединения = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеТестаНастройки", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ПараметрыУстановленногоСоединения", ПараметрыУстановленногоСоединения);
	
	Если ПараметрыУстановленногоСоединения <> Неопределено Тогда
		ПолучитьСертификатыСКлючаПослеПодключенияВК(
			ПараметрыУстановленногоСоединения.ПодключаемыйМодуль, ДополнительныеПараметры);
	Иначе
		ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
			"ПолучитьСертификатыСКлючаПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
		
		ПодключитьИИнициализироватьВК(ОповещениеПослеПодключенияВК, НастройкаОбмена);
	КонецЕсли;
		
КонецПроцедуры

// Предлагает пользователю аутентифицироваться на токене и получить дополненный сертификат (асинхронно).
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое в результате выполнения процедуры:
//     * Результат - Структура, Неопределено, Строка - возможные значения: 
//                 - Структура - данные сертификата;
//                 - Неопределено - пользователь отказался от продолжения операции;
//                 - Строка - текст сообщения о возникшей ошибке
//  ПодключаемыйМодуль - Произвольный - внешняя компонента банка.
//  ПараметрыСоединения - Структура - параметры соединения. Содержит следующие элементы:
//     * ИдентификаторОрганизации - Строка - идентификатор организации из настройки обмена;
//     * БИК - Строка - БИК банка;
//     * КлючУникальности - Строка - идентифицирующая пользователя строка, например “<инн>_<бик>”.
//                                   Используется для сбора статистики подключений.
//
Процедура ПолучитьДанныеСертификатаСТокенаВК(Оповещение, ПодключаемыйМодуль, ПараметрыСоединения) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещенияПослеПолученияДанныхСертификата", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ПараметрыСоединения", ПараметрыСоединения);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПредъявитьДанныеАутентификацииПослеПолучения", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьДанныеАутентификацииНаТокенеВК(Оповещение, ПодключаемыйМодуль);
	
КонецПроцедуры

// Получает подробную информацию об ошибке, возникшей при работе с внешней компонентой (асинхронно).
// Также производит запись в журнал регистрации.
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры:
//     * ТекстОшибки - Строка - текст ошибки для вывода пользователю в виде сообщения;
//     * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//    ПодключаемыйМодуль - Произвольный - внешняя компонента банка;
//    ВидОперации - Строка - выполняемая операция;
//    ТекстСообщения - Строка - начальный текст сообщения пользователю.
//
Процедура ПолучитьИнформациюОбОшибкеВК(Оповещение, ПодключаемыйМодуль, ВидОперации, ТекстСообщения) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВидОперации", ВидОперации);
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("ТекстСообщения", ТекстСообщения);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияИнформацииОбОшибкеВК", ЭтотОбъект, ДополнительныеПараметры);
	ПодключаемыйМодуль.НачатьВызовДеталиОшибки(Оповещение);
	
КонецПроцедуры

Процедура ПослеПолученияИнформацииОбОшибкеВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДеталиОшибки = ДеСериализованныеДанные(РезультатВызова);

	Если ДеталиОшибки.Код = 0 Тогда
		ПодробныйТекстОшибки = ДеталиОшибки.Сообщение;
	Иначе
		ШаблонОшибки = ДополнительныеПараметры.ТекстСообщения + Символы.ПС + НСтр("ru = 'Код ошибки: ВК-%1
																					|%2'");
		ПодробныйТекстОшибки = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧГ="), ДеталиОшибки.Сообщение);
	КонецЕсли;
	
	ОбработатьОшибку(ДополнительныеПараметры.ВидОперации, ПодробныйТекстОшибки);
		
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ПодробныйТекстОшибки);
	
КонецПроцедуры

// Получает данные ключа через внешнюю компоненту (асинхронно).
//
// Параметры:
//  Обработчик - ОписаниеОповещения - Обработчик, вызываемый после получения данных ключа:
//     * Результат - Структура - данные сертификата банка с элементами:
//                     ** ИдентификаторХранилища - Строка - идентификатор хранилища (для данных банковского ключа может не заполняться)
//                     ** Псевдоним - Строка - псевдоним ключа ЭП 
//                     ** Отпечаток - Строка - уникальный идентификатор ключа ЭП, уникальность поддерживается в рамках всех сертификатов данного вида (для сертификатов x.509 вернуть отпечаток сертификата)
//                     ** СерийныйНомер - Строка - серийный номер ключа в hex
//                                                 (для сертификатов x.509 серийный номер сертификата)
//                     ** ИмяИздателя - Строка - имя издателя сертификата (для сертификатов x.509 поле CN)
//                     ** ВладелецФИО* - Строка - ФИО владельца ключа ЭП 
//                     ** ВладелецДолжность* - Строка - должность владельца ключа ЭП
//                     ** ДатаОкончания* - Дата - указывает последний день, когда можно использовать ключ ЭП
//                     Поля отмеченные символом * заполнены после дополнения данных ключа ЭП
//                - Строка - текст ошибки.
//     * ДополнительныеПараметры - Произвольный - контекст выполнения.
//  ПодключаемыйМодуль - Произвольный - внешняя компонента банка;
//  СертификатBase64 - Строка - сертификат в формате Base64.
//
Процедура ПолучитьДанныеКлючаЧерезВК(Обработчик, ПодключаемыйМодуль, СертификатBase64) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияДанныхСертификатаВК", Обработчик);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияДанныхСертификатаВК", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияДанныхСертификатаВК", ЭтотОбъект);
	ПодключаемыйМодуль.НачатьВызовДанныеКлючаЭП(Оповещение, СертификатBase64);

КонецПроцедуры

// Проверяет подписи сообщения обмена
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                    которая будет вызвана после завершения вызова метода со следующими параметрами:
//     * РезультатВызова - Строка, Булево - результат вызова метода.
//             - Строка - текст ошибки.
//             - Булево - если Истина, то операция выполнена успешно.
//     * ДополнительныеПараметры - Произвольный - значение,
//                                                которое было указано при создании объекта ОписаниеОповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение обмена, у которого необходимо проверить подписи.
//
Процедура ПроверитьПодписиСообщенияОбменаЧерезВК(Оповещение, НастройкаОбмена, СообщениеОбмена) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиПодписей", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ОповещениеПослеИнициализацииВК = Новый ОписаниеОповещения(
		"ОбеспечитьПодключениеАппаратногоУстройстваПослеИнициализацииВК", ЭтотОбъект, ДополнительныеПараметры);
	ПодключитьИИнициализироватьВК(ОповещениеПослеИнициализацииВК, НастройкаОбмена);
	
КонецПроцедуры

// Осуществляет проверку установки подписи при проверке сертификата
//
// Параметры:
//  РезультатВызова - Произвольный - внешняя компонента банка;
//                  - Строка - текст ошибки;
//                  - Неопределено - внешняя компонента не подключена, текст ошибки выведен пользователю.
//  ДополнительныеПараметры - Произвольный - контекст операции.
//
Процедура АутентификацияНаКлючеПослеПодключенияВК(РезультатВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова = Неопределено Тогда
		ОписаниеОшибки = НСтр("ru = 'Не удалось подключить внешний модуль'");
		ДополнительныеПараметры.Контекст.ОписаниеОшибки = ОписаниеОшибки;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, ДополнительныеПараметры.Контекст);
		Возврат;
	ИначеЕсли ТипЗнч(РезультатВызова) = Тип("Строка") Тогда
		ДополнительныеПараметры.Контекст.ОписаниеОшибки = РезультатВызова;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, ДополнительныеПараметры.Контекст);
		Возврат;
	КонецЕсли;
	
	ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками.ПодключаемыйМодуль", РезультатВызова);
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", РезультатВызова);
	
	РеквизитыСертификата = Новый Структура("ДанныеСертификата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(
		ДополнительныеПараметры.Сертификат, РеквизитыСертификата);
	ДополнительныеПараметры.Вставить("ДанныеСертификата", РеквизитыСертификата.ДанныеСертификата);

	Оповещение = Новый ОписаниеОповещения("ПроверитьУстановкуПодписиПослеАутентификацииНаТокене", ЭтотОбъект, ДополнительныеПараметры);
	АутентифицироватьсяНаЭлектронномКлючеЧерезВК(Оповещение, РезультатВызова, ДополнительныеПараметры.Сертификат,
		РеквизитыСертификата.ДанныеСертификата, ДополнительныеПараметры.Контекст.Пароль, Истина);
	
КонецПроцедуры

// Подтверждает платежные документы через внешний модуль. Внешнее соединение должно быть установлено.
//
// Параметры:
//  ПодключаемыйМодуль - Произвольный - внешняя компонента.
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после подтверждения документов
//  МассивСообщенийОбмена - Массив - электронные документы, которые требуется подтвердить:
//    ДокументСсылка.СообщениеОбменСБанками - сообщение обмена с банком.
//
Процедура ПодтвердитьПлатежныеДокументыЧерезВК(ПодключаемыйМодуль, Оповещение, МассивСообщенийОбмена) Экспорт
	
	Если МассивСообщенийОбмена.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Оповещение);
		Возврат;
	КонецЕсли;
	
	СообщениеОбмена = МассивСообщенийОбмена.Получить(0);
	МассивСообщенийОбмена.Удалить(0);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ИдентификаторСообщенияОбмена = ОбменСБанкамиСлужебныйВызовСервера.ИдентификаторЭлектронногоДокумента(
		СообщениеОбмена);
	
	ОповещениеВК = Новый ОписаниеОповещения("ПослеНачалаПодтвержденияПлатежногоПорученияЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуНачалаПодтвержденияПлатежногоПорученияЧерезВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовНачатьПодтверждениеПлатежногоПоручения(ОповещениеВК, ИдентификаторСообщенияОбмена);
	
КонецПроцедуры

// Формирует и подписывает запросы выписок для отправки через внешнюю компоненту.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры.
//    * Результат - Структура - результат выполнения, содержит следующие поля:
//         ** Успех - Булево - если Истина, то запросы успешно сформированы и подписаны
//         ** МассивСообщенийОбмена - Массив - в элементах ДокументСсылка.СообщениеОбменСБанками - сформированные и подписанные запросы выписок.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//  ПараметрыЗапроса - Структура - см. описание в ОбменСБанкамиКлиентСервер.ПараметрыПолученияВыпискиБанка().
//  ЭтоСинхронизация - Булево - если Истина, то запрос формируется при синхронизации.
//
Процедура ПодготовитьЗапросыВыписокДляОтправкиЧерезВК(Оповещение, НастройкаОбмена, ПараметрыЗапроса, ЭтоСинхронизация = Ложь) Экспорт

	НастройкиОбмена = Неопределено;
	ПараметрыФормирования = Новый Структура("ДатаОкончания, ДатаНачала, НомерСчета");
	ЗаполнитьЗначенияСвойств(ПараметрыФормирования, ПараметрыЗапроса);
	МассивЗапросов = ОбменСБанкамиСлужебныйВызовСервера.ЗапросыВыписок(
		НастройкаОбмена, ПараметрыФормирования, Новый Массив, НастройкиОбмена, ЭтоСинхронизация);
	
	Если НЕ МассивЗапросов.Количество() ИЛИ НастройкиОбмена = Неопределено Тогда
		СтруктураВозврата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Оповещение, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("МассивСообщенийОбменаКОтправке", МассивЗапросов);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодготовкиЗапросовВыписки", Оповещение);
	
	Если НастройкиОбмена.Подписывать Тогда
		Если НЕ ЗначениеЗаполнено(НастройкиОбмена.ДоступныеСертификаты)
				ИЛИ НЕ НастройкиОбмена.СертификатДоступен Тогда
			ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат для подписи документа Запрос выписки'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			СтруктураВозврата = Новый Структура("Успех", Ложь);
			ВыполнитьОбработкуОповещения(Оповещение, СтруктураВозврата);
			Возврат;
		КонецЕсли;
		
		СоответствиеСертификатов = Новый Соответствие;
		
		Для Каждого СертификатСсылка Из НастройкиОбмена.ДоступныеСертификаты Цикл
			СоответствиеСертификатов.Вставить(СертификатСсылка, Новый Структура("ПарольПолучен", Ложь));
		КонецЦикла;
		Если ЕстьСертификатССохраненнымПаролем(СоответствиеСертификатов) Тогда
			Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
				ДанныеАутентификации = Новый Структура;
				ДанныеАутентификации.Вставить("ВыбранныйСертификат", КлючЗначение.Ключ);
				ДанныеАутентификации.Вставить("ПарольСертификата", КлючЗначение.Значение.ПарольСертификата);
				ПодписатьЗапросыВыписокПослеВводаПароляЧерезВК(ДанныеАутентификации, ДополнительныеПараметры);
				Прервать;
			КонецЦикла;
		Иначе
			Оповещение = Новый ОписаниеОповещения(
				"ПодписатьЗапросыВыписокПослеВводаПароляЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
			ВидОперации = НСтр("ru = 'Подписание электронных документов'");
			ПолучитьПарольКСертификату(Оповещение, СоответствиеСертификатов, ВидОперации, МассивЗапросов);
		КонецЕсли;
	Иначе
		СтруктураВозврата = Новый Структура("Успех, МассивСообщенийОбмена", Истина, МассивЗапросов);
		ВыполнитьОбработкуОповещения(Оповещение, СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Сбербанк

Процедура СинхронизироватьСбербанк(Параметры)

	Если Параметры.РеквизитыНастройкиОбмена.ИспользуетсяКриптография Тогда
		Параметры.Вставить("ИмяВнешнегоМодуля", Параметры.РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
		Оповещение = Новый ОписаниеОповещения("НачатьСинхронизациюПослеАутентификацииСбербанк", ЭтотОбъект, Параметры);
		УстановитьСоединениеИАутентифицироватьсяНаСервереЧерезТокенСбербанк(Оповещение, Параметры.НастройкаОбмена);
	Иначе
		РезультатОтправки = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияСинхронизацииСбербанк(
			Параметры.НастройкаОбмена);
		Параметры.Вставить("АутентификацияПроизводилась", Ложь);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Параметры.ВыводитьОкноОжидания;
		Оповещение = Новый ОписаниеОповещения("ПослеСинхронизацииСбербанк", ЭтотОбъект, Параметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатОтправки, Оповещение, ПараметрыОжидания);
		ВыполнитьОбработкуОповещения(
			Параметры.ОповещениеЗапускаДлительнойОперации, РезультатОтправки.ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодтвердитьСледующийДокументСбербанк(Оповещение, НастройкаОбмена, МассивСообщенийОбмена, Счетчик = 0)
	
	Если Не МассивСообщенийОбмена.Количество() Тогда // подтверждены все документы
		ВыполнитьОбработкуОповещения(Оповещение, Счетчик);
		Возврат;
	КонецЕсли;
	
	СообщениеОбмена = МассивСообщенийОбмена.Получить(0);
	МассивСообщенийОбмена.Удалить(0);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
	ДополнительныеПараметры.Вставить("АутентификацияПроизводилась", Ложь);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("Счетчик", Счетчик);
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияГенерацииSMSСбербанк(НастройкаОбмена, СообщениеОбмена);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	Оповещение = Новый ОписаниеОповещения("ПослеГенерацииSMSСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

// Производит аутентификацию на сервере сбербанка по сертификату. Канал уже должен быть создан.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - вызывается после выполнения процедуры
//      * Результат - Булево - результат аутентификации. Возможные значения:
//          - Истина - аутентификация выполнена
//          - Ложь - при аутентификации произошла ошибка
//  ИдентификаторВК - Строка - идентификатор внешней компоненты банка;
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//
Процедура АутентифицироватьсяПоСертификатуСбербанк(Оповещение, ИдентификаторВК, НастройкаОбмена = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификации", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ИдентификаторВК", ИдентификаторВК);
	
	ОповещениеПослеПолученияДанныхСертификатаСбербанка = Новый ОписаниеОповещения(
		"ПослеПолученияДанныхСертификатаСбербанка", ЭтотОбъект, ДополнительныеПараметры);
	ОпределитьСертификатПодписиСбербанк(ОповещениеПослеПолученияДанныхСертификатаСбербанка, ИдентификаторВК, НастройкаОбмена);

КонецПроцедуры

Процедура ПослеЗакрытияФормыСинхронизацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Результат);
	КонецЕсли;
	
	Оповестить("ОбновитьСостояниеОбменСБанками");

КонецПроцедуры

Процедура ОтправитьДанныеПослеБазовойАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		РезультатЗадания = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияОтправкиДанныхВСбербанк(
			ДополнительныеПараметры.НаименованиеФоновогоЗадания, ДополнительныеПараметры.НазваниеМетода,
			ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.СсылкаНаОбъект);
		Если РезультатЗадания.Статус = "Выполняется" Тогда
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
			Оповещение = Новый ОписаниеОповещения("ПослеОтправкиДанныхВСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
		Иначе
			ПослеОтправкиДанныхВСбербанк(РезультатЗадания, ДополнительныеПараметры);
		КонецЕсли;
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(ВидОперации, "GA==", ДополнительныеПараметры.НастройкаОбмена);
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьСтатусЗапросаПослеПодключенияВК1ССбербанк(ПодключаемыйМодуль1С, ДополнительныеПараметры) Экспорт
	
	Если ПодключаемыйМодуль1С = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияСтатусаЗапроса", ДополнительныеПараметры.Оповещение);
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияСтатусаЗапросаСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияСтатусаЗапросаСбербанк", ЭтотОбъект);
		
	ИдентификаторСессии = ЗначениеИзКешаСбербанк("ИдентификаторСессии");
	Ответ = "";
	
	ПодключаемыйМодуль1С.НачатьВызовПолучитьСтатусЗапросаSRP(Оповещение, ДополнительныеПараметры.Тикет,
		ДополнительныеПараметры.ИдентификаторОрганизации, ИдентификаторСессии, Ответ);

КонецПроцедуры

Процедура ПослеПолученияСтатусаЗапросаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		ВидОперации = НСтр("ru = 'Получение статуса запроса'");
		ШаблонСообщения = НСтр("ru = 'При получении данных с сервера банка произошла ошибка.'");
		ВывестиИнформацияОбОшибкеСбербанк(
			РезультатВызова, ВидОперации, ШаблонСообщения, ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСтатусаЗапроса);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСтатусаЗапроса, ПараметрыВызова[3]);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСтатусаЗапросаСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	
	ВидОперации = НСтр("ru = 'Получение статуса запроса'");
	ШаблонОшибки = НСтр("ru = 'При получении данных с сервера банка произошла ошибка.
							|%1'");

	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСтатусаЗапроса);
	
КонецПроцедуры

// Получает параметры фрод мониторинга с использование внешней компоненты банка.
// Внешняя компонента должна быть подключена до вызова метода.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после получения параметров для фрод-мониторинга
//  ПодключаемыйМодуль - Произвольный - подключаемый модуль банка.
//
Процедура ПолучитьПараметрыФродМониторингаСбербанк(Оповещение, ПодключаемыйМодуль)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияФродПараметров", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеПолученияАдресаКомпьютера = Новый ОписаниеОповещения(
		"ПолучитьПараметрыПКПослеПолученияАдресаКомпьютераСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	IP = ""; MAC = "";
	ПодключаемыйМодуль.НачатьВызовПолучитьIP_MAC(ОповещениеПослеПолученияАдресаКомпьютера, IP, MAC);
	
КонецПроцедуры

Процедура ПослеПодключенияВК1СДляСбербанка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Подключено Тогда
		УстановитьТаймаут(ДополнительныеПараметры.ОповещениеПослеПодключенияВК1СДляСбербанка, Результат.ПодключаемыйМодуль);
	Иначе
		Если ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияВК1СДляСбербанка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьВерсиюВКСбербанк(Оповещение, ИмяВнешнегоМодуля)
	
	ИнформацияОВК = ВнешниеКомпонентыВызовСервера.ИнформацияОКомпоненте(ИмяВнешнегоМодуля);
	
	Если НЕ ИнформацияОВК.Существует Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ИнформацияОВК.ОписаниеОшибки);
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
	ИначеЕсли НЕ ПоддерживаетсяВерсияКомпонентыСбербанк(ИнформацияОВК.Версия) Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьТаймаут(Оповещение, ПодключаемыйМодуль)
	
	ДополнительныеПараметры = Новый Структура("Оповещение, ПодключаемыйМодуль", Оповещение, ПодключаемыйМодуль);
	Оповещение = Новый ОписаниеОповещения("ПослеУстановкиТаймаута", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключаемыйМодуль.НачатьВызовУстановитьТаймаут(Оповещение, 100000);
	
КонецПроцедуры

Процедура ПослеУстановкиТаймаута(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура УстановитьВиртуальныйКаналПослеАутентификацииНаАппаратномУстройствеСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаСервере, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"АутентификацияНаСервереПослеУстановкиКаналаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
	УстановитьКаналНаТокенеСбербанк(Оповещение, ДополнительныеПараметры.ИмяМодуля, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ПровестиАутентификациюПослеПолученияФродПараметровСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	Кэш = Новый Структура(Результат);
	
	Кэш.Вставить("НомерКонтейнера", ЗначениеИзКешаСбербанк("НомерКонтейнера"));
	
	СтрокаФрод = ОбменСБанкамиСлужебныйВызовСервера.Фрод(Кэш);
	
	ЗакешироватьПараметрСбербанка("ФродПараметры", Кэш);
	
	Если НЕ ЗначениеЗаполнено(СтрокаФрод) Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("СтрокаФрод", СтрокаФрод);
	
	Оповещение = Новый ОписаниеОповещения("ПослеАутентификацииНаСервереСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуАутентификацииСбербанк", ЭтотОбъект);
	
	ИдентификаторСессии = ""; КодВозврата = "";
	
	ПодписьСоли = СтрЗаменить(СтрЗаменить(ДополнительныеПараметры.ПодписьСоли, Символы.ПС, ""), Символы.ВК, "");
	
	ДополнительныеПараметры.ПодключаемыйМодуль1С.НачатьВызовАутентификация(Оповещение,
		ДополнительныеПараметры.ИдентификаторСессии, ПодписьСоли, ДополнительныеПараметры.СтрокаФрод,
		ИдентификаторСессии, КодВозврата);
	
КонецПроцедуры

Процедура ОбработатьОшибкуАутентификацииСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	
	ВидОперации = НСтр("ru = 'Аутентификация на сервисе банка'");
	ШаблонОшибки = НСтр("ru = 'При аутентификации на сервисе банка произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
	
КонецПроцедуры

Процедура ПослеАутентификацииНаСервереСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	НастройкаОбмена = ДополнительныеПараметры.НастройкаОбмена;
	
	Если РезультатВызова <> 0 Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервисе банка'");
		ШаблонСообщения = НСтр("ru = 'При аутентификации на сервисе банка произошла ошибка.
								|Код ошибки: %1'");
		ТекстОшибки = СтрШаблон(ШаблонСообщения, РезультатВызова);
		ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстОшибки, НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;

	ИдентификаторСессииДвоичныеДанные = Base64Значение(ПараметрыВызова[3]);
	КодВозврата = ПараметрыВызова[4];

	Если КодВозврата <> "AA==" Тогда
		
		СписокКодовСбербанк = СписокОшибокАутентификацииПоСертификатуСбербанк();
		
		ТекОшибка = СписокКодовСбербанк.Получить(КодВозврата);
		
		Если ТекОшибка = Неопределено Тогда
			ШаблонОшибки = НСтр("ru = 'Ошибка аутентификации.
									|Код: %1'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, КодВозврата);
		Иначе
			ШаблонОшибки = НСтр("ru = 'Ошибка аутентификации.
									|Код: %1
									|Описание: %2'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, ТекОшибка.Код, ТекОшибка.Описание);
		КонецЕсли;
			
		ВидОперации = НСтр("ru = 'Аутентификация'");
		
		ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстОшибки, НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	ИдентификаторСессии = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(
		ИдентификаторСессииДвоичныеДанные);
	ЗакешироватьПараметрСбербанка("ИдентификаторСессии", ИдентификаторСессии);
		
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Истина);
	
КонецПроцедуры

Процедура ПолучитьПараметрыФродМониторингаПослеПодписиСолиСбербанк(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда // возврат из формы сенсорного токена
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	Если Не Результат.Успех Тогда
		Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ПодписьСоли", Результат.ЭП);
	
	Оповещение = Новый ОписаниеОповещения("ПровестиАутентификациюПослеПодключенияВКСбербанк", ЭтотОбъект, Параметры);
	ПодключитьКомпоненту(Оповещение, Параметры.ИдентификаторВК);
	
КонецПроцедуры

Процедура ПолучитьПараметрыПКПослеПолученияАдресаКомпьютераСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ПараметрыФродМониторинга = Новый Структура;
	ПараметрыФродМониторинга.Вставить("IP", ПараметрыВызова[0]);
	ПараметрыФродМониторинга.Вставить("MAC", ПараметрыВызова[1]);
	ДополнительныеПараметры.Вставить("ПараметрыФродМониторинга", ПараметрыФродМониторинга);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьПараметрыDevicePrintПослеПолученияПараметровПК", ЭтотОбъект, ДополнительныеПараметры);
	ИдентификаторКомпьютера = ""; ИдентификаторПроцессора = ""; ИдентификаторBIOS = ""; ИдентификаторДиска = "";
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПолучитьПараметрыПК(
		Оповещение, ИдентификаторКомпьютера, ИдентификаторПроцессора, ИдентификаторBIOS, ИдентификаторДиска);
	
КонецПроцедуры

Процедура ПолучитьПараметрыDevicePrintПослеПолученияПараметровПК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ИдентификаторКомпьютера", ПараметрыВызова.Получить(0));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ИдентификаторПроцессора", ПараметрыВызова.Получить(1));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ИдентификаторBIOS", ПараметрыВызова.Получить(2));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ИдентификаторДиска", ПараметрыВызова.Получить(3));
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияПараметровDevicePrint", ЭтотОбъект, ДополнительныеПараметры);
	
	РазрядностьОС = ""; РазрядностьПроцессора = ""; ГлубинаЦветаМонитора = ""; ШиринаМонитора = ""; ВысотаМонитора = "";
	ПлотностьПикселяX = ""; ПлотностьПикселяY = ""; ШиринаРабочегоСтола = ""; СмещениеЧасовогоПояса = "";
	УстановленныеКомпоненты = ""; Онлайн = Ложь; СглаживаниеШрифтов = Ложь; ЯзыкСистемы = ""; ЯзыкПользователя = "";
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПолучитьПараметрыDevicePrint(Оповещение, РазрядностьОС,
		РазрядностьПроцессора, ГлубинаЦветаМонитора, ШиринаМонитора, ВысотаМонитора, ПлотностьПикселяX, ПлотностьПикселяY,
		ШиринаРабочегоСтола, СмещениеЧасовогоПояса, УстановленныеКомпоненты, Онлайн, СглаживаниеШрифтов, ЯзыкСистемы,
		ЯзыкПользователя);
	
КонецПроцедуры

Процедура ОтправитьТестовыйЗапросПослеУстановкиСоединенияСбербанк(Результат, Параметры) Экспорт
	
	Если Не Результат Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиТестовогоЗапросаСбербанк", ЭтотОбъект, Параметры);
	
	ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, Параметры.ТестоваяСтрока, Параметры.НастройкаОбмена);

КонецПроцедуры

Процедура ПослеОтправкиТестовогоЗапросаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, ЗначениеЗаполнено(Результат));
	
КонецПроцедуры

Процедура ОтправитьЗапросНовыхДокументовПослеПодписиСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиЗапроса, Результат);
		Возврат;
	КонецЕсли;
	
	ДанныеПодписи = Новый Структура("ЭП, СертификатПодписи", Результат.ЭП, ДополнительныеПараметры.СертификатПодписи);
	
	Попытка
		ТекстЗапроса = ОбменСБанкамиСлужебныйВызовСервера.ТекстЗапросаНочнойВыписки(ДополнительныеПараметры.НастройкаОбмена,
			ДополнительныеПараметры.ИдентификаторЗапроса, ДополнительныеПараметры.ИдентификаторОрганизации, ДанныеПодписи);
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВидОперации = НСтр("ru = 'Формирование запроса новых документов'");
		ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстСообщения, ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиЗапроса, Результат);
		Возврат;
	КонецПопытки;
	ДополнительныеПараметры.Вставить("Результат", Результат);
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаНовыхДокументовСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, ТекстЗапроса, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ПослеОтправкиЗапросаНовыхДокументовСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.Результат.Успех = ЗначениеЗаполнено(Ответ);
	ДополнительныеПараметры.Результат.Вставить("Тикет", Ответ);
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеОтправкиЗапроса, ДополнительныеПараметры.Результат);
	
КонецПроцедуры

Процедура ПослеОтправкиЗапросовСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервисе банка'");
		ШаблонСообщения = НСтр("ru = 'При аутентификации на сервисе банка произошла ошибка.'");
		ВывестиИнформацияОбОшибкеСбербанк(
			РезультатВызова, ВидОперации, ШаблонСообщения, ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиЗапросов);
		Возврат;
	КонецЕсли;
		
	Ответ = ПараметрыВызова[2];
		
	Если Сред(Ответ, 1, 23) = "00000000-0000-0000-0000" Тогда
		ТекстСообщения = ОбменСБанкамиКлиентСервер.ТекстСообщенияСбербанк(Ответ);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиЗапросов);
		Возврат;
	Иначе
		Попытка
			//@skip-warning
			ИдентификаторДокумента = Новый УникальныйИдентификатор(Ответ);
		Исключение
			ТекстСообщения = "";
			Попытка
				ОбменСБанкамиСлужебныйВызовСервера.ОбработатьИзвещениеСОшибкойСбербанк(
					Ответ, ТекстСообщения, ДополнительныеПараметры.НастройкаОбмена);
			Исключение
				ВидОперации = НСтр("ru = 'Чтение ответа банка'");
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбработатьОшибку(ВидОперации, ТекстОшибки, , ДополнительныеПараметры.НастройкаОбмена);
			КонецПопытки;
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиЗапросов);
			Возврат;
		КонецПопытки;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиЗапросов, Ответ);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьОшибкуОтправкиЗапросовСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	
	ВидОперации = НСтр("ru = 'Аутентификация на сервисе банка'");
	ШаблонОшибки = НСтр("ru = 'При аутентификации на сервисе банка произошла ошибка.
						|%1'");
	ТекстОшибки = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстОшибки);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиЗапросов);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхСертификатаСбербанка(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ДанныеСертификата", Результат);
	
	Оповещение = Новый ОписаниеОповещения(
		"ВключитьЖурналированиеПослеПодключенияВК1СДляСбербанка", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьВнешнююКомпоненту1СДляСбербанка(Оповещение);
	
КонецПроцедуры

Процедура ПослеПредварительнойАутентификацииПоСертификатуСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова = 0 Тогда
		ПодписатьСольСбербанк(ПараметрыВызова, ДополнительныеПараметры);
	Иначе
		// Вторая попытка аутентификации
		Оповещение = Новый ОписаниеОповещения("ПослеВторойПредварительнойАутентификацииПоСертификатуСбербанк", ЭтотОбъект,
			ДополнительныеПараметры, "ОбработатьОшибкуПредварительнойАутентификацииПоСертификатуСбербанк", ЭтотОбъект);
		Соль = ""; ИдентификаторСессииBase64 = ""; КодВозврата = "";
		ДополнительныеПараметры.ПодключаемыйМодуль1С.НачатьВызовПреАутентификация(Оповещение,
			ДополнительныеПараметры.ИнформацияОСертификате.issuer, ДополнительныеПараметры.СерийныйНомерСертификата, Соль,
			ИдентификаторСессииBase64, КодВозврата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВторойПредварительнойАутентификацииПоСертификатуСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова = 0 Тогда
		ПодписатьСольСбербанк(ПараметрыВызова, ДополнительныеПараметры);
	Иначе
		ВидОперации = НСтр("ru = 'Запрос аутентификации'");
		ШаблонСообщения = НСтр("ru = 'При запросе аутентификации произошла ошибка.'");
		ВывестиИнформацияОбОшибкеСбербанк(
			РезультатВызова, ВидОперации, ШаблонСообщения, ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодписатьСольСбербанк(ПараметрыВызова, ДополнительныеПараметры)
	
	Соль = ПараметрыВызова[2];
	ИдентификаторСессииДвоичныеДанные = Base64Значение(ПараметрыВызова[3]);
	КодВозврата = ПараметрыВызова[4];
	
	Если КодВозврата <> "AA==" Тогда
		
		СписокКодовСбербанк = СписокОшибокАутентификацииПоСертификатуСбербанк();
		ТекОшибка = СписокКодовСбербанк.Получить(КодВозврата);
		
		ШаблонОшибки = НСтр("ru = 'Ошибка при запросе аутентификации.
									|Код: %1'");
		Если ТекОшибка = Неопределено Тогда
			ТекстОшибки = СтрШаблон(ШаблонОшибки, КодВозврата);
		Иначе
			ШаблонОшибки = ШаблонОшибки + Символы.ПС + НСтр("ru = 'Описание: %2'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, ТекОшибка.Код, ТекОшибка.Описание);
		КонецЕсли;
			
		ВидОперации = НСтр("ru = 'Аутентификация'");
		
		ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстОшибки, ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	ИдентификаторСессии = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(
		ИдентификаторСессииДвоичныеДанные);
	ДополнительныеПараметры.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПолучитьПараметрыФродМониторингаПослеПодписиСолиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	Представление = НСтр("ru = 'Данные аутентификации'");
	
	ПодписатьДанныеСбербанк(ОписаниеОповещения, ДополнительныеПараметры.ИдентификаторВК, Соль,
		ДополнительныеПараметры.ДанныеСертификата.ИдентификаторСертификата, Представление,
		ДополнительныеПараметры.ДанныеСертификата.ДвоичныеДанныеСертификата);
	
КонецПроцедуры

Процедура ПолучитьТекстОшибкиПослеПодключенияВК1ССбербанк(ПодключаемыйМодуль1С, ДополнительныеПараметры) Экспорт
	
	Если ПодключаемыйМодуль1С = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияТекстаОшибкиСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ПослеОшибкиПолученияТекстаОшибкиСбербанк", ЭтотОбъект);
	Описание = "";
	ПодключаемыйМодуль1С.НачатьВызовПолучитьОшибку(Оповещение, Описание);
	
КонецПроцедуры

Процедура ПослеПолученияТекстаОшибкиСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ШаблонСообщения = ДополнительныеПараметры.ШаблонСообщения + Символы.ПС
					+ НСтр("ru = 'Код ошибки: %1
								|Текст ошибки: %2'");
	ТекстОшибки = СтрШаблон(ШаблонСообщения, ДополнительныеПараметры.КодОшибки, ПараметрыВызова[0]);
	ОбработатьОшибку(
		ДополнительныеПараметры.ВидОперации, ТекстОшибки, ТекстОшибки, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ПослеОшибкиПолученияТекстаОшибкиСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Истина;
	ШаблонСообщения = ДополнительныеПараметры.ШаблонСообщения + Символы.ПС
					+ НСтр("ru = 'Код ошибки: %1'");
	ТекстОшибки = СтрШаблон(ШаблонСообщения, ДополнительныеПараметры.КодОшибки);
	ОбработатьОшибку(
		ДополнительныеПараметры.ВидОперации, ТекстОшибки, ТекстОшибки, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПредварительнойАутентификацииПоСертификатуСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	
	ВидОперации = НСтр("ru = 'Запрос аутентификации'");
	ШаблонОшибки = НСтр("ru = 'При запросе аутентификации произошла ошибка.
						|%1'");
	
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Отправка запроса в банк.'");
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
	
КонецПроцедуры

Процедура АутентификацияНаСервереПослеУстановкиКаналаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаСервере, Ложь);
		Возврат;
	КонецЕсли;
	
	АутентифицироватьсяПоСертификатуСбербанк(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаСервере,
		ДополнительныеПараметры.ИмяМодуля, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

// Определяет номер бизнес системы на банковском токене
// 
// Параметры:
//    Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//        * НомерБизнесСистемы - Строка, Неопределено - номер бизнес системы на токене, или Неопределено в случае ошибки
//        * ДополнительныеПараметры - Произвольный - контекст выполнения
//    ПодключаемыйМодуль - COMОбъект - внешняя компонента банка
//    НазваниеБизнесСистемы - Строка - название бизнес-системы.
//
Процедура ОпределитьНомерБизнесСистемыСбербанк(Оповещение, ПодключаемыйМодуль, НазваниеБизнесСистемы)
	
	Параметры = Новый Структура();
	Параметры.Вставить("ОповещенияПослеОпределенияНомераБизнесСистемы", Оповещение);
	Параметры.Вставить("НазваниеБизнесСистемы", НазваниеБизнесСистемы);
	ОповещениеПослеПолученияНомераБизнесСистемы = Новый ОписаниеОповещения(
		"НайтиБизнесСистемуПослеПолученияСпискаСТокенаСбербанк", ЭтотОбъект, Параметры);
	
	ПолучитьБизнесСистемыНаТокенеСбербанк(ОповещениеПослеПолученияНомераБизнесСистемы, ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ОпределитьНеобходимостьПовторнойАутентификацииНаТокенеПослеПолученияСпискаБизнесСистемСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			ДополнительныеПараметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
		АутентифицироватьсяНаТокенеСбербанка(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене,
			РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля, ДополнительныеПараметры.НастройкаОбмена, Истина);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура НайтиБизнесСистемуПослеПолученияСпискаСТокенаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеОпределенияНомераБизнесСистемы, Неопределено);
		Возврат;
	КонецЕсли;
	
	БизнесСистемы = Результат.БизнесСистемы;
	НазваниеБизнесСистемы = ДополнительныеПараметры.НазваниеБизнесСистемы;
	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(БизнесСистемы, Символы.ПС);
	Для Каждого Строка Из МассивСтрок Цикл
		Если СтрНайти(ВРег(Строка), ВРег(НазваниеБизнесСистемы)) > 0 Тогда
			ПозПервойКавычки = СтрНайти(Строка, """");
			БизнесСистемаСтрокой = Сред(Строка, ПозПервойКавычки + 1);
			ПозВторойКавычки = СтрНайти(БизнесСистемаСтрокой, """");
			БизнесСистемаСтрокой = Сред(БизнесСистемаСтрокой, 1 , ПозВторойКавычки - 1);
			НомерБизнесСистемы = ОбменСБанкамиКлиентСервер.ЧислоИзСтроки(БизнесСистемаСтрокой);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НомерБизнесСистемы = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не найдена бизнес система на банковском ключе.
									|Необходимо проверить работу TLS VPN Key.'");
		Операция = НСтр("ru = 'Поиск бизнес системы на электронном ключе.'");
		ШаблонОшибки = НСтр("ru = 'На электронном ключе не найдена бизнес-система %1.
							|Содержимое списка возврата: %2'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, НазваниеБизнесСистемы, БизнесСистемы);
		ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеОпределенияНомераБизнесСистемы, Неопределено);
		Возврат;
	КонецЕсли;
	
	БизнесСистемаСтрокой = Строка(НомерБизнесСистемы);
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещенияПослеОпределенияНомераБизнесСистемы, БизнесСистемаСтрокой);
	
КонецПроцедуры

// Производит аутентификацию на токене.
//
// Параметры:
//    ОповещениеПослеАутентификацииНаТокене - ОписаниеОповещения - оповещение после выполнения процедуры:
//       * Результат - Булево - если Истина, то аутентификация успешно выполнена, иначе Ложь;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком (может быть не заполнена);
//    ПодключаемыйМодуль - COMОбъект - внешняя компонента банка;
//    НомерКонтейнера - Число - номер бизнес системы. Для экранного токена значение не заполнено;
//    ПинКод - Строка - секретный код. Для экранного токена значение не заполнено;
//    ТипУстройства - Строка - тип токена (UNKNOWN - Неизвестно, ORDINARY - Обычный, BTN - Сенсорный(с кнопкой), SCR - Экранный).
//
Процедура ЗарегистрироватьсяНаТокенеСбербанка(ОповещениеПослеАутентификацииНаТокене, НастройкаОбмена, ПодключаемыйМодуль, НомерКонтейнера, ПинКод, ТипУстройства)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификацииНаТокене", ОповещениеПослеАутентификацииНаТокене);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("НомерКонтейнера", НомерКонтейнера);
	ДополнительныеПараметры.Вставить("ПинКод", ПинКод);
	ДополнительныеПараметры.Вставить("ТипУстройства", ТипУстройства);

	// Временно отключено, т.к. на сенсорном токене приводит к падению.
	ПредъявитьПинПослеЗавершенияСессии(Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПредъявитьПинПослеЗавершенияСессии(Результат, ДополнительныеПараметры)
	
	Оповещение = Новый ОписаниеОповещения("ПослеПредъявленияПинаНаТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПослеПредъявленияПинаНаТокенеСбербанк", ЭтотОбъект);
	Если ДополнительныеПараметры.ТипУстройства = "SCR" Тогда // Экранный токен
		ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПредъявитьПинЭкр(Оповещение);
	Иначе
		ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПредъявитьПин(
			Оповещение, ДополнительныеПараметры.НомерКонтейнера, ДополнительныеПараметры.ПинКод);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПослеПредъявленияПинаНаТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При аутентификации на аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Предъявление кода доступа на аппаратном устройстве'");
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	ОчиститьДанныеАвторизацииСбербанк();
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
	
КонецПроцедуры

Процедура ПослеПодписанияТестовойСтрокиСбербанк(Результат, Параметры) Экспорт
	
	Если Результат.Успех Тогда
		ЗакешироватьПараметрСбербанка("ТестоваяПодпись", Результат.ЭП);
	ИначеЕсли ПустаяСтрока(Результат.ТекстОшибки) Тогда  // Пользователь закрыл окно
		Возврат;
	Иначе
		Параметры.Контекст.ОписаниеОшибки = Результат.ТекстОшибки;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Параметры.Контекст);
	
КонецПроцедуры

Процедура ПолучитьИнформациюОТокенеСбербанк(Оповещение, ПодключаемыйМодуль)
	
	ДополнительныеПараметры = Новый Структура("Оповещение", Оповещение);
	ОповещениеПослеПолученияИнформацииОТокене = Новый ОписаниеОповещения("ПослеПолученияИнформацииОТокенеСбербанк",
		ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПолученияИнформацииОТокенеСбербанк", ЭтотОбъект);
	
	ВерсияПрошивки = 0;
	ТипУстройства = "";

	Попытка
		ПодключаемыйМодуль.НачатьВызовПолучитьПараметрыVPNKeyTLS(
			ОповещениеПослеПолученияИнформацииОТокене, ВерсияПрошивки, ТипУстройства);
	Исключение
		Операция = НСтр("ru = 'Получение информации об аппаратном устройстве.'");
		КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонСообщения = НСтр("ru = 'При получении информации об аппаратном устройстве произошла ошибка:
								|%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, КраткоеПредставлениеОшибки);
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ТекстОшибки);
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
		ВыполнитьОбработкуОповещения(Оповещение, Результат);
	КонецПопытки
	
КонецПроцедуры

Процедура ПослеПолученияИнформацииОТокенеСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если РезультатВызова <> 0 Тогда
		Операция = НСтр("ru = 'Получение информации об аппаратном устройстве.'");
		ТекстОшибки = НСтр("ru = 'При получении информации об аппаратном устройстве произошла ошибка.
							|Внешний модуль VpnKey-TLS вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		ОбработатьОшибку(Операция, ТекстОшибки);
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстОшибки);
	Иначе
		Результат = Новый Структура("Успех, ТипУстройства", Истина, ПараметрыВызова.Получить(1));
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияИнформацииОТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении информации об аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение информации об аппаратном устройстве.'");
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	
КонецПроцедуры

Процедура ПодписатьДанныеПослеПолученияИнформацииОТокенеСбербанк(Результат, ДополнительныеПараметры) Экспорт

	Если НЕ Результат.Успех Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ТипУстройства", Результат.ТипУстройства);
		
	СвойстваСертификата = ОбменСБанкамиСлужебныйВызовСервера.СвойстваСертификатаКриптографии(
		ДополнительныеПараметры.ДанныеСертификата);
		
	ДополнительныеПараметры.Вставить("АлгоритмПубличногоКлюча", СвойстваСертификата.АлгоритмПубличногоКлюча);
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатАсинхроннойПодписиСбербанк", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПодписиСбербанк", ЭтотОбъект);
	
	Если СвойстваСертификата.АлгоритмПубличногоКлюча = "GOST R 34.10-2012-256" Тогда
		ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПодписатьДанныеЧерезVPNKeyTLSФорматPKCS7(
			Оповещение, ДополнительныеПараметры.СтрокаПодписи, ДополнительныеПараметры.ИдентификаторСертификата, "1", "0", "1");
	Иначе
		ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПодписатьДанныеЧерезVPNKeyTLSАсинхр(
			Оповещение, ДополнительныеПараметры.СтрокаПодписи, ДополнительныеПараметры.ИдентификаторСертификата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьРезультатАсинхроннойПодписиСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		Операция = НСтр("ru = 'Подписание данных'");
		ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка
							|Внешний модуль Сбербанка при подписании вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		ОбработатьОшибку(Операция, ТекстОшибки);
		Результат = Новый Структура;
		Результат.Вставить("Успех", Ложь);
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, Результат);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = ДополнительныеПараметры.ПодключаемыйМодуль;
	ТипУстройства = ДополнительныеПараметры.ТипУстройства;
	Представление = ДополнительныеПараметры.Представление;
	
	Если ТипУстройства = "UNKNOWN" ИЛИ ТипУстройства = "ORDINARY" Тогда
		ПолучитьПодписанныеДанныеСбербанк(ДополнительныеПараметры.ОповещениеПослеПодписи, ПодключаемыйМодуль,
			ДополнительныеПараметры.АлгоритмПубличногоКлюча);
	Иначе // сенсорный или экранный
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ОбъектПодписи", Представление);
		ПараметрыФормы.Вставить("ТипУстройства", ТипУстройства);
		ПараметрыФормы.Вставить("ИмяМодуля", ДополнительныеПараметры.ИдентификаторВК);
		ПараметрыФормы.Вставить("АлгоритмПубличногоКлюча", ДополнительныеПараметры.АлгоритмПубличногоКлюча);
		
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ОжиданиеПодписиСбербанк", ПараметрыФормы, , , , ,
			ДополнительныеПараметры.ОповещениеПослеПодписи, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьОшибкуПодписиСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При подписи данных произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Подпись данных.'");
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ТекстОшибки", ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, Результат);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхСертификатаСТокенаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если РезультатВызова <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'При получении данных сертификата произошла ошибка.'");
		ТекстОшибки = НСтр("ru = 'Внешний модуль VpnKey-TLS при получении сертификата вернул код ошибки'") + РезультатВызова;
		Операция = НСтр("ru = 'Получение сертификата криптографии.'");
		ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификата, Неопределено);
		Возврат;
	КонецЕсли;
	
	СертификатBase64 = ПараметрыВызова[1];
	
	СертификатBase64 = СтрЗаменить(СертификатBase64, "-----BEGIN CERTIFICATE-----" + Символы.ПС, "");
	СертификатBase64 = СтрЗаменить(СертификатBase64, "-----BEGIN CERTIFICATE-----" + Символы.ВК + Символы.ПС, "");
	СертификатBase64 = СтрЗаменить(СертификатBase64, Символы.ПС + "-----END CERTIFICATE-----", "");
	
	ДвоичныеДанныеСертификата = Base64Значение(СертификатBase64);
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификата, ДвоичныеДанныеСертификата);

КонецПроцедуры

Процедура ОбработатьОшибкуПослеПолученияДанныхСертификатаСТокенаСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт

	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении сертификата с аппаратного устройства произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение данных сертификата с аппаратного устройства.'");
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	ОчиститьДанныеАвторизацииСбербанк();
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификата, Неопределено);
	
КонецПроцедуры

Процедура ПроверитьУстановкуПодписиПолученияСертификатовСТокенаСбербанк(Результат, Параметры) Экспорт
	
	Если НЕ Результат.Успех Тогда
		Параметры.Контекст.ОписаниеОшибки = Результат.ТекстОшибки;
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Параметры.Контекст);
		Возврат;
	КонецЕсли;
	
	СоответствиеСертификатов = Результат.СоответствиеСертификатов;
	
	РеквизитыПроверяемогоСертификата = Новый Структура("Отпечаток");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(
		Параметры.Сертификат, РеквизитыПроверяемогоСертификата);
	
	Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
		Попытка
			СтруктураСертификата = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСертификата(КлючЗначение.Значение);
		Исключение
			ТекстСообщения = НСтр("ru = 'Ошибка чтения сертификата.'");
			ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Параметры.Контекст.ОписаниеОшибки = ТекстСообщения + Символы.ПС + ТекстОшибки;
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Параметры.Контекст);
			Возврат;
		КонецПопытки;
		
		Если СтруктураСертификата.Отпечаток = РеквизитыПроверяемогоСертификата.Отпечаток Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодписанияТестовойСтрокиСбербанк", ЭтотОбъект, Параметры);
			СтрокаПодписиBase64 = "JiMxMDU4OyYjMTA3NzsmIzEwODk7JiMxMDkwOw=="; // "Тест" в Base64
			Представление = НСтр("ru = 'Тестовая строка'");
			ПодписатьДанныеСбербанк(ОписаниеОповещения, Параметры.ИмяВнешнегоМодуля, СтрокаПодписиBase64, КлючЗначение.Ключ,
				Представление, КлючЗначение.Значение);
			Возврат
		КонецЕсли;
	КонецЦикла;
	
	ТекстСообщения = НСтр("ru = 'Сертификат не найден на аппаратном устройстве.'");
	Параметры.Контекст.ОписаниеОшибки = ТекстСообщения;
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Параметры.Контекст);

КонецПроцедуры

Процедура ПослеПолученияСпискаСертификатовСТокенаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если РезультатВызова <> 0 Тогда
		ШаблонОшибки = НСтр("ru = 'При получении списка сертификатов на банковском ключе произошла ошибка.
							|Внешний модуль Сбербанка при получении списка доступных сертификатов вернул код ошибки: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, РезультатВызова);
		Операция = НСтр("ru = 'Подписание электронного документа.'");
		ОбработатьОшибку(Операция, ТекстОшибки);
		ОчиститьДанныеАвторизацииСбербанк();
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);
		Возврат;
	КонецЕсли;
	
	ИдентификаторыСертификатов = ПараметрыВызова[1];
	
	СоответствиеСертификатов = Новый Соответствие;
	КоличествоСтрок = СтрЧислоСтрок(ИдентификаторыСертификатов);
	Индекс = 2;
	Пока Индекс < КоличествоСтрок Цикл
		Текст = СтрПолучитьСтроку(ИдентификаторыСертификатов, Индекс);
		Текст = СтрЗаменить(Текст, ",", "");
		Текст = СтрЗаменить(Текст, ";", "");
		СоответствиеСертификатов.Вставить(Текст);
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Если СоответствиеСертификатов.Количество() Тогда
		ДополнительныеПараметры.Вставить("СоответствиеСертификатов", СоответствиеСертификатов);
		КопияСоответствия = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(СоответствиеСертификатов);
		ПолучитьДвоичныеДанныеОчередногоСертификатаСбербанк(КопияСоответствия, ДополнительныеПараметры);
		Возврат;
	Иначе
		ТекстСообщения = НСтр("ru = 'Не найден ни один сертификат на аппаратном устройстве.'");
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);

КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСпискаИдентификаторовСертификатов(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Операция = НСтр("ru = 'Получение списка сертификатов с аппаратного устройства.'");
	ТекстСообщения = НСтр("ru = 'Не удалось получить информацию о сертификатах с аппаратного устройства.'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ОбработатьОшибку(Операция, ТекстОшибки);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);
	
КонецПроцедуры

Процедура ПолучитьДвоичныеДанныеОчередногоСертификатаСбербанк(СоответствиеСертификатов, ДополнительныеПараметры)
	
	Если СоответствиеСертификатов.Количество() = 0 Тогда
		Результат = Новый Структура();
		Результат.Вставить("Успех", Истина);
		Результат.Вставить("СоответствиеСертификатов", ДополнительныеПараметры.СоответствиеСертификатов);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);
		Возврат;
	КонецЕсли;

	Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
		ИдентификаторСертификата = КлючЗначение.Ключ;
		Прервать;
	КонецЦикла;
	
	СоответствиеСертификатов.Удалить(ИдентификаторСертификата);
	
	Параметры = Новый Структура;
	Параметры.Вставить("СоответствиеСертификатов", СоответствиеСертификатов);
	Параметры.Вставить("Контекст", ДополнительныеПараметры);
	Параметры.Вставить("ИдентификаторСертификата", ИдентификаторСертификата);
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияДвоичныхДанныхСертификатаСбербанк", ЭтотОбъект, Параметры);
	
	ПолучитьДвоичныеДанныеСертификатаСбербанк(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ИдентификаторСертификата);
	
КонецПроцедуры

Процедура ПослеПолученияДвоичныхДанныхСертификатаСбербанк(ДвоичныеДанныеСертификата, ДополнительныеПараметры) Экспорт
	
	Если ДвоичныеДанныеСертификата =  Неопределено Тогда
		ДополнительныеПараметры.Контекст.СоответствиеСертификатов.Удалить(ДополнительныеПараметры.ИдентификаторСертификата);
	Иначе
		ДополнительныеПараметры.Контекст.СоответствиеСертификатов.Вставить(
			ДополнительныеПараметры.ИдентификаторСертификата, ДвоичныеДанныеСертификата);
	КонецЕсли;
	
	ПолучитьДвоичныеДанныеОчередногоСертификатаСбербанк(
		ДополнительныеПараметры.СоответствиеСертификатов, ДополнительныеПараметры.Контекст);
	
КонецПроцедуры

Процедура ПодобратьСертификатПослеПолученияСпискаСТокенаСбербанк(Результат, ДополнительныеПараметры) Экспорт

	Если НЕ Результат.Успех Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, Неопределено);
		ОчиститьДанныеАвторизацииСбербанк();
		Возврат;
	КонецЕсли;
	
	СоответствиеСертификатов = Результат.СоответствиеСертификатов;
	
	СписокСертификатов = Новый СписокЗначений;

	Если ДополнительныеПараметры.ЭтоПолучениеНастроек Тогда
		ДоступныеСертификатыНастройкиОбмена = Новый Массив;
	ИначеЕсли ДополнительныеПараметры.Свойство("ОграничениеПоСертификатам") Тогда
		ДоступныеСертификатыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатов(
			ДополнительныеПараметры.ОграничениеПоСертификатам);
	Иначе
		ДоступныеСертификатыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатовБанка(
			ДополнительныеПараметры.НастройкаОбмена);
	КонецЕсли;
	
	СоответствиеОтпечатокСертификат = Новый Соответствие;
	Для Каждого Элемент Из ДоступныеСертификатыНастройкиОбмена Цикл
		ПроверитьСрокДействияСертификатаБанка(
			Элемент.Сертификат, Элемент.ДействителенДО, Элемент.ПользовательОповещенОСрокеДействия);
		СоответствиеОтпечатокСертификат.Вставить(Элемент.Отпечаток, Элемент.Сертификат);
	КонецЦикла;

	ВыборкаСертификатов = Новый Соответствие;
	СоответствиеСертификатовСбербанка = Новый Соответствие;
	
	Для Каждого Элемент Из СоответствиеСертификатов Цикл
		
		ИдентификаторСертификата = Элемент.Ключ;
		ДвоичныеДанныеСертификата = Элемент.Значение;
		
		Если ДвоичныеДанныеСертификата = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			СтруктураСертификата = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСертификата(ДвоичныеДанныеСертификата);
		Исключение
			ОчиститьСообщения();
			ТекстСообщения = НСтр("ru = 'Ошибка чтения сертификата.'");
			ТекстОшибки = ОписаниеОшибки();
			Операция = НСтр("ru = 'Чтение данных сертификата.'");
			ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения, ДополнительныеПараметры.НастройкаОбмена);
			Продолжить;
		КонецПопытки;
		Если СтруктураСертификата.ДействителенДо < ОбщегоНазначенияКлиент.ДатаСеанса() Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДополнительныеПараметры.ЭтоПолучениеНастроек Тогда
			НайденныйСертификат = СтруктураСертификата.Представление;
		Иначе
			НайденныйСертификат = СоответствиеОтпечатокСертификат.Получить(СтруктураСертификата.Отпечаток);
			Если НайденныйСертификат = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("СертификатСсылка", НайденныйСертификат);
		СтруктураДанных.Вставить("ДвоичныеДанныеСертификата", ДвоичныеДанныеСертификата);
		СоответствиеСертификатовСбербанка.Вставить(ИдентификаторСертификата, СтруктураДанных);
		ВыборкаСертификатов.Вставить(ИдентификаторСертификата, Строка(НайденныйСертификат));
	КонецЦикла;
	
	Если ВыборкаСертификатов.Количество() = 1 Тогда
		Для Каждого КлючЗначение Из ВыборкаСертификатов Цикл
			СтруктураВозврата = Новый Структура();
			СтруктураВозврата.Вставить("ИдентификаторСертификата", КлючЗначение.Ключ);
			ДанныеСертификата = СоответствиеСертификатовСбербанка.Получить(КлючЗначение.Ключ);
			СтруктураВозврата.Вставить("ДвоичныеДанныеСертификата", ДанныеСертификата.ДвоичныеДанныеСертификата);
			Если НЕ ДополнительныеПараметры.ЭтоПолучениеНастроек Тогда
				СтруктураВозврата.Вставить("СертификатСсылка", ДанныеСертификата.СертификатСсылка);
			КонецЕсли;
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, СтруктураВозврата);
			Возврат;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ВыборкаСертификатов.Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат подписи.
									|Возможные причины:
									|- В настройке обмена через сервис 1С:ДиректБанк указан сертификат другой организации. Загрузите нужный сертификат с аппаратного устройства.
									|- Вставлено другое аппаратное устройство. Проверьте, какое устройство подключено к компьютеру.
									|- Указана неверная учетная запись. Выберите правильную учетную запись при входе на аппаратное устройство.
									|- Выбрана учетная запись, не соответствующая маршруту подписания документа. Проверьте настройки маршрутов подписания.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, Неопределено);
		ОчиститьДанныеАвторизацииСбербанк();
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из ВыборкаСертификатов Цикл
		СписокСертификатов.Добавить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
	ЗаголовокФормыВыбора = НСтр("ru = 'Выберите сертификат подписи'");
	
	ДополнительныеПараметры.Вставить("СоответствиеСертификатовСбербанка", СоответствиеСертификатовСбербанка);
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораСертификатаСбербанк", ЭтотОбъект, ДополнительныеПараметры);

	СписокСертификатов.ПоказатьВыборЭлемента(Оповещение, ЗаголовокФормыВыбора);
	
КонецПроцедуры

Процедура ПослеАутентификацииНаТокенеОпределитьСтатусыПодписейСбербанк(Успех, Параметры) Экспорт
	
	Если Не Успех Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПроверкиПодписи, Ложь);
		Возврат;
	КонецЕсли;
	
	МассивСообщенийОбмена = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Параметры.МассивСообщенийОбмена);
	
	ПроверитьСтатусыПодписейСледующегоСообщенияОбменаСбербанк(МассивСообщенийОбмена, Параметры)
	
КонецПроцедуры

Процедура ПроверитьСтатусыПодписейСледующегоСообщенияОбменаСбербанк(МассивСообщенийОбмена, Параметры)
	
	Если МассивСообщенийОбмена.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПроверкиПодписи, Истина);
		Возврат;
	КонецЕсли;
	
	ТекСообщениеОбмена = МассивСообщенийОбмена.Получить(0);
	МассивСообщенийОбмена.Удалить(0);
	
	ИмяМодуля = Неопределено;
	//@skip-warning
	СоответствиеПодписейИСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ДанныеУстановленныхПодписейИСертификатов(
		ТекСообщениеОбмена, ИмяМодуля);
	
	Если СоответствиеПодписейИСертификатов.Количество() Тогда
		//@skip-warning
		СтрокаФорматBase64 = ОбменСБанкамиСлужебныйВызовСервера.ПодписанныеДанныеBase64(ТекСообщениеОбмена);
		РезультатПроверки = Новый Массив();
		ОпределитьСтатусСледующейПодписиСбербанк(МассивСообщенийОбмена, ИмяМодуля, ТекСообщениеОбмена,
			СтрокаФорматBase64, СоответствиеПодписейИСертификатов, РезультатПроверки, Параметры);
	Иначе
		ПроверитьСтатусыПодписейСледующегоСообщенияОбменаСбербанк(МассивСообщенийОбмена, Параметры);
	КонецЕсли;

КонецПроцедуры

Процедура ОпределитьСтатусСледующейПодписиСбербанк(
	МассивСообщенийОбмена,
	ИмяМодуля,
	СообщениеОбмена,
	ПодписанныеДанные,
	СоответствиеПодписейИСертификатов,
	РезультатПроверки,
	Параметры)
	
	Если СоответствиеПодписейИСертификатов.Количество() = 0 Тогда
		Если РезультатПроверки.Количество() Тогда
			//@skip-warning
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьРезультатыПроверкиПодписей(СообщениеОбмена, РезультатПроверки);
		КонецЕсли;
		ПроверитьСтатусыПодписейСледующегоСообщенияОбменаСбербанк(МассивСообщенийОбмена, Параметры);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из СоответствиеПодписейИСертификатов Цикл
		Прервать;
	КонецЦикла;
	
	ДвоичныеДанныеСертификата = КлючЗначение.Значение;
	Подпись = КлючЗначение.Ключ;
	
	СоответствиеПодписейИСертификатов.Удалить(КлючЗначение.Ключ);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ПодписанныеДанные", ПодписанныеДанные);
	ДополнительныеПараметры.Вставить("СоответствиеПодписейИСертификатов", СоответствиеПодписейИСертификатов);
	ДополнительныеПараметры.Вставить("РезультатПроверки", РезультатПроверки);
	ДополнительныеПараметры.Вставить("Параметры", Параметры);
	ДополнительныеПараметры.Вставить("ИмяМодуля", ИмяМодуля);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПроверкиПодписиНаТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПроверитьПодписьНаТокенеСбербанк(Оповещение, ПодписанныеДанные, Подпись, ДвоичныеДанныеСертификата, ИмяМодуля);
	
КонецПроцедуры

Процедура ПослеПроверкиПодписиНаТокенеСбербанк(ПодписьВерна, ДополнительныеПараметры) Экспорт
	
	СтруктураЗаписи = Новый Структура;
	
	Если НЕ ПодписьВерна = Неопределено Тогда
		СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
		СтруктураЗаписи.Вставить("ПодписьВерна", ПодписьВерна);
	КонецЕсли;
	
	ДополнительныеПараметры.РезультатПроверки.Добавить(СтруктураЗаписи);
	
	ОпределитьСтатусСледующейПодписиСбербанк(ДополнительныеПараметры.МассивСообщенийОбмена,
		ДополнительныеПараметры.ИмяМодуля, ДополнительныеПараметры.СообщениеОбмена,
		ДополнительныеПараметры.ПодписанныеДанные, ДополнительныеПараметры.СоответствиеПодписейИСертификатов,
		ДополнительныеПараметры.РезультатПроверки, ДополнительныеПараметры.Параметры);
	
КонецПроцедуры

Процедура ОбработатьРезультатПроверкиПодписиНаТокенеСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписи, РезультатВызова = 0);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПослеПроверкиПодписиНаТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При аутентификации на аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Предъявление данных аутентификации на аппаратном устройстве.'");
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	ОчиститьДанныеАвторизацииСбербанк();
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписи, Неопределено);
	
КонецПроцедуры

Процедура ПолучитьПодписанныеДанныеСбербанк(Оповещение, ПодключаемыйМодуль, АлгоритмПубличногоКлюча)
	
	Состояние = 0;
	ЭП = "";
	
	Параметры = Новый Структура();
	Параметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	Параметры.Вставить("ОповещениеПослеПолученияПодписи", Оповещение);
	Параметры.Вставить("АлгоритмПубличногоКлюча", АлгоритмПубличногоКлюча);
	
	ОповещениеВызова = Новый ОписаниеОповещения("ПослеПолученияДанныхПодписиСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуПолученияДанныхПодписиСбербанк", ЭтотОбъект);

	Если АлгоритмПубличногоКлюча = "GOST R 34.10-2012-256" Тогда
		ПодключаемыйМодуль.НачатьВызовПолучитьДанныеПодписиИзVPNKeyTLSPKCS7(ОповещениеВызова, Состояние, ЭП);
	Иначе
		ПодключаемыйМодуль.НачатьВызовПолучитьДанныеПодписиИзVPNKeyTLS(ОповещениеВызова, Состояние, ЭП);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияДанныхПодписиСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		Операция = НСтр("ru = 'Подписание данных'");
		ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка
								|Внешний модуль VpnKey-TLS при подписании вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		ОбработатьОшибку(Операция, ТекстОшибки);
		Результат = Новый Структура;
		Результат.Вставить("Успех", Ложь);
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
		Возврат;
	КонецЕсли;
	
	Состояние = ПараметрыВызова.Получить(0);
	
	Если Состояние = "COMPLETE" Тогда
		Результат = Новый Структура;
		Результат.Вставить("Успех", Истина);
		Результат.Вставить("ЭП", ПараметрыВызова.Получить(1));
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
	ИначеЕсли Состояние = "FAILED" ИЛИ Состояние = "REJECTED" Тогда
		Операция = НСтр("ru = 'Подписание данных'");
		ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка
								|Код состояния: %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Состояние);
		ОбработатьОшибку(Операция, ТекстОшибки);
		Результат = Новый Структура;
		Результат.Вставить("Успех", Ложь);
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
	Иначе
		ПолучитьПодписанныеДанныеСбербанк(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи,
			ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.АлгоритмПубличногоКлюча);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияДанныхПодписиСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении данных подписи произошла ошибка:
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение данных подписи'");
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация);
	ОчиститьДанныеАвторизацииСбербанк();
	
	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ТекстОшибки", ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
	
КонецПроцедуры

Процедура ПослеПроверкиВерсииВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИспользуетсяКриптография Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ТестНастройкиОбменаСбербанкПослеУстановкиКанала", ЭтотОбъект, ДополнительныеПараметры);
		УстановитьСоединениеИАутентифицироватьсяНаСервереЧерезТокенСбербанк(Оповещение, ДополнительныеПараметры.НастройкаОбмена, Истина);
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"ТестНастройкиОбменаПослеБазовойАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ВыполнитьАутентификациюПоЛогинуСбербанк(Оповещение, ДополнительныеПараметры.ИмяВнешнегоМодуля,
			ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.НастройкаОбмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ТестНастройкиОбменаСбербанкПослеУстановкиКанала(Успех, Параметры) Экспорт
	
	Если Не Успех Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПодписатьТестовыйЗапросПослеОпределенияСертификатаСбербанк", ЭтотОбъект, Параметры);
	
	ОпределитьСертификатПодписиСбербанк(Оповещение, Параметры.ИмяВнешнегоМодуля, Параметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ОпределитьСертификатПослеАутентификацииНаТокенеСбербанк(Успех, Параметры) Экспорт

	Если Не Успех Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Результат);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораСертификатаПодписатьЭДСбербанк", ЭтотОбъект, Параметры);
	
	ОграничениеПоСертификатам = Неопределено;
	Параметры.Свойство("ОграничениеПоСертификатам", ОграничениеПоСертификатам);
	ОпределитьСертификатПодписиСбербанк(ОписаниеОповещения, Параметры.ИмяВнешнегоМодуля, Параметры.НастройкаОбмена, ОграничениеПоСертификатам);

КонецПроцедуры

Процедура ПослеВыбораСертификатаПодписатьЭДСбербанк(Результат, Параметры) Экспорт

	Если Результат = Неопределено Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Результат);
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;

	ЗакешироватьПараметрСбербанка("СертификатПодписи", Результат.СертификатСсылка);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
	ПараметрыФормы.Вставить("ВидОперации", "ПодписаниеСбербанк");
	ПараметрыФормы.Вставить("МассивСообщенийОбмена", Параметры.СообщенияОбменаСбербанк);
	ПараметрыФормы.Вставить("ИдентификаторСертификатаСбербанк", Результат.ИдентификаторСертификата);
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияФормыПодписаниеСбербанк", ЭтотОбъект, Параметры);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	
КонецПроцедуры

Процедура ПослеПредъявленияПинаНаТокенеСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ОбработатьРезультатАутентификацииНаТокенеСбербанка(ДополнительныеПараметры.НастройкаОбмена, РезультатВызова,
		ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене);
	
КонецПроцедуры

Процедура ПодписатьТестовыйЗапросПослеОпределенияСертификатаСбербанк(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
	РеквизитыНастройкиОбмена = Новый Структура("ИдентификаторОрганизации, ИмяВнешнегоМодуля");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		Параметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
	ИдентификаторОрганизации = РеквизитыНастройкиОбмена.ИдентификаторОрганизации;
	СтрокаПодписи = "ATTRIBUTES" + Символ(10) + "OrgId=" + ИдентификаторОрганизации + Символ(10)
					+ "RequestId=" + ИдентификаторЗапроса;

	СтрокаПодписиBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(СтрокаПодписи);
	Параметры.Вставить("СтрокаПодписиBase64", СтрокаПодписиBase64);

	Представление = НСтр("ru = 'Тестовый запрос'");
	Параметры.Вставить("ИдентификаторЗапроса", ИдентификаторЗапроса);
	Параметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	Параметры.Вставить("ИдентификаторСертификата", Результат.ИдентификаторСертификата);
	Параметры.Вставить("СертификатСсылка", Результат.СертификатСсылка);
	Параметры.Вставить("ИмяМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	
	РеквизитыСертификата = Новый Структура("ДанныеСертификата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(
		Результат.СертификатСсылка, РеквизитыСертификата);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПодписьПослеУстановкиСбербанк", ЭтотОбъект, Параметры);
	ПодписатьДанныеСбербанк(ОписаниеОповещения, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля, СтрокаПодписиBase64,
		Результат.ИдентификаторСертификата, Представление, РеквизитыСертификата.ДанныеСертификата);
	
КонецПроцедуры

Процедура ПроверитьПодписьПослеУстановкиСбербанк(Результат, Параметры) Экспорт
	
	Если Результат.Успех Тогда
		ПараметрыСертификата = Новый Структура("ДанныеСертификата");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(
			Параметры.СертификатСсылка, ПараметрыСертификата);
		
		Параметры.Вставить("ДанныеПодписи", Результат.ЭП);
		Оповещение = Новый ОписаниеОповещения("ОтправитьТестовыйЗапросПослеПроверкиПодписиСбербанк", ЭтотОбъект, Параметры);
		
		ПроверитьПодписьНаТокенеСбербанк(Оповещение, Параметры.СтрокаПодписиBase64, Результат.ЭП,
			ПараметрыСертификата.ДанныеСертификата, Параметры.ИмяМодуля);
	Иначе
		Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		КонецЕсли;
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьТестовыйЗапросПослеПроверкиПодписиСбербанк(ПодписьВерна, Параметры) Экспорт
	
	Если ПодписьВерна = Неопределено Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	Если Не ПодписьВерна Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Установленная подпись неверна.'"));
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	ДанныеПодписи = Новый Структура("ЭП, СертификатПодписи", Параметры.ДанныеПодписи, Параметры.СертификатСсылка);
	
	Попытка
		ТестоваяСтрока = ОбменСБанкамиСлужебныйВызовСервера.ТекстЗапросаНочнойВыписки(Параметры.НастройкаОбмена,
			Параметры.ИдентификаторЗапроса, Параметры.ИдентификаторОрганизации, ДанныеПодписи);
	Исключение
		ВидОперации = НСтр("ru = 'Формирование запроса новых документов'");
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстСообщения, Параметры.НастройкаОбмена);
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецПопытки;
	
	Параметры.Вставить("ТестоваяСтрока", ТестоваяСтрока);
	
	Оповещение = Новый ОписаниеОповещения(
		"ОтправитьТестовыйЗапросПослеУстановкиСоединенияСбербанк", ЭтотОбъект, Параметры);
	УстановитьСоединениеИАутентифицироватьсяНаСервереЧерезТокенСбербанк(Оповещение, Параметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ОтправитьПлатежныеПорученияПослеУстановкиКаналаСбербанк(Успех, Параметры) Экспорт

	Если Не Успех Тогда
		ОтправитьДокументыВСбербанк(Параметры);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиПлатежныхПорученийВСбербанк", ЭтотОбъект, Параметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидОперации", "ОтправкаДокументовСбербанк");
	ПараметрыФормы.Вставить("МассивСообщенийОбмена", Параметры.МассивСообщенийОбмена);
	ПараметрыФормы.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	
КонецПроцедуры

Процедура ПослеПолученияКонфигурационнойСтроки(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("КонфигурационнаяСтрока", ПараметрыВызова.Получить(0));
	
	СтруктураНормализованная = Новый Структура;
	
	Для каждого ЭлементКоллекции Из ДополнительныеПараметры.ПараметрыФродМониторинга Цикл
		
		СтрокаXML = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыXML(ЭлементКоллекции.Значение, "");
		СтруктураНормализованная.Вставить(ЭлементКоллекции.Ключ, СтрокаXML);
		
	КонецЦикла;
	
	ФродПараметры = Новый ФиксированнаяСтруктура(СтруктураНормализованная);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияФродПараметров, ФродПараметры);
	
КонецПроцедуры

Процедура ПослеПолученияПараметровDevicePrint(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("РазрядностьОС", ПараметрыВызова.Получить(0));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("РазрядностьПроцессора", ПараметрыВызова.Получить(1));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ГлубинаЦвета", ПараметрыВызова.Получить(2));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ШиринаМонитора", ПараметрыВызова.Получить(3));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ВысотаМонитора", ПараметрыВызова.Получить(4));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ПлотностьПикселяX", ПараметрыВызова.Получить(5));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ПлотностьПикселяY", ПараметрыВызова.Получить(6));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ШиринаРабочегоСтола", ПараметрыВызова.Получить(7));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("СмещениеЧасовогоПояса", ПараметрыВызова.Получить(8));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("УстановленныеКомпоненты", ПараметрыВызова.Получить(9));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("Онлайн", ПараметрыВызова.Получить(10));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("СглаживаниеШрифтов", ПараметрыВызова.Получить(11));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ЯзыкСистемы", ПараметрыВызова.Получить(12));
	ДополнительныеПараметры.ПараметрыФродМониторинга.Вставить("ЯзыкПользователя", ПараметрыВызова.Получить(13));
	
	Оповещение =  Новый ОписаниеОповещения("ПослеПолученияКонфигурационнойСтроки", ЭтотОбъект, ДополнительныеПараметры);
	
	КонфигурационнаяСтрока = "";
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПолучитьКонфигурационнуюСтроку(
		Оповещение, КонфигурационнаяСтрока);
	
КонецПроцедуры

Процедура ОтправитьДокументыВСбербанк(Параметры)
	
	Для Каждого КлючЗначение Из Параметры.ДанныеДляОтправкиВСбербанк Цикл
		Параметры.Вставить("НастройкаОбмена", КлючЗначение.Ключ);
		Параметры.Вставить("МассивСообщенийОбмена", КлючЗначение.Значение);
		Параметры.ДанныеДляОтправкиВСбербанк.Удалить(КлючЗначение.Ключ);
		Обработчик = Новый ОписаниеОповещения("ОтправитьПлатежныеПорученияПослеУстановкиКаналаСбербанк", ЭтотОбъект, Параметры);
		УстановитьСоединениеИАутентифицироватьсяНаСервереЧерезТокенСбербанк(Обработчик, КлючЗначение.Ключ);
		Возврат;
	КонецЦикла;
	
	ОтправкаСообщенийОбмена(Параметры);
	
КонецПроцедуры

Процедура НачатьПодписаниеЭДСбербанка(Знач ДанныеПодписи, Параметры)
	
	МассивОбработанныхНастроекОбмена = Параметры.МассивОбработанныхНастроекОбмена;
	ТекущийИндекс = 0;
	
	Для Каждого Элемент Из ДанныеПодписи Цикл
		
		ТекущийИндекс = ТекущийИндекс + 1;
		Если ТекущийИндекс <= Параметры.ИндексТретьейИтерации Тогда
			Продолжить;
		Иначе
			Параметры.ИндексТретьейИтерации = ТекущийИндекс;
		КонецЕсли;
		
		Если НЕ МассивОбработанныхНастроекОбмена.Найти(Элемент.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		МассивОбработанныхНастроекОбмена.Добавить(Элемент.Ключ);
		МассивСообщенийОбменаБанка = Элемент.Значение;
		Если МассивСообщенийОбменаБанка.Количество() > 0 Тогда
			Параметры.Вставить("ТекущиеДанныеПодписи", ДанныеПодписи);
			ОбработчикПослеПодписания = Новый ОписаниеОповещения("ПродолжитьПодписаниеЭДСбербанк", ЭтотОбъект, Параметры);
			ОграничениеПоСертификатам = Неопределено;
			Параметры.Свойство("МассивСертификатов", ОграничениеПоСертификатам);
			ПодписатьЭДСбербанк(
				ОбработчикПослеПодписания, Элемент.Ключ, МассивСообщенийОбменаБанка, ОграничениеПоСертификатам, Истина);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ПродолжитьПодписаниеБанковскихЭД(Параметры.РезультатВыбораСертификатаБанка, Параметры);

КонецПроцедуры

Процедура ПродолжитьПодписаниеЭДСбербанк(Результат, Параметры) Экспорт
	
	Если Результат.Успех Тогда
		Параметры.КоличествоПодписанных = Параметры.КоличествоПодписанных + Результат.Количество;
		НачатьПодписаниеЭДСбербанка(Параметры.ТекущиеДанныеПодписи, Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВыбораСертификатаСбербанк(ВыбраннаяСтрока, ДополнительныеПараметры) Экспорт
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, КодВозвратаДиалога.Отмена);
	Иначе
		ДанныеСертификата = ДополнительныеПараметры.СоответствиеСертификатовСбербанка.Получить(ВыбраннаяСтрока.Значение);
		СтруктураВозврата = Новый Структура();
		СтруктураВозврата.Вставить("ИдентификаторСертификата", ВыбраннаяСтрока.Значение);
		СтруктураВозврата.Вставить("ДвоичныеДанныеСертификата", ДанныеСертификата.ДвоичныеДанныеСертификата);
		Если НЕ ДополнительныеПараметры.ЭтоПолучениеНастроек Тогда
		
			СтруктураВозврата.Вставить("СертификатСсылка", ДанныеСертификата.СертификатСсылка);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьКаналПослеОпределенияНомераБизнесСистемыСбербанк(НомерБизнесСистемы, Параметры) Экспорт
	
	Если НомерБизнесСистемы = Неопределено Тогда //не удалось определить бизнес систему
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеУстановкиВиртуальногоКанала, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеУстановкиКаналаСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуУстановкиКаналаНаТокенеСбербанк", ЭтотОбъект);
	
	Параметры.ПодключаемыйМодуль.НачатьВызовУстановитьTLSКаналСБизнесСистемой(Оповещение, НомерБизнесСистемы);
	
КонецПроцедуры

Процедура ПослеУстановкиКаналаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатВызова = 0 Тогда
		Операция = НСтр("ru = 'Установка виртуального канала'");
		ТекстСообщения = НСтр("ru = 'Не удалось установить связь с сервером.
									|Необходимо проверить работу TLS VPN Key.'");
		ТекстОшибки = НСтр("ru = 'Компонента AddIn.Bicrypt при установке виртуального канала вернула код ошибки'")
							+ " " + РезультатВызова;
		ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения);
		ОчиститьДанныеАвторизацииСбербанк();
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиВиртуальногоКанала, РезультатВызова = 0);
	
КонецПроцедуры

Процедура ОбработатьОшибкуУстановкиКаналаНаТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При установке канала на аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Установка канала на аппаратном устройстве.'");
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	ОчиститьДанныеАвторизацииСбербанк();
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиВиртуальногоКанала, Ложь);
	
КонецПроцедуры

Процедура ПолучитьПараметрыТокенаПослеТихогоСтартаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Ошибка при запуске клиента аппаратного устройства.
									|Убедитесь, что устройство подключено к компьютеру.'");
		ТекстОшибки = НСтр("ru = 'Внешний модуль VpnKey-TLS при запуске Start.exe вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		Операция = НСтр("ru = 'Запуск клиента аппаратного устройства.'");
		ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения);
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ЗарегистрироватьсяНаТокенеПослеПолученияИнформацииОТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьИнформациюОТокенеСбербанк(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль);

КонецПроцедуры

Процедура ОбработатьОшибкуТихогоСтартаСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При запуске программы банка произошла ошибка:
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Запуск программы Start.exe'");
	ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения);
	ОчиститьДанныеАвторизацииСбербанк();
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
	
КонецПроцедуры

Процедура ЗарегистрироватьсяНаТокенеПослеПолученияИнформацииОТокенеСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат
	КонецЕсли;

	Если ДополнительныеПараметры.Свойство("ПринудительнаяАутентификация") Тогда
		ПринудительнаяАутентификация = ДополнительныеПараметры.ПринудительнаяАутентификация;
	Иначе
		ПринудительнаяАутентификация = Ложь;
	КонецЕсли;

	ДополнительныеПараметры.Вставить("ТипУстройства", Результат.ТипУстройства);
		
	НастройкаОбмена = ДополнительныеПараметры.НастройкаОбмена;
	
	Если ДополнительныеПараметры.ЭтоТест ИЛИ ПринудительнаяАутентификация
		ИЛИ ЗначениеИзКешаСбербанк("ЗапомнитьСессию") <> Истина
		ИЛИ Не (ЗначениеЗаполнено(ЗначениеИзКешаСбербанк("АутентификацияНаТокенеВыполнена"))
				И ЗначениеИзКешаСбербанк("ТекущаяНастройкаОбмена") = НастройкаОбмена) Тогда

		Если Результат.ТипУстройства <> "SCR" Тогда // не экранный токен
			Обработчик = Новый ОписаниеОповещения(
				"ПослеВводаДанныхАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросПинКодаСбербанк", , , , , , Обработчик);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НомерКонтейнера = Строка(ЗначениеИзКешаСбербанк("НомерКонтейнера"));
	ПинКод = ЗначениеИзКешаСбербанк("ПинКод");
	
	ЗарегистрироватьсяНаТокенеСбербанка(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, НастройкаОбмена,
		ДополнительныеПараметры.ПодключаемыйМодуль, НомерКонтейнера, ПинКод, Результат.ТипУстройства);
	
КонецПроцедуры

Процедура ПослеВводаДанныхАутентификацииСбербанк(ДанныеАутентификации, ДополнительныеПараметры) Экспорт
	
	Если ДанныеАутентификации = Неопределено ИЛИ ДанныеАутентификации = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	НомерКонтейнера = ДанныеАутентификации.НомерКонтейнера;
	ПинКод = ДанныеАутентификации.ПинКод;
	
	ЗакешироватьПараметрСбербанка("ЗапомнитьСессию", ДанныеАутентификации.ЗапомнитьСессию);
	ЗакешироватьПараметрСбербанка("НомерКонтейнера", НомерКонтейнера);
	ЗакешироватьПараметрСбербанка("ПинКод", ПинКод);
	
	ЗарегистрироватьсяНаТокенеСбербанка(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене,
		ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.ПодключаемыйМодуль, НомерКонтейнера, ПинКод,
		ДополнительныеПараметры.ТипУстройства);
	
КонецПроцедуры

Процедура ОбработатьРезультатАутентификацииНаТокенеСбербанка(НастройкаОбмена, РезультатАвторизации, ОповещениеПослеАутентификацииНаТокене)
	
	ЕстьОшибка = Ложь;
	
	Если Не (РезультатАвторизации = 0) Тогда
		Если РезультатАвторизации = 28 Тогда
			ШаблонСообщения = НСтр("ru = 'PIN%1 заблокирован'");
			НомерКонтейнера = ЗначениеИзКешаСбербанк("НомерКонтейнера");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, НомерКонтейнера);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ИначеЕсли РезультатАвторизации = 27 Тогда
			ШаблонСообщения = НСтр("ru = 'PUK%1 заблокирован'");
			ТекстСообщения = СтрШаблон(
								ШаблонСообщения, ЗначениеИзКешаСбербанк("НомерКонтейнера"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ИначеЕсли РезультатАвторизации = 25 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Пользователь заблокирован'"));
		ИначеЕсли РезультатАвторизации = 29 ИЛИ РезультатАвторизации = 30 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Неверные данные аутентификации на токене'"));
		Иначе
			ОчиститьСообщения();
			ТекстСообщения = НСтр("ru = 'Не удалось выполнить аутентификацию на аппаратном устройстве.
										|Необходимо выполнить его перезапуск'");
			ТекстОшибки = НСтр("ru = 'Внешний модуль VpnKey-TLS при авторизации на аппаратном устройстве вернул код ошибки: %1'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатАвторизации);
			Операция = НСтр("ru = 'Авторизация на токене'");
			ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения, НастройкаОбмена);
		КонецЕсли;
		ЕстьОшибка = Истина;
		ОчиститьДанныеАвторизацииСбербанк();
	КонецЕсли;
	ЗакешироватьПараметрСбербанка("АутентификацияНаТокенеВыполнена", Не ЕстьОшибка);
	ЗакешироватьПараметрСбербанка("ТекущаяНастройкаОбмена", НастройкаОбмена);
	
	ВыполнитьОбработкуОповещения(ОповещениеПослеАутентификацииНаТокене, Не ЕстьОшибка);
	
КонецПроцедуры

Процедура ПослеПолученияОтпечатковВыполнитьОбмен(ОтпечаткиСертификатов, Параметры) Экспорт
	
	МассивОтпечатковСертификатов = Новый Массив;
	Если ТипЗнч(ОтпечаткиСертификатов) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из ОтпечаткиСертификатов Цикл
			МассивОтпечатковСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	ИначеЕсли Не ЭлектроннаяПодписьКлиент.СоздаватьЭлектронныеПодписиНаСервере() Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеСинхронизации, Ложь);
	КонецЕсли;
	
	ДоступныеСертификаты = ОбменСБанкамиСлужебныйВызовСервера.СоответствиеДоступныхСертификатовИПараметров(
		МассивОтпечатковСертификатов, Параметры.НастройкаОбмена);
			
	МассивСертификатов = Новый Массив;
	ПарольПолучен = Ложь;
	Для Каждого КлючЗначение Из ДоступныеСертификаты Цикл
		МассивСертификатов.Добавить(КлючЗначение.Ключ);
		Если КлючЗначение.Значение.Свойство("ПарольПолучен", ПарольПолучен) И ПарольПолучен = Истина Тогда
			МассивСертификатов.Очистить();
			МассивСертификатов.Добавить(КлючЗначение.Ключ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если МассивСертификатов.Количество() Тогда
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
		ОписаниеДанных.Вставить("ЗаголовокДанных", "");
		
		ПараметрыЗапросаИдентификатораСессии = Новый Структура;
		ПараметрыЗапросаИдентификатораСессии.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
		
		ДанныеДляПолученияИдентификатораСессии = Новый ОписаниеОповещения(
			"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ПараметрыЗапросаИдентификатораСессии);

		ОписаниеДанных.Вставить("Данные", ДанныеДляПолученияИдентификатораСессии);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОтправитьИПолучитьДокументыВБанкПослеРасшифровкиМаркера", ЭтотОбъект, Параметры);
		
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, Новый УникальныйИдентификатор, ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеСинхронизации, Ложь);
	КонецЕсли;

КонецПроцедуры

Процедура ПолучитьБизнесСистемыНаТокенеСбербанк(Оповещение, ПодключаемыйМодуль)
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеПослеПолученияСпискаБизнесСистем", Оповещение);
	ОповещениеПослеПолученияНомераБизнесСистемы = Новый ОписаниеОповещения(
		"ПослеПолученияСпискаБизнесСистемСТокенаСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуПолученияСпискаБизнесСистемНаТокенеСбербанк", ЭтотОбъект);
	БизнесСистемы = "";
	ПодключаемыйМодуль.НачатьВызовПолучитьСписокБизнесСистемVPNKeyTLS(
		ОповещениеПослеПолученияНомераБизнесСистемы, БизнесСистемы);
	
КонецПроцедуры

Процедура ПровестиТестНастройкиСбербанк(Оповещение, НастройкаОбмена, ПараметрыОбмена)
	
	ОчиститьДанныеАвторизацииСбербанк();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ОповещениеПослеТестаНастройки", Оповещение);
	ДополнительныеПараметры.Вставить("ИмяВнешнегоМодуля", ПараметрыОбмена.ИмяВнешнегоМодуля);
	ДополнительныеПараметры.Вставить("ИспользуетсяКриптография", ПараметрыОбмена.ИспользуетсяКриптография);

	ОписаниеОбработчика = Новый ОписаниеОповещения("ПослеПроверкиВерсииВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПроверитьВерсиюВКСбербанк(ОписаниеОбработчика, ПараметрыОбмена.ИмяВнешнегоМодуля);
	
КонецПроцедуры

Процедура ПослеГенерацииSMSСбербанк(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.Счетчик);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'SMS-подписание документов'");
		ОбработатьОшибку(ВидОперации, РезультатЗадания.ПодробноеПредставлениеОшибки, РезультатЗадания.КраткоеПредставлениеОшибки,
			ДополнительныеПараметры.НастройкаОбмена);
		ПодтвердитьСледующийДокументСбербанк(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.НастройкаОбмена,
			ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.Счетчик);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		Если РезультатОперации.ТребуетсяАутентификация Тогда
			Если ДополнительныеПараметры.АутентификацияПроизводилась Тогда
				ТекстОшибки = НСтр("ru = 'Ошибка аутентификации на сервере банка'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
				ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.Счетчик);
			Иначе
				РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
				ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
					ДополнительныеПараметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
				Обработчик = Новый ОписаниеОповещения(
					"СгенерироватьSMSПослеБазовойАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
				ВыполнитьАутентификациюПоЛогинуСбербанк(Обработчик, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля,
					ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.НастройкаОбмена);
			КонецЕсли
		Иначе // запрос был отправлен
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
			ПараметрыФормы.Вставить("ВидОперации", "ИнициализацияПодтвержденияДокументаСбербанк");
			ПараметрыФормы.Вставить("ИсходныйТикетСбербанк", РезультатОперации.Тикет);
			ПараметрыФормы.Вставить("СообщениеОбмена", ДополнительныеПараметры.СообщениеОбмена);
			Оповещение = Новый ОписаниеОповещения(
				"ПослеИнициализацииПодтвержденияПлатежаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СгенерироватьSMSПослеБазовойАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияГенерацииSMSСбербанк(
			ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.СообщениеОбмена);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ДополнительныеПараметры.Вставить("АутентификацияПроизводилась", Истина);
		Оповещение = Новый ОписаниеОповещения("ПослеГенерацииSMSСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.Счетчик);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеИнициализацииПодтвержденияПлатежаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // ошибка
		ПодтвердитьСледующийДокументСбербанк(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.НастройкаОбмена,
			ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.Счетчик);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("СообщениеОбмена", ДополнительныеПараметры.СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ИдентификаторКриптопрофиля", Результат);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьSMSКодВСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ПодтверждениеПлатежныхПорученийПоSMS", ПараметрыФормы, ЭтотОбъект, , , ,
		ОписаниеОповещения);
	
КонецПроцедуры

Процедура ОтправитьSMSКодВСбербанк(Пароль, ДополнительныеПараметры) Экспорт
	
	Если Пароль = Неопределено ИЛИ Пароль = КодВозвратаДиалога.Отмена Тогда // пользователь отказался вводить смс
		ПодтвердитьСледующийДокументСбербанк(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.НастройкаОбмена,
			ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.Счетчик);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("SMSКод", Пароль);
	ДополнительныеПараметры.Вставить("АутентификацияПроизводилась", Ложь);
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияОтправкиSMSВСбербанк(
		ДополнительныеПараметры.НастройкаОбмена,ДополнительныеПараметры.СообщениеОбмена,
		ДополнительныеПараметры.ИдентификаторКриптопрофиля, Пароль);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиSMSВСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ПослеОтправкиSMSВСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // задание было отменено
		ПодтвердитьСледующийДокументСбербанк(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.НастройкаОбмена,
			ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.Счетчик);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Отправка одноразового пароля в Сбербанк'");
		ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки,
			ДополнительныеПараметры.НастройкаОбмена);
		ПодтвердитьСледующийДокументСбербанк(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.НастройкаОбмена,
			ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.Счетчик);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОперации.ТребуетсяАутентификация Тогда
			Если ДополнительныеПараметры.АутентификацияПроизводилась Тогда
				ТекстОшибки = НСтр("ru = 'Ошибка аутентификации на сервере банка'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
				ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.Счетчик);
			Иначе
				РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
				ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
					ДополнительныеПараметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
				Обработчик = Новый ОписаниеОповещения(
					"ОтправитьSMSПослеБазовойАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
				ВыполнитьАутентификациюПоЛогинуСбербанк(Обработчик, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля,
					ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.НастройкаОбмена);
			КонецЕсли
		Иначе // запрос был отправлен
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
			ПараметрыФормы.Вставить("ВидОперации", "ПолучениеРезультатаПодтверждения");
			ПараметрыФормы.Вставить("ИсходныйТикетСбербанк", РезультатОперации.Тикет);
			ПараметрыФормы.Вставить("СообщениеОбмена", ДополнительныеПараметры.СообщениеОбмена);
			Оповещение = Новый ОписаниеОповещения("ПослеПроверкиSMSВСбербанке", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьSMSПослеБазовойАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияОтправкиSMSВСбербанк(
			ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.СообщениеОбмена,
			ДополнительныеПараметры.ИдентификаторКриптопрофиля, ДополнительныеПараметры.SMSКод);
		Если Результат.Статус = "Выполняется" Тогда
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
			ДополнительныеПараметры.Вставить("АутентификацияПроизводилась", Истина);
			Оповещение = Новый ОписаниеОповещения("ПослеОтправкиSMSВСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
		Иначе
			ПослеОтправкиSMSВСбербанк(Результат, ДополнительныеПараметры)
		КонецЕсли;
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(ВидОперации, "GA==", ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.Счетчик);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПроверкиSMSВСбербанке(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ДополнительныеПараметры.Счетчик = ДополнительныеПараметры.Счетчик + 1;
	КонецЕсли;
	
	ПодтвердитьСледующийДокументСбербанк(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.НастройкаОбмена,
		ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.Счетчик);
	
КонецПроцедуры

Процедура ПослеСинхронизацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	Если Результат = Неопределено Тогда // задание было отменено пользователем
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Синхронизация документов со Сбербанком'");
		ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки,
			ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОперации.ТребуетсяАутентификация Тогда
			Если ДополнительныеПараметры.АутентификацияПроизводилась Тогда
				ТекстОшибки = НСтр("ru = 'Ошибка аутентификации на сервере банка'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
				ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
			Иначе
				Обработчик = Новый ОписаниеОповещения(
					"СинхронизацияПослеБазовойАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
				ВыполнитьАутентификациюПоЛогинуСбербанк(Обработчик,
					ДополнительныеПараметры.РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля, ДополнительныеПараметры.НастройкаОбмена,
					ДополнительныеПараметры.НастройкаОбмена, , , ДополнительныеПараметры.ВыводитьОкноОжидания);
			КонецЕсли
		Иначе
			ПоказатьРезультатСинхронизации(0, РезультатОперации.КоличествоПолученных);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СинхронизацияПослеБазовойАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ДополнительныеПараметры.Вставить("АутентификацияПроизводилась", Истина);
		РезультатОтправки = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияСинхронизацииСбербанк(
			ДополнительныеПараметры.НастройкаОбмена);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ДополнительныеПараметры.ВыводитьОкноОжидания;
		Оповещение = Новый ОписаниеОповещения("ПослеСинхронизацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатОтправки, Оповещение, ПараметрыОжидания);
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(ВидОперации, "GA==", ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьВыпискуСбербанкаПослеВопросаОбИхНаличии(РезультатВопроса, Параметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат
	ИначеЕсли НЕ РезультатВопроса Тогда
		
		Если Параметры.РеквизитыНастройкиОбмена.ИспользуетсяКриптография Тогда
			ОповещениеПослеАутентификацииНаТокене = Новый ОписаниеОповещения(
				"ОпределитьСертификатПодписиПослеАутентификацииНаТокенеСбербанк", ЭтотОбъект, Параметры);
			АутентифицироватьсяНаТокенеСбербанка(ОповещениеПослеАутентификацииНаТокене,
				Параметры.РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля, Параметры.НастройкаОбмена);
		Иначе
			Обработчик = Новый ОписаниеОповещения(
				"ПолучитьВыпискиПослеБазовойАутентификацииСбербанк", ЭтотОбъект, Параметры);
			ВыполнитьАутентификациюПоЛогинуСбербанк(Обработчик, Параметры.РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля,
				Параметры.НастройкаОбмена, Параметры.НастройкаОбмена);
		КонецЕсли;
	Иначе
		Результат = Новый Структура("Успех, Выписки", Истина, Параметры.ГотовыеВыписки);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияВыписки, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьВыпискиПослеБазовойАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ПараметрыФормы = Новый Структура("НастройкаОбмена, ГотовыеВыписки, ДатаНачала, ДатаОкончания, НомерСчета");
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, ДополнительныеПараметры);
		ПараметрыФормы.Вставить("ВидОперации", "ПолучениеВыписки");
		ПараметрыФормы.Вставить("ГотовыеВыписки", Новый Массив);
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписки", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(ВидОперации, "GA==", ДополнительныеПараметры.НастройкаОбмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьРезультатОтправкиДокументовВСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, Результат);
	ОтправкаСообщенийОбмена(ДополнительныеПараметры);
	
КонецПроцедуры

Функция СписокОшибокАутентификацииПоСертификатуСбербанк()
	
	СписокКодовСбербанк = Новый Соответствие;
	
	Описание = НСтр("ru = 'Сертификат не найден или не валидный или учетная запись заблокирована.'");
	СписокКодовСбербанк.Вставить("AQ==", Новый Структура("Код, Описание", "01", Описание));
	
	Описание = НСтр("ru = 'Истек срок действия сертификата.'");
	СписокКодовСбербанк.Вставить("Aw==", Новый Структура("Код, Описание", "03", Описание));
	
	Описание = НСтр("ru = 'Офис организации пользователя заблокирован.'");
	СписокКодовСбербанк.Вставить("BA==", Новый Структура("Код, Описание", "04", Описание));
	
	Описание = НСтр("ru = 'В аутентификации отказано ФРОД-мониторингом.'");
	СписокКодовСбербанк.Вставить("BQ==", Новый Структура("Код, Описание", "05", Описание));
	
	Описание = НСтр("ru = 'IP изменился.'");
	СписокКодовСбербанк.Вставить("Bg==", Новый Структура("Код, Описание", "06", Описание));
	
	Описание = НСтр("ru = 'Финансовый договор заблокирован.'");
	СписокКодовСбербанк.Вставить("Bw==", Новый Структура("Код, Описание", "07", Описание));
	
	Описание = НСтр("ru = 'Ошибка доступа к серверу.'");
	СписокКодовСбербанк.Вставить("CA==", Новый Структура("Код, Описание", "08", Описание));
	
	Описание = НСтр("ru = 'Не специфицированная ошибка.'");
	СписокКодовСбербанк.Вставить("CQ==", Новый Структура("Код, Описание", "09", Описание));
	
	Описание = НСтр("ru = 'Слишком частая ошибка входа в систему.'");
	СписокКодовСбербанк.Вставить("Cg==", Новый Структура("Код, Описание", "0A", Описание));
	
	Описание = НСтр("ru = 'Учетная запись отключена.'");
	СписокКодовСбербанк.Вставить("Cw==", Новый Структура("Код, Описание", "0B", Описание));
	
	Описание = НСтр("ru = 'Точка входа недоступна.'");
	СписокКодовСбербанк.Вставить("DA==", Новый Структура("Код, Описание", "0C", Описание));
	
	Описание = НСтр("ru = 'Ожидается заключение договора.'");
	СписокКодовСбербанк.Вставить("DQ==", Новый Структура("Код, Описание", "0D", Описание));
	
	Описание = НСтр("ru = 'Договор закрыт.'");
	СписокКодовСбербанк.Вставить("Dg==", Новый Структура("Код, Описание", "0E", Описание));
	
	Описание = НСтр("ru = 'Доступ закрыт настройками клиента.'");
	СписокКодовСбербанк.Вставить("Dw==", Новый Структура("Код, Описание", "0F", Описание));
	
	Описание = НСтр("ru = 'У пользователя в настройках отсутствует точка входа УПШ.'");
	СписокКодовСбербанк.Вставить("EA==", Новый Структура("Код, Описание", "10", Описание));

	Описание = НСтр("ru = 'У пользователя в настройках отсутствует точка входа УПШ_Холдинг.'");
	СписокКодовСбербанк.Вставить("EQ==", Новый Структура("Код, Описание", "11", Описание));
	
	Описание = НСтр("ru = 'Пользователь не найден по параметрам сертификата.'");
	СписокКодовСбербанк.Вставить("Eg==", Новый Структура("Код, Описание", "12", Описание));
	
	Возврат СписокКодовСбербанк;
		
КонецФункции

// Отправляет данные на сервер банка и получает ответ в виде тикета.
// 
// Параметры:
//    Оповещение - ОписаниеОповещения - содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//     * Тикет - Строка - полученный ответ банка;
//             - Неопределено - при возникновении ошибки.
//     * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//    Данные - Строка - данные для отправки в банк
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена с банком.
//
Процедура ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, Данные, НастройкаОбмена = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиЗапросов", Оповещение);
	ДополнительныеПараметры.Вставить("Данные", Данные);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеПодключенияВК1С = Новый ОписаниеОповещения(
		"ОтправитьЗапросыПослеПодключенияВК1ССбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьВнешнююКомпоненту1СДляСбербанка(ОповещениеПослеПодключенияВК1С);
	
КонецПроцедуры

Процедура ОтправитьЗапросыПослеПодключенияВК1ССбербанк(ПодключаемыйМодуль1С, ДополнительныеПараметры) Экспорт
	
	Если ПодключаемыйМодуль1С = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиЗапросов);
		Возврат;
	КонецЕсли;
	
	ИдентификаторСессии = ЗначениеИзКешаСбербанк("ИдентификаторСессии");
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросовСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуОтправкиЗапросовСбербанк", ЭтотОбъект);
	
	Ответ = "";
	
	ПодключаемыйМодуль1С.НачатьВызовПослатьЗапросыSRP(
		Оповещение, ДополнительныеПараметры.Данные, ИдентификаторСессии, Ответ);
	
КонецПроцедуры

Процедура ТестНастройкиОбменаПослеБазовойАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(ВидОперации, "GA==", ДополнительныеПараметры.НастройкаОбмена);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
	
КонецПроцедуры

Процедура ОтправитьДокументыПослеБазовойАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		
		Если ДополнительныеПараметры.МассивСообщенийОбмена.Количество() Тогда
			ПараметрыОтправки = Новый Структура;
			ПараметрыОтправки.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
			ПараметрыОтправки.Вставить("МассивСообщенийОбмена", ДополнительныеПараметры.МассивСообщенийОбмена);
			ПараметрыОтправки.Вставить("ПолучитьСтатусыДокументов", Истина);
			ВидЭДПлатежноеПоручение =ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ПлатежноеПоручение"); 
			ПараметрыОтправки.Вставить("ВидЭД", ВидЭДПлатежноеПоручение);
			ПараметрыОтправки.Вставить("ВыводитьПрогрессВыполнения", Ложь);

			РезультатОтправки = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПоОтправкеДокументовПоЛогинуВСбербанк(
				ПараметрыОтправки);
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
			Оповещение = Новый ОписаниеОповещения(
				"ПослеОтправкиПлатежныхДокументовСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатОтправки, Оповещение, ПараметрыОжидания);
		Иначе // требуется подтверждение документов
			Оповещение = Новый ОписаниеОповещения(
				"ПослеПодтвержденияДокументовПоНастройкеОбменаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ВыполнитьПодтверждениеДокументовСбербанк(Оповещение, ДополнительныеПараметры.НастройкаОбмена,
				ДополнительныеПараметры.МассивСообщенийТребующихПодтверждение);
		КонецЕсли;
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(
			ВидОперации, "GA==", ДополнительныеПараметры.НастройкаОбмена);
		ОтправитьДокументыПоЛогинуВСбербанк(ДополнительныеПараметры.ОповещениеПослеОтправкиДокументов,
			ДополнительныеПараметры.ДанныеДляОтправки);
	Иначе // ошибка, переход к следующей настройке обмена
		ОтправитьДокументыПоЛогинуВСбербанк(ДополнительныеПараметры.ОповещениеПослеОтправкиДокументов,
			ДополнительныеПараметры.ДанныеДляОтправки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеОтправкиПлатежныхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // задание было отменено
		ОтправитьДокументыПоЛогинуВСбербанк(ДополнительныеПараметры.ОповещениеПослеОтправкиДокументов,
			ДополнительныеПараметры.ДанныеДляОтправки, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Отправка платежных документов в Сбербанк.'");
		ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки,
			ДополнительныеПараметры.НастройкаОбмена);
		ОтправитьДокументыПоЛогинуВСбербанк(ДополнительныеПараметры.ОповещениеПослеОтправкиДокументов,
			ДополнительныеПараметры.ДанныеДляОтправки, ДополнительныеПараметры);
	Иначе // выполнено
		РезультатОтправки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОтправки.ТребуетсяАутентификация Тогда // неверный идентификатор сессии.
			ВидОперации = НСтр("ru = 'Отправка платежных документов в Сбербанк.'");
			ТекстСообщения = НСтр("ru = 'Неверный идентификатор сессии.
										|Обратитесь в техническую поддержку.'");
			ОбработатьОшибку(ВидОперации, ТекстСообщения, ТекстСообщения, ДополнительныеПараметры.НастройкаОбмена);
			ОтправитьДокументыПоЛогинуВСбербанк(ДополнительныеПараметры.ОповещениеПослеОтправкиДокументов,
				ДополнительныеПараметры.ДанныеДляОтправки, ДополнительныеПараметры);
		Иначе
			ОбработатьРезультатОтправкиДокументовПоНастройкеСбербанк(РезультатОтправки, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьРезультатОтправкиДокументовПоНастройкеСбербанк(РезультатОтправки, ДополнительныеПараметры)
	
	Оповестить("ОтправленоDirectBank", РезультатОтправки.ОтправленныеДокументы);
	ДополнительныеПараметры.КоличествоОтправленных =
		ДополнительныеПараметры.КоличествоОтправленных + ДополнительныеПараметры.МассивСообщенийОбмена.Количество();
	ДополнительныеПараметры.КоличествоПолученных =
		ДополнительныеПараметры.КоличествоПолученных + РезультатОтправки.КоличествоПолученных;
		
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДополнительныеПараметры.МассивСообщенийТребующихПодтверждение,
		РезультатОтправки.СообщенияТребуютПодтверждения, Истина);
	
	Если ДополнительныеПараметры.МассивСообщенийТребующихПодтверждение.Количество() Тогда
		РеквизитыНастройкиОбмена = Новый Структура("ОтправлятьДокументыБезПодписиSMS");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			ДополнительныеПараметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
			
		Если РеквизитыНастройкиОбмена.ОтправлятьДокументыБезПодписиSMS Тогда
			ОтправитьДокументыПоЛогинуВСбербанк(ДополнительныеПараметры.ОповещениеПослеОтправкиДокументов,
				ДополнительныеПараметры.ДанныеДляОтправки, ДополнительныеПараметры);
		Иначе
			Оповещение = Новый ОписаниеОповещения(
				"ПослеПодтвержденияДокументовПоНастройкеОбменаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ВыполнитьПодтверждениеДокументовСбербанк(Оповещение, ДополнительныеПараметры.НастройкаОбмена,
				ДополнительныеПараметры.МассивСообщенийТребующихПодтверждение);
		КонецЕсли;
	Иначе
		ОтправитьДокументыПоЛогинуВСбербанк(ДополнительныеПараметры.ОповещениеПослеОтправкиДокументов,
			ДополнительныеПараметры.ДанныеДляОтправки, ДополнительныеПараметры);
	КонецЕсли;

КонецПроцедуры

Процедура ПослеПодтвержденияДокументовПоНастройкеОбменаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.КоличествоПодтвержденных = ДополнительныеПараметры.КоличествоПодтвержденных + Результат;
	
	ОтправитьДокументыПоЛогинуВСбербанк(ДополнительныеПараметры.ОповещениеПослеОтправкиДокументов,
		ДополнительныеПараметры.ДанныеДляОтправки, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПолучитьФродПараметрыПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Подключено Тогда
		ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
	
		Оповещение = Новый ОписаниеОповещения(
			"ПолучитьДанныеАутентификацииПослеПолученияФродПараметровСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
		ПолучитьПараметрыФродМониторингаСбербанк(Оповещение, Результат.ПодключаемыйМодуль);
	Иначе
		Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДанныеАутентификацииПослеПолученияФродПараметровСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ФродПараметры", Результат);
	
	ДанныеАутентификации = Неопределено;
	
	Если ДополнительныеПараметры.ЛогинПароль = Неопределено Или НЕ ЗначениеЗаполнено(ДополнительныеПараметры.ЛогинПароль.Логин) Тогда
		Если ПолученыДанныеАвторизации(ДополнительныеПараметры.НастройкаОбмена, ДанныеАутентификации) Тогда
			АутентификацияНаСервереПослеПолученияДанныхАутентификацииСбербанк(ДанныеАутентификации, ДополнительныеПараметры);
		Иначе
			Оповещение = Новый ОписаниеОповещения(
				"АутентификацияНаСервереПослеПолученияДанныхАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ПолучитьДанныеАутентификации(Оповещение, ДополнительныеПараметры.НастройкаОбмена);
		КонецЕсли;
	Иначе
		АутентификацияНаСервереПослеПолученияДанныхАутентификацииСбербанк(ДополнительныеПараметры.ЛогинПароль, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура АутентификацияНаСервереПослеПолученияДанныхАутентификацииСбербанк(
	Результат,
	ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("Логин", Результат.Логин);
	ДополнительныеПараметры.Вставить("Пароль", Результат.Пароль);
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияАутентификацииПоЛогинуСбербанк(
		ДополнительныеПараметры.НастройкаОбмена, Результат.Логин, Результат.Пароль,
		ДополнительныеПараметры.ФродПараметры);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания = ДополнительныеПараметры.ВыводитьОкноОжидания;
	Оповещение = Новый ОписаниеОповещения("ПослеАутентификацииПоЛогинуСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);

КонецПроцедуры

Процедура ОтправитьОдноразовыйПарольПослеВводаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Ошибка = Ложь; ТребуетсяСменаПароля = Ложь;
	
	ОбменСБанкамиСлужебныйВызовСервера.ОтправитьОдноразовыйПарольВСбербанк(
		ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.КлючСессии, Результат, Ошибка, ТребуетсяСменаПароля);
		
	Если Ошибка Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Если ТребуетсяСменаПароля Тогда
		Обработчик = Новый ОписаниеОповещения("СменитьПарольПослеВводаНовогоСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.СменаПароля", , ЭтотОбъект, , , , Обработчик);
	Иначе
		ДополнительныеПараметры.СтруктураВозврата.Успех = Истина;
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

Процедура СменитьПарольПослеВводаНовогоСбербанк(НовыйПароль, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(НовыйПароль) Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияСменыПароляСбербанк(
		ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.КлючСессии, ДополнительныеПараметры.Логин,
		ДополнительныеПараметры.Пароль, НовыйПароль, ДополнительныеПараметры.Соль, ДополнительныеПараметры.B);
		
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		Оповещение = Новый ОписаниеОповещения("ПослеСменыПароляСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеСменыПароляСбербанк(Результат, ДополнительныеПараметры)
	КонецЕсли;

КонецПроцедуры

Процедура ТихийСтартПослеПодключенияВКСбербанка(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Подключено Тогда
		Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
	
	Если Не ДополнительныеПараметры.ЭтоТест И НЕ ДополнительныеПараметры.ПринудительнаяАутентификация
		И ЗначениеИзКешаСбербанк("АутентификацияНаТокенеВыполнена") = Истина
		И ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена)
		И ЗначениеИзКешаСбербанк("ТекущаяНастройкаОбмена") = ДополнительныеПараметры.НастройкаОбмена Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ОпределитьНеобходимостьПовторнойАутентификацииНаТокенеПослеПолученияСпискаБизнесСистемСбербанк", ЭтотОбъект,
			ДополнительныеПараметры);
		ПолучитьБизнесСистемыНаТокенеСбербанк(Оповещение, Результат.ПодключаемыйМодуль);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьПараметрыТокенаПослеТихогоСтартаСбербанк", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуТихогоСтартаСбербанк", ЭтотОбъект);
	Результат.ПодключаемыйМодуль.НачатьВызовТихийСтарт(Оповещение);
	
КонецПроцедуры

Процедура ПослеОтправкиДанныхВСбербанк(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено







		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВидОперации = НСтр("ru = 'Отправка документов в Сбербанк'");
		ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки,
			ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		Если РезультатОперации.ТребуетсяАутентификация Тогда
			Если ДополнительныеПараметры.АутентификацияПроизводилась Тогда
				ТекстОшибки = НСтр("ru = 'Ошибка аутентификации на сервере банка'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
				Результат = Новый Структура("Успех", Ложь);
				ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
				Возврат;
			Иначе
				Обработчик = Новый ОписаниеОповещения(
					"ОтправитьДанныеПослеБазовойАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
				ВыполнитьАутентификациюПоЛогинуСбербанк(Обработчик, ДополнительныеПараметры.ИмяВнешнегоМодуля,
					ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.НастройкаОбмена);
			КонецЕсли
		Иначе // данные были отправлены
			Результат = Новый Структура("Успех, РезультатОперации", Истина, РезультатОперации);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДанныеСертификатовПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Подключено Тогда
		ДополнительныеПараметры.Контекст.ОписаниеОшибки = Результат.ОписаниеОшибки;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, ДополнительныеПараметры.Контекст);
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеПолученияСпискаСертификатов = Новый ОписаниеОповещения(
		"ПроверитьУстановкуПодписиПолученияСертификатовСТокенаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьДанныеСертификатовСТокенаСбербанк(ОповещениеПослеПолученияСпискаСертификатов, Результат.ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ПроверитьПодписьПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Подключено Тогда
		Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписи);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатПроверкиПодписиНаТокенеСбербанк", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПослеПроверкиПодписиНаТокенеСбербанк", ЭтотОбъект);
		
	СвойстваСертификата = ОбменСБанкамиСлужебныйВызовСервера.СвойстваСертификатаКриптографии(
		ДополнительныеПараметры.ДвоичныеДанныеСертификата);
		
	СертификатPEM = СертификатВФорматеBase64(ДополнительныеПараметры.ДвоичныеДанныеСертификата);
		
	Если СвойстваСертификата.АлгоритмПубличногоКлюча = "GOST R 34.10-2012-256" Тогда
		Результат.ПодключаемыйМодуль.НачатьВызовПроверитьПодписьДанныхЧерезVPNKeyTLSCMS(Оповещение,
			ДополнительныеПараметры.ПодписанныеДанные, ДополнительныеПараметры.Подпись, "1", СертификатPEM);
	Иначе
		Результат.ПодключаемыйМодуль.НачатьВызовПроверитьПодписьДанныхЧерезVPNKeyTLS(Оповещение,
			ДополнительныеПараметры.ПодписанныеДанные, ДополнительныеПараметры.Подпись, СертификатPEM);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьСертификатПодписиПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Подключено Тогда
		Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, Неопределено);
		ОчиститьДанныеАвторизацииСбербанк();
		Возврат;
	КонецЕсли;

	ОповещениеПослеПолученияСертификатов = Новый ОписаниеОповещения(
		"ПодобратьСертификатПослеПолученияСпискаСТокенаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьДанныеСертификатовСТокенаСбербанк(ОповещениеПослеПолученияСертификатов, Результат.ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ПодписатьДанныеПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Подключено Тогда
		ВозвращаемоеЗначение = Новый Структура("Успех, ТекстОшибки", Ложь, Результат.ОписаниеОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, ВозвращаемоеЗначение);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПодписатьДанныеПослеПолученияИнформацииОТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьИнформациюОТокенеСбербанк(Оповещение, Результат.ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура УстановитьКаналПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Подключено Тогда
		ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
	
		Оповещение = Новый ОписаниеОповещения(
			"УстановитьКаналПослеОпределенияНомераБизнесСистемыСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
		НазваниеБизнесСистемы = ОбменСБанкамиСлужебныйВызовСервера.НазваниеБизнесСистемыСбербанк();
		ОпределитьНомерБизнесСистемыСбербанк(Оповещение, Результат.ПодключаемыйМодуль, НазваниеБизнесСистемы);
	Иначе
		Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиВиртуальногоКанала, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПровестиАутентификациюПослеПодключенияВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Подключено Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПровестиАутентификациюПослеПолученияФродПараметровСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ПолучитьПараметрыФродМониторингаСбербанк(Оповещение, Результат.ПодключаемыйМодуль);
	Иначе
		Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьСинхронизациюПослеАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеЗакрытияФормы = Новый ОписаниеОповещения(
		"ПослеЗакрытияФормыСинхронизацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
	ПараметрыФормы.Вставить("ВидОперации", "ВыполнениеСинхронизации");
	
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , ,
		ОповещениеПослеЗакрытияФормы, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

Процедура ПослеОтправкиПлатежныхПорученийВСбербанк(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Число") Тогда
		ДополнительныеПараметры.КоличествоОтправленных = ДополнительныеПараметры.КоличествоОтправленных + Результат;
	КонецЕсли;
	
	ОтправитьДокументыВСбербанк(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура СформироватьЗапросыВыписокПослеПолученияОтпечатков(ОтпечаткиСертификатов, Параметры) Экспорт
	
	МассивОтпечатков = Новый Массив;
	Если ТипЗнч(ОтпечаткиСертификатов) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из ОтпечаткиСертификатов Цикл
			МассивОтпечатков.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	ИначеЕсли Не ЭлектроннаяПодписьКлиент.СоздаватьЭлектронныеПодписиНаСервере() Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	НастройкиОбмена = Неопределено;
	ПараметрыЗапроса = ОбменСБанкамиКлиентСервер.ПараметрыПолученияВыпискиБанка();
	МассивЗапросов = ОбменСБанкамиСлужебныйВызовСервера.ЗапросыВыписок(
		НастройкаОбмена, ПараметрыЗапроса, МассивОтпечатков, НастройкиОбмена, Истина);
		
	Если НЕ МассивЗапросов.Количество() Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеСинхронизации, Ложь);
		Возврат;
	КонецЕсли;
	
	ВидЭДЗапросВыписки = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросВыписки");
	Если НЕ ОбменСБанкамиКлиентСервер.ЕстьДоступныеСертификаты(НастройкиОбмена, ВидЭДЗапросВыписки) Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеСинхронизации, Ложь);
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("МассивЗапросовВыписок", МассивЗапросов);
	Параметры.Вставить("ОтпечаткиСертификатов", ОтпечаткиСертификатов);
	Оповещение = Новый ОписаниеОповещения("ОтправитьЗапросыВыписокПослеПодписания", ЭтотОбъект, Параметры);
	Подписать(Оповещение, МассивЗапросов, НастройкиОбмена.ДоступныеСертификаты);
	
КонецПроцедуры

Процедура ОтправитьЗапросыВыписокПослеПодписания(Результат, ДополнительныеПараметры) Экспорт
	
	ДанныеАвторизации = Неопределено;

	Если НЕ Результат.Успех Тогда
		ДополнительныеПараметры.Удалить("МассивЗапросовВыписок");
	КонецЕсли;
	
	Если ДополнительныеПараметры.РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		ПослеПолученияОтпечатковВыполнитьОбмен(ДополнительныеПараметры.ОтпечаткиСертификатов, ДополнительныеПараметры);
	ИначеЕсли ПарольКСертификатуПолучен(ДополнительныеПараметры.НастройкаОбмена, ДанныеАвторизации) Тогда
		Если ДанныеАвторизации = Неопределено Тогда
			ДополнительныеПараметры.Вставить("ТребуетсяАвторизация", Ложь);
		КонецЕсли;
		ОтправитьДокументыВБанк(ДанныеАвторизации, ДополнительныеПараметры);
	Иначе
		Оповещение = Новый ОписаниеОповещения("ОтправитьДокументыВБанк", ЭтотОбъект, ДополнительныеПараметры);
		ПолучитьДанныеАутентификации(Оповещение, ДополнительныеПараметры.НастройкаОбмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеАутентификацииПоЛогинуСбербанк(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		УдалитьПарольИзСеанса(ДополнительныеПараметры.КлючСессии);
		ТекстСообщения = РезультатЗадания.КраткоеПредставлениеОшибки;
		Если ТекстСообщения = НСтр("ru = 'Неверный логин/пароль или учетная запись заблокирована.'") Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Иначе
			ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка'");
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстСообщения);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);
	Иначе // выполнено
		ПараметрыВозврата = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		Если ПараметрыВозврата.ТребуетсяТокен Тогда
			ДополнительныеПараметры.СтруктураВозврата.ТребуетсяТокен = Истина;
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации,
				ДополнительныеПараметры.СтруктураВозврата);
		ИначеЕсли ПараметрыВозврата.ТребуетсяСМСАутентификация Тогда
			ПараметрыСессии = Новый Структура("ИдентификаторСессии, ФродПараметры, Логин");
			ЗаполнитьЗначенияСвойств(ПараметрыСессии, ДополнительныеПараметры);
			ПараметрыСессии.ИдентификаторСессии = ПараметрыВозврата.ИдентификаторСессии;
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьСессиюНаСервереСбербанк(
				ДополнительныеПараметры.КлючСессии, ПараметрыСессии);
			ДополнительныеПараметры.Вставить("B", ПараметрыВозврата.B);
			ДополнительныеПараметры.Вставить("Соль", ПараметрыВозврата.Соль);
			Оповещение = Новый ОписаниеОповещения(
				"ОтправитьОдноразовыйПарольПослеВводаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросОдноразовогоПароля", , ЭтотОбъект, , , , Оповещение);
		Иначе
			ПараметрыСессии = Новый Структура("ИдентификаторСессии, ФродПараметры, Логин");
			ЗаполнитьЗначенияСвойств(ПараметрыСессии, ДополнительныеПараметры);
			ПараметрыСессии.ИдентификаторСессии = ПараметрыВозврата.ИдентификаторСессии;
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьСессиюНаСервереСбербанк(
				ДополнительныеПараметры.КлючСессии, ПараметрыСессии);
			ДополнительныеПараметры.СтруктураВозврата.Успех = Истина;
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации,
				ДополнительныеПараметры.СтруктураВозврата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеСменыПароляСбербанк(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания =  Неопределено Тогда
	ИначеЕсли РезультатЗадания.Статус = "Ошибка" Тогда
		ТекстСообщения = РезультатЗадания.КраткоеПредставлениеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		ДополнительныеПараметры.СтруктураВозврата.Успех = Истина;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеБазовойАутентификации, ДополнительныеПараметры.СтруктураВозврата);

КонецПроцедуры

Процедура ОпределитьСертификатПодписиПослеАутентификацииНаТокенеСбербанк(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьВыпискуПослеОпределенияСертификатаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОпределитьСертификатПодписиСбербанк(Оповещение, ДополнительныеПараметры.РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля,
		ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ПолучитьВыпискуПослеОпределенияСертификатаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ЗакешироватьПараметрСбербанка("СертификатПодписи", Результат.СертификатСсылка);
	
	ПараметрыФормы = Новый Структура("НастройкаОбмена, ДатаНачала, ДатаОкончания, НомерСчета");

	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ДополнительныеПараметры);
	ПараметрыФормы.Вставить("ВидОперации", "ПолучениеВыписки");
	ПараметрыФормы.Вставить("ИдентификаторСертификатаСбербанк", Результат.ИдентификаторСертификата);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписки", ЭтотОбъект, ДополнительныеПараметры);

	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	
КонецПроцедуры

Процедура ВключитьЖурналированиеПослеПодключенияВК1СДляСбербанка(ПодключаемыйМодуль1С, ДополнительныеПараметры) Экспорт
		
	Если ПодключаемыйМодуль1С = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль1С", ПодключаемыйМодуль1С);
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
		
		ПараметрыЖурналирования = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыЖурналирования(
			ДополнительныеПараметры.НастройкаОбмена);
			
		Если ПараметрыЖурналирования.ИспользоватьЖурналирование Тогда
			
			СистемнаяИнформация = Новый СистемнаяИнформация;
			Если ПараметрыЖурналирования.ИдентификаторКлиента = СистемнаяИнформация.ИдентификаторКлиента Тогда
				Оповещение = Новый ОписаниеОповещения;
				ПодключаемыйМодуль1С.НачатьВызовНачатьЖурналирование(Оповещение, ПараметрыЖурналирования.КаталогДляЖурналирования);
			Иначе
				ПараметрыФормы = Новый Структура("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
				Оповещение = Новый ОписаниеОповещения("ПослеУточненияКаталогаЖурналированияСбербанк", ЭтотОбъект, ДополнительныеПараметры);
				ОткрытьФорму("РегистрСведений.ПараметрыОбменСБанками.Форма.ЗапросКаталога", ПараметрыФормы, , , , , Оповещение);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ПредварительнаяАутентификацияЧерезТокенСбербанк(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ПослеУточненияКаталогаЖурналированияСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		Оповещение = Новый ОписаниеОповещения;
		ДополнительныеПараметры.ПодключаемыйМодуль1С.НачатьВызовНачатьЖурналирование(Оповещение, Результат);
	КонецЕсли;
	
	ПредварительнаяАутентификацияЧерезТокенСбербанк(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ПредварительнаяАутентификацияЧерезТокенСбербанк(ДополнительныеПараметры)
	
	ИнформацияОСертификате = ОбменСБанкамиСлужебныйВызовСервера.ИнформацияОСертификатеПодписиСбербанк(
			ДополнительныеПараметры.ДанныеСертификата.ДвоичныеДанныеСертификата);
			
	ДополнительныеПараметры.Вставить("ИнформацияОСертификате", ИнформацияОСертификате);
	СерийныйНомерСертификата = СтрЗаменить(Строка(ИнформацияОСертификате.SN), " ", "");
	ДополнительныеПараметры.Вставить("СерийныйНомерСертификата", СерийныйНомерСертификата);

	Оповещение = Новый ОписаниеОповещения("ПослеПредварительнойАутентификацииПоСертификатуСбербанк", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПредварительнойАутентификацииПоСертификатуСбербанк", ЭтотОбъект);
	
	Соль = ""; ИдентификаторСессииBase64 = ""; КодВозврата = "";
	
	ДополнительныеПараметры.ПодключаемыйМодуль1С.НачатьВызовПреАутентификация(
		Оповещение, ИнформацияОСертификате.issuer, СерийныйНомерСертификата, Соль, ИдентификаторСессииBase64, КодВозврата);
	
КонецПроцедуры

Процедура ПослеЗакрытияФормыПодписаниеСбербанк(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Отмена ИЛИ Результат = Неопределено Тогда
		Результат = Новый Структура("Успех", Ложь);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, Результат);
	
КонецПроцедуры

Процедура ОтправитьДокументыПоЛогинуВСбербанк(Оповещение, ДанныеДляОтправки, ДополнительныеПараметры = Неопределено)
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("КоличествоОтправленных", 0);
		ДополнительныеПараметры.Вставить("КоличествоПолученных", 0);
		ДополнительныеПараметры.Вставить("КоличествоПодтвержденных", 0);
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ДанныеДляОтправки Цикл
		ДополнительныеПараметры.Вставить("НастройкаОбмена", КлючЗначение.Ключ);
		ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", КлючЗначение.Значение.МассивСообщенийОбмена);
		ДополнительныеПараметры.Вставить(
			"МассивСообщенийТребующихПодтверждение", КлючЗначение.Значение.МассивСообщенийТребующихПодтверждение);
		ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиДокументов", Оповещение);
		ДополнительныеПараметры.Вставить("ИмяВнешнегоМодуля", КлючЗначение.Значение.ИмяВнешнегоМодуля);
		
		ДанныеДляОтправки.Удалить(КлючЗначение.Ключ);
		ДополнительныеПараметры.Вставить("ДанныеДляОтправки", ДанныеДляОтправки);
		
		РезультатОтправки = Неопределено;
		Если КлючЗначение.Значение.Свойство("РезультатОтправки", РезультатОтправки) Тогда // была попытка отправки на сервере
			ОбработатьРезультатОтправкиДокументовПоНастройкеСбербанк(РезультатОтправки, ДополнительныеПараметры);
		Иначе
			Обработчик = Новый ОписаниеОповещения(
				"ОтправитьДокументыПослеБазовойАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ВыполнитьАутентификациюПоЛогинуСбербанк(
				Обработчик, КлючЗначение.Значение.ИмяВнешнегоМодуля, КлючЗначение.Ключ, КлючЗначение.Ключ);
		КонецЕсли;
		Возврат;
	КонецЦикла;
	
	СтруктураВозврата = Новый Структура("КоличествоОтправленных, КоличествоПолученных, КоличествоПодтвержденных");
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, ДополнительныеПараметры);
	СтруктураВозврата.Вставить("КоличествоПодготовленных", СтруктураВозврата.КоличествоОтправленных);
	
	ВыполнитьОбработкуОповещения(Оповещение, СтруктураВозврата);
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеВыписки

Процедура ПослеПолученияВыписки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		СтруктураВозврата = Новый Структура("Успех", Ложь);
	Иначе
		СтруктураВозврата = Новый Структура("Успех, Выписки", Истина, Результат.ВыпискиБанка);
		Для Каждого Сообщение Из Результат.СообщенияПользователю Цикл
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияВыписки, СтруктураВозврата);
	
КонецПроцедуры

Процедура ПродолжитьОтправкуЗапросаВыпискиПослеРасшифровкиМаркера(Результат, Параметры) Экспорт

	Если Результат.Успех Тогда
		
		ПараметрыФормы = Новый Структура("ДанныеСертификата, ДатаНачала, ДатаОкончания, МассивСообщенийОбмена,
			|НастройкаОбмена, НомерСчета, СообщениеОбмена");
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, Параметры);
		ПараметрыФормы.Вставить("ИдентификаторСессии", Результат.РасшифрованныеДанные);
		ПараметрыФормы.Вставить("ВидОперации", "ПолучениеВыписки");
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписки", ЭтотОбъект, Параметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	Иначе
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияВыписки, Новый Структура("Успех, Выписки", Ложь));
	КонецЕсли;
	
КонецПроцедуры

// Запускает получение выписки после получения отпечатков.
// Является обработчиком оповещения
//
// Параметры:
//  Отпечатки - Соответствие - см. ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов
//              Строка - текст ошибки
//              Неопределено - пользователь отказался от установки расширения
//              Ложь - не удалось установить расширение
//  Параметры - Структура - второй параметр оповещения.
//
Процедура ПослеПолученияОтпечатковПолучитьВыпискуБанка(Отпечатки, Параметры) Экспорт
	
	МассивОтпечатков = Новый Массив;
	Если ТипЗнч(Отпечатки) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из Отпечатки Цикл
			МассивОтпечатков.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	ИначеЕсли Не ЭлектроннаяПодписьКлиент.СоздаватьЭлектронныеПодписиНаСервере() Тогда
		СтруктураВозврата = Новый Структура("Успех, Выписки", Ложь); 
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияВыписки, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	РеквизитыНастройкиОбмена = Параметры.РеквизитыНастройкиОбмена;
		
	ПараметрыЗапроса = ОбменСБанкамиКлиентСервер.ПараметрыПолученияВыпискиБанка();
	ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, Параметры);
	
	НастройкиОбмена = Неопределено;
	МассивЗапросов = ОбменСБанкамиСлужебныйВызовСервера.ЗапросыВыписок(
		НастройкаОбмена, ПараметрыЗапроса, МассивОтпечатков, НастройкиОбмена);
		
	Если НЕ МассивЗапросов.Количество() ИЛИ НастройкиОбмена = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияВыписки, Новый Структура("Успех, Выписки", Ложь));
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("МассивСообщенийОбмена", МассивЗапросов);
	
	Если НастройкиОбмена.Подписывать Тогда
		ВидЭДЗапросВыписки = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросВыписки");
		Если НЕ ОбменСБанкамиКлиентСервер.ЕстьДоступныеСертификаты(НастройкиОбмена, ВидЭДЗапросВыписки) Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияВыписки, Новый Структура("Успех, Выписки", Ложь));
			Возврат;
		КонецЕсли;
		
		Параметры.Вставить("МассивСертификатов", НастройкиОбмена.ДоступныеСертификаты);
		ПодписатьЗапросыВыписок(Неопределено, Параметры);
		Возврат;
	КонецЕсли;
		
	ПараметрыАвторизации = Новый Структура;
	
	Если РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		ДанныеДоступныхСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(НастройкаОбмена);
		Если ДанныеДоступныхСертификатов.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Нет доступных сертификатов для аутентификации на сервере банка.
										|Проверьте настройки прямого обмена с банком или обратитесь к администратору.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияВыписки, Новый Структура("Успех, Выписки", Ложь));
			Возврат;
		КонецЕсли;
		МассивСертификатов = Новый Массив;
		
		Для Каждого ДанныеСертификата Из ДанныеДоступныхСертификатов Цикл
			Если МассивОтпечатков.Найти(ДанныеСертификата.Значение.Отпечаток) <> Неопределено Тогда
				МассивСертификатов.Добавить(ДанныеСертификата.Ключ);
			КонецЕсли
		КонецЦикла;
		
		Если МассивСертификатов.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'На компьютере не найдены сертификаты для аутентификации на сервере банка.
										|Установите сертификаты в личное хранилище сертификатов операционной системы.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияВыписки, Новый Структура("Успех, Выписки", Ложь));
			Возврат;
		КонецЕсли;
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
		ОписаниеДанных.Вставить("ЗаголовокДанных", "");
			
		ДополнительныеПараметры = Новый Структура("НастройкаОбмена", НастройкаОбмена);
		ОписаниеПолученияДанных = Новый ОписаниеОповещения(
			"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ДополнительныеПараметры);
		ОписаниеДанных.Вставить("Данные", ОписаниеПолученияДанных);
				
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПродолжитьОтправкуЗапросаВыпискиПослеРасшифровкиМаркера", ЭтотОбъект, Параметры);
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, Новый УникальныйИдентификатор, ОписаниеОповещения);
	Иначе
		Если НЕ ПолученыДанныеАвторизации(НастройкаОбмена, ПараметрыАвторизации) Тогда
			Оповещение = Новый ОписаниеОповещения("ОткрытьФормуЗапросаВыписки", ЭтотОбъект, Параметры);
			ПолучитьДанныеАутентификации(Оповещение, НастройкаОбмена);
			Возврат;
		Иначе
			ОткрытьФормуЗапросаВыписки(ПараметрыАвторизации, Параметры);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область Криптография

Процедура ОбработатьРезультатПодписиЗапросовВыписок(Результат, ПараметрыОбработки) Экспорт
	
	Если Результат.Успех Тогда
		СтатусПодписан = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Подписан");
		ОбменСБанкамиСлужебныйВызовСервера.УстановитьСтатусыСообщенийОбмена(ПараметрыОбработки.МассивСообщенийОбмена, СтатусПодписан);
		ОтправитьЗапросыВыписокПослеПодписи(ПараметрыОбработки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьОчереднуюПодпись(ДополнительныеПараметры)
	
	ДополнительныеПараметры.ИндексТекущейПодписи = ДополнительныеПараметры.ИндексТекущейПодписи + 1;
	
	Если ДополнительныеПараметры.МассивПодписей.Количество() <= ДополнительныеПараметры.ИндексТекущейПодписи Тогда
		
		Если ДополнительныеПараметры.РезультатыПроверкиПодписей.Количество() Тогда
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьРезультатыПроверкиПодписей(
				ДополнительныеПараметры.СообщениеОбмена, ДополнительныеПараметры.РезультатыПроверкиПодписей);
		КонецЕсли;
		
		ПодписиВалидны = Истина;
		
		Для Каждого Запись Из ДополнительныеПараметры.РезультатыПроверкиПодписей Цикл
			Если НЕ Запись.ПодписьВерна Тогда
				ПодписиВалидны = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, ПодписиВалидны);
		
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеПроверкиПодписи = Новый ОписаниеОповещения(
		"ЗаписатьРезультатПослеПроверкиПодписи", ЭтотОбъект, ДополнительныеПараметры);
		
	ЭлектроннаяПодписьКлиент.ПроверитьПодпись(ОповещениеПослеПроверкиПодписи, ДополнительныеПараметры.ДвоичныеДанныеЭД,
		ДополнительныеПараметры.МассивПодписей[ДополнительныеПараметры.ИндексТекущейПодписи].Подпись);
	
КонецПроцедуры

Процедура ПолучитьОтпечаткиПослеУстановкиРасширения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(
			ДополнительныеПараметры.Оповещение, Истина, Не ДополнительныеПараметры.СоздаватьЭлектронныеПодписиНаСервере);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьЗапросыВыписокПослеПодписи(ПараметрыОбработки)
	
	РеквизитыНастройкиОбмена = ПараметрыОбработки.РеквизитыНастройкиОбмена;
	
	АсинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = АсинхронныйОбмен Тогда
		Если РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
			
			ОписаниеДанных = Новый Структура;
			ОписаниеДанных.Вставить("ОтборСертификатов", ПараметрыОбработки.МассивСертификатов);
			ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
			ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
			ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
			ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
			ОписаниеДанных.Вставить("ЗаголовокДанных", "");
			
			ДополнительныеПараметры = Новый Структура("НастройкаОбмена", ПараметрыОбработки.НастройкаОбмена);
			ОписаниеПолученияДанных = Новый ОписаниеОповещения(
				"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ДополнительныеПараметры);
			ОписаниеДанных.Вставить("Данные", ОписаниеПолученияДанных);
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПродолжитьОтправкуЗапросаВыпискиПослеРасшифровкиМаркера", ЭтотОбъект, ПараметрыОбработки);
			ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, Новый УникальныйИдентификатор, ОписаниеОповещения);
		Иначе
			ПараметрыАутентификации = Новый Структура;
			ПолученыДанныеАутентификации = ПолученыДанныеАвторизации(
				ПараметрыОбработки.НастройкаОбмена, ПараметрыАутентификации);
			Если ПолученыДанныеАутентификации Тогда
				ОткрытьФормуЗапросаВыписки(ПараметрыАутентификации, ПараметрыОбработки);
			Иначе
				Оповещение = Новый ОписаниеОповещения("ОткрытьФормуЗапросаВыписки", ЭтотОбъект, ПараметрыОбработки);
				ПолучитьДанныеАутентификации(Оповещение, ПараметрыОбработки.НастройкаОбмена);
			КонецЕсли;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ДанныеСертификата, ДатаНачала, ДатаОкончания, МассивСообщенийОбмена,
		|НастройкаОбмена, НомерСчета, СообщениеОбмена, ГотовыеВыписки, ПринудительноеПолучениеВыписки");
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ПараметрыОбработки);
	ПараметрыФормы.Вставить("ВидОперации", "ПолучениеВыписки");
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписки", ЭтотОбъект, ПараметрыОбработки);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область АсинхронныйОбмен

Процедура СинхронизироватьАсинхронныйОбмен(Параметры)
	
	ДанныеАвторизации = Неопределено;
	Если Параметры.РеквизитыНастройкиОбмена.ПодписываетсяЗапросВыписки Тогда
		Оповещение = Новый ОписаниеОповещения(
			"СформироватьЗапросыВыписокПослеПолученияОтпечатков", ЭтотОбъект, Параметры);
		ПолучитьОтпечаткиСертификатов(Оповещение);
	ИначеЕсли Параметры.РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтпечатковВыполнитьОбмен", ЭтотОбъект, Параметры);
		ПолучитьОтпечаткиСертификатов(Оповещение);
	ИначеЕсли ПарольКСертификатуПолучен(Параметры.НастройкаОбмена, ДанныеАвторизации) Тогда
		Если ДанныеАвторизации = Неопределено Тогда
			Параметры.Вставить("ТребуетсяАвторизация", Ложь);
		КонецЕсли;
		ОтправитьДокументыВБанк(ДанныеАвторизации, Параметры);
	Иначе
		Оповещение = Новый ОписаниеОповещения("ОтправитьДокументыВБанк", ЭтотОбъект, Параметры);
		ПолучитьДанныеАутентификации(Оповещение, Параметры.НастройкаОбмена);
	КонецЕсли;
		
КонецПроцедуры

#Область Аутентификация

// Вызывается из ЭлектроннаяПодписьКлиент.Расшифровать.
//
// Параметры:
//  Результат - Структура - содержит поля;
//     * ОписаниеДанных - Структура - значение первого параметра процедуры ЭлектроннаяПодписьКлиент.Расшифровать;
//     * Оповещение - ОписаниеОповещение - оповещение, которое должно быть вызвано после получения зашифрованного идентификатора сессии.
//  ДополнительныеПараметры - Произвольный - Параметры описания оповещения из поля Данные первого параметра процедуры ЭлектроннаяПодписьКлиент.Расшифровать.
//
Процедура ПолучитьЗашифрованныйИдентификаторСессии(Результат, ДополнительныеПараметры) Экспорт
	
	Попытка
		МаркерЗашифрованный = ОбменСБанкамиСлужебныйВызовСервера.ЗашифрованныйИдентификаторСессии(
			ДополнительныеПараметры.НастройкаОбмена, Результат.ОписаниеДанных.ВыбранныйСертификат);
	Исключение
		Результат.ОписаниеДанных.Вставить("ОписаниеОшибки", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
		
	Если НЕ МаркерЗашифрованный = Неопределено Тогда
		Результат.ОписаниеДанных.Вставить("Данные", МаркерЗашифрованный);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Результат.Оповещение, Результат.ОписаниеДанных);
	
КонецПроцедуры

Процедура ОтправитьСообщениеВБанкСАутентификациейЛогинПароль(ПараметрыАутентификации, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ПараметрыАутентификации) Тогда
		НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль(Параметры.Результат, Параметры);
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	РеквизитыНастройкиОбмена = Новый Структура("ПрограммаБанка, АдресСервера, ИдентификаторОрганизации, ВерсияФормата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(НастройкаОбмена, РеквизитыНастройкиОбмена);
	АсинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = АсинхронныйОбмен Тогда
		Обработчик = Новый ОписаниеОповещения("ОтправитьСообщениеВБанкПослеПолученияМаркера", ЭтотОбъект, Параметры);
		ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, РеквизитыНастройкиОбмена.АдресСервера,
			РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ПараметрыАутентификации, РеквизитыНастройкиОбмена.ВерсияФормата,
			НастройкаОбмена);
	Иначе
		МассивСообщенийОбменаКОтправке = Параметры.МассивСообщенийОбменаКОтправкеВБанкСАвторизациейЛогинПароль;
		СоотвНастроекОбменаИСтруктурСертификатов = Параметры.СоотвНастроекОбменаИСтруктурСертификатов;
		СоотвНастроекОбменаИСтруктурСертификатов.Вставить(Параметры.НастройкаОбмена, ПараметрыАутентификации);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль", ЭтотОбъект, Параметры);
		СформироватьИОтправитьПакетыВБанк(
			ОписаниеОповещения, МассивСообщенийОбменаКОтправке, СоотвНастроекОбменаИСтруктурСертификатов, Параметры.Фрод);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьСообщениеВБанкПослеПолученияМаркера(Маркер, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Маркер) Тогда
		НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль(Параметры.Результат, Параметры);
		Возврат;
	КонецЕсли;
	
	ПараметрыАутентификации = Новый Структура;
	ПараметрыАутентификации.Вставить("ИдентификаторСессии", Маркер);
	МассивСообщенийОбменаКОтправке = Параметры.МассивСообщенийОбменаКОтправкеВБанкСАвторизациейЛогинПароль;
	СоотвНастроекОбменаИСтруктурСертификатов = Параметры.СоотвНастроекОбменаИСтруктурСертификатов;
	СоотвНастроекОбменаИСтруктурСертификатов.Вставить(Параметры.НастройкаОбмена, ПараметрыАутентификации);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль", ЭтотОбъект, Параметры);
	
	СформироватьИОтправитьПакетыВБанк(
		ОписаниеОповещения, МассивСообщенийОбменаКОтправке, СоотвНастроекОбменаИСтруктурСертификатов, Параметры.Фрод);
	
КонецПроцедуры

Процедура РасшифроватьМаркерБанка(Результат, ДополнительныеПараметры) Экспорт

	Если НЕ ДополнительныеПараметры.Свойство("СоответствиеНастроекОбменаИСертификатов") Тогда
		ДополнительныеПараметры.Вставить("СоответствиеНастроекОбменаИСертификатов", Новый Соответствие);
	КонецЕсли;
	
	СоответствиеВозврата = ДополнительныеПараметры.СоответствиеНастроекОбменаИСертификатов;
	
	// В результате приходят расшифрованные данные маркера, поместим их в СоответствиеВозврата:
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		РасшифрованныеДанные = Неопределено;
		НастройкаОбмена = Неопределено;
		Если Результат.Свойство("РасшифрованныеДанные", РасшифрованныеДанные) Тогда
			Если ЭтоАдресВременногоХранилища(РасшифрованныеДанные) Тогда
				РасшифрованныеДанные = ПолучитьИзВременногоХранилища(РасшифрованныеДанные);
			КонецЕсли;
			Если ТипЗнч(РасшифрованныеДанные) = Тип("ДвоичныеДанные")
				И ДополнительныеПараметры.Свойство("НастройкаОбмена", НастройкаОбмена) Тогда
				
				ВыбранныйСертификат = Неопределено;
				Если Результат.Свойство("ВыбранныйСертификат", ВыбранныйСертификат)
					И ТипЗнч(ВыбранныйСертификат) = Тип("Структура")
					И ВыбранныйСертификат.Свойство("Ссылка", ВыбранныйСертификат)
					И ДополнительныеПараметры.СоотвСертификатовИИхСтруктур.Получить(ВыбранныйСертификат) <> Неопределено Тогда
					
					ПараметрыСертификата = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(
						ДополнительныеПараметры.СоотвСертификатовИИхСтруктур[ВыбранныйСертификат]);
					//@skip-warning
					ПараметрыСертификата.Вставить("ИдентификаторСессии", РасшифрованныеДанные);
					СоответствиеВозврата.Вставить(НастройкаОбмена, ПараметрыСертификата);
				Иначе
					СоответствиеВозврата.Вставить(НастройкаОбмена, Новый Структура("ИдентификаторСессии", РасшифрованныеДанные));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	ЗапуститьФинальныйОбработчикОповещения = Истина;
	ОбработчикОповещения = Неопределено;
	СоответствиеНастроекОбменаИСертификатов = Неопределено;
	Если ДополнительныеПараметры.Свойство("СоответствиеНастроекОбменаИСертификатов", СоответствиеНастроекОбменаИСертификатов)
		И ТипЗнч(СоответствиеНастроекОбменаИСертификатов) = Тип("Соответствие")
		И СоответствиеНастроекОбменаИСертификатов.Количество() > 0 Тогда
		
		Для Каждого Элемент Из СоответствиеНастроекОбменаИСертификатов Цикл
			НастройкаОбмена = Элемент.Ключ;
			Сертификаты = Элемент.Значение;
			
			Если НЕ (ТипЗнч(Сертификаты) = Тип("Массив") И ЗначениеЗаполнено(НастройкаОбмена))Тогда
				Продолжить;
			КонецЕсли;
			
			Маркер = Неопределено;
			МассивСертификатов = Новый Массив;
			Для Каждого Сертификат Из Сертификаты Цикл
				ПараметрыСтруктура = ДополнительныеПараметры.СоотвСертификатовИИхСтруктур.Получить(Сертификат);
				Если ПараметрыСтруктура.ПарольПолучен Тогда
					МассивСертификатов = Новый Массив;
					МассивСертификатов.Добавить(Сертификат);
					Прервать;
				Иначе
					МассивСертификатов.Добавить(Сертификат);
				КонецЕсли;
			КонецЦикла;
			// Если массив сертификатов пустой, значит либо уже есть расшифрованный маркер, либо нет сертификатов,
			// в обоих случаях переходим к обработке следующей Настройки обмена.
			Если МассивСертификатов.Количество() > 0 Тогда
				ОписаниеДанных = Новый Структура;
				ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
				ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
				ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
				ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
				ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
				ОписаниеДанных.Вставить("ЗаголовокДанных", "");

				ПараметрыЗапросаМаркера = Новый Структура("НастройкаОбмена", НастройкаОбмена);
				Маркер = Новый ОписаниеОповещения("ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ПараметрыЗапросаМаркера);
				ОписаниеДанных.Вставить("Данные", Маркер);
				
				ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
				ДополнительныеПараметры.Вставить("ОписаниеДанных", ОписаниеДанных);
				
				// Удалим из соответствия обработанный элемент:
				СоответствиеНастроекОбменаИСертификатов.Удалить(НастройкаОбмена);
				ОписаниеОповещения = Новый ОписаниеОповещения("РасшифроватьМаркерБанка", ЭтотОбъект, ДополнительныеПараметры);
				
				ЗапуститьФинальныйОбработчикОповещения = Ложь;
				ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОписаниеОповещения);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗапуститьФинальныйОбработчикОповещения
		И ДополнительныеПараметры.Свойство("ОбработчикОповещения", ОбработчикОповещения)
		И ТипЗнч(ОбработчикОповещения) = Тип("ОписаниеОповещения") Тогда
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("СоответствиеНастроекОбменаИПараметровСертификатов", СоответствиеВозврата);
		
		ВыполнитьОбработкуОповещения(ОбработчикОповещения, ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПодписанияЗапросаЗонда(Результат, ПараметрыОбработки) Экспорт
	
	Если Результат.Успех Тогда
		
		Если ПараметрыОбработки.РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		
			МассивСертификатов = Новый Массив;
			МассивСертификатов.Добавить(Результат.ВыбранныйСертификат.Ссылка);
			
			ОписаниеДанных = Новый Структура;
			ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
			ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
			ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
			ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
			ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
			ОписаниеДанных.Вставить("ЗаголовокДанных", "");
			
			ПараметрыЗапросаМаркера = Новый Структура("НастройкаОбмена", ПараметрыОбработки.НастройкаОбмена);
			ОбработчикПолученияМаркера = Новый ОписаниеОповещения(
				"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ПараметрыЗапросаМаркера);
				
			ОписаниеДанных.Вставить("Данные", ОбработчикПолученияМаркера);
				
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПродолжитьОтправкуЗапросаЗондаПослеРасшифровкиМаркера", ЭтотОбъект, ПараметрыОбработки);
			ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, Новый УникальныйИдентификатор, ОписаниеОповещения);
		Иначе
			Оповещение = Новый ОписаниеОповещения(
				"ПолучитьМаркерБанкаПослеВводаДанныхАутентификации", ЭтотОбъект, ПараметрыОбработки);
			ПолучитьДанныеАутентификации(Оповещение, ПараметрыОбработки.НастройкаОбмена);
		КонецЕсли;
	Иначе
		ВыполнитьОбработкуОповещения(ПараметрыОбработки.ОбработчикПослеТестаНастройки, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьМаркерБанкаПослеВводаДанныхАутентификации(ДанныеАутентификации, Параметры) Экспорт
	
	Если ДанныеАутентификации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ТестСвязиСБанкомAsync", ЭтотОбъект, Параметры);
		
	ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, Параметры.РеквизитыНастройкиОбмена.АдресСервера,
		Параметры.РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ДанныеАутентификации,
		Параметры.РеквизитыНастройкиОбмена.ВерсияФормата, Параметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ПродолжитьОтправкуЗапросаЗондаПослеРасшифровкиМаркера(Результат, Параметры) Экспорт
	
	Если Результат.Успех Тогда
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
		ПараметрыЗапроса.Вставить("СообщениеОбмена", Параметры.ЗапросЗонд);
		ПараметрыЗапроса.Вставить("ИдентификаторСессии", Результат.РасшифрованныеДанные);
		ПараметрыЗапроса.Вставить("ВидОперации", "Тестирование");
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыЗапроса, , , , ,
			Параметры.ОбработчикПослеТестаНастройки);
	Иначе
		СтруктураРезультата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, СтруктураРезультата);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

Процедура НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Параметры.КоличествоПодготовленных = Параметры.КоличествоПодготовленных + Результат.КоличествоПодготовленных;
		Параметры.КоличествоОтправленных = Параметры.КоличествоОтправленных + Результат.КоличествоОтправленных;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			Параметры.ПодтверждениеПлатежейВБанке.БанкиТребующиеПодтверждениеПлатежаВЛК,
			Результат.ПодтверждениеПлатежейВБанке.БанкиТребующиеПодтверждениеПлатежаВЛК, Истина);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			Параметры.ПодтверждениеПлатежейВБанке.НастройкиОбмена,
			Результат.ПодтверждениеПлатежейВБанке.НастройкиОбмена, Истина);
		Оповестить("ОтправленоDirectBank", Результат.ОтправленныеДокументы);
	КонецЕсли;
	СтруктураКОтправке = Параметры.Результат.СтруктураКОтправке;
	СоотвНастроекОбменаИМассивовСообщенийОбменаКОтправке = СтруктураКОтправке.САвторизациейЛогинПароль;
	
	ТекущийИндекс = 0;
	Для Каждого ТекЭл Из СоотвНастроекОбменаИМассивовСообщенийОбменаКОтправке Цикл
		
		ТекущийИндекс = ТекущийИндекс + 1;
		Если ТекущийИндекс <= Параметры.ТекущийИндексОтправкиСообщенийВБанкСАвторизациейЛогинПароль Тогда
			Продолжить;
		КонецЕсли;
		Параметры.ТекущийИндексОтправкиСообщенийВБанкСАвторизациейЛогинПароль = ТекущийИндекс;
		
		ПолностьюПодписанныеСообщенияОбмена = Новый Массив;
		Для Каждого СообщениеОбмена Из ТекЭл.Значение Цикл
			Если ОбменСБанкамиСлужебныйВызовСервера.ЭлектронныйДокументПолностьюПодписан(СообщениеОбмена) Тогда
				ПолностьюПодписанныеСообщенияОбмена.Добавить(СообщениеОбмена);
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ПолностьюПодписанныеСообщенияОбмена.Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		НастройкаОбмена = ТекЭл.Ключ;
		
		Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
		Параметры.Вставить("МассивСообщенийОбменаКОтправкеВБанкСАвторизациейЛогинПароль", ТекЭл.Значение);
		ПараметрыАвторизации = Неопределено;
		Если ПолученыДанныеАвторизации(ТекЭл.Ключ, ПараметрыАвторизации) Тогда
			ОтправитьСообщениеВБанкСАутентификациейЛогинПароль(ПараметрыАвторизации, Параметры);
		Иначе
			Оповещение = Новый ОписаниеОповещения("ОтправитьСообщениеВБанкСАутентификациейЛогинПароль", ЭтотОбъект, Параметры);
			ПолучитьДанныеАутентификации(Оповещение, ТекЭл.Ключ);
		КонецЕсли;
		Возврат;
	КонецЦикла;
	
	СоотвНастроекОбменаИМассивовСообщенийОбменаКОтправке.Очистить();
	ОтправкаСообщенийОбмена(Параметры);
	
КонецПроцедуры

Процедура ОтправкаСообщенийОбмена(Параметры)
	
	Если НЕ Параметры.Свойство("ПодтверждениеПлатежейВБанке") Тогда
		Параметры.Вставить("ПодтверждениеПлатежейВБанке", Параметры.Результат.ПодтверждениеПлатежейВБанке);
	КонецЕсли;
	
	Результат = Параметры.Результат;
	СтруктураКОтправке = Результат.СтруктураКОтправке;
	
	Параметры.Вставить("МассивКОтправкеБезПодписи", Новый Массив);
	Параметры.Вставить("МассивКОтправке", Новый Массив);
	Параметры.Вставить("СоотвНастроекОбменаИСтруктурСертификатов", Новый Соответствие);

	Если СтруктураКОтправке.САвторизациейЛогинПароль.Количество() > 0 Тогда
		Параметры.Вставить("ТекущийИндексОтправкиСообщенийВБанкСАвторизациейЛогинПароль", 0);
		НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль(Неопределено, Параметры);
	ИначеЕсли СтруктураКОтправке.ЧерезТокенСбербанка.Количество() Тогда
		ДанныеДляОтправкиВСбербанк = ОбменСБанкамиСлужебныйВызовСервера.ДанныеДляОтправкиВСбербанк(
			СтруктураКОтправке.ЧерезТокенСбербанка);
		Параметры.Вставить("ДанныеДляОтправкиВСбербанк", ДанныеДляОтправкиВСбербанк);
		СтруктураКОтправке.ЧерезТокенСбербанка.Очистить();
		ОтправитьДокументыВСбербанк(Параметры);
	ИначеЕсли СтруктураКОтправке.БазоваяАутентификацияСбербанка.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатОтправкиДокументовВСбербанк", ЭтотОбъект, Параметры);
		ОтправитьДокументыПоЛогинуВСбербанк(Оповещение, СтруктураКОтправке.БазоваяАутентификацияСбербанка);
	ИначеЕсли СтруктураКОтправке.ЧерезВК.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатОтправкиПакетов", ЭтотОбъект, Параметры);
		ДанныеКОтправке = ОбменСБанкамиСлужебныйВызовСервера.ДанныеДляОтправкиЧерезВК(СтруктураКОтправке.ЧерезВК);
		СтруктураКОтправке.ЧерезВК.Очистить();
		ОтправитьДокументыЧерезВК(Оповещение, ДанныеКОтправке);
	ИначеЕсли СтруктураКОтправке.ЧерезДополнительнуюОбработку.Количество() Тогда
		ОтправитьСообщенияОбменаЧерезДополнительнуюОбработку(
			СтруктураКОтправке.ЧерезДополнительнуюОбработку, Параметры);
	ИначеЕсли СтруктураКОтправке.САутентификациейПоСертификату.Количество() Тогда
		ОтправитьСообщенияОбменаСАутентификациейПоСертификату(СтруктураКОтправке.САутентификациейПоСертификату,
			Результат.СоотвНастроекОбменаИСертификатовАвторизации, Параметры);
	ИначеЕсли СтруктураКОтправке.ЧерезСервисFintech.Количество() Тогда
		ОтправитьПлатежныеДокументыЧерезFintech(СтруктураКОтправке.ЧерезСервисFintech, Параметры);
	ИначеЕсли СтруктураКОтправке.АльфаБанкОнлайн.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтправкиПакетовВАльфаБанк", ЭтотОбъект, Параметры);
		СформироватьИОтправитьПакетыВБанк(Оповещение, СтруктураКОтправке.АльфаБанкОнлайн);
	Иначе
		ВыполнитьДействияПослеОтправки(Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьСообщенияОбменаСАутентификациейПоСертификату(
	ДанныеКОтправке,
	СертификатыАутентификации,
	ДополнительныеПараметры)
	
	Для Каждого ТекЭл Из ДанныеКОтправке Цикл
		НастройкаОбмена = ТекЭл.Ключ;
		ЕстьСообщениеОбменаКОтправке = Ложь;
		Для Каждого ОтправляемоеСообщениеОбмена Из ТекЭл.Значение Цикл
			ДокументПодписанПолностью = ОбменСБанкамиСлужебныйВызовСервера.ЭлектронныйДокументПолностьюПодписан(
				ОтправляемоеСообщениеОбмена);
			Если ДокументПодписанПолностью Тогда
				ДополнительныеПараметры.МассивКОтправке.Добавить(ОтправляемоеСообщениеОбмена);
				ЕстьСообщениеОбменаКОтправке = Истина;
			КонецЕсли;
		КонецЦикла;
		Прервать;
	КонецЦикла;
	ДанныеКОтправке.Удалить(НастройкаОбмена);
	Если ЕстьСообщениеОбменаКОтправке Тогда
		ОбработчикОповещения = Новый ОписаниеОповещения(
			"ПродолжитьОтправкуСообщенийОбменаПослеРасшифровкиМаркера", ЭтотОбъект, ДополнительныеПараметры);
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ОбработчикОповещения", ОбработчикОповещения);
		ДопПараметры.Вставить("СоответствиеНастроекОбменаИСертификатов", СертификатыАутентификации);
		ДопПараметры.Вставить("СоотвСертификатовИИхСтруктур", ДополнительныеПараметры.СоотвСертификатовИИхСтруктур);
		РасшифроватьМаркерБанка(Неопределено, ДопПараметры);
	Иначе
		ОтправкаСообщенийОбмена(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуЗапросаВыписки(ПараметрыАутентификации, Параметры) Экспорт
	
	Если ПараметрыАутентификации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыНастройкиОбмена = Новый Структура(
		"ПрограммаБанка, АдресСервера, ИдентификаторОрганизации, ВерсияФормата");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		Параметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
		
	АсинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
	
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = АсинхронныйОбмен Тогда
		Обработчик = Новый ОписаниеОповещения("ЗапроситьВыпискуПослеПолученияМаркераБанка", ЭтотОбъект, Параметры);
		ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, РеквизитыНастройкиОбмена.АдресСервера,
			РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ПараметрыАутентификации,
			РеквизитыНастройкиОбмена.ВерсияФормата, Параметры.НастройкаОбмена);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ДанныеСертификата, ДатаНачала, ДатаОкончания, МассивСообщенийОбмена,
		|НастройкаОбмена, НомерСчета, СообщениеОбмена, ГотовыеВыписки, ПринудительноеПолучениеВыписки");
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Параметры);
	ПараметрыФормы.Вставить("ВидОперации", "ПолучениеВыписки");
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписки", ЭтотОбъект, Параметры);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	
КонецПроцедуры

// Отправка и получение электронных документов одной командой.
//
// Параметры:
//    ДанныеАутентификации - Структура, Неопределено - данные аутентификации на сервере банка
//    Параметры - Структура - контекст выполнения.
//
Процедура ОтправитьДокументыВБанк(ДанныеАутентификации, Параметры) Экспорт
	
	Если НЕ Параметры.ТребуетсяАвторизация ИЛИ ЗначениеЗаполнено(ДанныеАутентификации) Тогда
		РеквизитыНастройкиОбмена = Параметры.РеквизитыНастройкиОбмена;
		Если РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
			// При аутентификации по сертификату отправка выполняется после расшифровки маркера
			// из процедуры ПослеПолученияОтпечатковВыполнитьОбмен.
			Обработчик = Новый ОписаниеОповещения(
				"ОтправитьИПолучитьДокументыВБанкПослеПолученияМаркера", ЭтотОбъект, Параметры);
			
			ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, РеквизитыНастройкиОбмена.АдресСервера,
				РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ДанныеАутентификации,
				РеквизитыНастройкиОбмена.ВерсияФормата, Параметры.НастройкаОбмена);
		ИначеЕсли РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
			Оповещение = Новый ОписаниеОповещения(
				"ОтправитьПакетыПослеПодготовкиЗапросовВыписокЧерезВК", ЭтотОбъект, Параметры);
			ПараметрыЗапроса = ОбменСБанкамиКлиентСервер.ПараметрыПолученияВыпискиБанка();
			ПодготовитьЗапросыВыписокДляОтправкиЧерезВК(
				Оповещение, Параметры.НастройкаОбмена, ПараметрыЗапроса, Истина);
		ИначеЕсли РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			ДанныеДляОтправки = ОбменСБанкамиСлужебныйВызовСервера.ДанныеДляОтправкиВБанкЧерезДопОбработку(
				Параметры.НастройкаОбмена);
			Если НЕ ДанныеДляОтправки.Количество() Тогда
				Результат = Новый Структура("Успех, Отправлено", Истина, 0);
				ПослеОтправкиДокументовЧерезДополнительнуюОбработку(Результат, Параметры);
				Возврат;
			КонецЕсли;
			ДанныеДляОтправкиПоНастройке = ДанныеДляОтправки.Получить(Параметры.НастройкаОбмена);
			Если НЕ ДанныеДляОтправкиПоНастройке.ДанныеПакетов.Количество() Тогда
				Результат = Новый Структура("Успех, Отправлено", Истина, 0);
				ПослеОтправкиДокументовЧерезДополнительнуюОбработку(Результат, Параметры);
				Возврат;
			КонецЕсли;
			Оповещение = Новый ОписаниеОповещения("ПослеОтправкиДокументовЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
			ОтправитьДокументыЧерезДополнительнуюОбработку(Оповещение, Параметры.НастройкаОбмена,
				РеквизитыНастройкиОбмена.ДополнительнаяОбработка, ДанныеДляОтправкиПоНастройке);
		Иначе // АльфаБанкОнлайн
			СтруктураВозврата = Новый Структура;
			ОбменСБанкамиСлужебныйВызовСервера.ОтправитьЭДВБанк(
				Параметры.НастройкаОбмена, Неопределено, Новый Массив, СтруктураВозврата);
			
			ЕстьОшибка = Ложь;
			
			Если ЗначениеЗаполнено(СтруктураВозврата.ТекстОшибки) Тогда
				ЕстьОшибка = Истина;
			КонецЕсли;
		
			ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк'");
			ШаблонОповещения = НСтр("ru = 'Отправлено: (%1).'");
			ТекстОповещения = СтрШаблон(ШаблонОповещения, СтруктураВозврата.КоличествоОтправленныхПакетов);
			ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеСинхронизации, Не ЕстьОшибка);
		КонецЕсли;
	Иначе
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеСинхронизации, Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ТестНастроек

Процедура ПослеПолученияОтпечатковПроверитьСвязь(ОтпечаткиСертификатов, Параметры) Экспорт
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	РеквизитыНастройкиОбмена = Параметры.РеквизитыНастройкиОбмена;
	
	МассивОтпечатковСертификатов = Новый Массив;
	Если ТипЗнч(ОтпечаткиСертификатов) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из ОтпечаткиСертификатов Цикл
			МассивОтпечатковСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	ИначеЕсли (РеквизитыНастройкиОбмена.АутентификацияПоСертификату ИЛИ РеквизитыНастройкиОбмена.ТребуетсяПодпись)
		И Не ЭлектроннаяПодписьКлиент.СоздаватьЭлектронныеПодписиНаСервере() Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;

	НастройкиОбмена = Неопределено;
	СообщениеЗапросЗонд = Неопределено;
	ОбменСБанкамиСлужебныйВызовСервера.СформироватьЗапросЗонд(
		НастройкаОбмена, МассивОтпечатковСертификатов, СообщениеЗапросЗонд, НастройкиОбмена);
	Параметры.Вставить("ЗапросЗонд", СообщениеЗапросЗонд);
	
	Если НЕ ЗначениеЗаполнено(СообщениеЗапросЗонд) ИЛИ НастройкиОбмена = Неопределено Тогда
		СтруктураРезультата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, СтруктураРезультата);
		Возврат;
	КонецЕсли;
	Если НастройкиОбмена.Подписывать Тогда
		ВидЭДЗапросЗонд = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросЗонд");
		Если НЕ ОбменСБанкамиКлиентСервер.ЕстьДоступныеСертификаты(НастройкиОбмена, ВидЭДЗапросЗонд) Тогда
			СтруктураРезультата = Новый Структура("Успех", Ложь);
			ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, СтруктураРезультата);
			Возврат;
		КонецЕсли;

		МассивСообщенийОбмена = Новый Массив;
		МассивСообщенийОбмена.Добавить(СообщениеЗапросЗонд);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодписанияЗапросаЗонда", ЭтотОбъект, Параметры);
		Подписать(ОписаниеОповещения, МассивСообщенийОбмена, НастройкиОбмена.ДоступныеСертификаты);
	ИначеЕсли РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		ДанныеДоступныхСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(НастройкаОбмена);
		МассивСертификатов = Новый Массив;
		Для Каждого ДанныеСертификата Из ДанныеДоступныхСертификатов Цикл
			МассивСертификатов.Добавить(ДанныеСертификата.Ключ);
		КонецЦикла;
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
		ОписаниеДанных.Вставить("ЗаголовокДанных", "");
		
		ДополнительныеПараметры = Новый Структура("НастройкаОбмена", НастройкаОбмена);
		
		ОписаниеПолученияДанных = Новый ОписаниеОповещения(
			"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ДополнительныеПараметры);
		
		ОписаниеДанных.Вставить("Данные", ОписаниеПолученияДанных);
			
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПродолжитьОтправкуЗапросаЗондаПослеРасшифровкиМаркера", ЭтотОбъект, Параметры);
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, Новый УникальныйИдентификатор, ОписаниеОповещения);
	Иначе
		Оповещение = Новый ОписаниеОповещения("ПолучитьМаркерБанкаПослеВводаДанныхАутентификации", ЭтотОбъект, Параметры);
		ПолучитьДанныеАутентификации(Оповещение, НастройкаОбмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ТестСвязиСБанкомAsync(Маркер, Параметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Маркер) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
	ПараметрыЗапроса.Вставить("СообщениеОбмена", Параметры.ЗапросЗонд);
	ПараметрыЗапроса.Вставить("ИдентификаторСессии", Маркер);
	ПараметрыЗапроса.Вставить("ВидОперации", "Тестирование");

	ОткрытьФорму(
		"Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыЗапроса, , , , , Параметры.ОбработчикПослеТестаНастройки);

КонецПроцедуры

// Выполняет получение идентификатора сессии банка (маркера).
//
// Параметры:
//    Обработчик - ОписаниеОповещения - обработчик, вызываемый после получения маркера;
//        * Результат - Строка - маркер банка;
//        * ДополнительныеПараметры - Произвольный - параметры, указанные при создании оповещения.
//    АдресСервера - Строка - URL адрес сервера банка;
//    ИдентификаторОрганизации - Строка - идентификатор организации на сервере банка;
//    ДанныеАутентификации - Структура - данные аутентификации на сервере банка:
//        * Логин - Строка - логин на сервере банка;
//        * Пароль - Строка - пароль аутентификации на сервере банка.
//    ВерсияAPI - Строка - версия формата обмена.
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//    ПробнаяОперация - Булево - операция пробная, не нужно выводить сообщения об ошибках.
//
Процедура ПолучитьМаркерБанкаПоЛогинуИПаролю(
	Обработчик,
	АдресСервера,
	ИдентификаторОрганизации,
	ДанныеАутентификации,
	ВерсияAPI,
	НастройкаОбмена = Неопределено,
	ПробнаяОперация = Ложь) Экспорт
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.БазоваяАутентификация(
		АдресСервера, ИдентификаторОрганизации, ДанныеАутентификации, ВерсияAPI, НастройкаОбмена);
		
	Если ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки, НастройкаОбмена);
	КонецЕсли;
	
	Если Результат.НеверныеДанныеАутентификации Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Неверный логин или пароль.'"));
		УдалитьПарольИзСеанса(НастройкаОбмена);
	КонецЕсли;
	Если Результат.ТребуетсяSMSАвторизация Тогда
		Параметры = Новый Структура;
		Параметры.Вставить("ОбработчикПослеРасширеннойАутентификации", Обработчик);
		Параметры.Вставить("АдресСервера", АдресСервера);
		Параметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
		Параметры.Вставить("НеподтвержденныйИдентификаторСессии", Результат.ИдентификаторСессии);
		Параметры.Вставить("ВерсияAPI", ВерсияAPI);
		Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ИдентификаторСессии", Результат.ИдентификаторСессии);
		ПараметрыФормы.Вставить("Телефон", Результат.МаскаТелефона);
		ОО = Новый ОписаниеОповещения("ОтправитьОдноразовыйПарольSMSВБанк", ЭтотОбъект, Параметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросОдноразовогоПароля", ПараметрыФормы, , , , , ОО);
	Иначе
		ВыполнитьОбработкуОповещения(Обработчик, Результат.ИдентификаторСессии);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьОдноразовыйПарольSMSВБанк(ОдноразовыйПароль, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(ОдноразовыйПароль) Тогда
		ИдентификаторСессии = ОбменСБанкамиСлужебныйВызовСервера.МаркерБанкаПоSMS(Параметры.АдресСервера,
		Параметры.ИдентификаторОрганизации, Параметры.НеподтвержденныйИдентификаторСессии, ОдноразовыйПароль,
		Параметры.ВерсияAPI, Параметры.НастройкаОбмена);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеРасширеннойАутентификации, ИдентификаторСессии);

КонецПроцедуры

Процедура ЗапроситьВыпискуПослеПолученияМаркераБанка(ИдентификаторСессии, Параметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторСессии) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ДанныеСертификата, ДатаНачала, ДатаОкончания, МассивСообщенийОбмена,
		|НастройкаОбмена, НомерСчета, СообщениеОбмена");
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Параметры);
	ПараметрыФормы.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыФормы.Вставить("ВидОперации", "ПолучениеВыписки");
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписки", ЭтотОбъект, Параметры);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	
КонецПроцедуры

Процедура ОтправитьИПолучитьДокументыВБанкПослеПолученияМаркера(Маркер, Параметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Маркер) Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеСинхронизации, Ложь);
		Возврат;
	КонецЕсли;
	
	ОтправитьИПолучитьДокументыВБанк(Маркер, Параметры)

КонецПроцедуры

Процедура ОтправитьИПолучитьДокументыВБанкПослеРасшифровкиМаркера(Результат, Параметры) Экспорт
	
	Если НЕ Результат.Успех Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеСинхронизации, Ложь);
		Возврат;
	КонецЕсли;
		
	ОтправитьИПолучитьДокументыВБанк(Результат.РасшифрованныеДанные, Параметры)
	
КонецПроцедуры

Процедура ОтправитьИПолучитьДокументыВБанк(ИдентификаторСессии, Параметры)
	
	Параметры.Вставить("ИдентификаторСессии", ИдентификаторСессии);

	МассивЗапросовВыписок = Неопределено;
	Параметры.Свойство("МассивЗапросовВыписок", МассивЗапросовВыписок);
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияОтправкиДокументовАсинхронныйОбмен(
		Параметры.НастройкаОбмена, ИдентификаторСессии, МассивЗапросовВыписок);

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Параметры.ВыводитьОкноОжидания;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиДокументовВБанкАсинхронныйОбмен", ЭтотОбъект, Параметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
	Если Параметры.Свойство("ОповещениеЗапускаДлительнойОперации") Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеЗапускаДлительнойОперации, Результат.ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДвоичныеДанныеДляСообщенияОбмена(Результат, ДополнительныеПараметры) Экспорт
	
	СообщениеОбмена = Неопределено;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("СообщениеОбмена", СообщениеОбмена)
		И ТипЗнч(СообщениеОбмена) = Тип("ДокументСсылка.СообщениеОбменСБанками") Тогда
		ДвоичныеДанныеЭД = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(СообщениеОбмена);
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Данные", ДвоичныеДанныеЭД);
	
	ВыполнитьОбработкуОповещения(Результат.Оповещение, Параметры);
	
КонецПроцедуры

// Получает пароль к сертификату, если доступен текущему пользователю.
//
// Параметры:
//  СертификатЭП - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат ЭП.
//  ПарольСертификата - Строка - пароль к сертификату ЭП, полученный из глобальной переменной.
//  НаВремяСеанса - Булево - если Истина, то пароль получен на время сеанса.
//
// Возвращаемое значение:
//  Булево - Истина - если пароль для сертификата ЭП получен, иначе - Ложь.
//
Функция ПарольКСертификатуПолучен(СертификатЭП, ПарольСертификата, НаВремяСеанса = Ложь) Экспорт
	
	ПарольСертификата = Неопределено;
	СоответствиеСертификатаИПароля = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками.ПаролиСеанса"];
	Если ТипЗнч(СоответствиеСертификатаИПароля) = Тип("ФиксированноеСоответствие") Тогда
		ПарольСертификата = СоответствиеСертификатаИПароля.Получить(СертификатЭП);
		НаВремяСеанса = (ПарольСертификата <> Неопределено);
	КонецЕсли;
	
	Если ПарольСертификата = Неопределено Тогда
		ПарольСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ПарольКСертификату(СертификатЭП);
	КонецЕсли;
	
	Возврат (ПарольСертификата <> Неопределено);
	
КонецФункции

Процедура ВыполнитьДействияПослеОтправки(Параметры)
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	Если ЭлектронноеВзаимодействиеКлиентСервер.ЕстьДействие(Параметры.Действия, "Показать") Тогда
		Для Каждого ТекЭл Из Параметры.Результат.МассивНовыхСообщенийОбмена Цикл
			ОткрытьЭДДляПросмотра(ТекЭл);
		КонецЦикла;
	ИначеЕсли ЭлектронноеВзаимодействиеКлиентСервер.ЕстьДействие(Параметры.Действия, "Подписать") Тогда
		Результат = Параметры.Результат;
		СоотвСообщенийОбменаИОшибокПодписи = Неопределено;
		МассивСсылокНаОбъект = Неопределено;
		Если Параметры.Свойство("МассивСсылокНаОбъект", МассивСсылокНаОбъект)
			И МассивСсылокНаОбъект <> Неопределено
			И МассивСсылокНаОбъект.Количество() = 1
			И Результат.Свойство("СоотвСообщенийОбменаИОшибокПодписи", СоотвСообщенийОбменаИОшибокПодписи) 
			И СоотвСообщенийОбменаИОшибокПодписи <> Неопределено Тогда
			Для Каждого КлючИЗначение Из СоотвСообщенийОбменаИОшибокПодписи Цикл
				СообщениеОбОшибке = ЭлектронноеВзаимодействиеСлужебныйКлиент.ПредставлениеОшибкиПодписания(
					КлючИЗначение.Значение);
				ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеОбОшибке);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ВывестиИнформациюОбОбработанныхЭД(Параметры.КоличествоНовыхЭД, Параметры.КоличествоПодписанных,
		Параметры.КоличествоПодготовленных, Параметры.КоличествоОтправленных, Параметры.КоличествоПодтвержденных,
		Параметры.ПодтверждениеПлатежейВБанке);
	
КонецПроцедуры

#КонецОбласти

#Область ОткрытиеФорм

// Открывает форму просмотра электронного документа.
//
// Параметры:
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение открываемое для просмотра;
//  ПараметрыОткрытия - Структура - дополнительные параметры просмотра;
//  ВладелецФормы     - ФормаКлиентскогоПриложения - форма - владелец;
//  ТолькоЧтение - Булево - признак открытия формы в режиме просмотра без команд.
//
Процедура ОткрытьЭДДляПросмотра(СообщениеОбмена, ПараметрыОткрытия = Неопределено, ВладелецФормы = Неопределено, ТолькоЧтение = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СообщениеОбмена) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЭД = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОткрытияЭлектронногоДокумента(СообщениеОбмена);
	
	Если ПараметрыЭД.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.Письмо") Тогда
		ПоказатьЗначение( , ПараметрыЭД.Владелец);
	Иначе
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
		ДополнительныеПараметры.Вставить("ПараметрыОткрытия", ПараметрыОткрытия);
		ДополнительныеПараметры.Вставить("ВладелецФормы", ВладелецФормы);
		ДополнительныеПараметры.Вставить("ТолькоЧтение", ТолькоЧтение);
		Если ПараметрыЭД.ИспользуетсяКриптография Тогда
			Описание = Новый ОписаниеОповещения(
				"ОткрытьФормуЭлектронногоДокументаПослеПолученияОтпечатков", ЭтотОбъект, ДополнительныеПараметры);
			ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Описание, Истина, Ложь);
		Иначе
			ОткрытьФормуЭлектронногоДокументаПослеПолученияОтпечатков(Неопределено, ДополнительныеПараметры);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПолучитьНовыеДокументыИзБанкаРекурсивно(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.СоответствиеНастроекОбменаИПараметровСертификатов.Количество() = 0 Тогда
		ВыполнитьДействияПослеОтправкиПЭДЗавершить(Неопределено, ДополнительныеПараметры);
	КонецЕсли;
	
	Для Каждого ДанныеАутентификации Из ДополнительныеПараметры.СоответствиеНастроекОбменаИПараметровСертификатов Цикл
		РеквизитыНастройкиОбмена = Новый Структура("ПрограммаБанка");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(ДанныеАутентификации.Ключ, РеквизитыНастройкиОбмена);
		ДополнительныеПараметры.СоответствиеНастроекОбменаИПараметровСертификатов.Удалить(ДанныеАутентификации.Ключ);
		Если РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
			Оповещение = Новый ОписаниеОповещения(
				"ПослеПолученияДокументовПоОчереднойНастройкеОбмена", ЭтотОбъект, ДополнительныеПараметры);
			ПолучитьНовыеДокументыИзБанка(
				Оповещение, ДанныеАутентификации.Ключ, ДанныеАутентификации.Значение.ИдентификаторСессии, Истина);
		Иначе
			ПолучитьНовыеДокументыИзБанкаРекурсивно(ДополнительныеПараметры);
		КонецЕсли;
		Прервать;
	КонецЦикла

КонецПроцедуры

Процедура ПослеПолученияДокументовПоОчереднойНастройкеОбмена(СтруктураВозврата, ДополнительныеПараметры) Экспорт
	
	Если СтруктураВозврата = Неопределено Тогда
		ВыполнитьДействияПослеОтправкиПЭДЗавершить(Неопределено, ДополнительныеПараметры);
		Возврат
	КонецЕсли;
	
	ВызватьОповещения(СтруктураВозврата);
	Если СтруктураВозврата.Свойство("ДанныеЭП") И СтруктураВозврата.ДанныеЭП.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения;
		Для Каждого КлючЗначение Из СтруктураВозврата.ДанныеЭП Цикл
			ДобавитьПодписиИОпределитьСтатусы(Оповещение, КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	Оповестить("ОбновитьСостояниеОбменСБанками", СтруктураВозврата.ПараметрОповещения);

	ПолучитьНовыеДокументыИзБанкаРекурсивно(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ОткрытьФормуЭлектронногоДокументаПослеПолученияОтпечатков(Отпечатки, ДополнительныеПараметры) Экспорт
	
	СообщениеОбмена = ДополнительныеПараметры.СообщениеОбмена;
	ПараметрыОткрытия = ДополнительныеПараметры.ПараметрыОткрытия;
	ВладелецФормы = ДополнительныеПараметры.ВладелецФормы;
	ТолькоЧтение = ДополнительныеПараметры.ТолькоЧтение;
	
	МассивОтпечатков = Новый Массив;
	Если ТипЗнч(Отпечатки) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из Отпечатки Цикл
			МассивОтпечатков.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	КонецЕсли;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", СообщениеОбмена);
	ПараметрыФормы.Вставить("ТолькоЧтение", ТолькоЧтение);
	ПараметрыФормы.Вставить("МассивОтпечатков", МассивОтпечатков);
	Если ВладелецФормы = Неопределено Тогда
		ОткрытьФорму("Документ.СообщениеОбменСБанками.Форма.ЭлектронныйДокумент", ПараметрыФормы);
	Иначе
		Если ПараметрыОткрытия = Неопределено Тогда
			ОткрытьФорму(
				"Документ.СообщениеОбменСБанками.Форма.ЭлектронныйДокумент", ПараметрыФормы, ВладелецФормы);
		Иначе
			Окно = Неопределено;
			Если ТипЗнч(ПараметрыОткрытия) = Тип("ПараметрыВыполненияКоманды")
				ИЛИ ТипЗнч(ПараметрыОткрытия) = Тип("Структура")
				И ПараметрыОткрытия.Свойство("Окно") И ТипЗнч(ПараметрыОткрытия.Окно) = Тип("ОкноКлиентскогоПриложения") Тогда
				
				Окно = ПараметрыОткрытия.Окно;
			КонецЕсли;
			ОткрытьФорму("Документ.СообщениеОбменСБанками.Форма.ЭлектронныйДокумент", ПараметрыФормы, ВладелецФормы,
				ПараметрыОткрытия.Уникальность, Окно);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область НастройкиОбмена

Процедура НастроитьОбменСБанками(НастройкиОбъектов, Действия)
	
	Организация = Неопределено;
	Банк = Неопределено;
	Счетчик = 0;
	
	Для Каждого Элемент Из НастройкиОбъектов Цикл
		Если Элемент.Значение.Результат = "ТребуетсяНастройкаОбмена"
			И (Элемент.Значение.ПараметрыОбмена.Организация <> Организация
				ИЛИ Элемент.Значение.ПараметрыОбмена.Банк <> Банк) Тогда
			Счетчик = Счетчик + 1;
			Если Счетчик = 2 Тогда
				Прервать;
			КонецЕсли;
			Организация = Элемент.Значение.ПараметрыОбмена.Организация;
			Банк = Элемент.Значение.ПараметрыОбмена.Банк;
		КонецЕсли;
	КонецЦикла;
	
	Если Счетчик = 1 Тогда
		ДополнительныеПараметры = Новый Структура("НастройкиОбъектов, Действия", НастройкиОбъектов, Действия);
		Оповещение = Новый ОписаниеОповещения(
			"ОбработатьДокументыПослеСозданияНастройкиОбмена", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСБанкамиКлиент.ОткрытьСоздатьНастройкуОбмена(Организация, Банк, , Оповещение);
	Иначе
		Параметры = Новый Структура("НастройкиОбъектов, Действия", НастройкиОбъектов, Действия);
		ОткрытьФорму("Справочник.НастройкиОбменСБанками.Форма.СозданиеНастроекОбмена", Параметры, , , , , ,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодписаниеБанковскихЭД(Структура)
	
	СоотвСертификатовИИхСтруктур = Неопределено;
	МассивСертификатов = Неопределено;
	ДанныеДляСпецОбработки = Неопределено;
	ОписаниеПодписатьЭД = Неопределено;
	Структура.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД);
	Если Структура.Свойство("СоотвСертификатовИИхСтруктур", СоотвСертификатовИИхСтруктур)
		И ТипЗнч(СоотвСертификатовИИхСтруктур) = Тип("Соответствие")
		И Структура.Свойство("МассивСертификатов", МассивСертификатов)
		И ТипЗнч(МассивСертификатов) = Тип("Массив")
		И Структура.Свойство("ДанныеДляСпецОбработки", ДанныеДляСпецОбработки)
		И ТипЗнч(ДанныеДляСпецОбработки) = Тип("Соответствие") Тогда
		
		Соответствие = Новый Соответствие;
		Для Каждого Сертификат Из МассивСертификатов Цикл
			ПараметрыСертификата = СоотвСертификатовИИхСтруктур.Получить(Сертификат);
			Если ТипЗнч(ПараметрыСертификата) = Тип("Структура") Тогда
				Соответствие.Вставить(Сертификат, ПараметрыСертификата);
			КонецЕсли;
		КонецЦикла;
		
		МассивСообщенийОбменаКПодписи = Новый Массив;
		// ДанныеДляСпецОбработки - Соответствие:
		//   Ключ     - ПрограммаБанка.
		//   Значение - Соответствие:
		//     Ключ     - НастройкаОбмена.
		//     Значение - МассивСообщенийОбмена.
		// Т.к. это соответствие формируется в разрезе сертификата подписи, то оно однозначно:
		// массив сертификатов подписи по 1 программе банка и 1 настройке обмена (в каждом
		// рассматриваемом соответствии не больше одного элемента):
		Для Каждого КлючИЗначение Из ДанныеДляСпецОбработки Цикл
			Структура.Вставить("ПрограммаБанка", КлючИЗначение.Ключ);
			Для Каждого НастройкаИСообщениеОбмена Из КлючИЗначение.Значение Цикл
				Структура.Вставить("НастройкаОбмена", НастройкаИСообщениеОбмена.Ключ);
				Для Каждого СообщениеОбмена Из НастройкаИСообщениеОбмена.Значение Цикл
					МассивСообщенийОбменаКПодписи.Добавить(СообщениеОбмена);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		Если МассивСообщенийОбменаКПодписи.Количество() > 1 Тогда
			ВидОперации = НСтр("ru = 'Подписание электронных документов'");
		Иначе
			ВидОперации = НСтр("ru = 'Подписание электронного документа'");
		КонецЕсли;
		Структура.Вставить("МассивСообщенийОбменаКПодписи", МассивСообщенийОбменаКПодписи);
		ВызватьОповещение = Новый ОписаниеОповещения("ПродолжитьПодписаниеБанковскихЭД", ЭтотОбъект, Структура);
		ПолучитьПарольКСертификату(
			ВызватьОповещение, Соответствие, ВидОперации, МассивСообщенийОбменаКПодписи, Структура.ПрограммаБанка);
	ИначеЕсли ТипЗнч(ОписаниеПодписатьЭД) = Тип("ОписаниеОповещения") Тогда
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается из ПолучитьПарольКСертификату по выполнению описания оповещения.
//
// Параметры:
//    Результат - Структура - результат выбора сертификата подписи и ввода пароля к нему:
//       ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования.
//       ПарольСертификата  - Строка - пароль к сертификату.
//    Параметры - Структура - содержи поля:
//       * СоотвСертификатовИИхСтруктур - Соответствие - свойства сертификатов
//       * ПрограммаБанка - ПеречислениеСсылка.ПрограммыБанка - программа банка
//       * ОбработчикПродолжения - ОписаниеОповещения - описание, которое надо
//                                    выполнить после завершения обработки текущих ЭД.
//
Процедура ПродолжитьПодписаниеБанковскихЭД(Результат, Параметры) Экспорт
	
	ВыбранныйСертификат = Неопределено;
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("ВыбранныйСертификат", ВыбранныйСертификат)
		И ТипЗнч(ВыбранныйСертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		
		ПараметрыСертификата = Параметры.СоотвСертификатовИИхСтруктур[ВыбранныйСертификат];
		ПараметрыСертификата.Вставить("ПарольПолучен", Истина);
		ПараметрыСертификата.Вставить("ПарольСертификата", Результат.ПарольСертификата);
		ПараметрыСертификата.Вставить("ВыбранныйСертификат", ВыбранныйСертификат);
		Параметры.Вставить("СтруктураСертификата", ПараметрыСертификата);
		
		Если Параметры.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			Параметры.Вставить("МассивОбработанныхНастроекОбмена", Новый Массив);
			Параметры.Вставить("ОтправитьНаПодписьПослеОбработки");
			Параметры.Вставить("РезультатВыбораСертификатаБанка", Результат);
			ДанныеСпецОбработки = Параметры.ДанныеДляСпецОбработки;
			Для Каждого ЭлементСоответствия Из ДанныеСпецОбработки Цикл
				ТекущиеДанные = ЭлементСоответствия.Значение;
				Параметры.ДанныеДляСпецОбработки.Удалить(ЭлементСоответствия.Ключ);
				НачатьПодписаниеЭДСбербанка(ТекущиеДанные, Параметры);
				Возврат;
			КонецЦикла;
		ИначеЕсли Параметры.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ОповещениеПослеПодписания", Параметры.ОбработчикПродолжения);
			ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", Параметры.МассивСообщенийОбменаКПодписи);
			ДополнительныеПараметры.Вставить("СертификатСсылка", Результат.ВыбранныйСертификат);
			ДополнительныеПараметры.Вставить("Пароль", Результат.ПарольСертификата);
			ДополнительныеПараметры.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
			ДополнительныеПараметры.Вставить("КоличествоПодписанных", 0);
			ПодписатьРекурсивноЭДПоСертификатуЧерезВК(ДополнительныеПараметры);
			Возврат;
		ИначеЕсли Параметры.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			ОбработчикПослеПодключения = Новый ОписаниеОповещения(
				"ПроверитьНеобходимостьУстановкиПинКода", ЭтотОбъект, Параметры);
			РеквизитыНастройкиОбмена = Новый Структура("ДополнительнаяОбработка");
			ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
				Параметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
			ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(
				ОбработчикПослеПодключения, РеквизитыНастройкиОбмена.ДополнительнаяОбработка);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеПодписатьЭД = Неопределено;
	Если Параметры.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД) Тогда
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьСертификатыИзПодписиПослеПолученияМенеджераКриптографии(
	МенеджерКриптографии,
	ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(МенеджерКриптографии) = Тип("МенеджерКриптографии") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеДобавленияИПроверкиПодписей, Ложь);
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения(
		"ДобавитьПодписиВЭДПослеПолученияСертификатов", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьСертификаты(Обработчик, МенеджерКриптографии, ДополнительныеПараметры.ДанныеПодписей);
	
КонецПроцедуры

Процедура ДобавитьПодписиВЭДПослеПолученияСертификатов(МассивСертификатовИПодписей, ДополнительныеПараметры) Экспорт
	
	Если МассивСертификатовИПодписей = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеДобавленияИПроверкиПодписей, Ложь);
		Возврат;
	КонецЕсли;
	
	ОбменСБанкамиСлужебныйВызовСервера.ДобавитьПодписиВСообщениеОбмена(
		ДополнительныеПараметры.СообщениеОбмена, МассивСертификатовИПодписей);

	Если ТипЗнч(ДополнительныеПараметры.СообщениеОбмена) = Тип("ДокументСсылка.СообщениеОбменСБанками") Тогда
		ОпределитьСтатусыПодписей(ДополнительныеПараметры.ОповещениеПослеДобавленияИПроверкиПодписей,
			 ДополнительныеПараметры.СообщениеОбмена);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеДобавленияИПроверкиПодписей, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьСертификаты(Обработчик, МенеджерКриптографии, МассивПодписей)
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("МенеджерКриптографии", МенеджерКриптографии);
	ПараметрыОбработки.Вставить("ОбработчикПослеПолученияСертификатов", Обработчик);
	ПараметрыОбработки.Вставить("МассивПодписей", ОбщегоНазначенияКлиент.СкопироватьРекурсивно(МассивПодписей));
	ПараметрыОбработки.Вставить("МассивПодписейИСертификатов", Новый Массив);
	
	НачатьПолучениеСертификатов(ПараметрыОбработки);
	
КонецПроцедуры

Процедура НачатьПолучениеСертификатов(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.МассивПодписей.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОбработчикПослеПолученияСертификатов, ДополнительныеПараметры.МассивПодписейИСертификатов);
		Возврат;
	КонецЕсли;
	
	ДанныеПодписи = ДополнительныеПараметры.МассивПодписей.Получить(0);
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьОчереднойСертификат", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияСертификатаИзПодписи", ЭтотОбъект);
	ДополнительныеПараметры.МенеджерКриптографии.НачатьПолучениеСертификатовИзПодписи(Оповещение, ДанныеПодписи);
	
КонецПроцедуры

Процедура ПолучитьОчереднойСертификат(Сертификаты, ДополнительныеПараметры) Экспорт
	
	Если Сертификаты.Количество() <> 0 Тогда
		Сертификат = Сертификаты[0];
		Оповещение = Новый ОписаниеОповещения("ОбработатьДанныеПослеВыгрузкиСертификата", ЭтотОбъект,
			ДополнительныеПараметры, "ОбработатьОшибкуВыгрузкиСертификата", ЭтотОбъект);
		Сертификат.НачатьВыгрузку(Оповещение);
	Иначе
		ДополнительныеПараметры.МассивПодписей.Удалить(0);
		НачатьПолучениеСертификатов(ДополнительныеПараметры);
	КонецЕсли
	
КонецПроцедуры

Процедура ОбработатьДанныеПослеВыгрузкиСертификата(ДанныеСертификата, ДополнительныеПараметры) Экспорт

	ДанныеСертификатаИПодписи = Новый Соответствие;
	ДанныеСертификатаИПодписи.Вставить(ДанныеСертификата, ДополнительныеПараметры.МассивПодписей[0]);
	ДополнительныеПараметры.МассивПодписей.Удалить(0);
	ДополнительныеПараметры.МассивПодписейИСертификатов.Добавить(ДанныеСертификатаИПодписи);
	
	НачатьПолучениеСертификатов(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСертификатаИзПодписи(
	ИнформацияОбОшибке,
	СтандартнаяОбработка,
	ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Операция = НСтр("ru = 'Получение сертификата из подписи'");
	ТекстСообщения = НСтр("ru = 'Не удалось получить сертификат из подписи'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикПослеПолученияСертификатов, Неопределено);
	
КонецПроцедуры

Процедура ОбработатьОшибкуВыгрузкиСертификата(
	ИнформацияОбОшибке,
	СтандартнаяОбработка,
	ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Операция = НСтр("ru = 'Выгрузка сертификата подписи'");
	ТекстСообщения = НСтр("ru = 'Не удалось выгрузить сертификат полученной подписи'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикПослеПолученияСертификатов, Неопределено);
	
КонецПроцедуры

Процедура ВключитьЖурналированиеВК(Оповещение, НастройкаОбмена, ПодключаемыйМодуль, Каталог, ИдентификаторКлиента)
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеВключенияЖурналирования", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);

	Если СистемнаяИнформация.ИдентификаторКлиента = ИдентификаторКлиента Тогда
		ОповещениеПослеВключенияЖурналирования = Новый ОписаниеОповещения("ПослеВключенияЖурналирования", ЭтотОбъект,
			ДополнительныеПараметры, "ПослеОшибкиВключенияЖурналированияВК", ЭтотОбъект);
		ПодключаемыйМодуль.НачатьВызовНачатьЖурналирование(ОповещениеПослеВключенияЖурналирования, Каталог);
	Иначе
		ОповещениеПослеЗапросаКаталога = Новый ОписаниеОповещения(
			"ВключитьЖурналированиеПослеУточненияКаталогаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ПараметрыФормы = Новый Структура("НастройкаОбмена", НастройкаОбмена);
		ОткрытьФорму("РегистрСведений.ПараметрыОбменСБанками.Форма.ЗапросКаталога", ПараметрыФормы, , , , ,
			ОповещениеПослеЗапросаКаталога);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВключитьЖурналированиеПослеУточненияКаталогаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОповещениеПослеВключенияЖурналирования = Новый ОписаниеОповещения("ПослеВключенияЖурналирования", ЭтотОбъект,
			ДополнительныеПараметры, "ПослеОшибкиВключенияЖурналированияВК", ЭтотОбъект);
		ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовНачатьЖурналирование(
			ОповещениеПослеВключенияЖурналирования, Результат);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеВключенияЖурналирования, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Транспорт

Процедура ПослеПолученияДокументовАсинхронныйОбмен(РезультатПолучения, ДополнительныеПараметры) Экспорт
	
	РезультатОтправки = ДополнительныеПараметры.РезультатОтправки;
	
	ЕстьОшибка = Ложь;
	
	Если ЗначениеЗаполнено(РезультатОтправки.ТекстОшибки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатОтправки.ТекстОшибки, , , , ЕстьОшибка);
	КонецЕсли;
	
	Отправлено = РезультатОтправки.КоличествоОтправленныхПакетов;
	
	Если РезультатПолучения = Неопределено Тогда // отменено пользователем или критичная ошибка
		Получено = 0;
		Оповестить("ОбновитьСостояниеОбменСБанками"); // для отправленных документов
		ЕстьОшибка = Истина;
	Иначе
		Получено = РезультатПолучения.КоличествоПолученныхПакетов;
		ВызватьОповещения(РезультатПолучения);
	
		Если РезультатПолучения.Свойство("ДанныеЭП") И РезультатПолучения.ДанныеЭП.Количество() Тогда
			Оповещение = Новый ОписаниеОповещения;
			Для Каждого КлючЗначение Из РезультатПолучения.ДанныеЭП Цикл
				ДобавитьПодписиИОпределитьСтатусы(Оповещение, КлючЗначение.Ключ, КлючЗначение.Значение);
			КонецЦикла;
		КонецЕсли;
		
		Оповестить("ОбновитьСостояниеОбменСБанками", РезультатПолучения.ПараметрОповещения);
	КонецЕсли;
	
	ПоказатьРезультатСинхронизации(Отправлено, Получено);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Не ЕстьОшибка);
	
КонецПроцедуры

// Получает новые документы из банка.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - обработчик, вызываемый после выполнения процедуры
//     * Результат - Структура - см описание в ОбменСБанкамиКлиентСервер.ПараметрыПолученияНовыхДокументовАсинхронныйОбмен();
//                 - Неопределено - процесс завершен пользователем или произошла ошибка.
//     * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком;
//  ИдентификаторСессии - ДвоичныеДанные - идентификатор установленной сессии;
//  ВыводитьОкноОжидания - Булево - признак отображения формы с колесом.
//  ОповещениеЗапускаДлительнойОперации - ОписаниеОповещения - вызывается после запуска длительной операции.
//                                        Используется для прерывания процесса по инициативе пользователя.
//     * ИдентификаторЗадания - УникальныйИдентификатор - идентификатор длительной операции
//     * ДополнительныеПараметры - Структура - дополнительные параметры.
//
Процедура ПолучитьНовыеДокументыИзБанка(
	Оповещение,
	НастройкаОбмена,
	ИдентификаторСессии,
	ВыводитьОкноОжидания = Ложь,
	ОповещениеЗапускаДлительнойОперации = Неопределено)
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПоПолучениюНовыхДокументовИзБанка(
		НастройкаОбмена, ИдентификаторСессии);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияНовыхДокументовИзБанка", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Если ОповещениеЗапускаДлительнойОперации <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеЗапускаДлительнойОперации, Результат.ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияНовыхДокументовИзБанка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // задание было отменено пользователем
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение);
		Возврат;
	КонецЕсли;
	
	ВывестиСообщенияДлительнойОперации(Результат.Сообщения);

	Если Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Получение документов из банка'");
		ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, РезультатОперации);
	КонецЕсли;

КонецПроцедуры

Процедура ПродолжитьОтправкуСообщенийОбменаПослеРасшифровкиМаркера(Результат, Параметры) Экспорт
	
	СоотвНастроекОбменаИСтруктурСертификатов = Неопределено;
	Если НЕ (Параметры.Свойство("СоотвНастроекОбменаИСтруктурСертификатов", СоотвНастроекОбменаИСтруктурСертификатов)
			 И ТипЗнч(СоотвНастроекОбменаИСтруктурСертификатов) = Тип("Соответствие")) Тогда
		
		Параметры.Вставить("СоотвНастроекОбменаИСтруктурСертификатов", Новый Соответствие);
		СоотвНастроекОбменаИСтруктурСертификатов = Параметры.СоотвНастроекОбменаИСтруктурСертификатов;
	КонецЕсли;
	
	СоответствиеВозврата = Неопределено;
	Если ТипЗнч(Результат) = Тип("Структура")
		И Результат.Свойство("СоответствиеНастроекОбменаИПараметровСертификатов", СоответствиеВозврата)
		И ТипЗнч(СоответствиеВозврата) = Тип("Соответствие") Тогда
		
		Для Каждого КлючИЗначение Из СоответствиеВозврата Цикл
			СоотвНастроекОбменаИСтруктурСертификатов.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьРезультатОтправкиПакетов", ЭтотОбъект, Параметры);
	СформироватьИОтправитьПакетыВБанк(
		ОписаниеОповещения, Параметры.МассивКОтправке, СоотвНастроекОбменаИСтруктурСертификатов, Параметры.Фрод);
	
КонецПроцедуры

// Вызывается из процедуры ВыполнитьДействияПослеОтправкиПЭД(...).
// Вызывает процедуру НачатьОтправкуПакетовЧерезДополнительнуюОбработку(...),
//   либо выполняет описание оповещения переданное в параметре ОбработчикПослеОтправкиПЭД.
//
// Параметры:
//   ДопПараметры - Структура - содержит поля:
//      РезультатОтправкиПЭД - Структура - результат отправки документов 
//      Параметры - Структура - необязательный параметр, дополнительные параметры,
//                              переданные из метода инициировавшего отправку ПЭД.
//      ОбработчикПослеОтправкиПЭД - ОписаниеОповещения - необязательный параметр, обработка результата отправки ПЭД.
//
Процедура ВыполнитьДействияПослеОтправкиПЭДЗавершить(Результат, ДопПараметры)
	
	Параметры = Неопределено;
	РезультатОтправкиПЭД = Неопределено;
	ОбработчикПослеОтправкиПЭД = Неопределено;
	ДопПараметры.Свойство("Параметры", Параметры);
	ДопПараметры.Свойство("РезультатОтправкиПЭД", РезультатОтправкиПЭД);
	ДопПараметры.Свойство("ОбработчикПослеОтправкиПЭД", ОбработчикПослеОтправкиПЭД);
	
	Если ДопПараметры.Свойство("СообщенияОбмена") Тогда
		МассивВладельцев = ОбменСБанкамиСлужебныйВызовСервера.ВладельцыСообщенийОбмена(ДопПараметры.СообщенияОбмена);
		Для Каждого Владелец Из МассивВладельцев Цикл
			Оповестить("ОбновитьСостояниеОбменСБанками", , Владелец);
		КонецЦикла;
	КонецЕсли;
	
	ВыполнитьОбработчикПослеОтправки = Истина;
	Если ТипЗнч(РезультатОтправкиПЭД) = Тип("Структура") Тогда
		Параметры.КоличествоПодготовленных = Параметры.КоличествоПодготовленных + РезультатОтправкиПЭД.КоличествоПодготовленных;
		Параметры.КоличествоОтправленных = Параметры.КоличествоОтправленных + РезультатОтправкиПЭД.КоличествоОтправленных;
		Если РезультатОтправкиПЭД.Свойство("ДанныеДляОтправкиЧерезДопОбработку")
			И РезультатОтправкиПЭД.ДанныеДляОтправкиЧерезДопОбработку.Количество() > 0 Тогда
			
			Параметры.Вставить("ДанныеДляОтправки", РезультатОтправкиПЭД.ДанныеДляОтправкиЧерезДопОбработку);
			Параметры.Вставить("ОбработчикПослеОтправкиПЭД", ОбработчикПослеОтправкиПЭД);
			ОтправитьЧерезДополнительнуюОбработку(Неопределено, Параметры);
			ВыполнитьОбработчикПослеОтправки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ВыполнитьОбработчикПослеОтправки
		И ТипЗнч(ОбработчикПослеОтправкиПЭД) = Тип("ОписаниеОповещения") Тогда
		ВыполнитьОбработкуОповещения(ОбработчикПослеОтправкиПЭД, Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеОтправкиДокументовВБанкАсинхронныйОбмен(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // задание было отменено пользователем
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		// Т.к. проблема критичная, то не запускается процесс получения документов.
		ВидОперации = НСтр("ru = 'Отправка документов в банк'");
		ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки,
			ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
		ДополнительныеПараметры.Вставить("РезультатОтправки", РезультатОперации);
		
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПолученияДокументовАсинхронныйОбмен", ЭтотОбъект, ДополнительныеПараметры);

		ПолучитьНовыеДокументыИзБанка(Оповещение, ДополнительныеПараметры.НастройкаОбмена,
			ДополнительныеПараметры.ИдентификаторСессии, ДополнительныеПараметры.ВыводитьОкноОжидания,
			ДополнительныеПараметры.ОповещениеЗапускаДлительнойОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьИОтправитьПакетыВБанк(Оповещение, МассивСообщенийОбмена, ИдентификаторыСессий = Неопределено, Фрод = Неопределено)
	
	РезультатОтправки = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПоОтправкеДокументовВБанк(
		МассивСообщенийОбмена, ИдентификаторыСессий, Фрод);
	
	ДополнительныеПараметры = Новый Структура("Оповещение", Оповещение);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Отправка документов в банк'");
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтправкиПлатежныхДокументовВБанк", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатОтправки, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ПослеОтправкиПлатежныхДокументовВБанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // задание было отменено пользователем
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		// Т.к. проблема критичная, то не запускается процесс отправки документов.
		ВидОперации = НСтр("ru = 'Отправка платежных документов в банк'");
		ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Неопределено);
	Иначе // выполнено
		Если Результат.Сообщения <> Неопределено Тогда
			Для каждого ЭлементКоллекции Из Результат.Сообщения Цикл
				ЭлементКоллекции.Сообщить();
			КонецЦикла;
		КонецЕсли;
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаЭД

Процедура ОбработатьДокументыПослеСозданияНастройкиОбмена(Результат, ДополнительныеПараметры) Экспорт

	МассивОбъектовОбработки = Новый Массив;
	Для Каждого Элемент Из ДополнительныеПараметры.НастройкиОбъектов Цикл
		Если Элемент.Значение.Результат = "НастройкиОпределены" ИЛИ Результат <> Неопределено Тогда
			МассивОбъектовОбработки.Добавить(Элемент.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивОбъектовОбработки.Количество() Тогда
		ОбработатьЭД(МассивОбъектовОбработки, ДополнительныеПараметры.Действия);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьОтклонениеЭД(СообщениеОбмена, ОписаниеОповещения) Экспорт
	
	ПродолжитьОбработку = ОбменСБанкамиСлужебныйВызовСервера.МожноОтклонитьЭтотЭД(СообщениеОбмена);
	Заголовок = НСтр("ru = 'Укажите причины отклонения документа'");
	
	Если ПродолжитьОбработку Тогда
		ТекстУточнения = "";
		Параметры = Новый Структура("СообщениеОбмена, ОписаниеОповещения", СообщениеОбмена, ОписаниеОповещения);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтклонениеЭДЗавершить", ЭтотОбъект, Параметры);
		ПоказатьВводСтроки(ОписаниеОповещения, ТекстУточнения, Заголовок, , Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОтклонениеЭДЗавершить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ОписаниеОповещения = ДополнительныеПараметры.ОписаниеОповещения;
		НовыйСтатус = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Отклонен");
		СтруктураПараметров = Новый Структура("Статус, ПричинаОтклонения", НовыйСтатус, Результат);
		СообщениеОбмена = ДополнительныеПараметры.СообщениеОбмена;
		ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(СообщениеОбмена, СтруктураПараметров);
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Истина);
	ИначеЕсли Результат <> Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Причина не указана, действие отменено.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьСтатусыПодписей(Оповещение, СообщениеОбмена)

	СтруктураСодержимогоСообщенияОбмена = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСодержимогоСообщенияОбмена(
		СообщениеОбмена);
		
	Если СтруктураСодержимогоСообщенияОбмена = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
		Возврат;
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДвоичныеДанныеЭД", СтруктураСодержимогоСообщенияОбмена.ДанныеЭД);
	ДополнительныеПараметры.Вставить("Подписи", СтруктураСодержимогоСообщенияОбмена.Подписи);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("РезультатПроверки", Новый Массив);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиПодписей", Оповещение);
	ОпределитьСтатусОчереднойПодписи(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОпределитьСтатусОчереднойПодписи(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.Подписи.Количество() = 0 Тогда
		ОбменСБанкамиСлужебныйВызовСервера.СохранитьРезультатыПроверкиПодписей(
			ДополнительныеПараметры.СообщениеОбмена, ДополнительныеПараметры.РезультатПроверки);
		ВсеПодписиВерны = Истина;
		Для Каждого Элемент Из ДополнительныеПараметры.РезультатПроверки Цикл
			Если Не Элемент.ПодписьВерна Тогда
				ВсеПодписиВерны = Ложь;
			КонецЕсли;
		КонецЦикла;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, ВсеПодписиВерны);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОпределенияСтатусаОчереднойПодписи", ЭтотОбъект, ДополнительныеПараметры);
	ДвоичныеДанныеПодписи = ДополнительныеПараметры.Подписи.Получить(0).Подпись;
	ДополнительныеПараметры.Подписи.Удалить(0);
	ЭлектроннаяПодписьКлиент.ПроверитьПодпись(
		Оповещение, ДополнительныеПараметры.ДвоичныеДанныеЭД, ДвоичныеДанныеПодписи);

КонецПроцедуры

Процедура ПослеОпределенияСтатусаОчереднойПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Ложь);
		Возврат; //  Не удалось создать менеджер криптографии, дальнейшая проверка не имеет смысла
	КонецЕсли;
	
	СтруктураЗаписи = Новый Структура();
	
	Если Результат = Истина Тогда
		СтруктураЗаписи.Вставить("ПодписьВерна", Истина);
		СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
		ДополнительныеПараметры.РезультатПроверки.Добавить(СтруктураЗаписи);
	ИначеЕсли ТипЗнч(Результат) = Тип("Строка") Тогда
		ВидОперации = НСтр("ru = 'Проверка подписи'");
		ТекстСообщения = НСтр("ru = 'При проверке подписи электронного документа произошла ошибка.
									|Электронный документ: %1,
									|Текст ошибки: %2'");
		
		ТекстСообщения = СтрШаблон(ТекстСообщения, ДополнительныеПараметры.СообщениеОбмена, Результат);
		ОбработатьОшибку(ВидОперации, ТекстСообщения, ТекстСообщения, ДополнительныеПараметры.СообщениеОбмена);
		СтруктураЗаписи.Вставить("ПодписьВерна", Ложь);
		СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
		ДополнительныеПараметры.РезультатПроверки.Добавить(СтруктураЗаписи);
	КонецЕсли;

	ОпределитьСтатусОчереднойПодписи(ДополнительныеПараметры)
	
КонецПроцедуры

// Обрабатывает переданные документы ИБ в системе обмена электронными документами в соответствии с параметрами.
//
// Параметры:
//  МассивСсылокНаОбъект - Массив из ОпределяемыйТип.ВладелецОбменСБанками, ДокументСсылка.СообщениеОбменСБанками - 
//                      объекты, которые необходимо обработать;
//  Действия - Строка - представление действия, которое необходимо произвести с электронными документами,
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - ссылка на сообщение обмена, если нужно обработать только один ЭД.
//
Процедура ОбработатьЭД(Знач МассивСсылокНаОбъект, Действия, Знач СообщениеОбмена = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура("Действия, СообщениеОбмена", Действия, СообщениеОбмена);

	Если ЭлектронноеВзаимодействиеКлиентСервер.ЕстьДействие(Действия, "Сформировать") Тогда
		СтандартнаяОбработка = Истина;
		ОбработчикОповещения = Новый ОписаниеОповещения(
			"ПродолжитьОбработкуДокументовПослеПереопределения", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСБанкамиКлиентПереопределяемый.ПриФормированииЭлектронныхДокументов(
			ОбработчикОповещения, МассивСсылокНаОбъект, СтандартнаяОбработка);
		Если НЕ СтандартнаяОбработка Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПродолжитьОбработкуДокументовПослеПереопределения(МассивСсылокНаОбъект, ДополнительныеПараметры)

КонецПроцедуры

Процедура ПродолжитьОбработкуДокументовПослеПереопределения(МассивСсылокНаОбъект, ДополнительныеПараметры) Экспорт
	
	Если Не МассивСсылокНаОбъект.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Фрод = ЗначениеИзКеша("Фрод");
	
	Если Не ЗначениеЗаполнено(Фрод) И МожноПолучитьMAC() Тогда
		ДополнительныеПараметры.Вставить("МассивСсылокНаОбъект", МассивСсылокНаОбъект);
		ДополнительныеПараметры.Вставить("ФродОбязателен", Ложь);
		ОповещениеПослеПолученияФрода = Новый ОписаниеОповещения(
			"ОтправитьДокументыПослеПолученияФрода", ЭтотОбъект, ДополнительныеПараметры);
		ПолучитьФрод(ОповещениеПослеПолученияФрода);
	Иначе
		ОбработатьЭлектронныеДокументы(
			МассивСсылокНаОбъект, ДополнительныеПараметры.Действия, ДополнительныеПараметры.СообщениеОбмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияОтпечатковОбработатьЭД(ОтпечаткиСертификатов, Параметры) Экспорт
	
	МассивОтпечатковСертификатов = Новый Массив;
	
	Если ТипЗнч(ОтпечаткиСертификатов) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из ОтпечаткиСертификатов Цикл
			МассивОтпечатковСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	КонецЕсли;
		
	Параметры.Вставить("МассивОтпечатковСертификатов", МассивОтпечатковСертификатов);
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПоОбработкеЭлектронныхДокументов(Параметры);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	Оповещение = Новый ОписаниеОповещения("ПослеОбработкиДокументов", ЭтотОбъект, Параметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат.Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ПодписатьЭД(РезультатВыполнения, Параметры) Экспорт
	
	СертификатыСообщенийОбмена = Неопределено;
	ВходящаяСтруктура = Параметры.Результат;
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		
		Параметры.КоличествоПодписанных = Параметры.КоличествоПодписанных
			+ ЭлектронноеВзаимодействиеСлужебныйКлиент.КоличествоПодписанныхЭД(РезультатВыполнения);
			
		Если РезультатВыполнения.Свойство("КоличествоПодписанных") Тогда // подписание через внешний модуль
			Параметры.КоличествоПодписанных = Параметры.КоличествоПодписанных + РезультатВыполнения.КоличествоПодписанных;
		КонецЕсли;
		
		// Оповещение произошло из процедуры БСП
		Если РезультатВыполнения.Свойство("НаборДанных") Тогда
			// Если Успех, необходимо перебрать элементы массива Набор данных
			// в подписанных эд в элементе массива являющимся структурой будет свойство "Свойства подписи"
			// такие СообщенияОбмена надо добавить в массив "МассивСообщенийОбмена" для обновления их статусов.
			МассивСообщенийОбмена = Неопределено;
			СоответствиеСертификатовПодписаннымЭд = Неопределено;
			
			Если РезультатВыполнения.Свойство("ВыбранныйСертификат") Тогда
				Если НЕ Параметры.Свойство("СоответствиеСертификатовПодписаннымЭд", СоответствиеСертификатовПодписаннымЭд) Тогда
				
					СоответствиеСертификатовПодписаннымЭд = Новый Соответствие;
					Параметры.Вставить("СоответствиеСертификатовПодписаннымЭд", СоответствиеСертификатовПодписаннымЭд);
					
				КонецЕсли;
				
				МассивСообщенийОбмена = СоответствиеСертификатовПодписаннымЭд[РезультатВыполнения.ВыбранныйСертификат.Ссылка];
				Если МассивСообщенийОбмена = Неопределено Тогда
					МассивСообщенийОбмена = Новый Массив;
					СоответствиеСертификатовПодписаннымЭд.Вставить(РезультатВыполнения.ВыбранныйСертификат.Ссылка, МассивСообщенийОбмена);
				КонецЕсли;
			КонецЕсли;

			Для Каждого ПодписываемыеДанные Из РезультатВыполнения.НаборДанных Цикл
				Если ПодписываемыеДанные.Свойство("СвойстваПодписи") Тогда
					МассивСообщенийОбмена.Добавить(ПодписываемыеДанные.СообщениеОбменСБанками);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВходящаяСтруктура.Свойство("СертификатыСообщенийОбмена", СертификатыСообщенийОбмена)
		И СертификатыСообщенийОбмена.Количество() > 0 Тогда
		
		СоответствиеСообщенийОбменаИПодписей = Неопределено;
		Если НЕ (Параметры.Свойство("СоответствиеСообщенийОбменаИПодписей", СоответствиеСообщенийОбменаИПодписей)
			ИЛИ ТипЗнч(СоответствиеСообщенийОбменаИПодписей) = Тип("Соответствие")) Тогда
			
			Параметры.Вставить("СоответствиеСообщенийОбменаИПодписей", Новый Соответствие);
		КонецЕсли;
		
		Для Каждого Элемент Из СертификатыСообщенийОбмена Цикл
			
			Структура = Элемент.Значение;
			СертификатыСообщенийОбмена.Удалить(Элемент.Ключ);
			
			МассивСертификатов = Структура.МассивСертификатов;
			ОписаниеПодписатьЭД = Новый ОписаниеОповещения("ПодписатьЭД", ЭтотОбъект, Параметры);
			
			ДанныеДляСпецОбработки = Неопределено;
			СообщенияОбменаКПодписи = Неопределено;
			Если Структура.Свойство("СообщенияОбменаКПодписи", СообщенияОбменаКПодписи) Тогда
				Подписать(ОписаниеПодписатьЭД, СообщенияОбменаКПодписи, МассивСертификатов);
			ИначеЕсли Структура.Свойство("ДанныеДляСпецОбработки", ДанныеДляСпецОбработки)
				И ТипЗнч(ДанныеДляСпецОбработки) = Тип("Соответствие") И ДанныеДляСпецОбработки.Количество() Тогда
				Параметры.Вставить("ДанныеДляСпецОбработки", ДанныеДляСпецОбработки);
				Параметры.Вставить("МассивСертификатов", МассивСертификатов);
				Параметры.Вставить("ОбработчикПродолжения", ОписаниеПодписатьЭД);
				НачатьПодписаниеБанковскихЭД(Параметры);
			Иначе
				ПодписатьЭД(Неопределено, Параметры);
			КонецЕсли;
			Прервать;
		КонецЦикла;
	Иначе
		
		ДействияПослеПодписанияЭД(Параметры);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияПослеПодписанияЭД(Параметры)
	
	СоответствиеСертификатовПодписаннымЭд = Неопределено;
	Если ТипЗнч(Параметры) = Тип("Структура")
		И Параметры.Свойство("СоответствиеСертификатовПодписаннымЭд", СоответствиеСертификатовПодписаннымЭд)
		И ТипЗнч(СоответствиеСертификатовПодписаннымЭд) = Тип("Соответствие") Тогда
		
		ОбменСБанкамиСлужебныйВызовСервера.ДействияПослеПодписанияЭДНаСервере(СоответствиеСертификатовПодписаннымЭд);
		
		МассивСообщенийОбменаДляПроверкиПодписей = Новый Массив;
		Для Каждого СертификатЭД Из СоответствиеСертификатовПодписаннымЭд Цикл
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСообщенийОбменаДляПроверкиПодписей, СертификатЭД.Значение, Истина);
		КонецЦикла;
			
		Параметры.Вставить("МассивСообщенийОбменаДляПроверкиПодписей", МассивСообщенийОбменаДляПроверкиПодписей);
		
		ПроверитьСтатусыПодписейСообщенийОбменаРекурсивно(Неопределено, Параметры);
		
		Возврат;
		
	КонецЕсли;
	
	ОтправкаСообщенийОбмена(Параметры);
	
КонецПроцедуры

Процедура ПроверитьСтатусыПодписейСообщенийОбменаРекурсивно(Результат, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.МассивСообщенийОбменаДляПроверкиПодписей.Количество() = 0 Тогда
		ОтправкаСообщенийОбмена(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;

	СообщениеОбмена = ДополнительныеПараметры.МассивСообщенийОбменаДляПроверкиПодписей.Получить(0);
	ДополнительныеПараметры.МассивСообщенийОбменаДляПроверкиПодписей.Удалить(0);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьСтатусыПодписейСообщенийОбменаРекурсивно", ЭтотОбъект, ДополнительныеПараметры);
	
	ОпределитьСтатусыПодписей(Оповещение, СообщениеОбмена);
	
КонецПроцедуры

Процедура ПроверитьСрокДействияСертификатаБанка(Сертификат, ДействителенДо, ПользовательОповещенОСрокеДействия)
	
	Если ПользовательОповещенОСрокеДействия Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОбОкончанииСрокаДействия = ДобавитьМесяц(ОбщегоНазначенияКлиент.ДатаСеанса(), 1) > ДействителенДо;
	
	Если ОповеститьОбОкончанииСрокаДействия Тогда
		ПараметрыФормы = Новый Структура("Сертификат", Сертификат);
		ОткрытьФорму("Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.Форма.ОповещениеОбОкончанииСрокаДействия",
			ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ТестНастройкиОбменаЧерезДополнительнуюОбработку(Обработчик, НастройкаОбмена)
	
	РеквизитыНастройкиОбмена = Новый Структура("ДополнительнаяОбработка");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	// Блок проверки наличия внешней обработки для обмена с банком
	
	#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
		ВнешняяОбработкаПодключена = ОбменСБанкамиСлужебныйВызовСервера.ПодключитьДополнительнуюОбработку(
			РеквизитыНастройкиОбмена.ДополнительнаяОбработка, Неопределено);
		Если Не ВнешняяОбработкаПодключена Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось подключить дополнительную обработку.'");
			ВыполнитьОбработкуОповещения(Обработчик, ТекстСообщения);
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	// Блок инициализации интерфейса.
	
	ДоступныеСертификаты = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(НастройкаОбмена);
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИндексТестаСертификата", 0);
	Параметры.Вставить("ДоступныеСертификаты", ДоступныеСертификаты);
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("ДополнительнаяОбработка", РеквизитыНастройкиОбмена.ДополнительнаяОбработка);
	Параметры.Вставить("ОбработчикПослеТестаНастройки", Обработчик);
	
	АдресВК = Неопределено;
	ВнешнийПодключаемыйМодуль = ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(
		РеквизитыНастройкиОбмена.ДополнительнаяОбработка, АдресВК);
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		Если ЗначениеЗаполнено(АдресВК) Тогда

			Параметры.Вставить("ТекущаяНастройкаОбменаЧерезДополнительнуюОбработку", НастройкаОбмена);
			Параметры.Вставить("УстановленаВнешняяКомпонента");
			Оповещение = Новый ОписаниеОповещения(
				"НачатьТестНастройкиОбменаЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
			УстановитьВнешнююКомпонентуДляОбменаЧерезОбработку(Оповещение, АдресВК);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	НачатьТестНастройкиОбменаЧерезДополнительнуюОбработку(Истина, Параметры);
	
КонецПроцедуры

Функция АктуаленКэшПароляСертификатаЧерезДополнительнуюОбработку(ДанныеСертификата, ВнешнийПодключаемыйМодуль)
	
	РеквизитыСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(
		ДанныеСертификата.СертификатПодписи);
	
	СлужебныеДанныеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль,
		РеквизитыСертификата.ДвоичныеДанныеСертификата);
		
	Если СлужебныеДанныеСертификата = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;

	ЕстьОшибка = Ложь;
	ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку = ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, СлужебныеДанныеСертификата.ИдентификаторХранилища, ЕстьОшибка);
	Если  ЕстьОшибка ИЛИ ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		УстановленПарольСертификата = ВнешнийПодключаемыйМодуль.УстановленПарольСертификата(
			РеквизитыСертификата.ДвоичныеДанныеСертификата);
		Возврат УстановленПарольСертификата = Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;

КонецФункции

Процедура ПродолжитьПолучениеВыпискиЧерезДополнительнуюОбработку(Параметры)
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	ДанныеСертификата = Параметры.ДанныеСертификата;
	ПарольСертификата = Параметры.ПарольСертификата;
	
	ПарольУстановлен = УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата, ПарольСертификата, Параметры.ВыбранныйСертификат);
	Если НЕ ПарольУстановлен Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПродолжитьПолучениеВыпискиПослеУстановкиСоединенияЧерезДопОбработку", ЭтотОбъект, Параметры);
	УстановитьСоединениеЧерезДополнительнуюОбработку(
		Оповещение, ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата, НастройкаОбмена);
	
КонецПроцедуры

Процедура ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработку(Параметры)
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	СертификатXML = Параметры.СертификатXML;
	ПарольСертификата = Параметры.ПарольСертификата;
	
	// Блок проверка авторизации на ресурсе банка.
	Попытка
		ВнешнийПодключаемыйМодуль.УстановитьПарольСертификата(СертификатXML, ПарольСертификата);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка авторизации на ресурсе банка.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Аутентификация на ресурсе банка'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, , НастройкаОбмена);
		УдалитьПарольИзСеанса(Параметры.ВыбранныйСертификат);
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, ТекстСообщения);
		Возврат;
	КонецПопытки;
		
	// Блок проверка установки соединения с банком.
	
	Оповещение = Новый ОписаниеОповещения(
		"ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработкуПослеАутентификации", ЭтотОбъект, Параметры);
	УстановитьСоединениеЧерезДополнительнуюОбработку(
		Оповещение, ВнешнийПодключаемыйМодуль, СертификатXML, НастройкаОбмена);
		
КонецПроцедуры

Процедура ОтправитьСообщенияОбменаЧерезДополнительнуюОбработку(ДанныеКОтправке, ДополнительныеПараметры)
	
	МассивСообщенийКОтправке = Новый Массив;
	Для Каждого КлючЗначение Из ДанныеКОтправке Цикл
		Если КлючЗначение.Значение.МассивСообщенийОбмена.Количество() Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				МассивСообщенийКОтправке, КлючЗначение.Значение.МассивСообщенийОбмена);
		КонецЕсли;
	КонецЦикла;
	Если МассивСообщенийКОтправке.Количество() Тогда
		
		ДанныеДляОтправки = ОбменСБанкамиСлужебныйВызовСервера.ДанныеДляОтправкиВБанкЧерезДопОбработку(
			МассивСообщенийКОтправке);
		ДополнительныеПараметры.Вставить("ДанныеДляОтправки", ДанныеДляОтправки);
	Иначе
		ДополнительныеПараметры.Вставить("ДанныеДляОтправки", Новый Соответствие);
	КонецЕсли;
	
	// Добавление документов, которые требуется подтвердить
	Для Каждого КлючЗначение Из ДанныеКОтправке Цикл
		Если КлючЗначение.Значение.МассивСообщенийТребующихПодтверждение.Количество() Тогда
			ДанныеДляОтправкиПоНастройке = ДополнительныеПараметры.ДанныеДляОтправки.Получить(КлючЗначение.Ключ);
			Если ДанныеДляОтправкиПоНастройке = Неопределено Тогда
				ДанныеДляОтправкиПоНастройке = Новый Структура;
			КонецЕсли;
			ДанныеДляОтправкиПоНастройке.Вставить(
				"ДокументыДляПодтверждения", КлючЗначение.Значение.МассивСообщенийТребующихПодтверждение);
			ДополнительныеПараметры.ДанныеДляОтправки.Вставить(КлючЗначение.Ключ, ДанныеДляОтправкиПоНастройке);
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗавершитьОтправкуСообщенийОбменаЧерезОбработку", ЭтотОбъект, ДополнительныеПараметры);
	ДополнительныеПараметры.Вставить("ОбработчикПослеОтправкиПЭД", ОписаниеОповещения);
	ОтправитьЧерезДополнительнуюОбработку(Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПодписатьЗапросыВыписок(Результат, ПараметрыОбработки)
	
	МассивСообщенийОбмена = ПараметрыОбработки.МассивСообщенийОбмена;
	МассивСертификатов = ПараметрыОбработки.МассивСертификатов;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьРезультатПодписиЗапросовВыписок", ЭтотОбъект, ПараметрыОбработки);
	Подписать(ОписаниеОповещения, МассивСообщенийОбмена, МассивСертификатов);
	
КонецПроцедуры

Процедура ВывестиИнформациюОбОбработанныхЭД(
	КоличествоСформированных,
	КоличествоПодписанных,
	КоличествоПодготовленных,
	КоличествоОтправленных,
	КоличествоПодтвержденных,
	ПодтверждениеПлатежейВБанке = Неопределено)
	
	ТекстЗаголовка = НСтр("ru = '1С:ДиректБанк'");
	
	Если КоличествоПодготовленных + КоличествоОтправленных > 0 Тогда
		ДопТекст = ?(КоличествоОтправленных > 0, НСтр("ru = 'отправлено'"), НСтр("ru = 'подготовлено к отправке'"));
		Количество = ?(КоличествоОтправленных > 0, КоличествоОтправленных, КоличествоПодготовленных);
		Если КоличествоПодписанных > 0 Тогда
			Если КоличествоСформированных > 0 Тогда
				Текст = НСтр("ru = 'Сформировано: (%1), подписано: (%2), %3 пакетов: (%4)'");
				Текст = СтрШаблон(Текст, КоличествоСформированных, КоличествоПодписанных, ДопТекст, Количество);
			Иначе
				Текст = НСтр("ru = 'Подписано: (%1), %2 пакетов: (%3)'");
				Текст = СтрШаблон(Текст, КоличествоПодписанных, ДопТекст, Количество);
			КонецЕсли;
		Иначе
			ДопТекст = ?(КоличествоОтправленных > 0, НСтр("ru = 'Отправлено'"), НСтр("ru = 'Подготовлено к отправке'"));
			Текст = НСтр("ru = '%1 пакетов: (%2)'");
			Текст = СтрШаблон(Текст, ДопТекст, Количество);
		КонецЕсли;
	Иначе
		Если КоличествоПодписанных > 0 Тогда
			Если КоличествоСформированных > 0 Тогда
				Текст = НСтр("ru = 'Сформировано: (%1), подписано: (%2)'");
				Текст = СтрШаблон(Текст, КоличествоСформированных, КоличествоПодписанных);
			Иначе
				Текст = НСтр("ru = 'Подписано: (%1)'");
				Текст = СтрШаблон(Текст, КоличествоПодписанных);
			КонецЕсли;
		Иначе
			Если КоличествоСформированных > 0 Тогда
				Текст = НСтр("ru = 'Сформировано: (%1)'");
				Текст = СтрШаблон(Текст, КоличествоСформированных);
			ИначеЕсли КоличествоПодтвержденных = 0 Тогда
				Текст = НСтр("ru = 'Обработанных документов нет...'");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если КоличествоОтправленных > 0 И ПодтверждениеПлатежейВБанке <> Неопределено
		И ЗначениеЗаполнено(ПодтверждениеПлатежейВБанке.БанкиТребующиеПодтверждениеПлатежаВЛК) Тогда
		Текст = Текст + Символы.ПС + НСтр("ru = 'Необходимо подтвердить документы в личном кабинете банка.'");
		ПодтверждениеПлатежейВБанке.БанкиТребующиеПодтверждениеПлатежаВЛК = ОбщегоНазначенияКлиентСервер.СвернутьМассив(
			ПодтверждениеПлатежейВБанке.БанкиТребующиеПодтверждениеПлатежаВЛК);
		
		Если ПодтверждениеПлатежейВБанке.БанкиТребующиеПодтверждениеПлатежаВЛК.Количество() > 1 Тогда
			СписокБанков = "(";
			Для каждого ЭлементКоллекции Из ПодтверждениеПлатежейВБанке.БанкиТребующиеПодтверждениеПлатежаВЛК Цикл
				СписокБанков = СписокБанков + ЭлементКоллекции + ", ";
			КонецЦикла;
			ПоследняяЗапятая = СтрНайти(СписокБанков, ", ", НаправлениеПоиска.СКонца);
			СписокБанков = Сред(СписокБанков, 1, ПоследняяЗапятая - 1) + ")";
			Текст = Текст + Символы.ПС + СписокБанков;
		КонецЕсли;
		Если ПодтверждениеПлатежейВБанке.НастройкиОбмена.Количество() Тогда
			ПараметрыФормы = Новый Структура("НастройкиОбмена", ПодтверждениеПлатежейВБанке.НастройкиОбмена);
			ОткрытьФорму("Обработка.ОбменСБанками.Форма.ПодтверждениеПлатежейВЛичномКабинетеБанка", ПараметрыФормы, , ,
						, , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ПоказатьОповещениеПользователя(ТекстЗаголовка, , Текст);
	КонецЕсли;

КонецПроцедуры

Процедура ПослеОбработкиДокументов(РезультатЗадания, Параметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		Возврат;
	КонецЕсли;
	
	ВывестиСообщенияДлительнойОперации(РезультатЗадания.Сообщения);

	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Обработка электронных документов.'");
		ОбработатьОшибку(
			ВидОперации, РезультатЗадания.ПодробноеПредставлениеОшибки, РезультатЗадания.КраткоеПредставлениеОшибки);
		Возврат;
	Иначе // выполнено
		Результат = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		Если Результат <> Неопределено Тогда
			
			Если Результат.ТребуетсяНастройкаОбмена Тогда
				НастроитьОбменСБанками(Результат.НастройкиОбъектов, Параметры.Действия);
				Возврат;
			КонецЕсли;
			
			Если Результат.ПользовательОповещенОПлатформе8_3_18 Тогда
				ДобавитьЗначениеВКеш("ПользовательОповещенОПлатформе8_3_18", Истина);
			КонецЕсли;
			
			Если Результат.ТребуетсяФрод Тогда
				Параметры.Вставить("ФродОбязателен", Истина);
				ОповещениеПослеПолученияФрода = Новый ОписаниеОповещения(
					"ОтправитьДокументыПослеПолученияФрода", ЭтотОбъект, Параметры);
				ПолучитьФрод(ОповещениеПослеПолученияФрода, Истина);
				Возврат;
			КонецЕсли;

			
			Если Результат.ОтправленныеДокументы.Количество() Тогда
				Оповестить("ОтправленоDirectBank", Результат.ОтправленныеДокументы);
			КонецЕсли;
			
			Если Результат.КоличествоНовыхЭД > 0 Тогда
				Оповестить("ОбновитьСостояниеОбменСБанками");
			КонецЕсли;
			
			Параметры.Вставить("Результат", Результат);
			Параметры.Вставить("КоличествоОтправленных", Результат.КоличествоОтправленных);
			Параметры.Вставить("КоличествоПодготовленных", Результат.КоличествоПодготовленных);
			Параметры.Вставить("КоличествоНовыхЭД", Результат.КоличествоНовыхЭД);
			Параметры.Вставить("КоличествоПодписанных", 0);
			Параметры.Вставить("КоличествоПодтвержденных", 0);
			Параметры.Вставить("СоотвСертификатовИИхСтруктур", Результат.СоотвСертификатовИИхСтруктур);
			
			Если Результат.СоотвСертификатовИИхСтруктур.Количество() И Результат.СертификатыСообщенийОбмена.Количество() Тогда
				
				Параметры.Вставить("СертификатыСообщенийОбмена", Результат.СертификатыСообщенийОбмена);
				Параметры.Вставить("СоотвСертификатовИИхСтруктур", Результат.СоотвСертификатовИИхСтруктур);
				Параметры.Вставить("ИндексПервойИтерации", 0);
				Параметры.Вставить("ИндексВторойИтерации", 0);
				Параметры.Вставить("ИндексТретьейИтерации", 0);
				ПодписатьЭД(Неопределено, Параметры);
			Иначе
				Параметры.Вставить("СоотвСертификатовИИхСтруктур", Результат.СоотвСертификатовИИхСтруктур);
				ОтправкаСообщенийОбмена(Параметры);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ОтправитьДокументыПослеПолученияФрода(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат ИЛИ Не ДополнительныеПараметры.ФродОбязателен Тогда
		ОбработатьЭлектронныеДокументы(ДополнительныеПараметры.МассивСсылокНаОбъект, ДополнительныеПараметры.Действия,
			ДополнительныеПараметры.СообщениеОбмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьФрод(Оповещение, Обязательно = Ложь)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияФрода", Оповещение);
	ДополнительныеПараметры.Вставить("Обязательно", Обязательно);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеПопыткиПодключенияРасширенияПолученияИнформацииОКомпьютере", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиРасширениеКлиент.ПодключитьРасширениеПолученияИнформацииОКомпьютере(ОписаниеОповещения);
	
КонецПроцедуры

Процедура ПослеПопыткиПодключенияРасширенияПолученияИнформацииОКомпьютере(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		ПослеПодключенияРасширенияПолученияИнформацииОКомпьютере(Истина, ДополнительныеПараметры);
	Иначе
		Если ДополнительныеПараметры.Обязательно Тогда
			Обработчик = Новый ОписаниеОповещения(
				"УстановитьРасширениеПослеОтвета", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Обработка.ОбменСБанками.Форма.ВопросОбУстановкеРасширенияДляПолученияИнформацииОКомпьютере", ,
				, , , , Обработчик);
		Иначе
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияФрода, Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьРасширениеПослеОтвета(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Оповещение = Новый ОписаниеОповещения("ПодключитьРасширениеПослеУстановки", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСБанкамиРасширениеКлиент.УстановитьРасширениеПолученияИнформацииОКомпьютере(Оповещение);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияФрода, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодключитьРасширениеПослеУстановки(ДополнительныеПараметры) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеПодключенияРасширенияПолученияИнформацииОКомпьютере", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиРасширениеКлиент.ПодключитьРасширениеПолученияИнформацииОКомпьютере(ОписаниеОповещения);
	
КонецПроцедуры

Процедура ПослеПодключенияРасширенияПолученияИнформацииОКомпьютере(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПолученияИнформацииОСетевыхАдаптерах", ЭтотОбъект, ДополнительныеПараметры);
		ОбменСБанкамиРасширениеКлиент.ПолучениеИнформацииОСетевыхАдаптерах(Оповещение);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияФрода, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияИнформацииОСетевыхАдаптерах(ИнформацияОСетевыхАдаптерах, ДополнительныеПараметры) Экспорт
	
	МассивMAC = Новый Массив;
	Для Каждого Элемент Из ИнформацияОСетевыхАдаптерах Цикл
		МассивMAC.Добавить(Элемент.MACАдрес);
	КонецЦикла;
	
	ДобавитьЗначениеВКеш("Фрод", МассивMAC);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияФрода, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ЗарплатныйПроект

// Вызывает оповещения об изменении объектов, которые были заполнены в переопределяемой части.
//
// Параметры:
//   СтруктураДанных - Структура - данные возврата переопределяемой части:
//      * МассивОповещений - Массив - оповещения, которые необходимо вызвать.
//
Процедура ВызватьОповещения(СтруктураДанных) Экспорт
	
	Если СтруктураДанных.Свойство("МассивОповещений") Тогда
		Для Каждого Элемент Из СтруктураДанных.МассивОповещений Цикл
			Для Каждого КлючЗначение Из Элемент Цикл
				Если КлючЗначение.Значение = Неопределено Тогда
					Оповестить(КлючЗначение.Ключ);
				Иначе
					ОповеститьОбИзменении(КлючЗначение.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбменСИспользованиемДополнительнойОбработки

Функция ИнициализироватьИнтерфейсДополнительнойОбработки(ДополнительнаяОбработка, АдресФайлаВнешнегоМодуля, ИмяВнешнегоМодуля, АдресКомпоненты)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ВремФайл = ПолучитьИмяВременногоФайла("epf");
		ДвоичныеДанныеОбработки = ПолучитьИзВременногоХранилища(АдресФайлаВнешнегоМодуля);
		ДвоичныеДанныеОбработки.Записать(ВремФайл);
		ПодключаемыйМодуль = ВнешниеОбработки.ПолучитьФорму(ВремФайл);
	#Иначе
		ИмяФормы = "ВнешняяОбработка." + ИмяВнешнегоМодуля + ".Форма";
		ПодключаемыйМодуль = ПолучитьФорму(ИмяФормы, Новый Структура("РежимЭДО", Истина), , Новый УникальныйИдентификатор);
	#КонецЕсли
	
	Попытка
		ОписаниеОбработки = ПодключаемыйМодуль.ОписаниеОбработки();
		ВерсияAPI = ОписаниеОбработки.ВерсияAPI;
	Исключение
		ВерсияAPI = 1;
	КонецПопытки;
		
	Если ВерсияAPI = 1 Тогда
		Попытка
			ПодключаемыйМодуль.Инициализировать();
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка инициализации дополнительной обработки.
										|Код ошибки: ДО-%1
										|%2'");
			ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Инициализация внешней обработки'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
			Возврат Ложь;
		КонецПопытки;
	Иначе
		Попытка
			ИнициализацияВыполнена = ПодключаемыйМодуль.НачатьИнициализацию(АдресКомпоненты);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка инициализации дополнительной обработки.
										|Код ошибки: ДО-%1
										|%2'");
			ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Инициализация внешней обработки'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
			Возврат Ложь;
		КонецПопытки;
		
		Если НЕ ИнициализацияВыполнена Тогда
			Возврат Ложь;
		Иначе
			Попытка
				ПодключаемыйМодуль.ЗавершитьИнициализацию();
			Исключение
				ШаблонОшибки = НСтр("ru = 'Ошибка завершения инициализации.
											|Код ошибки: ДО-%1
											|%2'");
				ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
				ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
				Операция = НСтр("ru = 'Завершение инициализации внешней обработки'");
				ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
				Возврат Ложь;
			КонецПопытки;
		КонецЕсли;

	КонецЕсли;
	
	ЗакэшироватьФормуДополнительнойОбработки(ДополнительнаяОбработка, ПодключаемыйМодуль);
	
	Возврат Истина;
	
КонецФункции

Процедура ОтправитьДокументыЧерезДополнительнуюОбработку(Оповещение, НастройкаОбмена, ДополнительнаяОбработка, ДанныеДляОтправки)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиДокументов", Оповещение);
	ДополнительныеПараметры.Вставить("ДанныеДляОтправки", ДанныеДляОтправки);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОбработчикПослеПодключения = Новый ОписаниеОповещения(
		"ПродолжитьОтправкуПакетовЧерезДополнительнуюОбработкуПослеПодключения", ЭтотОбъект, ДополнительныеПараметры);
	ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(ОбработчикПослеПодключения, ДополнительнаяОбработка);

КонецПроцедуры

Процедура ПослеОтправкиДокументовЧерезДополнительнуюОбработку(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Свойство("Отправлено") Тогда
		ЗаголовокОповещения = НСтр("ru = 'Обмен 1С:ДиректБанк'");
		ШаблонОповещения = НСтр("ru = 'Отправлено: (%1).'");
		ТекстОповещения = СтрШаблон(ШаблонОповещения, Результат.Отправлено);
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Результат.Успех);
	
КонецПроцедуры

Процедура ПодтвердитьПлатежЗавершитьЧерезДополнительнуюОбработку(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ПодтвердитьПлатежиЧерезДополнительнуюОбработку(ДополнительныеПараметры.Оповещение,
			ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.ВнешнийПодключаемыйМодуль,
			ДополнительныеПараметры.СертификатXML, Истина);
		Возврат;
	КонецЕсли;
	
	Сессия = ДополнительныеПараметры.Сессия;
	ВнешнийПодключаемыйМодуль = ДополнительныеПараметры.ВнешнийПодключаемыйМодуль;
	СертификатXML = ДополнительныеПараметры.СертификатXML;
	Пароль = Результат;
	ПараметрыЗапроса = Новый Структура("Способ, Пароль, Сессия");
	ПараметрыЗапроса.Способ = "SMS";
	ПараметрыЗапроса.Пароль = Пароль;
	ПараметрыЗапроса.Сессия = Сессия;
		
	Попытка
		Результат = ВнешнийПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, 5, ПараметрыЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка подтверждения платежного поручения.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Подтверждение платежного поручения'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, ДополнительныеПараметры.СообщениеОбмена);
		ПодтвердитьПлатежиЧерезДополнительнуюОбработку(ДополнительныеПараметры.Оповещение,
			ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.ВнешнийПодключаемыйМодуль,
			ДополнительныеПараметры.СертификатXML, Истина);
		Возврат;
	КонецПопытки;
	
	Если Результат.Количество() = 0 Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Нет данных для подтверждения'"));
	КонецЕсли;
	
	Если Не ПустаяСтрока(Результат[0].ТекстОшибки) Тогда
		ТекстСообщения = НСтр("ru = 'Ошибка при подтверждении платежного поручения:'") + " " + Результат[0].ТекстОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ПодтвердитьПлатежиЧерезДополнительнуюОбработку(ДополнительныеПараметры.Оповещение,
			ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.ВнешнийПодключаемыйМодуль,
			ДополнительныеПараметры.СертификатXML, Истина);
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Доставлен"));
	ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(
		ДополнительныеПараметры.СообщениеОбмена, СтруктураРеквизитов);

	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Платеж подтвержден'"), , , БиблиотекаКартинок.Успешно32);

	ПодтвердитьПлатежиЧерезДополнительнуюОбработку(ДополнительныеПараметры.Оповещение,
		ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.ВнешнийПодключаемыйМодуль,
		ДополнительныеПараметры.СертификатXML, ДополнительныеПараметры.ЕстьОшибка);

КонецПроцедуры

Процедура РасширеннаяАутентификацияЧерезДопОбработку(ВнешнийПодключаемыйМодуль, Сертификат, Сессия, ОООЗ)

	Попытка
		ВнешнийПодключаемыйМодуль.ОтправитьОдноразовыйПарольРасширеннойАутентификацииПоSMS(
			Сертификат, Сессия);
	Исключение
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонОшибки = НСтр("ru = 'Ошибка запуска аутентификации по SMS.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(
			ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка соединения'");
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
		ВыполнитьОбработкуОповещения(ОООЗ, Ложь);
		Возврат;
	КонецПопытки;

	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеПослеОтправкиОдноразовогоПароля", ОООЗ);
	Параметры.Вставить("Сессия", Сессия);
	Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
	Параметры.Вставить("Сертификат", Сертификат);
	Оповещение = Новый ОписаниеОповещения(
		"ПослеВводаОдноразовогоПароляСессияЧерезДопОбработку", ЭтотОбъект, Параметры);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИдентификаторСессии", Сессия.Идентификатор);

	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросОдноразовогоПароля", ПараметрыФормы, , , , , Оповещение);

КонецПроцедуры

Процедура ПослеУстановкиСоединенияЧерезДополнительнуюОбработку(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификата, Неопределено);
		Возврат;
	КонецЕсли;
	
	Попытка
		СертификатXML = ДополнительныеПараметры.ПодключаемыйМодуль.ДополнитьСертификат(
			ДополнительныеПараметры.СертификатXML);
	Исключение
		ШаблонОшибки = НСтр("ru = 'При загрузке сертификата произошла ошибка.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ДополнительныеПараметры.ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Получение дополнительных данных сертификата'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификата, Неопределено);
		Возврат;
	КонецПопытки;

	ДанныеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(ДополнительныеПараметры.ПодключаемыйМодуль, СертификатXML);
	
	Если Не ДанныеСертификата = Неопределено Тогда
		ДанныеСертификата.Вставить("СертификатXML", СертификатXML);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификата, ДанныеСертификата);
	
КонецПроцедуры

Процедура ПродолжитьПодписаниеЧерезДополнительнуюОбработку(Параметры)
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	
	СертификатXML = Параметры.СертификатXMLЧерезДопОбработку;
	СтруктураСертификата = Параметры.СтруктураСертификата;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	ПарольУстановлен = УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, СертификатXML, СтруктураСертификата.ПарольСертификата, Параметры.ВыбранныйСертификат);
	
	Если ПарольУстановлен Тогда
		Оповещение = Новый ОписаниеОповещения("ПодписаниеЭДЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
		УстановитьСоединениеЧерезДополнительнуюОбработку(
			Оповещение, ВнешнийПодключаемыйМодуль, СертификатXML, НастройкаОбмена);
		Возврат;
	КонецЕсли;
	
	ОписаниеПодписатьЭД = Неопределено;
	Если Параметры.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД)Тогда
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьОтправкуПакетаЧерезДополнительнуюОбработку(Параметры)
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	
	ПарольУстановлен = Параметры.ПарольУстановлен;
	ДанныеСертификата = Параметры.ДанныеСертификата;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	Если ПарольУстановлен Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ОтправитьПакетыЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
		УстановитьСоединениеЧерезДополнительнуюОбработку(
			Оповещение, ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата, НастройкаОбмена);
		Возврат;
	КонецЕсли;

КонецПроцедуры

Функция ДоступноеХранилищеЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль)
	
	Хранилища = ПодключенныеХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Ложь);
	
	Если Хранилища = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого ИдентификаторХранилища Из Хранилища Цикл
		
		ЕстьОшибка = Ложь;
		НуженПин = ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, ЕстьОшибка);
			
		Если ЕстьОшибка Тогда
			Продолжить;
		КонецЕсли;
		
		УстановленPIN = УстановленPINКодХранилищаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, ЕстьОшибка);
			
		Если ЕстьОшибка Тогда
			Продолжить;
		КонецЕсли;
	
		Если НЕ НуженПин ИЛИ УстановленPIN Тогда
			Возврат ИдентификаторХранилища;

		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ФормаДополнительнойОбработкиИзКэш(Ключ)
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];

	Если ПараметрыПодсистемыОбменСБанками = Неопределено Тогда
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками", Новый Соответствие);
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
		ПараметрыПодсистемыОбменСБанками.Вставить("ДополнительныеОбработки");
	Иначе
		ВнешниеКомпонентыИзКеша = ПараметрыПодсистемыОбменСБанками.Получить("ДополнительныеОбработки");
		Если ЗначениеЗаполнено(ВнешниеКомпонентыИзКеша) Тогда
			Возврат ВнешниеКомпонентыИзКеша.Получить(Ключ);
		КонецЕсли;
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

Процедура ЗакэшироватьФормуДополнительнойОбработки(Ключ, ПодключаемыйМодуль)
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	ВнешниеКомпонентыИзКеша = ПараметрыПодсистемыОбменСБанками.Получить("ДополнительныеОбработки");
	Если ВнешниеКомпонентыИзКеша = Неопределено Тогда
		ВнешниеКомпонентыИзКеша = Новый Соответствие;
	КонецЕсли;
	ВнешниеКомпонентыИзКеша.Вставить(Ключ, ПодключаемыйМодуль);
	ПараметрыПодсистемыОбменСБанками.Вставить("ДополнительныеОбработки", ВнешниеКомпонентыИзКеша);
	
КонецПроцедуры

Процедура ПослеВопросаОУстановкеКомпоненты(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ОповещениеПослеУстановкиКомпоненты = Новый ОписаниеОповещения(
			"ПослеУстановкиКомпоненты", ЭтотОбъект, ДополнительныеПараметры);
		НачатьУстановкуВнешнейКомпоненты(ОповещениеПослеУстановкиКомпоненты, ДополнительныеПараметры.Местоположение);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеУстановкиКомпоненты(ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Истина);
	
КонецПроцедуры

Процедура ЗавершитьОтправкуСообщенийОбменаЧерезОбработку(Результат, Параметры) Экспорт
	
	ВыполнитьДействияПослеОтправки(Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область ОбменЧерезВК

// Производит сериализацию данных.
//
// Параметры:
//  Значение - Произвольный - данные для сериализации;
//
// Возвращаемое значение:
//  Строка - сериализованные данные.
//
Функция СериализованныеДанные(Знач Значение)

	#Если НЕ ВебКлиент Тогда

		Если Значение = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;

		Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
		ОбъектXDTO = Сериализатор.ЗаписатьXDTO(Значение);
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.УстановитьСтроку();
		ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ОбъектXDTO);

		Возврат ЗаписьXML.Закрыть();
		
	#Иначе
		
		Возврат ОбменСБанкамиСлужебныйВызовСервера.СериализованныеДанные(Значение);

	#КонецЕсли

КонецФункции

// Производит десериализацию данных.
//
// Параметры:
//   ПредставлениеXML - Строка - сериализованные данные;
//
// Возвращаемое значение:
//   Произвольный - десериализованные данные.
//
Функция ДеСериализованныеДанные(Знач ПредставлениеXML)

	#Если НЕ ВебКлиент Тогда
	
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку(ПредставлениеXML);
		ЧтениеXML.Прочитать();

		Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
		Возврат Сериализатор.ПрочитатьXML(ЧтениеXML);
	#Иначе
		
		Возврат ОбменСБанкамиСлужебныйВызовСервера.ДеСериализованныеДанные(ПредставлениеXML);
		
	#КонецЕсли

КонецФункции

Процедура ПроверитьУстановкуПодписиПослеАутентификацииНаТокене(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ДополнительныеПараметры.Контекст.ОписаниеОшибки = Результат;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, ДополнительныеПараметры.Контекст);
		Возврат;
	ИначеЕсли Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ОписаниеОшибки = НСтр("ru = 'Ошибка аутентификации на электронном ключе'");
		ДополнительныеПараметры.Контекст.ОписаниеОшибки = ОписаниеОшибки;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, ДополнительныеПараметры.Контекст);
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения("ПослеПодписиТестовойСтрокиЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	СтрокаПодписиBase64 = "JiMxMDU4OyYjMTA3NzsmIzEwODk7JiMxMDkwOw=="; // "Тест" в Base64
	
	ПодписатьДанныеЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, СтрокаПодписиBase64,
		ДополнительныеПараметры.ДанныеСертификата);

КонецПроцедуры

Процедура ПослеПодписиТестовойСтрокиЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ДополнительныеПараметры.Контекст.ОписаниеОшибки = Результат;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, ДополнительныеПараметры.Контекст);
		Возврат;
	ИначеЕсли Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ОписаниеОшибки = НСтр("ru = 'Ошибка подписи данных на электронном ключе'");
		ДополнительныеПараметры.Контекст.ОписаниеОшибки = ОписаниеОшибки;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, ДополнительныеПараметры.Контекст);
		Возврат;
	КонецЕсли;
	
	ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками.ДанныеПодписиВК", Результат.ДанныеПодписи);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, ДополнительныеПараметры.Контекст);

КонецПроцедуры

Процедура ПослеОправкиПакетовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат.Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.Результат);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
		Возврат;
	ИначеЕсли Результат.Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("КоличествоОтправленных", Результат.КоличествоОтправленных);
	ДополнительныеПараметры.Вставить("КоличествоПодготовленных", Результат.КоличествоПодготовленных);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПродолжитьСинхронизациюПослеПолученияНовыхДокументовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьНовыеДокументыВК(Оповещение, Результат.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ПродолжитьСинхронизациюПослеПолученияНовыхДокументовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат.Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.Результат);
	КонецЕсли;
	ПоказатьРезультатСинхронизации(ДополнительныеПараметры.КоличествоОтправленных, Результат.КоличествоПолученных);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Истина);
	
КонецПроцедуры

// Асинхронный обработчик после ввода пароля сертификата пользователем.
// Вызывается из модуля ОбменСБанкамиКлиент.
//
// Параметры:
//  Результат - Структура - сертификат и пароль для подписи электронного документа
//  ДополнительныеПараметры - Структура - контекст выполнения.
//
Процедура ПодписатьЗапросыВыписокПослеВводаПароляЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		СтруктураВозврата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодготовкиЗапросовВыписки, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодписанияЗапросовВыписокЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодписания", Оповещение);
	ДополнительныеПараметры.Вставить("СертификатСсылка", Результат.ВыбранныйСертификат);
	ДополнительныеПараметры.Вставить("Пароль", Результат.ПарольСертификата);
	ДополнительныеПараметры.Вставить("КоличествоПодписанных", 0);
	МассивСообщенийОбмена = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(
		ДополнительныеПараметры.МассивСообщенийОбменаКОтправке);
	ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
		
	ПодписатьРекурсивноЭДПоСертификатуЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ЗапуститьПроцессПолученияВыпискиЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.КоличествоОтправленных = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МассивСообщенийОбмена", ДополнительныеПараметры.МассивСообщенийОбменаКОтправке);
	ПараметрыФормы.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
	ПараметрыФормы.Вставить("ВидОперации", "ПолучениеВыписки");
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыписки", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, , , , , Оповещение);
	
КонецПроцедуры

Процедура ПодписатьРекурсивноЭДПоСертификатуЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.МассивСообщенийОбмена.Количество() = 0 Тогда
		СтруктураВозврата = Новый Структура("КоличествоПодписанных", ДополнительныеПараметры.КоличествоПодписанных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	СообщениеОбмена = ДополнительныеПараметры.МассивСообщенийОбмена.Получить(0);
	ДополнительныеПараметры.МассивСообщенийОбмена.Удалить(0);
	
	Оповещение = Новый ОписаниеОповещения("ПродолжитьРекурсивноеПодписаниеЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодписатьЭДПоСертификатуЧерезВК(Оповещение, СообщениеОбмена, ДополнительныеПараметры.НастройкаОбмена,
		ДополнительныеПараметры.СертификатСсылка, ДополнительныеПараметры.Пароль);
	
КонецПроцедуры

Процедура ПродолжитьРекурсивноеПодписаниеЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат);
		СтруктураВозврата = Новый Структура("КоличествоПодписанных", ДополнительныеПараметры.КоличествоПодписанных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено Или Результат = Ложь Тогда
		СтруктураВозврата = Новый Структура("КоличествоПодписанных", ДополнительныеПараметры.КоличествоПодписанных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.КоличествоПодписанных = ДополнительныеПараметры.КоличествоПодписанных + 1;
	
	ПодписатьРекурсивноЭДПоСертификатуЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

// Предлагает пользователю выбрать сертификат и ввести пароль (асинхронно).
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры:
//       * Результат - Структура - данные выбора пользователя:
//                       ** СертификатBase64 - Строка - данные сертификата;
//                       ** Пароль - Строка - введенный пароль к сертификату.
//                       ** ТребуетсяПароль - Булево - признак наличия пароля.
//                     Строка - текст ошибки для вывода в виде сообщения;
//                     Неопределено - пользователь отказался продолжать операцию.
//       * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//    ПодключаемыйМодуль - Произвольный - внешняя компонента банка.
//
Процедура ПолучитьДанныеАутентификацииНаТокенеВК(Оповещение, ПодключаемыйМодуль)
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияДанныхАутентификации", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеПолученияКлючей = Новый ОписаниеОповещения(
		"ПоказатьВыборПослеПолученияКлючейВК", ЭтотОбъект, ДополнительныеПараметры);

	ПолучитьСертификатыИзХранилищаВК(ОповещениеПослеПолученияКлючей, ПодключаемыйМодуль);
		
КонецПроцедуры

// Получает свойства сертификатов (асинхронно).
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//                                    которая будет вызвана после завершения вызова метода со следующими параметрами: 
//                   * Результат - Массив - результат вызова метода. В элементах структуры со свойствами:
//                                   ** СертификатBase64 - Строка - содержимое сертификата;
//                                   ** ДанныеСертификата - Структура - свойства сертификата:
//                                       *** Псевдоним - Строка - псевдоним ключа ЭП;
//                                       *** Отпечаток - Строка - уникальный идентификатор ключа ЭП;
//                                       *** СерийныйНомер - Строка - серийный номер ключа в hex;
//                                       *** ИмяИздателя - Строка - имя издателя сертификата.
//                               - Строка - текст ошибки.
//                   * ДополнительныеПараметры - Произвольный - значение, которое было указано
//                                                              при создании объекта ОписаниеОповещения.
//  ДанныеСертификатовBase64 - Массив - данные сертификатов в формате Base64, для которых необходимо получить свойства;
//  ПодключаемыйМодуль - Произвольный - внешняя компонента банка.
//
Процедура ПолучитьСвойстваСертификатовЧерезВК(Оповещение, ДанныеСертификатовBase64, ПодключаемыйМодуль)
	
	ДанныеСертификатов = Новый Массив;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДанныеСертификатов", ДанныеСертификатов);
	ДополнительныеПараметры.Вставить("СертификатыКлюча", ДанныеСертификатовBase64);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияКлючей", Оповещение);
	ПолучитьДанныеСертификатовРекурсивноВК(ДополнительныеПараметры);
	
КонецПроцедуры

// Устанавливает соединение с банком через внешнюю компоненту (асинхронно).
// Аутентификация на токене должна уже быть выполнена.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое при выполнении процедуры:
//      Результат - Булево - возвращает Истина, если соединение успешно установлено.
//                - Строка - содержит текст ошибки, если соединение не было установлено.
//                - Неопределено - пользователь не ввел одноразовый пароль.
//  ПодключаемыйМодуль - Произвольный - внешняя компонента банка;
//  СертификатBase64 - Строка - данные сертификата аутентификации в формате Base64
//  ПараметрыСоединения - Структура - параметры соединения. Содержит следующие элементы:
//     * ИдентификаторОрганизации - Строка - идентификатор организации из настройки обмена;
//     * БИК - Строка - БИК банка;
//     * КлючУникальности - Строка - идентифицирующая пользователя строка, например “<инн>_<бик>”.
//                          Используется для сбора статистики подключений.
//
Процедура УстановитьСоединениеВК(Оповещение, ПодключаемыйМодуль, СертификатBase64, ПараметрыСоединения)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановкиСоединения", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("СертификатBase64", СертификатBase64);
	ДополнительныеПараметры.Вставить("ПараметрыСоединения", ПараметрыСоединения);

	ОповещениеПослеПолученияДанныхКлючаЧерезВК = Новый ОписаниеОповещения(
		"УстановитьПинПослеПолученияДанныхКлючаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьДанныеКлючаЧерезВК(ОповещениеПослеПолученияДанныхКлючаЧерезВК, ПодключаемыйМодуль, СертификатBase64);
	
КонецПроцедуры

Процедура УстановитьСоединениеПослеПодключенияКлючаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	//@skip-warning
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ НЕ Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Результат);
		Возврат;
	КонецЕсли;
	
	ПараметрыСтрокой = СериализованныеДанные(ДополнительныеПараметры.ПараметрыСоединения);
	
	НастройкиПроксиСервера = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента().НастройкиПроксиСервера;
	НастройкиПроксиСтрокой = СериализованныеДанные(НастройкиПроксиСервера);

	ОповещениеПослеУстановкиСоединения = Новый ОписаниеОповещения("ПослеУстановкиСоединенияВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуУстановкиСоединенияВК", ЭтотОбъект);
		
	ПараметрыРасширеннойАутентификации = Неопределено;
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовУстановитьСоединение(ОповещениеПослеУстановкиСоединения,
		ДополнительныеПараметры.СертификатBase64, НастройкиПроксиСтрокой, ПараметрыСтрокой,
		ПараметрыРасширеннойАутентификации);
	
КонецПроцедуры

Процедура ПослеПолученияПодключаемогоМодуляВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК, Результат);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = Результат;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ДоступныеСертификаты = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(
		ДополнительныеПараметры.НастройкаОбмена);
		
	ДополнительныеПараметры.Вставить("ДоступныеСертификаты", ДоступныеСертификаты);
	
	Для Каждого КлючЗначение Из ДоступныеСертификаты Цикл
		Пароль = Неопределено;
		Если ПарольКСертификатуПолучен(КлючЗначение.Ключ, Пароль) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Пароль = Неопределено Тогда
		
		Оповещение = Новый ОписаниеОповещения(
			"ПолучитьПарольАутентификацииПослеОпределенияСертификата", ЭтотОбъект, ДополнительныеПараметры);
			
		МассивСертификатов = Новый Массив;
		Для Каждого КлючЗначение Из ДоступныеСертификаты Цикл
			МассивСертификатов.Добавить(КлючЗначение.Значение.ДанныеСертификата);
		КонецЦикла;
			
		ОпределитьСертификатСУстановленнымПаролемЧерезВК(Оповещение, ПодключаемыйМодуль, МассивСертификатов);
		
	Иначе
		УстановитьСоединениеПоСертификатуЧерезВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК,
			ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена, КлючЗначение.Ключ, Пароль);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьПарольАутентификацииПослеОпределенияСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат.Успех Тогда
		ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(
			ДополнительныеПараметры.НастройкаОбмена);
		УстановитьСоединениеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК,
			ДополнительныеПараметры.ПодключаемыйМодуль, Результат.СертификатBase64, ПараметрыСоединения);
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"УстановитьПарольПослеВводаПользователемЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ВидОперации = НСтр("ru = 'Аутентификация на сервере банка'") + " "; // Для запроса пароля сертификата
		ПолучитьПарольКСертификату(Оповещение, ДополнительныеПараметры.ДоступныеСертификаты, ВидОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьСертификатСУстановленнымПаролемЧерезВК(Оповещение, ПодключаемыйМодуль, МассивСертификатов)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПоискаСертификатаБезПароля", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("МассивСертификатов", МассивСертификатов);
	НайтиСертификатСУстановленнымПаролемРекурсивноЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура НайтиСертификатСУстановленнымПаролемРекурсивноЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.МассивСертификатов.Количество() = 0 Тогда // нет сертификата с паролем
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПоискаСертификатаБезПароля, Результат);
		Возврат;
	КонецЕсли;
	
	ДанныеСертификата = ДополнительныеПараметры.МассивСертификатов.Получить(0);
	ДополнительныеПараметры.МассивСертификатов.Удалить(0);
	
	ДополнительныеПараметры.Вставить("СертификатBase64", ДанныеСертификата);
	
	Оповещение = Новый ОписаниеОповещения(
			"ПроверитьУстановкуПароляСледующегоСертификатаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ВызватьПроверкуНеобходимостиУстановкиПароляСертификатаЧерезВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.СертификатBase64)
	
КонецПроцедуры

Процедура ПроверитьУстановкуПароляСледующегоСертификатаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	// Ошибка не обрабатывается, т.к. идет поиск сертификата для устройства без пароля
	Если ТипЗнч(Результат) = Тип("Строка") Тогда 
		НайтиСертификатСУстановленнымПаролемРекурсивноЧерезВК(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат Тогда  // не требуется установка пароля
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Успех", Истина);
		СтруктураВозврата.Вставить("СертификатBase64", ДополнительныеПараметры.СертификатBase64);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПоискаСертификатаБезПароля, СтруктураВозврата);
	Иначе
		НайтиСертификатСУстановленнымПаролемРекурсивноЧерезВК(ДополнительныеПараметры)
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПИНПослеПодключенияКлючаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"УстановитьСоединениеПослеПодключенияКлючаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	УстановитьПинЕслиТребуетсяВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.ИдентификаторХранилища);
		
КонецПроцедуры

Процедура УстановитьПинПослеПолученияДанныхКлючаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
		
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", Результат.ИдентификаторХранилища);
	
	Оповещение = Новый ОписаниеОповещения(
		"УстановитьПИНПослеПодключенияКлючаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбеспечитьПодключениеЭлектронногоКлючаЧерезВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, Результат.ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ВызватьПроверкуНеобходимостиУстановкиПароляСертификатаЧерезВК(Оповещение, ПодключаемыйМодуль, СертификатBase64)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиНеобходимостиУстановкиПароля", Оповещение);
	ОповещениеПослеВызоваПроверки = Новый ОписаниеОповещения("ПослеПроверкиНеобходимостиУстановкиПароляВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПроверкиНеобходимостиУстановкиПароляВК", ЭтотОбъект);
	ПодключаемыйМодуль.НачатьВызовТребуетсяПароль(ОповещениеПослеВызоваПроверки, СертификатBase64);
	
КонецПроцедуры

Процедура ПослеПроверкиНеобходимостиУстановкиПароляВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиНеобходимостиУстановкиПароля, РезультатВызова);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПроверкиНеобходимостиУстановкиПароляВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При аутентификации произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Проверка необходимости установки пароля.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПроверкиНеобходимостиУстановкиПароля,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура УстановитьПарольПослеВводаПользователемЧерезВК(ДанныеАутентификации, ДополнительныеПараметры) Экспорт
	
	Если ДанныеАутентификации = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК, Неопределено);
		Возврат;
	КонецЕсли;
	
	УстановитьСоединениеПоСертификатуЧерезВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК,
		ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена,
		ДанныеАутентификации.ВыбранныйСертификат, ДанныеАутентификации.ПарольСертификата);
	
КонецПроцедуры

Процедура УстановитьСоединениеПоСертификатуЧерезВК(Оповещение, ПодключаемыйМодуль, НастройкаОбмена, СертификатСсылка, Пароль)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановкиСоединения", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	РеквизитыСертификата = Новый Структура("ДанныеСертификата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(СертификатСсылка, РеквизитыСертификата);
	ДополнительныеПараметры.Вставить("СертификатBase64", РеквизитыСертификата.ДанныеСертификата);
	
	ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
		"УстановитьСоединениеПослеАутентификацииЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	АутентифицироватьсяНаЭлектронномКлючеЧерезВК(
		ОповещениеПослеАутентификации, ПодключаемыйМодуль, СертификатСсылка, РеквизитыСертификата.ДанныеСертификата, Пароль);
	
КонецПроцедуры

Процедура УстановитьСоединениеПослеАутентификацииЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	//@skip-warning
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Не Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Результат);
		Возврат;
	КонецЕсли;

	ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(
		ДополнительныеПараметры.НастройкаОбмена);
	
	УстановитьСоединениеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения,
		ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.СертификатBase64, ПараметрыСоединения);
	
КонецПроцедуры

Процедура ПослеПолученияХранилищКлючейЭПВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Хранилища = ДеСериализованныеДанные(РезультатВызова);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияИдентификаторовКлючей, Хранилища);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияХранилищКлючейЭП(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Получение информации об аппаратных устройствах.'");
	ТекстСообщения = НСтр("ru = 'При поиске аппаратных устройств, подключенных к компьютеру, произошла ошибка.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияИдентификаторовКлючей,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ДанныеОтправки.Количество() = 0 Тогда // все настройки обмена обработаны
		Результат = Новый Структура("КоличествоОтправленных, КоличествоПодготовленных", 0, 0);
		ЗаполнитьЗначенияСвойств(Результат, ДополнительныеПараметры);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиСообщенийОбменаПоНастройкам, Результат);
		Возврат;
	КонецЕсли;
	
	МассивСообщенийОбмена = Новый Массив;
	
	Для Каждого КлючЗначение Из ДополнительныеПараметры.ДанныеОтправки Цикл
		НастройкаОбмена = КлючЗначение.Ключ;
		МассивСообщенийОбмена = КлючЗначение.Значение.МассивСообщенийОбмена;
		МассивСообщенийТребующихПодтверждение = КлючЗначение.Значение.МассивСообщенийТребующихПодтверждение;
		Прервать;
	КонецЦикла;
	
	ДополнительныеПараметры.ДанныеОтправки.Удалить(НастройкаОбмена);
	
	РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
	ДополнительныеПараметры.Вставить("МассивСообщенийТребующихПодтверждение", МассивСообщенийТребующихПодтверждение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПродолжитьОтправкуПакетовПоНастройкамОбменаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ОтправитьПакетыЧерезВК(Оповещение, НастройкаОбмена, МассивСообщенийОбмена);
	
КонецПроцедуры

Процедура ПолучитьСвойстваСертификатовСообщенийОбменаЧерезВК(Оповещение, ПодключаемыйМодуль, МассивСообщенийОбмена, НастройкаОбмена)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияДанныхСертификатов", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	СообщенияОбменаИСертификаты = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатовСообщенийОбмена(
		МассивСообщенийОбмена, НастройкаОбмена);
		
	ДополнительныеПараметры.Вставить("СообщенияОбменаИСертификаты", СообщенияОбменаИСертификаты);
	ДополнительныеПараметры.Вставить("СообщенияОбменаИСвойстваСертификатов", Новый Соответствие);
		
	ПолучитьДанныеСертификатовРекурсивноЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПолучитьДанныеСертификатовРекурсивноЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.СообщенияОбменаИСертификаты.Количество() = 0 Тогда // обработаны все сообщения обмена
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификатов,
			ДополнительныеПараметры.СообщенияОбменаИСвойстваСертификатов);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ДополнительныеПараметры.СообщенияОбменаИСертификаты Цикл
		МассивСертификатовBase64 = КлючЗначение.Значение;
		ДополнительныеПараметры.Вставить("СообщениеОбмена", КлючЗначение.Ключ);
		ДополнительныеПараметры.СообщенияОбменаИСертификаты.Удалить(КлючЗначение.Ключ);
		Прервать;
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьСвойстваСертификатовСледующегоСообщенияОбмена", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьСвойстваСертификатовЧерезВК(Оповещение, МассивСертификатовBase64, ДополнительныеПараметры.ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ПродолжитьОтправкуПакетовПоНастройкамОбменаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.КоличествоОтправленных = ДополнительныеПараметры.КоличествоОтправленных + Результат.КоличествоОтправленных;
	Если ТипЗнч(Результат.Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.Результат);
	КонецЕсли;
	
	Если Результат.Результат <> Неопределено И ДополнительныеПараметры.ПолучитьНовыеДокументы Тогда
		ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
		Оповещение = Новый ОписаниеОповещения(
			"НачатьПодтверждениеПлатежныхДокументовПоSMSПослеПолученияНовыхДокументовЧерезВК", ЭтотОбъект,
			ДополнительныеПараметры);
		ПолучитьНовыеДокументыВК(Оповещение, Результат.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена);
	Иначе
		ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьСвойстваСертификатовСледующегоСообщенияОбмена(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификатов, Результат);
		Возврат;
	КонецЕсли;
		
	ДополнительныеПараметры.СообщенияОбменаИСвойстваСертификатов.Вставить(ДополнительныеПараметры.СообщениеОбмена, Результат);
	
	ПолучитьДанныеСертификатовРекурсивноЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ВызватьАутентификациюНаЭлектронномКлючеВК(Оповещение, ПодключаемыйМодуль, СертификатBase64, Пароль, СертификатСсылка = Неопределено)
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификацииВК", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("СертификатСсылка", СертификатСсылка);
	
	ОповещениеПослеУстановкиПароляСертификата = Новый ОписаниеОповещения(
		"ПослеУстановкиПароляВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуУстановкиПароляВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовУстановитьПароль(ОповещениеПослеУстановкиПароляСертификата, СертификатBase64, Пароль);
	
КонецПроцедуры

Процедура АутентифицироватьсяНаЭлектронномКлючеЧерезВК(Оповещение, ПодключаемыйМодуль, СертификатСсылка, СертификатBase64, Пароль, ЭтоТест = Ложь)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СертификатBase64", СертификатBase64);
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификации", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("СертификатСсылка", СертификатСсылка);
	ДополнительныеПараметры.Вставить("Пароль", Пароль);
	ДополнительныеПараметры.Вставить("ЭтоТест", ЭтоТест);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьНеобходимостьУстановкиПинПослеПолученияДанныхСертификата", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьДанныеКлючаЧерезВК(Оповещение, ПодключаемыйМодуль, СертификатBase64);
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьУстановкиПинПослеПолученияДанныхСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", Результат.ИдентификаторХранилища);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьНаличиеПинТокенаПослеПроверкиПодключенияКлюча", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбеспечитьПодключениеЭлектронногоКлючаЧерезВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, Результат.ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПроверитьНаличиеПинТокенаПослеПроверкиПодключенияКлюча(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Результат) = Тип("Массив") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьНеобходимостьУстановкиПароляСертификатаПослеУстановкиПин", ЭтотОбъект, ДополнительныеПараметры);
	
	УстановитьПинЕслиТребуетсяВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ОбеспечитьПодключениеЭлектронногоКлючаЧерезВК(Оповещение, ПодключаемыйМодуль, ИдентификаторХранилища = Неопределено)

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодключенияЭлектронногоКлюча", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	
	ОповещениеПослеПолученияИдентификаторов = Новый ОписаниеОповещения(
		"ПредложитьПодключитьКлючПослеПолученияИдентификаторовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьИдентификаторыПодключенныхКлючейЧерезВК(ОповещениеПослеПолученияИдентификаторов, ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ПредложитьПодключитьКлючПослеПолученияИдентификаторовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияЭлектронногоКлюча, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат.Количество() = 0
		ИЛИ (ЗначениеЗаполнено(ДополнительныеПараметры.ИдентификаторХранилища)
				И Результат.Найти(ДополнительныеПараметры.ИдентификаторХранилища) = Неопределено) Тогда
		
		// кешируем компоненту т.к. она будет нужна на открытой форме
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
		Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) <> Тип("Соответствие") Тогда
			ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками", Новый Соответствие);
			ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
		КонецЕсли;

		ПараметрыПодсистемыОбменСБанками.Вставить("ПодключаемыйМодуль", ДополнительныеПараметры.ПодключаемыйМодуль);
		
		Параметры = Новый Структура("ИдентификаторКлюча", ДополнительныеПараметры.ИдентификаторХранилища);
		
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
			"ПослеПодключенияЭлектронногоКлючаВК", ЭтотОбъект, ДополнительныеПараметры);
		
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ОжиданиеПодключенияЭлектронногоКлюча", Параметры, , , , ,
			ОписаниеОповещенияОЗакрытии);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияЭлектронногоКлюча, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПодключенияЭлектронногоКлючаВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияЭлектронногоКлюча, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияЭлектронногоКлюча, Неопределено);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияЭлектронногоКлюча, Результат);
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьУстановкиПароляСертификатаПослеУстановкиПин(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Результат);
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Неопределено);
		Возврат;
	КонецЕсли;
	
	ВызватьАутентификациюНаЭлектронномКлючеВК(ДополнительныеПараметры.ОповещениеПослеАутентификации,
			ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.СертификатBase64,
			ДополнительныеПараметры.Пароль, ДополнительныеПараметры.СертификатСсылка);
	
КонецПроцедуры

Процедура ИнициализироватьПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Подключено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат.ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатИнициализацииВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ИнициализироватьВК(Оповещение, Результат.ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ОбработатьРезультатИнициализацииВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, Результат);
	Иначе
		Если ДополнительныеПараметры.ИспользоватьЖурналирование Тогда
			Оповещение = Новый ОписаниеОповещения("ПослеВключенияЖурналированияВК", ЭтотОбъект, ДополнительныеПараметры);
			ВключитьЖурналированиеВК(Оповещение, ДополнительныеПараметры.НастройкаОбмена,
				ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.КаталогДляЖурналирования,
				ДополнительныеПараметры.ИдентификаторКлиента);
		Иначе
			ВыполнитьОбработкуОповещения(
				ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, ДополнительныеПараметры.ПодключаемыйМодуль);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура АутентифицироватьсяНаТокенеПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьНеобходимостьДополненияЭДПослеАутентификацииЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	АутентифицироватьсяНаЭлектронномКлючеЧерезВК(Оповещение, Результат, ДополнительныеПараметры.СертификатСсылка,
		ДополнительныеПараметры.СертификатBase64, ДополнительныеПараметры.Пароль, ДополнительныеПараметры.ЭтоТест);
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьДополненияЭДПослеАутентификацииЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Результат);
		Возврат;
	КонецЕсли;
		
	Оповещение = Новый ОписаниеОповещения("ПослеПроверкиНеобходимостиДополненияЭДЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПроверкиДополненияЧерезВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовТребуетсяДополнитьЭД(
		Оповещение, ДополнительныеПараметры.ДанныеЭДBase64);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПроверкиДополненияЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При подписании документа произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Проверка дополнения электронного документа.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПослеПроверкиНеобходимостиДополненияЭДЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если РезультатВызова Тогда // требуется дополнить ЭД
		Оповещение = Новый ОписаниеОповещения("ДополнитьЭДПослеУстановкиСоединенияВК", ЭтотОбъект, ДополнительныеПараметры);
		ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(ДополнительныеПараметры.НастройкаОбмена);
		УстановитьСоединениеВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль,
			ДополнительныеПараметры.СертификатBase64, ПараметрыСоединения);
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"СохранитьДанныеПодписиПослеПодписанияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ДвоичныеДанныеЭД = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(
			ДополнительныеПараметры.СообщениеОбмена);

		Если ДвоичныеДанныеЭД = Неопределено Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Неопределено);
			Возврат;
		КонецЕсли;
		
		ДанныеЭДBase64 = Base64Строка(ДвоичныеДанныеЭД);
		
		ПодписатьДанныеЧерезВК(
			Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДанныеЭДBase64, ДополнительныеПараметры.СертификатBase64);
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьДанныеПодписиПослеПодписанияЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодписьBase64", Результат.ДанныеПодписи);
	
	Оповещение = Новый ОписаниеОповещения(
		"СохранитьПодписьПослеПолученияСвойствСертификата", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьДанныеКлючаЧерезВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.СертификатBase64);
	
КонецПроцедуры

Процедура ДополнитьЭДПослеУстановкиСоединенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеДополненияЭДЧерезВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуДополненияЭДЧерезВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовДополнитьЭД(Оповещение, ДополнительныеПараметры.ДанныеЭДBase64);
	
КонецПроцедуры

Процедура ПослеДополненияЭДЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДвоичныеДанныеЭД = Base64Значение(РезультатВызова);
	
	ОбменСБанкамиСлужебныйВызовСервера.ОбновитьДанныеЭД(ДополнительныеПараметры.СообщениеОбмена, ДвоичныеДанныеЭД);
	
	Оповещение = Новый ОписаниеОповещения(
			"СохранитьДанныеПодписиПослеПодписанияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ДвоичныеДанныеЭД = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(
		ДополнительныеПараметры.СообщениеОбмена);

	Если ДвоичныеДанныеЭД = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Неопределено);
		Возврат;
	КонецЕсли;
		
	ДанныеЭДBase64 = Base64Строка(ДвоичныеДанныеЭД);
		
	ПодписатьДанныеЧерезВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДанныеЭДBase64, ДополнительныеПараметры.СертификатBase64);
	
КонецПроцедуры

Процедура ОбработатьОшибкуДополненияЭДЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При дополнении документа произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Дополнение электронного документа.'");
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияПричиныОтклоненияЭДЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьИнформациюОбОшибкеВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПослеПолученияПричиныОтклоненияЭДЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	СтатусОтклоненБанком = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.ОтклоненБанком");
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Статус", СтатусОтклоненБанком);
	СтруктураПараметров.Вставить("ПричинаОтклонения", Результат);
	
	ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(ДополнительныеПараметры.СообщениеОбмена, СтруктураПараметров);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Результат);
	
КонецПроцедуры

Процедура ПолучитьСертификатыСКлючаПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // ошибка подключения ВК с выводом сообщения
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда // ошибка подключения ВК
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат);
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияДанныхСертификатовСКлючаВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ИдентификаторХранилища = Неопределено;
	Если ДополнительныеПараметры.ПараметрыУстановленногоСоединения <> Неопределено Тогда
		ИдентификаторХранилища = ДополнительныеПараметры.ПараметрыУстановленногоСоединения.ИдентификаторХранилища;
	КонецЕсли;
	
	ПолучитьСертификатыИзХранилищаВК(Оповещение, Результат, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхСертификатовСКлючаВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Массив") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	ДоступныеСертификаты = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(
		ДополнительныеПараметры.НастройкаОбмена);
		
	Если ДоступныеСертификаты.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'В настройке обмена с сервисом 1С:ДиректБанк нет ни одного доступного сертификата.'");
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, ТекстСообщения);
		Возврат;
	КонецЕсли;
		
	ПараметрыСертификатовНастройкиОбмена = Новый Соответствие;
	Для Каждого КлючЗначение Из ДоступныеСертификаты Цикл
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("СсылкаНаСертификат", КлючЗначение.Ключ);
		СтруктураДанных.Вставить("СертификатBase64", КлючЗначение.Значение.ДанныеСертификата);
		СтруктураДанных.Вставить("КомуВыдан", КлючЗначение.Значение.КомуВыдан);
		ПараметрыСертификатовНастройкиОбмена.Вставить(КлючЗначение.Значение.Отпечаток, СтруктураДанных);
	КонецЦикла;
		
	ДанныеВыбора = Новый Соответствие;
	МассивОтпечатков = Новый Массив;
		
	Для Каждого Элемент Из Результат Цикл
		СвойстваСертификата = ПараметрыСертификатовНастройкиОбмена.Получить(Элемент.ДанныеСертификата.Отпечаток);
		Если СвойстваСертификата <> Неопределено Тогда
			СтруктураСертификата = Новый Структура;
			СтруктураСертификата.Вставить("СертификатBase64", СвойстваСертификата.СертификатBase64);
			СтруктураСертификата.Вставить("Псевдоним", Элемент.ДанныеСертификата.Псевдоним);
			СтруктураСертификата.Вставить("КомуВыдан", СвойстваСертификата.КомуВыдан);
			СтруктураСертификата.Вставить("Отпечаток", Элемент.ДанныеСертификата.Отпечаток);
			МассивОтпечатков.Добавить(Элемент.ДанныеСертификата.Отпечаток);
			СтруктураСертификата.Вставить("ПарольПолучен", Ложь);
			ДанныеВыбора.Вставить(СвойстваСертификата.СсылкаНаСертификат, СтруктураСертификата);
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеВыбора.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'На банковском ключе не найдены сертификаты, указанные в настройке обмена с банком.'");
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ДанныеВыбораСертификатов", ДанныеВыбора);
		
	НастройкиОбмена = Неопределено;
	СообщениеЗапросЗонд = Неопределено;
	
	ОбменСБанкамиСлужебныйВызовСервера.СформироватьЗапросЗонд(
		ДополнительныеПараметры.НастройкаОбмена, МассивОтпечатков, СообщениеЗапросЗонд, НастройкиОбмена);
		
	Если НЕ ЗначениеЗаполнено(СообщениеЗапросЗонд) ИЛИ НастройкиОбмена = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеЗапросЗонд);
	
	Если НастройкиОбмена.Подписывать Тогда
		ВидОперации = НСтр("ru = 'Подписание электронного документа'");
		Оповещение = Новый ОписаниеОповещения(
			"ПодписатьЗапросЗондПослеВыбораСертификатаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ПолучитьПарольКСертификату(Оповещение, ДанныеВыбора, ВидОперации, СообщениеЗапросЗонд);
	Иначе
		МассивСертификатов = Новый Массив;
		Для Каждого КлючЗначение Из ДанныеВыбора Цикл
			МассивСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОперации", НСтр("ru = 'Аутентификация на сервере банка'"));
		ПараметрыФормы.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
		ПараметрыФормы.Вставить("МассивСертификатов", МассивСертификатов);
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("АутентификацияПослеПолученияПароляЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросПароляКСертификату",
			ПараметрыФормы, , , , , ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура АутентификацияПослеПолученияПароляЧерезВК(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Неопределено);
		Возврат;
	КонецЕсли;
	
	СвойстваСертификата = ДополнительныеПараметры.ДанныеВыбораСертификатов.Получить(ДанныеВыбора.ВыбранныйСертификат);
	
	ПараметрыУстановленногоСоединения = Новый Структура;
	ПараметрыУстановленногоСоединения.Вставить("Отпечаток", СвойстваСертификата.Отпечаток);
	ПараметрыУстановленногоСоединения.Вставить("КомуВыданСертификат", СвойстваСертификата.КомуВыдан);
	ПараметрыУстановленногоСоединения.Вставить("СертификатBase64", СвойстваСертификата.СертификатBase64);
	ДополнительныеПараметры.Вставить("ПараметрыУстановленногоСоединения", ПараметрыУстановленногоСоединения);
	
	Оповещение = Новый ОписаниеОповещения(
		"УстановитьТестовоеСоединениеПослеАутентификацииНаКлючеВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ВызватьАутентификациюНаЭлектронномКлючеВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль,
		СвойстваСертификата.СертификатBase64, ДанныеВыбора.ПарольСертификата, ДанныеВыбора.ВыбранныйСертификат);
	
КонецПроцедуры

Процедура ПодписатьЗапросЗондПослеВыбораСертификатаЧерезВК(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Неопределено);
		Возврат;
	КонецЕсли;
	
	СвойстваСертификата = ДополнительныеПараметры.ДанныеВыбораСертификатов.Получить(ДанныеВыбора.ВыбранныйСертификат);
	
	ПараметрыУстановленногоСоединения = Новый Структура;
	ПараметрыУстановленногоСоединения.Вставить("Отпечаток", СвойстваСертификата.Отпечаток);
	ПараметрыУстановленногоСоединения.Вставить("КомуВыданСертификат", СвойстваСертификата.КомуВыдан);
	ПараметрыУстановленногоСоединения.Вставить("СертификатBase64", СвойстваСертификата.СертификатBase64);
	ДополнительныеПараметры.Вставить("ПараметрыУстановленногоСоединения", ПараметрыУстановленногоСоединения);
	
	Оповещение = Новый ОписаниеОповещения(
		"УстановитьТестовоеСоединениеПослеАутентификацииНаКлючеВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодписатьЭДПоСертификатуЧерезВК(Оповещение, ДополнительныеПараметры.СообщениеОбмена,
		ДополнительныеПараметры.НастройкаОбмена, ДанныеВыбора.ВыбранныйСертификат, ДанныеВыбора.ПарольСертификата, ,
		Истина);
	
КонецПроцедуры

Процедура УстановитьТестовоеСоединениеПослеАутентификацииНаКлючеВК(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения(
		"ОтправитьТестовыйЗапросПослеУстановкиСоединенияВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(
		ДополнительныеПараметры.НастройкаОбмена);
		
	УстановитьСоединениеВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль,
		ДополнительныеПараметры.ПараметрыУстановленногоСоединения.СертификатBase64, ПараметрыСоединения);
	
КонецПроцедуры

Процедура ОтправитьТестовыйЗапросПослеУстановкиСоединенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ОтправитьТестовыйПакетПослеПолученияСвойствСертификатовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);

	МассивСообщенийОбмена = Новый Массив;
	МассивСообщенийОбмена.Добавить(ДополнительныеПараметры.СообщениеОбмена);
		
	ПолучитьСвойстваСертификатовСообщенийОбменаЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль,
		МассивСообщенийОбмена, ДополнительныеПараметры.НастройкаОбмена);

КонецПроцедуры

Процедура ОтправитьТестовыйПакетПослеПолученияСвойствСертификатовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;

	Для Каждого СообщениеОбменаИСертификаты Из Результат Цикл
		СвойстваСертификатов = Новый Соответствие;
		Для Каждого Элемент Из СообщениеОбменаИСертификаты.Значение Цикл
			СвойстваСертификата = Новый Структура("СерийныйНомер, ИмяИздателя");
			ЗаполнитьЗначенияСвойств(СвойстваСертификата, Элемент.ДанныеСертификата);
			СвойстваСертификатов.Вставить(Элемент.ДанныеСертификата.Отпечаток, СвойстваСертификата)
		КонецЦикла;
		Прервать;
	КонецЦикла;
	
	СоответствиеДанных = Новый Соответствие;
	СоответствиеДанных.Вставить(ДополнительныеПараметры.СообщениеОбмена, СвойстваСертификатов);
	
	МассивПакетов = ОбменСБанкамиСлужебныйВызовСервера.СоздатьПакетыВК(
		ДополнительныеПараметры.НастройкаОбмена, СоответствиеДанных);
		
	ПакетОбменСБанками = МассивПакетов.Получить(0);
		
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьИзвещениеПослеОтправкиЗапросаЗондаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ОтправитьПакетЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена,
		ПакетОбменСБанками, ДополнительныеПараметры.ПараметрыУстановленногоСоединения.СертификатBase64);
	
КонецПроцедуры

Процедура ПолучитьИзвещениеПослеОтправкиЗапросаЗондаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
		
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияНовыхДокументовВК", ЭтотОбъект, ДополнительныеПараметры);
	ПолучитьНовыеДокументыВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ОтправитьПакетЧерезВК(Оповещение, ПодключаемыйМодуль, НастройкаОбмена, ПакетЭД, СертификатBase64)
	
	// Аутентификация на токене уже выполнена.
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиПакетаЭД", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ПакетЭД", ПакетЭД);
	ОповещениеПослеУстановкиСоединения = Новый ОписаниеОповещения(
		"ОтправитьПакетЭДПослеУстановкиСоединенияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(НастройкаОбмена);
	
	УстановитьСоединениеВК(ОповещениеПослеУстановкиСоединения, ПодключаемыйМодуль, СертификатBase64, ПараметрыСоединения);
	
КонецПроцедуры

Процедура ОтправитьПакетЭДПослеУстановкиСоединенияЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетаЭД, Результат);
		Возврат;
	КонецЕсли;
	
	ВызватьОтправкуПакетаЧерезВК(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетаЭД,
		ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.ПакетЭД);
	
КонецПроцедуры

Процедура ОтправитьПакетыЧерезВК(Оповещение, НастройкаОбмена, СообщенияОбмена, ПакетыЭД = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ПакетыЭД = ?(ПакетыЭД = Неопределено, Новый Массив, ПакетыЭД);
	ДополнительныеПараметры.Вставить("ПакетыЭД", ПакетыЭД);
	ДополнительныеПараметры.Вставить("СообщенияОбмена", СообщенияОбмена);
	ДополнительныеПараметры.Вставить("КоличествоОтправленных", 0);
	ДополнительныеПараметры.Вставить("КоличествоПодготовленных", 0);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиПакетов", Оповещение);
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ПолучитьПараметрыСертификатовПослеИнициализацииВК", ЭтотОбъект, ДополнительныеПараметры);
	ПодключитьИИнициализироватьВК(ОповещениеПослеПодключенияВК, НастройкаОбмена);
	
КонецПроцедуры

Процедура ОтправитьПакетыПослеУстановкиСоединенияЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("Результат", Результат);
		ДанныеВозврата.Вставить("КоличествоОтправленных", ДополнительныеПараметры.КоличествоОтправленных);
		ДанныеВозврата.Вставить("КоличествоПолученных", 0);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетов, ДанныеВозврата);
		Возврат;
	КонецЕсли;
	
	ВызватьОтправкуПакетовРекурсивноЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ВызватьОтправкуПакетовРекурсивноЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ПакетыЭД.Количество() = 0 Тогда
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("Результат", Истина);
		ДанныеВозврата.Вставить("КоличествоОтправленных", ДополнительныеПараметры.КоличествоОтправленных);
		ДанныеВозврата.Вставить("КоличествоПодготовленных", ДополнительныеПараметры.КоличествоОтправленных);
		ДанныеВозврата.Вставить("ПодключаемыйМодуль", ДополнительныеПараметры.ПодключаемыйМодуль); // используется для дальнейшего получения новых документов
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетов, ДанныеВозврата);
		Возврат;
	КонецЕсли;
	
	ПакетЭД = ДополнительныеПараметры.ПакетыЭД.Получить(0);
	ДополнительныеПараметры.Вставить("ПакетЭД", ПакетЭД);
	ДополнительныеПараметры.ПакетыЭД.Удалить(0);
	
	ОповещениеПослеОтправкиПакета = Новый ОписаниеОповещения(
		"ОтправитьСледующийПакетЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ВызватьОтправкуПакетаЧерезВК(ОповещениеПослеОтправкиПакета, ДополнительныеПараметры.ПодключаемыйМодуль, ПакетЭД)
	
КонецПроцедуры

Процедура ОтправитьСледующийПакетЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("Результат", Результат);
		ДанныеВозврата.Вставить("КоличествоОтправленных", ДополнительныеПараметры.КоличествоОтправленных);
		ДанныеВозврата.Вставить("КоличествоПодготовленных", ДополнительныеПараметры.КоличествоОтправленных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетов, ДанныеВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.КоличествоОтправленных = ДополнительныеПараметры.КоличествоОтправленных + 1;
	
	ВызватьОтправкуПакетовРекурсивноЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ВызватьОтправкуПакетаЧерезВК(Оповещение, ПодключаемыйМодуль, ПакетЭД)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиПакета", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ПакетЭД", ПакетЭД);
	ДвоичныеДанныеПакета = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(ПакетЭД);
	Если ДвоичныеДанныеПакета = Неопределено Тогда
		СтатусОтменен = ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Отменен");
		ОбменСБанкамиСлужебныйВызовСервера.УстановитьСтатусПакета(ПакетЭД, СтатусОтменен);
		ВыполнитьОбработкуОповещения(Оповещение);
		Возврат;
	КонецЕсли;
	ДанныеПакетаBase64 = Base64Строка(ДвоичныеДанныеПакета);
	
	ОповещениеПослеОтправкиПакета = Новый ОписаниеОповещения("ПослеОтправкиПакетаЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуОтправкиПакетаЧерезВК", ЭтотОбъект);
				
	ПодключаемыйМодуль.НачатьВызовОтправитьПакет(ОповещениеПослеОтправкиПакета, ДанныеПакетаBase64);
	
КонецПроцедуры

Процедура ОбработатьОшибкуОтправкиПакетаЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При отправке данных в банк произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Отправка пакетов электронных документов.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеОтправкиПакета,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПослеОтправкиПакетаЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	СтруктураЭД = Новый Структура("Статус", ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Отправлен"));
	СтатусПакетаОтправлен = ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Отправлен");
	ОбменСБанкамиСлужебныйВызовСервера.ОбновитьСтатусыДокументовПакетаЭДО(
		ДополнительныеПараметры.ПакетЭД, СтатусПакетаОтправлен, СтруктураЭД);
		
	Оповестить("ОбновитьСостояниеОбменСБанками");
		
	Попытка
		ОтветБанка = Base64Значение(РезультатВызова);
		ВремФайл = Неопределено;
		ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветБанкаНаОтправкуДокументаAsync(
			ОтветБанка, ДополнительныеПараметры.ПакетЭД, ВремФайл);
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстИсключения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибкиВЖурнале = НСтр("ru = 'При чтении ответа банка возникла ошибка:
										|%1
										|Файл ответа: %2'");
		ТекстОшибкиВЖурнале = СтрШаблон(ТекстОшибкиВЖурнале, ТекстИсключения, ВремФайл);
		ВидОперации = НСтр("ru = 'Чтение ответа, полученного из банка'");
		ОбработатьОшибку(ВидОперации, ТекстОшибкиВЖурнале, , ДополнительныеПараметры.ПакетЭД);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакета, ТекстСообщения);
		Возврат;
	КонецПопытки;
		
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакета, Истина);
	
КонецПроцедуры

Процедура ПослеПолученияНовыхДокументовВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат.Результат) = Тип("Строка") ИЛИ Результат.Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат.Результат);
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат.Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	ЕстьОшибка = Ложь;
	ПолученоИзвещение = ОбменСБанкамиСлужебныйВызовСервера.ПолученоИзвещениеПоЗапросу(
		ДополнительныеПараметры.СообщениеОбмена, ЕстьОшибка);
		
	Если ЕстьОшибка Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	Если ПолученоИзвещение Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Истина);
	Иначе
		Если ПроцессПрерван(ДополнительныеПараметры.НастройкаОбмена) Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Ложь);
			Возврат;
		КонецЕсли;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияНовыхДокументовВК", ЭтотОбъект, ДополнительныеПараметры);
		ПолучитьНовыеДокументыВК(
			Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВызватьПолучениеИдентификаторовНовыхПакетовЧерезВК(Оповещение, НастройкаОбмена, ПодключаемыйМодуль)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияИдентификаторовПакетов", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеЗапросаСпискаПакетов = Новый ОписаниеОповещения("ПослеПолученияСпискаПакетовВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПолученияСпискаПакетов", ЭтотОбъект);
		
	ПараметрыОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаСБанком(НастройкаОбмена);

	ДатаСтрокой = "";
	
	Если ЗначениеЗаполнено(ПараметрыОбмена.ПоследняяДатаПолученияЭД) Тогда
		ДатаСтрокой = Формат(ПараметрыОбмена.ПоследняяДатаПолученияЭД, ФорматДатыИВремениДляОбменаЧерезВКРоссия());
	КонецЕсли;
		
	ПодключаемыйМодуль.НачатьВызовПолучитьСписокПакетов(ОповещениеПослеЗапросаСпискаПакетов, ДатаСтрокой);
		
КонецПроцедуры

// Не исправлять, т.к. это не ошибка.
Функция ФорматДатыИВремениДляОбменаЧерезВКРоссия()
	
	Возврат "ДФ=yyyy-MM-ddTHH:mm:ss";
	
КонецФункции

Процедура ПослеПолученияСпискаПакетовВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ЕстьОшибка = Ложь;
	МассивИдентификаторов = Новый Массив;
	ОбменСБанкамиСлужебныйВызовСервера.ПрочитатьИдентификаторыПакетов(
		ДополнительныеПараметры.НастройкаОбмена, РезультатВызова, МассивИдентификаторов, ЕстьОшибка);
	
	Если ЕстьОшибка Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияИдентификаторовПакетов, Неопределено);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеПолученияИдентификаторовПакетов, МассивИдентификаторов);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСпискаПакетов(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'Ошибка получения списка пакетов документов из банка.'");
	ВидОперации = НСтр("ru = 'Получение списка пакетов.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияИдентификаторовПакетов,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПолучитьПакетыПослеПолученияИдентификаторовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Или Результат = Неопределено Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Результат);
		СтруктураВозврата.Вставить("КоличествоПолученных", 0);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Если Результат.Количество() Тогда
		ПолучитьОчереднойПакетВК(Результат, ДополнительныеПараметры);
	Иначе
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Истина);
		СтруктураВозврата.Вставить("КоличествоПолученных", 0);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьОчереднойПакетВК(МассивИдентификаторов, ДополнительныеПараметры)
	
	Если МассивИдентификаторов.Количество() = 0 Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Истина);
		Если ДополнительныеПараметры.Свойство("ТекстОшибки") И ЗначениеЗаполнено(ДополнительныеПараметры.ТекстОшибки) Тогда
			СтруктураВозврата.Вставить("Результат", ДополнительныеПараметры.ТекстОшибки);
		КонецЕсли;
		СтруктураВозврата.Вставить("КоличествоПолученных", ДополнительныеПараметры.КоличествоПолученных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ИдентификаторПакета = МассивИдентификаторов.Получить(0);
	МассивИдентификаторов.Удалить(0);
	ДополнительныеПараметры.Вставить("МассивИдентификаторов", МассивИдентификаторов);
	
	Оповещение = Новый ОписаниеОповещения("СохранитьПакетПослеПолученияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ВызватьПолучениеПакетаЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ИдентификаторПакета);
	
КонецПроцедуры

Процедура СохранитьПакетПослеПолученияЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Результат);
		СтруктураВозврата.Вставить("КоличествоПолученных", ДополнительныеПараметры.КоличествоПолученных);
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДанныеВозврата = ОбменСБанкамиКлиентСервер.ПараметрыПолученияНовыхДокументовАсинхронныйОбмен();
	
	ОбменСБанкамиСлужебныйВызовСервера.СохранитьПолученныйПакетЧерезВК(ДополнительныеПараметры.НастройкаОбмена,
		Результат.ПакетBase64, ДополнительныеПараметры.СоздаватьОперацииВыписки, ДанныеВозврата);
		
	Оповестить("ОбновитьСостояниеОбменСБанками", ДанныеВозврата.ПараметрОповещения);

	Если ДанныеВозврата.ЕстьОшибка Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Неопределено);
		СтруктураВозврата.Вставить("КоличествоПолученных", ДополнительныеПараметры.КоличествоПолученных);
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.КоличествоПолученных = ДополнительныеПараметры.КоличествоПолученных + 1;
	
	Если ДанныеВозврата.ДанныеЭП.Количество() Тогда
		
		ДополнительныеПараметры.Вставить("ДанныеЭД", ДанныеВозврата.ДанныеЭП);
		СертификатБанкаBase64 = ОбменСБанкамиСлужебныйВызовСервера.ПервыйСертификатБанкаВBase64(
			ДополнительныеПараметры.НастройкаОбмена);
		ДополнительныеПараметры.Вставить("СертификатБанкаBase64", СертификатБанкаBase64);
		
		Оповещение = Новый ОписаниеОповещения(
			"СохранитьДанныеПодписейПослеПолученияСвойствСертификатаБанка", ЭтотОбъект, ДополнительныеПараметры);
		
		ПолучитьДанныеКлючаЧерезВК(	Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, СертификатБанкаBase64);
		
	Иначе
		ПолучитьОчереднойПакетВК(ДополнительныеПараметры.МассивИдентификаторов, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВызватьПолучениеПакетаЧерезВК(Оповещение, ПодключаемыйМодуль, ИдентификаторПакета)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияПакета", Оповещение);
	
	ОповещениеВызоваПолученияПакета = Новый ОписаниеОповещения(
		"ПослеПолученияПакетаВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПолученияПакетаВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовПолучитьПакет(ОповещениеВызоваПолученияПакета, ИдентификаторПакета);
	
КонецПроцедуры

Процедура ПослеПолученияПакетаВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Результат = Новый Структура("ПакетBase64", РезультатВызова);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПакета, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияПакетаВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'Ошибка получения пакета документов из банка.'");
	ВидОперации = НСтр("ru = 'Получение пакета из банка.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияПакета,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
		
КонецПроцедуры

Процедура СохранитьДанныеПодписейПослеПолученияСвойствСертификатаБанка(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Результат);
		СтруктураВозврата.Вставить("КоличествоПолученных", ДополнительныеПараметры.КоличествоПолученных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("СвойстваСертификатаБанка", Результат);
	
	ПроверитьПодписиПолученныхДокументовЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ПроверитьПодписиПолученныхДокументовЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ДанныеЭД.Количество() = 0 Тогда // Обработаны все сообщения обмена
		ПолучитьОчереднойПакетВК(ДополнительныеПараметры.МассивИдентификаторов, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ДополнительныеПараметры.ДанныеЭД Цикл
		СообщениеОбмена = КлючЗначение.Ключ;
		МассивПодписей = КлючЗначение.Значение;
		Прервать;
	КонецЦикла;
	
	ДополнительныеПараметры.ДанныеЭД.Удалить(СообщениеОбмена);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("МассивПодписей", МассивПодписей);
	ДвоичныеДанныеЭД = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(
		ДополнительныеПараметры.СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ДанныеЭДBase64", Base64Строка(ДвоичныеДанныеЭД));
	ДополнительныеПараметры.Вставить("ДанныеПроверкиПодписи", Новый Массив);
	ДополнительныеПараметры.Вставить("ТекстОшибки");
	
	ПроверитьОчереднуюПодписьСообщенияОбменаЧерезВК(Неопределено, ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ПроверитьОчереднуюПодписьСообщенияОбменаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ДополнительныеПараметры.Вставить("ТекстОшибки", Результат);
		// возврат не делаем, т.к. нужно сохранить все подписи
		СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("ДанныеПодписи", ДополнительныеПараметры.ДвоичныеДанныеПодписи);
		СтруктураЗаписи.Вставить("ПодписьВерна");
		СтруктураЗаписи.Вставить("ДатаПроверкиПодписи");
		ДополнительныеПараметры.ДанныеПроверкиПодписи.Добавить(СтруктураЗаписи);
	КонецЕсли;
	
	Если Результат <> Неопределено Тогда
		СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("ДанныеПодписи", ДополнительныеПараметры.ДвоичныеДанныеПодписи);
		СтруктураЗаписи.Вставить("ПодписьВерна", Результат);
		СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
		ДополнительныеПараметры.ДанныеПроверкиПодписи.Добавить(СтруктураЗаписи);
	КонецЕсли;
	
	Если ДополнительныеПараметры.МассивПодписей.Количество() = 0 Тогда // проверены все подписи
		
		СвойстваСертификатаБанка = Новый Структура;
		СвойстваСертификатаБанка.Вставить("ДвоичныеДанные", Base64Значение(ДополнительныеПараметры.СертификатБанкаBase64));
		СвойстваСертификатаБанка.Вставить("КомуВыдан", ДополнительныеПараметры.СвойстваСертификатаБанка.ВладелецФИО);
		СвойстваСертификатаБанка.Вставить("Отпечаток", ДополнительныеПараметры.СвойстваСертификатаБанка.Отпечаток);
		
		ОбменСБанкамиСлужебныйВызовСервера.СохранитьДанныеЭлектронныхПодписей(
			ДополнительныеПараметры.СообщениеОбмена, СвойстваСертификатаБанка, ДополнительныеПараметры.ДанныеПроверкиПодписи);
			
		ПроверитьПодписиПолученныхДокументовЧерезВК(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеПодписи = ДополнительныеПараметры.МассивПодписей.Получить(0);
	ДополнительныеПараметры.МассивПодписей.Удалить(0);
	ДополнительныеПараметры.Вставить("ДвоичныеДанныеПодписи", ДвоичныеДанныеПодписи);
	
	ПодписьBase64 = Base64Строка(ДвоичныеДанныеПодписи);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьОчереднуюПодписьСообщенияОбменаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ПроверитьПодписьЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль,
		ДополнительныеПараметры.ДанныеЭДBase64, ДополнительныеПараметры.СертификатБанкаBase64, ПодписьBase64);
		
КонецПроцедуры

Процедура ПослеПроверкиПодписиЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписи, РезультатВызова);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПроверкиПодписиЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт

	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При проверке подписи документа произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Проверка подписи электронного документа.'");

	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписи,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПодписатьДанныеЧерезВК(Оповещение, ПодключаемыйМодуль, ДанныеЭДBase64, СертификатBase64)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодписанияЭД", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("СертификатBase64", СертификатBase64);
	ДополнительныеПараметры.Вставить("ДанныеЭДBase64", ДанныеЭДBase64);
	
	ОповещениеПослеПодготовкиПодписи = Новый ОписаниеОповещения("ПодписатьЭДПослеПодготовкиПодписиЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПодготовкиПодписиЧерезВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПодготовитьПодпись(
		ОповещениеПослеПодготовкиПодписи, СертификатBase64, ДанныеЭДBase64);

КонецПроцедуры

Процедура ПодписатьЭДПослеПодготовкиПодписиЧерезВК(Результат, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	СтруктураРезультата = ДеСериализованныеДанные(Результат);
	
	Если СтруктураРезультата.Время = -1 Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПодписанияЧерезВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПодписанияЧерезВК", ЭтотОбъект);
		
		ДанныеПодписи = Неопределено;
	
		ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПодписать(
			Оповещение, ДополнительныеПараметры.СертификатBase64, ДополнительныеПараметры.ДанныеЭДBase64, ДанныеПодписи);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПодготовкиПодписиЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При подготовки подписи электронного документа произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Подготовка подписи электронного документа.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПодписанияЭД,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПослеПодписанияЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если Не РезультатВызова Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЭД, Неопределено);
		Возврат;
	КонецЕсли;
	
	РезультатПодписи = Новый Структура("Успех, ДанныеПодписи", Истина, ПараметрыВызова[2]);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЭД, РезультатПодписи);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПодписанияЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Подписание электронного документа'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПодписанияЭД,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура СохранитьПодписьПослеПолученияСвойствСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Результат);
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеПодписи = Base64Значение(ДополнительныеПараметры.ПодписьBase64);
	
	СтруктураСертификата = Новый Структура;
	СтруктураСертификата.Вставить("Отпечаток",     Результат.Отпечаток);
	СтруктураСертификата.Вставить("КомуВыдан",     Результат.ВладелецФИО);
	СтруктураСертификата.Вставить("КемВыдан",      Результат.ИмяИздателя);
	СтруктураСертификата.Вставить("СерийныйНомер", Результат.СерийныйНомер);
	СтруктураСертификата.Вставить("ДатаОкончания", Результат.ДатаОкончания);
	СтруктураСертификата.Вставить("ДвоичныеДанные", Base64Значение(ДополнительныеПараметры.СертификатBase64));
	СтруктураСертификата.Вставить("СертификатСсылка", ДополнительныеПараметры.СертификатСсылка);
	
	ДанныеПроверкиПодписи = Новый Структура;
	ДанныеПроверкиПодписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
	ДанныеПроверкиПодписи.Вставить("ПодписьВерна", Истина);
	ОбменСБанкамиСлужебныйВызовСервера.ДобавитьПодпись(
		ДополнительныеПараметры.СообщениеОбмена, ДвоичныеДанныеПодписи, СтруктураСертификата, ДанныеПроверкиПодписи);
		
	Оповестить("ОбновитьСостояниеОбменСБанками");
		
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Истина);
	
КонецПроцедуры

Процедура ПослеУстановкиСоединенияВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Истина);
		Возврат;
	КонецЕсли;
	
	// Необходима расширенная аутентификация.
	
	Сессия = ДеСериализованныеДанные(ПараметрыВызова[3]).Сессия;
	СессияСтрокой = СериализованныеДанные(Сессия);
	ДополнительныеПараметры.Вставить("Сессия", Сессия);
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиПароляПоSMSВК", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуОтправкиПароляПоSMSВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовОтправитьПарольРасширеннойАутентификацииПоSMS(
		Оповещение, СессияСтрокой);
	
КонецПроцедуры

Процедура ПослеОтправкиПароляПоSMSВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаОдноразовогоПароляВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура("ИдентификаторСессии", ДополнительныеПараметры.Сессия.Идентификатор);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросОдноразовогоПароля", ПараметрыФормы, ЭтотОбъект, , , , Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ПослеВводаОдноразовогоПароляВК(ОдноразовыйПароль, ДополнительныеПараметры) Экспорт
	
	Если ОдноразовыйПароль = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Неопределено);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеРасширеннойАутентификации", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуРасширеннойАутентификации", ЭтотОбъект);
		
	ПараметрыАутентификации = Новый Структура;
	ПараметрыАутентификации.Вставить("Способ", "SMS");
	ПараметрыАутентификации.Вставить("Сессия", ДополнительныеПараметры.Сессия);
	ПараметрыАутентификации.Вставить("Пароль", ОдноразовыйПароль);
	
	ПараметрыАутентификацииСтрокой = СериализованныеДанные(ПараметрыАутентификации);
		
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовРасширеннаяАутентификация(
		Оповещение, ПараметрыАутентификацииСтрокой);
	
КонецПроцедуры

Процедура ПослеРасширеннойАутентификации(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Истина);
	
КонецПроцедуры

Процедура ОбработатьОшибкуРасширеннойАутентификации(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'Ошибка аутентификации по SMS.'");
	ВидОперации = НСтр("ru = 'Отправка одноразового пароля из SMS.'");
	
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ОбработатьОшибкуОтправкиПароляПоSMSВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'Ошибка отправки одноразового пароля по SMS.'");
	ВидОперации = НСтр("ru = 'Отправка одноразового пароля в SMS.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ОбработатьОшибкуУстановкиСоединенияВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При установке соединения с банком произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Установка соединения с банком.'");
	
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ПредъявитьДанныеАутентификацииПослеПолучения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Результат) = Тип("Структура") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("СертификатBase64", Результат.СертификатBase64);
	
	Если Результат.ТребуетсяПароль Тогда
		Оповещение = Новый ОписаниеОповещения(
			"УстановитьСоединениеПослеАутентификацииНаКлючеВК", ЭтотОбъект, ДополнительныеПараметры);
		ВызватьАутентификациюНаЭлектронномКлючеВК(
			Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, Результат.СертификатBase64, Результат.Пароль);
	Иначе
		УстановитьСоединениеПослеАутентификацииНаКлючеВК(Неопределено, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьСоединениеПослеАутентификацииНаКлючеВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата, Результат);
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения(
		"ДополнитьСертификатПослеУстановкиСоединенияВК", ЭтотОбъект, ДополнительныеПараметры);
	
	УстановитьСоединениеВК(Обработчик, ДополнительныеПараметры.ПодключаемыйМодуль,
		ДополнительныеПараметры.СертификатBase64, ДополнительныеПараметры.ПараметрыСоединения);
	
КонецПроцедуры

Процедура ДополнитьСертификатПослеУстановкиСоединенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = Истина Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеДополненияКлючаВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуДополненияДанныхКлючаВК", ЭтотОбъект);
		
	// Сертификат необходимо дополнить для проверки подписи при тесте настроек
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовДополнитьДанныеКлючаЭП(
		Оповещение, ДополнительныеПараметры.СертификатBase64)
	
КонецПроцедуры

Процедура ПослеДополненияКлючаВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.Вставить("СертификатBase64", РезультатВызова);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияДанныхКлючаВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьДанныеКлючаЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, РезультатВызова);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхКлючаВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Результат) = Тип("Структура") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата, Результат);
		Возврат;
	КонецЕсли;
	
	Результат.Вставить("СертификатBase64", ДополнительныеПараметры.СертификатBase64);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуДополненияДанныхКлючаВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При работе с электронным ключем произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Дополнение данных ключа.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ПослеУстановкиПароляВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииВК, Истина);
	
КонецПроцедуры

Процедура ОбработатьОшибкуУстановкиПароляВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.СертификатСсылка) Тогда
		УдалитьПарольИзСеанса(ДополнительныеПараметры.СертификатСсылка);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'Ошибка установки пароля.'");
	ВидОперации = НСтр("ru = 'Установка пароля ключа.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеАутентификацииВК,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
		
КонецПроцедуры

Процедура ВыбратьКлючПослеПолученияХранилищКлючейЭП(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат.Количество() > 1 Тогда
		СписокВыбора = Новый СписокЗначений;
		Для Каждого Элемент Из Результат Цикл
			СписокВыбора.Добавить(Элемент);
		КонецЦикла;
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеВыбораКлючаВК", ЭтотОбъект, ДополнительныеПараметры);
		ЗаголовокВыбора = НСтр("ru = 'Выберите электронный ключ:'");
		СписокВыбора.ПоказатьВыборЭлемента(ОповещениеОЗакрытии, ЗаголовокВыбора);
	Иначе
		ИдентификаторХранилища = Результат[0];
		ДополнительныеПараметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
		ОбработчикПродолжения = Новый ОписаниеОповещения(
			"ПолучитьКлючиПослеУстановкиПинВК", ЭтотОбъект, ДополнительныеПараметры);
		УстановитьПинЕслиТребуетсяВК(
			ОбработчикПродолжения, ДополнительныеПараметры.ПодключаемыйМодуль, ИдентификаторХранилища);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВыбораКлючаВК(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, Неопределено);
		Возврат;
	КонецЕсли;
	
	ИдентификаторХранилища = ВыбранныйЭлемент.Значение;
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	ОбработчикПродолжения = Новый ОписаниеОповещения(
		"ПолучитьКлючиПослеУстановкиПинВК", ЭтотОбъект, ДополнительныеПараметры);
	УстановитьПинЕслиТребуетсяВК(
		ОбработчикПродолжения, ДополнительныеПараметры.ПодключаемыйМодуль, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура УстановитьПинЕслиТребуетсяВК(Обработчик, ПодключаемыйМодуль, ИдентификаторХранилища)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановкиПин", Обработчик);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	
	ОповещениеПослеПроверкиНеобходимостиPIN = Новый ОписаниеОповещения(
		"УстановитьПинПослеПроверкиНеобходимостиЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ПроверитьНеобходимостьУстановкиПинЧерезВК(
		ОповещениеПослеПроверкиНеобходимостиPIN, ПодключаемыйМодуль, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура УстановитьПинПослеПроверкиНеобходимостиЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиПин, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат Тогда
		ОбработчикВводаPIN = Новый ОписаниеОповещения("ОбработатьВводPINВК", ЭтотОбъект, ДополнительныеПараметры);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИдентификаторХранилища", ДополнительныеПараметры.ИдентификаторХранилища);
		ОткрытьФорму(
			"Обработка.ОбменСБанками.Форма.ЗапросPINКода", ПараметрыФормы, ЭтотОбъект, Истина , , , ОбработчикВводаPIN);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиПин, Истина); // не требуется пин
	КонецЕсли
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьУстановкиПинЧерезВК(Оповещение, ПодключаемыйМодуль, ИдентификаторХранилища)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиНеобходимостиПин", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеПроверкиНеобходимостиPIN = Новый ОписаниеОповещения("ПослеПроверкиНеобходимостиПИНВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПроверкиНеобходимостиПИНВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовТребуетсяПин(ОповещениеПослеПроверкиНеобходимостиPIN, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПослеПроверкиНеобходимостиПИНВК(ТребуетсяПин, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиНеобходимостиПин, ТребуетсяПин);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПроверкиНеобходимостиПИНВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Проверка необходимости установки ПИН-кода.'");
	ТекстСообщения = НСтр("ru = 'При проверке необходимости ПИН-кода произошла ошибка.'");
	
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПроверкиНеобходимостиПин,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ОбработатьВводPINВК(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиПин, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеУстановкиПинВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуУстановкиПинВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовУстановитьПин(
		Оповещение, ДополнительныеПараметры.ИдентификаторХранилища, Результат);

КонецПроцедуры

Процедура ОбработатьОшибкуУстановкиПинВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Установка PIN кода.'");
	ТекстСообщения = НСтр("ru = 'При установки PIN кода произошла ошибка.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиПин,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ПослеУстановкиПинВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиПин, Истина);
	
КонецПроцедуры

Процедура ПолучитьКлючиПослеУстановкиПинВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, Результат);
		Возврат;
	КонецЕсли;
	
	Если Не Результат Тогда // Пользователь отказался вводить пин-код
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, Неопределено);
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения(
		"ПолучитьДанныеСертификатовПослеПолученияДвоичныхДанныхЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ПолучитьДанныеСертификатовХранилищаВК(
		Обработчик, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПолучитьДанныеСертификатовПослеПолученияДвоичныхДанныхЧерезВК(СертификатыКлюча, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(СертификатыКлюча) = Тип("Строка") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, СертификатыКлюча);
		Возврат;
	КонецЕсли;
	
	ДанныеСертификатов = Новый Массив;
	ДополнительныеПараметры.Вставить("ДанныеСертификатов", ДанныеСертификатов);
	ДополнительныеПараметры.Вставить("СертификатыКлюча", СертификатыКлюча);
	ПолучитьДанныеСертификатовРекурсивноВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПолучитьДанныеСертификатовРекурсивноВК(ДополнительныеПараметры)
	
	Если Не ДополнительныеПараметры.СертификатыКлюча.Количество() Тогда // закончен перебор массива
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, ДополнительныеПараметры.ДанныеСертификатов);
		Возврат;
	КонецЕсли;
	
	СертификатВК = ДополнительныеПараметры.СертификатыКлюча[0];
	Если ТипЗнч(СертификатВК) = Тип("ДвоичныеДанные") Тогда
		СертификатВК = Base64Строка(СертификатВК);
	КонецЕсли;
	ДополнительныеПараметры.СертификатыКлюча.Удалить(0);
	ДополнительныеПараметры.Вставить("СертификатBase64", СертификатВК);
	
	Обработчик = Новый ОписаниеОповещения(
		"ДобавитьСертификатВМассивПослеПолученияДанныхЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьДанныеКлючаЧерезВК(Обработчик, ДополнительныеПараметры.ПодключаемыйМодуль, СертификатВК);
	
КонецПроцедуры

Процедура ДобавитьСертификатВМассивПослеПолученияДанныхЧерезВК(ДанныеСертификата, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(ДанныеСертификата) = Тип("Структура") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, ДанныеСертификата);
		Возврат;
	КонецЕсли;
	
	СтруктураСертификата = Новый Структура;
	СтруктураСертификата.Вставить("СертификатBase64", ДополнительныеПараметры.СертификатBase64);
	СтруктураСертификата.Вставить("ДанныеСертификата", ДанныеСертификата);
	СтруктураСертификата.Вставить("ИдентификаторХранилища",ДанныеСертификата.ИдентификаторХранилища);
	ДополнительныеПараметры.ДанныеСертификатов.Добавить(СтруктураСертификата);
	
	ПолучитьДанныеСертификатовРекурсивноВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПоказатьВыборПослеПолученияКлючейВК(ДанныеСертификатов, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(ДанныеСертификатов) = Тип("Массив") Тогда // произошла ошибка или пользователь нажал Отмена.
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеПолученияДанныхАутентификации, ДанныеСертификатов);
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПослеВыбораСертификатаВК", ЭтотОбъект, ДополнительныеПараметры);
	ВыбратьКлючВК(Обработчик, ДополнительныеПараметры.ПодключаемыйМодуль, ДанныеСертификатов);
	
КонецПроцедуры

Процедура ВыбратьКлючВК(Обработчик, ПодключаемыйМодуль, ДанныеСертификатов)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОбработчикПослеВыбораКлюча", Обработчик);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ДополнительныеПараметры.Вставить("ДанныеСертификатов", ДанныеСертификатов);
	ДополнительныеПараметры.Вставить("ДанныеВыбора", Новый Соответствие);
	
	ПроверитьНаличиеПароляРекурсивноЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры
	
Процедура ПослеОчереднойПроверкиНеобходимостиПароляЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикПослеВыбораКлюча, Результат);
		Возврат;
	КонецЕсли;
	
	СтруктураСертификата = Новый Структура;
	СтруктураСертификата.Вставить("СертификатBase64", ДополнительныеПараметры.ДанныеТекущегоСертификата.СертификатBase64);
	СтруктураСертификата.Вставить(
		"Псевдоним", ДополнительныеПараметры.ДанныеТекущегоСертификата.ДанныеСертификата.Псевдоним);
	СтруктураСертификата.Вставить("ТребуетсяПароль", Результат);
	
	ДополнительныеПараметры.ДанныеВыбора.Вставить(
		ДополнительныеПараметры.ДанныеТекущегоСертификата.ДанныеСертификата.Отпечаток, СтруктураСертификата);
	
	ПроверитьНаличиеПароляРекурсивноЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ПроверитьНаличиеПароляРекурсивноЧерезВК(ДополнительныеПараметры)
	
	Если НЕ ДополнительныеПараметры.ДанныеСертификатов.Количество() Тогда // все обработаны
		Если ДополнительныеПараметры.ДанныеВыбора.Количество() = 1 Тогда
			Для каждого ЭлементКоллекции Из ДополнительныеПараметры.ДанныеВыбора Цикл
				Прервать;
			КонецЦикла;
			Если НЕ ЭлементКоллекции.Значение.ТребуетсяПароль Тогда
				СтруктураВозврата = Новый Структура;
				СтруктураВозврата.Вставить("Отпечаток", ЭлементКоллекции.Ключ);
				СтруктураВозврата.Вставить("СертификатBase64", ЭлементКоллекции.Значение.СертификатBase64);
				СтруктураВозврата.Вставить("Пароль", Неопределено);
				СтруктураВозврата.Вставить("ТребуетсяПароль", ЭлементКоллекции.Значение.ТребуетсяПароль);
				ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикПослеВыбораКлюча, СтруктураВозврата);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("ДанныеВыбора", ДополнительныеПараметры.ДанныеВыбора);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ВыборСертификатаДляДобавления", ПараметрыФормы, ЭтотОбъект, , , ,
			ДополнительныеПараметры.ОбработчикПослеВыбораКлюча, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Возврат;
	КонецЕсли;
	
	ДанныеТекущегоСертификата = ДополнительныеПараметры.ДанныеСертификатов.Получить(0);
	ДополнительныеПараметры.Вставить("ДанныеТекущегоСертификата", ДанныеТекущегоСертификата);
	ДополнительныеПараметры.ДанныеСертификатов.Удалить(0);
	
	Оповещение = Новый ОписаниеОповещения("ПослеОчереднойПроверкиНеобходимостиПароляЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ВызватьПроверкуНеобходимостиУстановкиПароляСертификатаЧерезВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДанныеТекущегоСертификата.СертификатBase64);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхСертификатаВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДанныеСертификата = ДеСериализованныеДанные(РезультатВызова);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификатаВК, ДанныеСертификата);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияДанныхСертификатаВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Получение данных ключа ЭП.'");
	ТекстСообщения = НСтр("ru = 'Произошла ошибка при получении данных ключа ЭП.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификатаВК,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);

КонецПроцедуры

Процедура ПослеВыбораСертификатаВК(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(ДанныеВыбора) = Тип("Структура") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхАутентификации, ДанныеВыбора);
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("СертификатBase64", ДанныеВыбора.СертификатBase64);
	СтруктураВозврата.Вставить("Пароль", ДанныеВыбора.Пароль);
	СтруктураВозврата.Вставить("ТребуетсяПароль", ДанныеВыбора.ТребуетсяПароль);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхАутентификации, СтруктураВозврата);
	
КонецПроцедуры

Процедура ПолучитьДанныеСертификатовХранилищаВК(Обработчик, ПодключаемыйМодуль, ИдентификаторХранилища)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияСертификатовХранилища", Обработчик);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеПолученияКлючейЭПВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПолученияКлючейВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовКлючиЭП(Оповещение, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПослеПолученияКлючейЭПВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	МассивДвоичныхДанныхСертификатов = ДеСериализованныеДанные(РезультатВызова);
	
	Если НЕ МассивДвоичныхДанныхСертификатов.Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствуют сертификаты на банковском ключе'");
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификатовХранилища, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	МассивСертификатовBase64 = Новый Массив;
	
	Для Каждого Элемент Из МассивДвоичныхДанныхСертификатов Цикл
		МассивСертификатовBase64.Добавить(Base64Строка(Элемент));
	КонецЦикла;
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеПолученияСертификатовХранилища, МассивСертификатовBase64);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияКлючейВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Получение ключей'");
	ТекстСообщения = НСтр("ru = 'Произошла ошибка при получении ключей.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияСертификатовХранилища,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ПослеИнициализацииВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеИнициализацииВК, Истина);
	
КонецПроцедуры

Процедура ОбработатьОшибкуИнициализацииВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При инициализации внешнего модуля произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Инициализация внешней компоненты.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеИнициализацииВК,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
		
КонецПроцедуры

Процедура ПослеВводаОдноразовогоПароляСессияЧерезДопОбработку(Пароль, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Пароль) Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиОдноразовогоПароля, Ложь);
		Возврат;
	КонецЕсли;
	
	ДанныеРасширеннойАутентификации = Новый Структура("Способ, Сессия, Пароль");
	ДанныеРасширеннойАутентификации.Способ = "SMS";
	ДанныеРасширеннойАутентификации.Сессия = ДополнительныеПараметры.Сессия;
	ДанныеРасширеннойАутентификации.Пароль = Пароль;

	Попытка
		ДополнительныеПараметры.ВнешнийПодключаемыйМодуль.РасширеннаяАутентификация(
			ДополнительныеПараметры.Сертификат, ДанныеРасширеннойАутентификации);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиОдноразовогоПароля, Истина);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка SMS аутентификации.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ДополнительныеПараметры.ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'SMS аутентификация'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиОдноразовогоПароля, Ложь);
	КонецПопытки;
	
КонецПроцедуры

// Предлагает пользователю выбрать хранилище и получает список ключей из хранилища (асинхронно).
//
// Параметры:
//  Оповещение - ОписаниеОповещения - вызывается после выполнения метода
//      * Результат - Массив - данные ключей. В элементах содержится структура с элементами:
//                       * СертификатBase64 - Строка - данные сертификата в Base64;
//                       * ДанныеСертификата - Структура - содержит информацию о сертификате:
//                            ** Псевдоним - Строка - псевдоним ключа ЭП;
//                            ** Отпечаток - Строка - уникальный идентификатор ключа ЭП;
//                            ** СерийныйНомер - Строка - серийный номер ключа в hex;
//                            ** ИмяИздателя - Строка - имя издателя сертификата.
//                  - Строка - текст ошибки
//                  - Неопределено - пользователь отказался от продолжать операцию.
//      * ДополнительныеПараметры - Произвольный - контекст вызова.
//  ПодключаемыйМодуль - Произвольный  - внешняя компонента;
//  ИдентификаторХранилища - Строка - хранилище из которого будут получены сертификаты.
//
Процедура ПолучитьСертификатыИзХранилищаВК(Оповещение, ПодключаемыйМодуль, ИдентификаторХранилища = Неопределено)
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияКлючей", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	
	ОповещениеПослеПолученияХранилищКлючей = Новый ОписаниеОповещения(
		"ВыбратьКлючПослеПолученияХранилищКлючейЭП", ЭтотОбъект, ДополнительныеПараметры);
			
	ОбеспечитьПодключениеЭлектронногоКлючаЧерезВК(
		ОповещениеПослеПолученияХранилищКлючей, ПодключаемыйМодуль, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ВойтиНаАппаратноеУстройствоПослеПодключенияЧерезВК(Результат, ДополнительныеПараметры) Экспорт

	Если Не ТипЗнч(Результат) = Тип("Массив") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Результат);
		Возврат;
	КонецЕсли;

	ИдентификаторХранилища = Результат.Получить(0);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьПодписиПослеВходаНаАппаратноеУстройствоЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	УстановитьПинЕслиТребуетсяВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ОбеспечитьПодключениеАппаратногоУстройстваПослеИнициализацииВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Результат);
		Возврат;
	КонецЕсли;

	ПодключаемыйМодуль = Результат;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения("ВойтиНаАппаратноеУстройствоПослеПодключенияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбеспечитьПодключениеЭлектронногоКлючаЧерезВК(Оповещение, ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ПроверитьОчереднуюПодписьЧерезВК(ДополнительныеПараметры)
	
	КоличествоПроверенныхПодписей = ДополнительныеПараметры.РезультатыПроверкиПодписей.Количество();
	Если КоличествоПроверенныхПодписей >= ДополнительныеПараметры.Подписи.Количество() Тогда
		ОбменСБанкамиСлужебныйВызовСервера.СохранитьРезультатыПроверкиПодписей(ДополнительныеПараметры.СообщениеОбмена, ДополнительныеПараметры.РезультатыПроверкиПодписей);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Истина);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ОбработатьРезультатПроверкиПодписиЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ТекущиеДанныеПодписи = ДополнительныеПараметры.Подписи.Получить(КоличествоПроверенныхПодписей);
	
	ПодписьСтрокой = Base64Строка(ТекущиеДанныеПодписи.Подпись);
	Если ТипЗнч(ТекущиеДанныеПодписи.Сертификат) = Тип("Строка") Тогда
		СертификатСтрокой = ТекущиеДанныеПодписи.Сертификат;
	Иначе
		СертификатСтрокой = Base64Строка(ТекущиеДанныеПодписи.Сертификат);
	КонецЕсли;
	ПроверитьПодписьЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.ДанныеBase64,
		СертификатСтрокой, ПодписьСтрокой);
	
КонецПроцедуры

Процедура ПроверитьПодписиПослеВходаНаАппаратноеУстройствоЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Результат);
		Возврат;
	КонецЕсли;
	
	Если Не Результат Тогда // Пользователь отказался вводить пин-код
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДанныеДляПроверкиПодписей = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСодержимогоСообщенияОбмена(
		ДополнительныеПараметры.СообщениеОбмена);
	
	Если ДанныеДляПроверкиПодписей = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ДанныеBase64", Base64Строка(ДанныеДляПроверкиПодписей.ДанныеЭД));
	
	ДополнительныеПараметры.Вставить("РезультатыПроверкиПодписей", Новый Массив);
	ДополнительныеПараметры.Вставить("Подписи", ДанныеДляПроверкиПодписей.Подписи);
	
	ПроверитьОчереднуюПодписьЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьРезультатПроверкиПодписиЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Результат);
		Возврат;
	КонецЕсли;
	
	СтруктураЗаписи = Новый Структура;
	СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
	СтруктураЗаписи.Вставить("ПодписьВерна", Результат);
	ДополнительныеПараметры.РезультатыПроверкиПодписей.Добавить(СтруктураЗаписи);
	ПроверитьОчереднуюПодписьЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПослеНачалаПодтвержденияПлатежногоПорученияЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ПараметрыПодтвержденияПлатежа = ДеСериализованныеДанные(РезультатВызова);
	ДополнительныеПараметры.Вставить("ИдентификаторСессии", ПараметрыПодтвержденияПлатежа.Сессия.Идентификатор);
	СессияСтрокой = СериализованныеДанные(ПараметрыПодтвержденияПлатежа.Сессия);
	ДополнительныеПараметры.Вставить("Сессия", СессияСтрокой);
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиКодаПодтвержденияПоSMSЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуОтправкиКодаПодтвержденияЧерезВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовОтправитьКодПодтвержденияПоSMS(Оповещение, СессияСтрокой);
	
КонецПроцедуры

Процедура ОбработатьОшибкуОтправкиКодаПодтвержденияЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При отправке кода подтверждения через SMS произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Отправка кода подтверждения платежного поручения.'");
	Оповещение = Новый ОписаниеОповещения("СообщитьОшибкуПользователюЧерезВК", ЭтотОбъект);
	
	ПолучитьИнформациюОбОшибкеВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ПослеОтправкиКодаПодтвержденияПоSMSЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИдентификаторСессии", ДополнительныеПараметры.ИдентификаторСессии);
	ПараметрыФормы.Вставить("СообщениеОбмена", ДополнительныеПараметры.СообщениеОбмена);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПодтвердитьПлатежноеПоручениеПослеВводаПароляЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму(
		"Обработка.ОбменСБанками.Форма.ПодтверждениеПлатежныхПорученийПоSMS", ПараметрыФормы, , , , , ОписаниеОповещения);
	
КонецПроцедуры

Процедура ПодтвердитьПлатежноеПоручениеПослеВводаПароляЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ПодтвердитьПлатежныеДокументыЧерезВК(ДополнительныеПараметры.ПодключаемыйМодуль,
			ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.МассивСообщенийОбмена);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодтвержденияПлатежногоПорученияЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПодтвержденияПлатежногоПорученияЧерезВК", ЭтотОбъект);
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПодтвердитьПлатежноеПоручение(
		Оповещение, ДополнительныеПараметры.Сессия, Результат);
		
КонецПроцедуры

Процедура ОбработатьОшибкуНачалаПодтвержденияПлатежногоПорученияЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При инициализации подтверждения платежного поручения произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Инициализация подтверждения платежного поручения.'");
	Оповещение = Новый ОписаниеОповещения("СообщитьОшибкуПользователюЧерезВК", ЭтотОбъект);
	
	ПолучитьИнформациюОбОшибкеВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
		
	ПодтвердитьПлатежныеДокументыЧерезВК(ДополнительныеПараметры.ПодключаемыйМодуль,
		ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.МассивСообщенийОбмена);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПодтвержденияПлатежногоПорученияЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При подтверждении платежного поручения произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Подтверждение платежного поручения.'");
	Оповещение = Новый ОписаниеОповещения("СообщитьОшибкуПользователюЧерезВК", ЭтотОбъект);
	
	ПолучитьИнформациюОбОшибкеВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
		
	ПодтвердитьПлатежныеДокументыЧерезВК(ДополнительныеПараметры.ПодключаемыйМодуль,
		ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.МассивСообщенийОбмена);
		
КонецПроцедуры

Процедура СообщитьОшибкуПользователюЧерезВК(ТекстСообщения, ДополнительныеПараметры) Экспорт
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

Процедура ПослеПодтвержденияПлатежногоПорученияЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	СтатусПринят = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Принят");
	СтруктураРеквизитов = Новый Структура("Статус", СтатусПринят);
	ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(
		ДополнительныеПараметры.СообщениеОбмена, СтруктураРеквизитов);
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Платеж подтвержден'"), , , БиблиотекаКартинок.Успешно32);
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	ПодтвердитьПлатежныеДокументыЧерезВК(ДополнительныеПараметры.ПодключаемыйМодуль,
		ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.МассивСообщенийОбмена);
	
КонецПроцедуры

Процедура НачатьПодтверждениеПлатежныхДокументовПоSMSПослеПолученияНовыхДокументовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат.Результат) = Тип("Строка") Тогда
		ВидОперации = НСтр("ru = 'Получение новых документов из банка.'");
		ОбработатьОшибку(ВидОперации, Результат.Результат, Результат.Результат, ДополнительныеПараметры.НастройкаОбмена);
		ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПродолжитьОтправкуДокументовПоНастройкамиПослеПодтвержденияДокументовЧерезВК",
		ЭтотОбъект, ДополнительныеПараметры);
	МассивСообщенийОбмена = ОбменСБанкамиСлужебныйВызовСервера.СообщенияОбменаТребующиеПодтверждения(
		ДополнительныеПараметры.МассивСообщенийОбмена);
		
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСообщенийОбмена, ДополнительныеПараметры.МассивСообщенийТребующихПодтверждение);
		
	ПодтвердитьПлатежныеДокументыЧерезВК(ДополнительныеПараметры.ПодключаемыйМодуль, Оповещение, МассивСообщенийОбмена);

КонецПроцедуры

Процедура ПродолжитьОтправкуДокументовПоНастройкамиПослеПодтвержденияДокументовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьРезультатОтправкиПакетов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ДополнительныеПараметры.КоличествоПодготовленных = ДополнительныеПараметры.КоличествоПодготовленных
			+ Результат.КоличествоПодготовленных;
		ДополнительныеПараметры.КоличествоОтправленных = ДополнительныеПараметры.КоличествоОтправленных
			+ Результат.КоличествоОтправленных;
	КонецЕсли;
	
	ОтправкаСообщенийОбмена(ДополнительныеПараметры);

КонецПроцедуры

Процедура ОтправитьПакетыПослеПодготовкиЗапросовВыписокЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ПодготовленныеПакеты = ОбменСБанкамиСлужебныйВызовСервера.ПодготовленныеКОтправкеПакеты(
			ДополнительныеПараметры.НастройкаОбмена);
		Оповещение = Новый ОписаниеОповещения("ПослеОправкиПакетовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ОтправитьПакетыЧерезВК(
			Оповещение, ДополнительныеПараметры.НастройкаОбмена, Результат.МассивСообщенийОбмена, ПодготовленныеПакеты);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСинхронизации, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьПолучениеВыпискиПослеПодготовкиЗапросовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("МассивСообщенийОбменаКОтправке", Результат.МассивСообщенийОбмена);
	Оповещение = Новый ОписаниеОповещения("ЗапуститьПроцессПолученияВыпискиЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ДанныеОтправки = Новый Соответствие;
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("МассивСообщенийОбмена", Результат.МассивСообщенийОбмена);
	СтруктураДанных.Вставить("МассивСообщенийТребующихПодтверждение", Новый Массив);
	ДанныеОтправки.Вставить(ДополнительныеПараметры.НастройкаОбмена, СтруктураДанных);
	ОтправитьДокументыЧерезВК(Оповещение, ДанныеОтправки, Ложь);

КонецПроцедуры

Процедура ПослеПодписанияЗапросовВыписокЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.КоличествоПодписанных = 0 Тогда
		СтруктураВозврата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодготовкиЗапросовВыписки, СтруктураВозврата);
	Иначе
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Успех", Истина);
		СтруктураВозврата.Вставить("МассивСообщенийОбмена", ДополнительныеПараметры.МассивСообщенийОбменаКОтправке);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодготовкиЗапросовВыписки, СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьПараметрыСертификатовПослеИнициализацииВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("Результат", Результат);
		ДанныеВозврата.Вставить("КоличествоОтправленных", ДополнительныеПараметры.КоличествоОтправленных);
		ДанныеВозврата.Вставить("КоличествоПодготовленных", ДополнительныеПараметры.КоличествоПодготовленных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетов, ДанныеВозврата);
		Возврат;
	КонецЕсли;

	ПодключаемыйМодуль = Результат;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения(
		"ОтправитьПакетыПослеПолученияДанныхСертификатовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьСвойстваСертификатовСообщенийОбменаЧерезВК(Оповещение, ПодключаемыйМодуль,
		ДополнительныеПараметры.СообщенияОбмена, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ОтправитьПакетыПослеПолученияДанныхСертификатовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат);
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("Результат", Результат);
		ДанныеВозврата.Вставить("КоличествоОтправленных", ДополнительныеПараметры.КоличествоОтправленных);
		ДанныеВозврата.Вставить("КоличествоПодготовленных", ДополнительныеПараметры.КоличествоПодготовленных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетов, ДанныеВозврата);
		Возврат;
	КонецЕсли;
	
	СоответствиеДанных = Новый Соответствие;
	
	Для Каждого СообщениеОбменаИСертификаты Из Результат Цикл
		СвойстваСертификатов = Новый Соответствие;
		Для Каждого Элемент Из СообщениеОбменаИСертификаты.Значение Цикл
			СвойстваСертификата = Новый Структура("СерийныйНомер, ИмяИздателя");
			ЗаполнитьЗначенияСвойств(СвойстваСертификата, Элемент.ДанныеСертификата);
			СвойстваСертификатов.Вставить(Элемент.ДанныеСертификата.Отпечаток, СвойстваСертификата)
		КонецЦикла;
		СоответствиеДанных.Вставить(СообщениеОбменаИСертификаты.Ключ, СвойстваСертификатов);
	КонецЦикла;
	
	МассивПакетовЭД = ОбменСБанкамиСлужебныйВызовСервера.СоздатьПакетыВК(
		ДополнительныеПараметры.НастройкаОбмена, СоответствиеДанных);

	Оповестить("ОбновитьСостояниеОбменСБанками");
	ДополнительныеПараметры.КоличествоПодготовленных = ДополнительныеПараметры.КоличествоПодготовленных
		+ МассивПакетовЭД.Количество();
		
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДополнительныеПараметры.ПакетыЭД, МассивПакетовЭД);
	
	ОповещениеПослеУстановкиСоединения = Новый ОписаниеОповещения(
		"ОтправитьПакетыПослеУстановкиСоединенияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	УстановитьСоединениеЧерезВК(ОповещениеПослеУстановкиСоединения, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

#КонецОбласти

#Область Fintech

Процедура ПровестиТестНастройкиFintech(Оповещение, НастройкаОбмена, ПараметрыОбмена, ВыводитьОкноОжидания)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеТеста", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ВыводитьОкноОжидания", ВыводитьОкноОжидания);
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПробногоПолученияВыпискиFintech(НастройкаОбмена);
	
	Если Результат.Свойство("ТребуетсяАутентификация") Тогда
		ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
			"ПровестиТестНастройкиПослеАутентификацииFintech", ЭтотОбъект, ДополнительныеПараметры);
		ВыполнитьАутентификациюFintech(ОповещениеПослеАутентификации, НастройкаОбмена, ПараметрыОбмена.Организация,
			ПараметрыОбмена.Банк, ,"Тест", ВыводитьОкноОжидания);
	Иначе
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
		ПослеОтправкиДокументовПоСчетуFintech = Новый ОписаниеОповещения(
			"ПослеПробногоПолученияВыпискиFintech", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ПослеОтправкиДокументовПоСчетуFintech, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПровестиТестНастройкиПослеАутентификацииFintech(Успех, ДополнительныеПараметры) Экспорт
	
	Если Успех Тогда
		Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПробногоПолученияВыпискиFintech(
			ДополнительныеПараметры.НастройкаОбмена);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = ДополнительныеПараметры.ВыводитьОкноОжидания;
		ПослеОтправкиДокументовПоСчетуFintech = Новый ОписаниеОповещения(
			"ПослеПробногоПолученияВыпискиFintech", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ПослеОтправкиДокументовПоСчетуFintech, ПараметрыОжидания);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТеста, Ложь);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры


Процедура ПослеПробногоПолученияВыпискиFintech(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТеста, Ложь);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Пробное получение выписки через сервис Fintech'");
		ОбработатьОшибку(ВидОперации, РезультатЗадания.ПодробноеПредставлениеОшибки,
			РезультатЗадания.КраткоеПредставлениеОшибки, ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТеста, Ложь);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТеста, РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияВыпискиFintech(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		СтруктураВозврата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияВыписки, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Получение выписки через сервис Fintech'");
		ОбработатьОшибку(ВидОперации, РезультатЗадания.ПодробноеПредставлениеОшибки,
			РезультатЗадания.КраткоеПредставлениеОшибки, ДополнительныеПараметры.НастройкаОбмена);
		СтруктураВозврата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияВыписки, СтруктураВозврата);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		Если РезультатОперации.Свойство("ТребуетсяАутентификация") Тогда
			ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
				"ПолучитьВыпискуПослеАутентификацииFintechСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ВыполнитьАутентификациюFintech(ОповещениеПослеАутентификации, ДополнительныеПараметры.НастройкаОбмена,
				ДополнительныеПараметры.Организация, ДополнительныеПараметры.Банк, ДополнительныеПараметры.НомерСчета);
		Иначе
			СтруктураВозврата = Новый Структура("Успех", Истина);
			СтруктураВозврата.Вставить("Выписки", РезультатОперации.Выписки);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияВыписки, СтруктураВозврата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьПлатежныеДокументыЧерезFintech(ДанныеДляОтправки, ДополнительныеПараметры)
	
	Для Каждого КлючЗначение Из ДанныеДляОтправки Цикл
		Если ЗначениеЗаполнено(КлючЗначение.Значение.СчетаТребующиеАутентификации) Тогда
			ДополнительныеПараметры.Вставить("ДанныеДляОтправкиЧерезFintech", ДанныеДляОтправки);
			ДополнительныеПараметры.Вставить("ТекущаяНастройкаОбмена", КлючЗначение.Ключ);
			Оповещение = Новый ОписаниеОповещения(
				"ОтправитьДокументыПоСчетуПослеАутентификацииFintech", ЭтотОбъект, ДополнительныеПараметры);
			НомерСчета = КлючЗначение.Значение.СчетаТребующиеАутентификации.Получить(0);
			ВыполнитьАутентификациюFintech(
				Оповещение, КлючЗначение.Ключ, Неопределено, Неопределено, НомерСчета, , Истина);
			Возврат;
		Иначе
			ДанныеДляОтправки.Удалить(КлючЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	ОтправкаСообщенийОбмена(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОтправитьДокументыПоСчетуПослеАутентификацииFintech(Успех, ДополнительныеПараметры) Экспорт
	
	Если Успех Тогда
		НастройкаОбмена = ДополнительныеПараметры.ТекущаяНастройкаОбмена;
		ДанныеДляОтправки = ДополнительныеПараметры.ДанныеДляОтправкиЧерезFintech.Получить(НастройкаОбмена);
		РезультатВыполнения = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияОтправкиДокументовПоСчетуFintech(
			НастройкаОбмена, ДанныеДляОтправки);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПослеОтправкиДокументовПоСчетуFintech = Новый ОписаниеОповещения(
			"ПослеОтправкиДокументовПоСчетуFintech", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатВыполнения, ПослеОтправкиДокументовПоСчетуFintech, ПараметрыОжидания);
	Иначе
		ДополнительныеПараметры.ДанныеДляОтправкиЧерезFintech.Удалить(ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ОтправкаСообщенийОбмена(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеОтправкиДокументовПоСчетуFintech(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // отменено фоновое задание
		ДополнительныеПараметры.ДанныеДляОтправкиЧерезFintech.Удалить(ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ОтправитьПлатежныеДокументыЧерезFintech(
			ДополнительныеПараметры.ДанныеДляОтправкиЧерезFintech, ДополнительныеПараметры);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.ОтправленныеСообщенияОбмена) Тогда
			Оповестить("ОбновитьСостояниеОбменСБанками");
			ДополнительныеПараметры.КоличествоОтправленных = ДополнительныеПараметры.КоличествоОтправленных
														+ РезультатОперации.ОтправленныеСообщенияОбмена.Количество();
		КонецЕсли;
		Если ЗначениеЗаполнено(РезультатОперации.НеотправленныеСообщенияОбмена) Тогда
			ДополнительныеПараметры.ДанныеДляОтправкиЧерезFintech.Вставить(
				ДополнительныеПараметры.ТекущаяНастройкаОбмена, РезультатОперации);
		Иначе
			ДополнительныеПараметры.ДанныеДляОтправкиЧерезFintech.Удалить(
				ДополнительныеПараметры.ТекущаяНастройкаОбмена);
			Если РезультатОперации.ПоказыватьОкноПодтвержденияПлатежей Тогда
				ДополнительныеПараметры.ПодтверждениеПлатежейВБанке.БанкиТребующиеПодтверждениеПлатежаВЛК.Добавить(
					РезультатОперации.Банк);
				ДополнительныеПараметры.ПодтверждениеПлатежейВБанке.НастройкиОбмена.Добавить(
					ДополнительныеПараметры.ТекущаяНастройкаОбмена);
			КонецЕсли;
		КонецЕсли;
		ОтправитьПлатежныеДокументыЧерезFintech(
			ДополнительныеПараметры.ДанныеДляОтправкиЧерезFintech, ДополнительныеПараметры);
	Иначе // Ошибка
		ВидОперации = НСтр("ru = 'Отправка документов через сервис Fintech API.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации,
			Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки, "ОбменСБанками",
			ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ДополнительныеПараметры.ДанныеДляОтправкиЧерезFintech.Удалить(ДополнительныеПараметры.ТекущаяНастройкаОбмена);
		ОтправитьПлатежныеДокументыЧерезFintech(
			ДополнительныеПараметры.ДанныеДляОтправкиЧерезFintech, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьПодключениеКFintechПослеПодключенияИППСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Возврат;
	КонецЕсли;
	
	РезультатВыполнения = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПолученияСсылкиНаАутентификациюFintechСбербанк(
		ДополнительныеПараметры.НастройкаОбмена);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = ДополнительныеПараметры.ВыводитьОкноОжидания;
	ПослеПолученияСсылкиFintechСбербанк = Новый ОписаниеОповещения(
		"ПослеПолучениеСсылкиНаАутентификациюFintechСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения, ПослеПолученияСсылкиFintechСбербанк, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ПослеПолучениеСсылкиНаАутентификациюFintechСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		РезультатПодключения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("URL", РезультатПодключения.authorizationUrl);
		ПараметрыФормы.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
		ПараметрыФормы.Вставить("ТокенДоступа", РезультатПодключения.ТокенДоступа);
		ПараметрыФормы.Вставить("Цель", ДополнительныеПараметры.Цель);
		ПараметрыФормы.Вставить("НомерСчета", ДополнительныеПараметры.НомерСчета);
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПодписанияДоговораОфертыСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Справочник.НастройкиОбменСБанками.Форма.АутентификацияСбербанк", ПараметрыФормы, ЭтотОбъект, , , ,
			Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе // Ошибка
		ВидОперации = НСтр("ru = 'Подключение к сервису 1С:ДиректБанк.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации,
			Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки, "ОбменСБанками",
			ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
	КонецЕсли;

КонецПроцедуры

Процедура ПослеПодписанияДоговораОфертыСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		Если Результат.Успех Тогда
			РезультатВыполнения = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПроверкиНаличияДоступаКДаннымFintechСбербанк(
				ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.Организация,
				ДополнительныеПараметры.Банк, ДополнительныеПараметры.НомерСчета);
	
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
			ПараметрыОжидания.ВыводитьОкноОжидания = ДополнительныеПараметры.ВыводитьОкноОжидания;
			ПроверкиРеквизитовFintechСбербанк = Новый ОписаниеОповещения(
				"ПослеПроверкиДоступаКДаннымFintechСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(
				РезультатВыполнения, ПроверкиРеквизитовFintechСбербанк, ПараметрыОжидания);

		Иначе
			Если ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстОшибки);
			КонецЕсли;
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		КонецЕсли;
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПроверкиДоступаКДаннымFintechСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если НЕ РезультатОперации.Успех Тогда
			Если ЗначениеЗаполнено(ДополнительныеПараметры.НомерСчета) Тогда
				ШаблонСообщения = НСтр("ru = 'Для данной учетной записи отсутствует доступ к счету %1.
											|Убедитесь в правильном указании логина, проверьте номер счета.
											|Если проблема повторяется, обратитесь в техническую поддержку.'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, ДополнительныеПараметры.НомерСчета);
			ИначеЕсли НЕ ЗначениеЗаполнено(РезультатОперации.ДоступныеСчета) Тогда
				ТекстСообщения = НСтр("ru = 'Для данной учетной записи нет доступа к банковским счетам.
							|Убедитесь в правильном указании логина, проверьте доступ к счетам в личном кабинете банка.
							|Если проблема повторяется, обратитесь в техническую поддержку.'");
			Иначе
				ТекстСообщения = НСтр("ru = 'Для данной учетной записи отсутствует доступ к запрашиваемым данным.
								|Убедитесь в правильном указании логина, проверьте реквизиты организации и БИК банка.
								|Если проблема повторяется, обратитесь в техническую поддержку.'");
			КонецЕсли;
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
		Иначе
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьИнформациюОДоступныхСчетахВПараметрахСеанса(
				ДополнительныеПараметры.НастройкаОбмена, РезультатОперации.ДоступныеСчета);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Истина); 
		КонецЕсли;
	Иначе // Ошибка
		ВидОперации = НСтр("ru = 'Проверка доступа к данным.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации,
			Результат.ПодробноеПредставлениеОшибки, Результат.КраткоеПредставлениеОшибки, "ОбменСБанками",
			ДополнительныеПараметры.НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ОбработатьОшибку(
	ВидОперации,
	Знач ПодробныйТекстОшибки,
	Знач ТекстСообщения = "",
	СсылкаНаОбъект = Неопределено)
	
	ПодробныйТекстОшибки = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(ПодробныйТекстОшибки);
	ТекстСообщения = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(ТекстСообщения);
	
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
		ВидОперации, ПодробныйТекстОшибки, ТекстСообщения, "ОбменСБанками", СсылкаНаОбъект);
	
КонецПроцедуры

Процедура ПослеОтправкиПакетовВАльфаБанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ДополнительныеПараметры.КоличествоПодготовленных = ДополнительныеПараметры.КоличествоПодготовленных + Результат.КоличествоПодготовленных;
		ДополнительныеПараметры.КоличествоОтправленных = ДополнительныеПараметры.КоличествоОтправленных + Результат.КоличествоОтправленных;
	КонецЕсли;
	ВыполнитьДействияПослеОтправки(ДополнительныеПараметры);
	
КонецПроцедуры

// Возвращает строку с содержимым сертификата в формате Base64.
//
// Параметры:
//  ДвоичныеДанныеСертификата  - ДвоичныеДанные - двоичные данные сертификата.
//
// Возвращаемое значение:
//   Строка   - Строка содержит данные сертификата в формате Base64.
//
Функция СертификатВФорматеBase64(ДвоичныеДанныеСертификата)
	
	СтрокаBase64 = Base64Строка(ДвоичныеДанныеСертификата);
	СтрокаBase64 = "-----BEGIN CERTIFICATE-----" + Символы.ПС + СтрокаBase64 + Символы.ПС + "-----END CERTIFICATE-----";

	Возврат СтрокаBase64;

КонецФункции // СертификатВФорматеBase64()

// Выводит сообщения, которые возникали в длительной операции
//
// Параметры:
//  * Сообщения - ФиксированныйМассив, Неопределено - массив объектов СообщениеПользователю,
//                сформированных в процедуре-обработчике длительной операции.
//
Процедура ВывестиСообщенияДлительнойОперации(Сообщения)
	
	Если Сообщения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Сообщение Из Сообщения Цикл
		Сообщение.Сообщить();
	КонецЦикла;
	
КонецПроцедуры

Функция ВозможноВыполнитьСинхронизацию(НастройкаОбмена, РеквизитыНастройкиОбмена, Оповещение)
		
	Если Не РеквизитыНастройкиОбмена.ПравоВыполненияОбмена Тогда
		ТекстСообщения = НСтр("ru = 'Недостаточно прав для выполнения обмена с банком.
									|Синхронизация отменена.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, НастройкаОбмена);
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
		Возврат Ложь;
	КонецЕсли;
		
	Если РеквизитыНастройкиОбмена.Недействительна Тогда
		ТекстСообщения = НСтр("ru = 'Настройка обмена с банком недействительна.
									|Синхронизация отменена.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, НастройкаОбмена);
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Добавляет произвольное значение в кеш клиента
//
// Параметры:
//  Название - Строка - название кешируемого параметра;
//  Значение - Произвольный - значение кешируемого параметра.
//
Процедура ДобавитьЗначениеВКеш(Название, Значение)
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) <> Тип("Соответствие") Тогда
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками", Новый Соответствие);
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	КонецЕсли;
	
	ПараметрыПодсистемыОбменСБанками.Вставить(Название, Значение);
	
КонецПроцедуры

// Возвращает закешированный параметр.
//
// Параметры:
//  НазваниеПараметра  - Строка - название параметра.
//
// Возвращаемое значение:
// Произвольный - значение параметра.
//
Функция ЗначениеИзКеша(НазваниеПараметра)
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие") Тогда
		Возврат ПараметрыПодсистемыОбменСБанками.Получить(НазваниеПараметра);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция МожноПолучитьMAC()
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	МинимальнаяВерсияПлатформыДляПолученияMAC = "8.3.18.0";
	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, МинимальнаяВерсияПлатформыДляПолученияMAC) > 0 Тогда
		Возврат Истина
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
