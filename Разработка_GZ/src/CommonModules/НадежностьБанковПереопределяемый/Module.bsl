#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьБИКБанковОрганизаций(МассивБИК) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация
	|ПОМЕСТИТЬ Организации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	БанковскиеСчета.Банк.Код КАК БИК
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Организации КАК Организации
	|		ПО БанковскиеСчета.Владелец = Организации.Организация
	|ГДЕ
	|	НЕ БанковскиеСчета.ПометкаУдаления
	|	И БанковскиеСчета.ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1)
	|	И БанковскиеСчета.Банк.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)";
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивБИК, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("БИК"));

КонецПроцедуры

Процедура ЗаполнитьДополнительнуюИнформациюОСобытии(ДанныеСобытия) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Рубли", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ЮридическоеЛицо", Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
	Запрос.УстановитьПараметр("БИКБанков", НадежностьБанков.КодыБанковПоИдентификаторуСобытия(
		ДанныеСобытия.Идентификатор));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Банки.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Банки
	|ИЗ
	|	Справочник.Банки КАК Банки
	|ГДЕ
	|	Банки.Код В(&БИКБанков)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БанковскиеСчета.Ссылка КАК БанковскийСчет,
	|	БанковскиеСчета.ВалютаДенежныхСредств КАК Валюта,
	|	ВЫРАЗИТЬ(БанковскиеСчета.Владелец КАК Справочник.Организации).ЮридическоеФизическоеЛицо = &ЮридическоеЛицо КАК ЭтоСчетЮридическогоЛица
	|ПОМЕСТИТЬ БанковскиеСчета
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Банки КАК Банки
	|		ПО БанковскиеСчета.Банк = Банки.Ссылка
	|ГДЕ
	|	БанковскиеСчета.Владелец ССЫЛКА Справочник.Организации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	БанковскийСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОстатки.Субконто1 КАК БанковскийСчет,
	|	&Рубли КАК Валюта,
	|	ХозрасчетныйОстатки.СуммаОстатокДт КАК Остаток
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(, НЕ Счет.Валютный, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета), ) КАК ХозрасчетныйОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.Валюта,
	|	ХозрасчетныйОстатки.ВалютнаяСуммаОстатокДт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(, Счет.Валютный, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета), ) КАК ХозрасчетныйОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	БанковскийСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскиеСчета.БанковскийСчет КАК БанковскийСчет,
	|	БанковскиеСчета.ЭтоСчетЮридическогоЛица КАК ЭтоСчетЮридическогоЛица,
	|	ЕСТЬNULL(Остатки.Валюта, БанковскиеСчета.Валюта) КАК Валюта,
	|	ЕСТЬNULL(Остатки.Остаток, 0) КАК Остаток
	|ИЗ
	|	БанковскиеСчета КАК БанковскиеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
	|		ПО БанковскиеСчета.БанковскийСчет = Остатки.БанковскийСчет";
	ТаблицаОстатки = Запрос.Выполнить().Выгрузить();
	
	ЕстьОстатки = ТаблицаОстатки.Итог("Остаток") > 0;
	ЕстьСчетаЮридическихЛиц = ТаблицаОстатки.Найти(Истина, "ЭтоСчетЮридическогоЛица") <> Неопределено;
	ЕстьСчетаФизическихЛиц = ТаблицаОстатки.Найти(Ложь, "ЭтоСчетЮридическогоЛица") <> Неопределено;
	
	ДанныеСобытия.ДополнительнаяИнформация.ЕстьОстатки = ЕстьОстатки;
	ДанныеСобытия.ДополнительнаяИнформация.ЕстьСчетаЮридическихЛиц = ЕстьСчетаЮридическихЛиц;
	ДанныеСобытия.ДополнительнаяИнформация.ЕстьСчетаФизическихЛиц = ЕстьСчетаФизическихЛиц;
	
	Если ЕстьОстатки Тогда
		ТаблицаОстатки.Свернуть("Валюта", "Остаток");
		ЧастиТекста = Новый Массив;
		Для каждого СтрокаОстаток Из ТаблицаОстатки Цикл
			ЧастиТекста.Добавить("" + СтрокаОстаток.Остаток + " " + СтрокаОстаток.Валюта);
		КонецЦикла;
		ТекстОстатки = СтрСоединить(ЧастиТекста, ", ");
		ДанныеСобытия.ТекстПояснения = СтрШаблон(НСтр("ru='На счетах в этом банке %1'"), ТекстОстатки);
	Иначе
		ДанныеСобытия.ТекстПояснения = НСтр("ru='На счетах в этом банке нет остатков.'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
