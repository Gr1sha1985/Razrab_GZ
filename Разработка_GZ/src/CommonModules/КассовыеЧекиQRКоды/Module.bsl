// Версия модуля kktService 1.0.1.39

// Приказ от 21.03.2017 № ММВ-7-20/229@
// "Об утверждении дополнительных реквизитов фискальных документов и форматов фискальных документов,
// обязательных к использованию"

#Область ПрограммныйИнтерфейс

// Десериализует данные QR-кода.
// Пример строки QR-кода: t=20150720T1638&s=9999999.00&fn=000110000105&i=12345678&fp=123456&n=2
//
// Параметры:
//  QRКод - Строка
// 
// Возвращаемое значение:
//  Структура - см. НовыйРеквизитыЧека
//
Функция РеквизитыЧека(QRКод) Экспорт
	
	РеквизитыЧека = НовыйРеквизитыЧека();
	ИменаПолей    = ИменаПолейQRКода();
	
	ПоляQRКода = СтрРазделить(QRКод, "&");
	ТипДата = Тип("Дата");
	
	Для Каждого СодержимоеПоля Из ПоляQRКода Цикл
		
		ЭлементыПоля = СтрРазделить(СодержимоеПоля, "=");
		Если ЭлементыПоля.Количество() <> 2 Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяПоля   = ЭлементыПоля[0];
		ТекстПоля = ЭлементыПоля[1];
		
		ИмяПоля = ИменаПолей[ИмяПоля];
		Если ИмяПоля = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭталонноеЗначение = РеквизитыЧека[ИмяПоля];
		ЭталонныйТип      = ТипЗнч(ЭталонноеЗначение);
		
		Если ЭталонныйТип = ТипДата Тогда
			Значение = ОбщегоНазначенияБП.ДесериализоватьДатуISO(ТекстПоля);
		Иначе
			ОписаниеТипов = Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭталонныйТип));
			Значение = ОписаниеТипов.ПривестиЗначение(ТекстПоля);
		КонецЕсли;
		
		РеквизитыЧека[ИмяПоля] = Значение;
		
	КонецЦикла;
	
	Возврат РеквизитыЧека;
	
КонецФункции

// Проверяет заполненность реквизитов чека
//
// Параметры:
//  РеквизитыЧека - Структура - см. НовыйРеквизитыЧека
// 
// Возвращаемое значение:
//  Булево
//
Функция РеквизитыЧекаЗаполнены(РеквизитыЧека) Экспорт
	
	Возврат ЗначениеЗаполнено(РеквизитыЧека.Дата) И ЗначениеЗаполнено(РеквизитыЧека.Номер);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Конструктор коллекции, содержащей реквизиты, помещаемые в строку QR-кода кассового чека
// 
// Возвращаемое значение:
//  Структура
//
Функция НовыйРеквизитыЧека()
	
	ПоляQRКода = Новый Структура;
	ПоляQRКода.Вставить("Дата",          '0001-01-01');
	ПоляQRКода.Вставить("Сумма",         0);
	ПоляQRКода.Вставить("КодУстройства", "");
	ПоляQRКода.Вставить("Номер",         "");
	ПоляQRКода.Вставить("КодПроверки",   "");
	ПоляQRКода.Вставить("ТипОперации",   "");
	
	Возврат ПоляQRКода;
	
КонецФункции

// Имена полей в строке QR-кода кассового чека.
// 
// Возвращаемое значение:
//  Структура - Ключ - имя поля в QR-коде на чеке, Значение - имя поля на естественном языке
//
Функция ИменаПолейQRКода()
	
	// Пример строки QR-кода: t=20150720T1638&s=9999999.00&fn=000110000105&i=12345678&fp=123456&n=2
	
	ПоляQRКода = Новый Соответствие;
	ПоляQRКода.Вставить("t",  "Дата");          // date/time - дата и время осуществления расчета в формате ГГГГММДДТЧЧММ
	ПоляQRКода.Вставить("s",  "Сумма");         // сумма расчета в рублях и копейках, разделенных точкой
	ПоляQRКода.Вставить("fn", "КодУстройства"); // заводской номер фискального накопителя
	ПоляQRКода.Вставить("i",  "Номер");         // порядковый номер фискального документа, нулями не дополняется
	ПоляQRКода.Вставить("fp", "КодПроверки");   // фискальный признак документа, нулями не дополняется
	ПоляQRКода.Вставить("n",  "ТипОперации");   // признак расчета
	
	Возврат ПоляQRКода;
	
КонецФункции

#КонецОбласти
