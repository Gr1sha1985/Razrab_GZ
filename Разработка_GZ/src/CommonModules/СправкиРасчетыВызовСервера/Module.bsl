#Область СлужебныйПрограммныйИнтерфейс

// Готовит описание налогового периода по налогу на прибыль, используемое в алгоритмах справок-расчетов.
// Предполагается, что справка-расчет формируется за целый отчетный период (с даты начала налогового периода).
//
// Параметры:
//  Период		 - Дата - конец периода отчета
//  Организация	 - СправочникСсылка.Организации - организация, по которой строится отчет
//  ИмяОтчета    - Строка - имя отчета
// 
// Возвращаемое значение:
//  Структура - описание периода. Содержит ключи
//      * НачалоПериода - Дата - начало налогового периода.
//                        Может содержать пустую дату, если дату начала периода определить нельзя.
//      * ПредставлениеПериода - Строка - представление отчетного периода
//      * ИнформацияНалоговыйПериод - Структура - см. ИнформированиеНалоговыйПериодКлиентСервер.НовыйИнформация
//                                                тексты информации для пользователя об особенностях налогового периода
//                                  - Неопределено - нет особенностей налогового периода
//
Функция ОписаниеНалоговогоПериода(Знач Период, Знач Организация, Знач ИмяОтчета) Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("Период",                    НачалоГода(Период));
	Описание.Вставить("НачалоПериода",             НачалоГода(Период));
	Описание.Вставить("ПредставлениеПериода",      "");
	Описание.Вставить("ИнформацияНалоговыйПериод", Неопределено);
	
	ВариантНалоговогоПериода = СправкиРасчеты.ВариантРасширенногоПервогоНалоговогоПериода(ИмяОтчета);
	
	Если НалоговыйУчет.ПрименяютсяПравилаРасширенногоНалоговогоПериода(ВариантНалоговогоПериода) Тогда
		
		Если Не УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Период) Тогда
			Возврат ДополнитьОписаниеНалоговогоПериодаПредставлением(Описание, Период);
		КонецЕсли;
		
		НалоговыйПериод = НалоговыйУчет.БлижайшийНалоговыйПериод(Период, Организация);
		
	ИначеЕсли БухгалтерскийУчет.ПрименяютсяПравилаРасширенногоНалоговогоПериода(ВариантНалоговогоПериода) Тогда
		
		НалоговыйПериод = БухгалтерскийУчет.БлижайшийОтчетныйПериод(Период, Организация);
		
	Иначе
		Возврат ДополнитьОписаниеНалоговогоПериодаПредставлением(Описание, Период);
	КонецЕсли;
	
	Если Период < НалоговыйПериод.Начало Тогда
		
		Возврат Описание;
		
	КонецЕсли;
	
	// Поскольку выбран интервал целиком, заданный в терминах отчетных периодов ("Год", "Полугодие"),
	// то определим начало интервала так, чтобы совпадало с границей отчетного периода
	Описание.НачалоПериода = НалоговыйПериод.Начало;
	Описание.Период        = НалоговыйПериод.Период;
		
	Описание.ПредставлениеПериода = ИнформированиеНалоговыйПериодКлиентСервер.ПредставлениеНалоговогоПериода(
		Описание.НачалоПериода,
		Период,
		НалоговыйПериод);
	
	Описание.ИнформацияНалоговыйПериод = НалоговыйУчет.ИнформацияНалоговыйПериод(Период, Организация, НалоговыйПериод);
		
	Возврат Описание;
	
КонецФункции

Функция ДоступныеПользователюНаборыСуммовыхПоказателей(ИмяОтчета, НачалоПериода, КонецПериода, Организация, НаборПоказателейЗависитОтКонтекста) Экспорт
	
	ДоступныеНаборы = СправкиРасчеты.ДоступныеПользователюНаборыСуммовыхПоказателей(
		ИмяОтчета,
		НачалоПериода,
		КонецПериода,
		Организация);
	
	Если НаборПоказателейЗависитОтКонтекста = Неопределено Тогда
		Если ДоступныеНаборы <> Неопределено Тогда
			ВидИспользованияСумм = БухгалтерскийУчетВызовСервераПовтИсп.ПользователюДоступныСуммыНалогНаПрибыль();
			НаборПоказателейЗависитОтКонтекста = БухгалтерскийУчетКлиентСервер.ИспользоватьСуммуНУ(ВидИспользованияСумм);
		Иначе
			НаборПоказателейЗависитОтКонтекста = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДоступныеНаборы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДополнитьОписаниеНалоговогоПериодаПредставлением(Описание, Период)
	
	Описание.ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		Описание.НачалоПериода,
		НачалоДня(КонецМесяца(Период)),
		Истина);
		
	Возврат Описание;
	
КонецФункции

#КонецОбласти