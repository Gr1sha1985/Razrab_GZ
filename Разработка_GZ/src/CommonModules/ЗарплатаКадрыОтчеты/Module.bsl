////////////////////////////////////////////////////////////////////////////////
// ЗарплатаКадрыОтчеты: Методы, используемые для работы отчетов.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.ПриОпределенииНастроек.
Процедура ПриОпределенииНастроек(Настройки) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КонфигурацииЗарплатаКадры") Тогда
		Настройки.ВыводитьОбщиеНастройкиКолонтитулов = Истина;
		Настройки.ВыводитьИндивидуальныеНастройкиКолонтитулов = Истина;
	КонецЕсли;
	
КонецПроцедуры

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	ЗарплатаКадрыОтчетыВнутренний.НастроитьВариантыОтчетов(Настройки);
	ЗарплатаКадрыОтчетыПереопределяемый.НастроитьВариантыОтчетов(Настройки);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

// Находит пользовательскую настройку по имени параметра.
//   Если пользовательская настройка не найдена (например,
//   если параметр не выведен в пользовательские настройки),
//   то получает общую настройку параметра.
//
// Параметры:
//   КомпоновщикНастроекКД - КомпоновщикНастроекКомпоновкиДанных - Компоновщик настроек.
//   ИмяПараметра          - Строка - Имя параметра.
//
// Возвращаемое значение:
//   ЗначениеПараметраНастроекКомпоновкиДанных - Пользовательская настройка параметра.
//   ЗначениеПараметраКомпоновкиДанных - Общая настройка параметра.
//   Неопределено - Если параметр не найден.
//
Функция НайтиПараметр(КомпоновщикНастроекКД, ИмяПараметра) Экспорт
	ПараметрКД = Новый ПараметрКомпоновкиДанных(ИмяПараметра);
	
	Для Каждого ПользовательскаяНастройка Из КомпоновщикНастроекКД.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ПользовательскаяНастройка) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")
			И ПользовательскаяНастройка.Параметр = ПараметрКД Тогда
			Возврат ПользовательскаяНастройка;
		КонецЕсли;
	КонецЦикла;
	
	Возврат КомпоновщикНастроекКД.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКД);
КонецФункции

// Добавляет элемент отбора, предварительно проверив доступность поля отбора.
//
// Параметры:
//		Отбор - ОтборКомпоновкиДанных
//		ИмяПоля - Строка
//		ВидСравнения - Системное перечисление ВидСравненияКомпоновкиДанных.
//		ПравоеЗначение - любое значение.
//
Процедура ДобавитьЭлементОтбора(Отбор, ИмяПоля, ВидСравнения, ПравоеЗначение) Экспорт
	
	Если Отбор.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных(ИмяПоля)) <> Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			Отбор, ИмяПоля, ВидСравнения, ПравоеЗначение);
			
	КонецЕсли; 
	
КонецПроцедуры

Функция КлючВарианта(КомпоновщикНастроекИлиНастройки) Экспорт
	Если ТипЗнч(КомпоновщикНастроекИлиНастройки) = Тип("НастройкиКомпоновкиДанных") Тогда
		НастройкиОтчета = КомпоновщикНастроекИлиНастройки;
	Иначе
		НастройкиОтчета = КомпоновщикНастроекИлиНастройки.ПолучитьНастройки();
	КонецЕсли;
	
	ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("КлючВарианта"));
	Если ЗначениеПараметра <> Неопределено
		И ЗначениеПараметра.Использование
		И ЗначениеЗаполнено(ЗначениеПараметра.Значение) Тогда
		Возврат ЗначениеПараметра.Значение;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Функция ЭтоКлючВариантаОтчетаРасчетныйЛисток(КлючВарианта) Экспорт
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.ЭтоКлючВариантаОтчетаРасчетныйЛисток(КлючВарианта);
	
КонецФункции

// Выводит в табличный документ области макета с заполненными параметрами коллекций данных.
//
// Параметры:
//		ДокументРезультат	- ТабличныйДокумент
//		Макет				- ТабличныйДокумент
//		ИменаОбластейМакета	- Строка, имена областей макета, перечисленные через запятую
//		Данные, ... Данные9	-Структура, Неопределено содержат значения параметров областей, выводимого макета.
//
Процедура ВывестиВДокументРезультатОбластиМакета(ДокументРезультат, Макет, ИменаОбластейМакета, Данные, 
	Данные1 = Неопределено, Данные2 = Неопределено, Данные3 = Неопределено, Данные4 = Неопределено, 
	Данные5 = Неопределено, Данные6 = Неопределено, Данные7 = Неопределено, Данные8 = Неопределено, 
	Данные9 = Неопределено) Экспорт
	
	ИменаОбластей = СтрРазделить(ИменаОбластейМакета, ",");
	Для Каждого ИмяОбласти Из ИменаОбластей Цикл
		
		Область = Макет.ПолучитьОбласть(ИмяОбласти);
		ЗаполнитьПараметрыОбластиМакета(Область, Данные, 
			Данные1, Данные2, Данные3, Данные4, 
			Данные5, Данные6, Данные7, Данные8, 
			Данные9);
		
		ДокументРезультат.Вывести(Область);
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет параметры области табличного документа.
//
// Параметры:
//		Область				- ОбластьЯчеекТабличногоДокумента
//		Данные, ... Данные9	-Структура, Неопределено содержат значения параметров областей, выводимого макета.
//
Процедура ЗаполнитьПараметрыОбластиМакета(Область, Данные, 
	Данные1 = Неопределено, Данные2 = Неопределено, Данные3 = Неопределено, Данные4 = Неопределено, 
	Данные5 = Неопределено, Данные6 = Неопределено, Данные7 = Неопределено, Данные8 = Неопределено, 
	Данные9 = Неопределено) Экспорт
	
	Область.Параметры.Заполнить(Данные);
	
	Если Данные1 <> Неопределено Тогда
		Область.Параметры.Заполнить(Данные1);
	КонецЕсли;
	
	Если Данные2 <> Неопределено Тогда
		Область.Параметры.Заполнить(Данные2);
	КонецЕсли;
	
	Если Данные3 <> Неопределено Тогда
		Область.Параметры.Заполнить(Данные3);
	КонецЕсли;
	
	Если Данные4 <> Неопределено Тогда
		Область.Параметры.Заполнить(Данные4);
	КонецЕсли;
	
	Если Данные5 <> Неопределено Тогда
		Область.Параметры.Заполнить(Данные5);
	КонецЕсли;
	
	Если Данные6 <> Неопределено Тогда
		Область.Параметры.Заполнить(Данные6);
	КонецЕсли;
	
	Если Данные7 <> Неопределено Тогда
		Область.Параметры.Заполнить(Данные7);
	КонецЕсли;
	
	Если Данные8 <> Неопределено Тогда
		Область.Параметры.Заполнить(Данные8);
	КонецЕсли;
	
	Если Данные9 <> Неопределено Тогда
		Область.Параметры.Заполнить(Данные9);
	КонецЕсли;
	
КонецПроцедуры

Функция ТабельныйНомерНаПечать(Знач ТабельныйНомер) Экспорт
	
	Возврат ПрефиксацияОбъектовКлиентСервер.УдалитьПрефиксыИзНомераОбъекта(ТабельныйНомер, Истина, Истина);
	
КонецФункции

Функция НомерНаПечать(Знач Номер, Знач НомерПриказа = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(НомерПриказа) Тогда
		Возврат СокрЛП(НомерПриказа);
	КонецЕсли;
	
	НастройкиПечатныхФорм = ЗарплатаКадры.НастройкиПечатныхФорм();
	
	Если НастройкиПечатныхФорм.УдалятьПрефиксыОрганизацииИИБИзНомеровКадровыхПриказов Тогда
		Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Номер, Истина, Истина);
	КонецЕсли;
	
	Возврат Номер;
	
КонецФункции

Функция ПодразделениеНаПечать(Знач Подразделение) Экспорт
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		
		НастройкиПечатныхФорм = ЗарплатаКадры.НастройкиПечатныхФорм();
		Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений Тогда
			Возврат Подразделение.ПолноеНаименование();
		Иначе
			Возврат Подразделение;
		КонецЕсли;
		
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция ТарифнаяСтавкаНаПечать(Знач ТарифнаяСтавка, Группировка = Ложь) Экспорт
	
	Если ЗначениеЗаполнено(ТарифнаяСтавка) Тогда
		
		ТарифнаяСтавка = Окр(ТарифнаяСтавка, 2);
		
		ФорматнаяСтрока = "ЧДЦ=0; ЧН=";
		Если Не Группировка Тогда
			ФорматнаяСтрока = ФорматнаяСтрока + "; ЧГ=";
		КонецЕсли;
		
		Возврат Формат(Цел(ТарифнаяСтавка), ФорматнаяСтрока) + " " + НСтр("ru='руб'") + ". " + Формат((ТарифнаяСтавка - Цел(ТарифнаяСтавка)), "ЧЦ=2; ЧДЦ=0; ЧС=-2; ЧН=; ЧВН=") + " " + НСтр("ru='коп'") + ".";
		
	КонецЕсли;
	
	Возврат "_______________ " + НСтр("ru='руб'") + ". ____ " + НСтр("ru='коп'") + ".";
	
КонецФункции

Функция РазрядКатегорияНаПечать(РазрядКатегория) Экспорт
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.РазрядКатегорияНаПечать(РазрядКатегория);
	
КонецФункции

Функция МесяцНачисленияНаПечать(Знач МесяцНачисления) Экспорт
	
	Возврат ВРег(Формат(МесяцНачисления, "ДФ='ММММ гггг'"));
	
КонецФункции

Функция ПросклоненныеФИО(Знач ФИО, Падеж, Пол, СсылкаНаОбъект = Неопределено) Экспорт
	
	РезультатСклонения = "";
	Если ФизическиеЛицаЗарплатаКадры.Просклонять(ФИО, Падеж, РезультатСклонения, Пол, СсылкаНаОбъект) Тогда
		ФИО = РезультатСклонения;
	КонецЕсли;
	
	Возврат ФИО;
	
КонецФункции

Функция ФорматДатыЧислоВКавычкахМесяцПрописью(Знач ФорматируемаяДата, ВыводитьШаблонПустойДаты = Истина) Экспорт
	
	Если ФорматируемаяДата = NULL Тогда
		ФорматируемаяДата = '00010101';
	КонецЕсли;
	
	СтрокаФормата = "Л=ru_RU; ДФ='''""''дд''""'' ММММ гггг ''г.'''";
	Если ВыводитьШаблонПустойДаты Тогда
		СтрокаФормата = СтрокаФормата + "; ДП='""___"" ______________ 20____ г.'";
	КонецЕсли;
	
	Возврат Формат(ФорматируемаяДата, СтрокаФормата);
	
КонецФункции

Процедура ПередНачаломКомпоновкиРезультата(ОбъектОтчета) Экспорт
	
	ЗапомнитьЗапросыНаборовДанных(ОбъектОтчета.СхемаКомпоновкиДанных.НаборыДанных, ОбъектОтчета);
	
	ДополнительныеПоляПредставлений = ВыбранныеДополнительныеПоляПредставленийОтчета(ОбъектОтчета);
	
	УдаляемыеПоляНаборов = Неопределено;
	УдалитьВыведенныеПоляИзНаборовДанных(ОбъектОтчета.СхемаКомпоновкиДанных.НаборыДанных, ОбъектОтчета.ДополнительныеПоляПредставлений(), УдаляемыеПоляНаборов);
	
	УдаленныеПоляНаборов = Новый Соответствие;
	УдалитьПоляНаборов(ОбъектОтчета.СхемаКомпоновкиДанных.НаборыДанных, УдаляемыеПоляНаборов, УдаленныеПоляНаборов);
	ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("УдаленныеПоляНаборов", УдаленныеПоляНаборов);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ВывестиДополнительныеПоляПредставленийВОтчет(ОбъектОтчета, ДополнительныеПоляПредставлений);
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьПредставленияЗапросов(ОбъектОтчета.СхемаКомпоновкиДанных.НаборыДанных, ОбъектОтчета);
	
КонецПроцедуры

Процедура ПриЗавершенииКомпоновкиРезультата(ОбъектОтчета) Экспорт
	
	ВосстановитьЗапросыНаборовДанных(ОбъектОтчета.СхемаКомпоновкиДанных.НаборыДанных, ОбъектОтчета)
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультатаВТабличныйДокумент(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, 
	СтандартнаяОбработка, ВстраиватьДополнительныеПоляПредставлений = Истина) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если ВстраиватьДополнительныеПоляПредставлений Тогда
		ПередНачаломКомпоновкиРезультата(ОбъектОтчета);
	КонецЕсли;
	
	ДокументРезультат.Очистить();
	
	НастройкиОтчета = ОбъектОтчета.КомпоновщикНастроек.ПолучитьНастройки();
	
	МакетКомпоновки = МакетКомпоновкиДанных(ОбъектОтчета.СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	// Создадим и инициализируем процессор компоновки.
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	// Обозначим начало вывода
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	ДопСвойства = ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
	ДопСвойства.Вставить("ОтчетПустой", ОтчетыСервер.ОтчетПустой(ОбъектОтчета, ПроцессорКомпоновки));
	
	Если ВстраиватьДополнительныеПоляПредставлений Тогда
		ПриЗавершенииКомпоновкиРезультата(ОбъектОтчета);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПоОбразцуПараметрыСтраницы(ДокументРезультат, ДокументРезультатОбразец) Экспорт
	
	ЗаполнитьЗначенияСвойств(ДокументРезультат, ДокументРезультатОбразец, ИменаПараметровСтраницыТабличногоДокумента());
	
КонецПроцедуры

Функция ПараметрыСтраницыТабличногоДокумента(ДокументРезультат) Экспорт
	
	ПараметрыСтраницы = Новый Структура(ИменаПараметровСтраницыТабличногоДокумента());
	ЗаполнитьЗначенияСвойств(ПараметрыСтраницы, ДокументРезультат);
	
	Возврат ПараметрыСтраницы;
	
КонецФункции

Функция РезультатКомпоновкиМакетаПечатнойФормы(ОбъектОтчета, ДанныеРасшифровки = Неопределено, 
	НастройкиОтчета = Неопределено, ВнешниеНаборыДанных = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатКомпоновкиМакета = Новый Структура;
	КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
	
	// Заполняется коллекция идентификаторов пользовательских полей
	Если НастройкиОтчета = Неопределено Тогда
		НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	КонецЕсли;
	
	РезультатКомпоновкиМакета.Вставить("КлючВарианта", КлючВарианта(НастройкиОтчета));
	ДопСвойства = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.Свойство("Отбор") Тогда
			
			Для Каждого ОписаниеОтбора Из ДополнительныеПараметры.Отбор Цикл
				
				ЭлементОтбора = НастройкиОтчета.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				
				ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеОтбора.ЛевоеЗначение);
				ЭлементОтбора.ПравоеЗначение = ОписаниеОтбора.ПравоеЗначение;
				ЭлементОтбора.ВидСравнения = ОписаниеОтбора.ВидСравнения;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если ДополнительныеПараметры.Свойство("КлючВарианта") Тогда
			РезультатКомпоновкиМакета.Вставить("КлючВарианта", ДополнительныеПараметры.КлючВарианта);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДопСвойства.Свойство("ИспользуютсяПользовательскиеНастройкиПечати") Тогда
		НастроитьВыводПользовательскихПолейОтчета(НастройкиОтчета);
	КонецЕсли;
	
	ИдентификаторыМакета = СоответствиеПользовательскихПолей(НастройкиОтчета);
	РезультатКомпоновкиМакета.Вставить("ИдентификаторыМакета", ИдентификаторыМакета);
	
	// Определяется макет печатной формы
	МакетПечатнойФормы = Неопределено;
	ДопСвойства.Свойство("МакетПечатнойФормы", МакетПечатнойФормы);
	
	Если МакетПечатнойФормы = Неопределено Тогда
		
		МакетыВариантовОтчетов = МакетыВариантовОтчетовПечатныхФорм();
		ПолноеИмяОбъекта = ОбъектОтчета.Метаданные().ПолноеИмя();
		
		ПутьКМакету = МакетыВариантовОтчетов.Получить(ПолноеИмяОбъекта + "." + РезультатКомпоновкиМакета.КлючВарианта);
		МакетПечатнойФормы = УправлениеПечатью.МакетПечатнойФормы(ПутьКМакету);
		
	КонецЕсли;
	
	РезультатКомпоновкиМакета.Вставить("МакетПечатнойФормы", МакетПечатнойФормы);
	
	Если ДопСвойства.Свойство("МакетКомпоновкиДанных")
		И ДопСвойства.МакетКомпоновкиДанных <> Неопределено Тогда
		
		МакетКомпоновки = ДопСвойства.МакетКомпоновкиДанных;
		ДопСвойства.Удалить("МакетКомпоновкиДанных");
		
	Иначе
		
		МакетКомпоновки = МакетКомпоновкиДанныхДляКоллекцииЗначений(ОбъектОтчета.СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
		
	КонецЕсли;
	
	Если ДопСвойства.Свойство("ФормированиеМакетаКомпоновкиДанных") Тогда
		
		ДопСвойства.Вставить("СкомпонованныйМакетКомпоновкиДанных", МакетКомпоновки);
		РезультатКомпоновкиМакета.Вставить("ДанныеОтчета", Новый Массив);
		
		ДопСвойства.Вставить("ОтчетПустой", Истина);
		ДопСвойства.Удалить("ФормированиеМакетаКомпоновкиДанных");
		
	Иначе
		
		ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, , Истина);
		
		ДанныеОтчета = Новый ДеревоЗначений;
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
		
		ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
		
		ДопСвойства.Вставить("ОтчетПустой", ДанныеОтчета.Строки.Количество() = 0);
		
		РезультатКомпоновкиМакета.Вставить("ДанныеОтчета", ДанныеОтчета);
		
	КонецЕсли;
	
	РезультатКомпоновкиМакета.Вставить("ОтчетПустой", ДопСвойства.ОтчетПустой);
	
	Возврат РезультатКомпоновкиМакета;
	
КонецФункции

Функция СоответствиеПользовательскихПолей(НастройкиОтчета) Экспорт
	
	ЭлементыПользовательскихПолей = НастройкиОтчета.ПользовательскиеПоля.Элементы;
	
	СоответствиеПользовательскихПолей = Новый Соответствие;
	
	Для каждого Элемент Из ЭлементыПользовательскихПолей Цикл
		
		ИдентификаторМакета = ЗарплатаКадрыКлиентСервер.ИдентификаторМакетаПечатнойФормы(Элемент.Заголовок);
		СоответствиеПользовательскихПолей.Вставить(ИдентификаторМакета, СтрЗаменить(Элемент.ПутьКДанным,".",""));
		
	КонецЦикла;
	
	Возврат СоответствиеПользовательскихПолей;
	
КонецФункции

Функция ЗначенияЗаполненияПользовательскихПолей(ИдентификаторыМакета, ДанныеОтчета) Экспорт
	
	ЗначенияЗаполнения = Новый Структура;
	
	Если ИдентификаторыМакета <> Неопределено Тогда
		
		СтруктураДанных = Новый Структура;
		Для Каждого ОписаниеИдентификатораМакета Из ИдентификаторыМакета Цикл
			СтруктураДанных.Вставить(ОписаниеИдентификатораМакета.Значение);
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(СтруктураДанных, ДанныеОтчета);
		
		Для Каждого ОписаниеИдентификатораМакета Из ИдентификаторыМакета Цикл
			ЗначенияЗаполнения.Вставить(ОписаниеИдентификатораМакета.Ключ, СтруктураДанных[ОписаниеИдентификатораМакета.Значение]);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ЗначенияЗаполнения;
	
КонецФункции

Процедура ЗаполнитьПараметрыПользовательскихПолей(Макет, Данные, СоответствиеПользовательскихПолей, 
	ИменаЗаполняемыхПолей = "") Экспорт
	
	ЗаполняемыеПоля = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаЗаполняемыхПолей);
	
	СтруктураДанных = Новый Структура;
	Для каждого СоответствиеПользовательскогоПоля Из СоответствиеПользовательскихПолей Цикл
		
		Если ЗаполняемыеПоля.Количество() > 0
			И ЗаполняемыеПоля.Найти(СоответствиеПользовательскогоПоля.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		СтруктураДанных.Вставить(СоответствиеПользовательскогоПоля.Значение);
		
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(СтруктураДанных, Данные);
	
	СтруктураЗначенийПользовательскихПолей = Новый Структура;
	Для каждого СоответствиеПользовательскогоПоля Из СоответствиеПользовательскихПолей Цикл
		
		Если ЗаполняемыеПоля.Количество() > 0
			И ЗаполняемыеПоля.Найти(СоответствиеПользовательскогоПоля.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		СтруктураЗначенийПользовательскихПолей.Вставить(СоответствиеПользовательскогоПоля.Ключ, СтруктураДанных[СоответствиеПользовательскогоПоля.Значение]);
		
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Макет.Параметры, СтруктураЗначенийПользовательскихПолей);
	
КонецПроцедуры

Процедура ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс = Ложь) Экспорт
	
	ЗарплатаКадрыОтчетыВнутренний.ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс);
	
КонецПроцедуры

Функция МакетКомпоновкиДанных(Схема, Настройки, ДанныеРасшифровки = Неопределено, МакетОформления = Неопределено,
	ПроверятьДоступностьПолей = Истина, ПараметрыФункциональныхОпций = Неопределено, ТипГенератора = Неопределено) Экспорт
	
	Если ТипГенератора = Неопределено Тогда
		ТипГенератора = Тип("ГенераторМакетаКомпоновкиДанных");
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(Схема, Настройки, ДанныеРасшифровки, МакетОформления,
		ТипГенератора, ПроверятьДоступностьПолей, ПараметрыФункциональныхОпций);
	
	УточнитьОтборыЗапросовНаборовДанныхМакетаКомпоновкиДанных(МакетКомпоновкиДанных.НаборыДанных);
	
	Возврат МакетКомпоновкиДанных;
	
КонецФункции

Функция МакетКомпоновкиДанныхДляКоллекцииЗначений(Схема, Настройки, ДанныеРасшифровки = Неопределено, МакетОформления = Неопределено,
	ПроверятьДоступностьПолей = Истина, ПараметрыФункциональныхОпций = Неопределено) Экспорт
	
	Возврат МакетКомпоновкиДанных(Схема, Настройки, ДанныеРасшифровки, МакетОформления,
		ПроверятьДоступностьПолей, ПараметрыФункциональныхОпций, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ЕстьГруппировкаДетальныхЗаписей(Структура)
	
	ЕстьДетальныеЗаписи = Ложь;
	
	Для каждого ЭлементСтруктуры Из Структура Цикл
		
		Если Не ЭлементСтруктуры.Использование Тогда
			Прервать;
		КонецЕсли;
		
		Если ЭлементСтруктуры.ПоляГруппировки.Элементы.Количество() > 0 Тогда
			
			Если ЕстьГруппировкаДетальныхЗаписей(ЭлементСтруктуры.Структура) Тогда
				
				ЕстьДетальныеЗаписи = Истина;
				Прервать;
				
			КонецЕсли;
			
		Иначе
			
			ЕстьДетальныеЗаписи = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЕстьДетальныеЗаписи;
	
КонецФункции

Процедура НастроитьВыводПользовательскихПолейОтчета(НастройкиОтчета)
	
	ОбработанныеПоля = Новый Соответствие;
	Для Каждого ЭлементВыбора Из НастройкиОтчета.Выбор.Элементы Цикл
		
		Если Не ЭлементВыбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбработанныеПоля.Получить(ЭлементВыбора.Поле) = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		ДоступноеПоле = НастройкиОтчета.ДоступныеПоляВыбора.НайтиПоле(ЭлементВыбора.Поле);
		Если ДоступноеПоле = Неопределено Или ДоступноеПоле.Ресурс Тогда
			Продолжить;
		КонецЕсли;
		
		ПутьПоля = Строка(ЭлементВыбора.Поле);
		
		ПользовательскоеПоле = ПользовательскоеПоле(НастройкиОтчета, ПутьПоля);
		Если ПользовательскоеПоле <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПозицияСкобки = СтрНайти(ПутьПоля, "[");
		Если ПозицияСкобки = 0 Тогда
			СловаПутиПоля = СтрРазделить(ПутьПоля, ".");
		Иначе
			
			ПутьПоляБезСкобки = Лев(ПутьПоля, ПозицияСкобки - 2);
			СловаПутиПоля = СтрРазделить(ПутьПоляБезСкобки, ".");
			
		КонецЕсли;
		
		Если СловаПутиПоля.Количество() > 1 Тогда
			
			СловаПутиПоля.Удалить(СловаПутиПоля.Количество() - 1);
			ПутьРодителя = СтрСоединить(СловаПутиПоля, ".");
			
			Родитель = Новый ПолеКомпоновкиДанных(ПутьРодителя);
			
		Иначе
			Родитель = Неопределено;
		КонецЕсли;
		
		Если Не ВывестиВСтруктуру(НастройкиОтчета.Структура, ЭлементВыбора.Поле, Родитель) Тогда
			
			ПользовательскоеПоле = НастройкиОтчета.ПользовательскиеПоля.Элементы.Добавить(Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных"));
			ПользовательскоеПоле.Заголовок = ПутьПоля;
			
			ПользовательскоеПоле.УстановитьВыражениеДетальныхЗаписей(ПользовательскоеПоле.Заголовок);
			ПользовательскоеПоле.УстановитьВыражениеИтоговыхЗаписей(ПользовательскоеПоле.Заголовок);
			
			ВыбранноеПоле = Новый ПолеКомпоновкиДанных(ПользовательскоеПоле.ПутьКДанным);
			
			НовоеПоле = НастройкиОтчета.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			НовоеПоле.Поле = ВыбранноеПоле;
			
		КонецЕсли;
		
		ОбработанныеПоля.Вставить(ЭлементВыбора.Поле, Истина);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПользовательскоеПоле(НастройкиОтчета, ПутьКДанным)
	
	Поле = Неопределено;
	
	Для Каждого ЭлементКоллекции Из НастройкиОтчета.ПользовательскиеПоля.Элементы Цикл
		
		Если ПутьКДанным = ЭлементКоллекции.ПутьКДанным Тогда
			
			Поле = ЭлементКоллекции;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Поле;
	
КонецФункции

Функция ВывестиВСтруктуру(СтруктураНастроек, Поле, Родитель)
	
	ВыведеноВСтруктуру = Ложь;
	
	Для Каждого ЭлементСтруктуры Из СтруктураНастроек Цикл
		
		Если Не ЭлементСтруктуры.Использование Тогда
			Прервать;
		КонецЕсли;
		
		Для Каждого ЭлементВыбора Из ЭлементСтруктуры.Выбор.Элементы Цикл
			
			Если ТипЗнч(ЭлементВыбора) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			Если Поле = ЭлементВыбора.Поле Тогда
				ВыведеноВСтруктуру = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не ВыведеноВСтруктуру Тогда
			
			РодительВыведенВСтруктуру = Ложь;
			Для Каждого ЭлементГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
				
				Если ТипЗнч(ЭлементГруппировки) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
					Продолжить;
				КонецЕсли;
				
				Если Поле = ЭлементГруппировки.Поле Тогда
					ВыведеноВСтруктуру = Истина;
					Прервать;
				ИначеЕсли Родитель = ЭлементГруппировки.Поле Тогда
					РодительВыведенВСтруктуру = Истина;
				КонецЕсли;
				
			КонецЦикла;
			
			Если Не ВыведеноВСтруктуру И РодительВыведенВСтруктуру Тогда
				
				НовоеПоле = ЭлементСтруктуры.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				НовоеПоле.Поле = Поле;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ВыведеноВСтруктуру Тогда
			Прервать;
		ИначеЕсли ВывестиВСтруктуру(ЭлементСтруктуры.Структура, Поле, Родитель) Тогда
			
			ВыведеноВСтруктуру = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВыведеноВСтруктуру;
	
КонецФункции

Процедура ДобавитьГруппировкиДетальныхЗаписей(СтруктураНастроек, ГруппировкиДетальныхЗаписей)
	
	Для Каждого ЭлементСтруктуры Из СтруктураНастроек Цикл
		
		Если Не ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементСтруктуры.Структура.Количество() = 0 Тогда
			
			НоваяГруппировка = ЭлементСтруктуры.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
			ГруппировкиДетальныхЗаписей.Добавить(НоваяГруппировка);
			
		Иначе
			ДобавитьГруппировкиДетальныхЗаписей(ЭлементСтруктуры.Структура, ГруппировкиДетальныхЗаписей);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НастроитьВариантОтчетаРасчетныйЛисток(НастройкиОтчета)
	
	ЗарплатаКадрыОтчетыВнутренний.НастроитьВариантОтчетаРасчетныйЛисток(НастройкиОтчета);
	
КонецПроцедуры

Процедура ОтчетАнализНачисленийИУдержанийПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	ЗарплатаКадрыОтчетыВнутренний.ОтчетАнализНачисленийИУдержанийПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
	
КонецПроцедуры

Функция ОтчетРасчетныйЛисток() Экспорт
	
	ОтчетАнализНачисленийИУдержаний = Отчеты.АнализНачисленийИУдержаний.Создать();
	
	ОтчетАнализНачисленийИУдержаний.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("УстанавливаетсяМакетКомпоновкиДанных", Истина);
	ОтчетАнализНачисленийИУдержаний.ИнициализироватьОтчет();
	
	ОтчетАнализНачисленийИУдержаний.КомпоновщикНастроек.ЗагрузитьНастройки(
		ОтчетАнализНачисленийИУдержаний.СхемаКомпоновкиДанных.ВариантыНастроек.РасчетныйЛисток.Настройки);
	
	МакетКомпоновкиДанных = МакетКомпоновкиДанныхПоМетаданнымОтчета(Метаданные.Отчеты.АнализНачисленийИУдержаний, "РасчетныйЛисток");
	ОтчетАнализНачисленийИУдержаний.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("МакетКомпоновкиДанных", МакетКомпоновкиДанных);
	
	Возврат ОтчетАнализНачисленийИУдержаний;
	
КонецФункции

Функция ВыбранныеДополнительныеПоляПредставленийОтчета(ОбъектОтчета)
	
	ДополнительныеПоляПредставлений = Новый Структура;
	ПолученныеОписанияСтрокПутейКДанным = Новый Соответствие;
	
	ВыбранныеДополнительныеПоля = Новый Соответствие;
	
	ВыбранныеПоля = Новый Соответствие;
	Настройки = ОбъектОтчета.КомпоновщикНастроек.ПолучитьНастройки();
	
	ЗаполнитьВыбранныеПоляОтборовЭлементаСтруктурыОтчета(ВыбранныеПоля, Настройки.Отбор.Элементы);
	ЗаполнитьВыбранныеПоляПорядкаЭлементаСтруктурыОтчета(ВыбранныеПоля, Настройки.Порядок.Элементы);
	ЗаполнитьВыбранныеПоляУсловногоОформленияСтруктурыОтчета(ВыбранныеПоля, Настройки.УсловноеОформление.Элементы);
	ЗаполнитьВыбранныеПоляОтчетаЭлементаСтруктуры(Настройки.Структура, ВыбранныеПоля, ОбъектОтчета);
	
	ЗаполнитьВыбранныеПоляЭлементовВыбора(ВыбранныеПоля, Настройки.Выбор.Элементы);
	
	Если ВыбранныеПоля.Количество() > 0 Тогда
		
		ВыраженияПолей = ВыраженияПолейНаборовДанных(ОбъектОтчета.СхемаКомпоновкиДанных.НаборыДанных);
		ДополнительныеПоля = ОбъектОтчета.ДополнительныеПоляПредставлений();
		
		Для Каждого ВыбранноеПоле Из ВыбранныеПоля Цикл
			
			Для Каждого КоллекцияПолей Из ДополнительныеПоля Цикл
				
				СтрокиПутей = ПолученныеОписанияСтрокПутейКДанным.Получить(КоллекцияПолей.Ключ);
				Если СтрокиПутей = Неопределено Тогда
					
					СтрокиПутей = СтрокиПутейКДанным(КоллекцияПолей.Значение);
					ПолученныеОписанияСтрокПутейКДанным.Вставить(КоллекцияПолей.Ключ, СтрокиПутей);
					
				КонецЕсли;
				
				ПутьПоля = ВыбранноеПоле.Ключ;
				ЧислоТочек = СтрЧислоВхождений(ПутьПоля, ".");
				Если ЧислоТочек = 0 Тогда
					
					ИмяПоля = ПутьПоля;
					ОписаниеПолей = СтрокиПутей.Получить(ПутьПоля);
					
				Иначе
					
					Пока ЧислоТочек > 0 Цикл
						
						ИмяПоля = Сред(ПутьПоля, СтрНайти(ПутьПоля, ".", НаправлениеПоиска.СКонца) + 1);
						ОписаниеПолей = СтрокиПутей.Получить(ПутьПоля);
						
						Если ОписаниеПолей <> Неопределено Тогда
							Прервать;
						КонецЕсли;
						
						ПутьПоля = Лев(ВыбранноеПоле.Ключ, СтрНайти(ПутьПоля, ".", НаправлениеПоиска.СКонца) - 1);
						ЧислоТочек = СтрЧислоВхождений(ПутьПоля, ".");
						
					КонецЦикла;
					
				КонецЕсли;
				
				Если ОписаниеПолей <> Неопределено Тогда
					
					Если ВРег(ОписаниеПолей.ПустоеЗначениеНаЯзыкеЗапросов) = ВРег(ВыраженияПолей.Получить(ПутьПоля)) Тогда
						
						Если Не ДополнительныеПоляПредставлений.Свойство(КоллекцияПолей.Ключ) Тогда
							ДополнительныеПоляПредставлений.Вставить(КоллекцияПолей.Ключ, ЗарплатаКадрыОбщиеНаборыДанных.ПустаяТаблицаДополнительныхПолейПредставлений());
						КонецЕсли;
						
						Если ДополнительныеПоляПредставлений[КоллекцияПолей.Ключ].Найти(ОписаниеПолей.ПутьПоляСКД, "ПутьПоляСКД") = Неопределено Тогда
							ЗаполнитьЗначенияСвойств(ДополнительныеПоляПредставлений[КоллекцияПолей.Ключ].Добавить(), ОписаниеПолей);
						КонецЕсли;
					
						Прервать;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ДополнительныеПоляПредставлений;
	
КонецФункции

Функция ВыраженияПолейНаборовДанных(НаборыДанных, ВыраженияПолей = Неопределено)
	
	Если ВыраженияПолей = Неопределено Тогда
		ВыраженияПолей = Новый Соответствие;
	КонецЕсли;
	
	Для Каждого НаборДанных Из НаборыДанных Цикл
		
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
			ВыраженияПолейНаборовДанных(НаборДанных.Элементы, ВыраженияПолей);
		ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			
			ЗапросыНабора = СтрРазделить(НаборДанных.Запрос, ";");
			
			Схема = Новый СхемаЗапроса;
			Схема.УстановитьТекстЗапроса(ЗапросыНабора[ЗапросыНабора.Количество() - 1]);
			
			ПакетСхемы = Схема.ПакетЗапросов[0];
			Для Каждого ПолеНабора Из НаборДанных.Поля Цикл
				
				Если ТипЗнч(ПолеНабора) = Тип("ПапкаПолейНабораДанныхСхемыКомпоновкиДанных") Тогда
					Продолжить;
				КонецЕсли;
				
				Колонка = ПакетСхемы.Колонки.Найти(ПолеНабора.Поле);
				Если Колонка <> Неопределено Тогда
					
					КлючКоллекции = ПолеНабора.ПутьКДанным;
					Если Не ЗначениеЗаполнено(КлючКоллекции) Тогда
						КлючКоллекции = ПолеНабора.Поле;
					КонецЕсли;
					
					Если ВыраженияПолей.Получить(КлючКоллекции) = Неопределено Тогда
						ВыраженияПолей.Вставить(КлючКоллекции, Строка(Колонка.Поля[0]));
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВыраженияПолей;
	
КонецФункции

Процедура УдалитьПоляНаборов(НаборыДанных, УдаляемыеПоляНаборов, УдаленныеПоляНаборов)
	
	Для Каждого НаборДанных Из НаборыДанных Цикл
		
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
			УдалитьПоляНаборов(НаборДанных.Элементы, УдаляемыеПоляНаборов, УдаленныеПоляНаборов);
		КонецЕсли;
		
		Для Каждого ПутьПоляСКД Из УдаляемыеПоляНаборов Цикл
			
			Поле = НаборДанных.Поля.Найти(ПутьПоляСКД);
			Если Поле <> Неопределено Тогда
				ПоляНабора = УдаленныеПоляНаборов.Получить(НаборДанных.Имя);
				Если ПоляНабора = Неопределено Тогда
					ПоляНабора = Новый Массив;
					УдаленныеПоляНаборов.Вставить(НаборДанных.Имя, ПоляНабора);
				КонецЕсли;
				ПоляНабора.Добавить(Новый Структура("Заголовок,Поле,ПутьКДанным", Поле.Заголовок, Поле.Поле, Поле.ПутьКДанным));
				НаборДанных.Поля.Удалить(Поле);
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьВыведенныеПоляИзНаборовДанных(НаборыДанных, ДополнительныеПоляПредставлений, УдаляемыеПоляНаборов = Неопределено)
	
	Если УдаляемыеПоляНаборов = Неопределено Тогда
		УдаляемыеПоляНаборов = Новый Массив;
	КонецЕсли;
	
	Для Каждого НаборДанных Из НаборыДанных Цикл
		
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
			УдалитьВыведенныеПоляИзНаборовДанных(НаборДанных.Элементы, ДополнительныеПоляПредставлений, УдаляемыеПоляНаборов);
		ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			
			ЗапросыНабора = СтрРазделить(НаборДанных.Запрос, ";");
			Схема = Новый СхемаЗапроса;
			Схема.УстановитьТекстЗапроса(ЗапросыНабора[ЗапросыНабора.Количество() - 1]);
			
			ЗапросПакета = Схема.ПакетЗапросов[0];
			ОператорПакета = ЗапросПакета.Операторы[0];
			
			Для Каждого ОписанияПолей Из ДополнительныеПоляПредставлений Цикл
				
				Для Каждого СтрокаОписанияПоля Из ОписанияПолей.Значение Цикл
					
					КоллекцияПсевдонимов = Новый Соответствие;
					КоллекцияПутейСКДПсевдонимов = Новый Соответствие;
					Если СтрокаОписанияПоля.ПсевдонимыПолей.Количество() > 0 Тогда
						
						Для Каждого ОписаниеПсевдонимов Из СтрокаОписанияПоля.ПсевдонимыПолей Цикл
							КоллекцияПсевдонимов.Вставить(ОписаниеПсевдонимов.Значение.ПсевдонимПоля, СтрокаОписанияПоля.ПустоеЗначениеНаЯзыкеЗапросов);
							КоллекцияПутейСКДПсевдонимов.Вставить(ОписаниеПсевдонимов.Значение.ПсевдонимПоля, ОписаниеПсевдонимов.Значение.ПутьПоляСКД);
						КонецЦикла;
						
					Иначе
						КоллекцияПсевдонимов.Вставить(СтрокаОписанияПоля.ИмяПоля, СтрокаОписанияПоля.ПустоеЗначениеНаЯзыкеЗапросов);
						КоллекцияПутейСКДПсевдонимов.Вставить(СтрокаОписанияПоля.ИмяПоля, СтрокаОписанияПоля.ПутьПоляСКД);
					КонецЕсли;
					
					Для Каждого ПсевдонимПоля Из КоллекцияПсевдонимов Цикл
						
						Колонка = ЗапросПакета.Колонки.Найти(ПсевдонимПоля.Ключ);
						Если Колонка <> Неопределено И ВРег(Строка(Колонка.Поля[0])) = ВРег(ПсевдонимПоля.Значение) Тогда
							
							УдаляемыеПоляНаборов.Добавить(КоллекцияПутейСКДПсевдонимов.Получить(ПсевдонимПоля.Ключ));
							ЗапросПакета.Колонки.Удалить(ЗапросПакета.Колонки.Индекс(Колонка));
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
			
			ЗапросыНабора[ЗапросыНабора.Количество() - 1] = Схема.ПолучитьТекстЗапроса();
			НаборДанных.Запрос = СтрСоединить(ЗапросыНабора, ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов());
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СтрокиПутейКДанным(КоллекцияПолей)
	
	СтрокиПутей = Новый Соответствие;
	Для Каждого СтрокаКоллекции Из КоллекцияПолей Цикл
		
		Если Не ПустаяСтрока(СтрокаКоллекции.ПутьПоляСКД) Тогда
			СтрокиПутей.Вставить(СтрокаКоллекции.ПутьПоляСКД, СтрокаКоллекции);
		КонецЕсли;
		
		Для Каждого ОписаниеПсевдонима Из СтрокаКоллекции.ПсевдонимыПолей Цикл
			СтрокиПутей.Вставить(ОписаниеПсевдонима.Значение.ПутьПоляСКД, СтрокаКоллекции);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат СтрокиПутей;
	
КонецФункции

Процедура ЗаполнитьВыбранныеПоляЭлементовВыбора(ВыбранныеПоля, ЭлементыВыбора)
	
	Для Каждого ВыбранноеПоле Из ЭлементыВыбора Цикл
		
		Если ТипЗнч(ВыбранноеПоле) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ВыбранноеПоле) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			ЗаполнитьВыбранныеПоляЭлементовВыбора(ВыбранныеПоля, ВыбранноеПоле.Элементы)
		Иначе
			
			ПутьКДанным = Строка(ВыбранноеПоле.Поле);
			ВыбранныеПоля.Вставить(ПутьКДанным, Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВыбранныеПоляОтчетаЭлементаСтруктуры(Структура, ВыбранныеПоля, ОбъектОтчета)
	
	Для Каждого ЭлементСтруктуры Из Структура Цикл
		
		Если ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных")
			Или ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
			Или ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			
			ЗаполнитьВыбранныеПоляЭлементовВыбора(ВыбранныеПоля, ЭлементСтруктуры.Выбор.Элементы);
			
			Если ТипЗнч(ЭлементСтруктуры) <> Тип("ТаблицаКомпоновкиДанных") Тогда
				
				Для Каждого ПолеГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
					
					Если ТипЗнч(ПолеГруппировки) = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
						Продолжить;
					КонецЕсли;
					
					ПутьКДанным = Строка(ПолеГруппировки.Поле);
					ВыбранныеПоля.Вставить(ПутьКДанным, Истина);
					
				КонецЦикла;
				
				ЗаполнитьВыбранныеПоляОтборовЭлементаСтруктурыОтчета(ВыбранныеПоля, ЭлементСтруктуры.Отбор.Элементы);
				ЗаполнитьВыбранныеПоляПорядкаЭлементаСтруктурыОтчета(ВыбранныеПоля, ЭлементСтруктуры.Порядок.Элементы);
				
			КонецЕсли;
			
			ЗаполнитьВыбранныеПоляУсловногоОформленияСтруктурыОтчета(ВыбранныеПоля, ЭлементСтруктуры.УсловноеОформление.Элементы);
			
		КонецЕсли;
		
		Если ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			ЗаполнитьВыбранныеПоляОтчетаЭлементаСтруктуры(ЭлементСтруктуры.Строки, ВыбранныеПоля, ОбъектОтчета);
			ЗаполнитьВыбранныеПоляОтчетаЭлементаСтруктуры(ЭлементСтруктуры.Колонки, ВыбранныеПоля, ОбъектОтчета);
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных")
			Или ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
			ЗаполнитьВыбранныеПоляОтчетаЭлементаСтруктуры(ЭлементСтруктуры.Структура, ВыбранныеПоля, ОбъектОтчета);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВыбранныеПоляОтборовЭлементаСтруктурыОтчета(ВыбранныеПоля, ЭлементыОтбора)
	
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			ЗаполнитьВыбранныеПоляОтборовЭлементаСтруктурыОтчета(ВыбранныеПоля, ЭлементОтбора.Элементы);
		Иначе
			
			Если ЭлементОтбора.Использование Тогда
				
				Если ТипЗнч(ЭлементОтбора.ЛевоеЗначение) = Тип("ПолеКомпоновкиДанных") Тогда
					ПутьКДанным = Строка(ЭлементОтбора.ЛевоеЗначение);
					ВыбранныеПоля.Вставить(ПутьКДанным, Истина);
				КонецЕсли;
				
				Если ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("ПолеКомпоновкиДанных") Тогда
					ПутьКДанным = Строка(ЭлементОтбора.ПравоеЗначение);
					ВыбранныеПоля.Вставить(ПутьКДанным, Истина);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВыбранныеПоляПорядкаЭлементаСтруктурыОтчета(ВыбранныеПоля, ЭлементыПорядка)
	
	Для Каждого ЭлементПорядка Из ЭлементыПорядка Цикл
		
		Если ТипЗнч(ЭлементПорядка) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементПорядка.Использование Тогда
			
			Если ТипЗнч(ЭлементПорядка.Поле) = Тип("ПолеКомпоновкиДанных") Тогда
				ПутьКДанным = Строка(ЭлементПорядка.Поле);
				ВыбранныеПоля.Вставить(ПутьКДанным, Истина);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВыбранныеПоляУсловногоОформленияСтруктурыОтчета(ВыбранныеПоля, ЭлементыУсловногоОформления)
	
	Для Каждого ЭлементУсловногоОформления Из ЭлементыУсловногоОформления Цикл
		
		Если ЭлементУсловногоОформления.Использование Тогда
			
			Для Каждого ЭлементОформления Из ЭлементУсловногоОформления.Оформление.Элементы Цикл
				
				Если Не ЭлементОформления.Использование Тогда
					Продолжить;
				КонецЕсли;
				
				Если ЭлементОформления.Параметр <> Новый ПараметрКомпоновкиДанных("Текст") Тогда
					Продолжить;
				КонецЕсли;
				
				Если ТипЗнч(ЭлементОформления.Значение) = Тип("ПолеКомпоновкиДанных") Тогда
					ПутьКДанным = Строка(ЭлементОформления.Значение);
					ВыбранныеПоля.Вставить(ПутьКДанным, Истина);
				КонецЕсли;
				
			КонецЦикла;
			
			ЗаполнитьВыбранныеПоляОтборовЭлементаСтруктурыОтчета(ВыбранныеПоля, ЭлементУсловногоОформления.Отбор.Элементы);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#Область РаботаСМакетамиКомпоновкиДанных

Функция МакетКомпоновкиДанныхОтчета(ОтчетОбъект, КлючВарианта, ПодготовитьМакетЕслиНеНайден = Истина) Экспорт
	
	МетаданныеОтчета = ОтчетОбъект.Метаданные();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Отчет = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеОтчета);
	МакетКомпоновки = СохраненныйМакетКомпоновкиДанныхОтчета(Отчет, КлючВарианта);;
	
	Если МакетКомпоновки = Неопределено И ПодготовитьМакетЕслиНеНайден Тогда
		МакетКомпоновки = ДобавитьМакетКомпоновкиДанныхОтчета(Отчет, МетаданныеОтчета.Имя, КлючВарианта, ОтчетОбъект);
	Иначе
		ЗагрузитьНастройкиПечатнойФормы(ОтчетОбъект.КомпоновщикНастроек, НастройкиПечатнойФормы(Отчет, КлючВарианта));
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат МакетКомпоновки;
	
КонецФункции

Функция МакетКомпоновкиДанныхПоМетаданнымОтчета(МетаданныеОтчета, КлючВарианта, ПодготовитьМакетЕслиНеНайден = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МакетКомпоновки = Неопределено;
	
	Отчет = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеОтчета);
	МакетКомпоновки = СохраненныйМакетКомпоновкиДанныхОтчета(Отчет, КлючВарианта);;
	
	Если МакетКомпоновки = Неопределено И ПодготовитьМакетЕслиНеНайден Тогда
		МакетКомпоновки = ДобавитьМакетКомпоновкиДанныхОтчета(Отчет, МетаданныеОтчета.Имя, КлючВарианта);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат МакетКомпоновки;
	
КонецФункции

Функция СохраненныйМакетКомпоновкиДанныхОтчета(Отчет, КлючВарианта)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отчет", Отчет);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МакетыКомпоновкиДанных.МакетКомпоновкиДанных,
		|	МакетыКомпоновкиДанных.ВерсияКонфигурации
		|ИЗ
		|	РегистрСведений.МакетыКомпоновкиДанных КАК МакетыКомпоновкиДанных
		|ГДЕ
		|	МакетыКомпоновкиДанных.Отчет = &Отчет
		|	И МакетыКомпоновкиДанных.КлючВарианта = &КлючВарианта";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Если Выборка.ВерсияКонфигурации = Метаданные.Версия Тогда
			Возврат Выборка.МакетКомпоновкиДанных.Получить();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ОчиститьМакетыКомпоновкиДанных() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
	
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.МакетыКомпоновкиДанных");
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.МакетыКомпоновкиДанных.СоздатьНаборЗаписей();
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Макеты компоновки данных'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ДобавитьМакетКомпоновкиДанныхОтчета(Отчет, ИмяОтчета, КлючВарианта, ОтчетОбъект = Неопределено)
	
	Если ОтчетОбъект = Неопределено Тогда
		ОтчетОбъект = Отчеты[ИмяОтчета].Создать();
	КонецЕсли;
	
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек[КлючВарианта].Настройки);
	
	НастройкиПечатнойФормы = НастройкиПечатнойФормы(Отчет, КлючВарианта);
	Если НастройкиПечатнойФормы <> Неопределено Тогда
		
		ОтчетОбъект.ИнициализироватьОтчет(КлючВарианта);
		
		НастройкиОтчетаКД = Неопределено;
		ПользовательскиеНастройкиКД = Неопределено;
		
		ИнициализироватьНастройкиОтчета(ОтчетОбъект, "", КлючВарианта, НастройкиОтчетаКД, ПользовательскиеНастройкиКД);
		
		Если НастройкиОтчетаКД = Неопределено Тогда
			НастройкиОтчетаКД = ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек[КлючВарианта].Настройки;
		КонецЕсли;
		
		ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиОтчетаКД);
		Если ТипЗнч(ПользовательскиеНастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
			ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПользовательскиеНастройкиКД);
		КонецЕсли;
		
	КонецЕсли;
	
	ОтчетОбъект.ИнициализироватьОтчет(КлючВарианта);
	
	ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ФормированиеМакетаКомпоновкиДанных", Истина);
	ОтчетОбъект.СкомпоноватьРезультат(Новый ТабличныйДокумент);
	
	МакетКомпоновкиДанных = ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.СкомпонованныйМакетКомпоновкиДанных;
	ЗапомнитьМакетКомпоновкиДанных(Отчет, КлючВарианта, МакетКомпоновкиДанных);
	
	Возврат МакетКомпоновкиДанных;
	
КонецФункции

Функция НастройкиПечатнойФормы(Отчет, КлючВарианта) Экспорт
	
	ВариантОтчета = ВариантыОтчетов.ВариантОтчета(Отчет, КлючВарианта);
	Если ВариантОтчета <> Неопределено Тогда
		
		НастройкиСхемы = ВариантОтчета.Настройки.Получить();
		Если ТипЗнч(НастройкиСхемы) = Тип("Структура") Тогда
			
			СтруктураНастроекПечатнойФормы = Новый Структура("Настройки,ПользовательскиеНастройкиКД");
			ЗаполнитьЗначенияСвойств(СтруктураНастроекПечатнойФормы, НастройкиСхемы);
			Если СтруктураНастроекПечатнойФормы.Настройки <> Неопределено
				Или СтруктураНастроекПечатнойФормы.ПользовательскиеНастройкиКД <> Неопределено Тогда
				
				Возврат СтруктураНастроекПечатнойФормы;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ЗагрузитьНастройкиПечатнойФормы(КомпоновщикНастроекКД, НастройкиПечатнойФормы) Экспорт
	
	Если НастройкиПечатнойФормы <> Неопределено Тогда
		ОтчетыКлиентСервер.ЗагрузитьНастройки(КомпоновщикНастроекКД, НастройкиПечатнойФормы.Настройки, НастройкиПечатнойФормы.ПользовательскиеНастройкиКД);
		КомпоновщикНастроекКД.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ИспользуютсяПользовательскиеНастройкиПечати", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗапомнитьМакетКомпоновкиДанных(Отчет, КлючВарианта, МакетКомпоновкиДанных) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.МакетыКомпоновкиДанных");
		ЭлементБлокировкиДанных.УстановитьЗначение("Отчет", Отчет);
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.МакетыКомпоновкиДанных.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Отчет.Установить(Отчет);
		НаборЗаписей.Отбор.КлючВарианта.Установить(КлючВарианта);
		
		Запись = НаборЗаписей.Добавить();
		Запись.Отчет = Отчет;
		Запись.КлючВарианта = КлючВарианта;
		Запись.МакетКомпоновкиДанных = Новый ХранилищеЗначения(МакетКомпоновкиДанных);
		Запись.ВерсияКонфигурации = Метаданные.Версия;
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Макеты компоновки данных'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОчиститьМакетОтчета(МетаданныеОтчета, КлючВарианта = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Отчет = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеОтчета);
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.МакетыКомпоновкиДанных");
		ЭлементБлокировкиДанных.УстановитьЗначение("Отчет", Отчет);
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.МакетыКомпоновкиДанных.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Отчет.Установить(Отчет);
		
		Если КлючВарианта <> Неопределено Тогда
			НаборЗаписей.Отбор.КлючВарианта.Установить(КлючВарианта);
		КонецЕсли;
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Макеты компоновки данных'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти


#Область МетодыПодсистемыВариантыОтчетов

// Возвращает список полей группировок всех группировок компоновщика настроек.
//
// Параметры: 
//		КомпоновщикНастроек - компоновщик настроек.
//		БезПользовательскихПолей - признак не включения пользовательских настроек СКД.
//
Функция ПолучитьПоляГруппировок(КомпоновщикНастроек, БезПользовательскихПолей = Ложь) Экспорт
	
	СписокПолей = Новый СписокЗначений;
	
	Структура = КомпоновщикНастроек.Настройки.Структура;
	ДобавитьПоляГруппировки(Структура, СписокПолей, БезПользовательскихПолей);
	Возврат СписокПолей;
	
КонецФункции

// Добавляет вложенные поля группировки.
Процедура ДобавитьПоляГруппировки(Структура, СписокПолей, БезПользовательскихПолей)
	
	Для каждого ЭлементСтруктуры Из Структура Цикл
		Если ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			ДобавитьПоляГруппировки(ЭлементСтруктуры.Строки, СписокПолей, БезПользовательскихПолей);
			ДобавитьПоляГруппировки(ЭлементСтруктуры.Колонки, СписокПолей, БезПользовательскихПолей);
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных") Тогда
			ДобавитьПоляГруппировки(ЭлементСтруктуры.Серии, СписокПолей, БезПользовательскихПолей);
			ДобавитьПоляГруппировки(ЭлементСтруктуры.Точки, СписокПолей, БезПользовательскихПолей);
		Иначе
			Для каждого ТекущееПолеГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
				ДоступноеПоле = ЭлементСтруктуры.Выбор.ДоступныеПоляВыбора.НайтиПоле(ТекущееПолеГруппировки.Поле);
				Если ДоступноеПоле <> Неопределено 
				  И (ДоступноеПоле.Родитель = Неопределено ИЛИ Не БезПользовательскихПолей ИЛИ ДоступноеПоле.Родитель.Поле <> Новый ПолеКомпоновкиДанных("UserFields")) Тогда
					СписокПолей.Добавить(Строка(ДоступноеПоле.Поле), ДоступноеПоле.Заголовок);
				КонецЕсли;
			КонецЦикла;
			ДобавитьПоляГруппировки(ЭлементСтруктуры.Структура, СписокПолей, БезПользовательскихПолей);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает последний элемент структуры - группировку.
//
// Параметры:
//		ЭлементСтруктурыНастроек - элемент структуры компоновки данных.
//		Строки - признак для получения последний группировки строк (Серий) или колонок (точек).
//
Функция ПолучитьПоследнийЭлементСтруктуры(ЭлементСтруктурыНастроек, Строки = Истина) Экспорт
	
	Если ТипЗнч(ЭлементСтруктурыНастроек) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Настройки = ЭлементСтруктурыНастроек.Настройки;
	ИначеЕсли ТипЗнч(ЭлементСтруктурыНастроек) = Тип("НастройкиКомпоновкиДанных") Тогда
		Настройки = ЭлементСтруктурыНастроек;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Структура = Настройки.Структура;
	Если Структура.Количество() = 0 Тогда
		Возврат Настройки;
	КонецЕсли;
	
	Если Строки Тогда
		ИмяСтруктурыТаблицы = "Строки";
		ИмяСтруктурыДиаграммы = "Серии";
	Иначе
		ИмяСтруктурыТаблицы = "Колонки";
		ИмяСтруктурыДиаграммы = "Точки";
	КонецЕсли;
	
	Пока Истина Цикл
		ЭлементСтруктуры = Структура[0];
		Если ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") И ЭлементСтруктуры[ИмяСтруктурыТаблицы].Количество() > 0 Тогда
			Если ЭлементСтруктуры[ИмяСтруктурыТаблицы][0].Структура.Количество() = 0 Тогда
				Структура = ЭлементСтруктуры[ИмяСтруктурыТаблицы];
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры[ИмяСтруктурыТаблицы][0].Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных") И ЭлементСтруктуры[ИмяСтруктурыДиаграммы].Количество() > 0 Тогда
			Если ЭлементСтруктуры[ИмяСтруктурыДиаграммы][0].Структура.Количество() = 0 Тогда
				Структура = ЭлементСтруктуры[ИмяСтруктурыДиаграммы];
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры[ИмяСтруктурыДиаграммы][0].Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных")
			  ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
			  ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
			Если ЭлементСтруктуры.Структура.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры.Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			Возврат ЭлементСтруктуры[ИмяСтруктурыТаблицы];
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных")	Тогда
			Возврат ЭлементСтруктуры[ИмяСтруктурыДиаграммы];
		Иначе
			Возврат ЭлементСтруктуры;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Структура[0];
	
КонецФункции

#КонецОбласти


#Область Т49

Процедура ПриКомпоновкеРезультатаТ49(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс = Ложь) Экспорт
	
	Попытка
		
		КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
		СхемаКомпоновкиДанных = ОбъектОтчета.СхемаКомпоновкиДанных;
		
		КлючВарианта = КлючВарианта(КомпоновщикНастроек);
		
		// Параметры документа
		ДокументРезультат.ТолькоПросмотр = Истина;
		ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УнифицированнаяФормаТ49";
		ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ДокументРезультат.АвтоМасштаб = Истина;
		
		НастройкиОтчета = ОбъектОтчета.КомпоновщикНастроек.ПолучитьНастройки();
		
		ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс);
		ДополнительныеНачисления = УчетНачисленнойЗарплаты.ДополнительныеНачисленияОтчетаАнализНачисленийИУдержанийТ49();
		УчетНачисленнойЗарплаты.ДобавитьПользовательскиеПоляДополнительныхНачисленийИУдержаний(ДополнительныеНачисления, НастройкиОтчета, 5, , НаАванс);
		ДополнительныеУдержания = УчетНачисленнойЗарплаты.ДополнительныеУдержанияОтчетаАнализНачисленийИУдержанийТ49();
		УчетНачисленнойЗарплаты.ДобавитьПользовательскиеПоляДополнительныхНачисленийИУдержаний(ДополнительныеУдержания, НастройкиОтчета, 3, "Удержания", НаАванс);
		
		МакетКомпоновки = МакетКомпоновкиДанныхДляКоллекцииЗначений(СхемаКомпоновкиДанных, НастройкиОтчета);
		
		СоответствиеПользовательскихПолей = СоответствиеПользовательскихПолей(НастройкиОтчета);
		
		Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ДанныеДокумента") Тогда
			НаборыВнешнихДанных = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.ДанныеДокумента;
		Иначе
			НаборыВнешнихДанных = НаборыВнешнихДанныхАнализНачисленийИУдержаний();
		КонецЕсли;
		
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, НаборыВнешнихДанных, , Истина);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ДанныеОтчета = Новый ДеревоЗначений;
		ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
		
		ПорядокДопНачислений = УчетНачисленнойЗарплаты.ПорядокДополнительныхНачислений(ДополнительныеНачисления, ДанныеОтчета, СоответствиеПользовательскихПолей, 8);
		ПорядокДопУдержаний = УчетНачисленнойЗарплаты.ПорядокДополнительныхУдержаний(ДополнительныеУдержания, ДанныеОтчета, СоответствиеПользовательскихПолей, 15);
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_Т49");
		
		Макеты = Новый Структура("ШапкаДокумента,Шапка,Строка,ПустаяСтрока,Подвал,ИтогоПоСтранице,ИтогоПоВедомости");
		
		Если НаАванс Тогда
			Макеты.ШапкаДокумента = Макет.ПолучитьОбласть("ШапкаДокументаПерваяПоловина");
		Иначе
			Макеты.ШапкаДокумента = Макет.ПолучитьОбласть("ШапкаДокумента");
		КонецЕсли;
		
		Макеты.Шапка = Макет.ПолучитьОбласть("Шапка");
		Макеты.Строка = Макет.ПолучитьОбласть("Строка");
		Макеты.ПустаяСтрока = Макет.ПолучитьОбласть("Строка");
		Макеты.Подвал = Макет.ПолучитьОбласть("Подвал");
		Макеты.ИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		Макеты.ИтогоПоВедомости = Макет.ПолучитьОбласть("ИтогоПоВедомости");
		
		ДанныеОтчета.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(18,0));
		Для каждого СтрокаМесяца Из ДанныеОтчета.Строки Цикл
			Для Каждого СтрокаОрганизации Из СтрокаМесяца.Строки Цикл
				ВывестиОрганизациюТ49(ОбъектОтчета, СтрокаОрганизации, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний);
			КонецЦикла;
		КонецЦикла;
		
		ДопСвойства = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
		ДопСвойства.Вставить("ОтчетПустой", ДанныеОтчета.Строки.Количество() = 0);
		
		СтандартнаяОбработка = Ложь;
		
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение НСтр("ru = 'В настройку отчета Т-49 внесены критичные изменения. Отчет не будет сформирован.'")
			+ " " + КраткоеПредставлениеОшибки(Инфо);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ВывестиОрганизациюТ49(ОбъектОтчета, СтрокаОрганизации, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний)
	
	КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
	
	НастройкиПечатныхФорм = ЗарплатаКадры.НастройкиПечатныхФорм();
	
	Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	КВыплатеПоВедомости = 0;
	
	Макеты.ШапкаДокумента.Параметры.Заполнить(СтрокаОрганизации);
	Макеты.ШапкаДокумента.Параметры.ДатаД = Формат(ТекущаяДатаСеанса(), "ДЛФ=D");
	Макеты.Шапка.Параметры.Заполнить(СтрокаОрганизации);
	
	Документ = Неопределено;
	КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("Документ", Документ);
	Если Документ <> Неопределено Тогда
		
		ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Документ,
			"Номер, Дата, Руководитель, ДолжностьРуководителя, ГлавныйБухгалтер, Кассир, ДолжностьКассира, Бухгалтер");
			ЗарплатаКадрыОтчетыПереопределяемый.ДанныеДокумента(Документ, ДанныеДокумента);
	Иначе
		ДанныеДокумента = Новый Структура("Организация,Руководитель,ДолжностьРуководителя,ГлавныйБухгалтер,Кассир,ДолжностьКассира", СтрокаОрганизации.Организация);
		ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ДанныеДокумента, КонецМесяца(СтрокаОрганизации.МесяцНачисления));
		ДанныеДокумента.Удалить("Организация");
		ДанныеДокумента.Вставить("Номер", "");
		ДанныеДокумента.Вставить("Дата", "");
		ДанныеДокумента.Вставить("Бухгалтер", Справочники.ФизическиеЛица.ПустаяСсылка());
		
	КонецЕсли;
	
	СписокОтветственных = Новый Массив;
	Если ЗначениеЗаполнено(ДанныеДокумента.Руководитель) Тогда
		СписокОтветственных.Добавить(ДанныеДокумента.Руководитель);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДокумента.ГлавныйБухгалтер) Тогда
		СписокОтветственных.Добавить(ДанныеДокумента.ГлавныйБухгалтер);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДокумента.Кассир) Тогда
		СписокОтветственных.Добавить(ДанныеДокумента.Кассир);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДокумента.Бухгалтер) Тогда
		СписокОтветственных.Добавить(ДанныеДокумента.Бухгалтер);
	КонецЕсли;
	
	РасшифровкиПодписей = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, СписокОтветственных,"ИОФамилия", КонецМесяца(СтрокаОрганизации.МесяцНачисления));
	
	Для каждого СтрокаТаблицы Из ПорядокДопНачислений Цикл
		Макеты.Шапка.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СтрокаТаблицы.Заголовок;
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из ПорядокДопУдержаний Цикл
		Макеты.Шапка.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СтрокаТаблицы.Заголовок;
	КонецЦикла;
	
	Макеты.ШапкаДокумента.Параметры.ДатаД = ДанныеДокумента.Дата;
	Если ЗначениеЗаполнено(ДанныеДокумента.Номер) Тогда
		Макеты.ШапкаДокумента.Параметры.НомерД = НомерНаПечать(ДанныеДокумента.Номер);
	КонецЕсли;
	
	Макеты.ШапкаДокумента.Параметры.ДолжностьРуководителя = ДанныеДокумента.ДолжностьРуководителя;
	ДанныеРуководителя = РасшифровкиПодписей.Найти(ДанныеДокумента.Руководитель, "ФизическоеЛицо");
	Если ДанныеРуководителя <> Неопределено Тогда
		Макеты.ШапкаДокумента.Параметры.РуководительРасшифровкаПодписи = ДанныеРуководителя.ИОФамилия;
	КонецЕсли;
	
	ДанныеГлавногоБухгалтера = РасшифровкиПодписей.Найти(ДанныеДокумента.ГлавныйБухгалтер, "ФизическоеЛицо");
	Если ДанныеГлавногоБухгалтера <> Неопределено Тогда
		Макеты.ШапкаДокумента.Параметры.ГлавныйБухгалтерРасшифровкаПодписи = ДанныеГлавногоБухгалтера.ИОФамилия;
	КонецЕсли;
	
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.ШапкаДокумента, СтрокаОрганизации, СоответствиеПользовательскихПолей);
	
	ДанныеВедомости = Неопределено;
	КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ДанныеВедомости", ДанныеВедомости);
	Если ДанныеВедомости = Неопределено Тогда
		КВыплате = СтрокаОрганизации[СоответствиеПользовательскихПолей.Получить("КВыплате")];
	Иначе
		
		КВыплате = ДанныеВедомости.Итог("КВыплате");
		СтрокаОрганизации[СоответствиеПользовательскихПолей.Получить("КВыплате")] = КВыплате;
		
		// Упорядочим строки как в документе
		Для Каждого СтрокаГоловногоСотрудника Из СтрокаОрганизации.Строки Цикл
			
			Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
				
				СтрокаДанныхВедомости = ДанныеВедомости.Найти(СтрокаСотрудника.Сотрудник, "Сотрудник");
				Если СтрокаДанныхВедомости = Неопределено Тогда
					СтрокаДанныхВедомости = ДанныеВедомости.Найти(СтрокаСотрудника.ГоловнойСотрудник, "Сотрудник");
				КонецЕсли;
				Если СтрокаДанныхВедомости <> Неопределено Тогда
					
					СтрокаСотрудника.НомерСтроки = СтрокаДанныхВедомости.НомерСтроки;
					Если СтрокаГоловногоСотрудника.НомерСтроки < СтрокаСотрудника.НомерСтроки Тогда
						СтрокаГоловногоСотрудника.НомерСтроки = СтрокаСотрудника.НомерСтроки;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		СтрокаОрганизации.Строки.Сортировать("НомерСтроки", Истина);
		
	КонецЕсли;
	
	Если КВыплате > 0 Тогда
		
		ВалютаУчета = ЗарплатаКадры.ВалютаУчетаЗаработнойПлаты();
		Макеты.ШапкаДокумента.Параметры.СуммаВсегоПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(КВыплате, ВалютаУчета);
		Макеты.ШапкаДокумента.Параметры.СуммаДокРублей = Цел(КВыплате);
		Макеты.ШапкаДокумента.Параметры.СуммаДокКопеек = (КВыплате - Цел(КВыплате)) * 100;
		
	КонецЕсли;
	
	ПодразделениеВШапке = Неопределено;
	КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ПодразделениеВШапке", ПодразделениеВШапке);
	Если ПодразделениеВШапке <> Неопределено Тогда
		Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода = ПодразделениеВШапке;
	КонецЕсли;
	
	Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений И ЗначениеЗаполнено(Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода) Тогда
		Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода = Макеты.ШапкаДокумента.Параметры.ПодразделениеНаКонецПериода.ПолноеНаименование();
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макеты.ШапкаДокумента);
	
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка, СтрокаОрганизации, СоответствиеПользовательскихПолей);
	ДокументРезультат.Вывести(Макеты.Шапка);
	
	НомерСтроки = 0;
	ПромежуточныеИтоги = ПромежуточныеИтогиПоСтраницеТ49(СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний);
	
	Для Каждого СтрокаСотрудника Из СтрокаОрганизации.Строки Цикл
		ВывестиСотрудникаТ49(НомерСтроки, СтрокаСотрудника, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ПромежуточныеИтоги, ДанныеВедомости, КВыплатеПоВедомости, ПорядокДопНачислений, ПорядокДопУдержаний);
	КонецЦикла;
	
	ДополнитьСтраницуТ49(НомерСтроки, Макеты, ДокументРезультат, ПромежуточныеИтоги);
	Макеты.ИтогоПоВедомости.Параметры.Заполнить(СтрокаОрганизации);
	
	Для каждого СтрокаТаблицы Из ПорядокДопНачислений Цикл
		СуммаЯчейки = СтрокаОрганизации[СоответствиеПользовательскихПолей.Получить(СтрокаТаблицы.Имя)];
		Макеты.ИтогоПоВедомости.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СуммаЯчейки;
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из ПорядокДопУдержаний Цикл
		СуммаЯчейки = СтрокаОрганизации[СоответствиеПользовательскихПолей.Получить(СтрокаТаблицы.Имя)];
		Макеты.ИтогоПоВедомости.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СуммаЯчейки;
	КонецЦикла;
	
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.ИтогоПоВедомости, СтрокаОрганизации, СоответствиеПользовательскихПолей);
	Макеты.Подвал.Параметры.Заполнить(СтрокаОрганизации);
	
	Если Документ <> Неопределено Тогда
		
		Макеты.Подвал.Параметры.ДолжностьКассира = ДанныеДокумента.ДолжностьКассира;
		ДанныеКассира = РасшифровкиПодписей.Найти(ДанныеДокумента.Кассир, "ФизическоеЛицо");
		Если ДанныеКассира <> Неопределено Тогда
			Макеты.Подвал.Параметры.ФИОКассира = ДанныеКассира.ИОФамилия;
		КонецЕсли;
		
		ДанныеБухгалтера = РасшифровкиПодписей.Найти(ДанныеДокумента.Бухгалтер, "ФизическоеЛицо");
		Если ДанныеБухгалтера <> Неопределено Тогда
			Макеты.Подвал.Параметры.ФИОБухгалтера = ДанныеБухгалтера.ИОФамилия;
		КонецЕсли;
		
	КонецЕсли;
	
	Макеты.ИтогоПоВедомости.Параметры.КВыплате = КВыплатеПоВедомости;
	ДокументРезультат.Вывести(Макеты.ИтогоПоВедомости);
	ДокументРезультат.Вывести(Макеты.Подвал);
	
КонецПроцедуры

Процедура ВывестиСотрудникаТ49(НомерСтроки, СтрокаГоловногоСотрудника, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ПромежуточныеИтоги, ДанныеВедомости, КВыплатеПоВедомости, ПорядокДопНачислений, ПорядокДопУдержаний)
	
	НастройкиПечатныхФорм = ЗарплатаКадры.НастройкиПечатныхФорм();
	НомерСтроки = НомерСтроки + 1;
	
	МакетСотрудника = Новый ТабличныйДокумент;
	ПромежуточныеИтогиПоГоловномуСотруднику = ПромежуточныеИтогиПоСтраницеТ49(СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний);
	НесколькоРабочихМест = СтрокаГоловногоСотрудника.Строки.Количество() > 1;
	
	Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
		
		Если ДанныеВедомости <> Неопределено Тогда
			
			СтрокаДанныхВедомости = ДанныеВедомости.Найти(СтрокаСотрудника.Сотрудник, "Сотрудник");
			Если СтрокаДанныхВедомости <> Неопределено Тогда
				СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("КВыплате")] = СтрокаДанныхВедомости.КВыплате;
			КонецЕсли;
			
		КонецЕсли;
		
		Макеты.Строка.Параметры.Заполнить(СтрокаСотрудника);
		
		Если НЕ НастройкиПечатныхФорм.ВыводитьПолныеФИОВСписочныхПечатныхФормах Тогда
			Макеты.Строка.Параметры.ФизическоеЛицоФИО = СтрокаСотрудника.ЛичныеДанныеФИОФамилияИИнициалы;
		КонецЕсли;
		
		ЗаполнитьПараметрыПользовательскихПолей(Макеты.Строка, СтрокаСотрудника, СоответствиеПользовательскихПолей);
		Для каждого СтрокаТаблицы Из ПорядокДопНачислений Цикл
			
			СуммаЯчейки = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить(СтрокаТаблицы.Имя)];
			Макеты.Строка.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СуммаЯчейки;
			
		КонецЦикла;
		
		Для каждого СтрокаТаблицы Из ПорядокДопУдержаний Цикл
			
			СуммаЯчейки = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить(СтрокаТаблицы.Имя)];
			Макеты.Строка.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СуммаЯчейки;
			
		КонецЦикла;
		
		ОтработаноРабочихДней = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноРабочихДней")];
		ОтработаноРабочихЧасов = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноРабочихЧасов")];
		ОтработаноПраздничныхДней = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноПраздничныхДней")];
		ОтработаноПраздничныхЧасов = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноПраздничныхЧасов")];
		ОтработаноВыходныхДней = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноВыходныхДней")];
		ОтработаноВыходныхЧасов = СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("ОтработаноВыходныхЧасов")];
		
		Если ЗначениеЗаполнено(ОтработаноРабочихДней) И ОтработаноРабочихДней > 0 Тогда 
			Макеты.Строка.Параметры.ОтработаноРабочихДней = "" + ОтработаноРабочихДней + " (" + ОтработаноРабочихЧасов + ")";
		Иначе
			Макеты.Строка.Параметры.ОтработаноРабочихДней = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОтработаноПраздничныхДней) И ОтработаноПраздничныхДней > 0 Тогда 
			Макеты.Строка.Параметры.ОтработаноПраздничныхДней = "" + ОтработаноПраздничныхДней + " (" + ОтработаноПраздничныхЧасов + ")";
		Иначе
			Макеты.Строка.Параметры.ОтработаноПраздничныхДней = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОтработаноВыходныхДней) И ОтработаноВыходныхДней > 0 Тогда 
			Макеты.Строка.Параметры.ОтработаноВыходныхДней = "" + ОтработаноВыходныхДней + " (" + ОтработаноВыходныхЧасов + ")";
		Иначе
			Макеты.Строка.Параметры.ОтработаноВыходныхДней = "";
		КонецЕсли;
		
		Макеты.Строка.Параметры.НомерПП = НомерСтроки;
		
		Если Макеты.Строка.Параметры.КВыплате < 0 Тогда
			Макеты.Строка.Параметры.КВыплате = 0;
		КонецЕсли;
		
		ДобавитьВПромежуточныйИтог(ПромежуточныеИтогиПоГоловномуСотруднику, Макеты.Строка.Параметры);
		КВыплатеПоВедомости = КВыплатеПоВедомости + Макеты.Строка.Параметры.КВыплате;
		
		Если НесколькоРабочихМест Тогда
			
			ЗаполнитьПараметрыПользовательскихПолей(
				Макеты.Строка, СтрокаГоловногоСотрудника, СоответствиеПользовательскихПолей,
				"ВсегоНачислено,ДолгЗаОрганизацией,ДолгЗаСотрудником");
			
			Для каждого СтрокаТаблицы Из ПорядокДопУдержаний Цикл
				
				СуммаЯчейки = СтрокаГоловногоСотрудника[СоответствиеПользовательскихПолей.Получить(СтрокаТаблицы.Имя)];
				Макеты.Строка.Параметры["Колонка" + СтрокаТаблицы.НомерКолонки] = СуммаЯчейки;
				
			КонецЦикла;
			
		КонецЕсли;
			
		Если  СтрокаСотрудника.Сотрудник <> СтрокаСотрудника.ГоловнойСотрудник Тогда
			Макеты.Строка.Параметры.ДолжностьНаКонецПериодаНаименованиеКраткое = СтрокаСотрудника.СотрудникУточнениеНаименования;
		КонецЕсли; 
		
		МакетСотрудника.Вывести(Макеты.Строка);
		
	КонецЦикла;
	
	Если Не ЕстьИтогиПоГоловномуСотруднику(ПромежуточныеИтогиПоГоловномуСотруднику) Тогда
		
		НомерСтроки = НомерСтроки - 1;
		Возврат;
		
	КонецЕсли;
	
	Если МакетСотрудника.ВысотаТаблицы > 1 Тогда
		
		// Номер по порядку
		МакетСотрудника.Область(1, 1, МакетСотрудника.ВысотаТаблицы, 1).Объединить();
		// Табельный номер
		МакетСотрудника.Область(1, 2, МакетСотрудника.ВысотаТаблицы, 2).Объединить();
		// Всего начислено
		МакетСотрудника.Область(1, 19, МакетСотрудника.ВысотаТаблицы, 19).Объединить();
		// Удержания
		МакетСотрудника.Область(1, 20, МакетСотрудника.ВысотаТаблицы, 20).Объединить();
		МакетСотрудника.Область(1, 21, МакетСотрудника.ВысотаТаблицы, 21).Объединить();
		МакетСотрудника.Область(1, 22, МакетСотрудника.ВысотаТаблицы, 22).Объединить();
		МакетСотрудника.Область(1, 23, МакетСотрудника.ВысотаТаблицы, 23).Объединить();
		// Задолженность за организацией.
		МакетСотрудника.Область(1, 24, МакетСотрудника.ВысотаТаблицы, 24).Объединить();
		// Задолженность за работником.
		МакетСотрудника.Область(1, 25, МакетСотрудника.ВысотаТаблицы, 26).Объединить();
		// К выплате
		МакетСотрудника.Область(1, 27, МакетСотрудника.ВысотаТаблицы, 28).Объединить();
		// ФИО
		МакетСотрудника.Область(1, 29, МакетСотрудника.ВысотаТаблицы, 29).Объединить();
		// Подпись
		МакетСотрудника.Область(1, 30, МакетСотрудника.ВысотаТаблицы, 30).Объединить();
		
	КонецЕсли;
	
	МассивВыводимыхОбластей = Новый Массив;
	МассивВыводимыхОбластей.Добавить(МакетСотрудника);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоСтранице);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоВедомости);
	МассивВыводимыхОбластей.Добавить(Макеты.Подвал);
	
	Если НЕ ДокументРезультат.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
		
		ЗаполнитьЗначенияСвойств(Макеты.ИтогоПоСтранице.Параметры, ПромежуточныеИтоги);
		ДокументРезультат.Вывести(Макеты.ИтогоПоСтранице);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка, СтрокаСотрудника, СоответствиеПользовательскихПолей);
		ДокументРезультат.Вывести(Макеты.Шапка);
		ПромежуточныеИтоги = ПромежуточныеИтогиПоСтраницеТ49(СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний);
		
	КонецЕсли;
	
	ДобавитьВПромежуточныйИтог(ПромежуточныеИтоги, ПромежуточныеИтогиПоГоловномуСотруднику);
	
	ДокументРезультат.Вывести(МакетСотрудника);
	
КонецПроцедуры

Процедура ДополнитьСтраницуТ49(НомерСтроки, Макеты, ДокументРезультат, ПромежуточныеИтоги)
	
	МассивВыводимыхОбластей = Новый Массив;
	МассивВыводимыхОбластей.Добавить(Макеты.ПустаяСтрока);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоСтранице);
	МассивВыводимыхОбластей.Добавить(Макеты.ИтогоПоВедомости);
	МассивВыводимыхОбластей.Добавить(Макеты.Подвал);
	
	Пока ДокументРезультат.ПроверитьВывод(МассивВыводимыхОбластей) Цикл
		ДокументРезультат.Вывести(Макеты.ПустаяСтрока);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Макеты.ИтогоПоСтранице.Параметры, ПромежуточныеИтоги);
	ДокументРезультат.Вывести(Макеты.ИтогоПоСтранице);
	
КонецПроцедуры

Функция ПромежуточныеИтогиПоСтраницеТ49(СоответствиеПользовательскихПолей, ПорядокДопНачислений, ПорядокДопУдержаний)
	
	ПромежуточныеИтоги = Новый Структура;
	
	Для каждого СтрокаТаблицы Из ПорядокДопНачислений Цикл
		ПромежуточныеИтоги.Вставить("Колонка" + СтрокаТаблицы.НомерКолонки, 0);
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из ПорядокДопУдержаний Цикл
		ПромежуточныеИтоги.Вставить("Колонка" + СтрокаТаблицы.НомерКолонки, 0);
	КонецЦикла;
	
	ПромежуточныеИтоги.Вставить("ВсегоНачислено", 0);
	ПромежуточныеИтоги.Вставить("ДолгЗаОрганизацией", 0);
	ПромежуточныеИтоги.Вставить("ДолгЗаСотрудником", 0);
	ПромежуточныеИтоги.Вставить("КВыплате", 0);
	
	Возврат ПромежуточныеИтоги;
	
КонецФункции

#КонецОбласти


#Область Т51

Процедура ПриКомпоновкеРезультатаТ51(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс = Ложь) Экспорт
	
	Попытка
		
		КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
		СхемаКомпоновкиДанных = ОбъектОтчета.СхемаКомпоновкиДанных;
		
		КлючВарианта = КлючВарианта(КомпоновщикНастроек);
		
		// Параметры документа
		ДокументРезультат.ТолькоПросмотр = Истина;
		ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УнифицированнаяФормаТ51";
		ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ДокументРезультат.АвтоМасштаб = Истина;
		
		НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
		
		НастройкиПечатныхФорм = ЗарплатаКадры.НастройкиПечатныхФорм();
		НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВыводитьПолныеФИОВСписочныхПечатныхФормах")).Значение =
			НастройкиПечатныхФорм.ВыводитьПолныеФИОВСписочныхПечатныхФормах;
		
		// Нужно проверить включена ли группировка по подразделениям.
		ЕстьГруппировкаПоПодразделению = Ложь;
		ПараметрГруппировки = Новый ПараметрКомпоновкиДанных("РазбиватьПоПодразделениям");
		ЕстьГруппировкаПоПодразделению = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрГруппировки).Значение;
		
		Если Не ЕстьГруппировкаПоПодразделению Тогда
			
			Для каждого ЭлементСортировки Из НастройкиОтчета.Порядок.Элементы Цикл
				
				Если ЭлементСортировки.Поле = Новый ПолеКомпоновкиДанных("Подразделение.РеквизитДопУпорядочивания") Тогда
					ЭлементСортировки.Использование = Ложь;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ИзменитьГруппировкиВариантаТ49Т51(НастройкиОтчета.Структура, ЕстьГруппировкаПоПодразделению);
		
		ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс);
		
		Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ДанныеДокумента") Тогда
			НаборыВнешнихДанных = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.ДанныеДокумента;
		Иначе
			НаборыВнешнихДанных = НаборыВнешнихДанныхАнализНачисленийИУдержаний();
		КонецЕсли;
		
		РезультатКомпоновки = РезультатКомпоновкиМакетаПечатнойФормы(ОбъектОтчета, ДанныеРасшифровки, НастройкиОтчета, НаборыВнешнихДанных);
		
		Для каждого СтрокаМесяца Из РезультатКомпоновки.ДанныеОтчета.Строки Цикл
			
			Для Каждого СтрокаОрганизации Из СтрокаМесяца.Строки Цикл
				
				ВывестиОрганизациюТ51(
					СтрокаОрганизации,
					ДокументРезультат,
					РезультатКомпоновки,
					ЕстьГруппировкаПоПодразделению,
					НаАванс);
				
			КонецЦикла;
			
		КонецЦикла;
		
		СтандартнаяОбработка = Ложь;
		
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение НСтр("ru = 'В настройку отчета Т-51 внесены критичные изменения. Отчет не будет сформирован.'")
			+ " " + КраткоеПредставлениеОшибки(Инфо);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ВывестиОрганизациюТ51(СтрокаОрганизации, ДокументРезультат, РезультатКомпоновки, ЕстьГруппировкаПоПодразделению, НаАванс)
	
	ЕстьОплатаВНатуральнойФорме = (СтрокаОрганизации[РезультатКомпоновки.ИдентификаторыМакета["НачисленоВНатуральнойФорме"]] <> 0);
	Если ЕстьГруппировкаПоПодразделению Тогда
		
		Для Каждого СтрокаПодразделения Из СтрокаОрганизации.Строки Цикл
			ВывестиВедомостьТ51(СтрокаПодразделения, ДокументРезультат, РезультатКомпоновки, ЕстьОплатаВНатуральнойФорме, НаАванс, Истина);
		КонецЦикла;
		
	Иначе
		ВывестиВедомостьТ51(СтрокаОрганизации, ДокументРезультат, РезультатКомпоновки, ЕстьОплатаВНатуральнойФорме, НаАванс, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиВедомостьТ51(СтрокаГруппировки, ДокументРезультат, РезультатКомпоновки, ЕстьОплатаВНатуральнойФорме, НаАванс, ЕстьГруппировкаПоПодразделению)
	
	НастройкиПечатныхФорм = ЗарплатаКадры.НастройкиПечатныхФорм();
	
	Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	КВыплатеПоВедомости = 0;
	
	ДанныеНаПечать = Новый Структура;
	ДанныеНаПечать.Вставить("ДанныеШапки", СтрокаГруппировки);
	ДанныеНаПечать.Вставить("ДанныеШапкиПользовательскиеПоля",
		ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, ДанныеНаПечать.ДанныеШапки));
	
	ДанныеПолучаемыеНаХоду = Новый Структура;
	ДанныеПолучаемыеНаХоду.Вставить("СообщениеОНеприменимостиПечатнойФормы",
		ЗарплатаКадры.СообщениеОНеприменимостиПечатнойФормы(
			ДанныеНаПечать.ДанныеШапки.ПараметрыДанныхДатаОтчета,
			'20150619',
			НСтр("ru='Приказа Минфина РФ'"),
			'20150330',
			"52н"));
	
	Если НаАванс Тогда
		ИмяМакетаШапки = "ШапкаДокументаПерваяПоловина";
	Иначе
		ИмяМакетаШапки = "ШапкаДокумента";
	КонецЕсли;
	
	Если Не ЕстьГруппировкаПоПодразделению Тогда
		ДанныеПолучаемыеНаХоду.Вставить("ПодразделениеНаКонецПериодаНаПечать");
	КонецЕсли;
	
	ВывестиВДокументРезультатОбластиМакета(ДокументРезультат, РезультатКомпоновки.МакетПечатнойФормы, ИмяМакетаШапки,
		ДанныеНаПечать.ДанныеШапки, ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля, ДанныеПолучаемыеНаХоду);
	
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	
	ДанныеПолучаемыеНаХоду = Новый Структура;
	Если ЕстьОплатаВНатуральнойФорме Тогда
		ДанныеПолучаемыеНаХоду.Вставить("ГруппаНачислений2", НСтр("ru='Оплата в натуральной форме'"));
	КонецЕсли;
	
	ВывестиВДокументРезультатОбластиМакета(ДокументРезультат, РезультатКомпоновки.МакетПечатнойФормы, "Шапка",
		ДанныеНаПечать.ДанныеШапки, ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля, ДанныеПолучаемыеНаХоду);
	
	НомерСтроки = 0;
	ПромежуточныеИтоги = ПромежуточныеИтогиПоСтраницеТ51();
	Для Каждого СтрокаСотрудника Из ДанныеНаПечать.ДанныеШапки.Строки Цикл
		
		ВывестиСотрудникаТ51(НомерСтроки, СтрокаСотрудника, ДокументРезультат, РезультатКомпоновки, 
			ПромежуточныеИтоги, КВыплатеПоВедомости, ЕстьОплатаВНатуральнойФорме, ДанныеНаПечать);
		
	КонецЦикла;
	
	ДополнитьСтраницуТ51(РезультатКомпоновки.МакетПечатнойФормы, ДокументРезультат, ДанныеНаПечать, ПромежуточныеИтоги);
	
	ДанныеПолучаемыеНаХоду = Новый Структура("КВыплате", КВыплатеПоВедомости);
	
	Если ЕстьОплатаВНатуральнойФорме Тогда
		
		ДанныеПолучаемыеНаХоду.Вставить("НачисленоСдельно", ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля.НачисленоВНатуральнойФорме);
		ДанныеПолучаемыеНаХоду.Вставить("ПрочиеДоходы", ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля.ПрочиеДоходы + ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля.НачисленоСдельно);
		
	КонецЕсли;
	
	ВывестиВДокументРезультатОбластиМакета(ДокументРезультат, РезультатКомпоновки.МакетПечатнойФормы, "ИтогоПоВедомости",
		ДанныеНаПечать.ДанныеШапки, ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля, ДанныеПолучаемыеНаХоду);
	
	ВывестиВДокументРезультатОбластиМакета(ДокументРезультат, РезультатКомпоновки.МакетПечатнойФормы, "Подвал",
		ДанныеНаПечать.ДанныеШапки, ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля);
	
КонецПроцедуры

Процедура ВывестиСотрудникаТ51(НомерСтроки, СтрокаГоловногоСотрудника, ДокументРезультат, РезультатКомпоновки, ПромежуточныеИтоги, КВыплатеПоВедомости, ЕстьОплатаВНатуральнойФорме, ДанныеНаПечать)
	
	ОбластьСтрокСотрудника = Новый ТабличныйДокумент;
	
	ПромежуточныеИтогиПоГоловномуСотруднику = ПромежуточныеИтогиПоСтраницеТ51();
	НесколькоРабочихМест = СтрокаГоловногоСотрудника.Строки.Количество() > 1;
	
	ДанныеПользовательскихПолейГоловногоСотрудника = ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, СтрокаГоловногоСотрудника);
	Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
		
		ОбластьСтроки = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("Строка");
		
		ДанныеПолучаемыеНаХоду = Новый Структура;
		ДанныеПолучаемыеНаХоду.Вставить("НомерПП", НомерСтроки + 1);
		
		ДанныеПользовательскихПолей = ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, СтрокаСотрудника);
		
		// Отработанное рабочее время
		ОтработаноДней = ДанныеПользовательскихПолей.ОтработаноРабочихДней;
		Если ЗначениеЗаполнено(ОтработаноДней) И ОтработаноДней > 0 Тогда 
			ОтработаноРабочихДней = "" + ОтработаноДней + " (" + ДанныеПользовательскихПолей.ОтработаноРабочихЧасов + ")";
		Иначе
			ОтработаноРабочихДней = "";
		КонецЕсли;
		ДанныеПолучаемыеНаХоду.Вставить("ОтработаноРабочихДней", ОтработаноРабочихДней);
		
		// Отработанные выходные и праздничные
		ОтработаноВыходныхДней = ДанныеПользовательскихПолей.ОтработаноПразднВыходныхДней;
		Если ЗначениеЗаполнено(ОтработаноВыходныхДней) И ОтработаноВыходныхДней > 0 Тогда 
			ОтработаноПразднВыходныхДней = "" + ОтработаноВыходныхДней + " (" + ДанныеПользовательскихПолей.ОтработаноПразднВыходныхЧасов + ")";
		Иначе
			ОтработаноПразднВыходныхДней = "";
		КонецЕсли;
		ДанныеПолучаемыеНаХоду.Вставить("ОтработаноПразднВыходныхДней", ОтработаноПразднВыходныхДней);
		
		// К выплате
		КВыплате = ДанныеПользовательскихПолей.КВыплате;
		Если КВыплате < 0 Тогда
			ДанныеПолучаемыеНаХоду.Вставить("КВыплате", 0);
		Иначе
			КВыплатеПоВедомости = КВыплатеПоВедомости + КВыплате;
		КонецЕсли;
		
		Если ЕстьОплатаВНатуральнойФорме Тогда
			
			ДанныеПолучаемыеНаХоду.Вставить("НачисленоСдельно", ДанныеПользовательскихПолей.НачисленоВНатуральнойФорме);
			ДанныеПолучаемыеНаХоду.Вставить("ПрочиеДоходы", ДанныеПользовательскихПолей.ПрочиеДоходы + ДанныеПользовательскихПолей.НачисленоСдельно);
			
		КонецЕсли;
		
		Если НесколькоРабочихМест Тогда
			
			// Обнуляем данные, учтенные у головного сотрудника
			ДанныеПользовательскихПолей.Удалить("ДолгЗаОрганизацией");
			ДанныеПользовательскихПолей.Удалить("ДолгЗаСотрудником");
			ДанныеПользовательскихПолей.Удалить("ВсегоНачислено");
			ДанныеПользовательскихПолей.Удалить("НДФЛ");
			ДанныеПользовательскихПолей.Удалить("ПрочиеУдержания");
			ДанныеПользовательскихПолей.Удалить("ВсегоУдержано");
			ДанныеПользовательскихПолей.Удалить("КВыплате");
			
		КонецЕсли;
		
		ЗаполнитьПараметрыОбластиМакета(ОбластьСтроки, ДанныеНаПечать.ДанныеШапки, ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля,
			ДанныеПользовательскихПолейГоловногоСотрудника, СтрокаСотрудника, ДанныеПользовательскихПолей, ДанныеПолучаемыеНаХоду);
		
		ОбластьСтрокСотрудника.Вывести(ОбластьСтроки);
		
		// Обновление итогов
		ДобавитьВПромежуточныйИтог(ПромежуточныеИтогиПоГоловномуСотруднику, ОбластьСтроки.Параметры);
	
	КонецЦикла;
	
	// В случае, когда сотрудник попадает в отчет из за взаимозакрывающихся остатков по подразделениям
	Если Не ЕстьИтогиПоГоловномуСотруднику(ПромежуточныеИтогиПоГоловномуСотруднику) Тогда
		Возврат;
	КонецЕсли;
	
	НомерСтроки = НомерСтроки + 1;
	Если НесколькоРабочихМест Тогда
		
		// Номер по порядку
		ОбластьСтрокСотрудника.Область(1, 1, ОбластьСтрокСотрудника.ВысотаТаблицы, 1).Объединить();
		// Табельный номер
		ОбластьСтрокСотрудника.Область(1, 2, ОбластьСтрокСотрудника.ВысотаТаблицы, 2).Объединить();
		// ФИО
		ОбластьСтрокСотрудника.Область(1, 3, ОбластьСтрокСотрудника.ВысотаТаблицы, 3).Объединить();
		// Всего начислено
		ОбластьСтрокСотрудника.Область(1, 17, ОбластьСтрокСотрудника.ВысотаТаблицы, 17).Объединить();
		// НДФЛ
		ОбластьСтрокСотрудника.Область(1, 18, ОбластьСтрокСотрудника.ВысотаТаблицы, 19).Объединить();
		// Прочие удержания
		ОбластьСтрокСотрудника.Область(1, 20, ОбластьСтрокСотрудника.ВысотаТаблицы, 20).Объединить();
		// Всего удержано
		ОбластьСтрокСотрудника.Область(1, 21, ОбластьСтрокСотрудника.ВысотаТаблицы, 22).Объединить();
		// Задолженность за организацией.
		ОбластьСтрокСотрудника.Область(1, 23, ОбластьСтрокСотрудника.ВысотаТаблицы, 23).Объединить();
		// Задолженность за работников.
		ОбластьСтрокСотрудника.Область(1, 24, ОбластьСтрокСотрудника.ВысотаТаблицы, 24).Объединить();
		// К выплате
		ОбластьСтрокСотрудника.Область(1, 25, ОбластьСтрокСотрудника.ВысотаТаблицы, 25).Объединить();
		
	КонецЕсли;
	
	МассивВыводимыхОбластей = Новый Массив;
	МассивВыводимыхОбластей.Добавить(ОбластьСтрокСотрудника);
	МассивВыводимыхОбластей.Добавить(РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("ИтогоПоСтранице"));
	МассивВыводимыхОбластей.Добавить(РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("ИтогоПоВедомости"));
	МассивВыводимыхОбластей.Добавить(РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("Подвал"));
	Если Не ДокументРезультат.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
		
		ВывестиВДокументРезультатОбластиМакета(ДокументРезультат, РезультатКомпоновки.МакетПечатнойФормы, "ИтогоПоСтранице",
			ДанныеНаПечать.ДанныеШапки, ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля, ПромежуточныеИтоги);
		
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		
		ВывестиВДокументРезультатОбластиМакета(ДокументРезультат, РезультатКомпоновки.МакетПечатнойФормы, "Шапка",
			ДанныеНаПечать.ДанныеШапки, ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля, ПромежуточныеИтоги);
		
		ПромежуточныеИтоги = ПромежуточныеИтогиПоСтраницеТ51();
		
	КонецЕсли;
	
	ДобавитьВПромежуточныйИтог(ПромежуточныеИтоги, ПромежуточныеИтогиПоГоловномуСотруднику);
	ДокументРезультат.Вывести(ОбластьСтрокСотрудника);
	
КонецПроцедуры

Процедура ДополнитьСтраницуТ51(МакетПечатнойФормы, ДокументРезультат, ДанныеНаПечать, ПромежуточныеИтоги)
	
	МассивВыводимыхОбластей = Новый Массив;
	МассивВыводимыхОбластей.Добавить(МакетПечатнойФормы.ПолучитьОбласть("ПустаяСтрока"));
	МассивВыводимыхОбластей.Добавить(МакетПечатнойФормы.ПолучитьОбласть("ИтогоПоСтранице"));
	МассивВыводимыхОбластей.Добавить(МакетПечатнойФормы.ПолучитьОбласть("ИтогоПоВедомости"));
	МассивВыводимыхОбластей.Добавить(МакетПечатнойФормы.ПолучитьОбласть("Подвал"));
	
	Пока ДокументРезультат.ПроверитьВывод(МассивВыводимыхОбластей) Цикл
		
		ВывестиВДокументРезультатОбластиМакета(ДокументРезультат, МакетПечатнойФормы, "ПустаяСтрока",
			ДанныеНаПечать.ДанныеШапки, ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля);
		
	КонецЦикла;
	
	ВывестиВДокументРезультатОбластиМакета(ДокументРезультат, МакетПечатнойФормы, "ИтогоПоСтранице",
		ДанныеНаПечать.ДанныеШапки, ДанныеНаПечать.ДанныеШапкиПользовательскиеПоля, ПромежуточныеИтоги);
	
КонецПроцедуры

Функция ПромежуточныеИтогиПоСтраницеТ51()
	
	ПромежуточныеИтоги = Новый Структура;
	
	ПромежуточныеИтоги.Вставить("НачисленоПовременно", 0);
	ПромежуточныеИтоги.Вставить("НачисленоСдельно", 0);
	ПромежуточныеИтоги.Вставить("ПрочиеДоходы", 0);
	ПромежуточныеИтоги.Вставить("ДругиеДоходы", 0);
	ПромежуточныеИтоги.Вставить("ВсегоНачислено", 0);
	ПромежуточныеИтоги.Вставить("НДФЛ", 0);
	ПромежуточныеИтоги.Вставить("ПрочиеУдержания", 0);
	ПромежуточныеИтоги.Вставить("ВсегоУдержано", 0);
	ПромежуточныеИтоги.Вставить("ДолгЗаОрганизацией", 0);
	ПромежуточныеИтоги.Вставить("ДолгЗаСотрудником", 0);
	ПромежуточныеИтоги.Вставить("КВыплате", 0);
	
	Возврат ПромежуточныеИтоги;
	
КонецФункции

#КонецОбласти


#Область РасчетныйЛисток

Процедура ПриКомпоновкеРезультатаРасчетныйЛисток(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс = Ложь) Экспорт
	
	КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
	КлючВарианта = КлючВарианта(КомпоновщикНастроек);
	
	Если (КлючВарианта = "РасчетныйЛистокПоРабочимМестам")
		Или (КлючВарианта = "РасчетныйЛистокПоРабочимМестамИСРазбивкойПоИсточникамФинансирования") Тогда
		
		ПриКомпоновкеРезультатаРасчетныйЛистокПоРабочимМестам(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс)
		
	Иначе
		ПриКомпоновкеРезультатаРасчетныйЛистокПоФизическимЛицам(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс);
	КонецЕсли;
	
КонецПроцедуры

#Область РасчетныйЛистокПоФизическимЛицам

Процедура ПриКомпоновкеРезультатаРасчетныйЛистокПоФизическимЛицам(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс = Ложь)
	
	СтандартнаяОбработка = Ложь;
	
	Попытка
		
		ДополнительныеПараметры = Новый Структура;
		
		ДанныеРасчетныхЛистков = Новый ТаблицаЗначений;
		ДанныеРасчетныхЛистков.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
		ДанныеРасчетныхЛистков.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		ДанныеРасчетныхЛистков.Колонки.Добавить("ПредставлениеРабочегоМеста");
		ДанныеРасчетныхЛистков.Колонки.Добавить("Группа", Новый ОписаниеТипов("ПеречислениеСсылка.ГруппыНачисленияУдержанияВыплаты"));
		ДанныеРасчетныхЛистков.Колонки.Добавить("ВидРасчета");
		ДанныеРасчетныхЛистков.Колонки.Добавить("ВидРасчетаКраткоеНаименование");
		ДанныеРасчетныхЛистков.Колонки.Добавить("РегистраторВыплаты");
		ДанныеРасчетныхЛистков.Колонки.Добавить("ПриоритетВидаРасчета");
		ДанныеРасчетныхЛистков.Колонки.Добавить("ПериодДействия");
		ДанныеРасчетныхЛистков.Колонки.Добавить("ОплаченныеДниЧасы");
		ДанныеРасчетныхЛистков.Колонки.Добавить("Сумма");
		
		ДанныеРасчетныхЛистковСтруктура = Новый Структура("ОбщиеДанныеРасчетныхЛистков,ДанныеРасчетныхЛистков", Новый Соответствие, ДанныеРасчетныхЛистков);
		ДополнительныеПараметры.Вставить("ДанныеРасчетныхЛистковСтруктура", ДанныеРасчетныхЛистковСтруктура);
		
		КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
		СхемаКомпоновкиДанных = ОбъектОтчета.СхемаКомпоновкиДанных;
		
		КлючВарианта = КлючВарианта(КомпоновщикНастроек);
		
		УправленческийРасчетныйЛисток = (КлючВарианта = "РасчетныйЛистокУправленческий");
		
		// Параметры документа
		ДокументРезультат.ТолькоПросмотр = Истина;
		ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РасчетныйЛисток";
		
		// Подготовка настроек
		НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			Модуль.ДополнитьНастройкиОтчетаРасчетныйЛисток(НастройкиОтчета, КлючВарианта);
		КонецЕсли;
		
		ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс);
		НастроитьВариантОтчетаРасчетныйЛисток(НастройкиОтчета);
		
		ПредварительныйПросмотр = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ПредварительныйПросмотр");
		Если ПредварительныйПросмотр Тогда
			
			НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(
				Новый ПараметрКомпоновкиДанных("ВыводитьОсобенностиРасчетаНДФЛ")).Значение = Ложь;
			
		КонецЕсли;
		
		НастройкиВывода = НастройкиВыводаРасчетногоЛистка(НастройкиОтчета, КлючВарианта, ПредварительныйПросмотр, НаАванс);
		ДополнительныеПараметры.Вставить("НастройкиВывода", НастройкиВывода);
		
		ОтключитьНеИспользуемыеПоляРасчетногоЛистка(НастройкиОтчета, НастройкиВывода);
		
		// Подготовка внешнего набора данных
		Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ДанныеДокумента") Тогда
			
			НаборыВнешнихДанных = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.ДанныеДокумента;
			КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Удалить("ДанныеДокумента");
			
		Иначе
			НаборыВнешнихДанных = НаборыВнешнихДанныхАнализНачисленийИУдержаний();
		КонецЕсли;
		
		Если Не ПредварительныйПросмотр И Не УправленческийРасчетныйЛисток Тогда
			
			ГруппировкиОтчета = Новый Массив;
			
			Если НастройкиВывода.ГруппировкаОтчета = "ПоСотрудникамИОрганизациям" Тогда
				ГруппировкиОтчета.Добавить("ФизическоеЛицо");
				ГруппировкиОтчета.Добавить("ОрганизацияСортировки");
				ГруппировкиОтчета.Добавить("ПодразделениеСортировки");
				ГруппировкиОтчета.Добавить("МесяцНачисления");
			ИначеЕсли НастройкиВывода.ГруппировкаОтчета = "ПоОрганизациямИПодразделениям" Тогда
				ГруппировкиОтчета.Добавить("МесяцНачисления");
				ГруппировкиОтчета.Добавить("ОрганизацияСортировки");
				ГруппировкиОтчета.Добавить("ПодразделениеСортировки");
				ГруппировкиОтчета.Добавить("ФизическоеЛицо");
			Иначе // По умолчанию - "ПоОрганизациям"
				ГруппировкиОтчета.Добавить("МесяцНачисления");
				ГруппировкиОтчета.Добавить("ОрганизацияСортировки");
				ГруппировкиОтчета.Добавить("ФизическоеЛицо");
				ГруппировкиОтчета.Добавить("ПодразделениеСортировки");
			КонецЕсли;
			
			Для ИндексГруппировки = 0 По ГруппировкиОтчета.Количество() -1 Цикл
				
				ГруппировкаУстановлена = Ложь;
				ГруппировкаОтчета = ГруппировкиОтчета[ИндексГруппировки];
				Для Каждого ЭлементПорядка Из НастройкиОтчета.Порядок.Элементы Цикл
					
					Если СтрНачинаетсяС(Строка(ЭлементПорядка.Поле), ГруппировкаОтчета) Тогда
						
						ЭлементПорядка.Использование = Истина;
						
						ИндексЭлемента = НастройкиОтчета.Порядок.Элементы.Индекс(ЭлементПорядка);
						Если ИндексЭлемента <> ИндексГруппировки Тогда
							НастройкиОтчета.Порядок.Элементы.Сдвинуть(ЭлементПорядка, ИндексГруппировки - ИндексЭлемента);
						КонецЕсли;
						
						ГруппировкаУстановлена = Истина;
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Если Не ГруппировкаУстановлена Тогда
					
					Попытка
					
						ЭлементПорядка = НастройкиОтчета.Порядок.Элементы.Вставить(ИндексГруппировки, Тип("ЭлементПорядкаКомпоновкиДанных"));
						ЭлементПорядка.Использование = Истина;
						ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных(ГруппировкаОтчета);
						
					Исключение
					
						ВызватьИсключение НСтр("ru = 'В настройку отчета ""Расчетный листок"" внесены критичные изменения. Отчет не будет сформирован.'")
							+ " " + КраткоеПредставлениеОшибки(НСтр("ru='Нарушена структура (порядок сортировки полей) отчета'"));
					
					КонецПопытки;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		РезультатКомпоновки = РезультатКомпоновкиМакетаПечатнойФормы(ОбъектОтчета, ДанныеРасшифровки, НастройкиОтчета, НаборыВнешнихДанных);
		
		Если ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ОтчетПустой")
			И ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.ОтчетПустой = Истина Тогда
			Возврат;
		КонецЕсли;
		
		СвойстваРегистраторов = СвойстваРегистраторовДляРасчетныхЛистков(РезультатКомпоновки.ДанныеОтчета, НастройкиВывода);
		ДополнительныеПараметры.Вставить("СвойстваРегистраторов", СвойстваРегистраторов);
		
		ДанныеПоНДФЛИВзносам = Новый Соответствие;
		ДополнительныеПараметры.Вставить("ДанныеПоНДФЛИВзносам", ДанныеПоНДФЛИВзносам);
		Если НастройкиВывода.ВыводитьОсобенностиРасчетаНДФЛ
			Или НастройкиВывода.ВыводитьИнформациюОНачисленныхВзносахВПФР Тогда
			
			МесяцыНачислений = ОбщегоНазначения.ВыгрузитьКолонку(РезультатКомпоновки.ДанныеОтчета.Строки, "МесяцНачисления", Истина);
			Для Каждого МесяцНачисления Из МесяцыНачислений Цикл
				ДанныеПоНДФЛИВзносам.Вставить(МесяцНачисления,
					ДанныеПоНДФЛИВзносамПоФизическимЛицам(РезультатКомпоновки.ДанныеОтчета.Строки, МесяцНачисления, НастройкиВывода.ВыводитьОсобенностиРасчетаНДФЛ, НастройкиВывода.ВыводитьИнформациюОНачисленныхВзносахВПФР));
			КонецЦикла;
			
		КонецЕсли;
		
		Если НастройкиВывода.РасшифровыватьСдельныйЗаработокПоВидамРабот Тогда
			СдельныйЗаработокСотрудников = ЗарплатаКадрыОтчетыВнутренний.СдельныйЗаработокСотрудников(РезультатКомпоновки.ДанныеОтчета.Строки);
		Иначе
			СдельныйЗаработокСотрудников = Неопределено;
		КонецЕсли;
		ДополнительныеПараметры.Вставить("СдельныйЗаработокСотрудников", СдельныйЗаработокСотрудников);
		
		Если ПредварительныйПросмотр Тогда
			ДокументРезультат.Вывести(РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("ПредварительныйПросмотр"));
		КонецЕсли;
		
		ДокументРезультатСтраница = Новый ТабличныйДокумент;
		УстановитьПоОбразцуПараметрыСтраницы(ДокументРезультатСтраница, ДокументРезультат);
		
		ВыводимыеГруппировки = Новый Массив;
		
		ОрганизацияСортировки = Неопределено;
		ПодразделениеСортировки = Неопределено;
		Для каждого СтрокаМесяца Из РезультатКомпоновки.ДанныеОтчета.Строки Цикл
			
			Если Не УправленческийРасчетныйЛисток Тогда
				
				Если Не ЗначениеЗаполнено(НастройкиВывода.ГруппировкаОтчета)
					Или НастройкиВывода.ГруппировкаОтчета = "ПоОрганизациям"
					Или НастройкиВывода.ГруппировкаОтчета = "ПоОрганизациямИПодразделениям" Тогда
					
					Если ОрганизацияСортировки <> СтрокаМесяца. ОрганизацияСортировки Тогда
						
						ПараметрыГруппировки = Новый Структура;
						ПараметрыГруппировки.Вставить("НазваниеГруппировки", НСтр("ru='Организация'"));
						ПараметрыГруппировки.Вставить("ЗначениеГруппировки", СтрокаМесяца.ОрганизацияСортировки);
						
						Область = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("Группировка");
						Область.Параметры.Заполнить(ПараметрыГруппировки);
						
						ВыводимыеГруппировки.Добавить(Область);
						ОрганизацияСортировки = СтрокаМесяца.ОрганизацияСортировки;
						
					КонецЕсли;
					
				КонецЕсли;
				
				Если НастройкиВывода.ГруппировкаОтчета = "ПоОрганизациямИПодразделениям" Тогда
					
					Если ПодразделениеСортировки <> СтрокаМесяца.ПодразделениеСортировки Тогда
						
						ПараметрыГруппировки = Новый Структура;
						ПараметрыГруппировки.Вставить("НазваниеГруппировки", НСтр("ru='Подразделение'"));
						ПараметрыГруппировки.Вставить("ЗначениеГруппировки", СтрокаМесяца.ПодразделениеСортировки);
						
						Область = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("Группировка");
						Область.Параметры.Заполнить(ПараметрыГруппировки);
						
						ВыводимыеГруппировки.Добавить(Область);
						ПодразделениеСортировки = СтрокаМесяца.ПодразделениеСортировки;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			ВыводимаяКоллекция = Новый Массив;
			Для Каждого СтрокаСотрудника Из СтрокаМесяца.Строки Цикл
				ВыводимаяКоллекция.Добавить(СтрокаСотрудника);
			КонецЦикла;
			
			ВывестиСотрудникаРасчетныйЛисток(ВыводимаяКоллекция, ДокументРезультат, ДокументРезультатСтраница, РезультатКомпоновки.МакетПечатнойФормы,
				РезультатКомпоновки.ИдентификаторыМакета, ВыводимыеГруппировки, НаАванс, ДополнительныеПараметры);
			
		КонецЦикла;
		
		Если ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ГотовитьДанныеДляКабинетаСотрудников")
			И ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.ГотовитьДанныеДляКабинетаСотрудников = Истина Тогда
			
			ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ДанныеРасчетныхЛистков", ДанныеРасчетныхЛистковСтруктура);
		КонецЕсли;
		
		Если ДокументРезультатСтраница.ВысотаТаблицы > 0 Тогда
			
			Если Не ПредварительныйПросмотр И ДокументРезультат.ВысотаТаблицы > 0 Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ДокументРезультат.Вывести(ДокументРезультатСтраница);
			
		КонецЕсли;
		
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение НСтр("ru = 'В настройку отчета ""Расчетный листок"" внесены критичные изменения. Отчет не будет сформирован.'")
			+ " " + КраткоеПредставлениеОшибки(Инфо);
		
	КонецПопытки;
	
КонецПроцедуры

Функция СвойстваРегистраторовДляРасчетныхЛистков(ДанныеОтчета, НастройкиВывода)
	
	// Сбор информации о регистраторах выплат
	СвойстваРегистраторов = Новый Соответствие;
	
	МетаданныеРегистраторов = Новый Соответствие;
	МетаданныеСПериодомРегистрации = Новый Соответствие;
	
	РегистраторыСоСпособомВыплаты = Новый Массив;
	РегистраторыСПериодомРегистрации = Новый Массив;
	РегистраторыБезПериодаРегистрации = Новый Массив;
	
	СтрокиВыплат = ДанныеОтчета.Строки.НайтиСтроки(Новый Структура("Группа", Перечисления.ГруппыНачисленияУдержанияВыплаты.Выплачено), Истина);
	Для Каждого СтрокаВыплат Из СтрокиВыплат Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаВыплат.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		МетаданныеРегистратора = МетаданныеРегистраторов.Получить(СтрокаВыплат.Регистратор);
		Если МетаданныеРегистратора = Неопределено Тогда
			
			МетаданныеРегистратора = СтрокаВыплат.Регистратор.Метаданные();
			МетаданныеРегистраторов.Вставить(СтрокаВыплат.Регистратор, МетаданныеРегистратора);
			
		КонецЕсли;
		
		СвойстваМетаданных = МетаданныеСПериодомРегистрации.Получить(МетаданныеРегистратора);
		Если СвойстваМетаданных = Неопределено Тогда
			
			СвойстваМетаданных = Новый Структура("РегистраторСоСпособомВыплаты,РегистраторСПериодомРегистрации", Ложь, Ложь);
			СвойстваМетаданных.РегистраторСоСпособомВыплаты =
				ОбщегоНазначения.ЕстьРеквизитОбъекта("СпособВыплаты", МетаданныеРегистратора);
			СвойстваМетаданных.РегистраторСПериодомРегистрации =
				ОбщегоНазначения.ЕстьРеквизитОбъекта("ПериодРегистрации", МетаданныеРегистратора);
			
			МетаданныеСПериодомРегистрации.Вставить(МетаданныеРегистратора, СвойстваМетаданных);
			
		КонецЕсли;
		
		Если СвойстваМетаданных.РегистраторСоСпособомВыплаты Тогда
			РегистраторыСоСпособомВыплаты.Добавить(СтрокаВыплат.Регистратор);
		ИначеЕсли СвойстваМетаданных.РегистраторСПериодомРегистрации Тогда
			РегистраторыСПериодомРегистрации.Добавить(СтрокаВыплат.Регистратор);
		Иначе
			РегистраторыБезПериодаРегистрации.Добавить(СтрокаВыплат.Регистратор);
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если РегистраторыСоСпособомВыплаты.Количество() > 0 Тогда
		СвойстваРегистраторов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(РегистраторыСоСпособомВыплаты, "Дата,Номер,СпособВыплаты,ПериодРегистрации");
	КонецЕсли;
	
	Если РегистраторыСПериодомРегистрации.Количество() > 0 Тогда
		
		СвойстваРегистраторовСПериодомРегистрации = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(РегистраторыСПериодомРегистрации, "Дата,Номер,ПериодРегистрации");
		Если СвойстваРегистраторов.Количество() = 0 Тогда
			СвойстваРегистраторов = СвойстваРегистраторовСПериодомРегистрации;
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(СвойстваРегистраторов, СвойстваРегистраторовСПериодомРегистрации, Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	Если РегистраторыБезПериодаРегистрации.Количество() > 0 Тогда
		
		СвойстваРегистраторовБезПериодаРегистрации = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(РегистраторыБезПериодаРегистрации, "Дата,Номер");
		Если СвойстваРегистраторов.Количество() = 0 Тогда
			СвойстваРегистраторов = СвойстваРегистраторовБезПериодаРегистрации;
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(СвойстваРегистраторов, СвойстваРегистраторовБезПериодаРегистрации, Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СвойстваРегистраторов;
	
КонецФункции

Процедура ВывестиСотрудникаРасчетныйЛисток(СтрокиГоловныхСотрудников, ДокументРезультатПромежуточный, ДокументРезультатСтраница, Макет, СоответствиеПользовательскихПолей, ВыводимыеГруппировки, НаАванс, ДополнительныеПараметры);
	
	НастройкиВывода = ДополнительныеПараметры.НастройкиВывода;
	
	НастройкиПечатныхФорм = ЗарплатаКадры.НастройкиПечатныхФорм();
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	СтрокаПервогоГоловногоСотрудника = СтрокиГоловныхСотрудников[0];
	Если НастройкиВывода.ГруппироватьПоИсточникамФинансирования Тогда
		СтрокаШапки = СтрокаПервогоГоловногоСотрудника.Строки[0].Строки[0];
	Иначе
		СтрокаШапки = СтрокаПервогоГоловногоСотрудника.Строки[0];
	КонецЕсли;
	
	ДанныеПоНДФЛИВзносам = ДополнительныеПараметры.ДанныеПоНДФЛИВзносам.Получить(СтрокаШапки.МесяцНачисления);
	
	ДанныеПечатнойФормы = Новый Массив;
	ДанныеПечатнойФормы.Добавить(СтрокаШапки);
	ДанныеПечатнойФормы.Добавить(ЗначенияЗаполненияПользовательскихПолей(СоответствиеПользовательскихПолей, СтрокаШапки));
	
	Если НастройкиВывода.ВыводитьОсобенностиРасчетаНДФЛ Тогда
		
		СтрокиДанныхПоНДФЛ = ДанныеПоНДФЛИВзносам.НДФЛ.НайтиСтроки(Новый Структура("ФизическоеЛицо,ГоловнаяОрганизация", СтрокаШапки.ФизическоеЛицо, СтрокаШапки.ОрганизацияГоловнаяОрганизация));
		Если СтрокиДанныхПоНДФЛ.Количество() > 0 Тогда
			
			СтрокаДанныхПоНДФЛ = СтрокиДанныхПоНДФЛ[0];
			ДанныеПечатнойФормы.Добавить(СтрокаДанныхПоНДФЛ);
			
		Иначе
			СтрокаДанныхПоНДФЛ = Неопределено;
		КонецЕсли;
		
	Иначе
		СтрокаДанныхПоНДФЛ = Неопределено;
	КонецЕсли;
	
	ДанныеВычисляемыеНаХоду = ОписаниеОбщихИтоговРасчетногоЛисткаПоФизическимЛицам();
	ДанныеПечатнойФормы.Добавить(ДанныеВычисляемыеНаХоду);
	
	ВсегоВзносов = 0;
	Если НастройкиВывода.ВыводитьИнформациюОНачисленныхВзносахВПФР Тогда
		
		Если НастройкиВывода.ГруппироватьПоИсточникамФинансирования Тогда
			
			ГоловныеОрганизации = Новый Массив;
			Для каждого СтрокаСтатьиФинансирования Из СтрокиГоловныхСотрудников Цикл
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ГоловныеОрганизации, ОбщегоНазначения.ВыгрузитьКолонку(СтрокаСтатьиФинансирования.Строки, "ОрганизацияГоловнаяОрганизация", Истина), Истина);
			КонецЦикла;
			
		Иначе
			ГоловныеОрганизации = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиГоловныхСотрудников, "ОрганизацияГоловнаяОрганизация", Истина);
		КонецЕсли;
		
		ПоляВзносов = СтрРазделить(УчетСтраховыхВзносовКлиентСервер.РассчитываемыеВзносыВПФР(), ",");
		Для каждого ГоловнаяОрганизации Из ГоловныеОрганизации Цикл
			
			СтрокиДанныхПоВзносам = ДанныеПоНДФЛИВзносам.Взносы.НайтиСтроки(Новый Структура("ФизическоеЛицо,ГоловнаяОрганизация", СтрокаШапки.ФизическоеЛицо, ГоловнаяОрганизации));
			Если СтрокиДанныхПоВзносам.Количество() > 0 Тогда
				
				СтрокаДанныхПоВзносам = СтрокиДанныхПоВзносам[0];
				Для каждого ПолеВзносов Из  ПоляВзносов Цикл
					ВсегоВзносов = ВсегоВзносов + СтрокаДанныхПоВзносам[ПолеВзносов];
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДанныеВычисляемыеНаХоду.Вставить("ВсегоВзносов", ВсегоВзносов);
	
	РазделительЛистков = Новый ТабличныйДокумент;
	ВывестиОбластиСтроки(РазделительЛистков, Макет, "РазделительЛистков", НастройкиВывода);
	
	ТелоРасчетногоЛистка = Новый ТабличныйДокумент;
	Если НастройкиВывода.ГруппироватьПоИсточникамФинансирования Тогда
		ИтогиПоГруппам = ВывестиГруппировкиПоСтатьямФинансирования(ТелоРасчетногоЛистка, Макет, СтрокиГоловныхСотрудников,
			СоответствиеПользовательскихПолей, ДанныеПечатнойФормы, ДополнительныеПараметры);
	Иначе
		ИтогиПоГруппам = ВывестиТелоРасчетногоЛистка(ТелоРасчетногоЛистка, Макет, СтрокиГоловныхСотрудников,
			СоответствиеПользовательскихПолей, ДанныеПечатнойФормы, ДополнительныеПараметры);
	КонецЕсли;
	
	// Остатки взаиморасчетов по НДФЛ
	Если СтрокаДанныхПоНДФЛ <> Неопределено
		И (ИтогиПоГруппам.СуммаДолгаНаНачалоМесяцаРасчета > 0
			Или ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета > 0) Тогда
		
		ДанныеВычисляемыеНаХоду.ИзлишнеУдержаноНДФЛНаНачалоМесяца = ИтогиПоГруппам.СуммаДолгаНаНачалоМесяцаРасчета - СтрокаДанныхПоНДФЛ.ЗарплатаКВыплатеНаНачалоПериода;
		ДанныеВычисляемыеНаХоду.ИзлишнеУдержаноНДФЛНаКонецМесяца = ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета - СтрокаДанныхПоНДФЛ.ЗарплатаКВыплатеНаКонецПериода;
		
		Если ДанныеВычисляемыеНаХоду.ИзлишнеУдержаноНДФЛНаНачалоМесяца < 0 Тогда
			ДанныеВычисляемыеНаХоду.ИзлишнеУдержаноНДФЛНаНачалоМесяца = 0;
		КонецЕсли;
		
		Если ДанныеВычисляемыеНаХоду.ИзлишнеУдержаноНДФЛНаКонецМесяца < 0 Тогда
			ДанныеВычисляемыеНаХоду.ИзлишнеУдержаноНДФЛНаКонецМесяца = 0;
		КонецЕсли;
		
	КонецЕсли;
	
	// Взаимные задолженности взаиморасчетов
	ЗаполнитьОбщиеИтоги(ДанныеВычисляемыеНаХоду, ИтогиПоГруппам);
	
	#Область ВыводШапки
	
	Если НаАванс Тогда
		
		ИмяОбластиШапка = "ШапкаПерваяПоловина";
		
		ДанныеВычисляемыеНаХоду.Вставить("КВыплатеЗаПервуюПоловинуМесяца", 0);
		Для каждого СтрокаСотрудника Из СтрокиГоловныхСотрудников Цикл
			ДанныеВычисляемыеНаХоду.КВыплатеЗаПервуюПоловинуМесяца = ДанныеВычисляемыеНаХоду.КВыплатеЗаПервуюПоловинуМесяца + СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("КВыплатеЗаПервуюПоловинуМесяца")];
		КонецЦикла;
		
	Иначе
		
		ИмяОбластиШапка = "Шапка";
		
		ДанныеВычисляемыеНаХоду.Вставить("КВыплатеЗарплаты", 0);
		Для каждого СтрокаСотрудника Из СтрокиГоловныхСотрудников Цикл
			ДанныеВычисляемыеНаХоду.КВыплатеЗарплаты = ДанныеВычисляемыеНаХоду.КВыплатеЗарплаты + СтрокаСотрудника[СоответствиеПользовательскихПолей.Получить("КВыплатеЗарплаты")];
		КонецЦикла;
		
	КонецЕсли;
	
	ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, ИмяОбластиШапка, НастройкиВывода, ДанныеПечатнойФормы);
	
	Если НастройкиВывода.ВыводитьСуммуНачисленнойЗарплатыВРазделеКВыплате Тогда
		
		ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, ИмяОбластиШапка, НастройкиВывода, ДанныеПечатнойФормы);
		НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено));
		
	Иначе
		НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено));
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ВыводРабочегоМеста
	
	ВывестиОбластиСтроки(ДокументРезультат, Макет, "РабочееМесто", НастройкиВывода, ДанныеПечатнойФормы);
	
	// Объединение ячеек для поля Организация
	НастроитьОбъединение(ДокументРезультат, 5, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) - 4, ДокументРезультат.ВысотаТаблицы - 1);
	
	// Объединение ячеек для поля Подразделение
	НастроитьОбъединение(ДокументРезультат, 5, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) - 4);
	
	// Объединение ячеек для поля Должность
	НастроитьОбъединение(ДокументРезультат, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + 5,
		ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено) - 4, ДокументРезультат.ВысотаТаблицы - 1);
	
	#КонецОбласти
	
	ДокументРезультат.Вывести(ТелоРасчетногоЛистка);
	
	#Область ВыводИтоговПоРасчетномуЛистку
	
	ВывестиИтогиВзаиморасчетов(ДокументРезультат, Макет, НастройкиВывода, ДанныеВычисляемыеНаХоду, ДанныеПечатнойФормы);
	
	// Итоги излишне удержанного НДФЛ
	Если СтрокаДанныхПоНДФЛ <> Неопределено
		И (ДанныеВычисляемыеНаХоду.ИзлишнеУдержаноНДФЛНаНачалоМесяца > 0
			Или ДанныеВычисляемыеНаХоду.ИзлишнеУдержаноНДФЛНаКонецМесяца > 0) Тогда
		
		ВывестиОбластиСтроки(ДокументРезультат, Макет, "ВТомЧислеНДФЛ", НастройкиВывода, ДанныеПечатнойФормы);
		
		НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено, "СуммаНачислено"));
		НастроитьОбъединение(ДокументРезультат, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + 1,
			ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено, "СуммаУдержаноВыплачено"));
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ТекстовойИнформации
	
	ТекстСекции = "";
	
	// Вычеты НДФЛ, авансовые платежи
	Если НастройкиВывода.ВыводитьОсобенностиРасчетаНДФЛ И СтрокаДанныхПоНДФЛ <> Неопределено
		Или НастройкиВывода.ВыводитьИнформациюОНачисленныхВзносахВПФР И ВсегоВзносов <> 0 Тогда
		
		Если НастройкиВывода.ВыводитьИнформациюОНачисленныхВзносахВПФР Тогда
			
			Если ВсегоВзносов <> 0 Тогда
				
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Страховые взносы в ПФР'") + ": " + Формат(ВсегоВзносов, "ЧДЦ=2; ЧН="));
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НастройкиВывода.ВыводитьОсобенностиРасчетаНДФЛ Тогда
			
			Если Не ПустаяСтрока(ТекстСекции) Тогда
				ТекстСекции = ТекстСекции + Символы.ПС;
			КонецЕсли;
			
			ТекстСекции = ТекстСекции
				+ СтрокаСНепереносимымиПробелами(НСтр("ru='Общий облагаемый доход'") + ": " + Формат(СтрокаДанныхПоНДФЛ.ОблагаемыйДоход, "ЧДЦ=2; ЧН="));
			
			Если ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.АвансовыеПлатежи) Тогда
				
				Если Не ПустаяСтрока(ТекстСекции) Тогда
					ТекстСекции = ТекстСекции + "; ";
				КонецЕсли; 
				
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Зачтено авансовых платежей'") + ": " + Формат(СтрокаДанныхПоНДФЛ.АвансовыеПлатежи, "ЧДЦ=2"));
				
			КонецЕсли;
			
			Если Не ПустаяСтрока(ТекстСекции) Тогда
				ТекстСекции = ТекстСекции + Символы.ПС;
			КонецЕсли;
			
			ДобавлятьТочкуСЗапятой = Ложь;
			Если СтрокаДанныхПоНДФЛ.ПоложеныЛичныеВычеты = Истина
				Или ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.ВычетНаФизлицо) Тогда
				
				ДобавлятьТочкуСЗапятой = Истина;
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Вычетов личных'") + ": " + Формат(СтрокаДанныхПоНДФЛ.ВычетНаФизлицо, "ЧДЦ=2; ЧН="));
				
			КонецЕсли;
			
			Если СтрокаДанныхПоНДФЛ.ПоложеныВычетыНаДетей = Истина
				Или ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.ВычетНаДетей) Тогда
				
				Если ДобавлятьТочкуСЗапятой И Не ПустаяСтрока(ТекстСекции) Тогда
					ТекстСекции = ТекстСекции + "; ";
				Иначе
					ДобавлятьТочкуСЗапятой = Истина;
				КонецЕсли;
				
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Вычетов на детей'") + ": " + Формат(СтрокаДанныхПоНДФЛ.ВычетНаДетей, "ЧДЦ=2; ЧН="));
				
			КонецЕсли; 
			
			Если ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.ВычетИмущественный) Тогда
				
				Если ДобавлятьТочкуСЗапятой И Не ПустаяСтрока(ТекстСекции) Тогда
					ТекстСекции = ТекстСекции + "; ";
				Иначе
					ДобавлятьТочкуСЗапятой = Истина;
				КонецЕсли;
				
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Вычетов имущественных'") + ": " + Формат(СтрокаДанныхПоНДФЛ.ВычетИмущественный, "ЧДЦ=2; ЧН="));
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.ВычетСоциальный) Тогда
				
				Если ДобавлятьТочкуСЗапятой И Не ПустаяСтрока(ТекстСекции) Тогда
					ТекстСекции = ТекстСекции + "; ";
				Иначе
					ДобавлятьТочкуСЗапятой = Истина;
				КонецЕсли; 
				
				ТекстСекции = ТекстСекции
					+ СтрокаСНепереносимымиПробелами(НСтр("ru='Вычетов социальных'") + ": " + Формат(СтрокаДанныхПоНДФЛ.ВычетСоциальный, "ЧДЦ=2; ЧН="));
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ПустаяСтрока(ТекстСекции) Тогда
			ВывестиОбластиСтроки(ДокументРезультат, Макет, "ПодвалТаблицы", НастройкиВывода, ДанныеПечатнойФормы);
		КонецЕсли;
		
		ВывестиОбластиТекстовуюОбласть(ДокументРезультат, Макет, НастройкиВывода, СокрЛП(ТекстСекции), ДанныеПечатнойФормы);
		
	КонецЕсли;
	
	#КонецОбласти
	
	ОбщиеДанныеРасчетногоЛиста = Новый Структура(
		"МесяцНачисленияНаПечать,
		|ФизическоеЛицоФИО,
		|РаботаСотрудникТабельныйНомерНаПечать,
		|ОрганизацияГоловногоСотрудникаНаКонецПериода,
		|ПодразделениеГоловногоСотрудникаНаКонецПериодаНаПечать,
		|ДолжностьГоловногоСотрудникаНаКонецПериода,
		|РазрядКатегорияГоловногоСотрудникаНаКонецПериодаНаПечать,
		|ТарифнаяСтавкаНаКонецПериода,
		|КВыплатеЗарплаты,
		|ТекстоваяИнформация");
	
	Для Каждого ДанныеНаПечать Из ДанныеПечатнойФормы Цикл
		ЗаполнитьЗначенияСвойств(ОбщиеДанныеРасчетногоЛиста, ДанныеНаПечать);
	КонецЦикла;
	ОбщиеДанныеРасчетногоЛиста.ТекстоваяИнформация = СокрЛП(ТекстСекции);
	ДополнительныеПараметры.ДанныеРасчетныхЛистковСтруктура.ОбщиеДанныеРасчетныхЛистков.Вставить(СтрокаШапки.ФизическоеЛицо, ОбщиеДанныеРасчетногоЛиста);
	
	// Установка идентификатора на область расчетного листка
	Ид = СтрокаШапки.ФизическоеЛицо.УникальныйИдентификатор();
	ДокументРезультат.Область(1, 1, ДокументРезультат.ВысотаТаблицы, ДокументРезультат.ШиринаТаблицы).Имя =
		"payslip_" + Формат(СтрокаШапки.МесяцНачисления, "ДФ=ММгггг") + "_0_" + СтрЗаменить(Ид, "-", "") + "_0";
	
	ВыведеныГруппировки = Ложь;
	Если ВыводимыеГруппировки.Количество() > 0 Тогда
		
		Если ДокументРезультатПромежуточный.ВысотаТаблицы > 0 Тогда
			ДокументРезультатПромежуточный.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Если ДокументРезультатСтраница.ВысотаТаблицы > 0 Тогда
			ДокументРезультатПромежуточный.Вывести(ДокументРезультатСтраница);
			ДокументРезультатПромежуточный.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ДокументРезультатСтраница = Новый ТабличныйДокумент;
		УстановитьПоОбразцуПараметрыСтраницы(ДокументРезультатСтраница, ДокументРезультатПромежуточный);
		
		Для каждого ВыводимаяОбласть Из ВыводимыеГруппировки Цикл
			ДокументРезультатСтраница.Вывести(ВыводимаяОбласть);
		КонецЦикла;
		
		ВыводимыеГруппировки.Очистить();
		
		ВыведеныГруппировки = Истина;
		
	КонецЕсли;
	
	ВывестиОбластиРасчетногоЛистка(ДокументРезультатПромежуточный, ДокументРезультатСтраница, РазделительЛистков, ДокументРезультат, ВыведеныГруппировки, НастройкиВывода);
	
КонецПроцедуры

Процедура ВывестиОбластиРасчетногоЛистка(ДокументРезультатПромежуточный, ДокументРезультатСтраница, РазделительЛистков, ДокументРезультат, ВыведеныГруппировки, НастройкиВывода)
	
	ВыводимыеОбласти = Новый Массив;
	ВыводимыеОбласти.Добавить(РазделительЛистков);
	ВыводимыеОбласти.Добавить(ДокументРезультат);
	
	Если Не НастройкиВывода.НеЖалетьБумаги
		И (ДокументРезультатСтраница.ВысотаТаблицы = 0 Или ВыведеныГруппировки) Тогда
		
		ВыводимыеОбласти.Удалить(0);
		
	ИначеЕсли НЕ ДокументРезультатСтраница.ПроверитьВывод(ВыводимыеОбласти)
		Или НастройкиВывода.НеЖалетьБумаги Тогда
		
		ДокументРезультатПромежуточный.ВывестиГоризонтальныйРазделительСтраниц();
		ДокументРезультатПромежуточный.Вывести(ДокументРезультатСтраница);
		
		ДокументРезультатСтраница = Новый ТабличныйДокумент;
		УстановитьПоОбразцуПараметрыСтраницы(ДокументРезультатСтраница, ДокументРезультатПромежуточный);
		
		ВыводимыеОбласти.Удалить(0);
		
	КонецЕсли;
	
	Для каждого ВыводимаяОбласть Из ВыводимыеОбласти Цикл
		ДокументРезультатСтраница.Вывести(ВыводимаяОбласть);
	КонецЦикла;
	
КонецПроцедуры

Функция ВывестиГруппировкиПоСтатьямФинансирования(ДокументРезультат, Макет, СтрокиГоловныхСотрудников, СоответствиеПользовательскихПолей, ДанныеПечатнойФормы, ДополнительныеПараметры)
	
	ИтогиПоГруппам = ОписаниеИтоговПоГруппам();
	НастройкиВывода = ДополнительныеПараметры.НастройкиВывода;
	
	// Сбор сведений по всем рабочим местам
	СтатьиФинансирования = Новый Массив;
	СтрокиПоСтатьямФинансирования = Новый Соответствие;
	
	Для каждого СтрокаСотрудника Из СтрокиГоловныхСотрудников Цикл
		
		Для каждого СтрокаСтатьи Из СтрокаСотрудника.Строки Цикл
			
			СтрокиПоСтатьям = СтрокиПоСтатьямФинансирования.Получить(СтрокаСтатьи.СтатьяФинансирования);
			Если СтрокиПоСтатьям = Неопределено Тогда
				СтатьиФинансирования.Добавить(СтрокаСтатьи.СтатьяФинансирования);
				СтрокиПоСтатьям = Новый Массив;
			КонецЕсли;
			
			СтрокиПоСтатьям.Добавить(СтрокаСтатьи);
			СтрокиПоСтатьямФинансирования.Вставить(СтрокаСтатьи.СтатьяФинансирования, СтрокиПоСтатьям);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для каждого СтатьяФинансирования Из СтатьиФинансирования Цикл
		
		СтрокиПоСтатьям = СтрокиПоСтатьямФинансирования.Получить(СтатьяФинансирования);
		
		СтрокаПоСтатье = СтрокиПоСтатьям[0];
		ЗначенияЗаполненияПользовательскихПолейСтрокиПоСтатье = ЗначенияЗаполненияПользовательскихПолей(СоответствиеПользовательскихПолей, СтрокаПоСтатье);
		
		ДанныеПолучаемыеНаХоду = ОписаниеИтоговРасчетногоЛисткаПоФизическимЛицам();
		Если ЗначениеЗаполнено(СтатьяФинансирования) Тогда
			
			ПредставлениеСтатьиФинансирования =
				?(ЗначениеЗаполнено(СтрокаПоСтатье.СтатьяФинансированияКод), "(" + СтрокаПоСтатье.СтатьяФинансированияКод + ")", "")
				+ ?(ЗначениеЗаполнено(СтрокаПоСтатье.СтатьяФинансированияНаименование) , " " + СтрокаПоСтатье.СтатьяФинансированияНаименование, "");
			
			ДанныеПолучаемыеНаХоду.Вставить("СтатьяФинансирования", ПредставлениеСтатьиФинансирования);
			
		КонецЕсли;
		
		ВывестиВДокументРезультатОбластиМакета(ДокументРезультат, Макет, "СтатьяФинансирования", СтрокаПоСтатье, ЗначенияЗаполненияПользовательскихПолейСтрокиПоСтатье, ДанныеПолучаемыеНаХоду);
		
		ИтогиПоСтатье = ВывестиТелоРасчетногоЛистка(ДокументРезультат, Макет, СтрокиПоСтатьям, СоответствиеПользовательскихПолей, ДанныеПечатнойФормы, ДополнительныеПараметры);
		
		ИтогиПоГруппам.Начислено = ИтогиПоГруппам.Начислено + ИтогиПоСтатье.Начислено;
		ИтогиПоГруппам.Удержано = ИтогиПоГруппам.Удержано + ИтогиПоСтатье.Удержано;
		ИтогиПоГруппам.Выплачено = ИтогиПоГруппам.Выплачено + ИтогиПоСтатье.Выплачено;
		ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца = ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца + ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца;
		ИтогиПоГруппам.СуммаДолгаНаКонецМесяца = ИтогиПоГруппам.СуммаДолгаНаКонецМесяца + ИтогиПоСтатье.СуммаДолгаНаКонецМесяца;
		ИтогиПоГруппам.СуммаДолгаНаНачалоМесяцаРасчета = ИтогиПоГруппам.СуммаДолгаНаНачалоМесяцаРасчета + ИтогиПоСтатье.СуммаДолгаНаНачалоМесяцаРасчета;
		ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета = ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета + ИтогиПоСтатье.СуммаДолгаНаКонецМесяцаРасчета;
		
		Если СтатьиФинансирования.Количество() > 1 Тогда
			ЗаполнитьОбщиеИтоги(ДанныеПолучаемыеНаХоду, ИтогиПоСтатье);
			ВывестиИтогиВзаиморасчетов(ДокументРезультат, Макет, НастройкиВывода, ДанныеПолучаемыеНаХоду, ДополненныеПараметры(ДанныеПечатнойФормы, ДанныеПолучаемыеНаХоду));
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтатьиФинансирования.Количество() > 1 Тогда
		
		ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, "ИтогиПоСтатьямФинансирования", НастройкиВывода, ИтогиПоГруппам);
		НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено, "СуммаНачислено"));
		
		ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, "ИтогиПоСтатьямФинансирования", НастройкиВывода, ИтогиПоГруппам);
		НастроитьОбъединение(ДокументРезультат, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + 1,
			ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено, "СуммаУдержаноВыплачено"));
		
	КонецЕсли;
	
	Возврат ИтогиПоГруппам;
	
КонецФункции

Функция ВывестиТелоРасчетногоЛистка(ДокументРезультат, Макет, СтрокиГоловныхСотрудников, СоответствиеПользовательскихПолей, ДанныеПечатнойФормы, ДополнительныеПараметры)
	
	ИтогиПоГруппам = ОписаниеИтоговПоГруппам();
	
	НастройкиВывода = ДополнительныеПараметры.НастройкиВывода;
	СвойстваРегистраторов = ДополнительныеПараметры.СвойстваРегистраторов;
	ДанныеРасчетныхЛистков = ДополнительныеПараметры.ДанныеРасчетныхЛистковСтруктура.ДанныеРасчетныхЛистков;
	
	СтрокиОтчетаНачислено = Новый ТаблицаЗначений;
	СтрокиОтчетаНачислено.Колонки.Добавить("ИмяОбласти");
	СтрокиОтчетаНачислено.Колонки.Добавить("СтрокаДанных");
	СтрокиОтчетаНачислено.Колонки.Добавить("Сотрудник");
	СтрокиОтчетаНачислено.Колонки.Добавить("ФизическоеЛицо");
	СтрокиОтчетаНачислено.Колонки.Добавить("Группа");
	СтрокиОтчетаНачислено.Колонки.Добавить("ПриоритетВидаРасчета");
	СтрокиОтчетаНачислено.Колонки.Добавить("ПредставлениеРабочегоМеста");
	СтрокиОтчетаНачислено.Колонки.Добавить("ВидРасчета");
	СтрокиОтчетаНачислено.Колонки.Добавить("ВидРасчетаКраткоеНаименование");
	СтрокиОтчетаНачислено.Колонки.Добавить("ПериодДействия");
	СтрокиОтчетаНачислено.Колонки.Добавить("ПериодДействияПредставление");
	СтрокиОтчетаНачислено.Колонки.Добавить("ОтработаноДней");
	СтрокиОтчетаНачислено.Колонки.Добавить("ОтработаноЧасов");
	СтрокиОтчетаНачислено.Колонки.Добавить("ОплаченныеДниЧасы");
	СтрокиОтчетаНачислено.Колонки.Добавить("Сумма");
	СтрокиОтчетаНачислено.Колонки.Добавить("РасшифровкаВидаРасчета");
	СтрокиОтчетаНачислено.Колонки.Добавить("ДокументОснование");
	СтрокиОтчетаНачислено.Колонки.Добавить("Показатель");
	СтрокиОтчетаНачислено.Колонки.Добавить("Значение");
	СтрокиОтчетаНачислено.Колонки.Добавить("ВидРабот");
	СтрокиОтчетаНачислено.Колонки.Добавить("ОбъемВыполненныхРабот");
	СтрокиОтчетаНачислено.Колонки.Добавить("Расшифровка", Новый ОписаниеТипов("Булево"));
	
	СтрокиОтчетаУдержано = СтрокиОтчетаНачислено.СкопироватьКолонки();
	
	СтрокиОтчетаВыплачено = СтрокиОтчетаУдержано.СкопироватьКолонки();
	СтрокиОтчетаВыплачено.Колонки.Добавить("РегистраторВыплаты");
	
	#Область СборСведений
	
	ИтогиРазделов = Новый Структура;
	ИтогиРазделов.Вставить("Начислено", 0);
	ИтогиРазделов.Вставить("Удержано", 0);
	ИтогиРазделов.Вставить("Выплачено", 0);
	ИтогиРазделов.Вставить("НачисленоВНатуральнойФорме", 0);
	
	КоллекцияНачислено = Новый ТаблицаЗначений;
	КоллекцияНачислено.Колонки.Добавить("ФизическоеЛицо");
	КоллекцияНачислено.Колонки.Добавить("Сотрудник");
	КоллекцияНачислено.Колонки.Добавить("Группа");
	КоллекцияНачислено.Колонки.Добавить("ВидРасчета");
	КоллекцияНачислено.Колонки.Добавить("ВидРасчетаКраткоеНаименование");
	КоллекцияНачислено.Колонки.Добавить("ПриоритетВидаРасчета");
	КоллекцияНачислено.Колонки.Добавить("ПериодДействия");
	КоллекцияНачислено.Колонки.Добавить("ПериодДействияПредставление");
	КоллекцияНачислено.Колонки.Добавить("МесяцНачисления");
	КоллекцияНачислено.Колонки.Добавить("ОтработаноДней");
	КоллекцияНачислено.Колонки.Добавить("ОтработаноЧасов");
	КоллекцияНачислено.Колонки.Добавить("ОплаченоДней");
	КоллекцияНачислено.Колонки.Добавить("ОплаченоЧасов");
	КоллекцияНачислено.Колонки.Добавить("Сумма");
	КоллекцияНачислено.Колонки.Добавить("ДокументОснование");
	КоллекцияНачислено.Колонки.Добавить("Показатель");
	КоллекцияНачислено.Колонки.Добавить("Значение");
	КоллекцияНачислено.Колонки.Добавить("ПорядокПоказателей");
	
	КоллекцияУдержано = Новый ТаблицаЗначений;
	КоллекцияУдержано.Колонки.Добавить("ФизическоеЛицо");
	КоллекцияУдержано.Колонки.Добавить("Сотрудник");
	КоллекцияУдержано.Колонки.Добавить("Группа");
	КоллекцияУдержано.Колонки.Добавить("ВидРасчета");
	КоллекцияУдержано.Колонки.Добавить("ВидРасчетаКраткоеНаименование");
	КоллекцияУдержано.Колонки.Добавить("ПриоритетВидаРасчета");
	КоллекцияУдержано.Колонки.Добавить("ДокументОснование");
	КоллекцияУдержано.Колонки.Добавить("ПериодДействия");
	КоллекцияУдержано.Колонки.Добавить("Сумма");
	КоллекцияУдержано.Колонки.Добавить("Показатель");
	КоллекцияУдержано.Колонки.Добавить("Значение");
	КоллекцияУдержано.Колонки.Добавить("ПорядокПоказателей");
	// Добавлены для совместимости процедур свертки строк
	КоллекцияУдержано.Колонки.Добавить("ПериодДействияПредставление");
	КоллекцияУдержано.Колонки.Добавить("МесяцНачисления");
	
	КоллекцияВыплачено = Новый ТаблицаЗначений;
	КоллекцияВыплачено.Колонки.Добавить("ФизическоеЛицо");
	КоллекцияВыплачено.Колонки.Добавить("Сотрудник");
	КоллекцияВыплачено.Колонки.Добавить("Группа");
	КоллекцияВыплачено.Колонки.Добавить("РегистраторВыплаты");
	КоллекцияВыплачено.Колонки.Добавить("ПредставлениеРегистратораВыплаты");
	КоллекцияВыплачено.Колонки.Добавить("ВидРасчета");
	КоллекцияВыплачено.Колонки.Добавить("ПриоритетВидаРасчета");
	КоллекцияВыплачено.Колонки.Добавить("ПериодДействия");
	КоллекцияВыплачено.Колонки.Добавить("Сумма");
	КоллекцияВыплачено.Колонки.Добавить("Номер");
	КоллекцияВыплачено.Колонки.Добавить("Дата");
	
	КоллекцииРазделов = Новый Структура;
	КоллекцииРазделов.Вставить("Начислено", Новый Массив);
	КоллекцииРазделов.Вставить("Удержано",	КоллекцияУдержано);
	КоллекцииРазделов.Вставить("Выплачено", КоллекцияВыплачено);
	КоллекцииРазделов.Вставить("Льготы",	Новый Массив);
	КоллекцииРазделов.Вставить("Справочно", Новый Массив);
	
	ИзвестныеСтрокиСправочно = Новый Соответствие;
	ИзвестныеСтрокиВыплат = Новый Соответствие;
	ИзвестныеСтрокиЛьготы = Новый Соответствие;
	
	ИнформацияОВидахРасчета = Новый Соответствие;
	
	СоответствиеСтрокРасчетногоЛисткаСтрокамОтчета = Новый Соответствие;
	
	ЗначенияЗаполненияПолейГоловногоСотрудника = Неопределено;
	
	ИменаИзмеренийНачислений = "Сотрудник,ВидРасчета,ВидРасчетаКраткоеНаименование";
	ИменаИзмеренийУдержаний = "ФизическоеЛицо,ВидРасчета,ВидРасчетаКраткоеНаименование";
	Если НастройкиВывода.ВыводитьПериодыНачислений Тогда
		ИменаИзмеренийНачислений = ИменаИзмеренийНачислений + ",ПериодДействия";
		ИменаИзмеренийУдержаний = ИменаИзмеренийУдержаний + ",ПериодДействия";
	КонецЕсли;
	Если НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний
		Или НастройкиВывода.ВыводитьВсеПоказателиНачисленийИУдержаний Тогда
		
		ИменаИзмеренийНачислений = ИменаИзмеренийНачислений + ",Показатель";
		ИменаИзмеренийУдержаний = ИменаИзмеренийУдержаний + ",Показатель";
		
		Если НастройкиВывода.ВыводитьРасшифровкиОднойСтрокой Тогда
			ИменаИзмеренийНачислений = ИменаИзмеренийНачислений + ",Значение";
			ИменаИзмеренийУдержаний = ИменаИзмеренийУдержаний + ",Значение";
		КонецЕсли;
		
	КонецЕсли;
	
	Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний Тогда
		
		ИменаИзмеренийНачислений = ИменаИзмеренийНачислений + ",ДокументОснование";
		ИменаИзмеренийУдержаний = ИменаИзмеренийУдержаний + ",ДокументОснование";
		
	КонецЕсли;
	
	ИменаИзмеренийВыплачено = "ФизическоеЛицо,ВидРасчета,ВидРасчетаКраткоеНаименование";
	Если НастройкиВывода.ВыводитьПериодыНачислений Тогда
		ИменаИзмеренийВыплачено = ИменаИзмеренийВыплачено + ",ПериодДействия";
	КонецЕсли;
	
	СотрудникОтчета = СтрокиГоловныхСотрудников[0].ГоловнойСотрудник;
	ВывестиСдельныйЗаработокПоФизическомуЛицу = Истина;
	
	ТекущееФизическоеЛицо = Неопределено;
	Для каждого СтрокаГоловногоСотрудника Из СтрокиГоловныхСотрудников Цикл
		
		ЗначенияЗаполненияПолейГоловногоСотрудника = ЗначенияЗаполненияПользовательскихПолей(СоответствиеПользовательскихПолей, СтрокаГоловногоСотрудника);
		
		Если ТекущееФизическоеЛицо = Неопределено Тогда
			ТекущееФизическоеЛицо = СтрокаГоловногоСотрудника.ФизическоеЛицо;
		КонецЕсли;
		
		Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
			
			// Начислено
			РабочееМесто = Новый Структура("СтрокаСотрудника,СтрокиНачислений", СтрокаСотрудника, КоллекцияНачислено.Скопировать());
			
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				
				СторноСтроки = Новый Соответствие;
				РегистраторыНачислений = Новый Соответствие;
				Для каждого СтрокаНачислений Из СтрокаГруппы.Строки Цикл
					
					СтрокаКоллекции = РабочееМесто.СтрокиНачислений.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаКоллекции, СтрокаНачислений);
					
					Если Не ЗначениеЗаполнено(СтрокаНачислений.ВидЗанятостиГоловногоСотрудникаНаКонецПериода) Тогда
						СтрокаКоллекции.Сотрудник = СотрудникОтчета;
					КонецЕсли;
					
					Если НастройкиВывода.ВыводитьПериодыНачислений Тогда
						
						Если СтрокаНачислений.ПериодДействия = СтрокаНачислений.МесяцНачисления Тогда
							
							Если СтрокаНачислений.Сторно = Истина
								И СтрокаНачислений.Владелец().Колонки.Найти("ДокументОснование") <> Неопределено
								И СтрокаНачислений.ДокументОснование <> СтрокаНачислений.Регистратор Тогда
								
								СторноСтроки.Вставить(СтрокаКоллекции, СтрокаНачислений.ДокументОснование);
								
							Иначе
								
								Если СтрокаНачислений.Сумма <> 0 Тогда
									
									СтрокиРегистратора = РегистраторыНачислений.Получить(СтрокаНачислений.Регистратор);
									Если СтрокиРегистратора = Неопределено Тогда
										СтрокиРегистратора = Новый Соответствие;
										РегистраторыНачислений.Вставить(СтрокаНачислений.Регистратор, СтрокиРегистратора);
									КонецЕсли;
									
									СтрокиРегистратора.Вставить(СтрокаНачислений.ВидРасчета, СтрокаКоллекции);
									
								КонецЕсли;
								
							КонецЕсли;
							
						КонецЕсли;
						
						СтрокаКоллекции.ПериодДействияПредставление = СтрокаКоллекции.ПериодДействия;
						Если ЗначениеЗаполнено(СтрокаНачислений.ДатаНачала) И ЗначениеЗаполнено(СтрокаНачислений.ДатаОкончания) Тогда
							
							Если СтрокаНачислений.ДатаНачала <> СтрокаНачислений.ПериодДействия
								Или СтрокаНачислений.ДатаОкончания <> НачалоДня(КонецМесяца(СтрокаНачислений.ПериодДействия)) Тогда
								
								Если СтрокаНачислений.ДатаНачала = СтрокаНачислений.ДатаОкончания Тогда
									СтрокаКоллекции.ПериодДействияПредставление = Формат(СтрокаНачислений.ДатаНачала, "ДФ=дд.ММ");
								Иначе
									
									СтрокаКоллекции.ПериодДействияПредставление = Формат(СтрокаНачислений.ДатаНачала, "ДФ=дд.ММ") + "-"
										+ Формат(СтрокаНачислений.ДатаОкончания, "ДФ=дд.ММ");
									
								КонецЕсли;
								
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЕсли;
					
					Если НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя Тогда
						
						СтрокаКоллекции.ОплаченоДней = 0;
						СтрокаКоллекции.ОплаченоЧасов = 0;
						
						УчетВремениВЧасах = СтрокаНачислений.ВремяВЧасах = Истина;
						
						Если УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаНачислений.ОплаченоЧасов) И СтрокаНачислений.ОплаченоЧасов <> 0 Тогда
							СтрокаКоллекции.ОплаченоЧасов = СтрокаНачислений.ОплаченоЧасов;
						ИначеЕсли НЕ УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаНачислений.ОплаченоДней) И СтрокаНачислений.ОплаченоДней <> 0 Тогда
							СтрокаКоллекции.ОплаченоДней = СтрокаНачислений.ОплаченоДней;
						КонецЕсли;
						
					КонецЕсли;
					
					ИтогиРазделов.Начислено = ИтогиРазделов.Начислено + СтрокаНачислений.Сумма;
					Если ТипЗнч(СтрокаНачислений.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.Начисления") Тогда
						
						ЯвляетсяДоходомВНатуральнойФорме = ИнформацияОВидахРасчета.Получить(СтрокаНачислений.ВидРасчета);
						Если ЯвляетсяДоходомВНатуральнойФорме = Неопределено Тогда
							ЯвляетсяДоходомВНатуральнойФорме = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаНачислений.ВидРасчета, "ЯвляетсяДоходомВНатуральнойФорме");
							ИнформацияОВидахРасчета.Вставить(СтрокаНачислений.ВидРасчета, ЯвляетсяДоходомВНатуральнойФорме);
						КонецЕсли;
						
						Если ЯвляетсяДоходомВНатуральнойФорме = Истина Тогда
							ИтогиРазделов.НачисленоВНатуральнойФорме = ИтогиРазделов.НачисленоВНатуральнойФорме + СтрокаНачислений.Сумма;
						КонецЕсли;
						
					КонецЕсли;
					
					ДобавитьВСоответствиеСтрокуДанных(
						СоответствиеСтрокРасчетногоЛисткаСтрокамОтчета,
						СтрокаНачислений,
						ИменаИзмеренийНачислений);
					
				КонецЦикла;
				
			КонецЕсли;
			
			Если РабочееМесто.СтрокиНачислений.Количество() > 0 Тогда
				
				Если СтрокаСотрудника.Сотрудник <> СтрокаСотрудника.ГоловнойСотрудник
					Или СтрокаСотрудника.Сотрудник <> СотрудникОтчета Тогда
					
					СтрокаОтчета = СтрокиОтчетаНачислено.Добавить();
					СтрокаОтчета.ИмяОбласти = "ЗаголовокРабочееМестоВыплачено";
					СтрокаОтчета.ПредставлениеРабочегоМеста = ПредставлениеРабочегоМеста(РабочееМесто.СтрокаСотрудника);
					СтрокаОтчета.СтрокаДанных = СтрокаСотрудника;
					
				КонецЕсли;
				
				Если СторноСтроки.Количество() > 0 Тогда
					
					Для Каждого ОписаниеСторноСтроки Из СторноСтроки Цикл
						
						СторноСтрока = ОписаниеСторноСтроки.Ключ;
						СтрокиРегистратора = РегистраторыНачислений.Получить(ОписаниеСторноСтроки.Значение);
						Если СтрокиРегистратора <> Неопределено Тогда
							
							СтрокаВидаРасчета = СтрокиРегистратора.Получить(СторноСтрока.ВидРасчета);
							Если СтрокаВидаРасчета <> Неопределено Тогда
								
								ЗаполнитьЗначенияСвойств(СторноСтрока, СтрокаВидаРасчета,
									"ПериодДействия,ПериодДействияПредставление,ПриоритетВидаРасчета,Показатель,Значение,ПорядокПоказателей");
								
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
				СуммируемыеКолонки = "Сумма";
				Если НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя Тогда
					СуммируемыеКолонки = СуммируемыеКолонки + ",ОтработаноДней,ОтработаноЧасов,ОплаченоДней,ОплаченоЧасов";
				КонецЕсли;
				
				СвернутьСтрокиВидовРасчета(РабочееМесто.СтрокиНачислений, "ФизическоеЛицо,Группа,Сотрудник,ВидРасчета,ВидРасчетаКраткоеНаименование,ПриоритетВидаРасчета", СуммируемыеКолонки, НастройкиВывода, Ложь);
				
				Если НастройкиВывода.ВыводитьПериодыНачислений Тогда
					
					УдалятьСтрокиБезПоказателей = РабочееМесто.СтрокиНачислений.Колонки.Найти("ПорядокПоказателей") = Неопределено;
					УдаляемыеСтроки = Новый Массив;
					Для каждого СтрокаНачислений Из РабочееМесто.СтрокиНачислений Цикл
						
						Если СтрокаНачислений.ПериодДействия = СтрокаНачислений.МесяцНачисления Тогда
							Продолжить;
						КонецЕсли;
						
						Если УдалятьСтрокиБезПоказателей Или СтрокаНачислений.ПорядокПоказателей = 1 Тогда
							
							УдалитьСтроку = Истина;
							Для каждого ИмяКолонки Из СтрРазделить(СуммируемыеКолонки, ",") Цикл
								
								Если СтрокаНачислений[ИмяКолонки] <> 0 Тогда
									УдалитьСтроку = Ложь;
									Прервать;
								КонецЕсли;
								
							КонецЦикла;
							
							Если УдалитьСтроку Тогда
								УдаляемыеСтроки.Добавить(СтрокаНачислений);
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла;
					
					Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
						РабочееМесто.СтрокиНачислений.Удалить(УдаляемаяСтрока);
					КонецЦикла;
					
				КонецЕсли;
				
				КоллекцииРазделов.Начислено.Добавить(РабочееМесто);
				
			КонецЕсли;
			
			ДобавитьСтрокиРаздела(
				СтрокиОтчетаНачислено,
				РабочееМесто.СтрокиНачислений,
				НастройкиВывода,
				СоответствиеСтрокРасчетногоЛисткаСтрокамОтчета,
				СоответствиеПользовательскихПолей,
				ИменаИзмеренийНачислений,
				Макет,
				"ВидНачислено",
				ДополнительныеПараметры.СдельныйЗаработокСотрудников,
				ВывестиСдельныйЗаработокПоФизическомуЛицу);
				
				ВывестиСдельныйЗаработокПоФизическомуЛицу = Ложь;
			
			// Удержано
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				
				Для каждого СтрокаУдержаний Из СтрокаГруппы.Строки Цикл
					
					СтрокаКоллекции = КоллекцииРазделов.Удержано.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаКоллекции, СтрокаУдержаний);
					
					ИтогиРазделов.Удержано = ИтогиРазделов.Удержано + СтрокаУдержаний.Сумма;
					
					ДобавитьВСоответствиеСтрокуДанных(
						СоответствиеСтрокРасчетногоЛисткаСтрокамОтчета,
						СтрокаУдержаний,
						ИменаИзмеренийУдержаний);
					
				КонецЦикла;
				
				СвернутьСтрокиВидовРасчета(КоллекцииРазделов.Удержано, "ФизическоеЛицо,Группа,Сотрудник,ВидРасчета,ВидРасчетаКраткоеНаименование,ПриоритетВидаРасчета", "Сумма", НастройкиВывода, Истина);
				
			КонецЕсли;
			
			// Выплачено
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Выплачено, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				
				Для каждого СтрокаВыплачено Из СтрокаГруппы.Строки Цикл
					
					Для каждого СтрокаСРегистратором Из СтрокаВыплачено.Строки Цикл
						
						СтрокаКоллекции = ИзвестныеСтрокиВыплат.Получить(СтрокаСРегистратором.Регистратор);
						Если СтрокаКоллекции = Неопределено Тогда
							
							СтрокаКоллекции = КоллекцииРазделов.Выплачено.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаКоллекции, СтрокаСРегистратором);
							СтрокаКоллекции.РегистраторВыплаты = СтрокаСРегистратором.Регистратор;
							
							СвойстваРегистратора = СвойстваРегистраторов.Получить(СтрокаСРегистратором.Регистратор);
							Если СвойстваРегистратора <> Неопределено Тогда
								
								Если НастройкиВывода.ДетализироватьВыплатыПоВедомостям Тогда
									
									СтрокаКоллекции.ПредставлениеРегистратораВыплаты = ПредставлениеРегистраторовВзаиморасчетов(
										СтрокаСРегистратором.Регистратор, СтрокаСРегистратором.ВидРасчета, СвойстваРегистратора);
									
								Иначе
									СтрокаКоллекции.ПредставлениеРегистратораВыплаты = ПредставлениеВыплаты(СтрокаСРегистратором.ВидРасчета, СвойстваРегистратора)
								КонецЕсли;
								
								Если НастройкиВывода.ВыводитьПериодыНачислений И СвойстваРегистратора.Свойство("ПериодРегистрации") Тогда
									СтрокаКоллекции.ПериодДействия = СвойстваРегистратора.ПериодРегистрации;
								КонецЕсли;
								
							КонецЕсли;
							
							ИзвестныеСтрокиВыплат.Вставить(СтрокаСРегистратором.Регистратор, СтрокаКоллекции);
							
						Иначе
							СтрокаКоллекции.Сумма = СтрокаКоллекции.Сумма + СтрокаСРегистратором.Сумма;
						КонецЕсли;
						
						ДобавитьВСоответствиеСтрокуДанных(
							СоответствиеСтрокРасчетногоЛисткаСтрокамОтчета,
							СтрокаВыплачено,
							ИменаИзмеренийВыплачено);
						
					КонецЦикла;
					
					ИтогиРазделов.Выплачено = ИтогиРазделов.Выплачено + СтрокаВыплачено.Сумма;
					
				КонецЦикла;
				
			КонецЕсли;
			
			// Льготы
			Если НастройкиВывода.ВыводитьИнформациюОЛьготах Тогда
				
				СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Льготы, "Группа");
				Если СтрокаГруппы <> Неопределено Тогда
					
					Для каждого СтрокаЛьготы Из СтрокаГруппы.Строки Цикл
						
						СтрокаКоллекции = ИзвестныеСтрокиЛьготы.Получить(СтрокаЛьготы.ВидРасчета);
						Если СтрокаКоллекции = Неопределено Тогда
							
							СтрокаКоллекции = Новый Структура("ФизическоеЛицо,Группа,Сотрудник,ВидРасчета,ВидРасчетаКраткоеНаименование,ПриоритетВидаРасчета,Сумма");
							ЗаполнитьЗначенияСвойств(СтрокаКоллекции, СтрокаЛьготы);
							КоллекцииРазделов.Льготы.Добавить(СтрокаКоллекции);
							ИзвестныеСтрокиЛьготы.Вставить(СтрокаЛьготы.ВидРасчета, СтрокаКоллекции);
							
						Иначе
							СтрокаКоллекции.Сумма = СтрокаКоллекции.Сумма + СтрокаЛьготы.Сумма;
						КонецЕсли;
						
					КонецЦикла;
				
				КонецЕсли;
			
			КонецЕсли;
			
			// Справочно
			Если НастройкиВывода.ВыводитьСправочнуюИнформацию Тогда
				
				СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно, "Группа");
				Если СтрокаГруппы <> Неопределено Тогда
					
					Для каждого СтрокаСправочно Из СтрокаГруппы.Строки Цикл
						
						СтрокаКоллекции = ИзвестныеСтрокиСправочно.Получить(СтрокаСправочно.ВидРасчета);
						Если СтрокаКоллекции = Неопределено Тогда
							
							СтрокаКоллекции = Новый Структура("ФизическоеЛицо,Группа,Сотрудник,ВидРасчета,ВидРасчетаКраткоеНаименование,ПриоритетВидаРасчета,Сумма");
							ЗаполнитьЗначенияСвойств(СтрокаКоллекции, СтрокаСправочно);
							КоллекцииРазделов.Справочно.Добавить(СтрокаКоллекции);
							ИзвестныеСтрокиСправочно.Вставить(СтрокаСправочно.ВидРасчета, СтрокаКоллекции);
							
						Иначе
							СтрокаКоллекции.Сумма = СтрокаКоллекции.Сумма + СтрокаСправочно.Сумма;
						КонецЕсли;
						
					КонецЦикла;
				
				КонецЕсли;
				
			КонецЕсли;
			
			// Начальное сальдо
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				УвеличитьРезультатНаСуммуСтроки(ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца, СтрокаГруппы);
				СтрокаДанныхРасчетногоЛистка = ДанныеРасчетныхЛистков.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДанныхРасчетногоЛистка, СтрокаГруппы);
				СтрокаДанныхРасчетногоЛистка.Сотрудник = СотрудникОтчета;
			КонецЕсли;
			
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдоПоМесяцамРасчета, "Группа");
			УвеличитьРезультатНаСуммуСтроки(ИтогиПоГруппам.СуммаДолгаНаНачалоМесяцаРасчета, СтрокаГруппы);
			
			// КонечноеСальдо
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо, "Группа");
			Если СтрокаГруппы <> Неопределено Тогда
				УвеличитьРезультатНаСуммуСтроки(ИтогиПоГруппам.СуммаДолгаНаКонецМесяца, СтрокаГруппы);
				СтрокаДанныхРасчетногоЛистка = ДанныеРасчетныхЛистков.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДанныхРасчетногоЛистка, СтрокаГруппы);
				СтрокаДанныхРасчетногоЛистка.Сотрудник = СотрудникОтчета;
			КонецЕсли;
			
			СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдоПоМесяцамРасчета, "Группа");
			УвеличитьРезультатНаСуммуСтроки(ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета, СтрокаГруппы);
			
			Если НастройкиВывода.РассчитатьИтогиПоДанным Тогда
				
				ИтогиПоГруппам.СуммаДолгаНаКонецМесяца =
					ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца
					+ ИтогиРазделов.Начислено
					- ИтогиРазделов.Удержано
					- ИтогиРазделов.Выплачено;
				
				ИтогиПоГруппам.СуммаДолгаНаКонецМесяцаРасчета =
					ИтогиПоГруппам.СуммаДолгаНаНачалоМесяцаРасчета
					+ ИтогиРазделов.Начислено
					- ИтогиРазделов.Удержано
					- ИтогиРазделов.Выплачено;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	СворачиваемыеКолонки = "ФизическоеЛицо,Группа,Сотрудник,ВидРасчета,ПриоритетВидаРасчета,ПредставлениеРегистратораВыплаты";
	Если НастройкиВывода.ДетализироватьВыплатыПоВедомостям Тогда
		СворачиваемыеКолонки = СворачиваемыеКолонки + ",РегистраторВыплаты,Дата,Номер";
		КоллекцииРазделов.Выплачено.Сортировать("Дата,Номер");
	КонецЕсли;
	Если НастройкиВывода.ВыводитьПериодыНачислений Тогда
		СворачиваемыеКолонки = СворачиваемыеКолонки + ",ПериодДействия";
	КонецЕсли;
	КоллекцииРазделов.Выплачено.Свернуть(СворачиваемыеКолонки, "Сумма");
	
	УдалитьСтрокиСНулевойСуммой(КоллекцииРазделов.Выплачено);
	
	#КонецОбласти
	
	#Область ВыводТела
	
	ИтогиПоГруппам.Начислено = ИтогиРазделов.Начислено;
	ИтогиПоГруппам.Удержано = ИтогиРазделов.Удержано;
	ИтогиПоГруппам.Выплачено = ИтогиРазделов.Выплачено;
	
	ДобавитьСтрокиРаздела(
		СтрокиОтчетаУдержано,
		КоллекцииРазделов.Удержано,
		НастройкиВывода,
		СоответствиеСтрокРасчетногоЛисткаСтрокамОтчета,
		СоответствиеПользовательскихПолей,
		ИменаИзмеренийУдержаний,
		Макет,
		"ВидУдержаноВыплачено");
	
	Для Каждого СтрокаВыплачено Из КоллекцииРазделов.Выплачено Цикл
		
		СтрокаОтчета = СтрокиОтчетаВыплачено.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаОтчета, СтрокаВыплачено);
		СтрокаОтчета.ИмяОбласти = "Строка";
		СтрокаОтчета.СтрокаДанных = СтрокаДанныхИзСоответствияСтрок(
			СоответствиеСтрокРасчетногоЛисткаСтрокамОтчета,
			СтрокаОтчета,
			ИменаИзмеренийВыплачено);
		
		СтрокаОтчета.ВидРасчета = СтрокаВыплачено.ПредставлениеРегистратораВыплаты;
		
	КонецЦикла;
	
	// Подготовка данных расчетных листков
	
	СтрокаОтчетаПредставлениеРабочегоМеста = "";
	Для Каждого СтрокаОтчетаНачислено Из СтрокиОтчетаНачислено Цикл
		
		Если СтрокаОтчетаПредставлениеРабочегоМеста <> СтрокаОтчетаНачислено.ПредставлениеРабочегоМеста
			И ЗначениеЗаполнено(СтрокаОтчетаНачислено.ПредставлениеРабочегоМеста) Тогда
			
			СтрокаОтчетаПредставлениеРабочегоМеста = СтрокаОтчетаНачислено.ПредставлениеРабочегоМеста;
		КонецЕсли;
		
		Если СтрокаОтчетаНачислено.ПредставлениеРабочегоМеста <> СтрокаОтчетаПредставлениеРабочегоМеста Тогда
			СтрокаОтчетаНачислено.ПредставлениеРабочегоМеста = СтрокаОтчетаПредставлениеРабочегоМеста
		КонецЕсли;
		
		СтрокаДанныхРасчетногоЛистка = ДанныеРасчетныхЛистков.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДанныхРасчетногоЛистка, СтрокаОтчетаНачислено);
		СтрокаДанныхРасчетногоЛистка.ПриоритетВидаРасчета =
			ПриоритетВидаРасчетаДанныхРасчетныхЛистков(СтрокаДанныхРасчетногоЛистка.ПриоритетВидаРасчета);
		
	КонецЦикла;
	
	Для Каждого СтрокаВидаРасчета Из СтрокиОтчетаУдержано Цикл
		
		СтрокаДанныхРасчетногоЛистка = ДанныеРасчетныхЛистков.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДанныхРасчетногоЛистка, СтрокаВидаРасчета);
		СтрокаДанныхРасчетногоЛистка.Сотрудник = СотрудникОтчета;
		СтрокаДанныхРасчетногоЛистка.ПриоритетВидаРасчета =
			ПриоритетВидаРасчетаДанныхРасчетныхЛистков(СтрокаДанныхРасчетногоЛистка.ПриоритетВидаРасчета);
		
	КонецЦикла;
	
	Для Каждого СтрокаВидаРасчета Из КоллекцииРазделов.Выплачено Цикл
		
		СтрокаДанныхРасчетногоЛистка = ДанныеРасчетныхЛистков.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДанныхРасчетногоЛистка, СтрокаВидаРасчета);
		СтрокаДанныхРасчетногоЛистка.Сотрудник = СотрудникОтчета;
		СтрокаДанныхРасчетногоЛистка.ПриоритетВидаРасчета =
			ПриоритетВидаРасчетаДанныхРасчетныхЛистков(СтрокаДанныхРасчетногоЛистка.ПриоритетВидаРасчета);
		
	КонецЦикла;
	
	Для Каждого СтрокаВидаРасчета Из КоллекцииРазделов.Льготы Цикл
		
		СтрокаДанныхРасчетногоЛистка = ДанныеРасчетныхЛистков.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДанныхРасчетногоЛистка, СтрокаВидаРасчета);
		СтрокаДанныхРасчетногоЛистка.Сотрудник = СотрудникОтчета;
		
	КонецЦикла;
	
	Для Каждого СтрокаВидаРасчета Из КоллекцииРазделов.Справочно Цикл
		
		СтрокаДанныхРасчетногоЛистка = ДанныеРасчетныхЛистков.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДанныхРасчетногоЛистка, СтрокаВидаРасчета);
		СтрокаДанныхРасчетногоЛистка.Сотрудник = СотрудникОтчета;
		
	КонецЦикла;
	
	// Сворачивание итогов и справочных данных
	ДанныеРасчетныхЛистков.Свернуть(
		"ФизическоеЛицо,Сотрудник,ПредставлениеРабочегоМеста,Группа,ВидРасчета,ВидРасчетаКраткоеНаименование,"
			+ "РегистраторВыплаты,ПриоритетВидаРасчета,ПериодДействия,ОплаченныеДниЧасы",
		"Сумма");
	
	#Область ДобавлениеИтоговПоРазделам
	
	СтрокаОтчета = СтрокиОтчетаНачислено.Вставить(0);
	СтрокаОтчета.ИмяОбласти = "ЗаголовокНачисленоУдержано";
	СтрокаОтчета.СтрокаДанных = СтрокиГоловныхСотрудников[0];
	СтрокаОтчета.Сумма = ИтогиРазделов.Начислено;
	
	СтрокаОтчета = СтрокиОтчетаУдержано.Вставить(0);
	СтрокаОтчета.ИмяОбласти = "ЗаголовокНачисленоУдержано";
	СтрокаОтчета.СтрокаДанных = СтрокиГоловныхСотрудников[0];
	СтрокаОтчета.Сумма = ИтогиРазделов.Удержано;
	
	СтрокаОтчета = СтрокиОтчетаВыплачено.Вставить(0);
	СтрокаОтчета.ИмяОбласти = "ЗаголовокРабочееМестоВыплачено";
	СтрокаОтчета.СтрокаДанных = СтрокиГоловныхСотрудников[0];
	СтрокаОтчета.Сумма = ИтогиРазделов.Выплачено;
	
	Если ИтогиРазделов.НачисленоВНатуральнойФорме <> 0 Тогда
		
		СтрокаОтчета = СтрокиОтчетаНачислено.Вставить(1);
		СтрокаОтчета.ИмяОбласти = "ЗаголовокНачисленоВНатуральнойФорме";
		СтрокаОтчета.Сумма = ИтогиРазделов.НачисленоВНатуральнойФорме;
		СтрокаОтчета.СтрокаДанных = СтрокаСотрудника;
		СтрокаОтчета.Расшифровка = Истина;
		
	КонецЕсли;
	
	#КонецОбласти
	
	РасшифровкаНачислений = Ложь;
	КорректировкаИндексаНачислений = 0;
	
	РасшифровкаУдержанийВыплат = Ложь;
	КорректировкаИндексаУдержанийВыплат = 0;
	
	ПустыеПараметрыЗаполнения = Новый Структура("ПредставлениеРабочегоМеста,ВидРасчета,ВидРасчетаКраткоеНаименование,ПериодДействия,ОтработаноДней,
		|ОтработаноЧасов,ОплаченныеДниЧасы,Сумма,ДокументОснование,Показатель,Значение");
	
	// Вывод шапки
	Если НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя Тогда
		ВывестиОбластиСтроки(ДокументРезультат, Макет, "ШапкаТаблицы", НастройкиВывода, ДополненныеПараметры(ДанныеПечатнойФормы, ЗначенияЗаполненияПолейГоловногоСотрудника));
	Иначе
		ВывестиОбластиСтроки(ДокументРезультат, Макет, "ШапкаТаблицыНизкая", НастройкиВывода, ДополненныеПараметры(ДанныеПечатнойФормы, ЗначенияЗаполненияПолейГоловногоСотрудника));
	КонецЕсли;
	
	Индекс = 0;
	Пока Индекс <= Макс(СтрокиОтчетаНачислено.Количество() - 1 + КорректировкаИндексаНачислений, СтрокиОтчетаУдержано.Количество() + СтрокиОтчетаВыплачено.Количество() - 1 + КорректировкаИндексаУдержанийВыплат) Цикл
		
		// Параметры выводимой строки
		
		РасшифровкаНачислений = Ложь;
		ИндексНачислений = Индекс - КорректировкаИндексаНачислений;
		Если ИндексНачислений < СтрокиОтчетаНачислено.Количество() Тогда
			
			СтрокаНачислений = СтрокиОтчетаНачислено[ИндексНачислений];
			РасшифровкаНачислений = СтрокаНачислений.Расшифровка;
			
		КонецЕсли;
		
		РасшифровкаУдержанийВыплат = Ложь;
		ИндексУдержанийВыплат = Индекс - КорректировкаИндексаУдержанийВыплат;
		Если ИндексУдержанийВыплат < СтрокиОтчетаУдержано.Количество()
			Или РасшифровкаНачислений И ИндексУдержанийВыплат = СтрокиОтчетаУдержано.Количество() Тогда
			
			Если ИндексУдержанийВыплат < СтрокиОтчетаУдержано.Количество() Тогда
				СтрокаУдержано = СтрокиОтчетаУдержано[ИндексУдержанийВыплат];
				РасшифровкаУдержанийВыплат = СтрокаУдержано.Расшифровка;
			КонецЕсли;
			
		КонецЕсли;
		
		//Начислено
		
		Если ИндексНачислений < СтрокиОтчетаНачислено.Количество() Тогда
			
			Если Не РасшифровкаУдержанийВыплат Или РасшифровкаНачислений Тогда
				
				ИмяОбластиСтроки = СтрокаНачислений.ИмяОбласти;
				
				Если ЗначениеЗаполнено(СтрокаНачислений.СтрокаДанных) Тогда
					ЗначенияЗаполненияПолейПоПараметрам = ЗначенияЗаполненияПользовательскихПолей(СоответствиеПользовательскихПолей, СтрокаНачислений.СтрокаДанных);
				Иначе
					ЗначенияЗаполненияПолейПоПараметрам = Неопределено;
				КонецЕсли;
				ПараметрыЗаполнения = СтрокаНачислений;
				
			Иначе
				ИмяОбластиСтроки = "СтрокаРасшифровкаДокументОснование";
				ПараметрыЗаполнения = ПустыеПараметрыЗаполнения;
			КонецЕсли;
			
		Иначе
			
			Если Не РасшифровкаУдержанийВыплат Тогда
				ИмяОбластиСтроки = "Строка";
				ЗначенияЗаполненияПолейПоПараметрам = ЗначенияЗаполненияПользовательскихПолей(СоответствиеПользовательскихПолей, СтрокиГоловныхСотрудников[0]);
			Иначе
				ИмяОбластиСтроки = "СтрокаРасшифровкаДокументОснование";
			КонецЕсли;
			
			ПараметрыЗаполнения = ПустыеПараметрыЗаполнения;
			
		КонецЕсли;
		
		ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода,
			ДополненныеПараметры(ДанныеПечатнойФормы, ЗначенияЗаполненияПолейПоПараметрам, ПараметрыЗаполнения),
			РасшифровкаНачислений И НастройкиВывода.ВыводитьРасшифровкиОднойСтрокой);
		
		// Удержано, выплачено
		Если ИндексУдержанийВыплат < СтрокиОтчетаУдержано.Количество()
			Или РасшифровкаНачислений И ИндексУдержанийВыплат = СтрокиОтчетаУдержано.Количество() Тогда
			
			Если Не РасшифровкаНачислений Или РасшифровкаУдержанийВыплат Тогда
				
				ИмяОбластиСтроки = СтрокаУдержано.ИмяОбласти;
				
				Если ЗначениеЗаполнено(СтрокаУдержано.СтрокаДанных) Тогда
					ЗначенияЗаполненияПолейПоПараметрам = ЗначенияЗаполненияПользовательскихПолей(СоответствиеПользовательскихПолей, СтрокаУдержано.СтрокаДанных);
				Иначе
					ЗначенияЗаполненияПолейПоПараметрам = Неопределено;
				КонецЕсли;
				ПараметрыЗаполнения = СтрокаУдержано;
				
			Иначе
				Если ИндексУдержанийВыплат = 1 Тогда
					ИмяОбластиСтроки = "СтрокаРасшифровка";
				Иначе
					ИмяОбластиСтроки = "СтрокаРасшифровкаДокументОснование";
				КонецЕсли;
				ПараметрыЗаполнения = ПустыеПараметрыЗаполнения;
			КонецЕсли;
			
		ИначеЕсли ИндексУдержанийВыплат < СтрокиОтчетаУдержано.Количество() + СтрокиОтчетаВыплачено.Количество() Тогда
			
			ИндексВыплат = ИндексУдержанийВыплат - СтрокиОтчетаУдержано.Количество();
			СтрокаВыплачено = СтрокиОтчетаВыплачено[ИндексВыплат];
			Если Не РасшифровкаНачислений Тогда
				
				ИмяОбластиСтроки = СтрокаВыплачено.ИмяОбласти;
				
				Если ЗначениеЗаполнено(СтрокаВыплачено.СтрокаДанных) Тогда
					ЗначенияЗаполненияПолейПоПараметрам = ЗначенияЗаполненияПользовательскихПолей(СоответствиеПользовательскихПолей, СтрокаВыплачено.СтрокаДанных);
				Иначе
					ЗначенияЗаполненияПолейПоПараметрам = Неопределено;
				КонецЕсли;
				ПараметрыЗаполнения = СтрокаВыплачено;
				
			Иначе
				Если ИндексВыплат = 1 Тогда
					ИмяОбластиСтроки = "СтрокаРасшифровка";
				Иначе
					ИмяОбластиСтроки = "СтрокаРасшифровкаДокументОснование";
				КонецЕсли;
				ПараметрыЗаполнения = ПустыеПараметрыЗаполнения;
			КонецЕсли;
			
		Иначе
			
			Если Не РасшифровкаНачислений Тогда
				ИмяОбластиСтроки = "Строка";
				ЗначенияЗаполненияПолейПоПараметрам = Неопределено;
			Иначе
				ИмяОбластиСтроки = "СтрокаРасшифровкаДокументОснование";
			КонецЕсли;
			
			ПараметрыЗаполнения = ПустыеПараметрыЗаполнения;
			
		КонецЕсли;
		
		ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода,
			ДополненныеПараметры(ДанныеПечатнойФормы, ЗначенияЗаполненияПолейПоПараметрам, ПараметрыЗаполнения),
			РасшифровкаУдержанийВыплат И НастройкиВывода.ВыводитьРасшифровкиОднойСтрокой);
		
		Если РасшифровкаНачислений И Не РасшифровкаУдержанийВыплат Тогда
			КорректировкаИндексаУдержанийВыплат = КорректировкаИндексаУдержанийВыплат + 1;
		ИначеЕсли Не РасшифровкаНачислений И РасшифровкаУдержанийВыплат Тогда
			КорректировкаИндексаНачислений = КорректировкаИндексаНачислений + 1;
		КонецЕсли;
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
	ВывестиОбластиСтроки(ДокументРезультат, Макет, "ПодвалТаблицы", НастройкиВывода, ДополненныеПараметры(ДанныеПечатнойФормы, ЗначенияЗаполненияПолейГоловногоСотрудника));
	
	// Льготы
	
	Если КоллекцииРазделов.Льготы.Количество() > 0 Тогда
		
		Область = Макет.ПолучитьОбласть("ШапкаЛьготы");
		ДокументРезультат.Вывести(Область);
		
		ВывестиТелоСправочнойИнформации(ДокументРезультат, Макет, КоллекцииРазделов.Льготы,
			ДополненныеПараметры(ДанныеПечатнойФормы, ЗначенияЗаполненияПолейГоловногоСотрудника));
		
	КонецЕсли;
	
	// Справочно
	
	Если КоллекцииРазделов.Справочно.Количество() > 0 Тогда
		
		Область = Макет.ПолучитьОбласть("ШапкаСправочно");
		ДокументРезультат.Вывести(Область);
		
		ВывестиТелоСправочнойИнформации(ДокументРезультат, Макет, КоллекцииРазделов.Справочно,
			ДополненныеПараметры(ДанныеПечатнойФормы, ЗначенияЗаполненияПолейГоловногоСотрудника));
		
	КонецЕсли;
	
	#КонецОбласти
	
	Возврат ИтогиПоГруппам;
	
КонецФункции

Функция ПриоритетВидаРасчетаДанныхРасчетныхЛистков(ПриоритетВидаРасчета)
	
	Если ЗначениеЗаполнено(ПриоритетВидаРасчета) Тогда
		
		Если ПриоритетВидаРасчета > 10000000000000000 Тогда
			Возврат Цел(ПриоритетВидаРасчета / 10000000000000000) * 100;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПриоритетВидаРасчета;
	
КонецФункции

Процедура СвернутьСтрокиВидовРасчета(СтрокиВидовРасчета, СворачиваемыеКолонки, СуммируемыеКолонки, НастройкиВывода, СвернутьСотрудников)
	
	Если НастройкиВывода.ВыводитьПериодыНачислений
		Или НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя
		Или НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний
		Или НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний
		Или НастройкиВывода.ВыводитьВсеПоказателиНачисленийИУдержаний Тогда
		
		СворачиваемыеКолонки = СворачиваемыеКолонки + ",ПриоритетВидаРасчета";
		
	КонецЕсли;
	
	Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний Тогда
		СворачиваемыеКолонки = СворачиваемыеКолонки + ",ДокументОснование";
	КонецЕсли;
	
	Если НастройкиВывода.ВыводитьПериодыНачислений Тогда
		СворачиваемыеКолонки = СворачиваемыеКолонки + ",ПериодДействия,ПериодДействияПредставление,МесяцНачисления";
	КонецЕсли;
	
	Если НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний
		Или НастройкиВывода.ВыводитьВсеПоказателиНачисленийИУдержаний Тогда
		
		СворачиваемыеКолонки = СворачиваемыеКолонки + ",Показатель,Значение,ПорядокПоказателей";
		
	КонецЕсли;
	
	ВидРасчета = Неопределено;
	Показатели = Новый Соответствие;
	СборСведений = Истина;
	СворачиваемыеСтроки = Новый Массив;
	
	СворачиваемыеКоллекции = Новый Массив;
	Для Каждого СтрокаВидаРасчета Из СтрокиВидовРасчета Цикл
		
		Если НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний
			Или НастройкиВывода.ВыводитьВсеПоказателиНачисленийИУдержаний Тогда
			
			Если ВидРасчета <> СтрокаВидаРасчета.ВидРасчета Тогда
				
				ВидРасчета = СтрокаВидаРасчета.ВидРасчета;
				СборСведений = Истина;
				СворачиваемыеСтроки = Новый Массив;
				СворачиваемыеКоллекции.Добавить(СворачиваемыеСтроки);
				
			ИначеЕсли ЗначениеЗаполнено(СтрокаВидаРасчета.Сумма) Тогда
				СборСведений = Ложь;
			КонецЕсли;
			
			СворачиваемыеСтроки.Добавить(СтрокаВидаРасчета);
			
			Если СборСведений Тогда
				
				Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний
					И ЗначениеЗаполнено(СтрокаВидаРасчета.ДокументОснование) Тогда
					
					Показатели.Вставить("ДокументОснование", СтрокаВидаРасчета.ДокументОснование);
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаВидаРасчета.Показатель) Тогда
					Показатели.Вставить(СтрокаВидаРасчета.Показатель, СтрокаВидаРасчета.Значение);
				КонецЕсли;
				
			Иначе
				
				Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний
					И ЗначениеЗаполнено(СтрокаВидаРасчета.ДокументОснование)
					И  Показатели.Получить("ДокументОснование") <> СтрокаВидаРасчета.ДокументОснование Тогда
					
					СворачиваемыеКоллекции.Удалить(СворачиваемыеКоллекции.Количество() - 1);
					ВидРасчета = Неопределено;
					
				ИначеЕсли Показатели.Получить(СтрокаВидаРасчета.Показатель) <> СтрокаВидаРасчета.Значение Тогда
					
					СворачиваемыеКоллекции.Удалить(СворачиваемыеКоллекции.Количество() - 1);
					ВидРасчета = Неопределено;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не НастройкиВывода.ВыводитьВсеПоказателиНачисленийИУдержаний Тогда
			
			Если НастройкиВывода.ВыводитьПериодыНачислений И ЗначениеЗаполнено(СтрокаВидаРасчета.ПериодДействияПредставление) Тогда
				
				Если ЗначениеЗаполнено(СтрокаВидаРасчета.ПриоритетВидаРасчета)
					И СтрокаВидаРасчета.ПриоритетВидаРасчета > 1000000 Тогда
					
					СтрокаВидаРасчета.ПриоритетВидаРасчета = Цел(СтрокаВидаРасчета.ПриоритетВидаРасчета / 1000000) * 1000000;
					
				КонецЕсли;
				
			Иначе
				
				Если ЗначениеЗаполнено(СтрокаВидаРасчета.ПриоритетВидаРасчета)
					И СтрокаВидаРасчета.ПриоритетВидаРасчета > 10000000000000000 Тогда
					
					СтрокаВидаРасчета.ПриоритетВидаРасчета = Цел(СтрокаВидаРасчета.ПриоритетВидаРасчета / 10000000000000000) * 10000000000000000;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СворачиваемаяКоллекция Из СворачиваемыеКоллекции Цикл
		
		Если СворачиваемаяКоллекция.Количество() > 1 Тогда
			
			Для ИндексСтроки = 1 По СворачиваемаяКоллекция.Количество() - 1 Цикл
				СворачиваемаяКоллекция[ИндексСтроки].ПриоритетВидаРасчета = СворачиваемаяКоллекция[0].ПриоритетВидаРасчета;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если СвернутьСотрудников Тогда
		СтрокиВидовРасчета.ЗаполнитьЗначения(Неопределено, "Сотрудник");
	КонецЕсли;
	
	СтрокиВидовРасчета.Свернуть(СворачиваемыеКолонки, СуммируемыеКолонки);
	
	Если НастройкиВывода.ИсключатьПустыеСтрокиНачисленийУдержаний Тогда
		
		УдаляемыеСтроки = Новый Массив;
		Для Каждого СтрокаВидовРасчета Из СтрокиВидовРасчета Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаВидовРасчета.Сумма)
				И Не ЗаполненРеквизитСтроки(СтрокаВидовРасчета, "Значение")
				И Не ЗаполненРеквизитСтроки(СтрокаВидовРасчета, "ОтработаноДней")
				И Не ЗаполненРеквизитСтроки(СтрокаВидовРасчета, "ОтработаноЧасов")
				И Не ЗаполненРеквизитСтроки(СтрокаВидовРасчета, "ОплаченоДней")
				И Не ЗаполненРеквизитСтроки(СтрокаВидовРасчета, "ОплаченоЧасов") Тогда
				
				УдаляемыеСтроки.Добавить(СтрокаВидовРасчета);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого СтрокаВидовРасчета Из УдаляемыеСтроки Цикл
			СтрокиВидовРасчета.Удалить(СтрокаВидовРасчета);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЗаполненРеквизитСтроки(СтрокаВидовРасчета, ИмяРеквизита)
	
	Если СтрокаВидовРасчета.Владелец().Колонки.Найти(ИмяРеквизита) = Неопределено
		Или Не ЗначениеЗаполнено(СтрокаВидовРасчета[ИмяРеквизита]) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ПредставлениеВыплаты(ВидВзаиморасчетов, СвойстваРегистратора)
	
	Если СвойстваРегистратора.Свойство("СпособВыплаты")
		И ЗначениеЗаполнено(СвойстваРегистратора.СпособВыплаты) Тогда
		
		Представление = Строка(СвойстваРегистратора.СпособВыплаты);
		Если ВидВзаиморасчетов = Перечисления.ВидыВзаиморасчетовССотрудниками.ВыплатаАванса Тогда
			Представление = НСтр("ru='За первую половину месяца'");
		ИначеЕсли ВидВзаиморасчетов = Перечисления.ВидыВзаиморасчетовССотрудниками.ВыплатаЗарплаты
			Или ВидВзаиморасчетов = Перечисления.ВидыВзаиморасчетовССотрудниками.ПогашениеЗадолженностиПоЗарплате Тогда
			
			Если СтрНайти(ВРег(Представление), ВРег(НСтр("ru='зарплата'"))) = 0 Тогда
				Представление = Представление + ", " + НСтр("ru='зарплата'");
			КонецЕсли;
			
		ИначеЕсли ВидВзаиморасчетов = Перечисления.ВидыВзаиморасчетовССотрудниками.ВыплатаВМежрасчетныйПериод Тогда
			
			Если СтрНайти(ВРег(Представление), ВРег(НСтр("ru='межрасчет'"))) = 0 Тогда
				Представление = Представление + ", " + НСтр("ru='межрасчет'");
			КонецЕсли;
			
		Иначе
			Представление = Представление + ", " + НРег(ВидВзаиморасчетов);
		КонецЕсли;
		
	Иначе
		Представление = Строка(ВидВзаиморасчетов);
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

Функция ПредставлениеРегистраторовВзаиморасчетов(Регистратор, ВидВзаиморасчетов, СвойстваРегистратора)
	
	ПредставлениеВыплаты = ПредставлениеВыплаты(ВидВзаиморасчетов, СвойстваРегистратора);
	
	ВидМестаВыплаты = ВедомостьНаВыплатуЗарплаты.ВидМестаВыплатыПоДокументу(Регистратор);
	Если Перечисления.ВидыМестВыплатыЗарплаты.ЭтоБезналично(ВидМестаВыплаты) Тогда
		ТипРегистратораСтрокой = НСтр("ru='Банк, вед.'");
	ИначеЕсли Перечисления.ВидыМестВыплатыЗарплаты.ЭтоНаличными(ВидМестаВыплаты) Тогда
		ТипРегистратораСтрокой = НСтр("ru='Касса, вед.'");
	Иначе
		ТипРегистратораСтрокой = ОбщегоНазначения.ПредставлениеОбъекта(Регистратор.Метаданные());
	КонецЕсли;
	ПредставлениеРегистратора = 
		СтрШаблон(
			НСтр("ru='%1 № %2 от %3'"), 
			ТипРегистратораСтрокой, 
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СвойстваРегистратора.Номер, Истина, Истина),
			Формат(СвойстваРегистратора.Дата, "ДФ=dd.MM.yy"));
	
	Возврат 
		СтрШаблон(
			НСтр("ru='%1 (%2)'"),
			ПредставлениеВыплаты,
			ПредставлениеРегистратора);
	
КонецФункции

Процедура ДобавитьСтрокиРаздела(СтрокиРаздела, СтрокиДанных, НастройкиВывода, СоответствиеСтрокРазделаСтрокамДанных, СоответствиеПользовательскихПолей, ИменаИзмерений, Макет, ИмяВертикальнойОбласти, СдельныйЗаработокСотрудников = Неопределено, ВывестиСдельныйЗаработокПоФизическомуЛицу = Ложь)
	
	ВидРасчета = Неопределено;
	ПриоритетВидаРасчета = Неопределено;
	ПериодДействия = Неопределено;
	ДокументОснование = Неопределено;
	
	ВыводитьОтработанноеОплаченноеВремя = НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя
		И СтрокиДанных.Колонки.Найти("ОплаченоДней") <> Неопределено;
	
	УчитыватьПриоритетВидаРасчета = СтрокиДанных.Колонки.Найти("ПриоритетВидаРасчета") <> Неопределено;
	
	СтрокаВидаРасчета = Ложь;
	РасшифровкиВидаРасчета = Новый Массив;
	СтрокаРасшифровкиВидаРасчета = Неопределено;
	Для Каждого СтрокаДанных Из СтрокиДанных Цикл
		
		Если НастройкиВывода.ВыводитьПериодыНачислений Тогда
			
			Если СтрокиДанных.Колонки.Найти("ПериодДействияПредставление") = Неопределено Тогда
				ПериодДействияСтроки = СтрокаДанных.ПериодДействия;
			Иначе
				ПериодДействияСтроки = СтрокаДанных.ПериодДействияПредставление;
			КонецЕсли;
			
			
		Иначе
			ПериодДействияСтроки = Неопределено;
		КонецЕсли;
		
		Если Не СтрокаВидаРасчета И ЗначениеЗаполнено(СтрокаДанных.Сумма) Тогда
			СтрокаВидаРасчета = Истина;
		ИначеЕсли ВидРасчета <> СтрокаДанных.ВидРасчета Тогда
			СтрокаВидаРасчета = Истина;
		ИначеЕсли НастройкиВывода.ВыводитьПериодыНачислений
			И ПериодДействия <> ПериодДействияСтроки Тогда
			
			СтрокаВидаРасчета = Истина;
			
		ИначеЕсли НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний
			И ДокументОснование <> СтрокаДанных.ДокументОснование Тогда
			
			СтрокаВидаРасчета = Истина;
			
		ИначеЕсли УчитыватьПриоритетВидаРасчета
			И ПриоритетВидаРасчета <> СтрокаДанных.ПриоритетВидаРасчета Тогда
			
			СтрокаВидаРасчета = Истина;
			
		КонецЕсли;
		
		Если СтрокаВидаРасчета Тогда
			
			ВидРасчета = СтрокаДанных.ВидРасчета;
			Если УчитыватьПриоритетВидаРасчета Тогда
				ПриоритетВидаРасчета = СтрокаДанных.ПриоритетВидаРасчета;
			КонецЕсли;
			Если НастройкиВывода.ВыводитьПериодыНачислений Тогда
				ПериодДействия = ПериодДействияСтроки;
			КонецЕсли;
			
			Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний Тогда
				ДокументОснование = СтрокаДанных.ДокументОснование;
			КонецЕсли;
			
			РасшифровкиВидаРасчета.Очистить();
			СтрокаРасшифровкиВидаРасчета = Неопределено;
			
		КонецЕсли;
		
		Если Не СтрокаВидаРасчета
			И Не НастройкиВывода.ВыводитьВсеПоказателиНачисленийИУдержаний Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если СтрокаВидаРасчета
			Или Не НастройкиВывода.ВыводитьРасшифровкиОднойСтрокой Тогда
			
			СтрокаОтчета = СтрокиРаздела.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаОтчета, СтрокаДанных);
			СтрокаОтчета.ИмяОбласти = "Строка";
			
			СтрокаОтчета.СтрокаДанных = СтрокаДанныхИзСоответствияСтрок(
				СоответствиеСтрокРазделаСтрокамДанных,
				СтрокаОтчета,
				ИменаИзмерений);
			
		КонецЕсли;
		
		СтрокаОтчетаОснования = Неопределено;
		Если СтрокаВидаРасчета Тогда
			
			СтрокаОтчета.ИмяОбласти = "Строка";
			
			// Добавим расшифровку по документу основанию
			Если НастройкиВывода.ВыводитьОснованияНачисленийИУдержаний
				И ЗначениеЗаполнено(СтрокаДанных.ДокументОснование) Тогда
				
				Если НастройкиВывода.ВыводитьРасшифровкиОднойСтрокой Тогда
					РасшифровкиВидаРасчета.Добавить(
						РасшифровкаПоСтроке(
							СтрокаДанныхИзСоответствияСтрок(
								СоответствиеСтрокРазделаСтрокамДанных,
								СтрокаДанных,
								ИменаИзмерений),
							СоответствиеПользовательскихПолей,
							Макет,
							"СтрокаРасшифровкаДокументОснование|" + ИмяВертикальнойОбласти));
				Иначе
					
					СтрокаОтчетаОснования = СтрокиРаздела.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаОтчетаОснования, СтрокаОтчета);
					СтрокаОтчетаОснования.ИмяОбласти = "СтрокаРасшифровкаДокументОснование";
					СтрокаОтчетаОснования.Расшифровка = Истина;
					СтрокаОтчетаОснования.СтрокаДанных = СтрокаОтчета.СтрокаДанных
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если НастройкиВывода.РасшифровыватьСдельныйЗаработокПоВидамРабот
				И СдельныйЗаработокСотрудников <> Неопределено Тогда
				
				Если ТипЗнч(ВидРасчета) = Тип("ПланВидовРасчетаСсылка.Начисления") Тогда
					
					Если ЭтоНачислениеСдельнойОплатыТруда(ВидРасчета) Тогда
						
						КоллекцииСдельногоЗаработка = Новый Массив;
						
						СтруктураПоиска = Новый Структура("МесяцНачисления,ФизическоеЛицо,СтатьяФинансирования,Сотрудник,ДатаНачала");
						ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаОтчета.СтрокаДанных);
						СтрокиСдельногоЗаработка = СдельныйЗаработокСотрудников.НайтиСтроки(СтруктураПоиска);
						КоллекцииСдельногоЗаработка.Добавить(СтрокиСдельногоЗаработка);
						
						Если КоллекцииСдельногоЗаработка.Количество() > 0 Тогда
							
							Если ВывестиСдельныйЗаработокПоФизическомуЛицу Тогда
								
								СтруктураПоиска.Сотрудник = Справочники.Сотрудники.ПустаяСсылка();
								СтрокиСдельногоЗаработка = СдельныйЗаработокСотрудников.НайтиСтроки(СтруктураПоиска);
								Если СтрокиСдельногоЗаработка.Количество() > 0 Тогда
									КоллекцииСдельногоЗаработка.Добавить();
								КонецЕсли;
								
							КонецЕсли;
							
							Для Каждого СтрокиСдельногоЗаработка Из КоллекцииСдельногоЗаработка Цикл
								
								Для Каждого СтрокаСдельногоЗаработка Из СтрокиСдельногоЗаработка Цикл
									
									Если НастройкиВывода.ВыводитьРасшифровкиОднойСтрокой Тогда
										РасшифровкиВидаРасчета.Добавить(
											РасшифровкаПоСтроке(
												СтрокаСдельногоЗаработка,
												СоответствиеПользовательскихПолей,
												Макет,
												"СтрокаРасшифровкаСдельногоЗаработка|" + ИмяВертикальнойОбласти));
										
									ИначеЕсли СтрокаВидаРасчета Тогда
										
										СтрокаОтчетаПоказателя = СтрокиРаздела.Добавить();
										ЗаполнитьЗначенияСвойств(СтрокаОтчетаПоказателя, СтрокаСдельногоЗаработка);
										СтрокаОтчетаПоказателя.ИмяОбласти = "СтрокаРасшифровкаСдельногоЗаработка";
										СтрокаОтчетаПоказателя.СтрокаДанных = СтрокаОтчета.СтрокаДанных;
										СтрокаОтчетаПоказателя.Расшифровка = Истина;
										
									КонецЕсли;
									
								КонецЦикла;
								
							КонецЦикла;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ВыводитьОтработанноеОплаченноеВремя Тогда
				
				Если ЗначениеЗаполнено(СтрокаДанных.ОплаченоДней) И СтрокаДанных.ОплаченоДней <> 0 Тогда
					СтрокаОтчета.ОплаченныеДниЧасы = Формат(СтрокаДанных.ОплаченоДней, "ЧДЦ=2; ЧН=") + Символы.НПП + НСтр("ru='дн'") + ".";
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаДанных.ОплаченоЧасов) И СтрокаДанных.ОплаченоЧасов <> 0 Тогда
					
					Если Не ПустаяСтрока(СтрокаОтчета.ОплаченныеДниЧасы) Тогда
						СтрокаОтчета.ОплаченныеДниЧасы = СтрокаОтчета.ОплаченныеДниЧасы + Символы.НПП + НСтр("ru='и'") + " ";
					КонецЕсли;
					
					СтрокаОтчета.ОплаченныеДниЧасы = Формат(СтрокаДанных.ОплаченоЧасов, "ЧДЦ=2; ЧН=") + Символы.НПП + НСтр("ru='чс'") + ".";
					
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли Не НастройкиВывода.ВыводитьРасшифровкиОднойСтрокой Тогда
			СтрокаОтчета.ИмяОбласти = "СтрокаРасшифровкаПоказатель";
			СтрокаОтчета.Расшифровка = Истина;
		КонецЕсли;
		
		// Вывод расшифровки показателей
		Если НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний
			Или НастройкиВывода.ВыводитьВсеПоказателиНачисленийИУдержаний Тогда
			
			Если ЗначениеЗаполнено(СтрокаДанных.Показатель) Тогда
				
				Если НастройкиВывода.ВыводитьРасшифровкиОднойСтрокой Тогда
					РасшифровкиВидаРасчета.Добавить(
						РасшифровкаПоСтроке(
							СтрокаДанныхИзСоответствияСтрок(
								СоответствиеСтрокРазделаСтрокамДанных,
								СтрокаДанных,
								ИменаИзмерений),
							СоответствиеПользовательскихПолей,
							Макет,
							"СтрокаРасшифровкаПоказатель|" + ИмяВертикальнойОбласти));
				ИначеЕсли СтрокаВидаРасчета Тогда
					
					СтрокаОтчетаПоказателя = СтрокиРаздела.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаОтчетаПоказателя, СтрокаОтчета);
					СтрокаОтчетаПоказателя.ИмяОбласти = "СтрокаРасшифровкаПоказатель";
					СтрокаОтчетаПоказателя.СтрокаДанных = СтрокаОтчета.СтрокаДанных;
					СтрокаОтчетаПоказателя.Расшифровка = Истина;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НастройкиВывода.ВыводитьРасшифровкиОднойСтрокой Тогда
			
			Если РасшифровкиВидаРасчета.Количество() > 0 Тогда
				
				Если СтрокаРасшифровкиВидаРасчета = Неопределено Тогда
					СтрокаРасшифровкиВидаРасчета = СтрокиРаздела.Добавить();
					СтрокаРасшифровкиВидаРасчета.Расшифровка = Истина;
					СтрокаРасшифровкиВидаРасчета.ИмяОбласти = "СтрокаРасшифровка";
				КонецЕсли;
				
				СтрокаРасшифровкиВидаРасчета.РасшифровкаВидаРасчета = СтрСоединить(РасшифровкиВидаРасчета, "; ");
				
			КонецЕсли;
			
		КонецЕсли;
		
		СтрокаВидаРасчета = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

Функция РасшифровкаПоСтроке(СтрокаДанных, СоответствиеПользовательскихПолей, Макет, ИмяОбласти)
	
	ВыводимаяОбласть = Макет.ПолучитьОбласть(ИмяОбласти);
	Если ВыводимаяОбласть.ТекущаяОбласть.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст Тогда
		Возврат "";
	ИначеЕсли ВыводимаяОбласть.ТекущаяОбласть.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Шаблон Тогда
		ТекстОбласти = ВыводимаяОбласть.ТекущаяОбласть.Текст;
	Иначе
		ТекстОбласти = "[" + ВыводимаяОбласть.ТекущаяОбласть.Параметр + "]";
	КонецЕсли;
	
	МассивПараметров = СтрРазделить(ТекстОбласти, "]");
	МассивПараметров.Удалить(МассивПараметров.Количество() - 1);
	
	СтруктураПараметров = Новый Структура;
	Для Каждого ОписаниеПараметра Из МассивПараметров Цикл
		
		МассивПодстрок = СтрРазделить(ОписаниеПараметра, "[");
		Если МассивПодстрок.Количество() <> 2 Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПараметров.Вставить(МассивПодстрок[1]);
		
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, СтрокаДанных);
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, ЗначенияЗаполненияПользовательскихПолей(СоответствиеПользовательскихПолей, СтрокаДанных));
	
	Для Каждого ПараметрОбласти Из СтруктураПараметров Цикл
		ТекстОбласти = СтрЗаменить(ТекстОбласти, "[" + ПараметрОбласти.Ключ + "]", ПараметрОбласти.Значение);
	КонецЦикла;
	
	ТекстОбласти = СтрЗаменить(ТекстОбласти, " ", Символы.НПП);
	
	Возврат ТекстОбласти;
	
КонецФункции

Процедура ДобавитьВСоответствиеСтрокуДанных(СоответствиеСтрок, СтрокаДанных, ИменаИзмеренийСтрокой)
	
	ЗначенияИзмерения = СоответствиеСтрок;
	
	ИменаИзмерений = СтрРазделить(ИменаИзмеренийСтрокой, ",");
	Для Каждого ИмяИзмерения Из ИменаИзмерений Цикл
		
		Если СтрокаДанных.Владелец().Колонки.Найти(ИмяИзмерения) = Неопределено Тогда
			КлючИзмерения = НСтр("ru = 'Неопределено'");
		Иначе
			
			КлючИзмерения = СтрокаДанных[ИмяИзмерения];
			Если КлючИзмерения = Неопределено Или КлючИзмерения = Null Тогда
				КлючИзмерения = НСтр("ru = 'Неопределено'");
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтрЗаканчиваетсяНа(ИменаИзмеренийСтрокой, ИмяИзмерения) Тогда
			ЗначенияИзмерения.Вставить(КлючИзмерения, СтрокаДанных);
		Иначе
			ЗначенияИзмерения = ПолучитьЗначенияИзмерения(ЗначенияИзмерения, КлючИзмерения);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СтрокаДанныхИзСоответствияСтрок(СоответствиеСтрок, СтрокаОтчета, ИменаИзмеренийСтрокой)
	
	ЗначенияИзмерения = СоответствиеСтрок;
	
	ИменаИзмерений = СтрРазделить(ИменаИзмеренийСтрокой, ",");
	Для Каждого ИмяИзмерения Из ИменаИзмерений Цикл
		
		ЗначениеИзмерения = СтрокаОтчета[ИмяИзмерения];
		Если ЗначениеИзмерения = Неопределено Или ЗначениеИзмерения = Null Тогда
			ЗначениеИзмерения = НСтр("ru = 'Неопределено'");
		КонецЕсли;
		
		ЗначенияИзмерения = ЗначенияИзмерения.Получить(ЗначениеИзмерения);
		Если ЗначенияИзмерения = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Если ТипЗнч(ЗначенияИзмерения) = Тип("СтрокаДереваЗначений") Тогда
			Возврат ЗначенияИзмерения;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

Функция ПолучитьЗначенияИзмерения(СоответствиеСтрок, Измерение)
	
	ЗначенияИзмерения = СоответствиеСтрок.Получить(Измерение);
	Если ЗначенияИзмерения = Неопределено Тогда
		ЗначенияИзмерения = Новый Соответствие;
		СоответствиеСтрок.Вставить(Измерение, ЗначенияИзмерения);
	КонецЕсли;
	
	Возврат ЗначенияИзмерения;
	
КонецФункции

Функция ПредставлениеРабочегоМеста(СтрокаСотрудника)
	
	Если СтрокаСотрудника.Сотрудник <> СтрокаСотрудника.ГоловнойСотрудник Тогда
		Возврат СтрокаСотрудника.СотрудникУточнениеНаименования;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаСотрудника.ДолжностьНаКонецПериода) Тогда
		Возврат Строка(СтрокаСотрудника.ДолжностьНаКонецПериода)
			+ " (" + СтрокаСотрудника.ВидЗанятостиГоловногоСотрудникаНаКонецПериода + ")";
	КонецЕсли;
	
	Возврат НСтр("ru = 'Прочее'");
	
КонецФункции

Процедура ВывестиОбластиСтроки(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, Параметры = Неопределено)
	
	ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, Параметры);
	ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, Параметры)
	
КонецПроцедуры

Процедура ВывестиОбластиТекстовуюОбласть(ДокументРезультат, Макет, НастройкиВывода, ТекстоваяИнформация, ДанныеПечатнойФормы)
	
	Если Не ПустаяСтрока(ТекстоваяИнформация) Тогда
		
		ДанныеПечатнойФормыЛокально = ДополненныеПараметры(ДанныеПечатнойФормы, Новый Структура("ТекстоваяИнформация", ТекстоваяИнформация));
		
		ВывестиОбластиСтроки(ДокументРезультат, Макет, "ТекстоваяИнформация", НастройкиВывода, ДанныеПечатнойФормыЛокально);
		НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, Параметры = Неопределено, Объединить = Ложь)
	
	ПрисоединитьОбласть = Ложь;
	Для каждого ИмяОбласти Из НастройкиВывода.ИменаОбластейНачислено Цикл
		
		Область = Макет.ПолучитьОбласть(ИмяОбластиСтроки + "|" + ИмяОбласти);
		ЗаполнитьПараметрыОбласти(Область, Параметры);
		
		Если ПрисоединитьОбласть Тогда
			ДокументРезультат.Присоединить(Область);
		Иначе
			ДокументРезультат.Вывести(Область);
			ПрисоединитьОбласть = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Объединить Тогда
		НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, Параметры = Неопределено, Объединить = Ложь)
	
	Для каждого ИмяОбласти Из НастройкиВывода.ИменаОбластейУдержаноВыплачено Цикл
		
		Область = Макет.ПолучитьОбласть(ИмяОбластиСтроки + "|" + ИмяОбласти);
		ЗаполнитьПараметрыОбласти(Область, Параметры);
		
		ДокументРезультат.Присоединить(Область);
		
	КонецЦикла;
	
	Если Объединить Тогда
		НастроитьОбъединение(ДокументРезультат, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + 1,
			ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено));
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыОбласти(Область, Параметры)
	
	Если Параметры <> Неопределено И Область.Параметры.Количество() > 0 Тогда
		
		Если ТипЗнч(Параметры) = Тип("Массив") Тогда
			
			Для Каждого ЗначенияПараметров Из Параметры Цикл
				Область.Параметры.Заполнить(ЗначенияПараметров);
			КонецЦикла;
			
		Иначе
			Область.Параметры.Заполнить(Параметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция НастройкиВыводаРасчетногоЛистка(НастройкиОтчета, КлючВарианта, ПредварительныйПросмотр = Ложь, НаАванс = Ложь)
	
	НастройкиВывода = Новый Структура;
	
	НастройкиВывода.Вставить("ПредварительныйПросмотр", ПредварительныйПросмотр);
	НастройкиВывода.Вставить("РассчитатьИтогиПоДанным", ПредварительныйПросмотр Или НаАванс);
	
	ИмяПараметра = "ВыводитьСуммуНачисленнойЗарплатыВРазделеКВыплате";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "ВыводитьПериодыНачислений";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "ВыводитьОтработанноеОплаченноеВремя";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "ДетализироватьВыплатыПоВедомостям";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "ВыводитьСправочнуюИнформацию";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "ВыводитьИнформациюОЛьготах";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "ВыводитьОснованияНачисленийИУдержаний";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "РасшифровыватьСдельныйЗаработокПоВидамРабот";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "ВыводитьПоказателиНачисленийИУдержаний";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "ВыводитьВсеПоказателиНачисленийИУдержаний";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "ВыводитьРасшифровкиОднойСтрокой";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИмяПараметра = "ВыводитьОсобенностиРасчетаНДФЛ";
	НастройкиВывода.Вставить(ИмяПараметра, Не НаАванс И (ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина));
	
	ИмяПараметра = "ВыводитьИнформациюОНачисленныхВзносахВПФР";
	НастройкиВывода.Вставить(ИмяПараметра, Не НаАванс И (ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина));
	
	ИмяПараметра = "ГруппировкаОтчета";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра));
	
	ИмяПараметра = "НеЖалетьБумаги";
	НастройкиВывода.Вставить(ИмяПараметра, ЗначениеПараметра(НастройкиОтчета, ИмяПараметра) = Истина);
	
	ИменаОбластейНачислено = "ВидНачислено";
	ИменаОбластейУдержаноВыплачено = "ВидУдержаноВыплачено";
	
	Если НастройкиВывода.ВыводитьПериодыНачислений Тогда
		ИменаОбластейНачислено = ИменаОбластейНачислено + ",ПериодНачислено";
		ИменаОбластейУдержаноВыплачено = ИменаОбластейУдержаноВыплачено + ",ПериодУдержаноВыплачено";
	КонецЕсли;
	
	Если НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя Тогда
		ИменаОбластейНачислено = ИменаОбластейНачислено + ",РабочиеОплаченныеДни";
	КонецЕсли;
	
	ИменаОбластейНачислено = ИменаОбластейНачислено + ",СуммаНачислено";
	ИменаОбластейУдержаноВыплачено = ИменаОбластейУдержаноВыплачено + ",СуммаУдержаноВыплачено";
	
	НастройкиВывода.Вставить("ИменаОбластейНачислено", СтрРазделить(ИменаОбластейНачислено, ","));
	НастройкиВывода.Вставить("ИменаОбластейУдержаноВыплачено", СтрРазделить(ИменаОбластейУдержаноВыплачено, ","));
	
	Если Не ПустаяСтрока(КлючВарианта) Тогда
		
		НастройкиВывода.Вставить("ГруппироватьПоИсточникамФинансирования", (КлючВарианта = "РасчетныйЛистокСРазбивкойПоИсточникамФинансирования")
			Или (КлючВарианта = "РасчетныйЛистокСРазбивкойПоИсточникамФинансированияПерваяПоловинаМесяца")
			Или (КлючВарианта = "РасчетныйЛистокПоРабочимМестамИСРазбивкойПоИсточникамФинансирования"));
		
	Иначе
		НастройкиВывода.Вставить("ГруппироватьПоИсточникамФинансирования", Ложь);
	КонецЕсли;
	
	НастройкиВывода.Вставить("ИсключатьПустыеСтрокиНачисленийУдержаний",
		ИсключатьПустыеСтрокиНачисленийУдержаний(НастройкиОтчета));
	
	Возврат НастройкиВывода;
	
КонецФункции

Функция ИсключатьПустыеСтрокиНачисленийУдержаний(НастройкиОтчета)
	
	Если ИспользуетсяОтборПоПолю(НастройкиОтчета.Структура[0], "Сумма")
		И ИспользуетсяОтборПоПолю(НастройкиОтчета.Структура[0], "ОтработаноДней")
		И ИспользуетсяОтборПоПолю(НастройкиОтчета.Структура[0], "ОтработаноЧасов")
		И ИспользуетсяОтборПоПолю(НастройкиОтчета.Структура[0], "ОплаченоДней")
		И ИспользуетсяОтборПоПолю(НастройкиОтчета.Структура[0], "ОплаченоЧасов")
		И ИспользуетсяОтборПоПолю(НастройкиОтчета.Структура[0], "Значение") Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ИспользуетсяОтборПоПолю(Структура, ИмяПоля)
	
	ЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Структура.Отбор, ИмяПоля);
	Если ЭлементыОтбора.Количество() > 0 И ЭлементыОтбора[0].Использование Тогда
		
		Если ЭлементыОтбора[0].Родитель <> НЕопределено И ЭлементыОтбора[0].Родитель.Использование Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого ПодчиненнаяСтруктура Из Структура.Структура Цикл
		
		Если ИспользуетсяОтборПоПолю(ПодчиненнаяСтруктура, ИмяПоля) Тогда
			Возврат Истина
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ЗначениеПараметра(НастройкиОтчета, ИмяПараметра)

	Значение = Неопределено;
	ПараметрОтчета = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Если ПараметрОтчета <> Неопределено И ПараметрОтчета.Использование = Истина Тогда
		Значение = ПараметрОтчета.Значение;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Функция НастроитьОбъединение(ДокументРезультат, НомерКолонки, Ширина, НомерСтроки = Неопределено)
	
	Если НомерСтроки = Неопределено Тогда
		НомерСтроки = ДокументРезультат.ВысотаТаблицы;
	КонецЕсли;
	
	Область = ДокументРезультат.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки + Ширина - 1);
	Область.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	Область.Объединить();
	
КонецФункции

Функция ШиринаОбластей(Макет, ИменаОбластей, СтрокаИменИсключаемыхОбластей = "")
	
	Ширина = 0;
	ИсключаемыеОбласти = СтрРазделить(ВРег(СтрокаИменИсключаемыхОбластей), ",");
	
	Для каждого ИмяОбласти Из ИменаОбластей Цикл
		
		Если ИсключаемыеОбласти.Найти(ВРег(ИмяОбласти)) <> Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		Область = Макет.ПолучитьОбласть("Строка|" + ИмяОбласти);
		Ширина = Ширина + Область.ШиринаТаблицы;
		
	КонецЦикла;
	
	Возврат Ширина;
	
КонецФункции

Процедура УдалитьСтрокиСНулевойСуммой(КоллекцияСтрок)
	
	СтрокиКУдалению = Новый Массив;
	
	Для каждого СтрокаКоллекции Из КоллекцияСтрок Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаКоллекции.Сумма) Тогда
			СтрокиКУдалению.Добавить(СтрокаКоллекции);
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		КоллекцияСтрок.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьОбщиеИтоги(ПараметрыОбщихИтогов, Итоги)
	
	Если ЗначениеЗаполнено(Итоги.СуммаДолгаНаНачалоМесяца) Тогда
		
		Если Итоги.СуммаДолгаНаНачалоМесяца > 0 Тогда
			ПараметрыОбщихИтогов.ДолгОрганизацииНаНачалоМесяца = Итоги.СуммаДолгаНаНачалоМесяца;
		Иначе
			ПараметрыОбщихИтогов.ДолгСотрудникаНаНачалоМесяца = - Итоги.СуммаДолгаНаНачалоМесяца;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Итоги.СуммаДолгаНаКонецМесяца) Тогда
		
		Если Итоги.СуммаДолгаНаКонецМесяца > 0 Тогда
			ПараметрыОбщихИтогов.ДолгОрганизацииНаКонецМесяца = Итоги.СуммаДолгаНаКонецМесяца;
		Иначе
			ПараметрыОбщихИтогов.ДолгСотрудникаНаКонецМесяца = - Итоги.СуммаДолгаНаКонецМесяца;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиИтогиВзаиморасчетов(ДокументРезультат, Макет, НастройкиВывода, ПараметрыИтогов, ДанныеПечатнойФормы)
	
	Если ЗначениеЗаполнено(ПараметрыИтогов.ДолгСотрудникаНаНачалоМесяца) Тогда
		ИмяОбластиСтроки = "ДолгЗаРаботником";
	Иначе
		ИмяОбластиСтроки = "ДолгЗаПредприятием";
	КонецЕсли;
	
	ВывестиОбластиСтрокиНачислено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, ДанныеПечатнойФормы);
	НастроитьОбъединение(ДокументРезультат, 1, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено, "СуммаНачислено"));
	
	Если ЗначениеЗаполнено(ПараметрыИтогов.ДолгСотрудникаНаКонецМесяца) Тогда
		ИмяОбластиСтроки = "ДолгЗаРаботником";
	Иначе
		ИмяОбластиСтроки = "ДолгЗаПредприятием";
	КонецЕсли;
	
	ПрисоединитьОбластиСтрокиУдержаноВыплачено(ДокументРезультат, Макет, ИмяОбластиСтроки, НастройкиВывода, ДанныеПечатнойФормы);
	
	НастроитьОбъединение(ДокументРезультат, ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейНачислено) + 1,
		ШиринаОбластей(Макет, НастройкиВывода.ИменаОбластейУдержаноВыплачено, "СуммаУдержаноВыплачено"));
	
КонецПроцедуры

Процедура ВывестиТелоСправочнойИнформации(ДокументРезультат, Макет, КоллекцияСправочныхДанных, ДополнительныеПараметры)
	
	КоличествоСекций = КоличествоСекций(ДокументРезультат);
	
	НомерСекции = 0;
	Для каждого СтрокаСправочно Из КоллекцияСправочныхДанных Цикл
		
		Область = Макет.ПолучитьОбласть("СуммаЛьготыСправочно|ЛьготыСправочно");
		
		ДополнительныеПараметры.Добавить(СтрокаСправочно);
		ЗаполнитьПараметрыОбласти(Область, ДополнительныеПараметры);
		
		Если НомерСекции = 0 Тогда
			ДокументРезультат.Вывести(Область);
		Иначе
			ДокументРезультат.Присоединить(Область);
		КонецЕсли;
		
		НомерСекции = НомерСекции + 1;
		Если НомерСекции = КоличествоСекций Тогда
			НомерСекции = 0;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция КоличествоСекций(ДокументРезультат)
	
	Возврат Цел((ДокументРезультат.ШиринаТаблицы  + 1) / 11);
	
КонецФункции

Функция СтрокаСНепереносимымиПробелами(ИсходнаяСтрока)
	
	Возврат СтрЗаменить(ИсходнаяСтрока, " ", Символы.НПП);
	
КонецФункции

Функция ДополненныеПараметры(ДанныеПечатнойФормы, ДополнительныеПараметры, ДополнительныеПараметры1 = Неопределено)
	
	КопияДанныхПечатнойФормы = ОбщегоНазначения.СкопироватьРекурсивно(ДанныеПечатнойФормы);
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		КопияДанныхПечатнойФормы.Добавить(ДополнительныеПараметры);
	КонецЕсли;
	
	Если ДополнительныеПараметры1 <> Неопределено Тогда
		КопияДанныхПечатнойФормы.Добавить(ДополнительныеПараметры1);
	КонецЕсли;
	
	Возврат КопияДанныхПечатнойФормы;
	
КонецФункции

Функция ОписаниеИтоговРасчетногоЛисткаПоФизическимЛицам()
	
	Итоги = Новый Структура;
	
	Итоги.Вставить("ДолгОрганизацииНаНачалоМесяца", 0);
	Итоги.Вставить("ДолгСотрудникаНаНачалоМесяца", 0);
	
	Итоги.Вставить("ДолгОрганизацииНаКонецМесяца", 0);
	Итоги.Вставить("ДолгСотрудникаНаКонецМесяца", 0);
	
	Возврат Итоги;
	
КонецФункции

Функция ОписаниеОбщихИтоговРасчетногоЛисткаПоФизическимЛицам()
	
	ОбщиеИтоги = ОписаниеИтоговРасчетногоЛисткаПоФизическимЛицам();
	
	ОбщиеИтоги.Вставить("ИзлишнеУдержаноНДФЛНаНачалоМесяца", 0);
	ОбщиеИтоги.Вставить("ИзлишнеУдержаноНДФЛНаКонецМесяца", 0);
	
	Возврат ОбщиеИтоги;
	
КонецФункции

Процедура УвеличитьРезультатНаСуммуСтроки(Результат, СтрокаССуммой)
	
	Если СтрокаССуммой <> Неопределено И ЗначениеЗаполнено(СтрокаССуммой.Сумма) Тогда
		Результат = Результат + СтрокаССуммой.Сумма;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область РасчетныйЛистокПоРабочимМестам

Процедура ПриКомпоновкеРезультатаРасчетныйЛистокПоРабочимМестам(ОбъектОтчета, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, НаАванс = Ложь)
	
	СтандартнаяОбработка = Ложь;
	
	Попытка
		
		КомпоновщикНастроек = ОбъектОтчета.КомпоновщикНастроек;
		СхемаКомпоновкиДанных = ОбъектОтчета.СхемаКомпоновкиДанных;
		
		КлючВарианта = КлючВарианта(КомпоновщикНастроек);
		
		ВыводитьПоРабочимМестам = (КлючВарианта = "РасчетныйЛистокПоРабочимМестам")
			Или (КлючВарианта = "РасчетныйЛистокПоРабочимМестамИСРазбивкойПоИсточникамФинансирования");
		
		// Параметры документа
		ДокументРезультат.ТолькоПросмотр = Истина;
		ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РасчетныйЛистокПоРабочимМестам";
		ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		ДокументРезультат.АвтоМасштаб = Истина;
		
		НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
		
		ЗаполнитьПользовательскиеПоляВариантаОтчета(КлючВарианта, НастройкиОтчета, НаАванс);
		НастроитьВариантОтчетаРасчетныйЛисток(НастройкиОтчета);
		
		// Нужно проверить включена ли группировка по подразделениям.
		ЕстьГруппировкаПоПодразделению = Ложь;
		ПараметрГруппировки = Новый ПараметрКомпоновкиДанных("РазбиватьПоПодразделениям");
		Если НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрГруппировки).Использование Тогда
			ЕстьГруппировкаПоПодразделению = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрГруппировки).Значение;
		КонецЕсли;
		
		НастройкиВывода = НастройкиВыводаРасчетногоЛистка(НастройкиОтчета, КлючВарианта);
		МакетКомпоновки = МакетКомпоновкиДанныхДляКоллекцииЗначений(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
		
		СоответствиеПользовательскихПолей = СоответствиеПользовательскихПолей(НастройкиОтчета);
		Если КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ДанныеДокумента") Тогда
			
			НаборыВнешнихДанных = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.ДанныеДокумента;
			КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Удалить("ДанныеДокумента");
			
		Иначе
			НаборыВнешнихДанных = НаборыВнешнихДанныхАнализНачисленийИУдержаний();
		КонецЕсли;
		
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, НаборыВнешнихДанных, , Истина);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ДанныеОтчета = Новый ДеревоЗначений;
		ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.АнализНачисленийИУдержаний.ПФ_MXL_РасчетныйЛисток");
		
		Макеты = Новый Структура("Шапка1,АвансовыеПлатежи,Шапка2,Подработка,НачисленоУдержано,ПрочиеВыплачено,Льготы,СтрокаДвижений,Итог12,Итог34,Итог56,Сальдо,Группировка,СтатьяФинансирования,ИтогСтатьяФинансирования,Итог12ВНатуральнойФорме");
		
		Макеты.Шапка1 = Макет.ПолучитьОбласть("Шапка1");
		Макеты.АвансовыеПлатежи = Макет.ПолучитьОбласть("АвансовыеПлатежи");
		Макеты.Шапка2 = Макет.ПолучитьОбласть("Шапка2");
		Макеты.Подработка = Макет.ПолучитьОбласть("Подработка");
		Макеты.НачисленоУдержано = Макет.ПолучитьОбласть("НачисленоУдержано");
		Макеты.ПрочиеВыплачено = Макет.ПолучитьОбласть("ПрочиеВыплачено");
		Макеты.Льготы = Макет.ПолучитьОбласть("Льготы");
		Макеты.СтрокаДвижений = Макет.ПолучитьОбласть("СтрокаДвижений");
		Макеты.Итог12 = Макет.ПолучитьОбласть("Итог12");
		Макеты.Итог12ВНатуральнойФорме = Макет.ПолучитьОбласть("Итог12ВНатуральнойФорме");
		Макеты.Итог34 = Макет.ПолучитьОбласть("Итог34");
		Макеты.Итог56 = Макет.ПолучитьОбласть("Итог56");
		Макеты.Сальдо = Макет.ПолучитьОбласть("Сальдо");
		Макеты.Группировка = Макет.ПолучитьОбласть("Группировка");
		Макеты.СтатьяФинансирования = Макет.ПолучитьОбласть("СтатьяФинансирования");
		Макеты.ИтогСтатьяФинансирования = Макет.ПолучитьОбласть("ИтогСтатьяФинансирования");
		
		ЕстьГруппировкаПоОрганизации = МожноГруппировать(НастройкиОтчета.Порядок, "Организация");
		
		Если ЕстьГруппировкаПоПодразделению И НЕ МожноГруппировать(НастройкиОтчета.Порядок, "ПодразделениеСортировки.РеквизитДопУпорядочиванияИерархического", СоответствиеПользовательскихПолей) Тогда
			ЕстьГруппировкаПоПодразделению = Ложь;
		КонецЕсли;
		
		Организация = Неопределено;
		Подразделение = Неопределено;
		
		ДокументРезультатСтраница = Новый ТабличныйДокумент;
		Для каждого СтрокаМесяца Из ДанныеОтчета.Строки Цикл
			
			ДанныеПоНДФЛИВзносам = ДанныеПоНДФЛИВзносамПоРабочимМестам(СтрокаМесяца.Строки, СтрокаМесяца.МесяцНачисления, Истина, Ложь);
			КоллекцияДанных = СтрокаМесяца.Строки;
			
			ВыводимыеГруппировки = Новый Массив;
			
			Для каждого СтрокаКоллекции Из КоллекцияДанных Цикл
				
				Если ЕстьГруппировкаПоОрганизации И Организация <> СтрокаКоллекции.Организация Тогда
					
					Макеты.Группировка.Параметры.НазваниеПараметра = НСтр("ru='Организация'");
					Макеты.Группировка.Параметры.Значение = СтрокаКоллекции.Организация;
					
					ДокументРезультат.Вывести(Макеты.Группировка);
					Организация = СтрокаКоллекции.Организация;
					
				КонецЕсли;
				
				Если ЕстьГруппировкаПоПодразделению И Подразделение <> СтрокаКоллекции.ПодразделениеГоловногоСотрудникаНаКонецПериода Тогда
					
					Макеты.Группировка.Параметры.НазваниеПараметра = НСтр("ru='Подразделение'");
					Макеты.Группировка.Параметры.Значение = СтрокаКоллекции.ПодразделениеГоловногоСотрудникаНаКонецПериода;
					
					ДокументРезультат.Вывести(Макеты.Группировка);
					Подразделение = СтрокаКоллекции.ПодразделениеГоловногоСотрудникаНаКонецПериода;
					
				КонецЕсли;
				
				ВыводимаяКоллекция = СтрокаКоллекции;
				ВывестиСотрудникаРасчетныйЛистокПоРабочимМестам(ВыводимаяКоллекция, ДокументРезультат, Макеты, СоответствиеПользовательскихПолей, ДанныеПоНДФЛИВзносам, НастройкиВывода.ГруппироватьПоИсточникамФинансирования);
				
			КонецЦикла;
			
		КонецЦикла;
		
		Если ДокументРезультатСтраница.ВысотаТаблицы > 0 Тогда
			
			Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ДокументРезультат.Вывести(ДокументРезультатСтраница);
			
		КонецЕсли;
		
		ДопСвойства = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
		ДопСвойства.Вставить("ОтчетПустой", ДанныеОтчета.Строки.Количество() = 0);
		
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение НСтр("ru = 'В настройку отчета ""Расчетный листок по рабочим местам"" внесены критичные изменения. Отчет не будет сформирован.'")
			+ " " + КраткоеПредставлениеОшибки(Инфо);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ВывестиСотрудникаРасчетныйЛистокПоРабочимМестам(СтрокиГоловныхСотрудников, ДокументРезультатПромежуточный, Макеты, СоответствиеПользовательскихПолей, ДанныеПоНДФЛИВзносам, ГруппироватьПоИсточникамФинансирования)
	
	НастройкиПечатныхФорм = ЗарплатаКадры.НастройкиПечатныхФорм();
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	СтрокаПервогоГоловногоСотрудника = СтрокиГоловныхСотрудников;
	
	Если ГруппироватьПоИсточникамФинансирования Тогда
		СтрокаШапки = СтрокаПервогоГоловногоСотрудника.Строки[0].Строки[0];
	Иначе
		СтрокаШапки = СтрокаПервогоГоловногоСотрудника.Строки[0];
	КонецЕсли;
	
	Макеты.Шапка1.Параметры.Заполнить(СтрокаШапки);
	Макеты.Шапка1.Параметры.ПериодПредставление = Формат(СтрокаШапки.МесяцНачисления, "ДФ='ММММ гггг'");
	
	Если ЗначениеЗаполнено(Макеты.Шапка1.Параметры.СотрудникКод) Тогда
		Макеты.Шапка1.Параметры.СотрудникКод = ТабельныйНомерНаПечать(Макеты.Шапка1.Параметры.СотрудникКод);
	Иначе
		Макеты.Шапка1.Параметры.СотрудникКод = "";
	КонецЕсли;
	
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка1, СтрокаШапки, СоответствиеПользовательскихПолей);
	ЗаполнитьПараметрыПользовательскихПолей(Макеты.Шапка1, СтрокаПервогоГоловногоСотрудника, СоответствиеПользовательскихПолей, "КВыплате");

	Если СтрокаПервогоГоловногоСотрудника.Владелец().Колонки.Найти("РазрядКатегорияГоловногоСотрудникаНаКонецПериода") <> Неопределено
		И ЗначениеЗаполнено(СтрокаПервогоГоловногоСотрудника.РазрядКатегорияГоловногоСотрудникаНаКонецПериода) Тогда
		
		Макеты.Шапка1.Параметры.РазрядКатегорияСотрудника = ", " + СтрокаПервогоГоловногоСотрудника.РазрядКатегорияГоловногоСотрудникаНаКонецПериода;
	Иначе
		Макеты.Шапка1.Параметры.РазрядКатегорияСотрудника = "";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Макеты.Шапка1.Параметры.КВыплате) ИЛИ Макеты.Шапка1.Параметры.КВыплате < 0 Тогда
		Макеты.Шапка1.Параметры.КВыплате = 0;
	КонецЕсли;
	
	СтрокаДанныхПоНДФЛ = ДанныеПоНДФЛИВзносам.НДФЛ.Найти(СтрокаШапки.ФизическоеЛицо, "ФизическоеЛицо");
	Если СтрокаДанныхПоНДФЛ <> Неопределено Тогда
		Макеты.Шапка1.Параметры.Заполнить(СтрокаДанныхПоНДФЛ);
	КонецЕсли;
	
	Если НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений И ЗначениеЗаполнено(Макеты.Шапка1.Параметры.ПодразделениеГоловногоСотрудникаНаКонецПериода) Тогда
		Макеты.Шапка1.Параметры.ПодразделениеГоловногоСотрудникаНаКонецПериода = Макеты.Шапка1.Параметры.ПодразделениеГоловногоСотрудникаНаКонецПериода.ПолноеНаименование();
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макеты.Шапка1);
	
	Если СтрокаДанныхПоНДФЛ <> Неопределено
		И ЗначениеЗаполнено(СтрокаДанныхПоНДФЛ.АвансовыеПлатежи) Тогда
		
		Макеты.АвансовыеПлатежи.Параметры.АвансовыеПлатежи = СтрокаДанныхПоНДФЛ.АвансовыеПлатежи;
		ДокументРезультат.Вывести(Макеты.АвансовыеПлатежи);
		
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макеты.Шапка2);
	
	Если ГруппироватьПоИсточникамФинансирования Тогда
		ИтогиПоГруппам = ВывестиГруппировкиПоСтатьямФинансированияПоРабочимМестам(ДокументРезультат, СтрокиГоловныхСотрудников,  Макеты, СоответствиеПользовательскихПолей);
	Иначе
		ИтогиПоГруппам = ВывестиТелоРасчетногоЛисткаПоРабочимМестам(ДокументРезультат, СтрокиГоловныхСотрудников, Макеты, СоответствиеПользовательскихПолей);
	КонецЕсли;
	
	Если СтрокаДанныхПоНДФЛ <> Неопределено Тогда
		Макеты.Сальдо.Параметры.Заполнить(СтрокаДанныхПоНДФЛ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИтогиПоГруппам.СуммаДолгаНаКонецМесяца) И ИтогиПоГруппам.СуммаДолгаНаКонецМесяца >= 0 Тогда
		Макеты.Сальдо.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за предприятием на конец месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаКонецПериода = ИтогиПоГруппам.СуммаДолгаНаКонецМесяца;
	ИначеЕсли ЗначениеЗаполнено(ИтогиПоГруппам.СуммаДолгаНаКонецМесяца) И ИтогиПоГруппам.СуммаДолгаНаКонецМесяца < 0 Тогда
		Макеты.Сальдо.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за работником на конец месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаКонецПериода = -ИтогиПоГруппам.СуммаДолгаНаКонецМесяца;
	Иначе
		Макеты.Сальдо.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за предприятием на конец месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаКонецПериода = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца) И ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца >= 0 Тогда
		Макеты.Сальдо.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за предприятием на начало месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаНачалоПериода = ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца;
	ИначеЕсли ЗначениеЗаполнено(ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца) И ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца < 0 Тогда
		Макеты.Сальдо.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за работником на начало месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаНачалоПериода = -ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца;
	Иначе
		Макеты.Сальдо.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за предприятием на начало месяца'");
		Макеты.Сальдо.Параметры.СуммаДолгНаНачалоПериода = "";
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макеты.Сальдо);
	
	Ид = СтрокаШапки.ФизическоеЛицо.УникальныйИдентификатор();
	ДокументРезультат.Область(1, 1, ДокументРезультат.ВысотаТаблицы, ДокументРезультат.ШиринаТаблицы).Имя =
		"payslip_" + Формат(СтрокаШапки.МесяцНачисления, "ДФ=ММгггг") + "_0_" + СтрЗаменить(Ид, "-", "") + "_0";
	
	Если НЕ ДокументРезультатПромежуточный.ПроверитьВывод(ДокументРезультат) Тогда
		ДокументРезультатПромежуточный.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	ДокументРезультатПромежуточный.Вывести(ДокументРезультат);
	
КонецПроцедуры

Функция ВывестиГруппировкиПоСтатьямФинансированияПоРабочимМестам(ДокументРезультат, СтрокиГоловныхСотрудников,  Макеты, СоответствиеПользовательскихПолей)
	
	ИтогиПоГруппам = ОписаниеИтоговПоГруппам();
	
	СтатьиФинансирования = Новый Массив;
	СтрокиПоСтатьямФинансирования = Новый Соответствие;
	
	Для каждого СтрокаСотрудника Из СтрокиГоловныхСотрудников.Строки Цикл
		
		СтрокиПоСтатьям = СтрокиПоСтатьямФинансирования.Получить(СтрокаСотрудника.СтатьяФинансирования);
		Если СтрокиПоСтатьям = Неопределено Тогда
			СтатьиФинансирования.Добавить(СтрокаСотрудника.СтатьяФинансирования);
			СтрокиПоСтатьям = Новый Массив;
		КонецЕсли;
		
		СтрокиПоСтатьям.Добавить(СтрокаСотрудника);
		СтрокиПоСтатьямФинансирования.Вставить(СтрокаСотрудника.СтатьяФинансирования, СтрокиПоСтатьям);
		
	КонецЦикла;
	
	Для каждого СтатьяФинансирования Из СтатьиФинансирования Цикл
		
		СтрокиПоСтатьям = СтрокиПоСтатьямФинансирования.Получить(СтатьяФинансирования);
		Для каждого СтрокаСтатьи Из СтрокиПоСтатьям Цикл
			
			Макеты.СтатьяФинансирования.Параметры.Заполнить(СтрокаСтатьи);
			
			Если ЗначениеЗаполнено(СтатьяФинансирования) Тогда
				
				Макеты.СтатьяФинансирования.Параметры.СтатьяФинансирования =
					?(ЗначениеЗаполнено(СтрокиПоСтатьям[0].СтатьяФинансированияКод), "(" + СтрокиПоСтатьям[0].СтатьяФинансированияКод + ")", "")
					+ ?(ЗначениеЗаполнено(СтрокиПоСтатьям[0].СтатьяФинансированияНаименование) , " " + СтрокиПоСтатьям[0].СтатьяФинансированияНаименование, "");
				
			КонецЕсли;
			
			ДокументРезультат.Вывести(Макеты.СтатьяФинансирования);
			
			ИтогиПоСтатье = ВывестиТелоРасчетногоЛисткаПоРабочимМестам(ДокументРезультат, СтрокаСтатьи, Макеты, СоответствиеПользовательскихПолей);
			
			ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца = ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца + ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца;
			ИтогиПоГруппам.СуммаДолгаНаКонецМесяца = ИтогиПоГруппам.СуммаДолгаНаКонецМесяца + ИтогиПоСтатье.СуммаДолгаНаКонецМесяца;
			
			Если ЗначениеЗаполнено(ИтогиПоСтатье.СуммаДолгаНаКонецМесяца) И ИтогиПоСтатье.СуммаДолгаНаКонецМесяца >= 0 Тогда
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за предприятием на конец месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаКонецПериода = ИтогиПоСтатье.СуммаДолгаНаКонецМесяца;
			ИначеЕсли ЗначениеЗаполнено(ИтогиПоСтатье.СуммаДолгаНаКонецМесяца) И ИтогиПоСтатье.СуммаДолгаНаКонецМесяца < 0 Тогда
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за работником на конец месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаКонецПериода = -ИтогиПоСтатье.СуммаДолгаНаКонецМесяца;
			Иначе
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаКонецПериода = НСтр("ru='Долг за предприятием на конец месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаКонецПериода = "";
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца) И ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца >= 0 Тогда
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за предприятием на начало месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаНачалоПериода = ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца;
			ИначеЕсли ЗначениеЗаполнено(ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца) И ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца < 0 Тогда
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за работником на начало месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаНачалоПериода = -ИтогиПоСтатье.СуммаДолгаНаНачалоМесяца;
			Иначе
				Макеты.ИтогСтатьяФинансирования.Параметры.ТекстДолгНаНачалоПериода = НСтр("ru='Долг за предприятием на начало месяца'");
				Макеты.ИтогСтатьяФинансирования.Параметры.СуммаДолгНаНачалоПериода = "";
			КонецЕсли;
			
			ДокументРезультат.Вывести(Макеты.ИтогСтатьяФинансирования);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Макеты.СтатьяФинансирования.Параметры.СтатьяФинансирования =
		НСтр("ru='Итого по всем статьям финансирования'");
	ДокументРезультат.Вывести(Макеты.СтатьяФинансирования);
	
	Возврат ИтогиПоГруппам;
	
КонецФункции

Функция ВывестиТелоРасчетногоЛисткаПоРабочимМестам(ДокументРезультат, СтрокаГоловногоСотрудника, Макеты, СоответствиеПользовательскихПолей)
	
	ИтогиПоГруппам = ОписаниеИтоговПоГруппам();
	
	СуммаНачисленоВНатуральнойФорме = 0;
	СуммаНачисления = 0;
	СуммаУдержания = 0;
	
	КоллекцияРабочихМест = Новый Массив;
	СтрокиУдержаний = Новый Соответствие;
	ИмяПоляСуммаУдержано = СоответствиеПользовательскихПолей.Получить("СуммаУдержано");
	
	Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
		
		ЭлементКоллекции = Новый Структура("СтрокаСотрудника,СтрокиНачислений,СтрокиУдержаний", СтрокаСотрудника, Новый Массив, Новый Массив);
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено, "Группа");
		Если СтрокаГруппы <> Неопределено Тогда
			
			Для каждого СтрокаНачислений Из СтрокаГруппы.Строки Цикл
				ЭлементКоллекции.СтрокиНачислений.Добавить(СтрокаНачислений);
			КонецЦикла;
			
		КонецЕсли;
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано, "Группа");
		Если СтрокаГруппы <> Неопределено Тогда
			
			Для каждого СтрокаУдержаний Из СтрокаГруппы.Строки Цикл
				
				СтрокаКоллекции = СтрокиУдержаний.Получить(СтрокаУдержаний.ВидРасчета);
				Если СтрокаКоллекции = Неопределено Тогда
					СтрокаКоллекции = СтрокаУдержаний;
				Иначе
					СтрокаКоллекции[ИмяПоляСуммаУдержано] = СтрокаКоллекции[ИмяПоляСуммаУдержано] + СтрокаУдержаний[ИмяПоляСуммаУдержано];
				КонецЕсли;
				СтрокиУдержаний.Вставить(СтрокаКоллекции.ВидРасчета, СтрокаКоллекции);
				
			КонецЦикла;
			
		КонецЕсли;
		
		КоллекцияРабочихМест.Добавить(ЭлементКоллекции);
		
	КонецЦикла;
	
	Если СтрокиУдержаний.Количество() > 0 Тогда
		КоллекцияРабочихМест[0].СтрокиУдержаний = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиУдержаний, "Значение");
	КонецЕсли;
	
	ШапкаВыводилась = Ложь;
	Для каждого РабочееМесто Из КоллекцияРабочихМест Цикл
		
		СтрокаСотрудника = РабочееМесто.СтрокаСотрудника;
		СтрокиНачислений = РабочееМесто.СтрокиНачислений;
		СтрокиУдержаний = РабочееМесто.СтрокиУдержаний;
		
		Если СтрокиНачислений.Количество() > 0 ИЛИ СтрокиУдержаний.Количество() > 0 Тогда
			
			Если НЕ ШапкаВыводилась Тогда
				ДокументРезультат.Вывести(Макеты.НачисленоУдержано);
				ШапкаВыводилась = Истина;
			КонецЕсли; 
			
			Если СтрокаСотрудника.Сотрудник <> СтрокаСотрудника.ГоловнойСотрудник Тогда
				ЗаполнитьЗначенияСвойств(Макеты.Подработка.Параметры, СтрокаСотрудника);
				ДокументРезультат.Вывести(Макеты.Подработка);
			КонецЕсли;
		
			НомерСтроки = 0;
			Пока НомерСтроки < СтрокиНачислений.Количество() ИЛИ НомерСтроки < СтрокиУдержаний.Количество() Цикл
				
				Для НомерПараметра = 0 По Макеты.СтрокаДвижений.Параметры.Количество() -1 Цикл
					Макеты.СтрокаДвижений.Параметры.Установить(НомерПараметра, "");
				КонецЦикла;
				
				Если НомерСтроки < СтрокиНачислений.Количество() Тогда
					
					СтрокаНачислений = СтрокиНачислений[НомерСтроки];
					
					Макеты.СтрокаДвижений.Параметры.ПериодДействияНачислений = СтрокаНачислений.ПериодДействия;
					
					УчетВремениВЧасах = СтрокаНачислений.ВремяВЧасах = Истина;
					
					Если УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаНачислений.ОплаченоЧасов) И СтрокаНачислений.ОплаченоЧасов > 0 Тогда
						ОплаченныеДниЧасы = Формат(СтрокаНачислений.ОплаченоЧасов, "ЧДЦ=2; ЧН=") + Символы.НПП + НСтр("ru='чс'") + ".";
					ИначеЕсли НЕ УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаНачислений.ОплаченоДней) И СтрокаНачислений.ОплаченоДней > 0 Тогда
						ОплаченныеДниЧасы = Формат(СтрокаНачислений.ОплаченоДней, "ЧДЦ=2; ЧН=") + Символы.НПП + НСтр("ru='дн'") + ".";
					Иначе
						ОплаченныеДниЧасы = 0;
					КонецЕсли;
					
					Макеты.СтрокаДвижений.Параметры.ОплаченныеДниЧасы = ОплаченныеДниЧасы;
					ЗаполнитьЗначенияСвойств(Макеты.СтрокаДвижений.Параметры, СтрокаНачислений, "ОтработаноДней,ОтработаноЧасов");
					
					ЗаполнитьПараметрыПользовательскихПолей(
						Макеты.СтрокаДвижений,
						СтрокаНачислений,
						СоответствиеПользовательскихПолей,
						"Начисление,СуммаНачислено");
					
					СуммаНачисления = СуммаНачисления + СтрокаНачислений[СоответствиеПользовательскихПолей.Получить("СуммаНачислено")];
					СуммаНачисленоВНатуральнойФорме = СуммаНачисленоВНатуральнойФорме + СтрокаНачислений[СоответствиеПользовательскихПолей.Получить("СуммаНачисленоВНатуральнойФорме")];
					
				КонецЕсли;
				
				Если НомерСтроки < СтрокиУдержаний.Количество() Тогда
					
					СтрокаУдержаний = СтрокиУдержаний[НомерСтроки];
					
					Макеты.СтрокаДвижений.Параметры.ПериодДействияУдержаний = СтрокаУдержаний.ПериодДействия;
					ЗаполнитьПараметрыПользовательскихПолей(
						Макеты.СтрокаДвижений,
						СтрокаУдержаний,
						СоответствиеПользовательскихПолей,
						"Удержание,СуммаУдержано");
					
					СуммаУдержания = СуммаУдержания + СтрокаУдержаний[СоответствиеПользовательскихПолей.Получить("СуммаУдержано")];
					
				КонецЕсли;
				
				НомерСтроки = НомерСтроки + 1;
				
				ДокументРезультат.Вывести(Макеты.СтрокаДвижений);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
		
	Если СуммаНачисления <> 0 ИЛИ СуммаУдержания <> 0 Тогда
		Макеты.Итог12.Параметры.СуммаНачисления = СуммаНачисления;
		Макеты.Итог12.Параметры.СуммаУдержания = СуммаУдержания;
		ДокументРезультат.Вывести(Макеты.Итог12);
	КонецЕсли;
	
	Если СуммаНачисленоВНатуральнойФорме <> 0 Тогда
		Макеты.Итог12ВНатуральнойФорме.Параметры.СуммаНачисления = СуммаНачисленоВНатуральнойФорме;
		ДокументРезультат.Вывести(Макеты.Итог12ВНатуральнойФорме);
	КонецЕсли;
	
	СуммаДолгаНаНачалоМесяца = 0;
	СуммаДолгаНаКонецМесяца = 0;
	
	СуммаНачисления = 0;
	СуммаВыплат = 0;
	
	ШапкаВыводилась = Ложь;
	Для каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно, "Группа");
		Если СтрокаГруппы = Неопределено Тогда
			СтрокиСправочно = Новый Массив;
		Иначе
			СтрокиСправочно = СтрокаГруппы.Строки;
		КонецЕсли;
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Выплачено, "Группа");
		Если СтрокаГруппы = Неопределено Тогда
			СтрокиВыплачено = Новый Массив;
		Иначе
			СтрокиВыплачено = СтрокаГруппы.Строки;
		КонецЕсли;
		
		Если СтрокиСправочно.Количество() > 0 ИЛИ СтрокиВыплачено.Количество() > 0 Тогда
			
			Если НЕ ШапкаВыводилась Тогда
				ДокументРезультат.Вывести(Макеты.ПрочиеВыплачено);
				ШапкаВыводилась = Истина;
			КонецЕсли;
			
			Если СтрокаСотрудника.Сотрудник <> СтрокаСотрудника.ГоловнойСотрудник Тогда
				ЗаполнитьЗначенияСвойств(Макеты.Подработка.Параметры, СтрокаСотрудника);
				ДокументРезультат.Вывести(Макеты.Подработка);
			КонецЕсли;
		
			НомерСтроки = 0;
			Пока НомерСтроки < СтрокиСправочно.Количество() ИЛИ НомерСтроки < СтрокиВыплачено.Количество() Цикл
				
				Для НомерПараметра = 0 По Макеты.СтрокаДвижений.Параметры.Количество() -1 Цикл
					Макеты.СтрокаДвижений.Параметры.Установить(НомерПараметра, "");
				КонецЦикла;
				
				Если НомерСтроки < СтрокиСправочно.Количество() Тогда
					
					СтрокаСправочно = СтрокиСправочно[НомерСтроки];
					
					Макеты.СтрокаДвижений.Параметры.ПериодДействияНачислений = СтрокаСправочно.ПериодДействия;
					ЗаполнитьПараметрыПользовательскихПолей(
						Макеты.СтрокаДвижений,
						СтрокаСправочно,
						СоответствиеПользовательскихПолей,
						"Начисление,СуммаНачислено");
					
				КонецЕсли;
				
				Если НомерСтроки < СтрокиВыплачено.Количество() Тогда
					
					СтрокаВыплачено = СтрокиВыплачено[НомерСтроки];
					
					ПредставлениеРегистраторов = "";
					Регистраторы = Новый Соответствие;
					Для каждого СтрокаСРегистратором Из СтрокаВыплачено.Строки Цикл
						
						Если ЗначениеЗаполнено(СтрокаСРегистратором.Регистратор) Тогда
							СвойстваРегистратора = Новый Структура("Номер,Дата");
							ЗаполнитьЗначенияСвойств(СвойстваРегистратора, СтрокаСРегистратором.Регистратор);
							Регистраторы.Вставить(СтрокаСРегистратором.Регистратор, СвойстваРегистратора);
						КонецЕсли;
						
					КонецЦикла;
					
					Для каждого СвойстваРегистратора Из Регистраторы Цикл
						
						ПредставлениеРегистраторов = ?(ПустаяСтрока(ПредставлениеРегистраторов), "", ПредставлениеРегистраторов + ", ")
							+ НомерНаПечать(СвойстваРегистратора.Значение.Номер)
							+ " " + НСтр("ru='от'") + " " + Формат(СвойстваРегистратора.Значение.Дата, "ДФ=dd.MM.yy");
						
					КонецЦикла;
					
					Макеты.СтрокаДвижений.Параметры.ПериодДействияУдержаний = СтрокаВыплачено.ПериодДействия;
					ЗаполнитьПараметрыПользовательскихПолей(
						Макеты.СтрокаДвижений,
						СтрокаВыплачено,
						СоответствиеПользовательскихПолей,
						"Удержание,СуммаУдержано");
						
					Если Не ПустаяСтрока(ПредставлениеРегистраторов) Тогда
						Макеты.СтрокаДвижений.Параметры.Удержание = Строка(Макеты.СтрокаДвижений.Параметры.Удержание) + " (" + ПредставлениеРегистраторов + ")";
					КонецЕсли;
					
				КонецЕсли;
				
				НомерСтроки = НомерСтроки + 1;
				
				ДокументРезультат.Вывести(Макеты.СтрокаДвижений);
				
			КонецЦикла;
			
			Если ТипЗнч(СтрокиСправочно) <> Тип("Массив") Тогда
				СуммаНачисления = СуммаНачисления + СтрокиСправочно.Итог(СоответствиеПользовательскихПолей.Получить("СуммаНачислено"));
			КонецЕсли;
			
			Если ТипЗнч(СтрокиВыплачено) <> Тип("Массив") Тогда
				СуммаВыплат	 = СуммаВыплат + СтрокиВыплачено.Итог(СоответствиеПользовательскихПолей.Получить("СуммаУдержано"));
			КонецЕсли;
			
		КонецЕсли;
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо, "Группа");
		УвеличитьРезультатНаСуммуСтроки(СуммаДолгаНаНачалоМесяца, СтрокаГруппы);
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо, "Группа");
		УвеличитьРезультатНаСуммуСтроки(СуммаДолгаНаКонецМесяца, СтрокаГруппы);
		
	КонецЦикла;
	
	Если СуммаНачисления <> 0 ИЛИ СуммаВыплат <> 0 Тогда
		Макеты.Итог34.Параметры.СуммаНачисления = СуммаНачисления;
		Макеты.Итог34.Параметры.СуммаВыплат	 = СуммаВыплат;
		ДокументРезультат.Вывести(Макеты.Итог34);
	КонецЕсли;
	
	ИтогиПоГруппам.СуммаДолгаНаНачалоМесяца = СуммаДолгаНаНачалоМесяца;
	ИтогиПоГруппам.СуммаДолгаНаКонецМесяца = СуммаДолгаНаКонецМесяца;
	
	СуммаНачисления = 0;
	ШапкаВыводилась = Ложь;
	
	Для Каждого СтрокаСотрудника Из СтрокаГоловногоСотрудника.Строки Цикл
		
		СтрокаГруппы = СтрокаСотрудника.Строки.Найти(Перечисления.ГруппыНачисленияУдержанияВыплаты.Льготы, "Группа");
		Если СтрокаГруппы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиЛьгот = СтрокаГруппы.Строки;
		Для Каждого СтрокаЛьгот Из СтрокиЛьгот Цикл 
			
			Если Не ШапкаВыводилась Тогда
				ДокументРезультат.Вывести(Макеты.Льготы);
				ШапкаВыводилась = Истина;
			КонецЕсли;
			
			Для НомерПараметра = 0 По Макеты.СтрокаДвижений.Параметры.Количество() - 1 Цикл
				Макеты.СтрокаДвижений.Параметры.Установить(НомерПараметра, "");
			КонецЦикла;
			
			Макеты.СтрокаДвижений.Параметры.ПериодДействияНачислений = СтрокаЛьгот.ПериодДействия;
			
			УчетВремениВЧасах = СтрокаЛьгот.ВремяВЧасах = Истина;
			
			Если УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаЛьгот.ОплаченоЧасов) И СтрокаЛьгот.ОплаченоЧасов > 0 Тогда
				ОплаченныеДниЧасы = Формат(СтрокаЛьгот.ОплаченоЧасов, "ЧДЦ=2; ЧН=") + Символы.НПП + НСтр("ru='чс'") + ".";
			ИначеЕсли НЕ УчетВремениВЧасах И ЗначениеЗаполнено(СтрокаЛьгот.ОплаченоДней) И СтрокаЛьгот.ОплаченоДней > 0 Тогда
				ОплаченныеДниЧасы = Формат(СтрокаЛьгот.ОплаченоДней, "ЧДЦ=2; ЧН=") + Символы.НПП + НСтр("ru='дн'") + ".";
			Иначе
				ОплаченныеДниЧасы = 0;
			КонецЕсли;
			
			Макеты.СтрокаДвижений.Параметры.ОплаченныеДниЧасы = ОплаченныеДниЧасы;
			ЗаполнитьЗначенияСвойств(Макеты.СтрокаДвижений.Параметры, СтрокаЛьгот, "ОтработаноДней,ОтработаноЧасов");
			
			ЗаполнитьПараметрыПользовательскихПолей(
				Макеты.СтрокаДвижений,
				СтрокаЛьгот,
				СоответствиеПользовательскихПолей,
				"Начисление,СуммаНачислено");
				
			ДокументРезультат.Вывести(Макеты.СтрокаДвижений);
			
		КонецЦикла;
		
		Если ТипЗнч(СтрокиЛьгот) <> Тип("Массив") Тогда
			СуммаНачисления = СуммаНачисления + СтрокиЛьгот.Итог(СоответствиеПользовательскихПолей.Получить("СуммаНачислено"));
		КонецЕсли;
		
	КонецЦикла;
	
	Если СуммаНачисления <> 0 Тогда
		Макеты.Итог56.Параметры.СуммаНачисления = СуммаНачисления;
		ДокументРезультат.Вывести(Макеты.Итог56);
	КонецЕсли;
	
	Возврат ИтогиПоГруппам;
	
КонецФункции

#КонецОбласти


// Вспомогательные методы вывода расчетных листков

Функция ДанныеПоНДФЛИВзносамПоРабочимМестам(СтрокиСотрудников, МесяцНачисления, ВыводитьОсобенностиРасчетаНДФЛ, ВыводитьИнформациюОНачисленныхВзносахВПФР)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Сотрудники", СтрокиСотрудников.ВыгрузитьКолонку("ГоловнойСотрудник"));
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Сотрудники.ГоловнаяОрганизация КАК ГоловнаяОрганизация
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&Сотрудники)";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДанныеПоНДФЛИВзносам(Запрос.МенеджерВременныхТаблиц, МесяцНачисления, ВыводитьОсобенностиРасчетаНДФЛ, ВыводитьИнформациюОНачисленныхВзносахВПФР);
	
КонецФункции

Функция ДанныеПоНДФЛИВзносамПоФизическимЛицам(СтрокиСотрудников, МесяцНачисления, ВыводитьОсобенностиРасчетаНДФЛ, ВыводитьИнформациюОНачисленныхВзносахВПФР)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ФизическиеЛица = Новый ТаблицаЗначений;
	ФизическиеЛица.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ФизическиеЛица.Колонки.Добавить("ГоловнаяОрганизация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	
	СтрокиСотрудниковМесяца = СтрокиСотрудников.НайтиСтроки(Новый Структура("МесяцНачисления", МесяцНачисления));
	Для Каждого СтрокаСотрудника Из СтрокиСотрудниковМесяца Цикл
		
		НоваяСтрокаФизическиеЛица = ФизическиеЛица.Добавить();
		НоваяСтрокаФизическиеЛица.ФизическоеЛицо = СтрокаСотрудника.ФизическоеЛицо;
		Если ЗначениеЗаполнено(СтрокаСотрудника.ОрганизацияСортировки) Тогда
			НоваяСтрокаФизическиеЛица.ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(СтрокаСотрудника.ОрганизацияСортировки);
		Иначе
			НоваяСтрокаФизическиеЛица.ГоловнаяОрганизация = Справочники.Организации.ПустаяСсылка();
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ФизическиеЛица.ГоловнаяОрганизация КАК ГоловнаяОрганизация
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	&ФизическиеЛица КАК ФизическиеЛица";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДанныеПоНДФЛИВзносам(Запрос.МенеджерВременныхТаблиц, МесяцНачисления, ВыводитьОсобенностиРасчетаНДФЛ, ВыводитьИнформациюОНачисленныхВзносахВПФР);
	
КонецФункции

Функция ДанныеПоНДФЛИВзносам(МенеджерВременныхТаблиц, МесяцНачисления, ВыводитьОсобенностиРасчетаНДФЛ, ВыводитьИнформациюОНачисленныхВзносахВПФР)
	
	ВозвращаемыеДанные = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	НачалоПериода = НачалоМесяца(МесяцНачисления);
	КонецПериода = КонецМесяца(МесяцНачисления);
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(МесяцНачисления));
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	// Подготовка сведений об НДФЛ
	Если ВыводитьОсобенностиРасчетаНДФЛ Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	&НачалоПериода КАК Период,
			|	ЗарплатаКВыплатеОстатки.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ЗарплатаКВыплатеОстатки.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ЗарплатаКВыплатеОстатки.СуммаКВыплатеОстаток КАК СуммаКВыплатеОстаток
			|ПОМЕСТИТЬ ВТОстаткиЗарплатыКВыплатеПредварительно
			|ИЗ
			|	РегистрНакопления.ЗарплатаКВыплате.Остатки(
			|			&НачалоПериода,
			|			ФизическоеЛицо В
			|				(ВЫБРАТЬ
			|					ФизическиеЛица.ФизическоеЛицо
			|				ИЗ
			|					ВТФизическиеЛица КАК ФизическиеЛица)) КАК ЗарплатаКВыплатеОстатки
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ
			|	&КонецПериода,
			|	ЗарплатаКВыплатеОстатки.Организация.ГоловнаяОрганизация,
			|	ЗарплатаКВыплатеОстатки.ФизическоеЛицо,
			|	ЗарплатаКВыплатеОстатки.СуммаКВыплатеОстаток
			|ИЗ
			|	РегистрНакопления.ЗарплатаКВыплате.Остатки(
			|			&КонецПериода,
			|			ФизическоеЛицо В
			|				(ВЫБРАТЬ
			|					ФизическиеЛица.ФизическоеЛицо
			|				ИЗ
			|					ВТФизическиеЛица КАК ФизическиеЛица)) КАК ЗарплатаКВыплатеОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОстаткиЗарплатыКВыплате.Период КАК Период,
			|	ОстаткиЗарплатыКВыплате.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ОстаткиЗарплатыКВыплате.ФизическоеЛицо КАК ФизическоеЛицо,
			|	СУММА(ОстаткиЗарплатыКВыплате.СуммаКВыплатеОстаток) КАК СуммаКВыплатеОстаток
			|ПОМЕСТИТЬ ВТОстаткиЗарплатыКВыплате
			|ИЗ
			|	ВТОстаткиЗарплатыКВыплатеПредварительно КАК ОстаткиЗарплатыКВыплате
			|
			|СГРУППИРОВАТЬ ПО
			|	ОстаткиЗарплатыКВыплате.Период,
			|	ОстаткиЗарплатыКВыплате.ГоловнаяОрганизация,
			|	ОстаткиЗарплатыКВыплате.ФизическоеЛицо
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СведенияОДоходахНДФЛОбороты.ФизическоеЛицо КАК ФизическоеЛицо,
			|	СведенияОДоходахНДФЛОбороты.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	СУММА(СведенияОДоходахНДФЛОбороты.СуммаДоходаОборот - ВЫБОР
			|			КОГДА СведенияОДоходахНДФЛОбороты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.ПустаяСсылка)
			|				ТОГДА 0
			|			ИНАЧЕ СведенияОДоходахНДФЛОбороты.СуммаВычетаОборот
			|		КОНЕЦ) КАК ОблагаемыйДоход,
			|	&НачалоПериода КАК МесяцНачисления
			|ПОМЕСТИТЬ ВТОблагаемыйДоход
			|ИЗ
			|	РегистрНакопления.СведенияОДоходахНДФЛ.Обороты(
			|			ДАТАВРЕМЯ(1, 1, 1),
			|			&КонецПериода,
			|			,
			|			МесяцНалоговогоПериода МЕЖДУ &НачалоГода И &КонецПериода
			|				И ФизическоеЛицо В
			|					(ВЫБРАТЬ
			|						ФизическиеЛица.ФизическоеЛицо
			|					ИЗ
			|						ВТФизическиеЛица КАК ФизическиеЛица)) КАК СведенияОДоходахНДФЛОбороты
			|
			|СГРУППИРОВАТЬ ПО
			|	СведенияОДоходахНДФЛОбороты.ГоловнаяОрганизация,
			|	СведенияОДоходахНДФЛОбороты.ФизическоеЛицо";
		
		Запрос.Выполнить();
		
		УчетНДФЛ.СоздатьВТАвансовыеПлатежиПоНДФЛДляАналитическогоОтчета(Запрос.МенеджерВременныхТаблиц, Истина, НачалоПериода, КонецПериода, "ВТФизическиеЛица");
		УчетНДФЛ.СоздатьВТПраваНаСтандартныеВычетыПоНДФЛДляАналитическогоОтчета(Запрос.МенеджерВременныхТаблиц, Истина, НачалоПериода, КонецПериода, ТекущаяДатаСеанса(), "ВТФизическиеЛица");
		УчетНДФЛ.СоздатьВТПредоставленныеВычетыПоНДФЛДляАналитическогоОтчета(Запрос.МенеджерВременныхТаблиц, Истина, НачалоПериода, КонецПериода, ТекущаяДатаСеанса(), Истина, "ВТФизическиеЛица");
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПредоставленныеВычеты.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ПредоставленныеВычеты.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ПредоставленныеВычеты.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
			|	СУММА(ВЫБОР
			|			КОГДА ПредоставленныеВычеты.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Стандартные)
			|				ТОГДА ПредоставленныеВычеты.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК ВычетНаФизлицо,
			|	СУММА(ВЫБОР
			|			КОГДА ПредоставленныеВычеты.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.СтандартныеНаДетей)
			|				ТОГДА ПредоставленныеВычеты.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК ВычетНаДетей,
			|	СУММА(ВЫБОР
			|			КОГДА ПредоставленныеВычеты.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Имущественные)
			|				ТОГДА ПредоставленныеВычеты.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК ВычетИмущественный,
			|	СУММА(ВЫБОР
			|			КОГДА ПредоставленныеВычеты.ГруппаВычета В (ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Социальные), ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.СоциальныеПоУведомлениюНО))
			|				ТОГДА ПредоставленныеВычеты.Сумма
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК ВычетСоциальный
			|ПОМЕСТИТЬ ВТВычеты
			|ИЗ
			|	ВТПредоставленныеВычетыПоНДФЛДляАналитическогоОтчета КАК ПредоставленныеВычеты
			|
			|СГРУППИРОВАТЬ ПО
			|	ПредоставленныеВычеты.ФизическоеЛицо,
			|	ПредоставленныеВычеты.ГоловнаяОрганизация,
			|	ПредоставленныеВычеты.МесяцНалоговогоПериода";
		
		Запрос.Выполнить();
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВТ.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ВТ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ВТ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
			|	МАКСИМУМ(ВЫБОР
			|			КОГДА ВТ.ЛичныйВычет
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ ЛОЖЬ
			|		КОНЕЦ) КАК ПоложеныЛичныеВычеты,
			|	МАКСИМУМ(ВЫБОР
			|			КОГДА ВТ.ЛичныйВычет = ЛОЖЬ
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ ЛОЖЬ
			|		КОНЕЦ) КАК ПоложеныВычетыНаДетей,
			|	МАКСИМУМ(ВТ.ОблагаемыйДоходСНачалаГода) КАК ОблагаемыйДоходСНачалаГода
			|ПОМЕСТИТЬ ВТПраваНаВычеты
			|ИЗ
			|	ВТПраваНаСтандартныеВычетыПоНДФЛДляАналитическогоОтчета КАК ВТ
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТ.ФизическоеЛицо,
			|	ВТ.ГоловнаяОрганизация,
			|	ВТ.МесяцНалоговогоПериода";
		
		Запрос.Выполнить();
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	&НачалоПериода КАК МесяцНачисления,
			|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ФизическиеЛица.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ЕСТЬNULL(Вычеты.ВычетНаФизлицо, 0) КАК ВычетНаФизлицо,
			|	ЕСТЬNULL(Вычеты.ВычетНаДетей, 0) КАК ВычетНаДетей,
			|	ЕСТЬNULL(Вычеты.ВычетИмущественный, 0) КАК ВычетИмущественный,
			|	ЕСТЬNULL(Вычеты.ВычетСоциальный, 0) КАК ВычетСоциальный,
			|	ОблагаемыйДоходПомесячно.ОблагаемыйДоход КАК ОблагаемыйДоход,
			|	ПраваНаВычеты.ОблагаемыйДоходСНачалаГода КАК ОблагаемыйДоходДляВычетов,
			|	АвансовыеПлатежи.Сумма КАК АвансовыеПлатежи,
			|	ПраваНаВычеты.ПоложеныЛичныеВычеты КАК ПоложеныЛичныеВычеты,
			|	ПраваНаВычеты.ПоложеныВычетыНаДетей КАК ПоложеныВычетыНаДетей,
			|	ЕСТЬNULL(ОстаткиЗарплатыКВыплатеНачалоПериода.СуммаКВыплатеОстаток, 0) КАК ЗарплатаКВыплатеНаНачалоПериода,
			|	ЕСТЬNULL(ОстаткиЗарплатыКВыплатеКонецПериода.СуммаКВыплатеОстаток, 0) КАК ЗарплатаКВыплатеНаКонецПериода
			|ИЗ
			|	ВТФизическиеЛица КАК ФизическиеЛица
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВычеты КАК Вычеты
			|		ПО ФизическиеЛица.ФизическоеЛицо = Вычеты.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = Вычеты.ГоловнаяОрганизация
			|			И (НАЧАЛОПЕРИОДА(Вычеты.МесяцНалоговогоПериода, МЕСЯЦ) = &НачалоПериода)
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОблагаемыйДоход КАК ОблагаемыйДоходПомесячно
			|		ПО (ОблагаемыйДоходПомесячно.МесяцНачисления = &НачалоПериода)
			|			И ФизическиеЛица.ФизическоеЛицо = ОблагаемыйДоходПомесячно.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = ОблагаемыйДоходПомесячно.ГоловнаяОрганизация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраваНаВычеты КАК ПраваНаВычеты
			|		ПО ФизическиеЛица.ФизическоеЛицо = ПраваНаВычеты.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = ПраваНаВычеты.ГоловнаяОрганизация
			|			И (ПраваНаВычеты.МесяцНалоговогоПериода = &НачалоПериода)
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАвансовыеПлатежиПоНДФЛДляАналитическогоОтчета КАК АвансовыеПлатежи
			|		ПО (АвансовыеПлатежи.МесяцНачисления = &НачалоПериода)
			|			И ФизическиеЛица.ФизическоеЛицо = АвансовыеПлатежи.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = АвансовыеПлатежи.ГоловнаяОрганизация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиЗарплатыКВыплате КАК ОстаткиЗарплатыКВыплатеНачалоПериода
			|		ПО (ОстаткиЗарплатыКВыплатеНачалоПериода.Период = &НачалоПериода)
			|			И ФизическиеЛица.ФизическоеЛицо = ОстаткиЗарплатыКВыплатеНачалоПериода.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = ОстаткиЗарплатыКВыплатеНачалоПериода.ГоловнаяОрганизация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиЗарплатыКВыплате КАК ОстаткиЗарплатыКВыплатеКонецПериода
			|		ПО (ОстаткиЗарплатыКВыплатеКонецПериода.Период = &КонецПериода)
			|			И ФизическиеЛица.ФизическоеЛицо = ОстаткиЗарплатыКВыплатеКонецПериода.ФизическоеЛицо
			|			И ФизическиеЛица.ГоловнаяОрганизация = ОстаткиЗарплатыКВыплатеКонецПериода.ГоловнаяОрганизация";
		
		ВозвращаемыеДанные.Вставить("НДФЛ", Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	// Подготовка сведений о взносах
	Если ВыводитьИнформациюОНачисленныхВзносахВПФР Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	&НачалоПериода КАК МесяцНачисления,
			|	ИсчисленныеСтраховыеВзносыОбороты.Организация,
			|	ИсчисленныеСтраховыеВзносыОбороты.ГоловнаяОрганизация,
			|	ИсчисленныеСтраховыеВзносыОбороты.ФизическоеЛицо,
			|	СУММА(&ПоляВзносов) КАК ПоляВзносов
			|ИЗ
			|	РегистрНакопления.ИсчисленныеСтраховыеВзносы.Обороты(
			|			&НачалоПериода,
			|			&КонецПериода,
			|			Месяц,
			|			ФизическоеЛицо В
			|				(ВЫБРАТЬ
			|					ФизическиеЛица.ФизическоеЛицо
			|				ИЗ
			|					ВТФизическиеЛица КАК ФизическиеЛица)) КАК ИсчисленныеСтраховыеВзносыОбороты
			|
			|СГРУППИРОВАТЬ ПО
			|	ИсчисленныеСтраховыеВзносыОбороты.ФизическоеЛицо,
			|	ИсчисленныеСтраховыеВзносыОбороты.ГоловнаяОрганизация,
			|	ИсчисленныеСтраховыеВзносыОбороты.Организация";
		
		ПоляВзносов = СтрРазделить(УчетСтраховыхВзносовКлиентСервер.РассчитываемыеВзносыВПФР(), ",");
		ТекстЗапросаСуммПолей = "";
		
		ДобавитьЗапятую = Ложь;
		Для каждого ПолеВзносов Из  ПоляВзносов Цикл
			
			Если ДобавитьЗапятую Тогда
				
				ТекстЗапросаСуммПолей = ТекстЗапросаСуммПолей + ",
				|	";
				
			Иначе
				ДобавитьЗапятую = Истина;
			КонецЕсли;
			
			ТекстЗапросаСуммПолей = ТекстЗапросаСуммПолей + "СУММА(ИсчисленныеСтраховыеВзносыОбороты." + ПолеВзносов + "Оборот) КАК " + ПолеВзносов;
			
		КонецЦикла;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СУММА(&ПоляВзносов) КАК ПоляВзносов", ТекстЗапросаСуммПолей);
		
		ВозвращаемыеДанные.Вставить("Взносы", Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	Возврат ВозвращаемыеДанные;
	
КонецФункции

Функция МожноГруппировать(Порядок, ИмяПоляГруппировки, СоответствиеПользовательскихПолей = Неопределено)
	
	ГруппировкаВозможна = Ложь;
	
	Для каждого ЭлементПорядка Из Порядок.Элементы Цикл
		
		Если ЭлементПорядка.Использование Тогда
			
			Если ТипЗнч(ЭлементПорядка) = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("МесяцНачисления") Тогда
				Продолжить;
			ИначеЕсли СоответствиеПользовательскихПолей <> Неопределено Тогда
				
				ПолеПорядкаОсновногоСотрудника = СоответствиеПользовательскихПолей.Получить(НСтр("ru='Порядок основного рабочего места'"));
				Если ПолеПорядкаОсновногоСотрудника <> Неопределено Тогда
					
					Если ПолеПорядкаОсновногоСотрудника = СтрЗаменить(ЭлементПорядка.Поле, ".", "") Тогда
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных(ИмяПоляГруппировки) Тогда
				
				ГруппировкаВозможна = Истина;
				Прервать;
				
			ИначеЕсли ЭлементПорядка.Поле <> Новый ПолеКомпоновкиДанных("ОрганизацияСортировки") Тогда
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ГруппировкаВозможна;
	
КонецФункции

Функция ОписаниеИтоговПоГруппам()
	
	ИтогиПоГруппам = Новый Структура;
	
	ИтогиПоГруппам.Вставить("Начислено", 0);
	ИтогиПоГруппам.Вставить("Удержано", 0);
	ИтогиПоГруппам.Вставить("Выплачено", 0);
	ИтогиПоГруппам.Вставить("СуммаДолгаНаНачалоМесяца", 0);
	ИтогиПоГруппам.Вставить("СуммаДолгаНаКонецМесяца", 0);
	ИтогиПоГруппам.Вставить("СуммаДолгаНаНачалоМесяцаРасчета", 0);
	ИтогиПоГруппам.Вставить("СуммаДолгаНаКонецМесяцаРасчета", 0);
	
	Возврат ИтогиПоГруппам;
	
КонецФункции

Процедура ОтключитьНеИспользуемыеПоляРасчетногоЛистка(НастройкиОтчета, НастройкиВывода)
	
	// Отключение выбора не выводимых полей
	Если Не НастройкиВывода.ВыводитьПериодыНачислений Тогда
		ОтключитьИспользованиеПолей(НастройкиОтчета, "ПериодДействия,ДатаНачал,ДатаОкончания");
	КонецЕсли;
	
	Если Не НастройкиВывода.ВыводитьОтработанноеОплаченноеВремя Тогда
		ОтключитьИспользованиеПолей(НастройкиОтчета, "ОтработаноДней,ОтработаноЧасов,ОплаченоДней,ОплаченоЧасов,ВремяВЧасах");
	КонецЕсли;
	
	Если Не НастройкиВывода.ВыводитьПоказателиНачисленийИУдержаний
		И Не НастройкиВывода.ВыводитьВсеПоказателиНачисленийИУдержаний Тогда
		
		ОтключитьИспользованиеПолей(НастройкиОтчета, "Показатель,Значение");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтключитьИспользованиеПолей(НастройкиОтчета, ИменаПолей)
	
	СписокПолей = СтрРазделить(ИменаПолей, ",");
	ОтключитьИспользованиеВыбранныхПолей(НастройкиОтчета.Выбор, СписокПолей);
	ОтключитьИспользованиеВыбранныхПолейСтруктуры(НастройкиОтчета.Структура, СписокПолей);
	
КонецПроцедуры

Процедура ОтключитьИспользованиеВыбранныхПолейСтруктуры(СтруктураНастроек, СписокПолей)
	
	Для каждого ЭлементСтруктуры Из СтруктураНастроек Цикл
		
		ОтключитьИспользованиеВыбранныхПолей(ЭлементСтруктуры.Выбор, СписокПолей);
		ОтключитьИспользованиеВыбранныхПолей(ЭлементСтруктуры.ПоляГруппировки, СписокПолей);
		
		ОтключитьИспользованиеВыбранныхПолейСтруктуры(ЭлементСтруктуры.Структура, СписокПолей);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОтключитьИспользованиеВыбранныхПолей(ЭлементСтруктурыСПолями, СписокПолей)
	
	Для каждого ВыбранныйЭлемент Из ЭлементСтруктурыСПолями.Элементы Цикл
		
		Для каждого ИмяПоля Из СписокПолей Цикл
			
			Если ВыбранныйЭлемент.Использование Тогда
				
				Если ТипЗнч(ВыбранныйЭлемент) = Тип("ВыбранноеПолеКомпоновкиДанных")
					Или ТипЗнч(ВыбранныйЭлемент) = Тип("ПолеГруппировкиКомпоновкиДанных") Тогда
					
					Если ВыбранныйЭлемент.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля) Тогда
						ВыбранныйЭлемент.Использование = Ложь;
					КонецЕсли;
					
				КонецЕсли; 
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


#Область РассылкиОтчетов

// Проверка настроек выполняется всегда, даже когда Объект.ОбменДанными.Загрузка = Истина.
Процедура КонтрольНастроекОтчетовПриЗаписиРассылкиОтчетов(Рассылка, Отказ) Экспорт
	РезультатПроверки = ПроверитьНастройкиРассылки(Рассылка);
	Если ЗначениеЗаполнено(РезультатПроверки.ТекстОшибки) Тогда
		ВызватьИсключение РезультатПроверки.ТекстОшибки;
	КонецЕсли;
КонецПроцедуры

// Проверяет настройки отчетов рассылки.
//
// Параметры:
//  Рассылка - СправочникОбъект.РассылкаОтчетов - Проверяемая рассылка отчетов.
//
// Возвращаемое значение:
//  Структура - Информация о результатах проверки, в т.ч. пригодная для записи в журнал регистрации.
//      * ТекстОшибки - Строка - Текст ошибки. Если ошибок нет, то возвращается пустая строка.
//      * ИмяСобытия  - Строка - Имя события для записи в журнал регистрации (если требуется).
//      * Метаданные  - ОбъектМетаданных - Метаданные для привязки события журнала регистрации.
//      * Данные      - Произвольный     - Данные для привязки события журнала регистрации.
//
Функция ПроверитьНастройкиРассылки(Рассылка) Экспорт
	Результат = Новый Структура("ИмяСобытия, Метаданные, Данные, ТекстОшибки");
	Результат.ИмяСобытия  = НСтр("ru = 'Рассылка отчетов. Проверка настроек'", ОбщегоНазначения.КодОсновногоЯзыка());
	Результат.Данные      = Рассылка.Ссылка;
	Результат.Метаданные  = Результат.Данные.Метаданные();
	Результат.ТекстОшибки = "";
	
	Если Рассылка.ЭтоГруппа Тогда
		Возврат Результат;
	КонецЕсли;
	Если Не Рассылка.Подготовлена
		Или Не Рассылка.ИспользоватьЭлектроннуюПочту
		Или Рассылка.Личная
		Или Рассылка.Отчеты.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	СсылкаОтчета = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Отчеты.АнализНачисленийИУдержаний);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтчетыРассылки.НомерСтроки,
	|	ОтчетыРассылки.ОтправлятьЕслиПустой,
	|	ОтчетыРассылки.Отчет
	|ПОМЕСТИТЬ втОтчетыРассылки
	|ИЗ
	|	&ОтчетыРассылки КАК ОтчетыРассылки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОтчетыРассылки.НомерСтроки,
	|	втОтчетыРассылки.Отчет КАК Вариант,
	|	втОтчетыРассылки.ОтправлятьЕслиПустой,
	|	ВариантыОтчетов.Отчет.Имя КАК ИмяОтчета,
	|	ВариантыОтчетов.КлючВарианта,
	|	ВариантыОтчетов.Пользовательский
	|ИЗ
	|	втОтчетыРассылки КАК втОтчетыРассылки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|		ПО втОтчетыРассылки.Отчет = ВариантыОтчетов.Ссылка
	|			И (ВариантыОтчетов.Отчет = &ОтчетАнализНачисленийИУдержаний)";
	Запрос.УстановитьПараметр("ОтчетыРассылки", Рассылка.Отчеты.Выгрузить(, "НомерСтроки, Отчет, ОтправлятьЕслиПустой"));
	Запрос.УстановитьПараметр("ОтчетАнализНачисленийИУдержаний", СсылкаОтчета);
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	Для Каждого ИнформацияОВарианте Из ТаблицаЗначений Цикл
		СтрокаОтчет = Рассылка.Отчеты.Найти(ИнформацияОВарианте.НомерСтроки, "НомерСтроки");
		
		// Инициализация отчета.
		ПараметрыОтчета = Новый Структура("Отчет, Настройки, Форматы, ОтправлятьЕслиПустой");
		ЗаполнитьЗначенияСвойств(ПараметрыОтчета, СтрокаОтчет);
		ПараметрыОтчета.Настройки = СтрокаОтчет.Настройки.Получить();
		
		МодульРассылкаОтчетов = ОбщегоНазначения.ОбщийМодуль("РассылкаОтчетов");
		Успех = МодульРассылкаОтчетов.ИнициализироватьОтчет(Результат, ПараметрыОтчета, Рассылка.Персонализирована);
		Если Не Успех Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверка настроек отчета.
		НастройкиКД = ПараметрыОтчета.КомпоновщикНастроекКД.ПолучитьНастройки();
		
		Если ТребоватьПерсонализацииОтчетаПриРассылке(ИнформацияОВарианте.ИмяОтчета, КлючВарианта(НастройкиКД))
			И Не ОтчетПерсонализирован(НастройкиКД) Тогда
			
			Результат.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Отчет ""%1"" запрещено рассылать без отбора по физическому лицу'"),
				Строка(ИнформацияОВарианте.Вариант));
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Определяет, должен-ли быть персонализирован отчет.
Функция ТребоватьПерсонализацииОтчетаПриРассылке(ИмяОтчета, КлючВарианта)
	Если ИмяОтчета = "АнализНачисленийИУдержаний" Тогда
		Если ЭтоКлючВариантаОтчетаРасчетныйЛисток(КлючВарианта)
			Или КлючВарианта = "РегламентированнаяФормаСправкаПоДСВ" Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

// Определяет, персонализирован-ли отчет.
Функция ОтчетПерсонализирован(НастройкиКД)
	ПолеФизическоеЛицо = Новый ПолеКомпоновкиДанных("ФизическоеЛицо");
	ПолеСотрудник      = Новый ПолеКомпоновкиДанных("Сотрудник");
	
	Для Каждого ЭлементОтбораКД Из НастройкиКД.Отбор.Элементы Цикл
		Если ТипЗнч(ЭлементОтбораКД) = Тип("ЭлементОтбораКомпоновкиДанных")
			И ЭлементОтбораКД.Использование
			И ЭлементОтбораКД.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
			И (ЭлементОтбораКД.ЛевоеЗначение = ПолеФизическоеЛицо
				Или ЭлементОтбораКД.ЛевоеЗначение = ПолеСотрудник) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

#КонецОбласти

Функция ИОФамилияФизическогоЛица(ФизическоеЛицо) Экспорт
	
	ИОФамилия = "";
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		
		КадровыеДанные = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ФизическоеЛицо, "ИОФамилия");
		Если КадровыеДанные.Количество() > 0 Тогда
			ИОФамилия = КадровыеДанные[0].ИОФамилия;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИОФамилия;
	
КонецФункции

Процедура ИзменитьГруппировкиВариантаТ49Т51(Структура, ЕстьГруппировкаПоПодразделению)
	
	Для каждого ЭлементСтруктуры Из Структура Цикл
		
		Если ЭлементСтруктуры.Имя = "Подразделение" Тогда
			
			Если ЕстьГруппировкаПоПодразделению Тогда
				ЭлементСтруктуры.Использование = Истина;
			Иначе
				ЭлементСтруктуры.Использование = Ложь;
			КонецЕсли;
			
		ИначеЕсли ЭлементСтруктуры.Имя = "Сотрудник" Тогда
			
			Если ЕстьГруппировкаПоПодразделению Тогда
				ЭлементСтруктуры.Использование = Ложь;
			Иначе
				ЭлементСтруктуры.Использование = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		ИзменитьГруппировкиВариантаТ49Т51(ЭлементСтруктуры.Структура, ЕстьГруппировкаПоПодразделению);
		
	КонецЦикла;
	
КонецПроцедуры

// Дополнение строк промежуточного итога.
//
Процедура ДобавитьВПромежуточныйИтог(ПромежуточныеИтоги, ДанныеСтроки) Экспорт
	
	Для каждого ПромежуточныйИтог Из ПромежуточныеИтоги Цикл
		ПромежуточныеИтоги.Вставить(ПромежуточныйИтог.Ключ, ПромежуточныйИтог.Значение + ДанныеСтроки[ПромежуточныйИтог.Ключ]);
	КонецЦикла;
	
КонецПроцедуры

Функция ЕстьИтогиПоГоловномуСотруднику(ПромежуточныеИтоги)
	
	Для Каждого ОписаниеИтога Из ПромежуточныеИтоги Цикл
		
		Если ЗначениеЗаполнено(ОписаниеИтога.Значение) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет, есть ли в настройках отчета установленный отбор по физическому лицу
//
// Параметры:
//		ОбъектОтчета	- ОтчетОбъект
//
// Возвращаемое значение:
//		Булево - Истина (отбор установлен), Ложь (нет).
//
Функция ОтчетНастроенПерсонифицировано(ОбъектОтчета) Экспорт
	
	ЭтоПерсонифицированныйОтчет = Ложь;
	ПолеФизическоеЛицо = Новый ПолеКомпоновкиДанных("ФизическоеЛицо");
	
	Для Каждого ЭлементКоллекции Из ОбъектОтчета.КомпоновщикНастроек.ПолучитьНастройки().Отбор.Элементы Цикл
		
		Если ТипЗнч(ЭлементКоллекции) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если ЭлементКоллекции.Использование
				И ЭлементКоллекции.ЛевоеЗначение = ПолеФизическоеЛицо
				И ЭлементКоллекции.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
				
				ЭтоПерсонифицированныйОтчет = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЭтоПерсонифицированныйОтчет;
	
КонецФункции

Функция НаборыВнешнихДанныхАнализНачисленийИУдержаний() Экспорт
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.НаборыВнешнихДанныхАнализНачисленийИУдержаний();
	
КонецФункции

Функция ТабличныеЧастиДляФормированияНабораДанныхДокументов() Экспорт
	
	Возврат Новый Структура("ТаблицыНачислений,ТаблицыУдержаний", Новый Массив, Новый Массив);
	
КонецФункции

Функция ДанныеДокументаФизическихЛиц(Форма, ФизическиеЛица, ТабличныеЧасти) Экспорт
	
	Объект = Форма.Объект;
	
	НачисленияУдержанияПоСотрудникам = Новый ТаблицаЗначений;
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Период",								Новый ОписаниеТипов("Дата"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Регистратор",						Новый ОписаниеТипов("ДокументСсылка.НачислениеЗарплаты"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Организация",						Новый ОписаниеТипов("СправочникСсылка.Организации"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ФизическоеЛицо",						Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Сотрудник",							Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Подразделение",						Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("НачислениеУдержание",				Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления,ПланВидовРасчетаСсылка.Удержания,ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ГруппаНачисленияУдержанияВыплаты",	Новый ОписаниеТипов("ПеречислениеСсылка.ГруппыНачисленияУдержанияВыплаты"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ПериодДействия",						Новый ОписаниеТипов("Дата"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ДокументОснование",					Метаданные.ОпределяемыеТипы.ОснованиеНачисленияУдержания.Тип);
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ДатаНачала",							Новый ОписаниеТипов("Дата"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ДатаОкончания",						Новый ОписаниеТипов("Дата"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("Сумма",								Новый ОписаниеТипов("Число"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ОтработаноДней",						Новый ОписаниеТипов("Число"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ОтработаноЧасов",					Новый ОписаниеТипов("Число"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ОплаченоДней",						Новый ОписаниеТипов("Число"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ОплаченоЧасов",						Новый ОписаниеТипов("Число"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ВремяВЧасах",						Новый ОписаниеТипов("Булево"));
	НачисленияУдержанияПоСотрудникам.Колонки.Добавить("ИдентификаторСтроки",				Новый ОписаниеТипов("Число"));
	
	Для Каждого ФизическоеЛицо Из ФизическиеЛица Цикл
		
		Для Каждого ОписаниеТаблицы Из ТабличныеЧасти.ТаблицыНачислений Цикл
			
			ТабличнаяЧасть = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				Форма, ОписаниеТаблицы.ПутьКДанным);
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("ФизическоеЛицо", ФизическоеЛицо);
			
			СтрокиТаблицы = ТабличнаяЧасть.НайтиСтроки(СтруктураПоиска);
			Если СтрокиТаблицы.Количество() = 0 Тогда
				
				СтруктураПоПустомуФизическомуЛицу = Новый Структура("ФизическоеЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
				СтрокиТаблицыБезФизическогоЛица = ТабличнаяЧасть.НайтиСтроки(СтруктураПоПустомуФизическомуЛицу);
				Если СтрокиТаблицыБезФизическогоЛица.Количество() > 0 Тогда
					
					Для Каждого СтрокаТаблицы Из СтрокиТаблицыБезФизическогоЛица Цикл
						
						СтрокаТаблицы.ФизическоеЛицо = СотрудникиКлиентСерверПовтИсп.ФизическиеЛицаСотрудников(СтрокаТаблицы.Сотрудник)[0];
						Если СтрокаТаблицы.ФизическоеЛицо = СтруктураПоиска.ФизическоеЛицо Тогда
							СтрокиТаблицы.Добавить(СтрокаТаблицы);
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
				
				НоваяСтрока = НачисленияУдержанияПоСотрудникам.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				
				Если ОписаниеТаблицы.ИмяТаблицы = "Льготы"
					Или ОписаниеТаблицы.ИмяТаблицы = "ЛьготыПерерасчет" Тогда
					
					НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Льготы;
					
				Иначе
					НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено;
				КонецЕсли;
				
				НоваяСтрока.Период = Объект.МесяцНачисления;
				НоваяСтрока.Организация = Объект.Организация;
				НоваяСтрока.Сумма = СтрокаТаблицы[ОписаниеТаблицы.ИмяПоляРезультат];
				
				Если ПустаяСтрока(ОписаниеТаблицы.ИмяРеквизитаДатаНачала) Тогда
					НоваяСтрока.ДатаНачала = Объект.МесяцНачисления;
				Иначе
					НоваяСтрока.ДатаНачала = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаДатаНачала];
				КонецЕсли;
				
				Если ПустаяСтрока(ОписаниеТаблицы.ИмяРеквизитаДатаОкончания) Тогда
					НоваяСтрока.ДатаОкончания = НачалоДня(КонецМесяца(Объект.МесяцНачисления));
				Иначе
					НоваяСтрока.ДатаОкончания = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаДатаОкончания];
				КонецЕсли;
				
				Если Не ПустаяСтрока(ОписаниеТаблицы.ИмяРеквизитаВидРасчета) Тогда
					НоваяСтрока.НачислениеУдержание = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаВидРасчета];
				ИначеЕсли ОписаниеТаблицы.ИмяТаблицы = "НачисленияПоДоговорам" Тогда
					НоваяСтрока.НачислениеУдержание = НачислениеДоговораГПХПоДокументуОснованию(СтрокаТаблицы.ДокументОснование);
				КонецЕсли;
				
				Если ОписаниеТаблицы.Свойство("ЗаполнятьОплаченноеВремяОтработанным") Тогда
					
					НоваяСтрока.ОплаченоДней = НоваяСтрока.ОтработаноДней;
					НоваяСтрока.ОплаченоЧасов = НоваяСтрока.ОплаченоЧасов;
					
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(НоваяСтрока.ПериодДействия) Тогда
					НоваяСтрока.ПериодДействия = НачалоМесяца(НоваяСтрока.ДатаНачала);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		Для Каждого ОписаниеТаблицы Из ТабличныеЧасти.ТаблицыУдержаний Цикл
			
			ТабличнаяЧасть = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				Форма, ОписаниеТаблицы.ПутьКДанным);
			
			СтруктураПоиска = Новый Структура;
			Если ОписаниеТаблицы.Свойство("ИмяРеквизитаФизическоеЛицо") Тогда
				СтруктураПоиска.Вставить(ОписаниеТаблицы.ИмяРеквизитаФизическоеЛицо, ФизическоеЛицо);
			Иначе
				СтруктураПоиска.Вставить("ФизическоеЛицо", ФизическоеЛицо);
			КонецЕсли;
			
			СтрокиТаблицы = ТабличнаяЧасть.НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
				
				Если ОписаниеТаблицы.ИмяТаблицы = "ПогашениеЗаймов" Тогда
					
					Если СтрокаТаблицы.МатериальнаяВыгода <> 0 Тогда
						
						НоваяСтрока = НачисленияУдержанияПоСотрудникам.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
						
						НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
						НоваяСтрока.Период = Объект.МесяцНачисления;
						НоваяСтрока.Организация = Объект.Организация;
						НоваяСтрока.Сумма = СтрокаТаблицы.МатериальнаяВыгода;
						НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
						НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.МатериальнаяВыгодаПоЗаймам;
						
					КонецЕсли;
					
					Если СтрокаТаблицы.НалогНаМатериальнуюВыгоду <> 0 Тогда
						
						НоваяСтрока = НачисленияУдержанияПоСотрудникам.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
						
						НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
						НоваяСтрока.Период = Объект.МесяцНачисления;
						НоваяСтрока.Организация = Объект.Организация;
						НоваяСтрока.Сумма = СтрокаТаблицы.НалогНаМатериальнуюВыгоду;
						НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
						НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛ;
						
					КонецЕсли;
					
					Если СтрокаТаблицы.ПогашениеЗайма <> 0 Тогда
						
						НоваяСтрока = НачисленияУдержанияПоСотрудникам.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
						
						НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
						НоваяСтрока.Период = Объект.МесяцНачисления;
						НоваяСтрока.Организация = Объект.Организация;
						НоваяСтрока.Сумма = СтрокаТаблицы.ПогашениеЗайма;
						НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
						НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.ПогашениеЗаймаИзЗарплаты;
						
					КонецЕсли;
					
				Иначе
					
					НоваяСтрока = НачисленияУдержанияПоСотрудникам.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					
					Если ОписаниеТаблицы.Свойство("ИмяРеквизитаФизическоеЛицо") Тогда
						НоваяСтрока.ФизическоеЛицо = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаФизическоеЛицо];
					КонецЕсли;
					
					НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
					НоваяСтрока.Период = Объект.МесяцНачисления;
					НоваяСтрока.Организация = Объект.Организация;
					НоваяСтрока.Сумма = СтрокаТаблицы[ОписаниеТаблицы.ИмяПоляРезультат];
					НоваяСтрока.ПериодДействия = НоваяСтрока.Период;
					
					Если ОписаниеТаблицы.ИмяТаблицы = "НДФЛ" Тогда
						НоваяСтрока.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НДФЛ;
					Иначе
						НоваяСтрока.НачислениеУдержание = СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаВидРасчета];
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос = ЗапросДанныеДокументаФизическихЛиц();
	Запрос.УстановитьПараметр("НачисленияУдержанияПоСотрудникам", НачисленияУдержанияПоСотрудникам);
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьЗапросыКПредставлениямВиртуальныхТаблиц(Запрос.Текст, Запрос);
	
	НаборыВнешнихДанных = НаборыВнешнихДанныхАнализНачисленийИУдержаний();
	НаборыВнешнихДанных.НачисленияУдержанияДокумента = Запрос.Выполнить().Выгрузить();
	
	Возврат НаборыВнешнихДанных;
	
КонецФункции

Функция ЗапросДанныеДокументаФизическихЛиц()
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.ЗапросДанныеДокументаФизическихЛиц();
	
КонецФункции

Функция НаборыДанныхЗапросы(НаборыДанных) Экспорт
	
	НаборыЗапросы = Новый Массив;
	
	Для Каждого НаборДанных Из НаборыДанных Цикл
		
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			НаборыЗапросы.Добавить(НаборДанных);
		ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НаборыЗапросы, НаборыДанныхЗапросы(НаборДанных.Элементы));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НаборыЗапросы;
	
КонецФункции

Функция НачислениеДоговораГПХПоДокументуОснованию(ДокументОснование) Экспорт
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.НачислениеДоговораГПХПоДокументуОснованию(ДокументОснование);
	
КонецФункции

#Область ВариантовОтчетовПечатныхФорм

Функция МакетыВариантовОтчетовПечатныхФорм() Экспорт
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.МакетыВариантовОтчетовПечатныхФорм();
	
КонецФункции

Процедура ИнициализироватьОтчетПечатнойФормы(Контекст, ОтчетОбъект, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	ИнициализироватьНастройкиОтчета(ОтчетОбъект, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
	ПодключитьСхему(Контекст, ОтчетОбъект, КлючСхемы, КлючВарианта, НовыеПользовательскиеНастройкиКД);
	
КонецПроцедуры

Процедура ИнициализироватьНастройкиОтчета(ОтчетОбъект, КлючСхемы, КлючВарианта, НовыеНастройкиКД = Неопределено, НовыеПользовательскиеНастройкиКД = Неопределено) Экспорт
	
	Если КлючСхемы <> КлючВарианта Тогда
		
		ОтчетОбъект.ИнициализироватьОтчет();
		
		Если ВариантНастраиваемойПечатнойФормы(ОтчетОбъект, КлючВарианта) Тогда
			
			НастройкиУстановлены = Ложь;
			Если НовыеПользовательскиеНастройкиКД <> Неопределено
				И НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Свойство("ПользовательскиеНастройкиУстановлены") Тогда
				
				НастройкиУстановлены = Истина;
				Если НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Свойство("Настройки") Тогда
					НовыеНастройкиКД = НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Настройки;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Не НастройкиУстановлены Тогда
				
				ВариантОтчета = ВариантОтчетаПечатнойФормыПоОбъекту(ОтчетОбъект, КлючВарианта);
				Если ВариантОтчета <> Неопределено Тогда
					
					НастройкиВарианта = ВариантОтчета.Настройки.Получить();
					Если НастройкиВарианта <> Неопределено Тогда
						
						Если ТипЗнч(НастройкиВарианта) = Тип("Структура") Тогда
							НовыеНастройкиКД = НастройкиВарианта.Настройки;
							НовыеПользовательскиеНастройкиКД = НастройкиВарианта.ПользовательскиеНастройкиКД;
						Иначе
							НовыеПользовательскиеНастройкиКД = НастройкиВарианта;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если НовыеПользовательскиеНастройкиКД <> Неопределено Тогда
				
				НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Вставить("ИспользуютсяПользовательскиеНастройкиПечати", Истина);
				
				Если НастройкиУстановлены Тогда
					УстановитьОтборПоследнейСсылкиНаОбъектПечати(ОтчетОбъект, НовыеПользовательскиеНастройкиКД);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодключитьСхему(Контекст, ОтчетОбъект, КлючСхемы, КлючВарианта, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если КлючСхемы <> КлючВарианта Тогда
		
		ОтчетыСервер.ПодключитьСхему(ОтчетОбъект, Контекст, ОтчетОбъект.СхемаКомпоновкиДанных, КлючСхемы);
		КлючСхемы = КлючВарианта;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ВариантОтчетаПечатнойФормыПоОбъекту(ОтчетОбъект, КлючВарианта)
	
	Возврат ВариантОтчетаПечатнойФормыПоИмениОбъекта("Отчет." + ОтчетОбъект.Метаданные().Имя, КлючВарианта);
	
КонецФункции

Функция ВариантОтчетаПечатнойФормыПоИмениОбъекта(ИмяОбъектаОтчет, КлючВарианта) Экспорт
	
	Отчет = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ИмяОбъектаОтчет);
	Возврат ВариантОтчетаПечатнойФормы(Отчет, КлючВарианта);
	
КонецФункции

Функция ВариантОтчетаПечатнойФормы(Отчет, КлючВарианта)
	
	Возврат ВариантыОтчетов.ВариантОтчета(Отчет, КлючВарианта);
	
КонецФункции

Процедура УстановитьОтборПоследнейСсылкиНаОбъектПечати(ОтчетОбъект, ПользовательскиеНастройкиКД)
	
	ПараметрСсылка = ПараметрОтбораПечатнойФормы(ОтчетОбъект, ПользовательскиеНастройкиКД);
	Если ПараметрСсылка <> Неопределено Тогда
		
		Если ТипЗнч(ПараметрСсылка) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			ЗначениеПараметра = ПараметрСсылка.Значение;
		Иначе
			ЗначениеПараметра = ПараметрСсылка.ПравоеЗначение;
		КонецЕсли;
		
		ПараметрНастройки = ОтчетОбъект.КомпоновщикНастроек.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(Новый ПолеКомпоновкиДанных("СсылкаНаОбъект"));
		ТипыЗначения = ПараметрНастройки.ТипЗначения.Типы();
		
		Если ЗначениеПараметра = Неопределено
			Или (ТипЗнч(ЗначениеПараметра) = Тип("Массив") Или ТипЗнч(ЗначениеПараметра) = Тип("СписокЗначений"))
				И ЗначениеПараметра.Количество() = 0 Тогда
			
			ТипыЗначений = Новый СписокЗначений;
			Для Каждого ТипПараметра Из ТипыЗначения Цикл
				
				Если ОбщегоНазначения.ЭтоСсылка(ТипПараметра) Тогда
					
					ПолноеИмяОбъектаМетаданных = Метаданные.НайтиПоТипу(ТипПараметра).ПолноеИмя();
					ТипыЗначений.Добавить(ТипПараметра, ПолноеИмяОбъектаМетаданных);
					
				КонецЕсли;
				
			КонецЦикла;
			
			ТипыЗначений.СортироватьПоПредставлению();
			
			ЗначениеПараметра = Новый СписокЗначений;
			Для Каждого ЭлементТипыЗначений Из ТипыЗначений Цикл
				
				ПолноеИмяОбъектаМетаданных = ЭлементТипыЗначений.Представление;
				ТипПараметра = ЭлементТипыЗначений.Значение;
				
				Запрос = Новый Запрос;
				Запрос.Текст =
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	ТаблицаСсылок.Ссылка КАК Ссылка
					|ИЗ
					|	&ТаблицаСсылок КАК ТаблицаСсылок
					|ГДЕ
					|	НЕ ТаблицаСсылок.ПометкаУдаления";
				
				ВидОбъекта = ОбщегоНазначения.ВидОбъектаПоТипу(ТипПараметра);
				Если ВидОбъекта = "Документ" Тогда
					
					Запрос.Текст = Запрос.Текст + "
						|	И ТаблицаСсылок.Проведен
						|УПОРЯДОЧИТЬ ПО
						|	ТаблицаСсылок.Дата УБЫВ";
					
				КонецЕсли;
				
				ПолноеИмяОбъектаМетаданных = Метаданные.НайтиПоТипу(ТипПараметра).ПолноеИмя();
				ИмяТипа = СтрРазделить(ПолноеИмяОбъектаМетаданных, ".")[1];
				
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТаблицаСсылок", ВидОбъекта + "." + ИмяТипа);
				
				РезультатЗапроса = Запрос.Выполнить();
				Если Не РезультатЗапроса.Пустой() Тогда
					
					Выборка = РезультатЗапроса.Выбрать();
					Выборка.Следующий();
					
					ЗначениеПараметра.Добавить(Выборка.Ссылка);
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
			УстановитьОбъектыОтбора(ПараметрСсылка, ЗначениеПараметра, ПользовательскиеНастройкиКД);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрОтбораПечатнойФормы(ОтчетОбъект, ПользовательскиеНастройки)
	
	Если ПользовательскиеНастройки = Неопределено Тогда
		ПользовательскиеНастройкиКД = ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки;
	Иначе
		ПользовательскиеНастройкиКД = ПользовательскиеНастройки;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОтчетОбъект.КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки) Тогда
		ОтчетОбъект.КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	ПараметрСсылка = Неопределено;
	
	ПараметрСсылкиНаОбъекты = Новый ПараметрКомпоновкиДанных("СсылкиНаОбъекты");
	Для Каждого ЭлементНастроек Из ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы Цикл
		
		Если ТипЗнч(ЭлементНастроек) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			
			Если ЭлементНастроек.Параметр = ПараметрСсылкиНаОбъекты Тогда
				
				Если Не ЗначениеЗаполнено(ЭлементНастроек.ИдентификаторПользовательскойНастройки) Тогда
					ЭлементНастроек.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор;
				КонецЕсли;
				
				ПараметрСсылка = ЭлементНастроек;
				Прервать;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПараметрСсылка = Неопределено Тогда
		
		ПолеКомпоновкиСсылка = Новый ПолеКомпоновкиДанных("СсылкаНаОбъект");
		Для Каждого ЭлементНастроек Из ОтчетОбъект.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
			
			Если ТипЗнч(ЭлементНастроек) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				
				Если ЭлементНастроек.ЛевоеЗначение = ПолеКомпоновкиСсылка Тогда
					
					Если Не ЗначениеЗаполнено(ЭлементНастроек.ИдентификаторПользовательскойНастройки) Тогда
						ЭлементНастроек.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор;
					КонецЕсли;
					
					ПараметрСсылка = ЭлементНастроек;
					Прервать;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ПараметрСсылка;
	
КонецФункции

Процедура УстановитьОбъектыОтбора(ПараметрОтбора, ОбъектыОтбора, ПользовательскиеНастройкиКД = Неопределено)
	
	Если ОбъектыОтбора <> Неопределено
		И ОбъектыОтбора.Количество() > 0 Тогда
		
		Если ТипЗнч(ПараметрОтбора) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			КонтейнерПараметра = ПараметрОтбора.Значение;
		Иначе
			КонтейнерПараметра = ПараметрОтбора.ПравоеЗначение;
		КонецЕсли;
		
		Если ТипЗнч(ОбъектыОтбора) = Тип("Массив") Тогда
			КонтейнерПараметра.ЗагрузитьЗначения(ОбъектыОтбора);
		Иначе
			КонтейнерПараметра = ОбъектыОтбора;
		КонецЕсли;
		
		Если ПользовательскиеНастройкиКД <> Неопределено Тогда
			
			ПользовательскаяНастройка = ПользовательскиеНастройкиКД.Элементы.Найти(ПараметрОтбора.ИдентификаторПользовательскойНастройки);
			Если ПользовательскаяНастройка <> Неопределено Тогда
				Если ТипЗнч(ПараметрОтбора) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
					ПользовательскаяНастройка.Значение = КонтейнерПараметра;
				Иначе
					ПользовательскаяНастройка.ПравоеЗначение = КонтейнерПараметра;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПараметрОтбора(ОтчетОбъект, ОбъектыОтбора, ПользовательскиеНастройкиКД = Неопределено) Экспорт
	
	ПараметрСсылка = ПараметрОтбораПечатнойФормы(ОтчетОбъект, ПользовательскиеНастройкиКД);
	Если ПараметрСсылка <> Неопределено Тогда
		УстановитьОбъектыОтбора(ПараметрСсылка, ОбъектыОтбора, ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки);
	КонецЕсли;
	
КонецПроцедуры

Функция ВариантНастраиваемойПечатнойФормы(ОтчетОбъект, КлючВарианта)
	
	МакетыВариантовОтчетов = МакетыВариантовОтчетовПечатныхФорм();
	ПолноеИмяОбъекта = ОтчетОбъект.Метаданные().ПолноеИмя();
	
	ПутьКМакету = МакетыВариантовОтчетов.Получить(ПолноеИмяОбъекта + "." + КлючВарианта);
	
	Возврат ПутьКМакету <> Неопределено;
	
КонецФункции

Процедура ВывестиВКоллекциюПечатнуюФорму(ИмяОтчета, МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода, ВнешниеНаборыДанных = Неопределено, ДополнительныеПараметрыФормирования = Неопределено) Экспорт
	
	ВариантыОтчетовПечатныхФорм = МакетыВариантовОтчетовПечатныхФорм();
	Для Каждого ОписаниеНастраиваемойФормы Из ВариантыОтчетовПечатныхФорм Цикл
		
		ПутьКВариантуОтчета = ОписаниеНастраиваемойФормы.Ключ;
		Если СтрНайти(ПутьКВариантуОтчета, ИмяОтчета) = 1 Тогда
			
			ПутьКМакетуПечатнойФормы = ОписаниеНастраиваемойФормы.Значение;
			ИмяМакетаПечатнойФормы = ИмяМакетаИзПутиКМакетуПечатнойФормы(ПутьКМакетуПечатнойФормы);
			
			Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакетаПечатнойФормы) Тогда
				
				Если ДополнительныеПараметрыФормирования = Неопределено Тогда
					ДополнительныеПараметры = Новый Структура;
				Иначе
					ДополнительныеПараметры = ОбщегоНазначения.СкопироватьРекурсивно(ДополнительныеПараметрыФормирования);
				КонецЕсли;
				
				ДокументРезультат = Новый ТабличныйДокумент;
				
				КлючВарианта = КлючВариантаИзПутиКВарианту(ПутьКВариантуОтчета);
				
				Если ВРег(Лев(ПутьКМакетуПечатнойФормы, 11)) = ВРег("ОбщийМакет.") Тогда
					ПредставлениеПечатнойФормы = Метаданные.ОбщиеМакеты[ИмяМакетаПечатнойФормы].Синоним;
				Иначе
					ПредставлениеПечатнойФормы = Метаданные.НайтиПоПолномуИмени(ИмяОтчета).Макеты[ИмяМакетаПечатнойФормы].Синоним;
				КонецЕсли;
				
				ОбъектОтчета = ОбщегоНазначения.ОбъектПоПолномуИмени(ИмяОтчета);
				
				Если ДополнительныеПараметры.Свойство("Отбор") Тогда
					МакетКомпоновкиДанных = Неопределено;
				Иначе
					МакетКомпоновкиДанных = МакетКомпоновкиДанныхОтчета(ОбъектОтчета, КлючВарианта);
				КонецЕсли;
				
				Если МакетКомпоновкиДанных = Неопределено Тогда
					
					НастройкиОтчетаКД = Неопределено;
					ПользовательскиеНастройкиКД = Неопределено;
					
					ИнициализироватьНастройкиОтчета(ОбъектОтчета, "", КлючВарианта, НастройкиОтчетаКД, ПользовательскиеНастройкиКД);
					
					Если НастройкиОтчетаКД = Неопределено Тогда
						НастройкиОтчетаКД = ОбъектОтчета.СхемаКомпоновкиДанных.ВариантыНастроек[КлючВарианта].Настройки;
					КонецЕсли;
					
					ОбъектОтчета.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиОтчетаКД);
					Если ТипЗнч(ПользовательскиеНастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
						ОбъектОтчета.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПользовательскиеНастройкиКД);
					КонецЕсли;
					
					УстановитьПараметрОтбора(ОбъектОтчета, МассивОбъектов);
					
				Иначе
					
					ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить(
						"МакетКомпоновкиДанных", МакетКомпоновкиДанных);
					
					ПараметрКлючВарианта = МакетКомпоновкиДанных.ЗначенияПараметров.Найти("КлючВарианта");
					Если ПараметрКлючВарианта <> Неопределено Тогда
						ДополнительныеПараметры.Вставить("КлючВарианта", ПараметрКлючВарианта.Значение);
					КонецЕсли;
					
					Для каждого Параметр Из МакетКомпоновкиДанных.ЗначенияПараметров Цикл
						
						ПараметрКомпоновщика = Лев(Параметр.Имя, 1) = "П" И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Сред(Параметр.Имя, 2));
						
						Если Параметр.Имя = "СсылкиНаОбъекты"
							Или ПараметрКомпоновщика
								И ТипЗнч(Параметр.Значение) = Тип("СписокЗначений")
								И Не ЗначениеЗаполнено(Параметр.Значение) Тогда
							
							Параметр.Значение.ЗагрузитьЗначения(МассивОбъектов);
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
				РезультатКомпоновки = РезультатКомпоновкиМакетаПечатнойФормы(ОбъектОтчета, , , ВнешниеНаборыДанных, ДополнительныеПараметры);
				
				МенеджерОтчета = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяОтчета);
				МенеджерОтчета.Сформировать(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати);
				
				УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
					КоллекцияПечатныхФорм,
					ИмяМакетаПечатнойФормы,
					ПредставлениеПечатнойФормы,
					ДокументРезультат,,);
				
				Прервать;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция КлючВариантаИзПутиКВарианту(ПутьКВариантуОтчета) Экспорт
	
	ПодстрокиПути = СтрРазделить(ПутьКВариантуОтчета, ".");
	Возврат ПодстрокиПути[ПодстрокиПути.Количество() - 1];
	
КонецФункции

Функция ИмяМакетаИзПутиКМакетуПечатнойФормы(ПутьКМакетуПечатнойФормы) Экспорт
	
	ПодстрокиПути = СтрРазделить(ПутьКМакетуПечатнойФормы, ".");
	Возврат ПодстрокиПути[ПодстрокиПути.Количество() - 1];
	
КонецФункции

Функция ПоляПредставленийКадровыхДанныхСотрудниковОтчетовПечатныхФорм(ИмяВТПредставления = "Представления_КадровыеДанныеСотрудников") Экспорт
	
	ДополнительныеПоляПредставлений = Новый Структура;
	ДополнительныеПоляПредставлений.Вставить(ИмяВТПредставления, КадровыйУчет.ПоляПредставленийКадровыхДанныхСотрудников());
	
	Возврат ДополнительныеПоляПредставлений;
	
КонецФункции

Процедура ЗагрузитьНастройкиВКомпоновщикКД(КомпоновщикНастроекКД, ОбъектОтчета, КлючВарианта) Экспорт
	
	ИмяОтчета = ОбъектОтчета.Метаданные().ПолноеИмя();
	Схема = ОбъектОтчета.СхемаКомпоновкиДанных;
	#Если ТолстыйКлиентУправляемоеПриложение Тогда
		КомпоновщикНастроекКД.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Схема));
	#Иначе
		АдресСхемы = ПоместитьВоВременноеХранилище(Схема, Новый УникальныйИдентификатор);
		КомпоновщикНастроекКД.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	#КонецЕсли
	
	Отчет = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ИмяОтчета);
	
	НастройкиПечатнойФормы = НастройкиПечатнойФормы(Отчет, КлючВарианта);
	Если НастройкиПечатнойФормы = Неопределено Тогда
		НастройкиПечатнойФормы = Новый Структура("Настройки,ПользовательскиеНастройкиКД");
	КонецЕсли;
	
	Если НастройкиПечатнойФормы.Настройки = Неопределено Тогда
		НастройкиПечатнойФормы.Настройки = Схема.ВариантыНастроек[КлючВарианта].Настройки;
	КонецЕсли;
	
	ЗагрузитьНастройкиПечатнойФормы(КомпоновщикНастроекКД, НастройкиПечатнойФормы);
	
КонецПроцедуры

#КонецОбласти

Процедура ЗапомнитьЗапросыНаборовДанных(НаборыДанных, ОбъектОтчета)
	
	Для Каждого НаборДанных Из НаборыДанных Цикл
		
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить(НаборДанных.Имя, НаборДанных.Запрос);
		ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
			ЗапомнитьЗапросыНаборовДанных(НаборДанных.Элементы, ОбъектОтчета);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВосстановитьЗапросыНаборовДанных(НаборыДанных, ОбъектОтчета)
	
	УдаленныеПоляНаборов = Неопределено;
	ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("УдаленныеПоляНаборов", УдаленныеПоляНаборов);
	Для Каждого НаборДанных Из НаборыДанных Цикл
		
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			
			Если ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Свойство(НаборДанных.Имя) Тогда
				НаборДанных.Запрос = ОбъектОтчета.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства[НаборДанных.Имя];
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
			ВосстановитьЗапросыНаборовДанных(НаборДанных.Элементы, ОбъектОтчета);
		КонецЕсли;
		
		Если УдаленныеПоляНаборов <> Неопределено Тогда
			
			ПоляНабора = УдаленныеПоляНаборов.Получить(НаборДанных.Имя);
			Если ПоляНабора <> Неопределено Тогда
				
				Для Каждого УдаленноеПоле Из ПоляНабора Цикл
					
					Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
					ЗаполнитьЗначенияСвойств(Поле, УдаленноеПоле);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УточнитьОтборыЗапросовНаборовДанныхМакетаКомпоновкиДанных(НаборыДанных)
	
	Для Каждого НаборДанных Из НаборыДанных Цикл
		
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеМакетаКомпоновкиДанных") Тогда
			УточнитьОтборыЗапросовНаборовДанныхМакетаКомпоновкиДанных(НаборДанных.Элементы);
		ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросМакетаКомпоновкиДанных") Тогда
			
			Если СтрНайти(НаборДанных.Запрос, "NULL ")
				Или СтрНайти(НаборДанных.Запрос, "НЕОПРЕДЕЛЕНО ") Тогда
				
				Схема = Новый СхемаЗапроса;
				Схема.УстановитьТекстЗапроса(НаборДанных.Запрос);
				Для Каждого ЗапросПакета Из Схема.ПакетЗапросов Цикл
					
					Если ТипЗнч(ЗапросПакета) = Тип("ЗапросВыбораСхемыЗапроса") Тогда
						
						Для Каждого ОператорПакета Из ЗапросПакета.Операторы Цикл
							
							ИндексыУдаляемыхОтборов = Новый Массив;
							Для Каждого УсловиеОтбора Из ОператорПакета.Отбор Цикл
								
								Если СтрНачинаетсяС(УсловиеОтбора, "NULL ")
									Или СтрНачинаетсяС(УсловиеОтбора, "НЕ NULL ")
									Или СтрНачинаетсяС(УсловиеОтбора, "НЕОПРЕДЕЛЕНО ")
									Или СтрНачинаетсяС(УсловиеОтбора, "НЕ НЕОПРЕДЕЛЕНО ") Тогда
									
									ИндексыУдаляемыхОтборов.Вставить(0, ОператорПакета.Отбор.Индекс(УсловиеОтбора));
									
								КонецЕсли;
								
							КонецЦикла;
							
							Для Каждого ИндексУдаляемогоОтбора Из ИндексыУдаляемыхОтборов Цикл
								ОператорПакета.Отбор.Удалить(ИндексУдаляемогоОтбора);
							КонецЦикла;
							
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЦикла;
				
				НаборДанных.Запрос = Схема.ПолучитьТекстЗапроса();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИменаПараметровСтраницыТабличногоДокумента()
	
	Возврат "ОриентацияСтраницы,АвтоМасштаб,МасштабПечати,ПолеСверху,ПолеСлева,ПолеСнизу,ПолеСправа,РазмерКолонтитулаСверху,РазмерКолонтитулаСнизу";
	
КонецФункции

Функция ЭтоНачислениеСдельнойОплатыТруда(ВидРасчета)
	
	Возврат ЗарплатаКадрыОтчетыВнутренний.ЭтоНачислениеСдельнойОплатыТруда(ВидРасчета);
	
КонецФункции

#КонецОбласти
