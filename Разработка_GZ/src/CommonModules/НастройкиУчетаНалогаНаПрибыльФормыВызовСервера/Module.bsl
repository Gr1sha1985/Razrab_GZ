
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Серверные вызовы методов формы записи регистра сведений НастройкиУчетаНалогаНаПрибыль
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция НастройкиСистемыНалогообложения(Организация, Период) Экспорт
	
	Настройки = Новый Структура;
	
	НачалоНалоговогоПериода = НалоговыйУчет.НачалоНалоговогоПериода(Период, Организация);
	Если НачалоНалоговогоПериода <> Неопределено Тогда
		ПлательщикЕНВД = УчетнаяПолитика.ПлательщикЕНВДЗаПериод(Организация, НачалоНалоговогоПериода, КонецГода(Период));
		Настройки.Вставить("ПлательщикЕНВД", ПлательщикЕНВД);
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

Функция НаличиеСписковПрямыхРасходов(Организация, Период) Экспорт
	
	// В регистре допускаются записи только на начало года.
	СтандартныйПериод = НачалоГода(Период);
	
	ПериодНастройкиТекущегоГода = РегистрыСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ.ПериодДействующейНастройки(
		СтандартныйПериод,
		Организация);
	
	ПериодНастройкиПрошлогоГода = РегистрыСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ.ПериодДействующейНастройки(
		СтандартныйПериод - 1,
		Организация);
	
	НаличиеСписков = Новый Структура;
	НаличиеСписков.Вставить("ЕстьСписокТекущегоПериода", ПериодНастройкиТекущегоГода = СтандартныйПериод);
	НаличиеСписков.Вставить(
		"ЕстьСписокПрошлогоПериода",
		ПериодНастройкиПрошлогоГода <> Неопределено
		И ПериодНастройкиПрошлогоГода < СтандартныйПериод); // Может быть, если организация создана в декабре
	
	Возврат НаличиеСписков;
	
КонецФункции

Процедура ЗаполнитьСписокПрямыхРасходов(Организация, Период, Скопировать = Неопределено) Экспорт
	
	РегистрыСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ.Настроить(Организация, Период, Скопировать);
	
КонецПроцедуры

#КонецОбласти