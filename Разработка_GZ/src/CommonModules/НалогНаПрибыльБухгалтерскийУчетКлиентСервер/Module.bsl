// Отражение в финансовой отчетности отложенных налогов на прибыль.
// Выполнение требований
// - ПБУ 18 "Учет расчетов по налогу на прибыль организаций"
// - IAS 12 "Налоги на прибыль"

#Область ПрограммныйИнтерфейс

// Хранит дату начала обязательного применения изменений в ПБУ18,
// внесенных Приказом Минфина от 20 ноября 2018 г. N 236н:
//
// "2. Установить, что предусмотренные настоящим Приказом изменения применяются организациями,
// начиная с бухгалтерской (финансовой) отчетности за 2020 год.
// Организация вправе принять решение о применении предусмотренных настоящим Приказом изменений до указанного срока."
// 
// Возвращаемое значение:
//  Дата - дата начала обязательного применения изменений
//
Функция ДатаНачалаПримененияПриказа236н() Экспорт
	
	Возврат '2020-01-01';
	
КонецФункции

// Определяет, обязательны ли к применению в указанном периоде,
// изменения в ПБУ18, внесенные Приказом Минфина от 20 ноября 2018 г. N 236н
//
// Параметры:
//  Период - Дата - отчетный период
// 
// Возвращаемое значение:
//  Булево - Истина, если изменения обязательны к применению
//
Функция Приказ236нОбязателен(Период) Экспорт
	
	Возврат Период >= ДатаНачалаПримененияПриказа236н();
	
КонецФункции

// Определяет основной метод учета отложенного налога в указанном периоде.
//
// Параметры:
//  Период - Дата - отчетный период
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ВариантыУчетаОтложенногоНалога - метод учета отложенного налога
//
Функция ОсновнойМетодУчетаОтложенногоНалога(Период) Экспорт
	
	Если Приказ236нОбязателен(Период) Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.ВариантыУчетаОтложенногоНалога.БалансовыйМетодПостоянныеРазницы");
		
	Иначе
		
		Возврат ПредопределенноеЗначение("Перечисление.ВариантыУчетаОтложенногоНалога.ЗатратныйМетод");
		
	КонецЕсли;
	
КонецФункции

// Хранит гиперссылку (URL) на описание методов учета отложенных налогов (на языке пользователя)
// 
// Возвращаемое значение:
//  Строка - URL методических материалов
//
Функция ГиперссылкаОписаниеМетодовУчета() Экспорт
	
	Возврат НСтр("ru = 'https://buh.ru/pbu18'");
	
КонецФункции

#КонецОбласти
