
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Серверные методы формы записи регистра сведений НастройкиУчетаУСН
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Запись = Форма.НастройкиУчетаУСН;
	
	ПодготовитьФормуНаСервере(Форма);
	
КонецПроцедуры

Процедура ПодготовитьФормуНаСервере(Форма, СписокНедействующихНастроек = Неопределено) Экспорт
	
	Запись = Форма.НастройкиУчетаУСН;
	
	Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
		Запись.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ПериодЧтенияНастройки = НастройкиУчетаУСНФормыКлиентСервер.ПериодЧтенияНастройки(Форма);
	
	ЗаполнитьЗначенияСвойств(Форма,
		НастройкиУчетаУСНФормыВызовСервера.НастройкиСистемыНалогообложения(
			Запись.Организация, ПериодЧтенияНастройки, СписокНедействующихНастроек));
	
	НастройкиУчетаУСНФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеНаСервере(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	Запись = Форма.НастройкиУчетаУСН;
	ПериодЧтенияНастройки = НастройкиУчетаУСНФормыКлиентСервер.ПериодЧтенияНастройки(Форма);
	
	СтавкиПоУмолчанию = УчетУСНКлиентСервер.НалоговыеСтавкиПоУмолчанию(ПериодЧтенияНастройки);
	ПределСтавки = ?(Форма.ПрименяетсяУСНДоходы, СтавкиПоУмолчанию.СтавкаУСНДоходы, СтавкиПоУмолчанию.СтавкаУСНДоходыМинусРасходы);
	Если Запись.СтавкаНалога > ПределСтавки Тогда
		ТекстПредупреждения = СтрШаблон(НСтр("ru = 'Выбранная ставка не может применяться.
			|Основная ставка налога для выбранного объекта налогообложения равна %1%2.'"), ПределСтавки, НСтр("ru = '%' "));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстПредупреждения,,"СтавкаНалога", "НастройкиУчетаУСН");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
