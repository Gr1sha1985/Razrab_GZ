#Область СлужебныйПрограммныйИнтерфейс

// Для вызова из модуля формы и расширенной реализации.
// Не следует использовать для вызова из других подсистем.

// Обработчик события ПриСозданииНаСервере формы документа ДепонированиеЗарплаты.
// 	Устанавливает первоначальные значения реквизитов объекта.
//	Инициализирует реквизиты формы.
//
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма, которая создается.
// 	Отказ - Булево - признак отказа от создания формы.
// 	СтандартнаяОбработка - Булево - признак выполнения стандартной обработки события.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	// Первоначальное заполнение объекта.
	Если Форма.Параметры.Ключ.Пустая() Тогда
		
		Форма.ЗаполнитьПервоначальныеЗначения();
		Форма.ПриПолученииДанныхНаСервере();

	КонецЕсли;
	
	ТипыВедомости = Метаданные.Документы.ДепонированиеЗарплаты.Реквизиты.Ведомость.Тип.Типы();
	МДВедомостьНаВыплатуЗарплаты = Метаданные.Документы.Найти("ВедомостьНаВыплатуЗарплаты");
	Если МДВедомостьНаВыплатуЗарплаты <> Неопределено Тогда
		ТипВедомостьНаВыплатуЗарплаты = Тип("ДокументСсылка." + МДВедомостьНаВыплатуЗарплаты.Имя);
		
		Если ТипыВедомости.Количество() > 1 Тогда
			ИспользуетсяНачислениеЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты");
			
			ТипыДляУдаления = Новый Массив;
			Для Каждого ТипВедомости Из ТипыВедомости Цикл
				Если ИспользуетсяНачислениеЗарплаты И ТипВедомости = ТипВедомостьНаВыплатуЗарплаты Тогда
					ТипыДляУдаления.Добавить(ТипВедомости);
				ИначеЕсли Не ИспользуетсяНачислениеЗарплаты И ТипВедомости <> ТипВедомостьНаВыплатуЗарплаты Тогда
					ТипыДляУдаления.Добавить(ТипВедомости);
				КонецЕсли;
			КонецЦикла;
			Для Каждого ТипДляУдаления Из ТипыДляУдаления Цикл
				ИндексВедомости = ТипыВедомости.Найти(ТипДляУдаления);
				ТипыВедомости.Удалить(ИндексВедомости);
			КонецЦикла;
			
			Форма.Элементы.Ведомость.ОграничениеТипа	= Новый ОписаниеТипов(ТипыВедомости);
		КонецЕсли;
	КонецЕсли;
	Форма.Элементы.Ведомость.ВыбиратьТип = ТипыВедомости.Количество() > 1;
	
КонецПроцедуры

#КонецОбласти
