#Область ПрограммныйИнтерфейс

// Настраивает (отображает, скрывает, если необходимо - создает) элементы формы,
// предназначенные для информирования пользователя об особенностях первого налогового периода.
// Следует вызывать в контексте формы.
// 
// Параметры:
//  Элементы	 - ВсеЭлементыФормы - коллекция элементов настраиваемой формы
//  Информация	 - Структура - см. ИнформированиеНалоговыйПериодКлиентСервер.НовыйИнформация,
//                 информация, подлежащая выводу в элементы формы
//               - Неопределено - ничего выводить не нужно, элементы информирования следует скрыть
//  Позиция		 - ПолеФормы, ГруппаФормы, ТаблицаФормы - элемент формы,
//                 перед которым размещаются элементы для информирования пользователя
//
Процедура НастроитьЭлементИнформирования(Элементы, Информация, Позиция = Неопределено) Экспорт
	
	Если Информация = Неопределено Тогда
		ИнформированиеНалоговыйПериодКлиентСервер.СкрытьИнформацию(Элементы);
		Возврат;
	КонецЕсли;
	
	ЭлементИнформация = ИнформированиеНалоговыйПериодКлиентСервер.НайтиЭлементИнформирования(Элементы);
	Если ЭлементИнформация = Неопределено Тогда
		ЭлементИнформация = СоздатьЭлементИнформирования(Элементы, Позиция);
	КонецЕсли;
	
	ИнформированиеНалоговыйПериодКлиентСервер.ОтобразитьИнформацию(ЭлементИнформация, Информация);
	
КонецПроцедуры

// Готовит тексты информации для пользователя об особенностях первого налогового периода организации,
// зарегистрированной в конце стандартного налогового (отчетного) периода.
//
// Параметры:
//  ВариантПериода						 - Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода - вариант требований законодательства
//  КонецОтчетногоПериода				 - Дата - конец отчетного периода, к которому относится информация
//  Организация							 - СправочникСсылка.Организации - организация (налогоплательщик)
//  ВидОтчетаВинительныйПадеж			 - Строка - наименование отчета (расчета), в который включаются данные отчетного периода,
//                                         в винительном падеже, например "декларацию" или "справку"
//  ПродолжительностьНалоговогоПериода	 - ПеречислениеСсылка.Периодичность - продолжительность налогового периода
//  ПродолжительностьОтчетногоПериода	 - ПеречислениеСсылка.Периодичность - продолжительность отчетного периода в рамках налогового
//  ОписаниеНалоговогоПериода			 - Структура - результат функции ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод.
//                                         Может быть передан для оптимизации производительности, если уже рассчитан в вызывающем коде.
// 
// Возвращаемое значение:
//  Структура - см. ИнформированиеНалоговыйПериодКлиентСервер.НовыйИнформация
//
Функция ИнформацияНалоговыйПериод(ВариантПериода, КонецОтчетногоПериода, Организация, ВидОтчетаВинительныйПадеж,
	ПродолжительностьНалоговогоПериода = Неопределено, ПродолжительностьОтчетногоПериода = Неопределено,
	ОписаниеНалоговогоПериода = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВариантПериода)
		Или ВариантПериода = Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.НеПредусмотрен Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ПродолжительностьНалоговогоПериода = Неопределено Тогда
		ПродолжительностьНалоговогоПериода = Перечисления.Периодичность.Год;
	КонецЕсли;
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Организация,
		"ДатаРегистрации, ЮридическоеФизическоеЛицо");
		
	Если ОписаниеНалоговогоПериода = Неопределено Тогда
		
		ОписаниеНалоговогоПериода = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(
			Организация,
			КонецОтчетногоПериода,
			ВариантПериода,
			ПродолжительностьНалоговогоПериода,
			РеквизитыОрганизации.ДатаРегистрации);
			
	КонецЕсли;
		
	Если ОписаниеНалоговогоПериода.Начало >= ОписаниеНалоговогоПериода.Период Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ПродолжительностьОтчетногоПериода <> Неопределено Тогда
		ПродолжительностьОтчетногоПериодаТочная = Истина;
	Иначе
		ПродолжительностьОтчетногоПериода       = ПродолжительностьНалоговогоПериода;
		ПродолжительностьОтчетногоПериодаТочная = Ложь;
	КонецЕсли;
	
	ПредставлениеОтчетногоПериода = ПредставлениеОтчетногоПериода(
		КонецОтчетногоПериода,
		ОписаниеНалоговогоПериода.Период,
		ПродолжительностьОтчетногоПериода,
		ПродолжительностьОтчетногоПериодаТочная);
	
	ТекстЗаголовка = СтрШаблон(
		НСтр("ru = 'Период с даты регистрации %1 по %2 включается в %3 за %4'"),
		Формат(РеквизитыОрганизации.ДатаРегистрации, "ДЛФ=D"),
		Формат(ОписаниеНалоговогоПериода.Период - 1, "ДЛФ=D"),
		ВидОтчетаВинительныйПадеж,
		ПредставлениеОтчетногоПериода);
		
	ТекстПодсказки = ИнформированиеНалоговыйПериодКлиентСервер.ТекстПодсказки(
		РеквизитыОрганизации.ЮридическоеФизическоеЛицо,
		ВариантПериода,
		ПродолжительностьНалоговогоПериода,
		ВидОтчетаВинительныйПадеж);
	
	Возврат ИнформированиеНалоговыйПериодКлиентСервер.НовыйИнформация(
		ТекстЗаголовка,
		ТекстПодсказки);
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоздатьЭлементИнформирования(Элементы, Позиция)
	
	ФонЭлементаИнформация = Элементы.Вставить(
		"ФонИнформацияНалоговыйПериод", 
		Тип("ГруппаФормы"),
		Позиция.Родитель,
		Позиция);
		
	ФонЭлементаИнформация.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		
	ФонЭлементаИнформация.Отображение         = ОтображениеОбычнойГруппы.Нет;
	ФонЭлементаИнформация.ОтображатьЗаголовок = Ложь;
		
	ФонЭлементаИнформация.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;
	ФонЭлементаИнформация.РастягиватьПоГоризонтали = Истина;
		
	ЭлементИнформация = Элементы.Добавить(
		"ИнформацияНалоговыйПериод",
		Тип("ДекорацияФормы"),
		ФонЭлементаИнформация);
		
	ЭлементИнформация.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	ЭлементИнформация.АвтоМаксимальнаяШирина = Ложь;
	
	Возврат ЭлементИнформация;
		
КонецФункции

Функция ПредставлениеОтчетногоПериода(Период, НалоговыйПериод, Продолжительность, ПродолжительностьТочная = Истина)
	
	Если ПродолжительностьТочная Тогда
		КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Продолжительность, Период);
	КонецЕсли;
	
	Если НалоговыйПериод > КонецПериода Тогда
		КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Продолжительность, НалоговыйПериод);
	КонецЕсли;
	
	Возврат КалендарьБухгалтера.ПредставлениеПериодаСобытия(НалоговыйПериод, КонецПериода);
	
КонецФункции

#КонецОбласти
