
#Область СлужебныеПроцедурыИФункции

// Функция проверяет использоваие измененного "старого" макета
// Параметры:
//  Ссылка - Документ.Ссылка.
// Возвращаемое значение:
//  Булево - признак того, что есть измененные макеты без областей для нового механизма факсимиле
//           Истина - есть измененные макеты
//           Ложь   - нет измененных макетов
//  
Функция ИспользуетсяИзмененныйМакет(Ссылка) Экспорт
	
	Возврат УправлениеПечатьюБП.ИспользуетсяИзмененныйМакет(Ссылка);
		
КонецФункции

// Функция возвращает массив основных спецификаций по массиву переданной номенклатуры,
//  а также массив номенклатуры без основных спецификаций
//
// Параметры:
//  МассивНоменклатуры	 - Массив - массив номенклатуры, по которому нужно распечатать основные спецификации
// 
// Возвращаемое значение:
//   - Структура со свойствами
//  	-- Спецификации
//  	-- НоменклатураБезСпецификации
//
Функция НастройкиПечатиСпецификацийНоменклатуры(МассивНоменклатуры) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.ОсновнаяСпецификацияНоменклатуры КАК Спецификация
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&МассивНоменклатуры)
	|	И Номенклатура.ОсновнаяСпецификацияНоменклатуры <> ЗНАЧЕНИЕ(Справочник.СпецификацииНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&МассивНоменклатуры)
	|	И Номенклатура.ОсновнаяСпецификацияНоменклатуры = ЗНАЧЕНИЕ(Справочник.СпецификацииНоменклатуры.ПустаяСсылка)";

	Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
	
	Результат = Запрос.ВыполнитьПакет();
	
	НастройкиПечати = Новый Структура;
	
	НастройкиПечати.Вставить("Спецификации", Результат[0].Выгрузить().ВыгрузитьКолонку("Спецификация"));
	НастройкиПечати.Вставить("НоменклатураБезСпецификации", Результат[1].Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	Возврат НастройкиПечати;
	
КонецФункции	

#КонецОбласти
