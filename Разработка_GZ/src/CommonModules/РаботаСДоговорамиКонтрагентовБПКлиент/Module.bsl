#Область ПрограммныйИнтерфейс

// Обработчик события выбора поля Договор
//
// Параметры:
// Элемент - Поле формы, для которого выполняется обработчик.
// ВыбранноеЗначение - Значение, выбранное в поле "Договор" формы
// ПараметрыСоздания - Структура - Содержит дополнительные параметры заполнения нового договора
// СтандартнаяОбработка
//
Процедура ДоговорОбработкаВыбора(Элемент, ВыбранноеЗначение, ПараметрыСоздания, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		
		Если ВыбранноеЗначение = "НовыйДоговор" Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ЗначенияЗаполнения = ЗначенияЗаполненияПоПараметрамВыбора(Элемент.ПараметрыВыбора);
			ЗначенияЗаполнения.Вставить("Организация");
			ЗначенияЗаполнения.Вставить("Владелец");
			
			// Дополняем ЗначенияЗаполнения явно переданными параметрами нового договора
			// вид договор берется из параметров выбора и не должен переопределяться
			ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, ПараметрыСоздания,,"ВидДоговора");
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ПараметрыФормы.Вставить("СозданИзФормыДокумента", Истина);
			
			ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаОбъекта", ПараметрыФормы, Элемент);
			
		ИначеЕсли ВыбранноеЗначение = "БезДоговора" Тогда
			
			ПараметрыСоздания.Вставить("Наименование", НСтр("ru='Без договора'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()));
			
			ПараметрыДоговора = Новый Структура;
			ПараметрыДоговора.Вставить("ЗначенияЗаполнения", ПараметрыСоздания);
			
			ВыбранноеЗначение = РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыДоговора);
			
		КонецЕсли;
	ИначеЕсли Элемент.СписокВыбора.Количество() > 0 Тогда 
		Элемент.СписокВыбора.Очистить();
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список выбор для поля Договор
//
// Параметры:
// ЭлементФормыСписка - Поле формы, для которого выполняется обработчик.
// Текст - Строка - Текст, введенный в поле "Договор" формы
// ПредлагатьНовыйДоговор - Булево - Истина, если в поле Договор должен быть доступен выбор из предопределенных значений.
// СтандартнаяОбработка
//
Процедура ЗаполнитьСписокВыбора(ЭлементФормыСписка, Текст, ПредлагатьНовыйДоговор, СтандартнаяОбработка) Экспорт
	
	СписокВыбора = ЭлементФормыСписка.СписокВыбора;
	
	Если СписокВыбора.Количество()> 0 Тогда
		СписокВыбора.Очистить();
	КонецЕсли;
	
	Если ПустаяСтрока(Текст) И ПредлагатьНовыйДоговор Тогда
		
		СписокВыбора.Добавить("БезДоговора", НСтр("ru='Без договора'", ОбщегоНазначенияКлиент.КодОсновногоЯзыка()));
		СписокВыбора.Добавить("НовыйДоговор", НСтр("ru='Создать договор ...'"));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НастроитьСпособЗаполненияСтавкиНДС(Форма, ИмяТаблицы, СтавкаНДС = Неопределено) Экспорт

	Элементы = Форма.Элементы;

	Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда
		ТекущиеДанные = Элементы[ИмяТаблицы].ТекущиеДанные;
		СтавкаНДС = ТекущиеДанные.СтавкаНДС;
	КонецЕсли;
	
	Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС") Тогда
		Форма.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.БезНДС");
	Иначе
		Форма.СпособЗаполненияСтавкиНДС = ПредопределенноеЗначение("Перечисление.СпособыЗаполненияСтавкиНДС.ИзКарточкиНоменклатуры");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗначенияЗаполненияПоПараметрамВыбора(ПараметрыВыбора)
	
	ЗначенияЗаполнения = Новый Структура;
	
	Для каждого ПараметрВыбора Из ПараметрыВыбора Цикл
		Если СтрНайти(ПараметрВыбора.Имя, "Отбор.") <> 0 Тогда
			ЗначенияЗаполнения.Вставить(СтрЗаменить(ПараметрВыбора.Имя, "Отбор.", ""), ПараметрВыбора.Значение);
		ИначеЕсли СтрНайти(ПараметрВыбора.Имя, ".") = 0 Тогда
			ЗначенияЗаполнения.Вставить(ПараметрВыбора.Имя, ПараметрВыбора.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗначенияЗаполнения;
	
КонецФункции

#КонецОбласти

