// Выполняет проверку модифицированности даты, организации и подразделения документа-источника.
// Если дата не входит в предыдущий период или изменены организации или подразделение 
// так, что у них изменился префикс, то номер документа очищается.
// Это необходимо для назначения нового номера документу
//
// Параметры:
//  Источник - ДокументОбъект - источник события подписки
//  Отказ    - Булево - флаг отказа
// 
Процедура ПроверитьНомерДокументаПоДатеОрганизацииПодразделению(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	ИначеЕсли Источник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИмяОрганизации   = ПрефиксацияОбъектовСобытия.ИмяРеквизитаОрганизация(Источник.Метаданные());
	ИмяПодразделения = ИмяРеквизитаПодразделение(Источник.Ссылка);
	
	СтарыеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка, 
		"Дата" + "," + ИмяОрганизации + "," + ИмяПодразделения);
		
	СтарыйПрефикс = ПрефиксОрганизацииПодразделения(СтарыеРеквизиты[ИмяОрганизации], СтарыеРеквизиты[ИмяПодразделения]);
	НовыйПрефикс  = ПрефиксОрганизацииПодразделения(Источник[ИмяОрганизации], Источник[ИмяПодразделения]);
		
	Если НовыйПрефикс <> СтарыйПрефикс 
		ИЛИ НЕ ПрефиксацияОбъектовСлужебный.ДатыОбъектаОдногоПериода(
			СтарыеРеквизиты.Дата, 
			Источник.Дата, 
			Источник.Ссылка) Тогда
			
		Источник.Номер = "";
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписиПрефиксУзлаРаспределеннойИнформационнойБазы(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.ПрефиксыИнформационныхБаз.ЗаполнитьПрефиксТекущейИБ();
	
КонецПроцедуры

// Устанавливает префикс источника в соответствии с префиксом информационной базы и префиксом организации или подразделения.
//
// Параметры:
//  Источник - Источник события подписки.
//             Любой объект из множества [Справочник, Документ, План видов характеристик, Бизнес процесс, Задача]
// СтандартнаяОбработка - Булево - флаг стандартной обработки подписки
// Префикс - Строка - префикс объекта, который нужно изменить
//
Процедура УстановитьПрефиксИнформационнойБазыОрганизацииПодразделения(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	ИмяОрганизации   = ПрефиксацияОбъектовСобытия.ИмяРеквизитаОрганизация(Источник.Метаданные());
	ИмяПодразделения = ИмяРеквизитаПодразделение(Источник.Ссылка);
	
	Префикс = ПрефиксИнформационнойБазыОрганизацииПодразделения(Источник[ИмяОрганизации], Источник[ИмяПодразделения], Префикс);
	
КонецПроцедуры

Функция ПрефиксИнформационнойБазыОрганизацииПодразделения(Знач Организация, Знач Подразделение, Знач Префикс) Экспорт
	
	ПрефиксИБ = ОбменДаннымиСервер.ПрефиксИнформационнойБазы();
	ПрефиксИБ = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ПрефиксИБ, 2, "0", "Слева");
	
	ПрефиксОП = ПрефиксОрганизацииПодразделения(Организация, Подразделение);
	ПрефиксОП = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ПрефиксОП, 2, "0", "Слева");
	
	Возврат ПрефиксОП + ПрефиксИБ + "-" + Префикс;
	
КонецФункции

Функция ПрефиксОрганизацииПодразделения(Организация, Подразделение)
	
	Префикс = "";
	Если ЗначениеЗаполнено(Подразделение) Тогда
		УстановитьПривилегированныйРежим(Истина);
		Префикс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Подразделение, "Префикс");
	КонецЕсли;
	Если ПустаяСтрока(Префикс)
		И ЗначениеЗаполнено(Организация) Тогда
		Префикс = ПолучитьФункциональнуюОпцию("ПрефиксыОрганизаций", Новый Структура("Организация", Организация));
	КонецЕсли;
	
	Возврат Префикс;

КонецФункции

Функция ИмяРеквизитаПодразделение(Ссылка)

	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		Возврат "ПодразделениеОтправитель";
	КонецЕсли;

	Возврат "ПодразделениеОрганизации";
	
КонецФункции
