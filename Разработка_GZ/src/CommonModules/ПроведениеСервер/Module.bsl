#Область ОбщиеПроцедурыИФункции

// Возвращает Истина, если проведение документа выполняется в режиме группового перепроведения.
//
// Параметры:
//	Объект - ДокументОбъект - документ, для которого необходимо вернуть режим.
//
// Возвращаемое значение:
//	Булево
Функция ГрупповоеПерепроведение(Объект) Экспорт

	Перем Результат;
	
	Если Объект.ДополнительныеСвойства.Свойство("ГрупповоеПерепроведение", Результат) Тогда
		Если Результат <> Ложь Тогда
			Результат = Истина;
		КонецЕсли;
	Иначе
		Результат = Ложь;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Функция формирует массив имен регистров, по которым документ имеет движения.
// Вызывается при подготовке записей к регистрации движений.
//
// Параметры:
//  Регистратор  - ДокументСсылка - документ, движения которого анализируются.
//  Движения     - КоллекцияЗначенийСвойстваОбъектаМетаданных - список регистров, для которых документ-регистратор.
//  ИсключаемыеРегистры - Структура, Массив - список имен регистров, которые не требуется проверять.
//
// Возвращаемое значение:
//   Массив      - список имен регистров, имеющих хотя бы одно движение.
//
Функция ПолучитьМассивИспользуемыхРегистров(Регистратор, Движения, ИсключаемыеРегистры = Неопределено) Экспорт

	Если ЗначениеЗаполнено(ИсключаемыеРегистры)
	   И ТипЗнч(ИсключаемыеРегистры) <> Тип("Структура") Тогда
	   
		ИсключаемыеРегистрыСтруктура = Новый Структура;
		Для каждого ИмяРегистра Из ИсключаемыеРегистры Цикл
			ИсключаемыеРегистрыСтруктура.Вставить(ИмяРегистра);
		КонецЦикла;

	Иначе
		
		ИсключаемыеРегистрыСтруктура = ИсключаемыеРегистры;
		
	КонецЕсли;
	
	РегистрыТребующиеОчистки = НовыеРегистрыТребующиеОчистки();
	РегистрыТребующиеОчистки.Регистратор = Регистратор;
	РегистрыТребующиеОчистки.ПроверяемыеРегистры = ИменаПроверяемыхРегистров(Движения, , ИсключаемыеРегистрыСтруктура);
	
	ПроверитьНаличиеДвижений(РегистрыТребующиеОчистки);
	
	Результат = РегистрыТребующиеОчистки.ПроверяемыеРегистры.ВыгрузитьКолонку("ИмяРегистра");
	
	Возврат Результат;

КонецФункции

// Для организации за период получает списки регистров, по которым нет ни одного движения и по которым есть хоть одно движение.
//
// Параметры:
//  Организация  - СправочникСсылка.Организации - анализ по данной организации.
//  ДатаНачала   - Дата - начало периода анализа.
//  ДатаОкончания - Дата - конец периода анализа.
//
// Возвращаемое значение:
//   Структура   - ключ = имя регистра (или предопределённое ПоОрганизации),
//                 значение = см. НовыеРегистрыТребующиеОчистки().
//
Функция РегистрыТребующиеОчисткиДляОрганизации(Организация, ДатаНачала, ДатаОкончания) Экспорт
	
	// Инициализируем структуру хранения информации для алгоритма.
	РегистрыТребующиеОчистки = НовыеРегистрыТребующиеОчистки();
	РегистрыТребующиеОчистки.Организация   = Организация;
	РегистрыТребующиеОчистки.ДатаНачала    = ДатаНачала;
	РегистрыТребующиеОчистки.ДатаОкончания = ДатаОкончания;

	ИменаПроверяемыхРегистров = РегистрыТребующиеОчистки.ПроверяемыеРегистры;
	Для каждого ПроверяемыйРегистр Из Метаданные.РегистрыНакопления Цикл
		
		Если Лев(ПроверяемыйРегистр.Имя, 7) = "Удалить" Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйПроверяемыйРегистр = ИменаПроверяемыхРегистров.Добавить();
		НовыйПроверяемыйРегистр.ВидРегистра = "Накопления";
		НовыйПроверяемыйРегистр.ИмяРегистра = ПроверяемыйРегистр.Имя;
		НовыйПроверяемыйРегистр.ЕстьОрганизация = (ПроверяемыйРегистр.Измерения.Найти("Организация") <> Неопределено
			Или ПроверяемыйРегистр.Реквизиты.Найти("Организация") <> Неопределено);
		НовыйПроверяемыйРегистр.ЕстьПериод = Истина;
			
	КонецЦикла;
	РежимПодчинениеРегистратору = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору;
	НепериодическийРегистр = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический;
	Для каждого ПроверяемыйРегистр Из Метаданные.РегистрыСведений Цикл
		
		Если ПроверяемыйРегистр.РежимЗаписи <> РежимПодчинениеРегистратору Тогда
			Продолжить;
		КонецЕсли;
		
		Если Лев(ПроверяемыйРегистр.Имя, 7) = "Удалить" Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйПроверяемыйРегистр = ИменаПроверяемыхРегистров.Добавить();
		НовыйПроверяемыйРегистр.ВидРегистра = "Сведений";
		НовыйПроверяемыйРегистр.ИмяРегистра = ПроверяемыйРегистр.Имя;
		НовыйПроверяемыйРегистр.ЕстьОрганизация = (ПроверяемыйРегистр.Измерения.Найти("Организация") <> Неопределено
			Или ПроверяемыйРегистр.Реквизиты.Найти("Организация") <> Неопределено
			Или ПроверяемыйРегистр.Ресурсы.Найти("Организация") <> Неопределено);
		НовыйПроверяемыйРегистр.ЕстьПериод = (ПроверяемыйРегистр.ПериодичностьРегистраСведений <> НепериодическийРегистр);
			
	КонецЦикла;
	Для каждого ПроверяемыйРегистр Из Метаданные.РегистрыБухгалтерии Цикл
		
		Если Лев(ПроверяемыйРегистр.Имя, 7) = "Удалить" Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйПроверяемыйРегистр = ИменаПроверяемыхРегистров.Добавить();
		НовыйПроверяемыйРегистр.ВидРегистра = "Бухгалтерии";
		НовыйПроверяемыйРегистр.ИмяРегистра = ПроверяемыйРегистр.Имя;
		НовыйПроверяемыйРегистр.ЕстьОрганизация = (ПроверяемыйРегистр.Измерения.Найти("Организация") <> Неопределено
			Или ПроверяемыйРегистр.Реквизиты.Найти("Организация") <> Неопределено);
		НовыйПроверяемыйРегистр.ЕстьПериод = Истина;
			
	КонецЦикла;
	ИменаПроверяемыхРегистров.Индексы.Добавить("ИмяРегистра");
	
	ПроверитьНаличиеДвижений(РегистрыТребующиеОчистки);
	
	РегистрыТребующиеОчисткиПоДокументам = Новый Структура("ПоОрганизации", РегистрыТребующиеОчистки);
	// В эту структуру будут добавлены элементы, где Ключ = имя регистра, а Значение = кеш информации по данному типу.
	
	Возврат РегистрыТребующиеОчисткиПоДокументам;
	
КонецФункции

// Процедура выполняет подготовку наборов записей документа к проведению документа.
// В стандартной ситуации непосредственная проверка наличия ранее сделанных движений по регистрам выполняется позже
// в процедуре УстановитьЗаписьОчищаемыхНаборовЗаписей().
// Существуют следующие нестандартные ситуации:
// 1. Очищает наборы записей от "старых записей" (ситуация возможна только в толстом клиенте).
// 2. Явно передан флаг ВыборочноОчищатьРегистры = Ложь (регистры будут очищены до формирования движений).
// 3. Устанавливает активность наборам записей документов с установленным флагом ручной корректировки.
// 4. Записывает пустые наборы, если дата ранее проведенного документа была сдвинута вперед.
// Вызывается из модуля документа в начале процедуры ОбработкаПроведения().
//
Процедура ПодготовитьНаборыЗаписейКПроведению(Объект, ВыборочноОчищатьРегистры = Истина) Экспорт
	
	// При перепроведении документа очистка предыдущих движений должна выполняться явно, установкой флага Записывать у
	// наборов записей. Чтобы не устанавливать этот флаг всем наборам, необходимо определить, какие из них содержат
	// движения. При интерактивном перепроведении отдельного документа в доп.свойстве РегистрыТребующиеОчисткиПоДокументам
	// сохраняется список наборов записей, которые планируется проверить. При групповом перепроведении это доп.свойство
	// передается из обработки ГрупповоеПерепроведениеДокументов и может уточняться (сужаться список).
	// Далее в процедуре УстановитьЗаписьОчищаемыхНаборовЗаписей мы применим дополнительный критерий для сужения списка и
	// сделаем запросы, анализирующие наличие движений по регистрам. Варианты критериев см. в комментарии ниже.
	// Может быть ситуация, когда очистка движений происходит в текущей процедуре (перед формированием новых движений),
	// а не в УстановитьЗаписьОчищаемыхНаборовЗаписей (после формирования). Варианты нестандартных ситуаций см. ниже.
	
	Перем РегистрыТребующиеОчистки;
	
	// С целью оптимизации производительности запоминаем значение свойства в переменной.
	ПропуститьПроверкуЗапретаИзменения = Объект.ДополнительныеСвойства.Свойство("ПропуститьПроверкуЗапретаИзменения");
	
	Если Объект.ДополнительныеСвойства.ЭтоНовый Тогда // очистка регистров в информационной базе не нужна
		
		Для каждого НаборЗаписей Из Объект.Движения Цикл
			Если НаборЗаписей.Количество() > 0 Тогда
				НаборЗаписей.Очистить();
			КонецЕсли;
			Если ПропуститьПроверкуЗапретаИзменения Тогда
				НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);
			КонецЕсли;
		КонецЦикла;
		Возврат;
		
	КонецЕсли;
	
	МетаданныеОбъекта = Объект.Метаданные();
	РучнаяКорректировка = ОбщегоНазначения.ЕстьРеквизитОбъекта("РучнаяКорректировка", МетаданныеОбъекта)
						И Объект.РучнаяКорректировка;
						
	// Варианты нестандартных ситуаций:
	// * Не ВыборочноОчищатьРегистры = нужно очистить все регистры до проведения (например, для зарплатных документов).
	// * РучнаяКорректировка = нужно лишь установить активность у наборов записей, т.к. они вводились пользователем.
	// * ДатаДокументаСдвинутаВперед = нужно записать все наборы, у которых сейчас "Записывать = Истина".
	СтандартнаяОчисткаРегистров = ВыборочноОчищатьРегистры 
									И Не РучнаяКорректировка
									И Не Объект.ДополнительныеСвойства.ДатаДокументаСдвинутаВперед;
	
	// Ведем список имен регистров, наличие движений по которым не нужно проверять, для случаев:
	// 1) движения содержались в памяти и были очищены, поэтому Записывать = Истина;
	// 2) после формирования движений документа заполненные наборы записей всегда будут перезаписаны;
	// 3) при запуске обработки "ГрупповоеПерепроведение" определили, что по отдельным регистрам за весь период
	//    нет ни одного движения. Для конкретного документа их проверять не нужно.
	// В данной процедуре список дополняется по варианту 1.
	Если Объект.ДополнительныеСвойства.Свойство("РегистрыТребующиеОчисткиПоДокументам") Тогда // групповое перепроведение
		
		РегистрыТребующиеОчистки = РегистрыТребующиеОчисткиДляТипа(
			Объект.ДополнительныеСвойства.РегистрыТребующиеОчисткиПоДокументам, МетаданныеОбъекта);
			
	Иначе // перепроведение отдельного документа
			
		РегистрыТребующиеОчистки = НовыеРегистрыТребующиеОчистки();
		Если СтандартнаяОчисткаРегистров Тогда
			// Запоминаем те регистры, которые нужно будет проверять.
			РегистрыТребующиеОчистки.ПроверяемыеРегистры = ИменаПроверяемыхРегистров(МетаданныеОбъекта.Движения);
		КонецЕсли;
		
	КонецЕсли;
	
	РегистрыНеТребующиеОчистки = Новый Структура;
	Для каждого НаборЗаписей Из Объект.Движения Цикл
		
		Если НаборЗаписей.Количество() > 0 Тогда
			
			НаборЗаписей.Очистить();
			
			Если СтандартнаяОчисткаРегистров Тогда
				// Этот набор записей будет записан по итогам проведения. Проверять его заполненность не нужно.
				РегистрыНеТребующиеОчистки.Вставить(НаборЗаписей.Метаданные().Имя);
			КонецЕсли;
			
		КонецЕсли;
		Если ПропуститьПроверкуЗапретаИзменения Тогда
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);
		КонецЕсли;
		
	КонецЦикла;

	Если СтандартнаяОчисткаРегистров Тогда
		// Проверка наличия ранее записанных движений в регистрах и их очистка
		// будут происходить в процедуре УстановитьЗаписьОчищаемыхНаборовЗаписей().
		Объект.ДополнительныеСвойства.Вставить("РегистрыТребующиеОчистки", РегистрыТребующиеОчистки);
		
		ПодготовитьНаборыЗаписейСтандартно(Объект, РегистрыНеТребующиеОчистки);
		
	Иначе
		
		Если РегистрыТребующиеОчистки.ПроверяемыеРегистры.Количество() = 0 Тогда // заполняем
			РегистрыТребующиеОчистки.ПроверяемыеРегистры = ИменаПроверяемыхРегистров(
				МетаданныеОбъекта.Движения, , РегистрыНеТребующиеОчистки);
		Иначе // используем имеющийся список при групповом перепроведении
			УдалитьНепроверяемыеРегистры(РегистрыТребующиеОчистки.ПроверяемыеРегистры, РегистрыНеТребующиеОчистки);
		КонецЕсли;
		
		ПараметрыНестандартнойСитуации = Новый Структура;
		ПараметрыНестандартнойСитуации.Вставить("ВыборочноОчищатьРегистры", ВыборочноОчищатьРегистры);
		ПараметрыНестандартнойСитуации.Вставить("РучнаяКорректировка",      РучнаяКорректировка);
		ПараметрыНестандартнойСитуации.Вставить("ДатаДокументаСдвинутаВперед",
			Объект.ДополнительныеСвойства.ДатаДокументаСдвинутаВперед);
		
		ПодготовитьНаборыЗаписейВОсобойСитуации(Объект, РегистрыТребующиеОчистки, ПараметрыНестандартнойСитуации);
	   
	КонецЕсли;
	
	// Если у документа установлен признак ручной корректировки, то процедуры изменения движений в последовательности
	// для него не вызываются, т.к. в ОбработкаПроведения() сразу выполняется Возврат после вызова текущей процедуры.
	// Поэтому скорректируем записи последовательности сейчас.
	Если РучнаяКорректировка Тогда
		РаботаСПоследовательностями.ЗарегистрироватьОтложенныеРасчетыВПоследовательности(Объект, Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Взводит флаг записи у наборов, по которым документ имел движения при прошлом проведении.
// Вызывается из модуля документа в конце процедуры ОбработкаПроведения().
//
Процедура УстановитьЗаписьОчищаемыхНаборовЗаписей(Объект) Экспорт 
	
	Перем РегистрыТребующиеОчистки;
	
	// Ведем список имен регистров, наличие движений по которым не нужно проверять, для случаев:
	// 1) движения содержались в памяти и были очищены, поэтому Записывать = Истина;
	// 2) после формирования движений документа заполненные наборы записей всегда будут перезаписаны;
	// 3) при запуске обработки "ГрупповоеПерепроведение" определили, что по отдельным регистрам за весь период
	//    нет ни одного движения. Для конкретного документа их проверять не нужно.
	// В данной процедуре список дополняется по варианту 2 и осуществляется установка "Записывать = Истина" для регистров,
	// по которым ранее были движения, а сейчас пустые наборы.
	
	Если Не Объект.ДополнительныеСвойства.Свойство("РегистрыТребующиеОчистки", РегистрыТребующиеОчистки) Тогда
		// Очистка наборов записей в информационной базе была выполнена в процедуре ПодготовитьНаборыЗаписейКПроведению().
		Возврат;
	КонецЕсли;
	
	РегистрыНеТребующиеОчистки = Новый Структура;
	Для каждого ПроверяемыйРегистр Из РегистрыТребующиеОчистки.ПроверяемыеРегистры Цикл
		
		НаборЗаписей = Объект.Движения[ПроверяемыйРегистр.ИмяРегистра];
		Если НаборЗаписей.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
			
		// Этот набор записей будет записан по итогам проведения. Очищать его старые записи не нужно.
		РегистрыНеТребующиеОчистки.Вставить(ПроверяемыйРегистр.ИмяРегистра);		
		
	КонецЦикла;
	
	УдалитьНепроверяемыеРегистры(РегистрыТребующиеОчистки.ПроверяемыеРегистры, РегистрыНеТребующиеОчистки);
	
	ПроверитьНаличиеДвижений(РегистрыТребующиеОчистки);
	
	Для Каждого ПроверяемыйРегистр Из РегистрыТребующиеОчистки.ПроверяемыеРегистры Цикл
		Объект.Движения[ПроверяемыйРегистр.ИмяРегистра].Записывать = Истина;
	КонецЦикла;

КонецПроцедуры

// Процедура выполняет подготовку наборов записей документа к отмене проведения документа.
// 1. Взводит флаг записи у наборов, по которым документ имел движения при прошлом проведении
// 2. Снимает активность у наборов записей документов с установленным флагом ручной корректировки
// Вызывается из модуля документа при отмене проведения.
//
Процедура ПодготовитьНаборыЗаписейКОтменеПроведения(Объект) Экспорт
	
	МетаданныеОбъекта = Объект.Метаданные();
	
	МассивИменРегистров = ПолучитьМассивИспользуемыхРегистров(
		Объект.Ссылка, 
		МетаданныеОбъекта.Движения);

	ПропуститьПроверкуЗапретаИзменения = Объект.ДополнительныеСвойства.Свойство("ПропуститьПроверкуЗапретаИзменения");

	Для каждого ИмяРегистра Из МассивИменРегистров Цикл
		НаборЗаписей = Объект.Движения[ИмяРегистра];
		НаборЗаписей.Записывать = Истина;
		Если ПропуститьПроверкуЗапретаИзменения Тогда
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);
		КонецЕсли;
	КонецЦикла;
	
	РучнаяКорректировка = МетаданныеОбъекта.Реквизиты.Найти("РучнаяКорректировка") <> Неопределено
		И Объект.РучнаяКорректировка;
	
	Если РучнаяКорректировка Тогда
		Для каждого ИмяРегистра Из МассивИменРегистров Цикл
			Объект.Движения[ИмяРегистра].Прочитать();
			Объект.Движения[ИмяРегистра].УстановитьАктивность(Ложь);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Создает структуру для хранения информации, необходимой алгоритму очистки движений.
//
Функция НовыеРегистрыТребующиеОчистки()
	
	РегистрыТребующиеОчистки = Новый Структура;
	
	// Хранит список регистров, которые можно проанализировать на наличие записанных движений.
	РегистрыТребующиеОчистки.Вставить("ПроверяемыеРегистры", НовыеИменаРегистровДляПроверки());
	
	РегистрыТребующиеОчистки.Вставить("ДатаНачала",    '00010101');
	РегистрыТребующиеОчистки.Вставить("ДатаОкончания", '00010101');
	РегистрыТребующиеОчистки.Вставить("Организация",   Справочники.Организации.ПустаяСсылка());
	РегистрыТребующиеОчистки.Вставить("ИмяДокумента",  "");
	РегистрыТребующиеОчистки.Вставить("Регистратор",   Неопределено);
	
	Возврат РегистрыТребующиеОчистки;
	
КонецФункции

// Создает таблицу для хранения информации, необходимой алгоритму очистки движений.
//
Функция НовыеИменаРегистровДляПроверки()
	
	ПроверяемыеРегистры = Новый ТаблицаЗначений;
	
	// Бухгалтерии, Сведений, Накопления
	ПроверяемыеРегистры.Колонки.Добавить("ВидРегистра",     ОбщегоНазначения.ОписаниеТипаСтрока(11));
	ПроверяемыеРегистры.Колонки.Добавить("ИмяРегистра",     Новый ОписаниеТипов("Строка"));
	ПроверяемыеРегистры.Колонки.Добавить("ЕстьОрганизация", Новый ОписаниеТипов("Булево"));
	ПроверяемыеРегистры.Колонки.Добавить("ЕстьПериод",      Новый ОписаниеТипов("Булево"));
	
	Возврат ПроверяемыеРегистры;
	
КонецФункции

// Для документов определённого типа по организации за период получает списки регистров, по которым нет ни одного
// движения и по которым есть хоть одно движение.
//
// Параметры:
//  РегистрыТребующиеОчисткиПоДокументам - Структура - см. РегистрыТребующиеОчисткиДляОрганизации().
//  МетаданныеДокумента - ОбъектМетаданных:Документ - описание документа.
//
// Возвращаемое значение:
//   Структура   - см. НовыеРегистрыТребующиеОчистки().
//
Функция РегистрыТребующиеОчисткиДляТипа(РегистрыТребующиеОчисткиПоДокументам, МетаданныеДокумента)
	
	Перем РегистрыПоТипуДокументов;
	
	// Добавляем регистры, движений которых нет ни для одного из документов данного типа.
	Если Не РегистрыТребующиеОчисткиПоДокументам.Свойство(МетаданныеДокумента.Имя, РегистрыПоТипуДокументов) Тогда
		
		РегистрыТребующиеОчистки = РегистрыТребующиеОчисткиПоДокументам.ПоОрганизации;
		
		РегистрыПоТипуДокументов = НовыеРегистрыТребующиеОчистки();
		РегистрыПоТипуДокументов.ДатаНачала        = РегистрыТребующиеОчистки.ДатаНачала;
		РегистрыПоТипуДокументов.ДатаОкончания     = РегистрыТребующиеОчистки.ДатаОкончания;
		РегистрыПоТипуДокументов.Организация       = РегистрыТребующиеОчистки.Организация;
		РегистрыПоТипуДокументов.ИмяДокумента      = МетаданныеДокумента.Имя;
		РегистрыПоТипуДокументов.ПроверяемыеРегистры  = ИменаПроверяемыхРегистров(
			МетаданныеДокумента.Движения, РегистрыТребующиеОчистки.ПроверяемыеРегистры);

		ПроверитьНаличиеДвижений(РегистрыПоТипуДокументов);
		
		РегистрыТребующиеОчисткиПоДокументам.Вставить(МетаданныеДокумента.Имя, РегистрыПоТипуДокументов);
			
	КонецЕсли;
	
	// Копируем имеющуюся информацию.
	РегистрыТребующиеОчистки = НовыеРегистрыТребующиеОчистки();
	РегистрыТребующиеОчистки.ПроверяемыеРегистры = РегистрыПоТипуДокументов.ПроверяемыеРегистры.Скопировать();
		
	Возврат РегистрыТребующиеОчистки;
	
КонецФункции

// Список регистров, которые требуется очистить перед началом перепроведения.
//
// Параметры:
//  ИсключаемыеРегистры - Структура - содержит имена регистров, которые не нужно включать в список.
//
// Возвращаемое значение:
//   ТаблицаЗначений - см. ИменаДляПроверки().
//
Функция РегистрыТребующиеПринудительнойОчистки(ПроверяемыеРегистры)
	
	ИменаРегистровПринудительнойОчистки = НовыеИменаРегистровДляПроверки();
	Если ПроверяемыеРегистры.Найти("РасходыПриУСН", "ИмяРегистра") <> Неопределено Тогда
		НовыйПроверяемыйРегистр = ИменаРегистровПринудительнойОчистки.Добавить();
		НовыйПроверяемыйРегистр.ВидРегистра     = "Накопления";
		НовыйПроверяемыйРегистр.ИмяРегистра     = "РасходыПриУСН";
		НовыйПроверяемыйРегистр.ЕстьОрганизация = Истина;
		НовыйПроверяемыйРегистр.ЕстьПериод      = Истина;
	КонецЕсли;
	
	Возврат ИменаРегистровПринудительнойОчистки;
	
КонецФункции

// Только составляем список имен регистров. Непосредственная проверка наличия ранее записанных движений
// будет происходить в процедуре УстановитьЗаписьОчищаемыхНаборовЗаписей().
//
Процедура ПодготовитьНаборыЗаписейСтандартно(Объект, РегистрыНеТребующиеОчистки)
	
	РегистрыТребующиеОчистки = Объект.ДополнительныеСвойства.РегистрыТребующиеОчистки;
	
	РегистрыТребующиеОчистки.Регистратор = Объект.Ссылка;
	
	// В дальнейшем (после формирования движений) в любом случае не проверять.
	ИменаРегистровПринудительнойОчистки = РегистрыТребующиеПринудительнойОчистки(
		РегистрыТребующиеОчистки.ПроверяемыеРегистры);
	Для каждого ПроверяемыйРегистр Из ИменаРегистровПринудительнойОчистки Цикл	
		РегистрыНеТребующиеОчистки.Вставить(ПроверяемыйРегистр.ИмяРегистра);
	КонецЦикла;
	УдалитьНепроверяемыеРегистры(РегистрыТребующиеОчистки.ПроверяемыеРегистры, РегистрыНеТребующиеОчистки);
		
	ПринудительноОчищаемыеРегистры = НовыеРегистрыТребующиеОчистки();
	ПринудительноОчищаемыеРегистры.ПроверяемыеРегистры = ИменаРегистровПринудительнойОчистки;
	ПринудительноОчищаемыеРегистры.Регистратор         = Объект.Ссылка;
	ПроверитьНаличиеДвижений(ПринудительноОчищаемыеРегистры);
	// Если в списке остались регистры, прошедшие проверку, то нужно очистить их сейчас (до начала формирования движений).
	Для каждого ПроверяемыйРегистр Из ПринудительноОчищаемыеРегистры.ПроверяемыеРегистры Цикл
		
		НаборЗаписей = Объект.Движения[ПроверяемыйРегистр.ИмяРегистра];
		НаборЗаписей.Записать();
		НаборЗаписей.Записывать = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

// Проверка наличия ранее записанных движений в регистрах и их очистка будут происходить согласно логике
// нестандартной ситуации.
//
Процедура ПодготовитьНаборыЗаписейВОсобойСитуации(Объект, РегистрыТребующиеОчистки, ПараметрыНестандартнойСитуации)
	
	РегистрыТребующиеОчистки.Регистратор = Объект.Ссылка;
	
	ПроверитьНаличиеДвижений(РегистрыТребующиеОчистки);
	
	ПроверяемыеРегистры = РегистрыТребующиеОчистки.ПроверяемыеРегистры;
	
	МассивДвиженийДляПринудительнойОчистки = Новый Массив;
	Если ПараметрыНестандартнойСитуации.ВыборочноОчищатьРегистры Тогда
		
		ИменаРегистровПринудительнойОчистки = РегистрыТребующиеПринудительнойОчистки(
			РегистрыТребующиеОчистки.ПроверяемыеРегистры);
		
		Для каждого ПроверяемыйРегистр Из ПроверяемыеРегистры Цикл
			Объект.Движения[ПроверяемыйРегистр.ИмяРегистра].Записывать = Истина;
			Если ИменаРегистровПринудительнойОчистки.Найти(ПроверяемыйРегистр.ИмяРегистра, "ИмяРегистра") <> Неопределено Тогда
				МассивДвиженийДляПринудительнойОчистки.Добавить(Объект.Движения[ПроверяемыйРегистр.ИмяРегистра]);
			КонецЕсли; 
		КонецЦикла;
		
	Иначе // очищаем все
		
		Для каждого ПроверяемыйРегистр Из ПроверяемыеРегистры Цикл
			Объект.Движения[ПроверяемыйРегистр.ИмяРегистра].Записывать = Истина;
			МассивДвиженийДляПринудительнойОчистки.Добавить(Объект.Движения[ПроверяемыйРегистр.ИмяРегистра]);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПараметрыНестандартнойСитуации.РучнаяКорректировка Тогда
		
		Для каждого ПроверяемыйРегистр Из ПроверяемыеРегистры Цикл
			Объект.Движения[ПроверяемыйРегистр.ИмяРегистра].Прочитать();
			Объект.Движения[ПроверяемыйРегистр.ИмяРегистра].УстановитьАктивность(Истина);
		КонецЦикла;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Движения документа %1 отредактированы вручную и не могут быть автоматически актуализированы'"), Объект);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект.Ссылка);
		
	ИначеЕсли НЕ ПараметрыНестандартнойСитуации.ДатаДокументаСдвинутаВперед Тогда
		
		Для каждого НаборЗаписей Из МассивДвиженийДляПринудительнойОчистки Цикл
			НаборЗаписей.Записать();
			НаборЗаписей.Записывать = Ложь;
		КонецЦикла; 
		
	КонецЕсли;
	
	Если ПараметрыНестандартнойСитуации.ДатаДокументаСдвинутаВперед Тогда
		Объект.Движения.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Анализирует наличие движений по регистрам. Удаляет регистры без движений из списка в РегистрыТребующиеОчистки.
//
// Параметры:
//  РегистрыТребующиеОчистки - см. НовыеРегистрыТребующиеОчистки().
//
Процедура ПроверитьНаличиеДвижений(РегистрыТребующиеОчистки)
	
	// Отсюда будем удалять пустые регистры.
	ИменаПроверяемыхРегистров = РегистрыТребующиеОчистки.ПроверяемыеРегистры;
	Если ИменаПроверяемыхРегистров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	// Сюда будем переносить найденные пустые регистры.
	РегистрыНеТребующиеОчистки = Новый Структура;
	
	Запрос = Новый Запрос;
	Если ЗначениеЗаполнено(РегистрыТребующиеОчистки.Регистратор) Тогда
		
		ВариантОтбора = "Документ";
		
		Запрос.УстановитьПараметр("Регистратор", РегистрыТребующиеОчистки.Регистратор);
		
	Иначе
		
		ВариантОтбора = ?(ПустаяСтрока(РегистрыТребующиеОчистки.ИмяДокумента), "Организация", "Тип");
		
		Запрос.УстановитьПараметр("Организация",   РегистрыТребующиеОчистки.Организация);
		Запрос.УстановитьПараметр("ДатаНачала",    РегистрыТребующиеОчистки.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", РегистрыТребующиеОчистки.ДатаОкончания);
		
	КонецЕсли;
	
	ШаблонПроверяющегоЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	""Хозрасчетный"" КАК ИмяРегистра
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	&ОтборПоРегистру";
	
	// Порциями делаем запросы к информационной базе за период.
	МаксимумПодзапросовВПорции = 50;
	ИменаРегистровТекущейПорции = Новый Массив;
	ОстатокПроверок = МаксимумПодзапросовВПорции + 1; // инициализируем значение для входа в алгоритм
	ТекстПроверяющегоЗапроса = "";
	Для каждого ПроверяемыйРегистр Из ИменаПроверяемыхРегистров Цикл
		
		ОстатокПроверок = ОстатокПроверок - 1;
		Если ОстатокПроверок = 0 Тогда
			
			Если Не ПустаяСтрока(ТекстПроверяющегоЗапроса) Тогда
				// Выполняем проверку, используя собранный запрос.
				Запрос.Текст = ТекстПроверяющегоЗапроса;
				РегистрыСДвижениями = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяРегистра");
				РегистрыБезДвижений = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ИменаРегистровТекущейПорции, РегистрыСДвижениями);
				Для каждого ИмяРегистра Из РегистрыБезДвижений Цикл
					РегистрыНеТребующиеОчистки.Вставить(ИмяРегистра);
				КонецЦикла;
				
				ТекстПроверяющегоЗапроса = "";
				ИменаРегистровТекущейПорции.Очистить();
				
			КонецЕсли;
			
			// Инициализируем значением, которое, с одной стороны, скорее всего превышает количество регистров, где данный
			// документ является регистратором, с другой стороны, позволяет при необходимости большой список регистров
			// анализировать порциями.
			ОстатокПроверок = МаксимумПодзапросовВПорции;
			
		КонецЕсли;
		
		ИменаРегистровТекущейПорции.Добавить(ПроверяемыйРегистр.ИмяРегистра);
		
		ТекстПодзапроса = ШаблонПроверяющегоЗапроса;
		Если ОстатокПроверок < МаксимумПодзапросовВПорции Тогда // убираем имя поля из всех подзапросов кроме первого
			ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, " КАК ИмяРегистра", "");
		КонецЕсли;
		Если ВариантОтбора = "Документ" Тогда // конкретный документ
			
			ОтборПоРегистру = "Хозрасчетный.Регистратор = &Регистратор";
			
		Иначе // за период по организации
			
			ОтборПоРегистру = "";
			
			Если ПроверяемыйРегистр.ЕстьПериод Тогда
				
				ОтборПоРегистру = "Хозрасчетный.Период МЕЖДУ &ДатаНачала И &ДатаОкончания";
				
			КонецЕсли;
			
			Если ВариантОтбора = "Тип" Тогда
				
				Если Не ПустаяСтрока(ОтборПоРегистру) Тогда
					ОтборПоРегистру = ОтборПоРегистру + Символы.ПС + Символы.Таб + "И ";
				КонецЕсли;
				ОтборПоРегистру = ОтборПоРегистру + "Хозрасчетный.Регистратор ССЫЛКА Документ."
					+ РегистрыТребующиеОчистки.ИмяДокумента;
				
			КонецЕсли;
			
			Если ПроверяемыйРегистр.ЕстьОрганизация Тогда
				
				Если Не ПустаяСтрока(ОтборПоРегистру) Тогда
					ОтборПоРегистру = ОтборПоРегистру + Символы.ПС + Символы.Таб + "И ";
				КонецЕсли;
				ОтборПоРегистру = ОтборПоРегистру + "Хозрасчетный.Организация = &Организация";
				
			КонецЕсли;
			
			Если ПустаяСтрока(ОтборПоРегистру) Тогда
				ОтборПоРегистру = "ИСТИНА";
			КонецЕсли;
				
		КонецЕсли; 
		ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&ОтборПоРегистру", ОтборПоРегистру);
		ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "Бухгалтерии", ПроверяемыйРегистр.ВидРегистра);
		ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "Хозрасчетный", ПроверяемыйРегистр.ИмяРегистра);
		
		ТекстПроверяющегоЗапроса = ?(ПустаяСтрока(ТекстПроверяющегоЗапроса), "", ТекстПроверяющегоЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|") + ТекстПодзапроса;
	
	КонецЦикла;
	Если Не ПустаяСтрока(ТекстПроверяющегоЗапроса) Тогда
		// Выполняем проверку, используя собранный запрос.
		Запрос.Текст = ТекстПроверяющегоЗапроса;
		РегистрыСДвижениями = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяРегистра");
		РегистрыБезДвижений = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ИменаРегистровТекущейПорции, РегистрыСДвижениями);
		Для каждого ИмяРегистра Из РегистрыБезДвижений Цикл
			РегистрыНеТребующиеОчистки.Вставить(ИмяРегистра);
		КонецЦикла;
		
	КонецЕсли;
	
	// Удалим из общего списка те регистры, чья пустота подтверждена.
	УдалитьНепроверяемыеРегистры(ИменаПроверяемыхРегистров, РегистрыНеТребующиеОчистки);
	
КонецПроцедуры

// Удаляет из списка УменьшаемыйНабор те, чьи имена есть в ВычитаемыйНабор.
//
// Параметры:
//  УменьшаемыйНабор - ТаблицаЗначений - см. НовыеИменаРегистровДляПроверки().
//  ВычитаемыйНабор - Структура - вычитаемый список.
//
Процедура УдалитьНепроверяемыеРегистры(УменьшаемыйНабор, ВычитаемыйНабор)
	
	Если ВычитаемыйНабор.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НомерСтрокиПроверки = 0;
	КоличествоПроверяемыхСтрок = УменьшаемыйНабор.Количество();
	Пока НомерСтрокиПроверки < КоличествоПроверяемыхСтрок Цикл
		Если ВычитаемыйНабор.Свойство(УменьшаемыйНабор[НомерСтрокиПроверки].ИмяРегистра) Тогда
			УменьшаемыйНабор.Удалить(НомерСтрокиПроверки);
			КоличествоПроверяемыхСтрок = КоличествоПроверяемыхСтрок - 1;
		Иначе
			НомерСтрокиПроверки = НомерСтрокиПроверки + 1;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Формирует список имен проверяемых регистров, на основе коллекции метаданных движений документа.
//
// Параметры:
//  Движения     - КоллекцияЗначенийСвойстваОбъектаМетаданных - список регистров, для которых документ-регистратор.
//  ПроверяемыеРегистры - ТаблицаЗначений - какие регистры можно использовать. Если не указано, то можно все.
//                                       См. НовыеИменаРегистровДляПроверки().
//  ИсключаемыеРегистры - Структура - какие регистры нельзя использовать. Если не указано, то можно все.
//
// Возвращаемое значение:
//   ТаблицаЗначений - см. НовыеИменаРегистровДляПроверки().
//
Функция ИменаПроверяемыхРегистров(Движения, ПроверяемыеРегистры = Неопределено, ИсключаемыеРегистры = Неопределено)
	
	ИменаПроверяемыхПоТипу = НовыеИменаРегистровДляПроверки();
	
	Если ПроверяемыеРегистры = Неопределено Тогда // вернуть список имен всех регистров.
		
		Если ИсключаемыеРегистры = Неопределено Тогда
			ИсключаемыеРегистры = Новый Структура;
		КонецЕсли;
		
		НепериодическийРегистр = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический;
	    		
		Для каждого ПроверяемыйРегистр Из Движения Цикл
			
			Если ИсключаемыеРегистры.Свойство(ПроверяемыйРегистр.Имя)
			 Или Лев(ПроверяемыйРегистр.Имя, 7) = "Удалить" Тогда
				Продолжить;
			КонецЕсли;
			
			ПризнакГруппыРегистров = ПроверяемыйРегистр.ПолноеИмя();
			ПризнакГруппыРегистров = Лев(ПризнакГруппыРегистров, СтрНайти(ПризнакГруппыРегистров, ".") - 1);
			ПризнакГруппыРегистров = ВРег(ПризнакГруппыРегистров);
			ЭтоРегистрСведений = (ПризнакГруппыРегистров = "РЕГИСТРСВЕДЕНИЙ" Или ПризнакГруппыРегистров = "INFORMATIONREGISTER");
			
			НовыйПроверяемыйРегистр = ИменаПроверяемыхПоТипу.Добавить();
			Если ЭтоРегистрСведений Тогда
				НовыйПроверяемыйРегистр.ВидРегистра = "Сведений";
			ИначеЕсли ПризнакГруппыРегистров = "РЕГИСТРБУХГАЛТЕРИИ" Или ПризнакГруппыРегистров = "ACCOUNTINGREGISTER" Тогда
				НовыйПроверяемыйРегистр.ВидРегистра = "Бухгалтерии";
			Иначе
				НовыйПроверяемыйРегистр.ВидРегистра = "Накопления";
			КонецЕсли;
			НовыйПроверяемыйРегистр.ИмяРегистра = ПроверяемыйРегистр.Имя;
			НовыйПроверяемыйРегистр.ЕстьОрганизация = (ПроверяемыйРегистр.Измерения.Найти("Организация") <> Неопределено
				Или ПроверяемыйРегистр.Реквизиты.Найти("Организация") <> Неопределено
				Или ЭтоРегистрСведений И ПроверяемыйРегистр.Ресурсы.Найти("Организация") <> Неопределено);
			НовыйПроверяемыйРегистр.ЕстьПериод = (Не ЭтоРегистрСведений
				Или ПроверяемыйРегистр.ПериодичностьРегистраСведений <> НепериодическийРегистр);
			
		КонецЦикла;

	Иначе // вернуть список с ограничением
		
		Для каждого ПроверяемыйРегистр Из Движения Цикл
			
			ИмяПроверяемогоРегистра = ПроверяемыеРегистры.Найти(ПроверяемыйРегистр.Имя, "ИмяРегистра");
			Если ИмяПроверяемогоРегистра = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйПроверяемыйРегистр = ИменаПроверяемыхПоТипу.Добавить();
			НовыйПроверяемыйРегистр.ВидРегистра     = ИмяПроверяемогоРегистра.ВидРегистра;
			НовыйПроверяемыйРегистр.ИмяРегистра     = ИмяПроверяемогоРегистра.ИмяРегистра;
			НовыйПроверяемыйРегистр.ЕстьОрганизация = ИмяПроверяемогоРегистра.ЕстьОрганизация;
			НовыйПроверяемыйРегистр.ЕстьПериод      = ИмяПроверяемогоРегистра.ЕстьПериод;
			
		КонецЦикла;

	КонецЕсли;
	
	Возврат ИменаПроверяемыхПоТипу;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииБЗК

// Очищает записи наборов из коллекции Движения и проставляет флаг Записывать наборам, по которым 
// документ уже имеет движения.
// 
//	Параметры:
//		Объект - документ
//		ЭтоНовый - признак того, что пишется новый документ.
//		ДвиженияМетаданные - свойство метаданных Движения.
//		ОтключитьПроверкуДатыЗапретаИзменения - Булево
//
Процедура ПодготовитьНаборыЗаписейКРегистрацииДвижений(Объект, ЭтоНовый = Ложь, ДвиженияМетаданные = НеОпределено, ОтключитьПроверкуДатыЗапретаИзменения = Ложь) Экспорт
	
	Для Каждого НаборЗаписей Из Объект.Движения Цикл
		
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Очистить();
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЭтоНовый Тогда
		МассивИменРегистров = ПолучитьИспользуемыеРегистры(Объект.Ссылка, Объект.Метаданные().Движения);
		Для Каждого ИмяРегистра Из МассивИменРегистров Цикл
			Объект.Движения[ИмяРегистра].Записывать = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Если ОтключитьПроверкуДатыЗапретаИзменения Тогда
		ОтключитьПроверкуДатыЗапретаИзменения(Объект.Движения, ОтключитьПроверкуДатыЗапретаИзменения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьНаборыЗаписейКУдалениюПроведения(Объект, ОтключитьПроверкуДатыЗапретаИзменения = Ложь) Экспорт
	
	Если ОтключитьПроверкуДатыЗапретаИзменения Тогда
		ОтключитьПроверкуДатыЗапретаИзменения(Объект.Движения, ОтключитьПроверкуДатыЗапретаИзменения);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает набор записей регистра по имени регистра.
//                                                                
// Параметры:
//  Движения - КоллекцияДвижений, Структура - Коллекция движений.
//  Имя      - Строка                       - Имя набора записей, который нужно получить.
// 
// Возвращаемое значение:
//  РегистрСведенийНаборЗаписей, РегистрНакопленияНаборЗаписей, Неопределено - Набор записей регистра, 
//                                                                             Неопределено - если не найден.
//
Функция НайтиНаборЗаписей(Движения, Имя) Экспорт 
	НаборЗаписей = Неопределено;
    Если ТипЗнч(Движения) = Тип("Структура") Тогда
        Движения.Свойство(Имя, НаборЗаписей);
    Иначе 
        НаборЗаписей = Движения.Найти(Имя);
	КонецЕсли; 
	Возврат НаборЗаписей;
КонецФункции

// Проверяет наличие набора записей регистра в коллекции движений.
//                                                                
// Параметры:
//  Движения - КоллекцияДвижений, Структура - Коллекция движений.
//  Имя      - Строка                       - Имя набора записей, наличие которого нужно проверить.
// 
// Возвращаемое значение:
//  Булево - Истина, если в коллекции движений есть указанынй набор записей. 
//
Функция ЕстьНаборЗаписей(Движения, Имя) Экспорт 
	Возврат ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Движения, Имя)
КонецФункции

// Проверяет наличие записей в указанном наборе записей коллекции движений.
//                                                                
// Параметры:
//  Движения - КоллекцияДвижений, Структура - Коллекция движений.
//  Имя      - Строка,                      - Имя набора записей, наличие записей в котором нужно проверить.
//                                            Если не указано, то проверяется наличие записей
//                                            во всех наборах переданной коллекции движений. 
// 
// Возвращаемое значение:
//  Булево - Истина, если в наборе (наборах) записей есть записи. 
//
Функция ЕстьДвижения(Движения, Имя = Неопределено) Экспорт 
	
	ПроверяемыеНаборы = Новый Массив;
	Если ЗначениеЗаполнено(Имя) Тогда
		ПроверяемыеНаборы.Добавить(Имя);
	Иначе
		Для Каждого НаборЗаписей Из Движения Цикл
			ПроверяемыеНаборы.Добавить(НаборЗаписей.Метаданные().Имя);
		КонецЦикла;	
	КонецЕсли;
	
	ЕстьДвижения = Ложь;
	Для Каждого ПроверяемыйНабор Из ПроверяемыеНаборы Цикл
		Если НайтиНаборЗаписей(Движения, ПроверяемыйНабор) <> Неопределено И Движения[ПроверяемыйНабор].Количество() <> 0 Тогда
			ЕстьДвижения = Истина;
			Прервать;
		КонецЕсли	
	КонецЦикла;	
	
	Возврат ЕстьДвижения;
	
КонецФункции

// Функция формирует массив имен регистров, по которым документ имеет движения.
// Вызывается при подготовке записей к регистрации движений.
//
Функция ПолучитьИспользуемыеРегистры(Регистратор, Движения, МассивИсключаемыхРегистров = Неопределено)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);

	Результат = Новый Массив;
	МаксимумТаблицВЗапросе = 256;

	СчетчикТаблиц   = 0;
	СчетчикДвижений = 0;

	ВсегоДвижений = Движения.Количество();
	ТекстЗапроса  = "";
	Для Каждого Движение Из Движения Цикл

		СчетчикДвижений = СчетчикДвижений + 1;

		ПропуститьРегистр = МассивИсключаемыхРегистров <> Неопределено
							И МассивИсключаемыхРегистров.Найти(Движение.Имя) <> Неопределено;

		Если Не ПропуститьРегистр Тогда

			Если СчетчикТаблиц > 0 Тогда

				ТекстЗапроса = ТекстЗапроса + "
				|ОБЪЕДИНИТЬ ВСЕ
				|";

			КонецЕсли;

			СчетчикТаблиц = СчетчикТаблиц + 1;

			ТекстЗапроса = ТекстЗапроса + 
			"
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|""" + Движение.Имя + """ КАК ИмяРегистра
			|
			|ИЗ " + Движение.ПолноеИмя() + "
			|
			|ГДЕ Регистратор = &Регистратор
			|";

		КонецЕсли;

		Если СчетчикТаблиц = МаксимумТаблицВЗапросе Или СчетчикДвижений = ВсегоДвижений Тогда

			Запрос.Текст  = ТекстЗапроса;
			ТекстЗапроса  = "";
			СчетчикТаблиц = 0;

			Если Результат.Количество() = 0 Тогда

				Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяРегистра");

			Иначе

				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					Результат.Добавить(Выборка.ИмяРегистра);
				КонецЦикла;

			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

Процедура ОтключитьПроверкуДатыЗапретаИзменения(КоллекцияДвижений, ОтключитьПроверкуДатыЗапретаИзменения) Экспорт
	
	Если ОтключитьПроверкуДатыЗапретаИзменения Тогда
		
		Для Каждого ЭлементКоллекции Из КоллекцияДвижений Цикл
			
			Если ТипЗнч(ЭлементКоллекции) = Тип("КлючИЗначение") Тогда
				НаборЗаписей = ЭлементКоллекции.Значение;
			Иначе
				НаборЗаписей = ЭлементКоллекции;
			КонецЕсли;
			
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОтложенноеПроведение

// Возвращает Истина, если для организации в указанном периоде используется 
// отложенное проведение.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация, для которой проверяется режим проведения.
//	Период - Дата+Время - Дата, по состоянию на которую проверяется режим проведения.
//	ПроверятьЭквайрингПриУСН - Булево - Истина, если для организации на УСН необходимо учитывать наличие эквайринговых операций.
//
Функция ИспользуетсяОтложенноеПроведение(Организация, Период, ПроверятьЭквайрингПриУСН = Истина) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьОтложенноеПроведение") Тогда
		// Отложенное проведение выключено.
		Возврат Ложь;
	КонецЕсли;

	ПрименяетсяУСНПатент		= УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, Период);
	ПрименяетсяУСНДоходы 		= УчетнаяПолитика.ПрименяетсяУСНДоходы(Организация, Период);
	ПлательщикНалогаНаПрибыль 	= УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Период);
	ПрименяетсяОсобыйПорядокНалогообложения = УчетнаяПолитика.ПрименяетсяОсобыйПорядокНалогообложения(Организация, Период);

	Если ПрименяетсяУСНДоходы
		ИЛИ ПлательщикНалогаНаПрибыль
		ИЛИ ПрименяетсяОсобыйПорядокНалогообложения Тогда
		// Отложенное проведение применяется только для:
		// 1. организаций на общей системе (налог на прибыль, НДС)
		// 2. организаций и ИП на УСН-доходы, в т.ч. совместно с патентами и/или ЕНВД
		// 3. ИП только на патенте
		// 4. ИП только на ЕНВД.
	Иначе
		// Все остальные системы налогообложения не совместимы с отложенным проведением.
		Возврат Ложь;
	КонецЕсли;
	
	СпособОценкиМПЗ = УчетнаяПолитика.СпособОценкиМПЗ(Организация, Период);
	Если СпособОценкиМПЗ <> Перечисления.СпособыОценки.ПоСредней Тогда
		// Отложенное проведение поддерживается только при оценке по средней стоимости МПЗ.
		Возврат Ложь;
	КонецЕсли;
	
	Если УчетнаяПолитика.РаздельныйУчетНДС(Организация, Период) Тогда
		// При раздельном учете НДС отложенное проведение не поддерживается.
		Возврат Ложь;
	КонецЕсли;

	СпособОценкиТоваровВРознице = УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Период);
	Если СпособОценкиТоваровВРознице <> Перечисления.СпособыОценкиТоваровВРознице.ПоСтоимостиПриобретения Тогда
		// При учете в рознице по ценам продаж отложенное проведение не поддерживается.
		Возврат Ложь;
	КонецЕсли;
	
	Если ПроверятьЭквайрингПриУСН И (ПрименяетсяУСНДоходы ИЛИ ПрименяетсяУСНПатент) Тогда
		// Для УСН и/или патента использование отложенного проведения возможно, 
		// если нет операций с платежными картами.
		Если ОбщегоНазначенияБПВызовСервераПовтИсп.ПрименяетсяОплатаПлатежнымиКартами(Организация) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;

	Возврат Истина;

КонецФункции

// Возвращает таблицу организаций и периодов, в течение которых поддерживается отложенное проведение.
//
// Параметры:
//	Организации - Массив, Неопределено - Список организаций, для которых требуется определить периоды.
//		Если не передан, то по всем организациям.
//
// Возвращаемое значние:
//	ТаблицаЗначений - содержит колонки
//		* Организация 	- СправочникСсылка.Организации
//		* ДатаНачала 	- Дата - первое число месяца, с которого организация может применять отложенное проведение.
//		* ДатаОкончания - Дата - последнее число месяца, в котором организация еще может применять отложенное проведение.
//
Функция ПериодыИспользованияОтложенногоПроведения(Организации = Неопределено) Экспорт

	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Организация",   Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Результат.Колонки.Добавить("ДатаНачала",    ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Результат.Колонки.Добавить("ДатаОкончания", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации", Организации);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Период КАК Период,
	|	НастройкиСистемыНалогообложения.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_Периоды
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|ГДЕ
	|	НастройкиСистемыНалогообложения.Организация В(&Организации)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	НастройкиУчетаНДС.Период,
	|	НастройкиУчетаНДС.Организация
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|ГДЕ
	|	НастройкиУчетаНДС.Организация В(&Организации)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	УчетнаяПолитика.Период,
	|	УчетнаяПолитика.Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|ГДЕ
	|	УчетнаяПолитика.Организация В(&Организации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация,
	|	МАКСИМУМ(НастройкиСистемыНалогообложения.Период) КАК ПериодБлижайший
	|ПОМЕСТИТЬ ВТ_НастройкиСистемыНалогообложения
	|ИЗ
	|	ВТ_Периоды КАК ВТ_Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|		ПО ВТ_Периоды.Организация = НастройкиСистемыНалогообложения.Организация
	|			И ВТ_Периоды.Период >= НастройкиСистемыНалогообложения.Период
	|ГДЕ
	|	НастройкиСистемыНалогообложения.Организация В(&Организации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация,
	|	МАКСИМУМ(УчетнаяПолитика.Период) КАК ПериодБлижайший
	|ПОМЕСТИТЬ ВТ_УчетнаяПолитика
	|ИЗ
	|	ВТ_Периоды КАК ВТ_Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|		ПО ВТ_Периоды.Организация = УчетнаяПолитика.Организация
	|			И ВТ_Периоды.Период >= УчетнаяПолитика.Период
	|ГДЕ
	|	УчетнаяПолитика.Организация В(&Организации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация,
	|	МАКСИМУМ(НастройкиУчетаНДС.Период) КАК ПериодБлижайший
	|ПОМЕСТИТЬ ВТ_НастройкиУчетаНДС
	|ИЗ
	|	ВТ_Периоды КАК ВТ_Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|		ПО ВТ_Периоды.Организация = НастройкиУчетаНДС.Организация
	|			И ВТ_Периоды.Период >= НастройкиУчетаНДС.Период
	|ГДЕ
	|	НастройкиУчетаНДС.Организация В(&Организации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НастройкиСистемыНалогообложения.Период КАК Период,
	|	ВТ_НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	ЕСТЬNULL(НастройкиСистемыНалогообложения.ПлательщикНДФЛ, ЛОЖЬ) КАК ПлательщикНДФЛ,
	|	ЕСТЬNULL(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходыМинусРасходы, ЛОЖЬ) КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	ЕСТЬNULL(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходы, ЛОЖЬ) КАК ПрименяетсяУСНДоходы,
	|	ЕСТЬNULL(НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент, ЛОЖЬ) КАК ПрименяетсяУСНПатент,
	|	ЛОЖЬ КАК СложныйУчетНДС,
	|	НЕОПРЕДЕЛЕНО КАК СпособОценкиМПЗ,
	|	НЕОПРЕДЕЛЕНО КАК СпособОценкиТоваровВРознице,
	|	ЕСТЬNULL(НастройкиСистемыНалогообложения.ПрименяетсяОсобыйПорядокНалогообложения, ЛОЖЬ) КАК ПрименяетсяОсобыйПорядокНалогообложения,
	|	ЕСТЬNULL(НастройкиСистемыНалогообложения.ПлательщикНалогаНаПрибыль, ЛОЖЬ) КАК ПлательщикНалогаНаПрибыль
	|ПОМЕСТИТЬ ВТ_Настройки
	|ИЗ
	|	ВТ_НастройкиСистемыНалогообложения КАК ВТ_НастройкиСистемыНалогообложения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|		ПО ВТ_НастройкиСистемыНалогообложения.Организация = НастройкиСистемыНалогообложения.Организация
	|			И ВТ_НастройкиСистемыНалогообложения.ПериодБлижайший = НастройкиСистемыНалогообложения.Период
	|ГДЕ
	|	НастройкиСистемыНалогообложения.Организация В(&Организации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_УчетнаяПолитика.Период,
	|	ВТ_УчетнаяПолитика.Организация,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(УчетнаяПолитика.СпособОценкиМПЗ, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(УчетнаяПолитика.СпособОценкиТоваровВРознице, НЕОПРЕДЕЛЕНО),
	|	ЛОЖЬ,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|		ПО ВТ_УчетнаяПолитика.Организация = УчетнаяПолитика.Организация
	|			И ВТ_УчетнаяПолитика.ПериодБлижайший = УчетнаяПолитика.Период
	|ГДЕ
	|	УчетнаяПолитика.Организация В(&Организации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_НастройкиУчетаНДС.Период,
	|	ВТ_НастройкиУчетаНДС.Организация,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(НастройкиУчетаНДС.СложныйУчетНДС, ЛОЖЬ),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_НастройкиУчетаНДС КАК ВТ_НастройкиУчетаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|	    ПО ВТ_НастройкиУчетаНДС.Организация = НастройкиУчетаНДС.Организация
	|			И ВТ_НастройкиУчетаНДС.ПериодБлижайший = НастройкиУчетаНДС.Период
	|ГДЕ
	|	НастройкиУчетаНДС.Организация В(&Организации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Настройки.Период КАК Период,
	|	ВТ_Настройки.Организация КАК Организация,
	|	МАКСИМУМ(ВТ_Настройки.ПлательщикНДФЛ) КАК ПлательщикНДФЛ,
	|	МАКСИМУМ(ВТ_Настройки.ПрименяетсяУСНДоходыМинусРасходы) КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	МАКСИМУМ(ВТ_Настройки.ПрименяетсяУСНДоходы) КАК ПрименяетсяУСНДоходы,
	|	МАКСИМУМ(ВТ_Настройки.ПрименяетсяУСНПатент) КАК ПрименяетсяУСНПатент,
	|	МАКСИМУМ(ВТ_Настройки.СложныйУчетНДС) КАК СложныйУчетНДС,
	|	МАКСИМУМ(ВТ_Настройки.СпособОценкиМПЗ) КАК СпособОценкиМПЗ,
	|	МАКСИМУМ(ВТ_Настройки.СпособОценкиТоваровВРознице) КАК СпособОценкиТоваровВРознице,
	|	МАКСИМУМ(ВТ_Настройки.ПрименяетсяОсобыйПорядокНалогообложения) КАК ПрименяетсяОсобыйПорядокНалогообложения,
	|	МАКСИМУМ(ВТ_Настройки.ПлательщикНалогаНаПрибыль) КАК ПлательщикНалогаНаПрибыль
	|ПОМЕСТИТЬ ВТ_НастройкиУчета
	|ИЗ
	|	ВТ_Настройки КАК ВТ_Настройки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Настройки.Период,
	|	ВТ_Настройки.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НастройкиУчета.Организация,
	|	ВТ_НастройкиУчета.Период,
	|	ВТ_НастройкиУчета.ПрименяетсяОсобыйПорядокНалогообложения,
	|	ВТ_НастройкиУчета.ПлательщикНалогаНаПрибыль,
	|	ВТ_НастройкиУчета.ПрименяетсяУСНДоходы,
	|	ВТ_НастройкиУчета.ПрименяетсяУСНПатент,
	|	ВТ_НастройкиУчета.СпособОценкиМПЗ,
	|	ВТ_НастройкиУчета.СложныйУчетНДС,
	|	ВТ_НастройкиУчета.СпособОценкиТоваровВРознице,
	|	МИНИМУМ(ВТ_НастройкиУчетаСледующая.Период) КАК ПериодСледующий
	|ПОМЕСТИТЬ ВТ_ПериодыДействияУчетнойПолитики
	|ИЗ
	|	ВТ_НастройкиУчета КАК ВТ_НастройкиУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НастройкиУчета КАК ВТ_НастройкиУчетаСледующая
	|		ПО ВТ_НастройкиУчета.Организация = ВТ_НастройкиУчетаСледующая.Организация
	|			И ВТ_НастройкиУчета.Период < ВТ_НастройкиУчетаСледующая.Период
	|ГДЕ
	|	ВТ_НастройкиУчета.Организация В(&Организации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_НастройкиУчета.Организация,
	|	ВТ_НастройкиУчета.Период,
	|	ВТ_НастройкиУчета.ПрименяетсяОсобыйПорядокНалогообложения,
	|	ВТ_НастройкиУчета.ПлательщикНалогаНаПрибыль,
	|	ВТ_НастройкиУчета.ПрименяетсяУСНДоходы,
	|	ВТ_НастройкиУчета.ПрименяетсяУСНПатент,
	|	ВТ_НастройкиУчета.СпособОценкиМПЗ,
	|	ВТ_НастройкиУчета.СложныйУчетНДС,
	|	ВТ_НастройкиУчета.СпособОценкиТоваровВРознице
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПериодыДействияУчетнойПолитики.Организация КАК Организация,
	|	ВТ_ПериодыДействияУчетнойПолитики.Период КАК ДатаНачала,
	|	ВТ_ПериодыДействияУчетнойПолитики.ПрименяетсяУСНДоходы КАК ПрименяетсяУСНДоходы,
	|	ВТ_ПериодыДействияУчетнойПолитики.ПрименяетсяУСНПатент КАК ПрименяетсяУСНПатент,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ЕСТЬNULL(ВТ_ПериодыДействияУчетнойПолитики.ПериодСледующий, ДАТАВРЕМЯ(2999, 12, 31)), ДЕНЬ), СЕКУНДА, -1) КАК ДатаОкончания
	|ИЗ
	|	ВТ_ПериодыДействияУчетнойПолитики КАК ВТ_ПериодыДействияУчетнойПолитики
	|ГДЕ
	|	(ВТ_ПериодыДействияУчетнойПолитики.ПлательщикНалогаНаПрибыль
	|			ИЛИ ВТ_ПериодыДействияУчетнойПолитики.ПрименяетсяУСНДоходы
	|			ИЛИ ВТ_ПериодыДействияУчетнойПолитики.ПрименяетсяОсобыйПорядокНалогообложения)
	|	И ВТ_ПериодыДействияУчетнойПолитики.СпособОценкиМПЗ = ЗНАЧЕНИЕ(Перечисление.СпособыОценки.ПоСредней)
	|	И ВТ_ПериодыДействияУчетнойПолитики.СложныйУчетНДС = ЛОЖЬ
	|	И ВТ_ПериодыДействияУчетнойПолитики.СпособОценкиТоваровВРознице = ЗНАЧЕНИЕ(Перечисление.СпособыОценкиТоваровВРознице.ПоСтоимостиПриобретения)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	ДатаНачала
	|ИТОГИ ПО
	|	Организация";

	Если НЕ ЗначениеЗаполнено(Организации) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НастройкиСистемыНалогообложения.Организация В(&Организации)", "ИСТИНА");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НастройкиУчетаНДС.Организация В(&Организации)",               "ИСТИНА");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УчетнаяПолитика.Организация В(&Организации)",                 "ИСТИНА");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТ_НастройкиУчета.Организация В(&Организации)",               "ИСТИНА");
	КонецЕсли;

	// Объединим непрерывные периоды действия в один, даже если за это время были изменения в учетной политике из-за других настроек.
	ВыборкаОрганизаций = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОрганизаций.Следующий() Цикл
	
		СтрокаТаблицы = Неопределено;
	
		Выборка = ВыборкаОрганизаций.Выбрать();
		Пока Выборка.Следующий() Цикл
		
			Если Выборка.ПрименяетсяУСНДоходы ИЛИ Выборка.ПрименяетсяУСНПатент Тогда
				// Для УСН и/или патента применение отложенного проведения возможно, 
				// если нет операций по платежным картам.
				Если ОбщегоНазначенияБПВызовСервераПовтИсп.ПрименяетсяОплатаПлатежнымиКартами(Выборка.Организация) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		
			Если СтрокаТаблицы = Неопределено Тогда
				СтрокаТаблицы = Результат.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
			Иначе
				Если СтрокаТаблицы.ДатаОкончания = Выборка.ДатаНачала - 1 Тогда
					// Продолжение предыдущего периода, продлим дату окончания.
					СтрокаТаблицы.ДатаОкончания = Выборка.ДатаОкончания;
				Иначе
					// Новый период.
					СтрокаТаблицы = Результат.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
				КонецЕсли;
			КонецЕсли;
		
		КонецЦикла;
	
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Возвращает таблицу организаций, для которых не может использоваться отложенное проведение,
// с пояснением причины.
//
Функция ПричиныНепримененияОтложенногоПроведения() Экспорт

	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Результат.Колонки.Добавить("Причина", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	Результат.Колонки.Добавить("ЕстьПериодыИспользованияОтложенногоПроведения", Новый ОписаниеТипов("Булево"));

	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиСистемыНалогообложения.Период,
	|	НастройкиСистемыНалогообложения.Организация
	|ПОМЕСТИТЬ ВТ_Периоды
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	НастройкиУчетаНДС.Период,
	|	НастройкиУчетаНДС.Организация
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	УчетнаяПолитика.Период,
	|	УчетнаяПолитика.Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация,
	|	МАКСИМУМ(НастройкиСистемыНалогообложения.Период) КАК ПериодБлижайший
	|ПОМЕСТИТЬ ВТ_НастройкиСистемыНалогообложения
	|ИЗ
	|	ВТ_Периоды КАК ВТ_Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|		ПО ВТ_Периоды.Организация = НастройкиСистемыНалогообложения.Организация
	|			И ВТ_Периоды.Период >= НастройкиСистемыНалогообложения.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация,
	|	МАКСИМУМ(УчетнаяПолитика.Период) КАК ПериодБлижайший
	|ПОМЕСТИТЬ ВТ_УчетнаяПолитика
	|ИЗ
	|	ВТ_Периоды КАК ВТ_Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|		ПО ВТ_Периоды.Организация = УчетнаяПолитика.Организация
	|			И ВТ_Периоды.Период >= УчетнаяПолитика.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация,
	|	МАКСИМУМ(НастройкиУчетаНДС.Период) КАК ПериодБлижайший
	|ПОМЕСТИТЬ ВТ_НастройкиУчетаНДС
	|ИЗ
	|	ВТ_Периоды КАК ВТ_Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|		ПО ВТ_Периоды.Организация = НастройкиУчетаНДС.Организация
	|			И ВТ_Периоды.Период >= НастройкиУчетаНДС.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Периоды.Период,
	|	ВТ_Периоды.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_НастройкиСистемыНалогообложения.Период КАК Период,
	|	ВТ_НастройкиСистемыНалогообложения.Организация КАК Организация,
	|	ЕСТЬNULL(НастройкиСистемыНалогообложения.ПлательщикНДФЛ, ЛОЖЬ) КАК ПлательщикНДФЛ,
	|	ЕСТЬNULL(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходыМинусРасходы, ЛОЖЬ) КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	ЕСТЬNULL(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходы, ЛОЖЬ) КАК ПрименяетсяУСНДоходы,
	|	ЕСТЬNULL(НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент, ЛОЖЬ) КАК ПрименяетсяУСНПатент,
	|	ЛОЖЬ КАК СложныйУчетНДС,
	|	НЕОПРЕДЕЛЕНО КАК СпособОценкиМПЗ,
	|	НЕОПРЕДЕЛЕНО КАК СпособОценкиТоваровВРознице
	|ПОМЕСТИТЬ ВТ_Настройки
	|ИЗ
	|	ВТ_НастройкиСистемыНалогообложения КАК ВТ_НастройкиСистемыНалогообложения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|		ПО ВТ_НастройкиСистемыНалогообложения.Организация = НастройкиСистемыНалогообложения.Организация
	|			И ВТ_НастройкиСистемыНалогообложения.ПериодБлижайший = НастройкиСистемыНалогообложения.Период
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_УчетнаяПолитика.Период,
	|	ВТ_УчетнаяПолитика.Организация,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(УчетнаяПолитика.СпособОценкиМПЗ, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(УчетнаяПолитика.СпособОценкиТоваровВРознице, НЕОПРЕДЕЛЕНО)
	|ИЗ
	|	ВТ_УчетнаяПолитика КАК ВТ_УчетнаяПолитика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|		ПО ВТ_УчетнаяПолитика.Организация = УчетнаяПолитика.Организация
	|			И ВТ_УчетнаяПолитика.ПериодБлижайший = УчетнаяПолитика.Период
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_НастройкиУчетаНДС.Период,
	|	ВТ_НастройкиУчетаНДС.Организация,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(НастройкиУчетаНДС.СложныйУчетНДС, ЛОЖЬ),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ВТ_НастройкиУчетаНДС КАК ВТ_НастройкиУчетаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|		ПО ВТ_НастройкиУчетаНДС.Организация = НастройкиУчетаНДС.Организация
	|			И ВТ_НастройкиУчетаНДС.ПериодБлижайший = НастройкиУчетаНДС.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Настройки.Период,
	|	ВТ_Настройки.Организация,
	|	ВТ_Настройки.Организация.Наименование КАК ОрганизацияНаименование,
	|	МАКСИМУМ(ВТ_Настройки.ПлательщикНДФЛ) КАК ПлательщикНДФЛ,
	|	МАКСИМУМ(ВТ_Настройки.ПрименяетсяУСНДоходыМинусРасходы) КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	МАКСИМУМ(ВТ_Настройки.ПрименяетсяУСНДоходы) КАК ПрименяетсяУСНДоходы,
	|	МАКСИМУМ(ВТ_Настройки.ПрименяетсяУСНПатент) КАК ПрименяетсяУСНПатент,
	|	МАКСИМУМ(ВТ_Настройки.СложныйУчетНДС) КАК СложныйУчетНДС,
	|	МАКСИМУМ(ВТ_Настройки.СпособОценкиМПЗ) КАК СпособОценкиМПЗ,
	|	МАКСИМУМ(ВТ_Настройки.СпособОценкиТоваровВРознице) КАК СпособОценкиТоваровВРознице
	|ИЗ
	|	ВТ_Настройки КАК ВТ_Настройки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Настройки.Период,
	|	ВТ_Настройки.Организация
	|УПОРЯДОЧИТЬ ПО
	|	ОрганизацияНаименование,
	|	Период
	|
	|ИТОГИ ПО
	|	Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОписаниеТипаБулево = Новый ОписаниеТипов("Булево");
	
	ТаблицаПериоды = Новый ТаблицаЗначений;
	ТаблицаПериоды.Колонки.Добавить("ДатаНачала",                            ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаПериоды.Колонки.Добавить("ДатаОкончания",                         ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаПериоды.Колонки.Добавить("ПлательщикНДФЛ",                        ОписаниеТипаБулево);
	ТаблицаПериоды.Колонки.Добавить("ПрименяетсяУСНДоходыМинусРасходы",      ОписаниеТипаБулево);
	ТаблицаПериоды.Колонки.Добавить("СпособОценкиМПЗФИФО",                   ОписаниеТипаБулево);
	ТаблицаПериоды.Колонки.Добавить("СложныйУчетНДС",                        ОписаниеТипаБулево);
	ТаблицаПериоды.Колонки.Добавить("УчетВПродажныхЦенах",                   ОписаниеТипаБулево);
	ТаблицаПериоды.Колонки.Добавить("ПрименяетсяОплатаПлатежнымиКартамиУСН", ОписаниеТипаБулево);
	
	МассивСтрок = Новый Массив;
	
	ВыборкаПоОрганизации = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоОрганизации.Следующий() Цикл
	
		// Инициализация переменных для новой организации.
		ТаблицаПериоды.Очистить();
		СтрокаТаблицы = Неопределено;
		ЕстьПериодыИспользованияОтложенногоПроведения = Ложь;
		
		Выборка = ВыборкаПоОрганизации.Выбрать();
		Пока Выборка.Следующий() Цикл
		
			СпособОценкиМПЗФИФО = Выборка.СпособОценкиМПЗ = Перечисления.СпособыОценки.ФИФО;
			УчетВПродажныхЦенах = Выборка.СпособОценкиТоваровВРознице = Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;
			
			ПрименяетсяОплатаПлатежнымиКартамиУСН = Ложь;
			Если Выборка.ПрименяетсяУСНДоходы ИЛИ Выборка.ПрименяетсяУСНПатент Тогда
				// Для УСН и/или патента отложенное проведение может применяться, если нет операций по оплате платежными картами.
				ПрименяетсяОплатаПлатежнымиКартамиУСН = ОбщегоНазначенияБПВызовСервераПовтИсп.ПрименяетсяОплатаПлатежнымиКартами(
					Выборка.Организация);
			КонецЕсли;
		
			Если Выборка.ПлательщикНДФЛ
				ИЛИ Выборка.ПрименяетсяУСНДоходыМинусРасходы
				ИЛИ СпособОценкиМПЗФИФО
				ИЛИ Выборка.СложныйУчетНДС
				ИЛИ УчетВПродажныхЦенах
				ИЛИ ПрименяетсяОплатаПлатежнымиКартамиУСН Тогда
				
				// Если предыдущего периода не было, то начинаем новый.
				Если СтрокаТаблицы = Неопределено Тогда
					СтрокаТаблицы = ТаблицаПериоды.Добавить();
					СтрокаТаблицы.ДатаНачала = Выборка.Период;
				КонецЕсли;
				
				// В разных записях учетной политки настройки могут отличаться, 
				// складываем их по ИЛИ, чтобы вывести соответствующую причину, 
				// если хотя бы в одной из записей установлена блокирующая настройка.
				СтрокаТаблицы.ПлательщикНДФЛ = СтрокаТаблицы.ПлательщикНДФЛ
					ИЛИ Выборка.ПлательщикНДФЛ;
					
				СтрокаТаблицы.ПрименяетсяУСНДоходыМинусРасходы = СтрокаТаблицы.ПрименяетсяУСНДоходыМинусРасходы
					ИЛИ Выборка.ПрименяетсяУСНДоходыМинусРасходы;
					
				Если НЕ Выборка.ПлательщикНДФЛ И НЕ Выборка.ПрименяетсяУСНДоходыМинусРасходы Тогда
					// Если применение ФИФО не связано с применяемой системой налогообложения,
					// то выводим его.
					СтрокаТаблицы.СпособОценкиМПЗФИФО = СтрокаТаблицы.СпособОценкиМПЗФИФО
						ИЛИ СпособОценкиМПЗФИФО;
				КонецЕсли;
				
				СтрокаТаблицы.СложныйУчетНДС = СтрокаТаблицы.СложныйУчетНДС
					ИЛИ Выборка.СложныйУчетНДС;

				СтрокаТаблицы.УчетВПродажныхЦенах = СтрокаТаблицы.УчетВПродажныхЦенах
					ИЛИ УчетВПродажныхЦенах;

				СтрокаТаблицы.ПрименяетсяОплатаПлатежнымиКартамиУСН = СтрокаТаблицы.ПрименяетсяОплатаПлатежнымиКартамиУСН
					ИЛИ ПрименяетсяОплатаПлатежнымиКартамиУСН;
				
			Иначе
			
				// Отложенное проведение можно использовать.
				ЕстьПериодыИспользованияОтложенногоПроведения = Истина;
			
				// Закроем предыдущий период, если он был.
				Если СтрокаТаблицы <> Неопределено Тогда
					СтрокаТаблицы.ДатаОкончания = Выборка.Период - 1;
				КонецЕсли;
				СтрокаТаблицы = Неопределено;
			
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТаблицаПериоды.Количество() > 0 Тогда
			
			МассивСтрок.Очистить();
			ДатаОкончанияДействия = '0001-01-01';
		
			Для Каждого СтрокаТаблицы Из ТаблицаПериоды Цикл
		
				// Если за все время ни разу не было периода,
				// в котором можно было бы использовать отложенное проведение,
				// то даты не показываем совсем.
				
				ТекстОтступ = "";
				Если ЕстьПериодыИспользованияОтложенногоПроведения Тогда
					Если ЗначениеЗаполнено(СтрокаТаблицы.ДатаОкончания) Тогда
						ПрошедшееВремя = Истина;
						ТекстДаты = СтрШаблон(НСтр("ru = 'С %1 по %2'"), 
							Формат(СтрокаТаблицы.ДатаНачала, 	"ДЛФ=Д"),
							Формат(СтрокаТаблицы.ДатаОкончания, "ДЛФ=Д"));
					Иначе
						ТекстДаты = СтрШаблон(НСтр("ru = 'С %1'"), Формат(СтрокаТаблицы.ДатаНачала, "ДЛФ=Д"));
					КонецЕсли;
					МассивСтрок.Добавить(ТекстДаты);
				КонецЕсли;

				Если СтрокаТаблицы.ПлательщикНДФЛ Тогда
					МассивСтрок.Добавить(ТекстОтступ + НСтр("ru = 'ИП применяет общую систему налогообложения'"));
				КонецЕсли;
				
				Если СтрокаТаблицы.ПрименяетсяУСНДоходыМинусРасходы Тогда
					МассивСтрок.Добавить(ТекстОтступ + НСтр("ru = 'Применяет УСН (доходы минус расходы)'"));
				КонецЕсли;
				
				Если СтрокаТаблицы.СпособОценкиМПЗФИФО Тогда
					МассивСтрок.Добавить(ТекстОтступ + НСтр("ru = 'Способ оценки материально-производственных запасов по ФИФО'"));
				КонецЕсли;

				Если СтрокаТаблицы.СложныйУчетНДС Тогда
					МассивСтрок.Добавить(ТекстОтступ + НСтр("ru = 'Ведется раздельный учет входящего НДС'"));
				КонецЕсли;
				
				Если СтрокаТаблицы.УчетВПродажныхЦенах Тогда
					МассивСтрок.Добавить(ТекстОтступ + НСтр("ru = 'Способ оценки товаров в рознице по продажной стоимости'"));
				КонецЕсли;
				
				Если СтрокаТаблицы.ПрименяетсяОплатаПлатежнымиКартамиУСН Тогда
					МассивСтрок.Добавить(ТекстОтступ + НСтр("ru = 'Применяет оплату платежными картами при УСН и/или патенте'"));
				КонецЕсли;
				
			КонецЦикла;
			
			// Сохраняем описание причин в таблицу.
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.Организация = ВыборкаПоОрганизации.Организация;
			НоваяСтрока.Причина = СтрСоединить(МассивСтрок, Символы.ПС);
			НоваяСтрока.ЕстьПериодыИспользованияОтложенногоПроведения = ЕстьПериодыИспользованияОтложенногоПроведения;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Функция КлючФоновогоЗаданияПереключениеОтложенногоПроведения() Экспорт

	Возврат "ПереключениеОтложенногоПроведения";

КонецФункции

// Проверяет, изменился ли состав организаций, которые применяют отложенное проведение в результате изменения настроек. 
// Если изменился, то запускает фоновогое задание для переключения режима проведения.
//
// Параметры:
//	ПериодыОтложенногоПроведенияДоИзменения - ТаблицаЗначений - Периоды отложенного проведения,
//  	полученные ранее до изменения настроек с помощью функции ПериодыИспользованияОтложенногоПроведения().
//	ИдентификаторФормы - УникальныйИдентификатор - Идентификатор формы, из которой вызвано изменение настроек.
//	ГоловнаяОрганизация - СправочникСсылка.Организации - Проверяемая головная организация.
//
// Возвращаемое значение:
//	Структура, Неопределено - Сведения о запущенном фоновом задании:
//		* Структура - см. ДлительныеОперации.ВыполнитьВФоне().
//		* Неопределено - если переключение не требуется и фоновое задание не было запущено.
//
Функция ПроверитьОтложенноеПроведениеПослеИзмененияНастроек(ПериодыОтложенногоПроведенияДоИзменения, ИдентификаторФормы, ГоловнаяОрганизация = Неопределено) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьОтложенноеПроведение") Тогда
		Возврат Неопределено;
	КонецЕсли;

	ПериодыОтложенногоПроведенияПослеИзменения = ПериодыИспользованияОтложенногоПроведения(ГоловнаяОрганизация);
	
	Если ПериодыОтложенногоПроведенияДоИзменения.Количество() = 0 
		И ПериодыОтложенногоПроведенияПослеИзменения.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Составим таблицу периодов, в которых произошли изменения условий применения отложенного проведения.
	ОбъединениеПериодов = ПериодыОтложенногоПроведенияДоИзменения.Скопировать();
	ОбъединениеПериодов.Колонки.Добавить("ИспользованиеДо",    ОбщегоНазначения.ОписаниеТипаЧисло(1));
	ОбъединениеПериодов.Колонки.Добавить("ИспользованиеПосле", ОбщегоНазначения.ОписаниеТипаЧисло(1));
	ОбъединениеПериодов.ЗаполнитьЗначения(1, "ИспользованиеДо");

	Для каждого СтрокаТаблицы Из ПериодыОтложенногоПроведенияПослеИзменения Цикл
		НоваяСтрока = ОбъединениеПериодов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.ИспользованиеПосле = 1;
	КонецЦикла;

	ОбъединениеПериодов.Свернуть("Организация, ДатаНачала,ДатаОкончания", "ИспользованиеДо, ИспользованиеПосле");

	ОбъединениеПериодов.Сортировать("Организация, ДатаНачала, ИспользованиеДо УБЫВ, ДатаОкончания", Новый СравнениеЗначений);
	
	ИзмененныеПериоды = ОбъединениеПериодов.СкопироватьКолонки();
	
	ТекущаяОрганизация = Неопределено;
	ТекущийПериод      = Неопределено; 
	
	Для каждого АнализируемыйПериод Из ОбъединениеПериодов Цикл

		Если АнализируемыйПериод.Организация <> ТекущаяОрганизация Тогда
			// Начинаются данные по новой организации, добавляем для нее новую строку.
			ТекущаяОрганизация = АнализируемыйПериод.Организация;
			
			ТекущийПериод = ИзмененныеПериоды.Добавить();
			ЗаполнитьЗначенияСвойств(ТекущийПериод, АнализируемыйПериод);

			Продолжить;

		КонецЕсли;

		Если АнализируемыйПериод.ДатаНачала = ТекущийПериод.ДатаНачала И АнализируемыйПериод.ДатаОкончания = ТекущийПериод.ДатаОкончания Тогда
			// Границы периодов полностью совпадают, выделять подпериоды не требуется.
			// Обновим только признаки.
			ТекущийПериод.ИспользованиеДо  = ?(ТекущийПериод.ИспользованиеДо + АнализируемыйПериод.ИспользованиеДо > 0, 1, 0);
			ТекущийПериод.ИспользованиеПосле = ?(ТекущийПериод.ИспользованиеПосле + АнализируемыйПериод.ИспользованиеПосле > 0, 1, 0);

		ИначеЕсли АнализируемыйПериод.ДатаНачала >= ТекущийПериод.ДатаНачала И АнализируемыйПериод.ДатаОкончания <= ТекущийПериод.ДатаОкончания Тогда
			// Анализируемый период лежит полностью внутри текущего периода.
			// Выделяем три подпериода [-1-(-2-)-3-]---> t
			
			Если АнализируемыйПериод.ДатаНачала > ТекущийПериод.ДатаНачала Тогда
				Подпериод1 = ИзмененныеПериоды.Добавить();
				ЗаполнитьЗначенияСвойств(Подпериод1, ТекущийПериод);
				Подпериод1.ДатаОкончания = АнализируемыйПериод.ДатаНачала - 1;
			КонецЕсли;
			
			Подпериод2 = ИзмененныеПериоды.Добавить();
			ЗаполнитьЗначенияСвойств(Подпериод2, АнализируемыйПериод);
			Подпериод2.ИспользованиеДо = ТекущийПериод.ИспользованиеДо;
			
			Если АнализируемыйПериод.ДатаОкончания < ТекущийПериод.ДатаОкончания Тогда
				Подпериод3 = ИзмененныеПериоды.Добавить();
				ЗаполнитьЗначенияСвойств(Подпериод3, ТекущийПериод);
				Подпериод3.ДатаНачала = АнализируемыйПериод.ДатаОкончания + 1;
			Иначе
				Подпериод3 = Неопределено;
			КонецЕсли;
			
			// В качестве текущего периода используем последний из подпериодов.
			ИзмененныеПериоды.Удалить(ТекущийПериод);
			ТекущийПериод = ?(Подпериод3 <> Неопределено, Подпериод3, Подпериод2);
			
		ИначеЕсли АнализируемыйПериод.ДатаНачала < ТекущийПериод.ДатаОкончания И АнализируемыйПериод.ДатаОкончания > ТекущийПериод.ДатаОкончания Тогда
			// Анализируемый период начинается в пределах текущего, а заканчивается после.
			// Выделяем три подпериода [-1-(-2-]-3-)---> t

			Если АнализируемыйПериод.ДатаНачала > ТекущийПериод.ДатаНачала Тогда
				Подпериод1 = ИзмененныеПериоды.Добавить();
				ЗаполнитьЗначенияСвойств(Подпериод1, ТекущийПериод);
				Подпериод1.ДатаОкончания = АнализируемыйПериод.ДатаНачала - 1;
			КонецЕсли;
			
			Подпериод2 = ИзмененныеПериоды.Добавить();
			ЗаполнитьЗначенияСвойств(Подпериод2, ТекущийПериод);
			Подпериод2.ДатаНачала         = АнализируемыйПериод.ДатаНачала;
			Подпериод2.ИспользованиеПосле = АнализируемыйПериод.ИспользованиеПосле;
			
			Подпериод3 = ИзмененныеПериоды.Добавить();
			ЗаполнитьЗначенияСвойств(Подпериод3, АнализируемыйПериод);
			Подпериод3.ДатаНачала = ТекущийПериод.ДатаОкончания + 1;

			// В качестве текущего периода используем последний из подпериодов.
			ИзмененныеПериоды.Удалить(ТекущийПериод);
			ТекущийПериод = Подпериод3;
		
		ИначеЕсли АнализируемыйПериод.ДатаНачала > ТекущийПериод.ДатаОкончания Тогда
			// Анализируемый период лежит полностью вне текущего периода.
			// Выделяем три подпериода [-1-]-2-(-3-)---> t
			// Первый подпериод совпадает с ТекущийПериод, его не дублируем.
			
			Подпериод2 = ИзмененныеПериоды.Добавить();
			ЗаполнитьЗначенияСвойств(Подпериод2, ТекущийПериод);
			Подпериод2.ДатаНачала = ТекущийПериод.ДатаОкончания + 1;
			Подпериод2.ДатаОкончания = АнализируемыйПериод.ДатаНачала - 1;
			Подпериод2.ИспользованиеДо    = 0;
			Подпериод2.ИспользованиеПосле = 0;
			
			Подпериод3 = ИзмененныеПериоды.Добавить();
			ЗаполнитьЗначенияСвойств(Подпериод3, АнализируемыйПериод);
			
			// В качестве текущего периода используем последний из подпериодов.
			ТекущийПериод = Подпериод3;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Оставим в таблице только строки тех периодов, за которые требуется переключение режима.
	ВключитьДляОрганизаций  = Новый Массив;
	ОтключитьДляОрганизаций = Новый Массив;
	
	Индекс = ИзмененныеПериоды.Количество() - 1;
	Пока Индекс >= 0 Цикл
	
		ТекущийПериод = ИзмененныеПериоды[Индекс];
		Если ТекущийПериод.ИспользованиеДо = ТекущийПериод.ИспользованиеПосле Тогда
			ИзмененныеПериоды.Удалить(Индекс);

		ИначеЕсли ТекущийПериод.ИспользованиеДо = 0 И ТекущийПериод.ИспользованиеПосле <> 0 Тогда
			ВключитьДляОрганизаций.Добавить(ТекущийПериод.Организация);

		ИначеЕсли ТекущийПериод.ИспользованиеДо <> 0 И ТекущийПериод.ИспользованиеПосле = 0 Тогда
			ОтключитьДляОрганизаций.Добавить(ТекущийПериод.Организация);
		КонецЕсли;

		Индекс = Индекс - 1;
	
	КонецЦикла;

	Если ИзмененныеПериоды.Количество() = 0 Тогда
		// В результате изменений переключение не требуется.
		Возврат Неопределено;
	КонецЕсли;

	// Запускаем фоновое задание для непосредственного переключения.

	Результат = Новый Структура();
	Результат.Вставить("ДлительнаяОперация");
	Результат.Вставить("НаименованиеФоновогоЗадания", "");

	ПараметрыЗадания = Новый Структура();
	ПараметрыЗадания.Вставить("ИзмененныеПериоды", ИзмененныеПериоды);
	
	НаименованиеЗадания = "";
	Если ЗначениеЗаполнено(ВключитьДляОрганизаций) Тогда
		НаименованиеЗадания = СтрШаблон(
			НСтр("ru = 'Включение отложенного проведения по %1'"),
			СтрСоединить(ВключитьДляОрганизаций, ", "));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтключитьДляОрганизаций) Тогда
		НаименованиеЗадания = НаименованиеЗадания + ?(ПустаяСтрока(НаименованиеЗадания), "", ". ") +
			СтрШаблон(НСтр("ru = 'Отключение отложенного проведения по %1'"),
					  СтрСоединить(ОтключитьДляОрганизаций, ", "));
	КонецЕсли;

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыВыполнения.КлючФоновогоЗадания 		= КлючФоновогоЗаданияПереключениеОтложенногоПроведения();
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
		
	Результат.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	Результат.ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
				"ПроведениеСервер.ПереключитьОтложенноеПроведениеПоПериодамВФоне",
				ПараметрыЗадания,
				ПараметрыВыполнения);
				
	Возврат Результат;

КонецФункции

// Запускает фоновое задание по переключению отложенного проведения для указанных организаций.
//
// Параметры:
//	ВключитьДляОрганизаций 	- Неопределено, Массив - Организации, которые сейчас используют 
//							отложенное проведение.
//							Если не задано, то не включается ни для одной.
//	ОтключитьДляОрганизаций	- Неопределено, Массив - Организации, которые должны использовать 
//							отложенное проведение после переключения.
//							Если не задано, то не отключается ни для одной.
//
// Возвращаемое значение:
//	Неопределено или Структура с ключами:
//		* НаименованиеФоновогоЗадания - Строка
//		* ДлительнаяОперация - Структура - см. ДлительныеОперации.ВыполнитьВФоне().
//
//
Функция НачатьПереключениеОтложенногоПроведения(ВключитьДляОрганизаций, ОтключитьДляОрганизаций, ИдентификаторФормы) Экспорт

	Если НЕ ЗначениеЗаполнено(ВключитьДляОрганизаций)
		И НЕ ЗначениеЗаполнено(ОтключитьДляОрганизаций) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Результат = Новый Структура();
	Результат.Вставить("ДлительнаяОперация");
	Результат.Вставить("НаименованиеФоновогоЗадания", "");

	ПараметрыЗадания = Новый Структура();
	
	НаименованиеЗадания = "";

	Если ЗначениеЗаполнено(ВключитьДляОрганизаций) Тогда
		ПараметрыЗадания.Вставить("ВключитьДляОрганизаций", ВключитьДляОрганизаций);

		НаименованиеЗадания = СтрШаблон(
			НСтр("ru = 'Включение отложенного проведения по %1'"),
			СтрСоединить(ВключитьДляОрганизаций, ", "));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтключитьДляОрганизаций) Тогда
		ПараметрыЗадания.Вставить("ОтключитьДляОрганизаций", ОтключитьДляОрганизаций);
		
		НаименованиеЗадания = НаименованиеЗадания + ?(ПустаяСтрока(НаименованиеЗадания), "", ". ") +
			СтрШаблон(НСтр("ru = 'Отключение отложенного проведения по %1'"),
					  СтрСоединить(ОтключитьДляОрганизаций, ", "));
	КонецЕсли;

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыВыполнения.КлючФоновогоЗадания 		= КлючФоновогоЗаданияПереключениеОтложенногоПроведения();
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
		
	Результат.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	Результат.ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
				"ПроведениеСервер.ПереключитьОтложенноеПроведениеВФоне",
				ПараметрыЗадания,
				ПараметрыВыполнения);
				
	Возврат Результат;

КонецФункции

// Выполняет инициализацию объектов базы данных, в зависимости от использования отложенного проведения.
//
// Параметры:
//	Параметры - Структура - Может содержать ключи:
//		* ВключитьДляОрганизаций - СправочникСсылка.Организации, Массив - Организация или массив организаций, 
//			по которой(-ым) включить отложенное проведение.
//			Необязательный ключ, если отсутствует, то не включает ни для одной организации.
//			Если содержит пустое значение, то включить для всех, для которых доступно.
//		* ОтключитьДляОрганизаций - СправочникСсылка.Организации, Массив - Организация или массив организаций, 
//			по которой(-ым) отключить отложенное проведение.
//			Необязательный ключ, если отсутствует, то не отключает ни для одной организации.
//			Если содержит пустое значение, то отключить для всех.
//
Процедура ПереключитьОтложенноеПроведениеВФоне(Параметры, АдресХранилища) Экспорт

	РезультатВыполнения = Новый Структура();
	РезультатВыполнения.Вставить("Успешно", Ложь);

	Отказ = Ложь;
	
	ЕстьВключить	= Ложь;
	ЕстьОтключить	= Ложь;
	ВключитьВсе		= Ложь;
	ОтключитьВсе	= Ложь;
	УказаныОрганизацииДляПереключения = Ложь;
	
	ВключитьДляОрганизаций	= Неопределено;
	ОтключитьДляОрганизаций	= Неопределено;
	
	Если Параметры.Свойство("ВключитьДляОрганизаций") Тогда
		ЕстьВключить = Истина;
		УказаныОрганизацииДляПереключения = Истина;
		ВключитьДляОрганизаций = Параметры.ВключитьДляОрганизаций;
		ВключитьВсе = НЕ ЗначениеЗаполнено(ВключитьДляОрганизаций);
	КонецЕсли;
	
	Если Параметры.Свойство("ОтключитьДляОрганизаций") Тогда
		ЕстьОтключить = Истина;
		УказаныОрганизацииДляПереключения = Истина;
		ОтключитьДляОрганизаций = Параметры.ОтключитьДляОрганизаций;
		ОтключитьВсе = НЕ ЗначениеЗаполнено(ОтключитьДляОрганизаций);
	КонецЕсли;
	
	Если НЕ УказаныОрганизацииДляПереключения ИЛИ (ВключитьВсе И ОтключитьВсе) Тогда
		// Не можем одновременно и включить и выключить.
		ВызватьИсключение НСтр("ru = 'ПроведениеСервер.ПереключитьОтложенноеПроведениеВФоне(): 
			|некорректно заданы организации для переключения режима отложенного проведения'");
	КонецЕсли;

	НачатьТранзакцию();

	Попытка
	
		Если ЕстьВключить Тогда
			ВключитьОтложенноеПроведение(ВключитьДляОрганизаций, Отказ);
		КонецЕсли;
		
		Если ЕстьОтключить Тогда
			ОтключитьОтложенноеПроведение(ОтключитьДляОрганизаций, Неопределено, Неопределено);
		КонецЕсли;
		
		Если Отказ Тогда
			ОтменитьТранзакцию();
		Иначе
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
	
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОтменитьТранзакцию();
		Отказ = Истина;
		
		КраткоеПредставление = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		ПодробноеПредставление = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		
		ШаблонОшибки = НСтр("ru = 'Ошибка при переключении отложенного проведения: %1.'");
		
		КраткийТекстОшибки = СтрШаблон(
			ШаблонОшибки,
			КраткоеПредставление);

		ПодробныйТекстОшибки = СтрШаблон(
			ШаблонОшибки,
			ПодробноеПредставление);
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткийТекстОшибки);
							
		ЗаписьЖурналаРегистрации(
			УчетВзаиморасчетовОтложенноеПроведение.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ПодробныйТекстОшибки);
	
	КонецПопытки;

	РезультатВыполнения.Успешно = НЕ Отказ;
	
	ПоместитьВоВременноеХранилище(РезультатВыполнения, АдресХранилища);

КонецПроцедуры

// Выполняет инициализацию объектов базы данных, в зависимости от использования отложенного проведения
// при переключении настроек учетной политики и системы налогообложения по организациям за отдельные периоды.
// Вызывается из ПроведениеСервер.ПроверитьОтложенноеПроведениеПослеИзмененияНастроек().
//
// Параметры:
//	Параметры - Структура - Содержит ключи:
//		* ИзмененныеПериоды - ТаблицаЗначений - Содержит колонки:
//			** Организация    - СправочникСсылка.Организации - Организация.
//			** ДатаНачала     - Дата - Дата начала периода.
//			** ДатаОкончания  - Дата - Дата окончания периода.
//			** ИспользованиеДо  - Число - Признак использования отложенного проведения в периоде до изменения.
//			** ИспользованиеПосле  - Число - Признак использования отложенного проведения в периоде после изменения.
//
Процедура ПереключитьОтложенноеПроведениеПоПериодамВФоне(Параметры, АдресХранилища) Экспорт

	Отказ = Ложь;
	МенеджерВременныхТаблиц = Неопределено; // Будет создан при первом обращении.
	ЕстьОрганизацииСОтложеннымПроведением = Ложь;
	
	Для каждого СтрокаПериода Из Параметры.ИзмененныеПериоды Цикл
	
		Если СтрокаПериода.ИспользованиеДо = 0 И СтрокаПериода.ИспользованиеПосле <> 0 Тогда
			// Включаем по организации за период.
			УчетВзаиморасчетовОтложенноеПроведение.ЗарегистрироватьОтложенныеРасчетыСКонтрагентамиПриВключенииПоОрганизации(
				СтрокаПериода.Организация,
				СтрокаПериода.ДатаНачала,
				СтрокаПериода.ДатаОкончания,
				МенеджерВременныхТаблиц,
				Отказ);
				
			ЕстьОрганизацииСОтложеннымПроведением = Истина;

		ИначеЕсли СтрокаПериода.ИспользованиеДо <> 0 И СтрокаПериода.ИспользованиеПосле = 0 Тогда
			// Отключаем по организации за период.
			ОтключитьОтложенноеПроведение(
				СтрокаПериода.Организация,
				СтрокаПериода.ДатаНачала,
				СтрокаПериода.ДатаОкончания);
		КонецЕсли;
	
	КонецЦикла;

	Если ЕстьОрганизацииСОтложеннымПроведением Тогда
		УстановитьКонстантыОтложенногоПроведения();
	КонецЕсли;

КонецПроцедуры

Процедура ВключитьОтложенноеПроведение(Организации, Отказ)

	УстановитьПривилегированныйРежим(Истина);

	УчетВзаиморасчетовОтложенноеПроведение.ЗарегистрироватьОтложенныеРасчетыСКонтрагентамиПриВключении(Организации, Отказ);
	
	Если НЕ Отказ Тогда
		УстановитьКонстантыОтложенногоПроведения();
	КонецЕсли;

КонецПроцедуры

// Выполняет отключение режима отложенного проведения.
// Если не задан период, то считается, что в базе отложенное проведение выключается полностью.
//
// Параметры:
//  Организации - СправочникСсылка.Организации, Массив - Организация или массив организаций.
//	ДатаНачала - Дата - Дата, с которой начинается период для отключения отложенного проведения.
//		Если не заполнена, то с самого начала ведения учета.
//	ДатаОкончания - Дата - Дата, на которой заканчивается период для отключения отложенного проведения.
//		Если не заполнена, то до самого последнего документа.
//
Процедура ОтключитьОтложенноеПроведение(Организации, ДатаНачала, ДатаОкончания)

	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации",    Организации);
	Запрос.УстановитьПараметр("ДатаНачала",     ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",  ДатаОкончания);
	
	ПолноеОтключение = НЕ ЗначениеЗаполнено(ДатаНачала) И НЕ ЗначениеЗаполнено(ДатаОкончания);
	
	Если ПолноеОтключение Тогда
		// Отключение режима в принципе, стираем записи в регистре порциями с отбором по организации.
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Организация,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияОтложенныхРасчетов.КВыполнению) КАК СостояниеРасчетов,
		|	МИНИМУМ(ВЫБОР
		|				КОГДА РасчетыСКонтрагентамиОтложенноеПроведение.СостояниеРасчетов В (ЗНАЧЕНИЕ(Перечисление.СостоянияОтложенныхРасчетов.КВыполнению), ЗНАЧЕНИЕ(Перечисление.СостоянияОтложенныхРасчетов.КИсключениюИзРасчетов))
		|					ТОГДА РасчетыСКонтрагентамиОтложенноеПроведение.Дата
		|				ИНАЧЕ ДАТАВРЕМЯ(2999, 12, 31)
		|		КОНЕЦ) КАК Дата
		|ИЗ
		|	РегистрСведений.РасчетыСКонтрагентамиОтложенноеПроведение КАК РасчетыСКонтрагентамиОтложенноеПроведение
		|ГДЕ
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Организация В(&Организации)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Организация";
	Иначе
		// Отключение за отдельный период, стираем по отдельным записям,
		// начиная с самых старых, чтобы правильно сбросить общую последовательность.
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Организация,
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Документ,
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Контрагент,
		|	РасчетыСКонтрагентамиОтложенноеПроведение.ДоговорКонтрагента,
		|	РасчетыСКонтрагентамиОтложенноеПроведение.СостояниеРасчетов,
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Дата
		|ИЗ
		|	РегистрСведений.РасчетыСКонтрагентамиОтложенноеПроведение КАК РасчетыСКонтрагентамиОтложенноеПроведение
		|ГДЕ
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Организация В(&Организации)
		|	И РасчетыСКонтрагентамиОтложенноеПроведение.Дата >= &ДатаНачала
		|	И РасчетыСКонтрагентамиОтложенноеПроведение.Дата <= &ДатаОкончания
		|
		|УПОРЯДОЧИТЬ ПО
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Организация,
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Дата,
		|	РасчетыСКонтрагентамиОтложенноеПроведение.Документ";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организации) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РасчетыСКонтрагентамиОтложенноеПроведение.Организация В(&Организации)", "ИСТИНА");
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ДатаНачала) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РасчетыСКонтрагентамиОтложенноеПроведение.Дата >= &ДатаНачала", "ИСТИНА");
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РасчетыСКонтрагентамиОтложенноеПроведение.Дата <= &ДатаОкончания", "ИСТИНА");
	КонецЕсли;

	НаборЗаписей = РегистрыСведений.РасчетыСКонтрагентамиОтложенноеПроведение.СоздатьНаборЗаписей();
	
	// Организации, по которым уже была сброшена последовательность на предыдущих шагах цикла.
	ПоследовательностьИзменена = Новый Соответствие;

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		// По каждой организации сбрасываем общую последовательность на первый документ по данным регистра отложенных расчетов.
		Если Выборка.СостояниеРасчетов <> Перечисления.СостоянияОтложенныхРасчетов.Выполнено
			И ПоследовательностьИзменена[Выборка.Организация] = Неопределено Тогда

			МоментПервогоДокумента = РаботаСПоследовательностями.МоментВремениПервогоДокументаВПоследовательности(
				Выборка.Организация,
				Выборка.Дата,
				Ложь);
			Если МоментПервогоДокумента <> Неопределено Тогда
				// Переносим момент нарушения последовательности на найденный документ.
				РаботаСПоследовательностями.СброситьСостояниеПоследовательностиДокумента(
					МоментПервогоДокумента.Ссылка,
					МоментПервогоДокумента.Дата,
					Выборка.Организация);
			КонецЕсли;
			
			// Запомним, что последовательность по текущей организаци уже однажды скорректировали и больше ее проверять не требуется.
			ПоследовательностьИзменена.Вставить(Выборка.Организация, Истина);
			
		КонецЕсли;

		// Очистим регистр сведений РасчетыСКонтрагентамиОтложенноеПроведение.
		Если ПолноеОтключение Тогда
			// Очищаем целиком по организации.
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Записать();
		Иначе
			// Очищаем по отдельным записям.
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
			НаборЗаписей.Отбор.Контрагент.Установить(Выборка.Контрагент);
			НаборЗаписей.Отбор.ДоговорКонтрагента.Установить(Выборка.ДоговорКонтрагента);
			НаборЗаписей.Записать();
		КонецЕсли;

	КонецЦикла;
	
	// Сбросим константу, если выполняется полное отключение для всей базы в целом или в базе только одна организация.
	Если ПолноеОтключение Тогда
		НадоСброситьКонстанту = НЕ ЗначениеЗаполнено(Организации)
			ИЛИ Справочники.Организации.КоличествоОрганизаций() = 1;
		
		Если НадоСброситьКонстанту И Константы.ИспользоватьОтложенноеПроведение.Получить() Тогда
			Константы.ИспользоватьОтложенноеПроведение.Установить(Ложь);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьКонстантыОтложенногоПроведения() 

	Если Константы.ИспользоватьОтложенноеПроведение.Получить() Тогда
		Возврат;
	КонецЕсли;

	Константы.ИспользоватьОтложенноеПроведение.Установить(Истина);
	// Одновременно устанавливаем константу проверки актуальности 
	// для отображения в отчетах кнопки "Актуализировать" отложенные расчеты.
	Константы.ПроверятьАктуальностьДанныхУчета.Установить(Истина);

КонецПроцедуры

#КонецОбласти

#Область ОбработкаОшибокПриПроведении

// Возвращает таблицу значений для хранения сообщений, выдаваемых в процессе проведения.
//
Функция НовыеСообщенияПользователю() Экспорт

	ОписаниеТиповЧисло = ОбщегоНазначения.ОписаниеТипаЧисло(1);

	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("Организация",  Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Результат.Колонки.Добавить("КлючДанных");  // Ссылка на объект.
	Результат.Колонки.Добавить("Дата");        // Дата, к которой относятся данные объекта. Используется для сортировки списка сообщений.
	Результат.Колонки.Добавить("Сообщение");   // СообщениеПользователю
	Результат.Колонки.Добавить("ТипСообщения", ОписаниеТиповЧисло); // 0 - ошибка, 1 - информация.
	
	Возврат Результат;

КонецФункции

// Возвращает пустую структуру с параметрами формирования отчета об ошибках.
//
// Параметры:
//	ВариантОтчета - Строка - "Перепроведение", "АктуализацияРасчетовСКонтрагентами"
//	Сообщения - ТаблицаЗначений - Результат функции НовыеСообщенияПользователю()
//
// Возвращаемое значение:
//	Структура - Содержит параметры формирования отчета:
//		* ВариантОтчета - Строка - "Перепроведение", "АктуализацияРасчетовСКонтрагентами".
//		* Сообщения - ТаблицаЗначений - Результат функции НовыеСообщенияПользователю().
//		* ДатаНачала - Дата - Начало периода расчета.
//		* ДатаОкончания - Дата - Окончание периода расчета.
//		* ПроведеноДокументов - Число - Количество перепроведенных документов.
//		* НеУдалосьПровести - Число - Количество документов с ошибками.
//		* АктуализированоДоговоров - Число - Количество договоров, расчеты по которым актуализированы.
//		* НеУдалосьАктуализировать - Число - Количество договоров с ошибками в расчетах.
// 
Функция НовыеПараметрыОтчетаССообщениямиПользователю(ВариантОтчета, Сообщения) Экспорт

	Если ВариантОтчета <> "Перепроведение"
		И ВариантОтчета <> "АктуализацияРасчетовСКонтрагентами" Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не поддерживаемый вариант отчета %1'"), ВариантОтчета);
	КонецЕсли;

	Результат = Новый Структура();
	
	Результат.Вставить("ВариантОтчета", 	ВариантОтчета);
	Результат.Вставить("Сообщения", 		Сообщения);
	Результат.Вставить("ДополнительноеОписание", "");
	Результат.Вставить("ДатаНачала",		'0001-01-01');
	Результат.Вставить("ДатаОкончания",		'0001-01-01');
	Результат.Вставить("ПроведеноДокументов",      0);
	Результат.Вставить("НеУдалосьПровести",        0);
	Результат.Вставить("АктуализированоДоговоров", 0);
	Результат.Вставить("НеУдалосьАктуализировать", 0);
	
	Возврат Результат;

КонецФункции

// Формирует табличный документ с информацией о сообщениях при проведении.
//
// Параметры:
//	ПараметрыОтчета - Структура - содержит параметры вывода сообщений, см.НовыеПараметрыОтчетаССообщениямиПользователю() 
//
// Возвращаемое значение:
//	Табличный документ - отчет об ошибках проведения.
//
Функция ВывестиСообщенияПользователю(ПараметрыОтчета) Экспорт

	// Выводим в отчет в иерархическом виде:
	//	- организация
	//		- группа сообщений (ошибки, информация) - если есть несколько типов сообщений
	//			- документ
	//  			- сообщения по этому документу
	
	ПараметрыОтчета.Сообщения.Сортировать("Организация, ТипСообщения, Дата, КлючДанных", Новый СравнениеЗначений());
	
	КоличествоСообщений 		     = ПараметрыОтчета.Сообщения.Количество();
	ЕстьСообщенияРазныхТипов 	     = Ложь;
	ЕстьРазныеОрганизации 		     = Ложь;
	ИспользуетсяОтложенноеПроведение = (ПараметрыОтчета.АктуализированоДоговоров > 0 Или ПараметрыОтчета.НеУдалосьАктуализировать > 0);

	Если КоличествоСообщений > 0 Тогда
		ПерваяОрганизация 			= ПараметрыОтчета.Сообщения[0].Организация;
		ЕстьСообщенияРазныхТипов 	= ПараметрыОтчета.Сообщения[КоличествоСообщений - 1].ТипСообщения <> 0;
		ЕстьРазныеОрганизации 		= ПараметрыОтчета.Сообщения[КоличествоСообщений - 1].Организация <> ПерваяОрганизация;
	КонецЕсли;

	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ОписаниеОшибокПроведения");
	
	Если ПараметрыОтчета.ВариантОтчета = "АктуализацияРасчетовСКонтрагентами" Тогда
		ИмяОбластиШапки = "ШапкаВосстановлениеРасчетов";
	ИначеЕсли ИспользуетсяОтложенноеПроведение Тогда
		ИмяОбластиШапки = "ШапкаОтложенноеПерепроведение";
	Иначе
		ИмяОбластиШапки = "ШапкаПерепроведение";
	КонецЕсли;
	ОбластьШапка             = Макет.ПолучитьОбласть(ИмяОбластиШапки);
	ОбластьГруппаОрганизация = Макет.ПолучитьОбласть("ГруппаОрганизация");
	ОбластьГруппаОшибки 	 = Макет.ПолучитьОбласть("ГруппаОшибки");
	ОбластьГруппаИнформация  = Макет.ПолучитьОбласть("ГруппаИнформация");
	ОбластьСсылка 	         = Макет.ПолучитьОбласть("Ссылка");
	ОбластьТекст2 	         = Макет.ПолучитьОбласть("ТекстВторогоУровня");

	Если НЕ ЕстьРазныеОрганизации Тогда
		// Если организация одна, выводим ее в шапке.
		ОбластьШапка.Параметры.Организация = ПерваяОрганизация;
	КонецЕсли;
	
	// Период отчета.
	Если ЗначениеЗаполнено(ПараметрыОтчета.ДатаНачала) И ЗначениеЗаполнено(ПараметрыОтчета.ДатаОкончания) Тогда
		ОбластьШапка.Параметры.Период = ПредставлениеПериода(ПараметрыОтчета.ДатаНачала, ПараметрыОтчета.ДатаОкончания);
	
	ИначеЕсли ЗначениеЗаполнено(ПараметрыОтчета.ДатаНачала) Тогда
		ОбластьШапка.Параметры.Период = СтрШаблон(
			НСтр("ru = '%1 - ...'"), 
			Формат(ПараметрыОтчета.ДатаНачала, 
					?(ПараметрыОтчета.ДатаНачала = НачалоДня(ПараметрыОтчета.ДатаНачала), "ДЛФ=Д", "ДЛФ=ДВ")));

	ИначеЕсли ЗначениеЗаполнено(ПараметрыОтчета.ДатаОкончания) Тогда
		ОбластьШапка.Параметры.Период = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '... - %1'"), 
			Формат(ПараметрыОтчета.ДатаОкончания, 
					?(ПараметрыОтчета.ДатаОкончания = НачалоДня(ПараметрыОтчета.ДатаОкончания) 
						ИЛИ ПараметрыОтчета.ДатаОкончания = КонецДня(ПараметрыОтчета.ДатаОкончания), 
					"ДЛФ=Д", 
					"ДЛФ=ДВ")));
	КонецЕсли;
	
	ОбластьШапка.Параметры.ДополнительноеОписание = ПараметрыОтчета.ДополнительноеОписание;

	Если ПараметрыОтчета.ВариантОтчета = "АктуализацияРасчетовСКонтрагентами"
	 Или ИспользуетсяОтложенноеПроведение Тогда
		ОбластьШапка.Параметры.АктуализированоДоговоров = ПараметрыОтчета.АктуализированоДоговоров;
		ОбластьШапка.Параметры.НеУдалосьАктуализировать = ПараметрыОтчета.НеУдалосьАктуализировать;
	КонецЕсли;
	Если ПараметрыОтчета.ВариантОтчета = "Перепроведение" Тогда
		ОбластьШапка.Параметры.ПроведеноДокументов 	    = ПараметрыОтчета.ПроведеноДокументов;
		ОбластьШапка.Параметры.НеУдалосьПровести 	    = ПараметрыОтчета.НеУдалосьПровести;
	КонецЕсли;

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Отчет_об_ошибках_проведения_документов";
	
	ТабДокумент.Вывести(ОбластьШапка);
	
	ТекущийКлючДанных 	= Неопределено;
	ТекущаяОрганизация 	= Неопределено;

	Сч = 0;
	
	Пока Сч < КоличествоСообщений Цикл
	
		СтрокаТаблицы = ПараметрыОтчета.Сообщения[Сч];
		
		Если ЕстьРазныеОрганизации Тогда
			Если СтрокаТаблицы.Организация <> ТекущаяОрганизация Тогда
				Если Сч > 0 Тогда
					// Закроем группу по предыдущей организации.
					ТабДокумент.ЗакончитьГруппуСтрок();
				КонецЕсли;
				// Начинаем группу по новой организации.
				ОбластьГруппаОрганизация.Параметры.Организация = СтрокаТаблицы.Организация;
				ТабДокумент.Вывести(ОбластьГруппаОрганизация);
				ТабДокумент.НачатьГруппуСтрок();
			КонецЕсли;
		КонецЕсли;
		
		ТекущаяОрганизация	= СтрокаТаблицы.Организация;
		ТекущийТипСообщения = СтрокаТаблицы.ТипСообщения;
		
		Если ЕстьСообщенияРазныхТипов Тогда
			Если ТекущийТипСообщения = 0 Тогда
				// Ошибка
				ТабДокумент.Вывести(ОбластьГруппаОшибки);
			Иначе
				// Информация
				ТабДокумент.Вывести(ОбластьГруппаИнформация);
			КонецЕсли;
		
			ТабДокумент.НачатьГруппуСтрок();
		КонецЕсли;
		
		НомерПП = 0;
		
		Пока Сч < КоличествоСообщений
			И ТекущаяОрганизация 	= ПараметрыОтчета.Сообщения[Сч].Организация
			И ТекущийТипСообщения 	= ПараметрыОтчета.Сообщения[Сч].ТипСообщения Цикл
		
			НомерПП = НомерПП + 1;
			
			СтрокаТаблицы = ПараметрыОтчета.Сообщения[Сч];
			
			ТекущийКлючДанных = СтрокаТаблицы.КлючДанных;
		
		    // Выводим документ и все его сообщения.
		    ОбластьСсылка.Параметры.НомерПП			= НомерПП;
	    	ОбластьСсылка.Параметры.Ссылка 			= СтрокаТаблицы.КлючДанных;
		    ОбластьСсылка.Параметры.Представление 	= Строка(СтрокаТаблицы.КлючДанных);
		    ТабДокумент.Вывести(ОбластьСсылка);
		    ТабДокумент.НачатьГруппуСтрок();
			
		    ВложенныйНомерПП = 0;
		    
		    // Из-за того сообщения могут приходит из нескольких расчетов, они могут повторяться для одного и того же объекта (КлючДанных).
		    // Такие сообщения выводим только один раз.
		    ТекстСообщения = "";
		    
		    Пока Сч < КоличествоСообщений
		    	И ТекущийТипСообщения = ПараметрыОтчета.Сообщения[Сч].ТипСообщения
		    	И ТекущийКлючДанных = ПараметрыОтчета.Сообщения[Сч].КлючДанных Цикл
		    
		    	СтрокаТаблицы = ПараметрыОтчета.Сообщения[Сч];
		    	
		    	Если СтрокаТаблицы.Сообщение.Текст = ТекстСообщения Тогда
		    		// Такое сообщение уже было выведено ранее, второй раз не повторяем.
		    		Сч = Сч + 1;
		    		Продолжить;
		    	КонецЕсли;
		    	ТекстСообщения = СтрокаТаблицы.Сообщение.Текст;

		    	ВложенныйНомерПП = ВложенныйНомерПП + 1;
		    	
	    		// Если документ сообщил о нескольких ошибках, то будем выводить для них субномера.
		    	Если ВложенныйНомерПП = 1 
		    		И (Сч = КоличествоСообщений - 1  // Это последнее сообщение в таблице или следующее сообщение относится к другом типу/документу.
		    				ИЛИ ПараметрыОтчета.Сообщения[Сч + 1].КлючДанных <> ТекущийКлючДанных
		    				ИЛИ ПараметрыОтчета.Сообщения[Сч + 1].ТипСообщения <> ТекущийТипСообщения)  Тогда
		    		ОбластьТекст2.Параметры.НомерПП = "";
		    	Иначе
		    		ОбластьТекст2.Параметры.НомерПП	= Формат(НомерПП, "ЧГ=") + "." + Формат(ВложенныйНомерПП, "ЧГ=");
		    	КонецЕсли;
		    	ОбластьТекст2.Параметры.Текст 				= СтрокаТаблицы.Сообщение.Текст;
		    	ОбластьТекст2.Параметры.Расшифровка 		= СтрокаТаблицы.Сообщение;
		    	
		    	ТабДокумент.Вывести(ОбластьТекст2);
		    	
		    	Сч = Сч + 1;
		    КонецЦикла; // по сообщениям одного документа
		    
		    ТабДокумент.ЗакончитьГруппуСтрок();
		
		КонецЦикла; // по документам
		
		Если ЕстьСообщенияРазныхТипов Тогда
	    	ТабДокумент.ЗакончитьГруппуСтрок();
	    КонецЕсли;
		
	КонецЦикла; // по типам сообщений
	
	Если ЕстьРазныеОрганизации И КоличествоСообщений > 0 Тогда
		// Закроем группу по последней организации.
		ТабДокумент.ЗакончитьГруппуСтрок();
	КонецЕсли;
	
	Возврат ТабДокумент;

КонецФункции

// Добавляет новое сообщение в общую таблицу сообщений.
//
// Параметры:
//	Сообщения - ТаблицаЗначений - см. НовыеСообщенияПользователю().
//	Организация - СправочникСсылка.Организации - Организация, для которой зарегистрировать сообщение.
//	НовоеСообщение - СообщениеПользователю - Добавляемое сообщение.
//	СсылкаНаОбъект - Произвольный - Ссылка на объект, с которым должно быть связано сообщение.
//	Дата - Дата - Дата, к которой относится сообщение пользователю. Используется для сортировки списка сообщений в хронологическом порядке.
//	ЭтоОшибка - Булево - Истина, если сообщение является сообщение об ошибке.
//
Процедура ДобавитьСообщениеПользователю(Сообщения, Организация, НовоеСообщение, СсылкаНаОбъект, Дата, ЭтоОшибка) Экспорт

	СтрокаТаблицы 					= Сообщения.Добавить();
	СтрокаТаблицы.Организация		= Организация;
	Если НЕ ЗначениеЗаполнено(НовоеСообщение.КлючДанных) Тогда
		СтрокаТаблицы.КлючДанных 	= СсылкаНаОбъект;
		НовоеСообщение.КлючДанных	= СсылкаНаОбъект;
	Иначе
		СтрокаТаблицы.КлючДанных 	= НовоеСообщение.КлючДанных;
	КонецЕсли;
	СтрокаТаблицы.Дата				= Дата;
	СтрокаТаблицы.Сообщение 		= НовоеСообщение;	
	СтрокаТаблицы.ТипСообщения 		= ?(ЭтоОшибка, 0, 1);

КонецПроцедуры

// Добавляет все сгенерированные сообщения пользователю при проведении объекта в общую таблицу сообщений.
//
// Параметры:
//	Сообщения - ТаблицаЗначений - см. НовыеСообщенияПользователю()
//	Организация - СправочникСсылка.Организации - Организация, к которой относится документ.
//	ТекстПричины - Строка - Строковое описание причины возникновения сообщения.
//	СсылкаНаОбъект - Произвольный - Ссылка на объект, с которым связано сообщение.
//	ЭтоОшибка - Булево - Истина, если сообщение является сообщением об ошибке.
//	АвтоматическиФормироватьТекстСообщения - Булево - Если Истина, то в текст сообщения будет включена ссылка на объект.
//
Процедура ЗапомнитьСообщенияПользователю(Сообщения, Организация, ТекстПричины, СсылкаНаОбъект, Дата, ЭтоОшибка = Истина, АвтоматическиФормироватьТекстСообщения = Истина) Экспорт

	СообщенияПользователю = ОбщегоНазначенияБП.ПолучитьСообщенияПользователюБезСлужебных(Истина);
	КоличествоСообщений = СообщенияПользователю.Количество();
	
	Если КоличествоСообщений > 0 Тогда
		Для ИндексСообщения = 0 По КоличествоСообщений - 1 Цикл
			ДобавитьСообщениеПользователю(Сообщения, Организация, СообщенияПользователю[ИндексСообщения], СсылкаНаОбъект, Дата, ЭтоОшибка);
		КонецЦикла;
	ИначеЕсли ЭтоОшибка Тогда
		// Объект сам о себе ничего не сказал, поэтому выводим общий текст сообщения о том,
		// что возникла ошибка.
		Если АвтоматическиФормироватьТекстСообщения Тогда
			Если ЗначениеЗаполнено(ТекстПричины) Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Обработка %1 не выполнена по причине: 
					|%2'"),
					Строка(СсылкаНаОбъект), ТекстПричины);
			Иначе
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При обработке %1 возникла ошибка!'"),
					Строка(СсылкаНаОбъект));
			КонецЕсли;
		Иначе
			ТекстСообщения = ТекстПричины;
		КонецЕсли;
	
		НовоеСообщение 				= Новый СообщениеПользователю();
		НовоеСообщение.КлючДанных 	= СсылкаНаОбъект;
		НовоеСообщение.Текст 		= ТекстСообщения;
		
		ДобавитьСообщениеПользователю(Сообщения, Организация, НовоеСообщение, СсылкаНаОбъект, Дата, ЭтоОшибка);
	
	КонецЕсли;

КонецПроцедуры

// Подготавливает сообщения пользователю для передачи в форму отображения ошибок проведения.
//
// Параметры:
//	Сообщения - ТаблицаЗначений - результат НовыеСообщенияПользователю()
//	ТабДокумент - ТабличныйДокумент - результат ВывестиСообщенияПользователю()
//	АдресХранилища - УникальныйИдентификатор, Строка - см. второй параметр платформенной функции ПоместитьВоВременноеХранилище()
//
// Возвращаемое значение:
//	Строка - Адрес созданного временного хранилища.
//
Функция ПоместитьСообщенияПользователюВоВременноеХранилищеДляФормыОшибок(Сообщения, ТабДокумент, АдресХранилища) Экспорт

	// Проиндексируем таблицу для поиска ней по КлючуДанных в форме ошибок.
	Сообщения.Индексы.Добавить("КлючДанных");

	Данные = Новый Структура();
	
	Данные.Вставить("ТаблицаСообщенийПользователю", Сообщения);
	Данные.Вставить("ОтчетПоОшибкам", 				ТабДокумент);

	Возврат ПоместитьВоВременноеХранилище(Данные, АдресХранилища);

КонецФункции

#КонецОбласти
