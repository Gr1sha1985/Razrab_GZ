#Область ПрограммныйИнтерфейс

// Возвращает строку, где все слова обработаны для поиска:
// - исключены знаки препинания
// - слова усечены до основы (см. РаботаСТекстомКлиентСервер.ПолучитьОсновуСлова())
//
// Параметры:
//	Текст - Строка - Строка, которую необходимо обработать.
//	МинимальнаяДлинаСлова - Число - Минимальная длина обработанного слова.
//	Если в результате обработки слово оказывается меньше указанной длины, оно не включается в результат.
//
// Возвращаемое значение:
//	Строка - Обработанная строка.
//
Функция ОбработатьСловаДляПоиска(Текст, МинимальнаяДлинаСлова = 0) Экспорт
	
	ТекстДляОбработки = СтрЗаменить(Нрег(Текст), "ё", "е");
	ТекстДляОбработки = СтрЗаменить(ТекстДляОбработки, "-", " ");
	ТекстДляОбработки = СтрЗаменить(ТекстДляОбработки, ".", " ");
	ТекстДляОбработки = СтрЗаменить(ТекстДляОбработки, ",", " ");
	ТекстДляОбработки = СтрЗаменить(ТекстДляОбработки, "!", " ");
	ТекстДляОбработки = СтрЗаменить(ТекстДляОбработки, "?", " ");
	ТекстДляОбработки = СтрЗаменить(ТекстДляОбработки, ":", " ");
	ТекстДляОбработки = СтрЗаменить(ТекстДляОбработки, ";", " ");
	ТекстДляОбработки = СтрЗаменить(ТекстДляОбработки, ")", " ");
	ТекстДляОбработки = СтрЗаменить(ТекстДляОбработки, "(", " ");
	ТекстДляОбработки = СтрЗаменить(ТекстДляОбработки, """", " ");
	
	Слова = СтрРазделить(ТекстДляОбработки, " ", Ложь);
	
	Результат = Новый Массив;
	Для каждого Слово Из Слова Цикл
		ОсноваСлова = ПолучитьОсновуСлова(Слово);
		Если СтрДлина(ОсноваСлова) < МинимальнаяДлинаСлова Тогда
			Продолжить;
		КонецЕсли;
		Результат.Добавить(ОсноваСлова);
	КонецЦикла;
	
	Возврат СтрСоединить(Результат, " ");

КонецФункции

// Возвращает основу слова (слово без оконачния).
//
// Параметры:
//	Слово - Строка - Слово, окончание которого требуется удалить.
//
// Возвращаемое значение:
//	Строка - Основа слова без окончания.
//
Функция ПолучитьОсновуСлова(Слово) Экспорт
	
	Область1 = ПолучитьОбласть1(Слово);
	НачалоСлова = Лев(Слово, СтрДлина(Слово) - СтрДлина(Область1));
	
	Результат = УдалитьОкончаниеСовершеннойФормыГлагола(Область1);
	Если Результат <> Область1 Тогда
		Область1 = Результат;
	Иначе
		Область1 = УдалитьВозвратнуюЧастицу(Область1);
		Результат = УдалитьОкончаниеПрилагательногоИДеепричастия(Область1);
		Если Результат <> Область1 Тогда
			Область1 = Результат;
		Иначе
			Результат = УдалитьОкончаниеГлагола(Область1);
			Если Результат <> Область1 Тогда
				Область1 = Результат;
			Иначе
				Область1 = УдалитьОкончаниеСуществительного(Область1);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрЗаканчиваетсяНа(Область1, "и") Тогда
		Область1 = Лев(Область1, СтрДлина(Область1) - 1);
	КонецЕсли;
	
	Область2 = ПолучитьОбласть2(Область1);
	НачалоОбласти = Лев(Область1, СтрДлина(Область1) - СтрДлина(Область2));
	
	Результат = НачалоОбласти + УдалитьСуффикс(Область2);
	
	Результат = УдалитьОкончаниеПревосходнойСтепени(Результат);
	
	Если СтрЗаканчиваетсяНа(Результат, "нн") Тогда
		Результат = Лев(Результат, СтрДлина(Результат) - 1);
	КонецЕсли;
	
	Если СтрЗаканчиваетсяНа(Результат, "ь") Тогда
		Результат = Лев(Результат, СтрДлина(Результат) - 1);
	КонецЕсли;
	
	Возврат НачалоСлова + Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция УдалитьОкончание(Слово, Окончания, Смещение = 0)
	
	Для каждого Окончание Из Окончания Цикл
		Если СтрЗаканчиваетсяНа(Слово, Окончание) Тогда
			Возврат Сред(Слово, 1, СтрДлина(Слово) - СтрДлина(Окончание) + Смещение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Слово;
	
КонецФункции

Функция УдалитьОкончаниеСовершеннойФормыГлагола(Слово)
	
	ОкончанияСовершеннойФормыГлагола1 = ПолучитьОкончанияСовершеннойФормыГлагола1();
	ОкончанияСовершеннойФормыГлагола2 = ПолучитьОкончанияСовершеннойФормыГлагола2();
	
	Результат = УдалитьОкончание(Слово, ОкончанияСовершеннойФормыГлагола2);
	Если Результат <> Слово Тогда
		Возврат Результат;
	Иначе
		Возврат УдалитьОкончание(Слово, ОкончанияСовершеннойФормыГлагола1, 1);
	КонецЕсли;
	
КонецФункции

Функция УдалитьОкончаниеДеепричастия(Слово)
	
	ОкончанияДеепричастия1 = ПолучитьОкончанияДеепричастия1();
	ОкончанияДеепричастия2 = ПолучитьОкончанияДеепричастия2();
	
	Результат = УдалитьОкончание(Слово, ОкончанияДеепричастия2);
	Если Результат <> Слово Тогда
		Возврат Результат;
	Иначе
		Возврат УдалитьОкончание(Слово, ОкончанияДеепричастия1, 1);
	КонецЕсли;
	
КонецФункции

Функция УдалитьОкончаниеПрилагательногоИДеепричастия(Слово)
	
	ОкончанияПрилагательного = ПолучитьОкончанияПрилагательного();
	
	Результат = УдалитьОкончание(Слово, ОкончанияПрилагательного);
	Если Результат <> Слово Тогда
		Результат = УдалитьОкончаниеДеепричастия(Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция УдалитьОкончаниеГлагола(Слово)
	
	ОкончанияГлагола1 = ПолучитьОкончанияГлагола1();
	ОкончанияГлагола2 = ПолучитьОкончанияГлагола2();
	
	Результат = УдалитьОкончание(Слово, ОкончанияГлагола2);
	Если Результат <> Слово Тогда
		Возврат Результат;
	Иначе
		Возврат УдалитьОкончание(Слово, ОкончанияГлагола1, 1);
	КонецЕсли;
	
КонецФункции

Функция УдалитьОкончаниеСуществительного(Слово)
	
	ОкончанияСуществительного = ПолучитьОкончанияСуществительного();
	
	Возврат УдалитьОкончание(Слово, ОкончанияСуществительного);
	
КонецФункции

Функция УдалитьВозвратнуюЧастицу(Слово)
	
	ВозвратныеЧастицы = ПолучитьВозвратныеЧастицы();
	
	Возврат УдалитьОкончание(Слово, ВозвратныеЧастицы);
	
КонецФункции

Функция УдалитьСуффикс(Слово)
	
	Суффиксы = ПолучитьСуффиксы();
	
	Возврат УдалитьОкончание(Слово, Суффиксы);
	
КонецФункции

Функция УдалитьОкончаниеПревосходнойСтепени(Слово)
	
	ОкончанияПревосходнойСтепени = ПолучитьОкончанияПревосходнойСтепени();
	
	Возврат УдалитьОкончание(Слово, ОкончанияПревосходнойСтепени);
	
КонецФункции

Функция ПолучитьОкончанияСовершеннойФормыГлагола1()
	
	Возврат СтрРазделить(
		"авшись,явшись,авши,явши,ав,яв",
		","
	);
	
КонецФункции

Функция ПолучитьОкончанияСовершеннойФормыГлагола2()
	
	Возврат СтрРазделить(
		"ившись,ывшись,ивши,ывши,ив,ыв",
		","
	);
	
КонецФункции

Функция ПолучитьВозвратныеЧастицы()
	
	Возврат СтрРазделить(
		"ся,сь",
		","
	);
	
КонецФункции

Функция ПолучитьОкончанияПрилагательного()
	
	Возврат СтрРазделить(
		"его,ему,ими,ого,ому,ыми,ая,ее,ей,ем,ею,ие,ий,им,их,ое,ой,ом,ою,ую,ые,ый,ым,ых,юю,яя",
		","
	);
	
КонецФункции

Функция ПолучитьОкончанияДеепричастия1()
	
	Возврат СтрРазделить(
		"авш,аем,анн,ающ,явш,яем,янн,яющ,ащ,ящ",
		","
	);
	
КонецФункции

Функция ПолучитьОкончанияДеепричастия2()
	
	Возврат СтрРазделить(
		"ивш,ывш,ующ",
		","
	);
	
КонецФункции

Функция ПолучитьОкончанияГлагола1()
	
	Возврат СтрРазделить(
		"аете,аешь,айте,анно,яете,яешь,яйте,янно,аем,ает,ала,али,ало,ана,ано,аны,ать,ают,яем,яет,яла,яли,яло,яна,яно,яны,ять,яют,ай,ал,ан,яй,ял,ян",
		","
	);
	
КонецФункции

Функция ПолучитьОкончанияГлагола2()
	
	Возврат СтрРазделить(
		"ейте,уйте,ена,ено,ены,ила,или,ило,ите,ить,ишь,ует,уют,ыла,ыли,ыло,ыть,ей,ен,ил,им,ит,уй,ую,ыл,ым,ыт,ят,ю",
		","
	);
	
КонецФункции

Функция ПолучитьОкончанияСуществительного()
	
	Возврат СтрРазделить(
		"иями,ами,ией,ием,иям,иях,ями,ам,ах,ев,еи,ей,ем,ие,ии,ий,ию,ия,ов,ой,ом,ье,ью,ья,ям,ях,а,е,и,й,о,у,ы,ь,ю,я",
		","
	);
	
КонецФункции

Функция ПолучитьОкончанияПревосходнойСтепени()
	
	Возврат СтрРазделить(
		"ейше,ейш",
		","
	);
	
КонецФункции

Функция ПолучитьСуффиксы()
	
	Возврат СтрРазделить(
		"ость,ост",
		","
	);

КонецФункции

Функция ПолучитьОбласть1(Слово)
	
	Гласные = ГласныеБуквы();
	
	Для НачальныйНомер = 1 По СтрДлина(Слово) Цикл
		Буква = Сред(Слово, НачальныйНомер, 1);
		Если СтрНайти(Гласные, Буква) > 0 Тогда
			Возврат Сред(Слово, НачальныйНомер + 1);
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

Функция ПолучитьОбласть2(Область1)
	
	Согласные = СогласныеБуквы();
	
	Буква = Лев(Область1, 1);
	Если СтрНайти(Согласные, Буква) > 0 Тогда
		Подстрока = Сред(Область1, 2);
	Иначе
		Подстрока = ПолучитьОбластьПослеГласнойИСогласной(Область1);
	КонецЕсли;
	
	Возврат ПолучитьОбластьПослеГласнойИСогласной(Подстрока);
	
КонецФункции

Функция ПолучитьОбластьПослеГласнойИСогласной(Подстрока)
	
	Гласные = ГласныеБуквы();
	Согласные = СогласныеБуквы();
	
	Для НачальныйНомер = 1 По СтрДлина(Подстрока) Цикл
		Буква = Сред(Подстрока, НачальныйНомер, 1);
		Если СтрНайти(Гласные, Буква) > 0 Тогда
			СледующаяБуква = Сред(Подстрока, НачальныйНомер + 1, 1);
			Если СтрНайти(Согласные, СледующаяБуква) > 0 Тогда
				Возврат Сред(Подстрока, НачальныйНомер + 2);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

Функция ГласныеБуквы()
	
	Возврат "аеиоуыэюя";
	
КонецФункции

Функция СогласныеБуквы()
	
	Возврат "бвгджйзклмнпрстфхцчшщ";
	
КонецФункции

#КонецОбласти
