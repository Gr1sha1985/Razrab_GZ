////////////////////////////////////////////////////////////////////////////////
// Модуль содержит переопределяемые процедуры и функции.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции получения сведений об организации (предпринимателе).

// Возвращает сведения об организации (предпринимателе).
//
// Параметры:
//  Организация - СправочникСсылка.Организации - ссылка на элемент справочника "Организации".
//
// Возвращаемое значение:
//  Структура - структура реквизитов:
//   *ИНН          - Строка - ИНН предпринимателя.
//   *Наименование - Строка - ФИО предпринимателя.
//
Функция СведенияОбОрганизации(Организация) Экспорт
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "ИНН, ФамилияИП, ИмяИП, ОтчествоИП");
	
	СоставляющиеНаименования = Новый Массив;
	Если НЕ ПустаяСтрока(РеквизитыОрганизации.ФамилияИП) Тогда
		СоставляющиеНаименования.Добавить(ВРЕГ(СокрЛП(РеквизитыОрганизации.ФамилияИП)));
	КонецЕсли;
	Если НЕ ПустаяСтрока(РеквизитыОрганизации.ИмяИП) Тогда
		СоставляющиеНаименования.Добавить(ВРЕГ(СокрЛП(РеквизитыОрганизации.ИмяИП)));
	КонецЕсли;
	Если НЕ ПустаяСтрока(РеквизитыОрганизации.ОтчествоИП) Тогда
		СоставляющиеНаименования.Добавить(ВРЕГ(СокрЛП(РеквизитыОрганизации.ОтчествоИП)));
	КонецЕсли;
	
	СведенияОбОрганизации = Новый Структура();
	СведенияОбОрганизации.Вставить("ИНН",          РеквизитыОрганизации.ИНН);
	СведенияОбОрганизации.Вставить("Наименование", СтрСоединить(СоставляющиеНаименования, " "));
	
	Возврат СведенияОбОрганизации;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции получения сведений о контрагенте.

// Получает отдельные сведения о контрагенте.
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты - ссылка на элемент справочника "Контрагенты".
//  ПолучаемыеЗначения - структура, элементы которой имеют имена, идентифицирующие получаемые значения.
//
Процедура ДополнительныеСведенияОКонтрагенте(Контрагент, ПолучаемыеЗначения) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Контрагент)
		Или ТипЗнч(Контрагент) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучаемыеЗначения.Свойство("ЭтоИностраннаяОрганизация") Тогда
		ПолучаемыеЗначения.ЭтоИностраннаяОрганизация = Справочники.Контрагенты.ЭтоИностраннаяОрганизация(Контрагент);
	КонецЕсли;
	Если ПолучаемыеЗначения.Свойство("ЭтоФизическоеЛицо") Тогда
		ПолучаемыеЗначения.ЭтоФизическоеЛицо = Справочники.Контрагенты.ЭтоФизическоеЛицо(Контрагент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти