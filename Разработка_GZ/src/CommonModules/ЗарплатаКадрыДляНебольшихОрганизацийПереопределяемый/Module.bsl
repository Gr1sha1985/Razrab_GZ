#Область ПрограммныйИнтерфейс

#Область ПрямыеВыплаты

// В данной процедуре можно добавить способ прямых выплат 'На банковский счет',
//   а также переопределить заголовки других способов прямых выплат.
//
// Параметры:
//   ТипыСпособов - ТаблицаЗначений - Типы способов прямых выплат.
//       * ИмяТипа - Строка - Имя ссылочного типа, добавленного в определяемый тип СпособыПрямыхВыплатФСС.
//           Например: "СправочникСсылка.БанковскиеСчетаКонтрагентов".
//       * Заголовок - Строка - Представление типа. Выводится в заголовке поля ввода значения данного типа.
//       * СпособВыплатыПособия - ПеречислениеСсылка.СпособыВыплатыПособия - Способ выплаты пособия,
//           использующийся при выборе данного способа.
//
// Пример:
//	ОписаниеСпособа = ТипыСпособов.Добавить();
//	ОписаниеСпособа.ИмяТипа   = "СправочникСсылка.БанковскиеСчетаКонтрагентов";
//	ОписаниеСпособа.Заголовок = НСтр("ru = 'На банковский счет'");
//	ОписаниеСпособа.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ЧерезБанк;
//
Процедура ПриОпределенииСпособовПрямыхВыплат(ТипыСпособов) Экспорт
	
	ОписаниеСпособа = ТипыСпособов.Добавить();
	ОписаниеСпособа.ИмяТипа   = "СправочникСсылка.БанковскиеСчета";
	ОписаниеСпособа.Заголовок = НСтр("ru = 'На банковский счет'");
	ОписаниеСпособа.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ЧерезБанк;
	
КонецПроцедуры

// В данной процедуре необходимо по указанному списку физических лиц получить банковские реквизиты.
//
// Параметры:
//   ФизическиеЛица      - Массив из СправочникСсылка.ФизическиеЛица - Фильтр по физическим лицам.
//   БанковскиеРеквизиты - Неопределено - В данный параметр необходимо поместить таблицу значений с колонками:
//       * Значение              - ОпределяемыйТип.СпособыПрямыхВыплатФСС - Зарплатный проект / банковский счет.
//       * ПредставлениеЗначения - Строка
//       * Банк                  - СправочникСсылка.КлассификаторБанков - Ссылка на элемент классификатора банков.
//       * НаименованиеБанка     - Строка                               - Наименование банка.
//       * БИК                   - Строка                               - БИК банка.
//       * КоррСчет              - Строка                               - Корреспондентский счет банка.
//       * ФизическоеЛицо        - СправочникСсылка.ФизическиеЛица
//       * Организация           - СправочникСсылка.Организации
//       * НомерСчета            - Строка - Номер счета физического лица.
//       * ДатаОткрытияСчета     - Дата   - Дата открытия банковского счета.
//       * ГоловнаяОрганизация   - СправочникСсылка.Организации
//       * ЯвляетсяМестомВыплатыЗарплаты - Булево - Истина для зарплатных проектов / банковских счетов,
//           которые используются как основной способ выплаты зарплаты данного физлица в данной организации.
//     Если оставить значение Неопределено то будет выполнена стандартая обработка события.
//
Процедура ПриОпределенииЗарплатныхПроектовИБанковскихСчетов(ФизическиеЛица, БанковскиеРеквизиты) Экспорт
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Параметры = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	Параметры.СписокФизическихЛиц = ФизическиеЛица;
	Параметры.НачалоПериода       = ТекущаяДатаСеанса();
	Параметры.ОкончаниеПериода    = Параметры.НачалоПериода;
	Параметры.КадровыеДанные      = "Организация, ФизическоеЛицо";
	
	УстановитьПривилегированныйРежим(Истина);
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(МенеджерВТ, Ложь, Параметры);
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МестаВыплатыЗарплатыСотрудников.Вид КАК МестоВыплаты,
	|	Сотрудники.Организация КАК Организация,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТМестаВыплатыЗарплаты
	|ИЗ
	|	ВТСотрудникиОрганизации КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестаВыплатыЗарплатыСотрудников КАК МестаВыплатыЗарплатыСотрудников
	|		ПО Сотрудники.Сотрудник = МестаВыплатыЗарплатыСотрудников.Сотрудник
	|ГДЕ
	|	Сотрудники.ФизическоеЛицо В(&ФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.МестоВыплаты КАК МестоВыплаты,
	|	Сотрудники.Организация КАК Организация,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТМестаВыплатыЗарплатныеПроекты
	|ИЗ
	|	ВТМестаВыплатыЗарплаты КАК Сотрудники
	|ГДЕ
	|	Сотрудники.МестоВыплаты = ЗНАЧЕНИЕ(Перечисление.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.МестоВыплаты КАК МестоВыплаты,
	|	Сотрудники.Организация КАК Организация,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТМестаВыплатыБанковскиеСчета
	|ИЗ
	|	ВТМестаВыплатыЗарплаты КАК Сотрудники
	|ГДЕ
	|	Сотрудники.МестоВыплаты = ЗНАЧЕНИЕ(Перечисление.ВидыМестВыплатыЗарплаты.БанковскийСчет)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗарплатныеПроекты.Ссылка КАК Значение,
	|	ЗарплатныеПроекты.Представление КАК ПредставлениеЗначения,
	|	ЗарплатныеПроекты.Банк КАК Банк,
	|	ЛицевыеСчета.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЛицевыеСчета.НомерЛицевогоСчета КАК НомерСчета,
	|	ЛицевыеСчета.ДатаОткрытияЛицевогоСчета КАК ДатаОткрытияСчета,
	|	ЛицевыеСчета.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА МестаВыплаты.МестоВыплаты ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЯвляетсяМестомВыплатыЗарплаты,
	|	ЗарплатныеПроекты.ПометкаУдаления КАК ПометкаУдаления
	|ПОМЕСТИТЬ ВТИтогиБезБИК
	|ИЗ
	|	РегистрСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам КАК ЛицевыеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
	|		ПО ЛицевыеСчета.ЗарплатныйПроект = ЗарплатныеПроекты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМестаВыплатыЗарплатныеПроекты КАК МестаВыплаты
	|		ПО ЛицевыеСчета.ФизическоеЛицо = МестаВыплаты.ФизическоеЛицо
	|			И ЛицевыеСчета.Организация = МестаВыплаты.Организация
	|ГДЕ
	|	ЛицевыеСчета.ФизическоеЛицо В(&ФизическиеЛица)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка,
	|	БанковскиеСчета.Представление,
	|	БанковскиеСчета.Банк,
	|	БанковскиеСчета.Владелец,
	|	БанковскиеСчета.НомерСчета,
	|	БанковскиеСчета.ДатаОткрытия,
	|	ЕСТЬNULL(МестаВыплаты.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)),
	|	ВЫБОР
	|		КОГДА МестаВыплаты.МестоВыплаты ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	БанковскиеСчета.ПометкаУдаления
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМестаВыплатыБанковскиеСчета КАК МестаВыплаты
	|		ПО БанковскиеСчета.Владелец = МестаВыплаты.ФизическоеЛицо
	|ГДЕ
	|	БанковскиеСчета.Владелец В(&ФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТИтогиБезБИК.Значение КАК Значение,
	|	ВТИтогиБезБИК.ПредставлениеЗначения КАК ПредставлениеЗначения,
	|	КлассификаторБанков.Ссылка КАК Банк,
	|	КлассификаторБанков.Код КАК БИК,
	|	КлассификаторБанков.КоррСчет КАК КоррСчет,
	|	ВТИтогиБезБИК.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТИтогиБезБИК.НомерСчета КАК НомерСчета,
	|	ВТИтогиБезБИК.ДатаОткрытияСчета КАК ДатаОткрытияСчета,
	|	ЕСТЬNULL(Организации.Ссылка, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	ЕСТЬNULL(Организации.ГоловнаяОрганизация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК ГоловнаяОрганизация,
	|	ВТИтогиБезБИК.ЯвляетсяМестомВыплатыЗарплаты КАК ЯвляетсяМестомВыплатыЗарплаты,
	|	КлассификаторБанков.Наименование КАК НаименованиеБанка,
	|	ВТИтогиБезБИК.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	ВТИтогиБезБИК КАК ВТИтогиБезБИК
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ВТИтогиБезБИК.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанков КАК КлассификаторБанков
	|		ПО ВТИтогиБезБИК.Банк.Код = КлассификаторБанков.Код";
	
	Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	
	БанковскиеРеквизиты = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
