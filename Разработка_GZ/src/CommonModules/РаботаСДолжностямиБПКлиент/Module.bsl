#Область ПрограммныйИнтерфейс

#Область СозданиеНовыхОбъектовПриВводе

// Реализация обработчика АвтоПодбор поля ввода
// Добавляет в данные выбора "предопределенные" элементы из макета НаименованияДолжностейПодписантов
//
// Параметры:
//   Форма - Форма, на которой размещен элемент поле ввода. Форма должна содержать реквизит "ДолжностиПодписантов"
//   Прочие см. описание обработчика АвтоПодбор
//
Процедура ДолжностьАвтоПодбор(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	Если НЕ ПустаяСтрока(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСДолжностямиБПВызовСервера.ДанныеВыбора(Форма.ДолжностиПодписантов, ПараметрыПолученияДанных);
	КонецЕсли;
	
КонецПроцедуры

// Реализация обработчика ОкончаниеВводаТекста поля ввода
// Добавляет в данные выбора "предопределенные" элементы из макета НаименованияДолжностейПодписантов
//
// Параметры:
//   Форма - Форма, на которой размещен элемент поле ввода. Форма должна содержать реквизит "ДолжностиПодписантов"
//   Прочие см. описание обработчика ОкончаниеВводаТекста
//
Процедура ДолжностьОкончаниеВводаТекста(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка) Экспорт
	
	Если НЕ ПустаяСтрока(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСДолжностямиБПВызовСервера.ДанныеВыбора(Форма.ДолжностиПодписантов, ПараметрыПолученияДанных);
	КонецЕсли;
	
КонецПроцедуры

// Реализация обработчика ОбработкаВыбора поля ввода
// Создает должность с выбранным наименованием по данным макета НаименованияДолжностейПодписантов
//
// Параметры:
//   Прочие см. описание обработчика ОбработкаВыбора
//
Процедура ДолжностьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка")
		И НЕ ПустаяСтрока(ВыбранноеЗначение) Тогда
		
		ВыбранноеЗначение = РаботаСДолжностямиБПВызовСервера.ДобавитьДолжностьИзМакета(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
