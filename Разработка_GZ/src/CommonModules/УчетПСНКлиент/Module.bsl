#Область ПрограммныйИнтерфейс

// Открывает форму соответствующего вида заявления
//
// Параметры:
//  ВидЗаявления - ПеречислениеСсылка.ВидыУведомленийОСпецрежимахНалогообложения - вид заявления
//  ПараметрыФормы - Структура - параметры формы заявления
//   * Организация - СправочникСсылка.Организации
//  ВладелецФормы - Управляемая форма - владелец формы
//
Процедура ОткрытьФормуЗаявления(ВидЗаявления, ПараметрыФормы, ВладелецФормы = Неопределено) Экспорт
	
	ИмяФормыЗаявления = УчетПСНВызовСервера.ИмяФормыЗаявления(ВидЗаявления);
	Если ПустаяСтрока(ИмяФормыЗаявления) Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидЗаявления = ПредопределенноеЗначение(
			"Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеОбУтратеПраваНаПатент") Тогда
		
		// При утрате права, спрашиваем у пользователя дату события
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИмяФормыЗаявления", ИмяФормыЗаявления);
		ДополнительныеПараметры.Вставить("ПараметрыФормы",    ПараметрыФормы);
		ДополнительныеПараметры.Вставить("ВладелецФормы",     ВладелецФормы);
		
		ОповещениеОВыборе = Новый ОписаниеОповещения("ВыборДатыУтратыПраваЗаявленияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ОткрытьФорму("Справочник.Патенты.Форма.ФормаВыборДатыУтратыПрава",,ВладелецФормы,,,,ОповещениеОВыборе);
		
	Иначе
		
		ОткрытьФорму(ИмяФормыЗаявления, ПараметрыФормы, ВладелецФормы);
		
	КонецЕсли;
	
КонецПроцедуры

// Дополнительная обработка перед заполнением заявления по кнопке Заполнить
//
// Параметры:
//   ПараметрыОтчета - Структура
//     * Организация - СправочникСсылка.Организации
//   ФормаОтчета - Управляемая форма - форма заявления
//   ОписаниеОповещения - ОписаниеОповещения - обратный вызов формы отчета
//
Процедура ПередЗаполнениемЗаявленияУтратаПраваНаПатент(ПараметрыОтчета, ФормаОтчета, ОписаниеОповещения) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);

	ОповещениеОВыборе = Новый ОписаниеОповещения("ПередЗаполнениемЗаявленияУтратаПраваПатентЗавершение",
		ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Справочник.Патенты.Форма.ФормаВыборДатыУтратыПрава",,ФормаОтчета,,,,ОповещениеОВыборе);
	
КонецПроцедуры

// Дополнительная обработка перед заполнением заявления по кнопке Заполнить
//
// Параметры:
//   ПараметрыОтчета - Структура
//     * Организация - СправочникСсылка.Организации
//   ФормаОтчета - Управляемая форма - форма заявления
//   ОписаниеОповещения - ОписаниеОповещения - обратный вызов формы отчета
//
Процедура ПередЗаполнениемЗаявленияПрекращениеПатента(ПараметрыОтчета, ФормаОтчета, ОписаниеОповещения) Экспорт
	
	Перем Организация;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("ПередЗаполнениемЗаявленияПрекращениеПатентаЗавершение",
		ЭтотОбъект, ДополнительныеПараметры);
	
	Отбор = Новый Структура;
	
	ПараметрыФормыВыбора = Новый Структура;
	Если ПараметрыОтчета.Свойство("Организация", Организация) И ЗначениеЗаполнено(Организация) Тогда
		Отбор.Вставить("Владелец", Организация);
	КонецЕсли;
	
	ПараметрыФормыВыбора.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("Справочник.Патенты.ФормаВыбора", ПараметрыФормыВыбора, ФормаОтчета, , , , ОповещениеОВыборе);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область СозданиеНовогоПатента

Процедура ОткрытьФормуСозданияНовогоПатента(ВладелецФормы, Организация, Дата, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("Владелец",      Организация);
	ДанныеЗаполнения.Вставить("ДатаНачала",    НачалоГода(Дата));
	ДанныеЗаполнения.Вставить("ДатаОкончания", КонецГода(Дата));
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ДанныеЗаполнения);
	
	ОткрытьФорму("Справочник.Патенты.ФормаОбъекта", ПараметрыФормы, ВладелецФормы);
	
КонецПроцедуры

Процедура ОтражениеДоходаОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	ВариантыОтраженияДоходов = БанкИКассаФормыКлиентСервер.ВариантыОтраженияДоходов(Форма);
	
	НовоеОтражениеДохода = БанкИКассаФормыКлиентСервер.ВариантОтраженияДоходовЗначение(ВыбранноеЗначение, ВариантыОтраженияДоходов);
	
	ОтражениеДоходаСоздатьПатент = УчетПСНКлиентСервер.ОтражениеДоходовСоздатьПатентЗначение();
	
	Если НовоеОтражениеДохода = ОтражениеДоходаСоздатьПатент Тогда
		
		// Первый вызов - выбран пункт "Создать патент..."
		// Перед открытием формы нового патента выбранное значение очищается и дальнейшая обработка прерывается.
		
		ВыбранноеЗначение = Неопределено;
		
		ОткрытьФормуСозданияНовогоПатента(Элемент, Объект.Организация, Объект.Дата, СтандартнаяОбработка);
		
	ИначеЕсли НовоеОтражениеДохода = Неопределено
		И ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Патенты") Тогда
		
		// После создания нового патента обработка выбора вызывается повторно.
		// Необходимо дополнить варианты отражения доходов и список выбора поля,
		// а затем продолжить выбор стандартным поведением поля: в выбранном значении заменить патент представлением нового варианта.
		
		ЭлементНовыйПатент = ВариантыОтраженияДоходов.НайтиПоЗначению(ОтражениеДоходаСоздатьПатент);
		
		Если ЭлементНовыйПатент <> Неопределено Тогда
			
			// Вставим вариант отражения доходов по новому патенту перед пунктом "Создать патент..."
			
			ПредставлениеВарианта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Доход по патенту ""%1""'"),
				ВыбранноеЗначение);
			
			ВариантыОтраженияДоходов.Вставить(ВариантыОтраженияДоходов.Индекс(ЭлементНовыйПатент), ВыбранноеЗначение, ПредставлениеВарианта);
			
			БанкИКассаФормыКлиентСервер.ОбновитьСписокВыбораОтраженияДоходов(Элемент.СписокВыбора, ВариантыОтраженияДоходов);
			
			// В выбранное значение передадим представление отражения доходов по новому патенту.
			ВыбранноеЗначение = ПредставлениеВарианта;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыборДатыУтратыПраваЗаявленияЗавершение(ДатаУтратыПрава, ДополнительныеПараметры) Экспорт
	
	Если ДатаУтратыПрава = Неопределено Или ТипЗнч(ДатаУтратыПрава) <> Тип("Дата") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДополнительныеПараметры.ПараметрыФормы);
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ДатаУтратыПрава", ДатаУтратыПрава);
	ПараметрыФормы.Вставить("ПараметрыЗаполнения", ПараметрыЗаполнения);
	
	ОткрытьФорму(
		ДополнительныеПараметры.ИмяФормыЗаявления,
		ПараметрыФормы,
		ДополнительныеПараметры.ВладелецФормы);
	
КонецПроцедуры

Процедура ПередЗаполнениемЗаявленияУтратаПраваПатентЗавершение(ДатаУтратыПрава, ДополнительныеПараметры) Экспорт
	
	Если ДатаУтратыПрава <> Неопределено Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("ДатаУтратыПрава", ДатаУтратыПрава);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещения, ПараметрыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаполнениемЗаявленияПрекращениеПатентаЗавершение(Патент, ДополнительныеПараметры) Экспорт
	
	Если Патент <> Неопределено Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Патент", Патент);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещения, ПараметрыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

