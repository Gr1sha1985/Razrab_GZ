

// Открывает форму ошибок регламентной операции.
//
// Параметры:
//  Ссылка	 - Ссылка на регламентную операцию с ошибками.
//
Процедура ОткрытьФормуОшибок(Ссылка) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Ссылка);
	ОткрытьФорму("Документ.РегламентнаяОперация.Форма.ФормаОшибок", ПараметрыФормы, ЭтотОбъект, Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// АКТУАЛИЗАЦИЯ ДАННЫХ

// Показывает описание ошибок, возникших в ходе актуализации.
//
// Параметры:
//  Форма                 - ФормаКлиентскогоПриложения - форма, из которой вызвана актуализация.
//  РезультатАктуализации - Структура - результат выполнения фонового задания актуализации,
//                                      см. ЗакрытиеМесяцаКлиентСервер.НовыйРезультатАктуализации().
//  ТекстПредупреждения   - Строка - текст выводимого предупреждения об ошибке; если не передан - будет показан текст по умолчанию.
//
Процедура ПоказатьОшибкиАктуализации(Форма, РезультатАктуализации, Знач ТекстПредупреждения = "") Экспорт
	
	Если РезультатАктуализации.Выполнено
	   И Не РезультатАктуализации.ВывестиИнформациюУведомлений Тогда
		Возврат;
	КонецЕсли;
	
	// Ошибки восстановления последовательности.
	Если РезультатАктуализации.Свойство("АдресХранилищаСОшибками") Тогда
		
		ОбщегоНазначенияБПКлиент.ОткрытьФормуОшибокПерепроведения(Форма, РезультатАктуализации.АдресХранилищаСОшибками);
		
	// Ошибки закрытия месяца.
	ИначеЕсли РезультатАктуализации.Свойство("РегламентнаяОперацияСОшибками") Тогда 
	
		ОткрытьФормуОшибок(РезультатАктуализации.РегламентнаяОперацияСОшибками);
		
	Иначе
		
		Если РезультатАктуализации.Свойство("СообщенияПользователю") Тогда
			Для каждого ТекстСообщения Из РезультатАктуализации.СообщенияПользователю Цикл
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецЦикла;
		КонецЕсли;
		Если РезультатАктуализации.Свойство("ВидОперации") Тогда
			ТекстСообщения = НСтр("ru = 'Ошибка при выполнении операции ""%1"" за %2 г.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, 
				РезультатАктуализации.ВидОперации,
				Формат(РезультатАктуализации.ПериодРегистрации, "ДФ='MMMM yyyy'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстПредупреждения) Тогда
		ТекстПредупреждения = НСтр("ru='Актуализация не выполнена. Обнаружены ошибки.'");
	КонецЕсли;
	
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

Процедура Актуализировать(Форма, Организация, Период = Неопределено) Экспорт
	
	// Если настройки текущего отчета изменились после проверки актуальности в последний раз,
	// то запускать актуализацию не будем, т.к. возможно, что с новыми настройками актуализация должна
	// выполняться иным образом, чем со старыми.
	// Для проверки такой ситуации используем свойство ОтображениеСостояния табличного документа,
	// которое устанавливается при изменении настроек отчета.
	ПолеТабличногоДокумента = Форма.Элементы.Найти("Результат");
	Если ПолеТабличногоДокумента <> Неопределено 
		И ПолеТабличногоДокумента.Вид = ВидПоляФормы.ПолеТабличногоДокумента Тогда
		ОтображениеСостояния = ПолеТабличногоДокумента.ОтображениеСостояния;
		Если ОтображениеСостояния.Видимость Тогда
			ПоказатьПредупреждение(, ОтображениеСостояния.Текст);
			СкрытьПанельАктуализации(Форма, Истина);
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ВыполняемоеЗадание = ЗакрытиеМесяцаВызовСервера.НайтиФоновоеЗаданиеАктуализацииПоОрганизации(Организация, Форма.УникальныйИдентификатор);
	
	Если ВыполняемоеЗадание = Неопределено Тогда
		АктуализироватьДанные(Форма, Организация, Период);
	Иначе
		ОтобразитьСостояниеАктуализации(Форма, ВыполняемоеЗадание);	
		Форма.Элементы.Актуализация.Видимость = Истина;
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьЗавершениеАктуализации", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура АктуализироватьДанные(Форма, Организация, Период = Неопределено)
	
	Элементы = Форма.Элементы;
	
	Форма.ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	
	ОтобразитьСостояниеАктуализации(Форма);
	
	ПараметрыАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыАктуализации();
	ПараметрыАктуализации.Организация                               = Организация;
	ПараметрыАктуализации.Период                                    = Период;
	ПараметрыАктуализации.ИдентификаторЗадания                      = Форма.ИдентификаторЗаданияАктуализации;
	ПараметрыАктуализации.УникальныйИдентификаторФормы              = Форма.УникальныйИдентификатор;
	ПараметрыАктуализации.ПроверятьКонстантуАктуальностиДанныхУчета = Истина;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Отчет") Тогда // Актуальность может проверяться в форме обработки
		ОтборыРасчетыСКонтрагентами = БухгалтерскиеОтчетыКлиентСервер.ОпределитьПараметрыОтчетаРасчетыСКонтрагентами(Форма);
		ПараметрыАктуализации.ОтборыРасчетыСКонтрагентами = ОтборыРасчетыСКонтрагентами;
		ПодготовитьДанныеРасшифровкиДляФоновогоЗадания(Форма, ОтборыРасчетыСКонтрагентами, ПараметрыАктуализации);
	КонецЕсли;
	
	РезультатВыполнения = ЗакрытиеМесяцаВызовСервера.АктуализироватьДанные(ПараметрыАктуализации);
	
	Форма.АдресХранилищаАктуализации = РезультатВыполнения.АдресХранилища;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		ОбработатьРезультатАктуализации(Форма, Организация, Период);
	Иначе
		Форма.ПрогрессорАктуализации = "0%.";
		Форма.ИдентификаторЗаданияАктуализации = РезультатВыполнения.ИдентификаторЗадания;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(Форма.ПараметрыОбработчикаОжиданияАктуализации);
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации", 1, Истина);
	КонецЕсли;

КонецПроцедуры

Процедура ОтменитьАктуализацию(Форма, Организация, Период = Неопределено) Экспорт
	
	Форма.ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	ЗакрытиеМесяцаВызовСервера.ОтменитьВыполнениеЗадания(Форма.ИдентификаторЗаданияАктуализации);
	Форма.ИдентификаторЗаданияАктуализации = Неопределено;
	
	ПроверитьАктуальностьДанных(Форма, Организация, Период);
	
	ПараметрыОповещения = Новый Структура("Организация", Организация);
	Оповестить("АктуализацияОтменена", ПараметрыОповещения);
	
КонецПроцедуры

Процедура ПроверитьВыполнениеАктуализацииОтчета(Форма, Организация, Период = Неопределено) Экспорт

	Если ЗакрытиеМесяцаВызовСервера.ЗаданиеВыполнено(Форма.ИдентификаторЗаданияАктуализации) Тогда
		ОбработатьРезультатАктуализации(Форма, Организация, Период);
		Возврат;
	КонецЕсли;
		
	ОбновитьПроцентПрогресса(Форма);
	ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(Форма.ПараметрыОбработчикаОжиданияАктуализации);
	Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации",
		Форма.ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал, Истина);

КонецПроцедуры

Процедура ОбработатьРезультатАктуализации(Форма, Организация, Период = Неопределено) Экспорт
	
	Перем РезультатАктуализации;
	
	Форма.ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	
	Если ЭтоАдресВременногоХранилища(Форма.АдресХранилищаАктуализации) Тогда
		РезультатАктуализации = ПолучитьИзВременногоХранилища(Форма.АдресХранилищаАктуализации);
		УдалитьИзВременногоХранилища(Форма.АдресХранилищаАктуализации);
	КонецЕсли;
	Форма.АдресХранилищаАктуализации = "";
	Если ТипЗнч(РезультатАктуализации) <> Тип("Структура")
	 Или Не РезультатАктуализации.Свойство("Выполнено") Тогда
		РезультатАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыйРезультатАктуализации();
	КонецЕсли;

	Если РезультатАктуализации.Свойство("ВывестиИнформациюУведомлений")
		И РезультатАктуализации.ВывестиИнформациюУведомлений
	 Или Не РезультатАктуализации.Выполнено Тогда
		ПоказатьОшибкиАктуализации(Форма, РезультатАктуализации);
	КонецЕсли;
	
	Форма.ИдентификаторЗаданияАктуализации = Неопределено;
	
	// Оповестим другие формы о событии актуализации.
	ПараметрыОповещения = Новый Структура("Организация", Организация);
	ИмяСобытия = "АктуализацияЗавершенаУспешно";
	Если РезультатАктуализации.Выполнено Тогда
		Если Не РезультатАктуализации.ПолнаяАктуализация Тогда
			// После повторного формирования будут актуальны те данные, которые отображаются в текущем отчете.
			// При полной актуализации были бы актуальны все данные.
			БухгалтерскийУчетКлиентПереопределяемый.ОбработкаОповещенияАктуализации(
				Форма, Организация, РезультатАктуализации.ПределАктуализации, ИмяСобытия, ПараметрыОповещения, Форма);
			ИмяСобытия = "";
		КонецЕсли;
	Иначе 
		Форма.Элементы.Актуализация.Видимость = Истина;
		ИмяСобытия = "АктуализацияОтменена";
	КонецЕсли;
	Если Не ПустаяСтрока(ИмяСобытия) Тогда
		Оповестить(ИмяСобытия, ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗавершениеАктуализации(Форма, Организация, Период = Неопределено) Экспорт
	
	ВыполняемоеЗадание = ЗакрытиеМесяцаВызовСервера.НайтиФоновоеЗаданиеАктуализацииПоОрганизации(Организация, Форма.УникальныйИдентификатор);
	
	Если ВыполняемоеЗадание = Неопределено Тогда
		Форма.ОтключитьОбработчикОжидания("Подключаемый_ПроверитьЗавершениеАктуализации");
		ПроверитьАктуальностьДанных(Форма, Организация, Период);
	Иначе
		ОтобразитьСостояниеАктуализации(Форма, ВыполняемоеЗадание);
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьЗавершениеАктуализации", 1, Истина);
	КонецЕсли;

КонецПроцедуры

Процедура ОтобразитьСостояниеАктуализации(Форма, ВыполняемоеЗаданиеАктуализации = Неопределено) 
	
	ИдетАктуализация = ВыполняемоеЗаданиеАктуализации = Неопределено;
	
	Элементы = Форма.Элементы;
	Элементы.РисПриАктуализации.Видимость = ИдетАктуализация;
	Элементы.ПрогрессорАктуализации.Видимость = ИдетАктуализация;
	Элементы.ОтменитьАктуализацию.Видимость = ИдетАктуализация;
	
	Если ИдетАктуализация Тогда
		
		Элементы.ТекстПриАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
		Элементы.СкрытьПриАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
		Элементы.ИдетАктуализация.ЦветФона = Новый Цвет;
		
		ТекстПриАктуализации = НСтр("ru='Данные этого отчета могут измениться.'");
		Элементы.ТекстПриАктуализации.Заголовок = ТекстПриАктуализации;
		
	Иначе // Актуализация выполняется в другом отчете или другим пользователем.
		
		Элементы.ТекстПриАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Низ;
		Элементы.СкрытьПриАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Низ;
		Элементы.ИдетАктуализация.ЦветФона = ЗакрытиеМесяцаВызовСервера.ПолучитьПредупреждающийЦвет();
		
		Если ПустаяСтрока(ВыполняемоеЗаданиеАктуализации.УникальныйИдентификатор) Тогда // Актуализация выполняется другим пользователем в файловом режиме.
			ТекстПриАктуализации = НСтр("ru='Актуализация данных, запущенная другим пользователем, еще не закончилась. Данные этого отчета могут быть неактуальны.'");
			Элементы.ТекстПриАктуализации.Заголовок = ТекстПриАктуализации;
		ИначеЕсли ВыполняемоеЗаданиеАктуализации.Пользователь <> ПользователиКлиентСервер.ТекущийПользователь() Тогда // Актуализация выполняется другим пользователем в клиент-серверном режиме.
			ТекстПриАктуализации = НСтр("ru='Идет актуализация данных пользователем %1. Данные этого отчета могут быть неактуальны.'");
			ТекстПриАктуализации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПриАктуализации, ВыполняемоеЗаданиеАктуализации.Пользователь);
			Элементы.ТекстПриАктуализации.Заголовок = ТекстПриАктуализации;
		Иначе // Актуализация выполняется текущим пользователем в другом отчете.
			ТекстПриАктуализации = НСтр("ru='Актуализация данных, запущенная в другом отчете, еще не закончилась. Данные этого отчета могут быть неактуальны.'");
			Элементы.ТекстПриАктуализации.Заголовок = ТекстПриАктуализации;
		КонецЕсли;
		
	КонецЕсли;
	
	СброситьСостояниеАктуализации(Элементы);
	Элементы.ИдетАктуализация.Видимость = Истина;
	
КонецПроцедуры

Процедура ОбработкаОповещенияАктуализации(Форма, Организация, Период, ИмяСобытия, Параметр, Источник) Экспорт
	
	Элементы = Форма.Элементы;
	
	// В формах отчетов информация об актуальности данных выводится, 
	// только если поле Результат доступно для просмотра.
	Если Элементы.Найти("РезультатСхемы") <> Неопределено Тогда
		ВыводитьИнформацию = Истина;
	Иначе
		Попытка
			ВыводитьИнформацию = Элементы.Результат.ОтображениеСостояния.Видимость = Ложь;
		Исключение
			ВыводитьИнформацию = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если НЕ ВыводитьИнформацию Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "АктуализацияЗавершенаУспешно" И Организация = Параметр.Организация Тогда
		
		Форма.ОтключитьОбработчикОжидания("Подключаемый_ПроверитьЗавершениеАктуализации");
		СброситьСостояниеАктуализации(Элементы);
		
		ПорядокАктуализации = ЗакрытиеМесяцаВызовСервера.ПорядокАктуализацииОтчета(Форма.ИмяФормы);
		
		Если ПорядокАктуализации = 2 Тогда
			
			РеквизитыСтандартныхОтчетов = Новый Структура("Счет");
			Отчет = Форма.Отчет;
			ЗаполнитьЗначенияСвойств(РеквизитыСтандартныхОтчетов, Отчет);
			
			Если ЗначениеЗаполнено(РеквизитыСтандартныхОтчетов.Счет)
			   И УчетНДСВызовСервера.ЭтоСчетУчетаНДС(РеквизитыСтандартныхОтчетов.Счет) Тогда
				ПроверитьАктуальностьНДС(Организация, Форма);
			Иначе
				Элементы.ДанныеАктуализированы.Видимость = Истина;
				Элементы.Актуализация.Видимость = Истина;
			КонецЕсли;
			
		ИначеЕсли ПорядокАктуализации = 3 Тогда
			ПроверитьАктуальностьНДС(Организация, Форма);
		Иначе
			Элементы.ДанныеАктуализированы.Видимость = Истина;
			Элементы.Актуализация.Видимость = Истина;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "АктуализацияОтменена" И Организация = Параметр.Организация Тогда
		Форма.ОтключитьОбработчикОжидания("Подключаемый_ПроверитьЗавершениеАктуализации");
		Если Элементы.Актуализация.Видимость Тогда
			ПроверитьАктуальность(Форма, Организация, Период);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СкрытьПанельАктуализацииАвтоматически(Форма) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Форма.ИдентификаторЗаданияАктуализации) Тогда
		СкрытьПанельАктуализации(Форма, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура СкрытьПанельАктуализации(Форма, АвтоматическийВызов = Ложь) Экспорт
	
	ЗакрытиеМесяцаКлиентСервер.СкрытьПанельАктуализации(Форма);
	
	Если НЕ АвтоматическийВызов Тогда
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "АктуализацияДанныхСкрыть");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьПроцентПрогресса(Форма, Знач ИдентификаторЗадания = Неопределено) Экспорт
	Перем Процент;
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ИдентификаторЗадания = Форма.ИдентификаторЗаданияАктуализации;
	КонецЕсли;
	
	Прогресс = ЗакрытиеМесяцаВызовСервера.ПрочитатьПрогресс(ИдентификаторЗадания);
	Если Прогресс.Свойство("Процент", Процент) Тогда
		// Все проценты выше 99 отображаются как "99%".
		Форма.ПрогрессорАктуализации = Формат(Процент, "ЧЦ=2; ЧН=0; ЧГ=0") + "%.";
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает нажатие на ссылку "Закрытие месяца" в отчете, открывает закрытие месяца на дату актуальности.
//
// Параметры:
//	НавигационнаяСсылка           - Строка - Строка навигационной ссылки, по которой произошло нажатие.
//	СтандартнаяОбработка          - Булево - Флаг стандартной обработки навигационной ссылки.
//	ФормаОтчета                   - ФормаКлиентскогоПриложения - Форма отчета, в котором проводилась проверка актуальности.
//	ПараметрыАктуализации         - Структура - Параметры, которые нужны для выполнения актуализации и возврата в отчет. 
//	                                Подробнее см. ЗакрытиеМесяцаКлиентСервер.НовыйПараметрыАктуализацииОтчета().
//
Процедура ТекстПриНеобходимостиАктуализацииОбработкаНавигационнойСсылки(НавигационнаяСсылка, СтандартнаяОбработка, ФормаОтчета, ПараметрыАктуализации) Экспорт
	
	Если СтрНайти(НавигационнаяСсылка, "ЗакрытиеМесяца") <> 0 Тогда

		СтандартнаяОбработка = Ложь;
		
		Оповещение = Новый ОписаниеОповещения("ЗакрытиеМесяцаЗавершение", ФормаОтчета);
		ФормаЗакрытияМесяца = ОткрытьФорму("Обработка.ЗакрытиеМесяца.Форма.Форма", ПараметрыАктуализации, , , , , Оповещение);
		ФормаОтчета.ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ЗакрытиеОтчетаОповещение", ФормаЗакрытияМесяца);
		
	ИначеЕсли СтрНайти(НавигационнаяСсылка, "ПомощникРасчетаНДС") <> 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыАктуализации.Вставить("ПериодСобытия", ПараметрыАктуализации.ДатаОкончанияАктуализации);
		ПараметрыАктуализации.Вставить("КонтекстныйВызов", Истина);
		
		Оповещение = Новый ОписаниеОповещения("ЗакрытиеМесяцаЗавершение", ФормаОтчета);
		ФормаЗакрытияМесяца = ОткрытьФорму("Обработка.ПомощникРасчетаНДС.Форма.Форма", ПараметрыАктуализации, , , , , Оповещение);
		
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "АктуализацияДанныхРасчетНДС");
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОВЕРКА АКТУАЛЬНОСТИ

Процедура ПодключитьПроверкуАктуальности(Форма) Экспорт
	
	Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьАктуальность", 0.7, Истина);
	
КонецПроцедуры

Процедура ПроверитьАктуальность(Форма, Организация, Период = Неопределено) Экспорт
	
	Если НЕ ЗакрытиеМесяцаВызовСервера.ПроверятьАктуальность() Тогда
		СкрытьПанельАктуализацииАвтоматически(Форма);
		Возврат;
	КонецЕсли;
	
	Форма.ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	Форма.ОтключитьОбработчикОжидания("Подключаемый_ПроверитьЗавершениеАктуализации");
	
	ВыполняемоеЗадание = ЗакрытиеМесяцаВызовСервера.НайтиФоновоеЗаданиеАктуализацииПоОрганизации(Организация, Форма.УникальныйИдентификатор);
	
	Если ВыполняемоеЗадание = Неопределено Тогда
		ПроверитьАктуальностьДанных(Форма, Организация, Период);
	Иначе
		Если Форма.ИдентификаторЗаданияАктуализации = ВыполняемоеЗадание.УникальныйИдентификатор Тогда // Задание дано этим отчетом.
			ОтобразитьСостояниеАктуализации(Форма);	
			ОбновитьПроцентПрогресса(Форма);
			Форма.Элементы.Актуализация.Видимость = Истина;
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(Форма.ПараметрыОбработчикаОжиданияАктуализации);
			Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации", 1, Истина);
		Иначе // Задание дал другой отчет.
			ОтобразитьСостояниеАктуализации(Форма, ВыполняемоеЗадание);	
			Форма.Элементы.Актуализация.Видимость = Истина;
			Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьЗавершениеАктуализации", 1, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СброситьСостояниеАктуализации(Элементы)
	
	Элементы.ИдетПроверкаАктуальности.Видимость	= Ложь;
	Элементы.ТребуетсяАктуализация.Видимость	= Ложь;
	Элементы.ИдетАктуализация.Видимость			= Ложь;
	Элементы.ДанныеАктуализированы.Видимость	= Ложь;

КонецПроцедуры

Процедура ПроверитьАктуальностьДанных(Форма, Организация, Период = Неопределено)
	
	Элементы = Форма.Элементы;
	
	СброситьСостояниеАктуализации(Элементы);
	Элементы.ИдетПроверкаАктуальности.Видимость = Истина;
	УИДЗамера = ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Ложь, "АктуализацияДанныхТребуетсяАктуализацияЗаПериод");
	
	ПараметрыПроверки = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыПроверкиАктуальности();
	ПараметрыПроверки.Организация                  = Организация;
	ПараметрыПроверки.Период                       = Период;
	ПараметрыПроверки.УИДЗамера                    = УИДЗамера;
	ПараметрыПроверки.УникальныйИдентификаторФормы = Форма.УникальныйИдентификатор;
	ПараметрыПроверки.ПроверятьКонстантуАктуальностиДанныхУчета = Истина;
	
	ПроверятьАктуальностьНДС = Ложь;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Отчет") Тогда
	// Актуальность может проверяться в форме обработки
	
		ПорядокАктуализации = ЗакрытиеМесяцаВызовСервера.ПорядокАктуализацииОтчета(Форма.ИмяФормы);
		РеквизитыСтандартныхОтчетов = Новый Структура("Счет");
		
		Если ПорядокАктуализации = 1 Тогда
			
			// Актуализация НДС
			ПроверитьАктуальностьНДС(Организация, Форма);
			Возврат;
			
		ИначеЕсли ПорядокАктуализации = 2 Тогда
			
			Отчет = Форма.Отчет;
			ЗаполнитьЗначенияСвойств(РеквизитыСтандартныхОтчетов, Отчет);
			
			Если ЗначениеЗаполнено(РеквизитыСтандартныхОтчетов.Счет)
			   И УчетНДСВызовСервера.ЭтоСчетУчетаНДС(РеквизитыСтандартныхОтчетов.Счет) Тогда
				
				// Актуализация НДС
				ПроверитьАктуальностьНДС(Организация, Форма);
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ПорядокАктуализации = 3 Тогда
		
			// Актуализация БУ и НДС
			ПроверятьАктуальностьНДС = Истина;
		
		КонецЕсли;
		
		ОтборыРасчетыСКонтрагентами = БухгалтерскиеОтчетыКлиентСервер.ОпределитьПараметрыОтчетаРасчетыСКонтрагентами(Форма);
		ПодготовитьДанныеРасшифровкиДляФоновогоЗадания(Форма, ОтборыРасчетыСКонтрагентами, ПараметрыПроверки);
		ПараметрыПроверки.ОтборыРасчетыСКонтрагентами = ОтборыРасчетыСКонтрагентами;
		
	КонецЕсли;
	
	РезультатВыполнения = ЗакрытиеМесяцаВызовСервера.ПроверитьАктуальность(ПараметрыПроверки);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("Организация", Организация);
	ДополнительныеПараметры.Вставить("ПроверятьАктуальностьНДС", ПроверятьАктуальностьНДС);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатПроверкиАктуальности", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ПроверитьАктуальностьНДС(Организация, Форма)
	
	ПараметрыОтчета = Форма.Отчет;
	Элементы        = Форма.Элементы;
	
	Если ПараметрыОтчета.Свойство("НачалоПериода")
		И ПараметрыОтчета.Свойство("КонецПериода") Тогда 
		ТребуетсяАктуализацияРасчетаНДС = УчетНДСВызовСервера.ТребуетсяАктуализацияРасчетаНДС(
			Организация, ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	Иначе
		ТребуетсяАктуализацияРасчетаНДС = Ложь;
	КонецЕсли;
	
	Если ТребуетсяАктуализацияРасчетаНДС Тогда
		
		МассивПодстрок = Новый Массив;
		МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Данные учета НДС неактуальны.'")));
		МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(" " + НСтр("ru='Рекомендуется выполнить '")));
		МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='расчет НДС'"),,,, "e1cib/app/Обработка.ПомощникРасчетаНДС"));
		МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ' и сформировать отчет повторно.'")));
		
		Элементы.ТекстПриНеобходимостиАктуализации.ОтображениеПодсказки   = ОтображениеПодсказки.Нет;
		Элементы.ТекстПриНеобходимостиАктуализации.ВертикальноеПоложение  = ВертикальноеПоложениеЭлемента.Низ;
		Элементы.СкрытьПриНеобходимостиАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Низ;
		
		Элементы.ТекстПриНеобходимостиАктуализации.Заголовок = Новый ФорматированнаяСтрока(МассивПодстрок);
		
		СброситьСостояниеАктуализации(Элементы);
		
		Элементы.ТребуетсяАктуализация.Видимость = Истина;
		Элементы.Актуализация.Видимость          = Истина;
		Элементы.Актуализировать.Видимость       = Ложь;
		
	Иначе
		Элементы.Актуализация.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьРезультатПроверкиАктуальности(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	УдалитьИзВременногоХранилища(Результат.АдресРезультата);

	Форма = ДополнительныеПараметры.Форма;
	Элементы = Форма.Элементы;
	
	Если ТипЗнч(РезультатПроверки) <> Тип("Структура") Тогда
		РезультатПроверки = Новый Структура;
	КонецЕсли;
	Если Не РезультатПроверки.Свойство("ТребуетсяАктуализация") Тогда
		РезультатПроверки.Вставить("ТребуетсяАктуализация", Ложь);
	КонецЕсли;
		
	Если НЕ РезультатПроверки.ТребуетсяАктуализация Тогда
		
		Если ДополнительныеПараметры.ПроверятьАктуальностьНДС Тогда
			ПроверитьАктуальностьНДС(ДополнительныеПараметры.Организация, Форма);
		Иначе
			Элементы.Актуализация.Видимость = Ложь;
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Форма.ДатаАктуальности = РезультатПроверки.ДатаАктуальности;
	МассивПодстрок = Новый Массив;
	ТекстДанныеУчетаНеАктуальны = НСтр("ru='Данные учета неактуальны с '");
	МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(ТекстДанныеУчетаНеАктуальны));
	МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(Формат(Форма.ДатаАктуальности, "ДЛФ=D"), Новый Шрифт(,, Истина)));
	МассивПодстрок.Добавить(Новый ФорматированнаяСтрока("."));
	
	Если НЕ РезультатПроверки.АктуализацияДоступна Тогда
		Элементы.ТекстПриНеобходимостиАктуализации.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
		Элементы.ТекстПриНеобходимостиАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Низ;
		Элементы.СкрытьПриНеобходимостиАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Низ;
	Иначе
		Если РезультатПроверки.АктуализацияВозможна Тогда
			Элементы.ТекстПриНеобходимостиАктуализации.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
			Элементы.ТекстПриНеобходимостиАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
			Элементы.СкрытьПриНеобходимостиАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Центр;
		Иначе
			МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(" " + НСтр("ru='Рекомендуется выполнить '")));
			МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='закрытие месяца'"),,,, "e1cib/app/Обработка.ЗакрытиеМесяца"));
			МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ' и сформировать отчет повторно.'")));
			Элементы.ТекстПриНеобходимостиАктуализации.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
			Элементы.ТекстПриНеобходимостиАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Низ;
			Элементы.СкрытьПриНеобходимостиАктуализации.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Низ;
		КонецЕсли;
	КонецЕсли;

	Элементы.Актуализировать.Видимость = РезультатПроверки.АктуализацияДоступна И РезультатПроверки.АктуализацияВозможна;
	Элементы.ТекстПриНеобходимостиАктуализации.Заголовок = Новый ФорматированнаяСтрока(МассивПодстрок);

	СброситьСостояниеАктуализации(Элементы);
	Элементы.ТребуетсяАктуализация.Видимость = Истина;
	Элементы.Актуализация.Видимость = Истина;
	
	Если РезультатПроверки.УИДЗамера <> Неопределено Тогда
		ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(РезультатПроверки.УИДЗамера);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИНДИКАЦИЯ ХОДА ВЫПОЛНЕНИЯ

Функция ОписаниеПроцессаДлительнойОперации() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьАктуализациюРасчетовСКонтрагентами", Ложь);
	Результат.Вставить("ИспользоватьЗакрытиеМесяца", Ложь);
	Результат.Вставить("ИспользоватьПерепроведениеДокументов", Ложь);
	Результат.Вставить("ИспользоватьПереносГраницы", Ложь);
	Результат.Вставить("КоличествоМесяцев", 1);
	Результат.Вставить("КоличествоОрганизаций", 1);
	Результат.Вставить("Месяц", '0001-01-01');
	Результат.Вставить("Организация"); // СправочникСсылка.Организации
	
	Возврат Результат;
	
КонецФункции

// Открывает форму индикации хода длительной операции.
// 
// Параметры:
//  ВладелецФормы        - ФормаКлиентскогоПриложения - форма, из которой производится открытие. 
//  ИдентификаторЗадания - УникальныйИдентификатор - Идентификатор фонового задания.
//  ОписаниеПроцесса     - Структура - см. ОписаниеПроцессаДлительнойОперации()
//
// Возвращаемое значение:
//  ФормаКлиентскогоПриложения     - ссылка на открытую форму.
// 
Функция ОткрытьФормуДлительнойОперации(Знач ВладелецФормы, Знач ИдентификаторЗадания, ОписаниеПроцесса) Экспорт
	
	ПараметрыФормыИндикации = Новый Структура;
	ПараметрыФормыИндикации.Вставить("ИдентификаторЗадания",   ИдентификаторЗадания);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыФормыИндикации, ОписаниеПроцесса, Ложь);
	
	ФормаДлительнойОперации = ОткрытьФорму("ОбщаяФорма.ИндикацияХодаПроцесса", ПараметрыФормыИндикации, ВладелецФормы);
	
	Возврат ФормаДлительнойОперации;
	
КонецФункции

// Читает сообщение от фонового задания и обновляет индикатор у составной длительной операции.
//
// Параметры:
//  ФормаСоставнойДлительнойОперации - ФормаКлиентскогоПриложения - форма с прогресс-баром.
//
// Возвращаемое значение:
//   Неопределено - если фоновое задание не найдено.
//   Булево      - Истина, если сообщение от фонового задания было; иначе Ложь.
//
Функция ВыполненШагДлительнойОперации(Форма) Экспорт
	
	ИдентификаторЗадания = Форма.ИдентификаторЗадания;
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Получаем долю (число от 0 до 1) шагов, которые сделаны.
	Прогресс = ЗакрытиеМесяцаВызовСервера.ПрочитатьПрогресс(ИдентификаторЗадания);
	
	ВыполненШаг = Неопределено;
	Если Прогресс.ЗаданиеВыполнено Тогда
		
		// фоновое задание не найдено
		
	ИначеЕсли Прогресс.Свойство("ДополнительныеПараметры")
		И ТипЗнч(Прогресс.ДополнительныеПараметры) = Тип("Структура") Тогда // Отражаем информацию о выполнении части операции
		
		ТекстСообщения = ?(Прогресс.Свойство("Текст"), Прогресс.Текст, "");
		
		Форма.ОбновитьСостояниеВыполнения(Прогресс.ДополнительныеПараметры, ТекстСообщения);

		ВыполненШаг = Истина;
		
	Иначе // немного продвигаем индикатор на один из условных этапов внутри шага
		
		Форма.ПродвинутьПрогрессБезСобытия();
		ВыполненШаг = Ложь;
		
	КонецЕсли;
	
	Возврат ВыполненШаг;
	
КонецФункции

// Уменьшает текущий интервал выполнения задания в два раза. Применяется после выполнения очередного шага составной
// длительной операции.
//
// Параметры:
//  ПараметрыОбработчикаОжидания - Структура - см. ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания()
//
Процедура ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания) Экспорт
	
	ПараметрыОбработчикаОжидания.ТекущийИнтервал = Макс(ПараметрыОбработчикаОжидания.МинимальныйИнтервал, ПараметрыОбработчикаОжидания.ТекущийИнтервал / 2);
	
КонецПроцедуры

// Закрывает форму индикации хода выполнения длительной операции.
// 
// Параметры:
//  ФормаСоставнойДлительнойОперации - ФормаКлиентскогоПриложения - форма, из которой производилось открытие. 
//
Процедура ЗакрытьФормуДлительнойОперации(ФормаСоставнойДлительнойОперации) Экспорт
	
	ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаСоставнойДлительнойОперации);
	
КонецПроцедуры

Процедура ПодготовитьДанныеРасшифровкиДляФоновогоЗадания(Форма, ОтборыРасчетыСКонтрагентами, ПараметрыЗадания)

	Если ОтборыРасчетыСКонтрагентами.ЕстьВалюта Тогда
		// В настройках отчета включен показ валютной суммы, но фактически данных по валюте может и не быть,
		// поэтому передадим в фоновое задание проверки актуальности данные расшифровки отчета для дополнительного анализа.
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ДанныеРасшифровки") Тогда
			ПараметрыЗадания.Вставить("АдресХранилищаДанныеРасшифровки", Форма.ДанныеРасшифровки);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры
