
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Клиентские методы формы записи регистра сведений НастройкиУчетаНалогаНаПрибыль
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытийЭлементовШапкиФормы

Процедура ПоложенияПереходногоПериодаУСНПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.НастройкиУчетаНалогаНаПрибыль;
	
	НастройкиУчетаНалогаНаПрибыльФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

Процедура СтавкиНалогаНаПрибыльВБюджетСубъектовРФ(Форма, Команда) Экспорт
	
	Запись = Форма.НастройкиУчетаНалогаНаПрибыль;
	
	Отбор = Новый Структура("Организация", Запись.Организация);
	
	ПараметрыФормы = Новый Структура("Организация", Запись.Организация);
	
	ОткрытьФорму("РегистрСведений.СтавкиНалогаНаПрибыльВБюджетСубъектовРФ.ФормаСписка",
		Новый Структура("Отбор", Отбор),
		Форма,
		Форма.УникальныйИдентификатор);
	
КонецПроцедуры

Процедура НоменклатурныеГруппыРеализацииПродукцииУслуг(Форма, Команда) Экспорт
	
	ПараметрыОтбора = Новый Структура("Организация", Форма.ГоловнаяОрганизация);
	
	ОткрытьФорму("РегистрСведений.НоменклатурныеГруппыРеализацииПродукцииУслуг.ФормаСписка",
		Новый Структура("Отбор", ПараметрыОтбора),
		Форма,
		Форма.УникальныйИдентификатор);
	
КонецПроцедуры

Процедура ПереченьПрямыхРасходов(Форма, Команда) Экспорт
	
	Запись = Форма.НастройкиУчетаНалогаНаПрибыль;
	
	Если Форма.ПравоРедактированияСпискаПрямыхРасходов Тогда
	
		НаличиеСписков = НастройкиУчетаНалогаНаПрибыльФормыВызовСервера.НаличиеСписковПрямыхРасходов(Форма.ГоловнаяОрганизация, Запись.Период);
		ЗаголовокДиалогаВопроса = НСтр("ru = 'Методы определения прямых расходов НУ'");

		Если Не НаличиеСписков.ЕстьСписокТекущегоПериода И Не НаличиеСписков.ЕстьСписокПрошлогоПериода Тогда

			ТекстВопроса = НСтр("ru = 'Список пуст.
			|
			|Создать записи, соответствующие рекомендациям ст.318 НК РФ?'");
			Оповещение = Новый ОписаниеОповещения("ВопросСоздатьЗаписиСоответствующиеРекомендациямНКЗавершение", ЭтотОбъект, Форма);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ЗаголовокДиалогаВопроса);
			Возврат;

		ИначеЕсли Не НаличиеСписков.ЕстьСписокТекущегоПериода И НаличиеСписков.ЕстьСписокПрошлогоПериода Тогда

			ТекстВопроса = НСтр("ru = 'Список пуст.		
			|
			| Скопировать записи прошлого периода?'");
			Оповещение = Новый ОписаниеОповещения("ВопросСкопироватьСписокПрошлогоПериодаЗавершение", ЭтотОбъект, Форма);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ЗаголовокДиалогаВопроса);
			Возврат;

		КонецЕсли;
		
	КонецЕсли;

	МетодыОпределенияПрямыхРасходовПроизводстваНУЗавершение(Форма, Ложь, Ложь);
	
КонецПроцедуры

#КонецОбласти

Процедура ВопросСкопироватьСписокПрошлогоПериодаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		МетодыОпределенияПрямыхРасходовПроизводстваНУЗавершение(ДополнительныеПараметры, Ложь, Истина);
	Иначе
		ЗаголовокДиалогаВопроса = НСтр("ru = 'Методы определения прямых расходов НУ'");
		ТекстВопроса = НСтр("ru = 'Список пуст.
		|
		|Создать записи, соответствующие рекомендациям ст.318 НК РФ?'");
		Оповещение = Новый ОписаниеОповещения("ВопросСоздатьЗаписиСоответствующиеРекомендациямНКЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ЗаголовокДиалогаВопроса);
	КонецЕсли;   
		
КонецПроцедуры

Процедура ВопросСоздатьЗаписиСоответствующиеРекомендациямНКЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьСписокПоРекомендациям = (Результат = КодВозвратаДиалога.Да);
	
	МетодыОпределенияПрямыхРасходовПроизводстваНУЗавершение(ДополнительныеПараметры, ЗаполнитьСписокПоРекомендациям, Ложь);
	
КонецПроцедуры

Процедура МетодыОпределенияПрямыхРасходовПроизводстваНУЗавершение(Форма, ЗаполнитьСписокПоРекомендациям, СкопироватьСписокПрошлогоПериода)
	
	Запись = Форма.НастройкиУчетаНалогаНаПрибыль;

	Если ЗаполнитьСписокПоРекомендациям ИЛИ СкопироватьСписокПрошлогоПериода Тогда
		НастройкиУчетаНалогаНаПрибыльФормыВызовСервера.ЗаполнитьСписокПрямыхРасходов(Запись.Организация, Запись.Период, СкопироватьСписокПрошлогоПериода);
	КонецЕсли;

	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Организация",    Форма.ГоловнаяОрганизация);
	ПараметрыОтбора.Вставить("ПериодДействия", НачалоГода(Запись.Период));// В регистре допускаются записи только на начало года.
	
	ОткрытьФорму("РегистрСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ.ФормаСписка",
		Новый Структура("Отбор", ПараметрыОтбора),
		Форма,
		Форма.УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти

