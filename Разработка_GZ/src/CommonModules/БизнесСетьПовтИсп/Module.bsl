
#Область СлужебныйПрограммныйИнтерфейс

// Параметры соединения к сервисам.
//
// Параметры:
//  ИмяСервиса	 - Строка - имя сервиса "БизнесСеть", "ТорговаяПлощадка"
// 
// Возвращаемое значение:
//  Структура - параметры соединения:
//   * Сервер - Строка - имя сервера.
//   * Порт - Число - порт соединения.
//   * Аутентификация - Булево - требуется аутентификация по токену.
//   * Таймаут - Число - длительность ожидания ответа в секундах.
//   * ЗащищенноеСоединение - ЗащищенноеСоединениеOpenSSL - объект защищенного соединения OpenSSL.
//   * Прокси - ИнтернетПрокси - параметры прокси-сервера.
//   * ИдентификаторПрограммы - Строка - уникальный идентификатор информационной базы.
//
Функция ПараметрыСоединения(ИмяСервиса) Экспорт
	
	Если ИмяСервиса = "EDI" Тогда
		ПараметрыСоединения = НовыйПараметрыСоединения("edi.1cfresh.com", 443, Истина);
		ПараметрыСоединения.Аутентификация = Истина;
		ПараметрыСоединения.Таймаут = 30;
		
	ИначеЕсли ИмяСервиса = "КороткихСсылок" Тогда
		ПараметрыСоединения = НовыйПараметрыСоединения("1c.ru", 443, Истина);
		ПараметрыСоединения.Аутентификация = Истина;
		ПараметрыСоединения.Таймаут = 30;
		
	ИначеЕсли ИмяСервиса = "БизнесСеть"
		ИЛИ ИмяСервиса = "ТорговаяПлощадка" Тогда
		ПараметрыСоединения = НовыйПараметрыСоединения("api.1cbn.ru", 443, Истина);
		ПараметрыСоединения.Аутентификация = Истина;
		ПараметрыСоединения.Таймаут = 30;
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Передано неподдерживаемое имя сервиса'");
	КонецЕсли;
	
	Возврат ПараметрыСоединения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйПараметрыСоединения(Сервер = "", Порт = 80, ЗащищенноеСоединение = Ложь)

	Результат = Новый Структура;
	Результат.Вставить("Сервер",         Сервер);
	Результат.Вставить("Порт",           Порт);
	Результат.Вставить("Аутентификация", Ложь);
	Результат.Вставить("Таймаут",        30);
	Результат.Вставить("Прокси",         Неопределено);
	Результат.Вставить("ЗащищенноеСоединение", Неопределено);
	Результат.Вставить("ИдентификаторПрограммы", Неопределено);
	
	Если ЗащищенноеСоединение Тогда
		Результат.ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение(,
			Новый СертификатыУдостоверяющихЦентровОС);
		Результат.Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	Иначе
		Результат.Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("http");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти