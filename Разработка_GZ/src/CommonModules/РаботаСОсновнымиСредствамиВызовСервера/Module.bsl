////////////////////////////////////////////////////////////////////////////////
// Работа с основными средствами.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция ДанныеВыбораОС(Параметры, ОкончаниеВводаТекста = Ложь) Экспорт
	
	ДанныеВыбора = Справочники.ОсновныеСредства.ПолучитьДанныеВыбора(Параметры);
	
	ШрифтКоманды    = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста);
	ШрифтПодСтроки  = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , , Истина);
	ЦветПодстроки   = Новый Цвет(0, 175, 0);
	
	Представление = Новый ФорматированнаяСтрока(
		Новый ФорматированнаяСтрока(НСтр("ru='Создать: '") + """", ШрифтКоманды),
		Новый ФорматированнаяСтрока(СокрЛП(Параметры.СтрокаПоиска), ШрифтПодСтроки, ЦветПодстроки),
		Новый ФорматированнаяСтрока("""", ШрифтКоманды)
		);
			
	ДанныеВыбора.Добавить(Параметры.ГруппаОС, Представление);
	
	Если ОкончаниеВводаТекста Тогда
		// Добавляем вторую строку, чтобы в этом режиме не срабатывал автовыбор единственной строки.
		ДанныеВыбора.Добавить(""); 
	КонецЕсли;
	
	Возврат ДанныеВыбора;
	
КонецФункции

Функция СоздатьОСБыстро(Знач ГруппаОС, Знач ПараметрыСоздания) Экспорт
	
	НовыйОбъект = Справочники.ОсновныеСредства.СоздатьЭлемент();
	
	Если ПараметрыСоздания.Свойство("ЗначенияЗаполнения") Тогда
		НовыйОбъект.Заполнить(ПараметрыСоздания.ЗначенияЗаполнения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ГруппаОС) Тогда
		НовыйОбъект.ГруппаОС = ГруппаОС;
		НовыйОбъект.НедвижимоеИмущество = УправлениеВнеоборотнымиАктивамиКлиентСервер.ЭтоНедвижимоеИмущество(ГруппаОС);
	Иначе
		НовыйОбъект.ГруппаОС = Перечисления.ГруппыОС.ДругиеВидыОсновныхСредств;
	КонецЕсли;
	
	НовыйОбъект.Наименование = ПараметрыСоздания.ДополнительныеПараметрыСоздания.Наименование;
	НовыйОбъект.НаименованиеПолное = НовыйОбъект.Наименование;
	
	Попытка
		НовыйОбъект.Записать();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции

#КонецОбласти