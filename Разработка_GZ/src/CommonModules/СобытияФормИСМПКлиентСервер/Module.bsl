////////////////////////////////////////////////////////////////////////////////
// Клиент-серверные процедуры и функции событий форм ИСМП
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура, вызываемая при чтении/создании/записи формы, а так же при изменении
// ключевых реквизитов документа  ИСМП.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, из которой происходит вызов процедуры.
//
Процедура УправлениеДоступностьюЭлементовФормы(Форма) Экспорт
	
	МассивИмен = СтрРазделить(Форма.ИмяФормы, ".");
	
	ЭтоФормаДокумента = Ложь;
	
	Пока МассивИмен.Количество() > 2 Цикл
		ЭтоФормаДокумента = ЭтоФормаДокумента ИЛИ МассивИмен[МассивИмен.ВГраница()] = "ФормаДокумента";
		МассивИмен.Удалить(МассивИмен.ВГраница());
	КонецЦикла;
	
	Если Не ЭтоФормаДокумента Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяМетаданных = СтрСоединить(МассивИмен, ".");
	
	Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ДокументМожетИметьДокументОснование(ПолноеИмяМетаданных) Тогда
		Форма.Элементы.ДокументОснование.Видимость = Ложь;
		Форма.Элементы.ТоварыПерезаполнитьПоОснованию.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Получает структуру дополнительных параметров интеграции, хранящиеся на форме.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Форма для полученя параметров.
// 	СоздаватьПриОтстутствии - Булево   - Признак инициализации стурктуры дополнительных параметров.
// Возвращаемое значение:
// 	Неопределено, Структура - Дополнительные параметры.
Функция ДополнительныеПараметрыИнтеграции(Форма, СоздаватьПриОтстутствии = Ложь) Экспорт
	
	Подсистема = ИнтеграцияИСМПКлиентСервер.ИмяПодсистемы();
	
	ДополнительныеПараметры = Неопределено;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыИнтеграцииГосИС") Тогда
		
		ПараметрыИнтеграцииПодсистемы = Форма.ПараметрыИнтеграцииГосИС.Получить(Подсистема);
		Если ПараметрыИнтеграцииПодсистемы = Неопределено И СоздаватьПриОтстутствии Тогда
			ПараметрыИнтеграцииПодсистемы = Новый Структура;
			Форма.ПараметрыИнтеграцииГосИС.Вставить(Подсистема, ПараметрыИнтеграцииПодсистемы);
		КонецЕсли;
		
		Если ПараметрыИнтеграцииПодсистемы <> Неопределено
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыИнтеграцииПодсистемы, "ДополнительныеПараметры") Тогда
			ДополнительныеПараметры = ПараметрыИнтеграцииПодсистемы.ДополнительныеПараметры;
		ИначеЕсли СоздаватьПриОтстутствии Тогда
			ДополнительныеПараметры = Новый Структура;
			ПараметрыИнтеграцииПодсистемы.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

#КонецОбласти