#Область ПрограммныйИнтерфейс

// Устанавливает склонения по падежам для должностей, входящих в список "частотных" должностей подписантов
// Параметры:
//   Источник - СправочникОбъект.Должности - объект, для которого устанавливаются склонения
//   Отказ - Булево - признак отказа записи
//
Процедура ЗаполнитьСклоненияДолжностейПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаименованиеДолжности = Источник.Наименование;
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешированиеДанных.Добавить(НаименованиеДолжности);
	ХешПредставления = СтрЗаменить(Строка(ХешированиеДанных.ХешСумма), " ", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СклоненияПредставленийОбъектов.Объект
	|ИЗ
	|	РегистрСведений.СклоненияПредставленийОбъектов КАК СклоненияПредставленийОбъектов
	|ГДЕ
	|	СклоненияПредставленийОбъектов.Объект = &Ссылка
	|	И СклоненияПредставленийОбъектов.ХешПредставления = &ХешПредставления";
	Запрос.УстановитьПараметр("Ссылка",           Источник.Ссылка);
	Запрос.УстановитьПараметр("ХешПредставления", ХешПредставления);
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДолжностей = ДолжностиПодписантов();
	ДанныеДолжности = ТаблицаДолжностей.Найти(НаименованиеДолжности, "ИменительныйПадеж");
	
	Если ДанныеДолжности <> Неопределено Тогда
		
		
		НаборЗаписейСклонения = РегистрыСведений.СклоненияПредставленийОбъектов.СоздатьНаборЗаписей();
		НаборЗаписейСклонения.Отбор.ХешПредставления.Установить(ХешПредставления);
		НаборЗаписейСклонения.Отбор.Объект.Установить(Источник.Ссылка);
		
		НоваяСтрока = НаборЗаписейСклонения.Добавить();
		НоваяСтрока.Объект = Источник.Ссылка;
		НоваяСтрока.ХешПредставления = ХешПредставления;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДолжности);
		НаборЗаписейСклонения.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Таблица склонений "частотных" должностей лиц с правом подписи документов
//
// Возвращаемое значение:
//   ТаблицаЗначений - см. колонки ОбщегоМакета "НаименованияДолжностейПодписантов".
//
Функция ДолжностиПодписантов() Экспорт
	
	// Заполняет список из общего макета
	КлассификаторXML = ПолучитьОбщийМакет("СклоненияДолжностейЛицСПравомПодписи").ПолучитьТекст();
	
	Возврат ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
КонецФункции

#КонецОбласти