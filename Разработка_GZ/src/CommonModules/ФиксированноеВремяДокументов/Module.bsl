
#Область СлужебныеПроцедурыИФункции

Процедура УстановитьВремяНовогоДокумента(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ФиксированноеВремяВДокументах") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяДата = НачалоДня(Источник.Дата);
	
	// Устанавливаем время только для новых документов и у существующих, если изменилась дата документа.
	Если НЕ Источник.ЭтоНовый() Тогда
		ПредыдущаяДата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "Дата");
		Если ТекущаяДата = НачалоДня(ПредыдущаяДата) Тогда
			// В течение дня пользователь может менять время у записанных документов, время не устанавливаем.
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Источник.Ссылка);
	Если Менеджер = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВремяДокумента = Менеджер.ВремяДокументаПоУмолчанию();
	Источник.Дата  = ТекущаяДата + ВремяДокумента.Часы * 3600 + ВремяДокумента.Минуты * 60
		+ ?(ВремяДокумента.Свойство("Секунды"), ВремяДокумента.Секунды, 0);
	
КонецПроцедуры

#КонецОбласти
