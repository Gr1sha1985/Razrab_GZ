#Область СлужебныйПрограммныйИнтерфейс

Функция JsonDump(Данные) Экспорт 
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные);
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

Функция JsonLoad(Строка) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Строка);
	Данные = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	Возврат Данные;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция MD5(Строка) Экспорт 
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешированиеДанных.Добавить(Строка);
	Возврат СтрЗаменить(НРег(ХешированиеДанных.ХешСумма), " ", "");
	
КонецФункции

Функция ИзвлечьДанныеКартинки(Знач ДанныеКартинки) Экспорт
	
	ДанныеКартинки = СтрЗаменить(ДанныеКартинки, "data:image/png;base64,", "");
	ДанныеКартинки = СтрЗаменить(ДанныеКартинки, "data:image/jpeg;base64,", "");
	
	Возврат Новый ХранилищеЗначения(Base64Значение(ДанныеКартинки));
	
КонецФункции

Функция ПривестиТип(Знач ТекСтрока, ТекТип) Экспорт
	
	мТипов = Новый Массив(1);
	мТипов.Добавить(ТекТип);
	Если ТекТип = Тип("Число") Тогда
		ТекСтрока = СтрЗаменить(НРег(ТекСтрока), "без", ""); // в таблице может быть "Без НДС"
		ТекСтрока = СтрЗаменить(ТекСтрока, "6e3", ""); // в таблице может быть "6e3 HAC" (английский)
		ТекСтрока = СтрЗаменить(ТекСтрока, "з", "3");
		ТекСтрока = СтрЗаменить(ТекСтрока, "о", "0"); // "о" русская -> "0"
		ТекСтрока = СтрЗаменить(ТекСтрока, "o", "0"); // "o" английская -> "0"
		ТекСтрока = СтрЗаменить(ТекСтрока, "-", ".");
		ТекСтрока = СтрЗаменить(ТекСтрока, Символ(8218), "."); // Нижняя одинарная открывающая кавычка -> точка
		ТекСтрока = УдалитьЛишниеСимволыВЧисле(ТекСтрока);
	ИначеЕсли ТекТип = Тип("Дата") Тогда
		Возврат СтрокуВДату(ТекСтрока);
	КонецЕсли;
	
	ОписаниеТипа = Новый ОписаниеТипов(мТипов);
	Возврат ОписаниеТипа.ПривестиЗначение(ТекСтрока);
	
КонецФункции

Функция УдалитьЛишниеСимволыВЧисле(Знач Значение)
	
	Результат = "";
	ТочкаБыла = Ложь;
	Индекс = СтрДлина(Значение);
	
	// Заполняем справа, пока не увидим запятую или точку
	// Когда встретили - ставим точку. После этого момента все запятые и точки игнорируем
	Пока Индекс > 0 Цикл
		ТекСимвол = Сред(Значение, Индекс, 1);
		КодСимвола = КодСимвола(ТекСимвол);
		Если (КодСимвола >= 48 И КодСимвола <= 57) Тогда // цифры
			Результат = ТекСимвол + Результат;
		ИначеЕсли Не ТочкаБыла И (КодСимвола = 44 Или КодСимвола = 46) Тогда // "," или "."
			ТочкаБыла = Истина;
			Результат = "." + Результат;
		КонецЕсли;
		Индекс = Индекс - 1;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СтрокуВДату(Знач СтрДата)
	
	СтрДата = СтрЗаменить(НРег(СокрЛП(СтрДата)), ".", "/");
	СтрДата = СтрЗаменить(СтрДата, ":", "/");
	СтрДата = СтрЗаменить(СтрДата, ";", "/");
	СтрДата = СтрЗаменить(СтрДата, "з", "3");
	СтрДата = СтрЗаменить(СтрДата, ",", "/");
	СтрДата = СтрЗаменить(СтрДата, " ", "/");
	СтрДата = СтрЗаменить(СтрДата, Символы.Таб, "/");
	СтрДата = УдалитьЛишниеСимволыВДате(СтрДата);
	СтрДата = ВставитьРазделителиМеждуЧисломИБуквой(СтрДата, "/");
	Пока СтрНайти(СтрДата, "//") <> 0 Цикл
		СтрДата = СтрЗаменить(СтрДата, "//", "/");
	КонецЦикла;
	Если Лев(СтрДата, 1) = "/" Тогда
		СтрДата = Сред(СтрДата, 2);
	КонецЕсли;
	
	ОписаниеДаты = Новый ОписаниеТипов("Дата");
	ПриведеннаяДата = ОписаниеДаты.ПривестиЗначение(СтрДата);
	Если ЗначениеЗаполнено(ПриведеннаяДата) Тогда
		Возврат ПриведеннаяДата;
	КонецЕсли;
	
	ПозицияГода = НайтиПозициюГода(СтрДата);
	Если ПозицияГода <> 0 Тогда
		СтрДата = УдалитьЛишниеСимволыЛевееГода(СтрДата, ПозицияГода);
	Иначе
		СтрДата = УдалитьСимволыДоПервогоЧисла(СтрДата);
	КонецЕсли;
	
	мДата = СтрРазделить(СтрДата, "/", Ложь);
	Кол = мДата.Количество();
	Если Кол > 1 Тогда
		ТекДень = ПривестиКЧислу(мДата[0]);
		
		Если СтрДлина(мДата[1]) <= 2 Тогда
			ТекМесяц = ПривестиКЧислу(мДата[1]);
		Иначе
			ТекМесяц = 13;
			Пока ТекМесяц > 1 Цикл
				ТекМесяц = ТекМесяц - 1;
				СтрМесяц = НРег(Формат(Дата("2000"+Формат(ТекМесяц, "ЧЦ=2; ЧВН=")+"01"), "ДФ=МММ"));
				Если Прав(СтрМесяц, 1) = "." Тогда
					//Удаляем точку в конце
					СтрМесяц = Лев(СтрМесяц, СтрДлина(СтрМесяц)-1);
				КонецЕсли;
				
				Если СтрНачинаетсяС(мДата[1], СтрМесяц) > 0 Тогда
					Прервать;
				ИначеЕсли СтрМесяц = "июль" Тогда
					Если СтрНачинаетсяС(мДата[1], "июля") > 0 Тогда
						Прервать;
					КонецЕсли;
				ИначеЕсли СтрМесяц = "июнь" Тогда
					Если СтрНачинаетсяС(мДата[1], "июня") > 0 Тогда
						Прервать;
					КонецЕсли;
				ИначеЕсли СтрМесяц = "май" Тогда
					Если СтрНачинаетсяС(мДата[1], "мая") > 0 Тогда
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если Кол > 2 Тогда
			ТекГод = ПривестиКЧислу(мДата[2]);
			Если СтрДлина(мДата[2]) <= 2 Тогда
				Если ТекГод > 29 Тогда
					ТекГод = 1900 + ТекГод;
				Иначе
					ТекГод = 2000 + ТекГод;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ТекГод = 1;
		КонецЕсли;
		
		Если ТекДень < 1 Или ТекДень > 31 Тогда
			СтрДень = "01";
		Иначе
			СтрДень = Формат(ТекДень, "ЧЦ=2; ЧВН=");
		КонецЕсли;
		Если ТекМесяц < 1 Или ТекМесяц > 12 Тогда
			СтрМесяц = "01";
		Иначе
			СтрМесяц = Формат(ТекМесяц, "ЧЦ=2; ЧВН=");
		КонецЕсли;
		Если ТекГод < 1 Или ТекГод > 2100 Тогда
			СтрГод = "0001";
		Иначе
			СтрГод = Формат(ТекГод, "ЧЦ=4; ЧВН=; ЧГ=");
		КонецЕсли;
	Иначе
		//Запись без разделителей недопустима
		СтрДень = "01";
		СтрМесяц = "01";
		СтрГод = "0001";
	КонецЕсли;
	
	ОписаниеТипа = Новый ОписаниеТипов("Дата");
	Результат = ОписаниеТипа.ПривестиЗначение(СтрГод + СтрМесяц + СтрДень);
	Если Результат = '00010101' Тогда
		Если СтрДень <> "01" Тогда
			Результат = ОписаниеТипа.ПривестиЗначение(СтрГод + СтрМесяц + "01");
		КонецЕсли;
		Если СтрМесяц <> "01" И Результат = '00010101' Тогда
			Результат = ОписаниеТипа.ПривестиЗначение(СтрГод + "01" + СтрДень);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВставитьРазделителиМеждуЧисломИБуквой(Знач Значение, Разделитель)
	
	Если Значение = "" Тогда
		Возврат Значение;
	КонецЕсли;
	
	КодРазделителя = КодСимвола(Разделитель);
	
	Результат = "";
	БылСтатус = 0;// 0 - был разделитель, 1 - число, 2 - остальные
	
	Для Индекс = 1 По СтрДлина(Значение) Цикл
		ТекСимвол = Сред(Значение, Индекс, 1);
		КодСимвола = КодСимвола(ТекСимвол);
		
		Если ТекСимвол = Разделитель Тогда
			ТекСтатус = 0;
		ИначеЕсли КодСимвола >= 48 И КодСимвола <= 57 Тогда // цифры
			ТекСтатус = 1;
		Иначе
			ТекСтатус = 2;
		КонецЕсли;
		
		Если (БылСтатус = 1 И ТекСтатус = 2)
			Или (БылСтатус = 2 И ТекСтатус = 1) Тогда
			Результат = Результат + Разделитель + ТекСимвол;
		Иначе
			Результат = Результат + ТекСимвол;
		КонецЕсли;
		БылСтатус = ТекСтатус;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция УдалитьЛишниеСимволыВДате(Знач Значение)
	
	Результат = "";
	Для Индекс = 1 По СтрДлина(Значение) Цикл
		ТекСимвол = Сред(Значение, Индекс, 1);
		КодСимвола = КодСимвола(ТекСимвол);
		Если (КодСимвола >= 47 И КодСимвола <= 57)			// символ "/" и цифры
			//Или (КодСимвола >= 65 И КодСимвола <= 90)		// латиница большие
			Или (КодСимвола >= 97 И КодСимвола <= 122)		// латиница маленькие
			//Или (КодСимвола >= 1040 И КодСимвола <= 1071)	// кириллица большие
			Или (КодСимвола >= 1072 И КодСимвола <= 1103)	// кириллица маленькие
			//Или КодСимвола = 1025							// символ "Ё"
			Или КодСимвола = 1105 Тогда						// символ "ё"
			
			Результат = Результат + ТекСимвол;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПривестиКЧислу(Знач Значение) Экспорт
	ТипЧисло  = Новый ОписаниеТипов("Число");
	Возврат ТипЧисло.ПривестиЗначение(Значение);
КонецФункции

Функция УдалитьЛишниеСимволыЛевееГода(Знач Значение, ПозицияГода)
	
	ПодстрокаДоГода = Лев(Значение, ПозицияГода - 1);
	ЧислоРазделителей = СтрЧислоВхождений(ПодстрокаДоГода, "/");
	Если ЧислоРазделителей = 0 Тогда
		Возврат "01/01/"+Значение;
	ИначеЕсли ЧислоРазделителей = 1 Тогда
		Возврат "01/"+Значение;
	Иначе
		ПозицияТретьейЧерты = СтрНайти(ПодстрокаДоГода, "/", НаправлениеПоиска.СКонца, , 3);
		Возврат Сред(Значение, ПозицияТретьейЧерты + 1);
	КонецЕсли;
	
КонецФункции

Функция УдалитьСимволыДоПервогоЧисла(Знач Значение)
	
	Для Индекс = 1 По СтрДлина(Значение) Цикл
		ТекСимвол = Сред(Значение, Индекс, 1);
		КодСимвола = КодСимвола(ТекСимвол);
		Если (КодСимвола >= 48 И КодСимвола <= 57) Тогда	// цифры
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Сред(Значение, Индекс);
	
КонецФункции

Функция НайтиПозициюГода(Знач Значение)
	
	Если Значение = "" Тогда
		Возврат 0;
	КонецЕсли;
	
	Века = Новый Массив;
	Века.Добавить("20");
	Века.Добавить("19");
	
	Для Каждого Век Из Века Цикл 
		ТекПозиция = СтрНайти(Значение, Век);
		Пока ТекПозиция <> 0 Цикл
			Подстрока = Сред(Значение, ТекПозиция + 2, 2);
			Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Подстрока) Тогда
				Возврат ТекПозиция;
			Иначе
				ТекПозиция = СтрНайти(Значение, Век, , ТекПозиция + 1);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции

#КонецОбласти

