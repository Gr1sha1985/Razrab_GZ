////////////////////////////////////////////////////////////////////////////////
// Работа с последовательностями
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает для организации узел РИБ, в котором выполняется полное перепроведение
// документов по этой организации.
//
Функция УзелПерепроведенияДокументовПоОрганизации(Знач Организация, Знач УзелОтправитель) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПерепроведениеДокументовВУзлахРИБ.Узел КАК Узел
	|ИЗ
	|	РегистрСведений.ПерепроведениеДокументовВУзлахРИБ КАК ПерепроведениеДокументовВУзлахРИБ
	|ГДЕ
	|	ПерепроведениеДокументовВУзлахРИБ.Организация = &Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Узел;
	Иначе
		Результат = Неопределено;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		// Если в регистре для организации настройки нет, то принимаем в качестве
		// узла перепроведения центральный узел РИБ.
		
		МетаданныеПланаОбмена = Метаданные.НайтиПоТипу(ТипЗнч(УзелОтправитель));
		Если МетаданныеПланаОбмена <> Неопределено Тогда
			Результат = ПланыОбмена.ГлавныйУзел();
		КонецЕсли;
		
	КонецЕсли;

	Возврат Результат;
	
КонецФункции


// Определяет, является ли текущий узел узлом перепроведения по организации.
//
Функция ЭтоУзелПерепроведенияДокументовПоОрганизации(Знач Организация) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПерепроведениеДокументовВУзлахРИБ.Узел КАК Узел
	|ИЗ
	|	РегистрСведений.ПерепроведениеДокументовВУзлахРИБ КАК ПерепроведениеДокументовВУзлахРИБ
	|ГДЕ
	|	ПерепроведениеДокументовВУзлахРИБ.Организация = &Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		УзелПерепроведения = Выборка.Узел;
	Иначе
		УзелПерепроведения = ПланыОбмена.ГлавныйУзел();
	КонецЕсли;

	Если УзелПерепроведения = Неопределено Тогда
		Результат = Истина;
	Иначе
		МетаданныеПланаОбмена = Метаданные.НайтиПоТипу(ТипЗнч(УзелПерепроведения));
		ЭтотУзел = ПланыОбмена[МетаданныеПланаОбмена.Имя].ЭтотУзел();
		Результат = ЭтотУзел = УзелПерепроведения;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

#КонецОбласти
