#Область ПрограммныйИнтерфейс

// Устанавливает отбор ПометкаУдаления=Ложь на список.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - Форма, в которой размещается список.
//	ИмяСписка - Строка - Имя элемента списка на форме. Необязательный.
//
Процедура СкрытьПомеченныеНаУдаление(Форма, ИмяСписка = "Список") Экспорт
	
	Если НЕ СкрыватьПомеченныеНаУдаление() Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма[ИмяСписка],
		"ПометкаУдаления",
		Ложь,
		ВидСравненияКомпоновкиДанных.Равно,,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный
	);
	
	Форма.ОтслеживатьПометкуУдаления = ОтслеживатьПометкуУдаления();
	
КонецПроцедуры

// Удаляет пользовательский отбор по полю ПометкаУдаления.
//
// Параметры:
//	Настройки - ПользовательскиеНастройкиКомпоновкиДанных - Пользовательские настройки компоновки данных.
//
Процедура УдалитьОтборПометкаУдаления(Настройки) Экспорт
	
	Если НЕ СкрыватьПомеченныеНаУдаление() Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Настройка Из Настройки.Элементы Цикл
		Если ТипЗнч(Настройка) = Тип("ОтборКомпоновкиДанных") Тогда
			Для каждого ЭлементНастройки Из Настройка.Элементы Цикл
				Если ЭлементНастройки.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления") Тогда
					Настройка.Элементы.Удалить(ЭлементНастройки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обработчик события "ПередЗаписью" документов и справочников.
//
// Параметры:
//	Источник - ДокументОбъект или СправочникОбъект - Источник события.
//	Отказ - Булево - Признак ошибки.
//
Процедура ПередЗаписьюОбъектаДляПометкиУдаления(Источник, Отказ) Экспорт
	
	Если НЕ СкрыватьПомеченныеНаУдаление() Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Источник.ДополнительныеСвойства.Вставить("ПометкаУдаленияПередЗаписью",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "ПометкаУдаления"));
	
КонецПроцедуры

// Обработчик события "ПриЗаписи" документов и справочников.
//
// Параметры:
//	Источник - ДокументОбъект или СправочникОбъект - Источник события.
//	Отказ - Булево - Признак ошибки.
//
Процедура ПриЗаписиОбъектаДляПометкиУдаления(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Источник.ДополнительныеСвойства.Свойство("ПометкаУдаленияПередЗаписью") Тогда
		Возврат;
	КонецЕсли;
	
	УстановленаПометкаУдаления = Источник.ПометкаУдаления
		И НЕ Источник.ДополнительныеСвойства.ПометкаУдаленияПередЗаписью;
	
	Если НЕ УстановленаПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Источник, "ЭтоГруппа")
		И Источник.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПоследнейПометки = Новый Структура;
	ДанныеПоследнейПометки.Вставить("Ссылка", Источник.Ссылка);
	ДанныеПоследнейПометки.Вставить("Время", ТекущаяУниверсальнаяДатаВМиллисекундах());
	ХранилищеОбщихНастроек.Сохранить("ПомеченныеНаУдаление", "ДанныеПоследнейПометки", ДанныеПоследнейПометки);
	
КонецПроцедуры

// Возвращает признак скрытия помеченных на удаление объектов в списках.
//
// Возвращаемое значение:
//	Булево - Истина, если помеченные на удаление объекты должны скрываться в списках.
//
Функция СкрыватьПомеченныеНаУдаление() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростой")
		И ОбщегоНазначения.РазделениеВключено()
		И Врег(Метаданные.Имя) = Врег("БухгалтерияПредприятияБазовая1")
		И НЕ ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком()
		И НЕ ОбщегоНазначенияБП.ИспользуетсяСервисЭлектронныхТрудовыхКнижек();
	
КонецФункции

// Возвращает признак отслеживания интерактивной установки текущим пользователем пометки удаления в списках.
//
// Возвращаемое значение:
//	Булево - Истина, если интерактивная пометка удаления в списках должна отслеживаться.
//
Функция ОтслеживатьПометкуУдаления() Экспорт
	
	Возврат ХранилищеОбщихНастроек.Загрузить("ПомеченныеНаУдаление",
		"НеПоказыватьВопросПриПометкеНаУдаление") <> Истина;
	
КонецФункции

// Устанавливает функциональные опции для отображения ссылок в разделе "Настройки" простого интерфейса.
//
Процедура УстановитьОтображениеПомеченныхНаУдаление() Экспорт
	
	СкрыватьПомеченные = СкрыватьПомеченныеНаУдаление();
	
	Константы.УдалениеПомеченныхОбъектовПростойИнтерфейс.Установить(НЕ СкрыватьПомеченные
		И ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростой"));
	Константы.ПомеченныеНаУдалениеПростойИнтерфейс.Установить(СкрыватьПомеченные);
	
КонецПроцедуры

#КонецОбласти