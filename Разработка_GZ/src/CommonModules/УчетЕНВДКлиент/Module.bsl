#Область ПрограммныйИнтерфейс

// Открывает форму соответствующего вида заявления
//
// Параметры:
//   ВидЗаявления - ПеречислениеСсылка.ВидыУведомленийОСпецрежимахНалогообложения - вид заявления
//   ПараметрыФормы - Структура - параметры формы заявления
//   ВладелецФормы - Форма - владелец формы
//
Процедура ОткрытьФормуЗаявления(ВидЗаявления, ПараметрыФормы, ВладелецФормы = Неопределено) Экспорт
	
	ИмяФормыЗаявления = УчетЕНВДВызовСервера.ИмяФормыЗаявления(ВидЗаявления);
	Если ПустаяСтрока(ИмяФормыЗаявления) Тогда
		Возврат;
	КонецЕсли;
	
	Если    ВидЗаявления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ФормаЕНВД3")
		Или ВидЗаявления = ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ФормаЕНВД4") Тогда
		
		// При снятии с учета, спрашиваем у пользователя причину
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИмяФормыЗаявления", ИмяФормыЗаявления);
		ДополнительныеПараметры.Вставить("ПараметрыФормы", ПараметрыФормы);
		ДополнительныеПараметры.Вставить("ВладелецФормы", ВладелецФормы);
		
		ОповещениеОВыборе = Новый ОписаниеОповещения("ВыборПричиныЗаявленияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ОткрытьФорму("Справочник.ВидыДеятельностиЕНВД.Форма.ФормаВыбораПричиныЗаявления", , ВладелецФормы, , , , ОповещениеОВыборе);
		
	Иначе
		
		ОткрытьФорму(ИмяФормыЗаявления, ПараметрыФормы, ВладелецФормы, ПараметрыФормы.НалоговыйОрган);
		
	КонецЕсли;
	
КонецПроцедуры

// Дополнительная обработка перед заполнением заявления по кнопке Заполнить
//
// Параметры:
//   ПараметрыОтчета - Структура
//     * Организация - СправочникСсылка.Организации
//     * КодНалоговогоОргана - СправочникСсылка.РегистрацииНалоговомОргане
//   ФормаОтчета - Форма - форма заявления
//   ОписаниеОповещения - ОписаниеОповещения - обратный вызов формы отчета
//
Процедура ПередЗаполнениемЗаявления(ПараметрыОтчета, ФормаОтчета, ОписаниеОповещения) Экспорт
	
	Перем Организация, РегистрацияВНалоговомОргане;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	ОповещениеОВыборе = Новый ОписаниеОповещения("ПередЗаполнениемЗаявленияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Отбор = Новый Структура;
	
	ПараметрыФормыВыбора = Новый Структура;
	Если ПараметрыОтчета.Свойство("Организация", Организация) И ЗначениеЗаполнено(Организация) Тогда
		Отбор.Вставить("Владелец", ОбщегоНазначенияБПВызовСервераПовтИсп.ВсяОрганизация(Организация));
		ПараметрыФормыВыбора.Вставить("Организация", Организация);
	КонецЕсли;
	Если ПараметрыОтчета.Свойство("КодНалоговогоОргана", РегистрацияВНалоговомОргане) И ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		ПараметрыФормыВыбора.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	КонецЕсли;
	
	ПараметрыФормыВыбора.Вставить("Отбор", Отбор);
	ПараметрыФормыВыбора.Вставить("МножественныйВыбор", Истина);
	
	ОткрытьФорму("Справочник.ВидыДеятельностиЕНВД.ФормаВыбора",
		ПараметрыФормыВыбора, ФормаОтчета, , , , ОповещениеОВыборе, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработка оповещения процедуры ОткрытьФормуЗаявления()
//
Процедура ВыборПричиныЗаявленияЗавершение(КодПричины, ДополнительныеПараметры) Экспорт
	
	Если КодПричины = Неопределено Или ТипЗнч(КодПричины) <> Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ДополнительныеПараметры.ПараметрыФормы);
	ПараметрыФормы.Вставить("КодПричины", КодПричины);
	
	ОткрытьФорму(
		ДополнительныеПараметры.ИмяФормыЗаявления,
		ПараметрыФормы,
		ДополнительныеПараметры.ВладелецФормы,
		ПараметрыФормы.НалоговыйОрган);
	
КонецПроцедуры

// Обработка оповещения процедуры ПередЗаполнениемЗаявления()
//
Процедура ПередЗаполнениемЗаявленияЗавершение(ВидыДеятельности, ДополнительныеПараметры) Экспорт
	
	Перем ОписаниеОповещения;
	
	Если ВидыДеятельности <> Неопределено И ДополнительныеПараметры.Свойство("ОписаниеОповещения", ОписаниеОповещения) Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("ВидыДеятельности", ВидыДеятельности);
		Если ДополнительныеПараметры.Свойство("РегистрацияВНалоговомОргане")
			И ЗначениеЗаполнено(ДополнительныеПараметры.РегистрацияВНалоговомОргане) Тогда
			ПараметрыЗаполнения.Вставить("НалоговыйОрган", ДополнительныеПараметры.РегистрацияВНалоговомОргане);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, ПараметрыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти