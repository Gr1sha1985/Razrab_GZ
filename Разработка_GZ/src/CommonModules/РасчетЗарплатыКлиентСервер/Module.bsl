
#Область СлужебныйПрограммныйИнтерфейс
////////////////////////////////////////////////////////////////////////////////
// Обслуживание полей аванса

// Устанавливает отображение полей ввода аванса.
//
// Параметры:
//		Форма									- ФормаКлиентскогоПриложения
//		ПутьРеквизитаСпособаРасчетаАванса		- Строка
//		ИмяГруппыРазмераАванса					- Строка
//		ПутьРеквизитаРеквизитаРазмерностиАванса	- Строка
//
Процедура УстановитьПоказРазмераАванса(Форма, ПутьРеквизитаСпособаРасчетаАванса = "Объект.СпособРасчетаАванса",  ИмяГруппыРазмераАванса = "АвансРазмерГруппа", ПутьРеквизитаРеквизитаРазмерностиАванса = "АвансРазмерность") Экспорт
	
	СпособРасчетаАванса = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьРеквизитаСпособаРасчетаАванса);

	ИменаСпособовРасчетаАванса = ИменаСпособовРасчетаАванса();
	
	СтраницаРазмераАванса = Неопределено;
	Для Каждого ИмяСпособаРасчетаАванса Из ИменаСпособовРасчетаАванса Цикл
		Если СпособРасчетаАванса = ПредопределенноеЗначение("Перечисление.СпособыРасчетаАванса."+ИмяСпособаРасчетаАванса) Тогда
			СтраницаРазмераАванса = Форма.Элементы[ИмяГруппыРазмераАванса].ПодчиненныеЭлементы.Найти(ИмяГруппыРазмераАванса + "Страница" + ИмяСпособаРасчетаАванса);
			Прервать
		КонецЕсли
	КонецЦикла;	
	
	Если СтраницаРазмераАванса <> Неопределено Тогда
		Форма.Элементы[ИмяГруппыРазмераАванса].ТекущаяСтраница = СтраницаРазмераАванса;
		Если ЗначениеЗаполнено(ПутьРеквизитаРеквизитаРазмерностиАванса) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, ПутьРеквизитаРеквизитаРазмерностиАванса, РазмерностьАванса(СпособРасчетаАванса));
		КонецЕсли	
	КонецЕсли;	
	
КонецПроцедуры

// Возвращает представление способа расчета аванса.
//
// Параметры:
//		ТекущийСпособРасчетаАванса	- ПеречислениеСсылка.СпособыРасчетаАванса
//		ТекущийРазмерАванса			- Число
//		ИзменитьАванс				- Булево
//		СжатыйФормат				- Булево
//
// Возвращаемое значение:
//		Строка
//
Функция КомментарийИзмененияАванса(ТекущийСпособРасчетаАванса, ТекущийРазмерАванса, ИзменитьАванс = Истина, СжатыйФормат = Ложь) Экспорт 
	
	КомментарийИзмененияАванса = "";
	
	Если ИзменитьАванс Тогда
		Если ЗначениеЗаполнено(ТекущийСпособРасчетаАванса) Тогда
			
			РазмерностьАванса = РазмерностьАванса(ТекущийСпособРасчетаАванса);
			
			ПредставлениеСпособаРасчета = ПредставлениеСпособаРасчетаАванса(ТекущийСпособРасчетаАванса, СжатыйФормат);
			
			КомментарийИзмененияАванса = 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеСпособаРасчета, НРег(ТекущийСпособРасчетаАванса), ТекущийРазмерАванса, РазмерностьАванса)
				
		КонецЕсли;
	КонецЕсли;	
	
	Возврат КомментарийИзмененияАванса;
		
КонецФункции	

#Область КорректировкиВыплаты

Процедура ДополнитьОписаниеПанелиВычеты(ОписаниеПанели) Экспорт
	ОписаниеПанели.Вставить("ТабличнаяЧастьКорректировкиВыплаты", Новый Структура);
КонецПроцедуры

Функция НомерТаблицыКорректировкиВыплаты() Экспорт
	Возврат 9;
КонецФункции

Функция ОписаниеТабличнойЧастиКорректировкиВыплаты() Экспорт
	Возврат РасчетЗарплатыКлиентСерверВнутренний.ОписаниеТабличнойЧастиКорректировкиВыплаты();
КонецФункции

Функция ФормаПодробнееОРасчетеНДФЛОписаниеТаблицыКорректировкиВыплаты() Экспорт
	Возврат РасчетЗарплатыКлиентСерверВнутренний.ФормаПодробнееОРасчетеНДФЛОписаниеТаблицыКорректировкиВыплаты();
КонецФункции

Процедура ФормаПодробнееОРасчетеНДФЛУстановитьВидимостьЭлементов(Форма) Экспорт
	
	ИспользоватьСтатьиФинансирования = Форма.ИспользоватьСтатьиФинансирования;
	
	ВидимостьСтраницыКорректировкиВыплаты = Форма.МногосотрудниковыйДокумент;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"КорректировкаВыплаты",
		"Видимость",
		Не ВидимостьСтраницыКорректировкиВыплаты);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"СтраницаКорректировкиВыплаты",
		"Видимость",
		ВидимостьСтраницыКорректировкиВыплаты);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ОбщаяГруппа",
		"ОтображениеСтраниц",
		?(ВидимостьСтраницыКорректировкиВыплаты, ОтображениеСтраницФормы.ЗакладкиСверху, ОтображениеСтраницФормы.Нет));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"КорректировкаВыплатыКомандаРедактированияРаспределения",
		"Видимость",
		ИспользоватьСтатьиФинансирования И Не ВидимостьСтраницыКорректировкиВыплаты);
	
	Если ИспользоватьСтатьиФинансирования И Не ВидимостьСтраницыКорректировкиВыплаты И Форма.КорректировкиВыплаты.Количество() > 0 Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"КорректировкаВыплатыКомандаРедактированияРаспределения",
			"Заголовок",
			Форма.КорректировкиВыплаты[0].КомандаРедактированияРаспределения);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"КорректировкаВыплатыКомандаРедактированияРаспределенияРасширеннаяПодсказка",
			"Заголовок",
			Форма.КорректировкиВыплаты[0].КомандаРедактированияРаспределения);
	Иначе
		Если Форма.РаботаВБюджетномУчреждении Тогда
			Заголовок = НСтр("ru = 'Финансир., расходы'");
		Иначе
			Заголовок = НСтр("ru = 'Финансирование'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"КорректировкаВыплатыКомандаРедактированияРаспределения",
			"Заголовок",
			Заголовок);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"КорректировкаВыплатыКомандаРедактированияРаспределенияРасширеннаяПодсказка",
			"Заголовок",
			НСтр("ru = 'Редактирование распределения'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаПолученияДанныхВыбораВидовОграниченияПособия(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	РасчетЗарплатыКлиентСерверВнутренний.ОбработкаПолученияДанныхВыбораВидовОграниченияПособия(ДанныеВыбора, Параметры, СтандартнаяОбработка);
КонецПроцедуры

Функция РазмерностьАванса(СпособРасчетаАванса)
	Возврат РасчетЗарплатыКлиентСерверВнутренний.РазмерностьАванса(СпособРасчетаАванса);
КонецФункции	

Функция ПредставлениеСпособаРасчетаАванса(СпособРасчетаАванса, СжатыйФормат)
	Возврат РасчетЗарплатыКлиентСерверВнутренний.ПредставлениеСпособаРасчетаАванса(СпособРасчетаАванса, СжатыйФормат);
КонецФункции

Функция ИменаСпособовРасчетаАванса()
	Возврат РасчетЗарплатыКлиентСерверВнутренний.ИменаСпособовРасчетаАванса();
КонецФункции

#КонецОбласти
