#Область ПрограммныйИнтерфейс

// Построить дерево упаковок на основании данных о кодах маркировки.
//
// Параметры:
// 	Значение - Массив, Строка - Коды маркировки для которых необходимо построить дерево упаковок
// 	Детализация - ПеречислениеСсылка.ДетализацияСтруктурыХраненияИС, Неопределено - Детализация хранения табачной продукции
// 	ПараметрыСканирования - См. ШтрихкодированиеИС.ПараметрыСканирования
// Возвращаемое значение:
// 	Структура - Описание:
//	 * ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	 * ТекстОшибки                    - Строка - Текст ошибки.
//	 * ДеревоУпаковок                 - ДеревоЗначений, Неопределено - Дерево упаковок, построенное по переданным кодам маркировки.
//	 * GTIN - ТаблицаЗначений - Список GTIN в разрезе МРЦ:
//	  ** GTIN - Строка - GTIN.
//	  ** МРЦ - Число - МРЦ.
//
Функция ДеревоУпаковок(Значение, Детализация = Неопределено, ПараметрыСканирования = Неопределено) Экспорт
	
	ПараметрыДерева = ПараметрыПостроенияДерева(ПараметрыСканирования);
	
	ДеревоУпаковок = Новый ДеревоЗначений;
	ДеревоУпаковок.Колонки.Добавить("Штрихкод",                  Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(200)));
	ДеревоУпаковок.Колонки.Добавить("GTIN",                      Метаданные.ОпределяемыеТипы.GTIN.Тип);
	ДеревоУпаковок.Колонки.Добавить("ТипШтрихкода",              Новый ОписаниеТипов("ПеречислениеСсылка.ТипыШтрихкодов"));
	ДеревоУпаковок.Колонки.Добавить("ВидУпаковки",               Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУпаковокИС"));
	ДеревоУпаковок.Колонки.Добавить("ВидПродукции",              Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПродукцииИС"));
	ДеревоУпаковок.Колонки.Добавить("СоставКодаМаркировки");
	ДеревоУпаковок.Колонки.Добавить("НормализованныйШтрихкод",   Новый ОписаниеТипов("Строка"));
	ДеревоУпаковок.Колонки.Добавить("ХэшСуммаНормализации",      Новый ОписаниеТипов("Строка"));
	ДеревоУпаковок.Колонки.Добавить("СодержимоеНедоступно",      Новый ОписаниеТипов("Булево"));
	ДеревоУпаковок.Колонки.Добавить("Статус",                    ШтрихкодированиеМОТП.ОписаниеТиповКолонкиСтатус());
	ДеревоУпаковок.Колонки.Добавить("ПредставлениеНоменклатуры", ОбщегоНазначения.ОписаниеТипаСтрока(200));
	
	ДополнительныйВидУпаковки = Неопределено;
	ЭталоннаяСтруктураДерева  = Новый Структура;
	
	ДополнительныеКолонки                        = Новый Структура;
	ДополнительныеКолонкиДляВложенныхСтрокДерева = Новый Структура;
	
	ПоляВДанныхСтатуса                           = Новый Соответствие;
	ПоляВДанныхСтатусаДляВложенныхСтрокДерева    = Новый Соответствие;
	
	Если ПараметрыДерева.ЭтоПроверкаКодовМаркировкиИСМП Тогда
		ДополнительныйВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская;
		ШтрихкодированиеМОТП.КоллекцияДополнительныхКолонокДереваУпаковокДляФормыПроверкиКодовМаркировки(ДополнительныеКолонки);
		ШтрихкодированиеМОТП.КоллекцияДополнительныхКолонокДереваУпаковокДляФормыПроверкиКодовМаркировки(ДополнительныеКолонкиДляВложенныхСтрокДерева);
	КонецЕсли;
	
	СоответствиеСтрокДерева = Новый Соответствие;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	ВозвращаемоеЗначение.Вставить("ДеревоУпаковок",                 Неопределено);
	ВозвращаемоеЗначение.Вставить("GTIN",                           Неопределено);
	ВозвращаемоеЗначение.Вставить("СоответствиеСтрокДерева",        СоответствиеСтрокДерева);
	ВозвращаемоеЗначение.Вставить("СтатусыКодовМаркировки",         Новый Соответствие());
	
	Если ТипЗнч(Значение) = Тип("Структура") Тогда
		ИсходныйМассивСтрок = Новый Массив();
		ИсходныйМассивСтрок.Добавить(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("Массив") Или ТипЗнч(Значение) = Тип("ТаблицаЗначений") Тогда
		ИсходныйМассивСтрок = Значение;
	Иначе
		ВызватьИсключение НСтр("ru = 'Внутренняя ошибка'");
	КонецЕсли;
	
	ЛогистическиеУпаковкиСоСкобками = Новый Соответствие;
	ЛогистическиеУпаковкиБезСкобок  = Новый Соответствие;
	
	ПараметрыНормализацииТабакЛогистическаяУпаковка = РазборКодаМаркировкиИССлужебныйКлиентСервер.ПараметрыНормализацииКодаМаркировки();
	ПараметрыНормализацииТабакЛогистическаяУпаковка.ИмяСвойстваКодМаркировки = "Штрихкод";
	ПараметрыНормализацииТабакЛогистическаяУпаковка.НачинаетсяСоСкобки       = Ложь;
	
	ПараметрыНабораПакетов = НовыеПараметрыНабораПакетов();
	
	Для Каждого СтрокаКодаМаркировки Из ИсходныйМассивСтрок Цикл
		
		// Поиск со скобками и без скобок - только для табачной продукции
		Если Не ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(СтрокаКодаМаркировки.ВидПродукции)
			И СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			
			КодМаркировкиБезСкобок = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
				СтрокаКодаМаркировки, СтрокаКодаМаркировки.ВидПродукции, ПараметрыНормализацииТабакЛогистическаяУпаковка);
			
			Если КодМаркировкиБезСкобок <> СтрокаКодаМаркировки.Штрихкод Тогда
				
				// Формирование копии элемента без скобок
				СтрокаКодаМаркировкиБезСкобок = ШтрихкодированиеИС.НоваяСтруктураОбработкиШтрихкода();
				ЗаполнитьЗначенияСвойств(СтрокаКодаМаркировкиБезСкобок, СтрокаКодаМаркировки);
				СтрокаКодаМаркировкиБезСкобок.Штрихкод = КодМаркировкиБезСкобок;
				ШтрихкодированиеМОТП.РассчитатьХэшСуммуНормализации(
					СтрокаКодаМаркировкиБезСкобок,
					СтрокаКодаМаркировкиБезСкобок.ДанныеРазбора);
				ЛогистическиеУпаковкиБезСкобок.Вставить(СтрокаКодаМаркировкиБезСкобок, СтрокаКодаМаркировки);
				ЛогистическиеУпаковкиСоСкобками.Вставить(СтрокаКодаМаркировки, СтрокаКодаМаркировкиБезСкобок);
				
				ДобавитьСтрокуВНаборПакетовЗапросов(СтрокаКодаМаркировкиБезСкобок, ПараметрыНабораПакетов);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДобавитьСтрокуВНаборПакетовЗапросов(СтрокаКодаМаркировки, ПараметрыНабораПакетов);
		
	КонецЦикла;
	
	СтатусыКодовМаркировки = ВозвращаемоеЗначение.СтатусыКодовМаркировки;
	
	Для Каждого НаборПакета Из ПараметрыНабораПакетов.НаборыПакетов Цикл
		
		Для Каждого ПакетКодовМаркировки Из НаборПакета.ПакетыКодовМаркировки Цикл
			
			Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(НаборПакета.ВидПродукцииПодсистемы) Тогда
				РезультатЗапросаСтатусовКодовМаркировок = ЗапроситьСтатусыКодовМаркировкиПакетно(
					ПакетКодовМаркировки,
					СтатусыКодовМаркировки,
					ПараметрыДерева.Организация);
			Иначе
				РезультатЗапросаСтатусовКодовМаркировок = ИнтерфейсИСМП.СтатусыКодовМаркировкиПакетно(
					ПакетКодовМаркировки,
					СтатусыКодовМаркировки,
					ПараметрыДерева.Организация,
					Истина); // Включать статусы вложенных кодов
			КонецЕсли;
			
			Если РезультатЗапросаСтатусовКодовМаркировок.ТребуетсяОбновлениеКлючаСессии Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки                    = РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки;
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
				
				Возврат ВозвращаемоеЗначение;
				
			ИначеЕсли РезультатЗапросаСтатусовКодовМаркировок.СтатусыКодовМаркировки = Неопределено Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки;
				
				Возврат ВозвращаемоеЗначение;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ИсходныеПакетыКодовМаркировки = Новый Массив();
	
	Для Каждого ПараметрыНабора Из ПараметрыНабораПакетов.НаборыПакетов Цикл
		
		Если ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(ПараметрыНабора.ВидПродукцииПодсистемы) Тогда
			ШтрихкодированиеМОТП.КоллекцияДополнительныхКолонокДереваУпаковокИСМП(
				ПараметрыДерева, ДополнительныеКолонки);
			ШтрихкодированиеМОТП.КоллекцияДополнительныхКолонокДереваУпаковокИСМП(
				ПараметрыДерева, ДополнительныеКолонкиДляВложенныхСтрокДерева, Истина);
		ИначеЕсли ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(ПараметрыНабора.ВидПродукцииПодсистемы) Тогда
			ШтрихкодированиеМОТП.КоллекцияДополнительныхКолонокДереваУпаковокМОТП(
				ПараметрыДерева, ДополнительныеКолонки);
			ШтрихкодированиеМОТП.КоллекцияДополнительныхКолонокДереваУпаковокМОТП(
				ПараметрыДерева, ДополнительныеКолонкиДляВложенныхСтрокДерева, Истина);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИсходныеПакетыКодовМаркировки, ПараметрыНабора.ПакетыКодовМаркировки);
		
	КонецЦикла;
	
	ШтрихкодированиеМОТП.НормализироватьДанныеДереваУпаковокПоПереданнымКолонкам(ДеревоУпаковок, ДополнительныеКолонки);
	
	Для Каждого Колонка Из ДеревоУпаковок.Колонки Цикл
		ЭталоннаяСтруктураДерева.Вставить(Колонка.Имя, Колонка.ТипЗначения.ПривестиЗначение(Неопределено));
	КонецЦикла;
	
	ПараметрыНабораПакетов.НаборыПакетов.Очистить();
	ПараметрыНабораПакетов.ПараметрыПоВидуПродукции.Очистить();
	
	ДанныеОбАгрегации = Новый Соответствие;
	
	ПараметрыНабораПакетовБезВидаУпаковки = НовыеПараметрыНабораПакетов();
	ПараметрыНабораПакетовВложенныхКодов  = НовыеПараметрыНабораПакетов();
	
	Для Каждого КлючИЗначение Из СтатусыКодовМаркировки Цикл
		
		ВложенныеУпаковки = КлючИЗначение.Значение.ВложенныеУпаковки;
		Если ВложенныеУпаковки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаВложенногоКодаМаркировки Из ВложенныеУпаковки Цикл
			
			СтрокаКодаМаркировки = СтрокаВложенногоКодаМаркировки.Ключ;
			
			ВидУпаковки = СтрокаКодаМаркировки.ВидУпаковки;
			ВозможнаАгрегация = (ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая
				Или ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
				Или (ВидУпаковки = ДополнительныйВидУпаковки
					И ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(СтрокаКодаМаркировки.ВидПродукции)));
			
			Если ВозможнаАгрегация И Не ПараметрыДерева.ЭтоПроверкаКодовМаркировкиИСМП Тогда
				
				ДобавитьСтрокуВНаборПакетовЗапросов(СтрокаКодаМаркировки, ПараметрыНабораПакетовВложенныхКодов);
				
			ИначеЕсли (ВозможнаАгрегация И ПараметрыДерева.ЭтоПроверкаКодовМаркировкиИСМП)
				Или ШтрихкодированиеИСКлиентСервер.ВозможнаГрупповаяУпаковка(
					СтрокаКодаМаркировки.ВидУпаковки,
					СтрокаКодаМаркировки.ДанныеРазбора) Тогда
				
				ДобавитьСтрокуВНаборПакетовЗапросов(СтрокаКодаМаркировки, ПараметрыНабораПакетовБезВидаУпаковки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ДетализироватьДоПачек = Истина;
	Если Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.КоробаСГрупповымиУпаковками
		Или Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки
		Или Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ПалетыСМонотоварнымиКоробами Тогда
		ДетализироватьДоПачек = Ложь;
	КонецЕсли;
	
	Пока ПараметрыНабораПакетовБезВидаУпаковки.НаборыПакетов.Количество() > 0 Цикл
		
		СледующиеПараметрыНабораПакетовВложенныхКодов = НовыеПараметрыНабораПакетов();
		
		Для Каждого ПараметрНабораПакетовБезВидаУпаковки Из ПараметрыНабораПакетовБезВидаУпаковки.НаборыПакетов Цикл
			
			Для Каждого ПакетКодовМаркировкиБезВидаУпаковки Из ПараметрНабораПакетовБезВидаУпаковки.ПакетыКодовМаркировки Цикл
				
				Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(ПараметрНабораПакетовБезВидаУпаковки.ВидПродукцииПодсистемы) Тогда
					РезультатЗапросаСтатусовКодовМаркировок = ЗапроситьСтатусыКодовМаркировкиПакетно(
						ПакетКодовМаркировкиБезВидаУпаковки,
						СтатусыКодовМаркировки,
						ПараметрыДерева.Организация);
				Иначе
					РезультатЗапросаСтатусовКодовМаркировок = ИнтерфейсИСМП.СтатусыКодовМаркировкиПакетно(
						ПакетКодовМаркировкиБезВидаУпаковки,
						СтатусыКодовМаркировки,
						ПараметрыДерева.Организация);
				КонецЕсли;
				
				Если РезультатЗапросаСтатусовКодовМаркировок.ТребуетсяОбновлениеКлючаСессии Тогда
					
					ВозвращаемоеЗначение.ТекстОшибки                    = РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки;
					ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
					
					Возврат ВозвращаемоеЗначение;
					
				ИначеЕсли РезультатЗапросаСтатусовКодовМаркировок.СтатусыКодовМаркировки = Неопределено Тогда
					
					ВозвращаемоеЗначение.ТекстОшибки = РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки;
					
					Возврат ВозвращаемоеЗначение;
					
				КонецЕсли;
				
				Для Каждого СтрокаКодаМаркировки Из ПакетКодовМаркировкиБезВидаУпаковки Цикл
					
					ПараметрыКодаМаркировки = СтатусыКодовМаркировки[СтрокаКодаМаркировки];
					
					Если Не ЗаполненВидУпаковкиПоДаннымСтатуса(СтрокаКодаМаркировки, ПараметрыКодаМаркировки) Тогда
						Продолжить;
					КонецЕсли;
					
					ВложенныеУпаковки = ПараметрыКодаМаркировки.ВложенныеУпаковки;
					Если ВложенныеУпаковки = Неопределено Тогда
						ВложенныеУпаковки = Новый Соответствие();
					КонецЕсли;
					
					Если ДетализироватьДоПачек Или СтрокаКодаМаркировки.ВидУпаковки <> Перечисления.ВидыУпаковокИС.Групповая Тогда
						
						Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
							Или СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
							
							Для Каждого КлючИЗначение Из ВложенныеУпаковки Цикл
								
								ВложеннаяСтрокаКодаМаркировки = КлючИЗначение.Ключ;
								
								Если Не ЗначениеЗаполнено(ВложеннаяСтрокаКодаМаркировки.ВидУпаковки)
									Или ВложеннаяСтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая
									Или ВложеннаяСтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
									Или ВложеннаяСтрокаКодаМаркировки.ВидУпаковки = ДополнительныйВидУпаковки Тогда
									
									ДобавитьСтрокуВНаборПакетовЗапросов(
										ВложеннаяСтрокаКодаМаркировки,
										СледующиеПараметрыНабораПакетовВложенныхКодов);
									
								КонецЕсли;
								
							КонецЦикла;
							
							ДанныеОбАгрегации.Вставить(СтрокаКодаМаркировки, ВложенныеУпаковки);
							
						КонецЕсли;
						
					ИначеЕсли СтрокаКодаМаркировки <> Перечисления.ВидыУпаковокИС.Потребительская Тогда
						
						ДанныеОбАгрегации.Вставить(СтрокаКодаМаркировки, ВложенныеУпаковки.Количество());
						
					КонецЕсли;
					
				КонецЦикла;
			
			КонецЦикла;
			
		КонецЦикла;
		
		ПараметрыНабораПакетовБезВидаУпаковки = СледующиеПараметрыНабораПакетовВложенныхКодов;
		
	КонецЦикла;
	
	Пока ПараметрыНабораПакетовВложенныхКодов.НаборыПакетов.Количество() > 0 Цикл
		
		СледующиеПараметрыНабораПакетовВложенныхКодов = НовыеПараметрыНабораПакетов();
		ПараметрыНабораПакетовСтатусаВложенныхКодов   = НовыеПараметрыНабораПакетов();
		
		Для Каждого ПараметрНабораПакетаВложенныхКодов Из ПараметрыНабораПакетовВложенныхКодов.НаборыПакетов Цикл
			
			Для Каждого ПакетКодовМаркировки Из ПараметрНабораПакетаВложенныхКодов.ПакетыКодовМаркировки Цикл
				
				Результат = ЗапроситьДанныеОбАгрегацииКодовМаркировкиПакетно(
					ПакетКодовМаркировки,
					ПараметрыДерева.Организация);
				
				Если Результат.ДанныеОбАгрегации <> Неопределено
					И Результат.ДанныеОбАгрегации.Количество() > 0 Тогда
					
					Для Каждого СтрокаКодаМаркировки Из ПакетКодовМаркировки Цикл
						
						СоответствиеВложенныхКодов = Результат.ДанныеОбАгрегации[СтрокаКодаМаркировки.ИсходныйШтрихкод];
						Если СоответствиеВложенныхКодов = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						ВидУпаковки   = СтрокаКодаМаркировки.ВидУпаковки;
						ВидПродукции  = СтрокаКодаМаркировки.ВидПродукции;
						ДанныеСтатуса = СтатусыКодовМаркировки[СтрокаКодаМаркировки];
						
						Если ДетализироватьДоПачек Или ВидУпаковки <> Перечисления.ВидыУпаковокИС.Групповая Тогда
							
							ВложенныеУпаковки = Новый Соответствие();
							Для Каждого КлючИЗначение Из СоответствиеВложенныхКодов Цикл
								
								ВложенныйКодМаркировки = КлючИЗначение.Ключ;
								
								ВложеннаяСтрокаКодаМаркировки = ШтрихкодированиеИС.НоваяСтруктураОбработкиШтрихкода(
									ВложенныйКодМаркировки, ВидПродукции, Ложь);
								
								ШтрихкодированиеМОТП.РассчитатьХэшСуммуНормализации(
									ВложеннаяСтрокаКодаМаркировки,
									ВложеннаяСтрокаКодаМаркировки.ДанныеРазбора);
								
								Если ТребуетсяЗапросВложенныхСтатусов(
									СтрокаКодаМаркировки,
									ВложеннаяСтрокаКодаМаркировки,
									ПараметрыДерева,
									ДанныеСтатуса) Тогда
									
									ДобавитьСтрокуВНаборПакетовЗапросов(
										ВложеннаяСтрокаКодаМаркировки,
										ПараметрыНабораПакетовСтатусаВложенныхКодов);
									
								КонецЕсли;
								
								ВложенныеУпаковки.Вставить(ВложеннаяСтрокаКодаМаркировки);
								
								Если ВложеннаяСтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая
									Или ВложеннаяСтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая Тогда
									
									ДобавитьСтрокуВНаборПакетовЗапросов(
										ВложеннаяСтрокаКодаМаркировки,
										СледующиеПараметрыНабораПакетовВложенныхКодов);
									
								КонецЕсли;
								
							КонецЦикла;
							
							ДанныеОбАгрегации.Вставить(СтрокаКодаМаркировки, ВложенныеУпаковки);
							
						Иначе
							
							ДанныеОбАгрегации.Вставить(СтрокаКодаМаркировки, СоответствиеВложенныхКодов.Количество());
							
						КонецЕсли;
						
					КонецЦикла;
					
				ИначеЕсли Результат.ТребуетсяОбновлениеКлючаСессии Тогда
					
					ВозвращаемоеЗначение.ТекстОшибки                    = Результат.ТекстОшибки;
					ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
				
					Возврат ВозвращаемоеЗначение;
					
				ИначеЕсли ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
					
					ВозвращаемоеЗначение.ТекстОшибки = Результат.ТекстОшибки;
					
					Возврат ВозвращаемоеЗначение;
					
				КонецЕсли;
				
			КонецЦикла;
		
		КонецЦикла;
		
		Для Каждого НаборПакета Из ПараметрыНабораПакетовСтатусаВложенныхКодов.НаборыПакетов Цикл
		
			Для Каждого ПакетКодовМаркировки Из НаборПакета.ПакетыКодовМаркировки Цикл
				
				Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(НаборПакета.ВидПродукцииПодсистемы) Тогда
					РезультатЗапросаСтатусовКодовМаркировок = ЗапроситьСтатусыКодовМаркировкиПакетно(
						ПакетКодовМаркировки,
						СтатусыКодовМаркировки,
						ПараметрыДерева.Организация);
				Иначе
					РезультатЗапросаСтатусовКодовМаркировок = ИнтерфейсИСМП.СтатусыКодовМаркировкиПакетно(
						ПакетКодовМаркировки,
						СтатусыКодовМаркировки,
						ПараметрыДерева.Организация);
				КонецЕсли;
				
				Если РезультатЗапросаСтатусовКодовМаркировок.ТребуетсяОбновлениеКлючаСессии Тогда
					
					ВозвращаемоеЗначение.ТекстОшибки                    = РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки;
					ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
					
					Возврат ВозвращаемоеЗначение;
					
				ИначеЕсли РезультатЗапросаСтатусовКодовМаркировок.СтатусыКодовМаркировки = Неопределено Тогда
					
					ВозвращаемоеЗначение.ТекстОшибки = РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки;
					
					Возврат ВозвращаемоеЗначение;
					
				КонецЕсли;
				
				Для Каждого СтрокаКодаМаркировки Из ПакетКодовМаркировки Цикл
					
					ПараметрыКодаМаркировки = СтатусыКодовМаркировки[СтрокаКодаМаркировки];
					Если Не ЗаполненВидУпаковкиПоДаннымСтатуса(СтрокаКодаМаркировки, ПараметрыКодаМаркировки) Тогда
						Продолжить;
					КонецЕсли;
					
					Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
						Или СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
						ДанныеОбАгрегации.Вставить(СтрокаКодаМаркировки, ПараметрыКодаМаркировки.ВложенныеУпаковки);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
		ПараметрыНабораПакетовВложенныхКодов = СледующиеПараметрыНабораПакетовВложенныхКодов;
		
	КонецЦикла;
	
	// Соответствие кодов маркировки и строк дерева
	КешGTIN = Новый ТаблицаЗначений;
	КешGTIN.Колонки.Добавить("GTIN", Метаданные.ОпределяемыеТипы.GTIN.Тип);
	Если ПараметрыДерева.УчитыватьМРЦ Тогда
		КешGTIN.Колонки.Добавить("МРЦ",  Новый ОписаниеТипов("Число"));
		КолонкиИндексаКеша = "GTIN, МРЦ";
	Иначе
		КолонкиИндексаКеша = "GTIN";
	КонецЕсли;
	КешGTIN.Индексы.Добавить(КолонкиИндексаКеша);
	
	Кеш = Новый Структура;
	Кеш.Вставить("GTIN",                  КешGTIN);
	Кеш.Вставить("КодыМаркировки",        Новый Соответствие());
	Кеш.Вставить("ЗапросИУстановкаМРЦ",   Новый Массив);
	Кеш.Вставить("ВычислениеМРЦ",         Новый Массив);
	
	ОбщиеПараметрыЗаполнения                     = ПараметрыЗаполненияДереваУпаковок();
	ОбщиеПараметрыЗаполнения.ПараметрыДерева     = ПараметрыДерева;
	ОбщиеПараметрыЗаполнения.Кеш                 = Кеш;
	ОбщиеПараметрыЗаполнения.ВариантПолученияМРЦ = ПараметрыДерева.ВариантПолученияМРЦ;
	
	Для Каждого ПакетКодовМаркировки Из ИсходныеПакетыКодовМаркировки Цикл
		
		Для Каждого СтрокаКодаМаркировки Из ПакетКодовМаркировки Цикл
			
			СтрокаДереваИзКеша = Кеш.КодыМаркировки[СтрокаКодаМаркировки.Штрихкод];
			Если СтрокаДереваИзКеша <> Неопределено
				И ПараметрыДерева.ПроверятьДублиКодовМаркировки <> "НеПроверять" Тогда
				// Код маркировки уже добавлен в упаковку
				Продолжить;
			КонецЕсли;
			
			Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская Тогда
				
				ДанныеСтатуса = СтатусыКодовМаркировки[СтрокаКодаМаркировки];
				
				СтрокаДерева = ДеревоУпаковок.Строки.Добавить();
				
				ЗаполнитьСтрокуДанныхДереваПоСтрокеКодаМаркировки(
					СтрокаДерева,
					СтрокаКодаМаркировки);
				
				ЗаполнитьСтрокуДереваПоДополнительнымКолонкам(
					СтрокаДерева,
					ДанныеСтатуса,
					ДополнительныеКолонки,
					ПоляВДанныхСтатуса);
				
				ЗаполнитьСтрокуДереваСпецификойПоСоставуКода(
					СтрокаДерева,
					СтрокаКодаМаркировки,
					ОбщиеПараметрыЗаполнения);
				
				Кеш.КодыМаркировки.Вставить(СтрокаКодаМаркировки.Штрихкод, СтрокаДерева);
				ОбновитьКешGTIN(СтрокаДерева, Кеш.GTIN, ПараметрыДерева);
				СоответствиеСтрокДерева.Вставить(СтрокаКодаМаркировки, СтрокаДерева);
				
			Иначе
				
				Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая Тогда
					
					ДанныеСтатуса = СтатусыКодовМаркировки[СтрокаКодаМаркировки];
					
				Иначе
					
					СтрокаКодаМаркировкиБезСкобок  = ЛогистическиеУпаковкиСоСкобками.Получить(СтрокаКодаМаркировки);
					СтрокаКодаМаркировкиСоСкобками = ЛогистическиеУпаковкиБезСкобок.Получить(СтрокаКодаМаркировки);
					
					Если СтрокаКодаМаркировкиБезСкобок <> Неопределено
						Или СтрокаКодаМаркировкиСоСкобками <> Неопределено Тогда
						
						// Обрабатываем из пары только код со скобками
						Если СтрокаКодаМаркировкиБезСкобок = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						
						ДанныеСтатусаБезСкобок  = СтатусыКодовМаркировки[СтрокаКодаМаркировкиБезСкобок];
						ДанныеСтатусаСоСкобками = СтатусыКодовМаркировки[СтрокаКодаМаркировкиСоСкобками];
						
						Если ДанныеСтатусаСоСкобками <> Неопределено Тогда
							ДанныеСтатуса = ДанныеСтатусаСоСкобками;
						ИначеЕсли ДанныеСтатусаБезСкобок <> Неопределено Тогда
							ДанныеСтатуса = ДанныеСтатусаБезСкобок;
						Иначе
							ДанныеСтатуса        = ИнтерфейсМОТПСлужебный.ПараметрыКодаМаркировки();
							ДанныеСтатуса.Статус = Перечисления.СтатусыКодовМаркировкиМОТП.Неопределен;
						КонецЕсли;
						
					Иначе
						ДанныеСтатуса = СтатусыКодовМаркировки[СтрокаКодаМаркировки];
					КонецЕсли;
					
				КонецЕсли;
				
				ДобавлятьУпаковкуНаВерхнийУровень =
					// Если детализация полная - любые упаковки добавляются в дерево упаковок.
					(Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.Полная
						Или Не ЗначениеЗаполнено(Детализация))
					
					// Если детализация - палеты с коробками, то все верхнеуровневые упаковки добавляются в дерево упаковок.
					Или Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ПалетыСМонотоварнымиКоробами
					
					// Если детализация - коробки с блоками, то все верхнеуровневые упаковки добавляются в дерево упаковок.
					Или Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.КоробаСГрупповымиУпаковками
					
					// Если детализация - только блоки, то все верхнеуровневые упаковки добавляются в дерево упаковок.
					Или Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки
					
					// Если детализация - блоки с пачками, то верхнеуровневые упаковки добавляются в дерево
					// упаковок только в случае если они являются блоками или пачками.
					Или (Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими
						И СтрокаКодаМаркировки.ВидУпаковки <> Перечисления.ВидыУпаковокИС.Логистическая);
					
				Если ДанныеСтатуса.Статус <> Перечисления.СтатусыКодовМаркировкиМОТП.Неопределен
					Или ДанныеСтатуса.Статус <> Перечисления.СтатусыКодовМаркировкиИСМП.Неопределен Тогда
					
					ДанныеОбАгрегацииСтрокиДерева = ДанныеСтатуса.ВложенныеУпаковки;
					
					Если ДанныеОбАгрегацииСтрокиДерева <> Неопределено
						И ДанныеОбАгрегацииСтрокиДерева.Количество() > 0 Тогда
						
						ПараметрыЗаполнения = ПараметрыЗаполненияДереваУпаковок();
						ПараметрыЗаполнения.КодыМаркировкиУпаковок  = ДанныеОбАгрегацииСтрокиДерева;
						ПараметрыЗаполнения.Кеш                     = Кеш;
						ПараметрыЗаполнения.Детализация             = Детализация;
						ПараметрыЗаполнения.ВариантПолученияМРЦ     = ПараметрыДерева.ВариантПолученияМРЦ;
						ПараметрыЗаполнения.СоответствиеСтрокДерева = СоответствиеСтрокДерева;
						ПараметрыЗаполнения.ДанныеОбАгрегации       = ДанныеОбАгрегации;
						ПараметрыЗаполнения.СтатусыКодовМаркировки  = СтатусыКодовМаркировки;
						ПараметрыЗаполнения.ДополнительныеКолонки   = ДополнительныеКолонки;
						ПараметрыЗаполнения.ПоляВДанныхСтатуса      = ПоляВДанныхСтатуса;
						ПараметрыЗаполнения.ВидПродукции            = СтрокаКодаМаркировки.ВидПродукции;
						ПараметрыЗаполнения.ПараметрыДерева         = ПараметрыДерева;
						ПараметрыЗаполнения.ЭталонСтроки            = ЭталоннаяСтруктураДерева;
						
						ПараметрыЗаполнения.ДополнительныеКолонкиДляВложенныхСтрокДерева = ДополнительныеКолонкиДляВложенныхСтрокДерева;
						ПараметрыЗаполнения.ПоляВДанныхСтатусаДляВложенныхСтрокДерева    = ПоляВДанныхСтатусаДляВложенныхСтрокДерева;
						
						Если ДобавлятьУпаковкуНаВерхнийУровень Тогда
							
							СтрокаДерева = ДеревоУпаковок.Строки.Добавить();
							
							ЗаполнитьСтрокуДанныхДереваПоСтрокеКодаМаркировки(
								СтрокаДерева,
								СтрокаКодаМаркировки);
							
							ЗаполнитьСтрокуДереваПоДополнительнымКолонкам(
								СтрокаДерева,
								ДанныеСтатуса,
								ДополнительныеКолонки,
								ПоляВДанныхСтатуса);
							
							ЗаполнитьСтрокуДереваСпецификойПоСоставуКода(
								СтрокаДерева,
								СтрокаКодаМаркировки,
								ОбщиеПараметрыЗаполнения);
							
							ПараметрыЗаполнения.ДеревоУпаковок       = СтрокаДерева;
							ПараметрыЗаполнения.ДанныеВерхнегоУровня = СтрокаДерева;
							
							ЗаполнитьДеревоУпаковокРекурсивно(ПараметрыЗаполнения);
							
							ОбновитьКешGTIN(СтрокаДерева, Кеш.GTIN, ПараметрыДерева);
							СоответствиеСтрокДерева.Вставить(СтрокаКодаМаркировки, СтрокаДерева);
							
						Иначе
							
							ДанныеСтроки = ОбщегоНазначения.СкопироватьРекурсивно(ЭталоннаяСтруктураДерева);
							
							ЗаполнитьСтрокуДанныхДереваПоСтрокеКодаМаркировки(
								ДанныеСтроки,
								СтрокаКодаМаркировки);
							
							ЗаполнитьСтрокуДереваПоДополнительнымКолонкам(
								ДанныеСтроки,
								ДанныеСтатуса,
								ДополнительныеКолонки,
								ПоляВДанныхСтатуса);
							
							ЗаполнитьСтрокуДереваСпецификойПоСоставуКода(
								ДанныеСтроки,
								СтрокаКодаМаркировки,
								ОбщиеПараметрыЗаполнения,
								Истина);
							
							ПараметрыЗаполнения.ДеревоУпаковок       = ДеревоУпаковок;
							ПараметрыЗаполнения.ДанныеВерхнегоУровня = ДанныеСтроки;
							
							ЗаполнитьДеревоУпаковокРекурсивно(ПараметрыЗаполнения);
							
							ОбновитьКешGTIN(ДанныеСтроки, Кеш.GTIN, ПараметрыДерева);
							
						КонецЕсли;
						
					ИначеЕсли ДобавлятьУпаковкуНаВерхнийУровень Тогда
						
						// Коробка без вложений или содержимое недоступно
						СтрокаДерева = ДеревоУпаковок.Строки.Добавить();
						
						ЗаполнитьСтрокуДанныхДереваПоСтрокеКодаМаркировки(
							СтрокаДерева,
							СтрокаКодаМаркировки);
						
						ЗаполнитьСтрокуДереваПоДополнительнымКолонкам(
							СтрокаДерева,
							ДанныеСтатуса,
							ДополнительныеКолонки,
							ПоляВДанныхСтатуса);
						
						ЗаполнитьСтрокуДереваСпецификойПоСоставуКода(
							СтрокаДерева,
							СтрокаКодаМаркировки,
							ОбщиеПараметрыЗаполнения);
						
						СтрокаДерева.СодержимоеНедоступно = Ложь;
						
						Кеш.КодыМаркировки.Вставить(СтрокаКодаМаркировки.Штрихкод, СтрокаДерева);
						ОбновитьКешGTIN(СтрокаДерева, Кеш.GTIN, ПараметрыДерева);
						СоответствиеСтрокДерева.Вставить(СтрокаКодаМаркировки, СтрокаДерева);
						
					КонецЕсли;
					
				ИначеЕсли ДобавлятьУпаковкуНаВерхнийУровень Тогда
					
					// Статус неопределен => вложений не существует
					СтрокаДерева = ДеревоУпаковок.Строки.Добавить();
					
					ЗаполнитьСтрокуДанныхДереваПоСтрокеКодаМаркировки(
						СтрокаДерева,
						СтрокаКодаМаркировки);
					
					ЗаполнитьСтрокуДереваПоДополнительнымКолонкам(
						СтрокаДерева,
						ДанныеСтатуса,
						ДополнительныеКолонки,
						ПоляВДанныхСтатуса);
					
					ЗаполнитьСтрокуДереваСпецификойПоСоставуКода(
						СтрокаДерева,
						СтрокаКодаМаркировки,
						ОбщиеПараметрыЗаполнения);
					
					Кеш.КодыМаркировки.Вставить(СтрокаКодаМаркировки.Штрихкод, СтрокаДерева);
					ОбновитьКешGTIN(СтрокаДерева, Кеш.GTIN, ПараметрыДерева);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ВозвращаемоеЗначение.GTIN           = Кеш.GTIN;
	ВозвращаемоеЗначение.ДеревоУпаковок = ДеревоУпаковок;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Получить данные продукции по штрихкоду EAN.
// 
// Параметры:
// 	Значение     - Массив Из Строка, Строка - GTIN для которых необходимо получить представление и прочие данные.
// 	ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции.
// Возвращаемое значение:
// 	Структура - Описание:
// 	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
// 	* ДанныеПродукцииПоШтрихкодуEAN - Соответствие Из КлючИЗначение, Неопределено - Соответствие штрихкода EAN и данных продукции из сервиса ИС МОТП:
// 	 ** Ключ - Строка - Штрихкод EAN.
// 	 ** Значение - Структура - Реквизиты продукции:
// 		*** Идентификатор - Строка - Идентификатор продукции.
// 		*** Наименование - Строка - Идентификатор продукции.
// 		*** НаименованиеПолное - Строка - Идентификатор продукции.
// 		*** GTIN - Строка - Идентификатор продукции.
// 		*** ТорговаяМарка - Строка - Идентификатор продукции.
// 		*** ТипУпаковки - Строка - Идентификатор продукции.
// 		*** КоличествоВложенныхЕдиниц - Строка - Идентификатор продукции.
// 		*** Производители - Массив Из Структура - Массив производителей:
// 			**** Наименование - Строка - Наименование производителя.
// 			**** НаименованиеПолное - Строка - Полное наименование организации.
// 			**** ИНН - Строка - ИНН.
// 			**** КПП - Строка - КПП.
// 			**** ФИО - Строка - ФИО.
// 			**** Адрес - Строка - Фактический адрес организации.
// 		*** ВыполненаСинхронизацияGS1 - Булево - Признак выполненной синхронизации с GS1.
// 	* ТекстОшибки - Строка - Текст ошибки.
Функция ДанныеПродукцииПоШтрихкодуEAN(Значение, ВидПродукции = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("ДанныеПродукцииПоШтрихкодуEAN",  Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	ДанныеПродукцииПоШтрихкодуEAN = Новый Соответствие;
	ИсходныеЗначенияЗапроса       = Новый Соответствие();
	
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		GTIN = ШтрихкодированиеИСКлиентСервер.GTINПоШтрихкодуEAN(Значение);
		ИсходныеЗначенияЗапроса.Вставить(GTIN, Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("Массив") Тогда
		Для Каждого Штрихкод Из Значение Цикл
			GTIN = ШтрихкодированиеИСКлиентСервер.GTINПоШтрихкодуEAN(Штрихкод);
			ИсходныеЗначенияЗапроса.Вставить(GTIN, Штрихкод);
		КонецЦикла;
	Иначе
		ВызватьИсключение НСтр("ru = 'Внутренняя ошибка'");
	КонецЕсли;
	
	МассивGTIN = Новый Массив();
	Для Каждого КлючИЗначение Из ИсходныеЗначенияЗапроса Цикл
		МассивGTIN.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	РезультатЗапросаСпискаПродукции  = ЗапроситьКарточкуПродукции(МассивGTIN, ВидПродукции);
	
	ВозвращаемоеЗначение.ТекстОшибки = РезультатЗапросаСпискаПродукции.ТекстОшибки;
		
	Если РезультатЗапросаСпискаПродукции.ДанныеПродукции <> Неопределено Тогда
		
		ПолученныеДанные = РезультатЗапросаСпискаПродукции.ДанныеПродукции;
		
		Для Каждого КлючИЗначение Из ИсходныеЗначенияЗапроса Цикл
			
			GTIN            = КлючИЗначение.Ключ;
			ДанныеПродукции = ПолученныеДанные.Получить(GTIN);
			
			Если ДанныеПродукции = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			КлючРезультата = ИсходныеЗначенияЗапроса.Получить(GTIN);
			Если КлючРезультата = Неопределено Тогда
				КлючРезультата = ШтрихкодированиеИСКлиентСервер.ШтрихкодEANИзGTIN(GTIN);
			КонецЕсли;
			
			ДанныеПродукцииПоШтрихкодуEAN.Вставить(КлючРезультата, ДанныеПродукции);
			
		КонецЦикла;
	
	ИначеЕсли РезультатЗапросаСпискаПродукции.ТребуетсяОбновлениеКлючаСессии Тогда
		
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		Возврат ВозвращаемоеЗначение;
		
	Иначе
		
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение.ДанныеПродукцииПоШтрихкодуEAN = ДанныеПродукцииПоШтрихкодуEAN;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#Область РеестрКодовМаркировки

// Запрос статусов кодов маркировки (включая владельца).
// В результате успешного выполнения запроса в ответе вернется список кодов маркировки,
// их статус и владелец на момент запроса. Статусы могут принимать следующие значение:
// 	EMITTED - Эмитирован,
// 	APPLIED - Нанесён,
// 	INTRODUCED - Введён в оборот,
// 	WRITTEN_OFF - Выведен из оборота, списан,
// 	WITHDRAWN - Выведен из оборота, продан,
// 	UNDEFINED - Неопределен.
// 
// Параметры:
// 	ДанныеДляЗапроса          - Массив из Структура,СтрокаТаблицыЗначений,СтрокаДереваЗначений
// 	                            Структура,СтрокаТаблицыЗначений           - Данные для запроса статусов.
// 	СтатусыКодовМаркировкиКеш - Соответствие, Неопределено                - Кеш статусов кодов маркировки.
// 	Организация               - ОпределяемыйТип.Организация               - Организация.
// Возвращаемое значение:
//	Структура - Описание:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	* РезультатОтправкиЗапроса - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* СтатусыКодовМаркировки - Соответствие - кодов маркировки и структур:
//		* Статус       - ПеречислениеСсылка.СтатусыКодовМаркировкиМОТП - Статус кода маркировки.
//		* ИННВладельца - Строка                                        - ИНН владельца кода маркировки.
// * ТекстОшибки - Строка - Текст сообщения об ошибке.
Функция ЗапроситьСтатусыКодовМаркировки(ДанныеДляЗапроса, Организация = Неопределено) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии(Организация));
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("СтатусыКодовМаркировки",         Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ИсходныеСтрокиЗапросов = Новый Соответствие();
	
	ПараметрыНормализацииТабакЛогистическаяУпаковка = РазборКодаМаркировкиИССлужебныйКлиентСервер.ПараметрыНормализацииКодаМаркировки();
	ПараметрыНормализацииТабакЛогистическаяУпаковка.ИмяСвойстваКодМаркировки = "Штрихкод";
	ПараметрыНормализацииТабакЛогистическаяУпаковка.НачинаетсяСоСкобки       = Ложь;
	ПараметрыНормализацииТабакЛогистическаяУпаковка.ВключатьСрокГодности     = Ложь;
	
	ПакетыКодовМаркировки = Новый Массив;
	ПакетКодовМаркировки  = Неопределено;
	
	Если ТипЗнч(ДанныеДляЗапроса) = Тип("Структура")
		Или ТипЗнч(ДанныеДляЗапроса) = Тип("СтрокаТаблицыЗначений") 
		Или ТипЗнч(ДанныеДляЗапроса) = Тип("СтрокаДереваЗначений") Тогда
		
		МассивИсходныхСтрок = Новый Массив();
		МассивИсходныхСтрок.Добавить(ДанныеДляЗапроса);
		
	Иначе
		
		МассивИсходныхСтрок = ДанныеДляЗапроса;
		
	КонецЕсли;
	
	Для Каждого СтрокаКодаМаркировки Из МассивИсходныхСтрок Цикл
		
		Если ПакетКодовМаркировки = Неопределено
			Или ПакетКодовМаркировки.Количество() >= 1000 Тогда
			ПакетКодовМаркировки = Новый Массив();
			ПакетыКодовМаркировки.Добавить(ПакетКодовМаркировки);
		КонецЕсли;
		
		Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			
			ЗапрашиваемыеКоды = Новый Соответствие();
			ВидыПродукции     = Новый Массив();
			ВсеСтрокиЗапросов = Новый Массив();
			ИсходныеСтрокиЗапросов.Вставить(СтрокаКодаМаркировки, ВсеСтрокиЗапросов);
			
			Если СтрокаКодаМаркировки.ТипШтрихкода <> Перечисления.ТипыШтрихкодов.SSCC Тогда
				ПакетКодовМаркировки.Добавить(СтрокаКодаМаркировки);
				ВсеСтрокиЗапросов.Добавить(СтрокаКодаМаркировки);
				ЗапрашиваемыеКоды.Вставить(СтрокаКодаМаркировки.Штрихкод, СтрокаКодаМаркировки);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаКодаМаркировки.ВидПродукции) Тогда
				ВидыПродукции.Добавить(СтрокаКодаМаркировки.ВидПродукции);
			Иначе
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
					СтрокаКодаМаркировки, "ВидыПродукцииКодаМаркировки") Тогда
					Для Каждого ВидПродукции Из СтрокаКодаМаркировки.ВидыПродукцииКодаМаркировки Цикл
						ВидыПродукции.Добавить(ВидПродукции);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			// Формирование строк с нормализацией
			Для Каждого ВидПродукции Из ВидыПродукции Цикл
				
				НормализованныйКодМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
					СтрокаКодаМаркировки, ВидПродукции, ПараметрыНормализацииТабакЛогистическаяУпаковка);
				
				Если ЗапрашиваемыеКоды.Получить(НормализованныйКодМаркировки) <> Неопределено Тогда
					Продолжить
				КонецЕсли;
				
				НормализированнаяСтрока = ШтрихкодированиеИС.НоваяСтруктураОбработкиШтрихкода(, ВидПродукции);
				ЗаполнитьЗначенияСвойств(НормализированнаяСтрока, СтрокаКодаМаркировки);
				НормализированнаяСтрока.Штрихкод = НормализованныйКодМаркировки;
				ШтрихкодированиеМОТП.РассчитатьХэшСуммуНормализации(
					НормализированнаяСтрока,
					НормализированнаяСтрока.ДанныеРазбора);
				
				ПакетКодовМаркировки.Добавить(НормализированнаяСтрока);
				ВсеСтрокиЗапросов.Добавить(НормализированнаяСтрока);
				ЗапрашиваемыеКоды.Вставить(НормализованныйКодМаркировки, НормализированнаяСтрока);
				
			КонецЦикла;
			
		Иначе
			
			ПакетКодовМаркировки.Добавить(СтрокаКодаМаркировки);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтатусыКодовМаркировки = Новый Соответствие;
	
	Для Каждого ПакетКодовМаркировки Из ПакетыКодовМаркировки Цикл
		
		РезультатЗапросаСтатусовКодовМаркировок = ЗапроситьСтатусыКодовМаркировкиПакетно(
			ПакетКодовМаркировки, СтатусыКодовМаркировки, Организация);
		
		ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатЗапросаСтатусовКодовМаркировок.РезультатОтправкиЗапроса;
		
		Если РезультатЗапросаСтатусовКодовМаркировок.ТребуетсяОбновлениеКлючаСессии Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки                    = РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки;
			ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			
			Возврат ВозвращаемоеЗначение;
			
		ИначеЕсли ЗначениеЗаполнено(РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки) Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки = РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки;
			
			Возврат ВозвращаемоеЗначение;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из ИсходныеСтрокиЗапросов Цикл
		
		ИсходнаяСтрокаЗапроса = КлючИЗначение.Ключ;
		ЗапрашиваемыеСтроки   = КлючИЗначение.Значение;
		СтрокиДляУдаления     = Новый Массив();
		ДанныеСтатуса         = Неопределено;
		
		Для Каждого ЗапрошеннаяСтрока Из ЗапрашиваемыеСтроки Цикл
			
			Если ЗапрошеннаяСтрока <> ИсходнаяСтрокаЗапроса Тогда
				СтрокиДляУдаления.Добавить(ЗапрошеннаяСтрока);
			КонецЕсли;
			
			ТекущиеДанные = СтатусыКодовМаркировки[ЗапрошеннаяСтрока];
			
			Если ДанныеСтатуса = Неопределено
				И ТекущиеДанные <> Неопределено
				И Не (ТекущиеДанные.Статус = Перечисления.СтатусыКодовМаркировкиМОТП.Неопределен
				   Или ТекущиеДанные.Статус = Перечисления.СтатусыКодовМаркировкиИСМП.Неопределен) Тогда
				ДанныеСтатуса = ТекущиеДанные;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ДанныеСтатуса <> Неопределено Тогда
			СтатусыКодовМаркировки[ИсходнаяСтрокаЗапроса] = ДанныеСтатуса;
		КонецЕсли;
		
		Для Каждого УдаляемаяСтрока Из СтрокиДляУдаления Цикл
			СтатусыКодовМаркировки.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		
	КонецЦикла;
	
	ВозвращаемоеЗначение.СтатусыКодовМаркировки = СтатусыКодовМаркировки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполнить запрос статусов для списка кодов маркировки.
// В результате успешного выполнения запроса в ответе вернется список кодов маркировки,
// их статус и владелец на момент запроса. Статусы могут принимать следующие значение:
// 	EMITTED - Эмитирован,
// 	APPLIED - Нанесён,
// 	INTRODUCED - Введён в оборот,
// 	WRITTEN_OFF - Выведен из оборота, списан,
// 	WITHDRAWN - Выведен из оборота, продан,
// 	UNDEFINED - Неопределен.
// 
// Параметры:
// 	МассивИсходныхСтрок       - Массив из Структура,СтрокаТаблицыЗначений - Массив кодов маркировки.
// 	СтатусыКодовМаркировкиКеш - Соответствие, Неопределено                - Кеш статусов кодов маркировки.
// 	Организация               - ОпределяемыйТип.Организация               - Организация.
// Возвращаемое значение:
//	Структура - Описание:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	* РезультатОтправкиЗапроса - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* СтатусыКодовМаркировки - Соответствие - кодов маркировки и структур:
//		* Статус       - ПеречислениеСсылка.СтатусыКодовМаркировкиМОТП - Статус кода маркировки.
//		* ИННВладельца - Строка                                        - ИНН владельца кода маркировки.
// * ТекстОшибки - Строка - Текст сообщения об ошибке.
Функция ЗапроситьСтатусыКодовМаркировкиПакетно(МассивИсходныхСтрок, СтатусыКодовМаркировкиКеш = Неопределено, Организация = Неопределено, ПолеИсточник = Неопределено) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии(Организация));
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("СтатусыКодовМаркировки",         Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	СоответствиеКодовМаркировки      = Новый Соответствие;
	ПараметрыЗапросаПоВидамПродукции = Новый Соответствие;
	
	ПараметрыНормализации = РазборКодаМаркировкиИССлужебныйКлиентСервер.ПараметрыНормализацииКодаМаркировки();
	ПараметрыНормализации.ИмяСвойстваКодМаркировки = "Штрихкод";
	ПараметрыНормализации.ВключатьМРЦ              = Ложь;
	
	Для Каждого ЭлементМассива Из МассивИсходныхСтрок Цикл
		
		Если ПолеИсточник = Неопределено Тогда
			СтрокаТаблицы = ЭлементМассива;
		Иначе
			СтрокаТаблицы = ЭлементМассива[ПолеИсточник];
		КонецЕсли;
		
		ВидыПродукции = Новый Массив;
		Если ЗначениеЗаполнено(СтрокаТаблицы.ВидПродукции) Тогда
			ВидыПродукции.Добавить(СтрокаТаблицы.ВидПродукции);
		Иначе
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
				СтрокаТаблицы, "ВидыПродукцииКодаМаркировки") Тогда
				Для Каждого ВидПродукции Из СтрокаТаблицы.ВидыПродукцииКодаМаркировки Цикл
					ВидыПродукции.Добавить(ВидПродукции);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыЗапроса = ПараметрыЗапросаПоВидамПродукции[СтрокаТаблицы.ВидПродукции];
		Если ПараметрыЗапроса = Неопределено Тогда
			
			ПараметрыЗапроса = Новый Структура;
			ПараметрыЗапроса.Вставить("ПараметрыURL",    Новый Массив);
			ПараметрыЗапроса.Вставить("КодыМаркировки",  Новый Массив);
			ПараметрыЗапроса.Вставить("ДобавленныеКоды", Новый Соответствие);
			
			ПараметрыЗапросаПоВидамПродукции.Вставить(СтрокаТаблицы.ВидПродукции, ПараметрыЗапроса);
			
		КонецЕсли;
		
		Для Каждого ВидПродукции Из ВидыПродукции Цикл
			
			Если СтрокаТаблицы.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
				НормализованныйКодМаркировки = СтрокаТаблицы.Штрихкод;
			Иначе
				ПараметрыНормализации.НачинаетсяСоСкобки = ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(ВидПродукции);
				НормализованныйКодМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
					СтрокаТаблицы, ВидПродукции, ПараметрыНормализации);
			КонецЕсли;
			
			СтрокиПоКодуМаркировки = СоответствиеКодовМаркировки[НормализованныйКодМаркировки];
			Если СтрокиПоКодуМаркировки = Неопределено Тогда
				СтрокиПоКодуМаркировки = Новый Соответствие;
				СоответствиеКодовМаркировки[НормализованныйКодМаркировки] = СтрокиПоКодуМаркировки;
			КонецЕсли;
			
			СтрокиПоКодуМаркировки.Вставить(СтрокаТаблицы, Истина);
			
			Если ПараметрыЗапроса.ДобавленныеКоды[НормализованныйКодМаркировки] <> Неопределено Тогда
				Продолжить;
			Иначе
				ПараметрыЗапроса.ДобавленныеКоды.Вставить(НормализованныйКодМаркировки, Истина);
			КонецЕсли;
			
			ПараметрыЗапроса.КодыМаркировки.Добавить(НормализованныйКодМаркировки);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого КлючИЗначениеПараметровЗапроса Из ПараметрыЗапросаПоВидамПродукции Цикл
		
		ВидПродукции     = КлючИЗначениеПараметровЗапроса.Ключ;
		ПараметрыЗапроса = КлючИЗначениеПараметровЗапроса.Значение;
		
		URLЗапроса = СтрШаблон(
			"api/v3/true-api/cises/info%1",
			ИнтерфейсИСМП.ПараметрыЗапроса(ПараметрыЗапроса.ПараметрыURL));
		
		РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(
			URLЗапроса, ПараметрыЗапроса.КодыМаркировки, КлючСессии,
			"POST", ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов(""));
		
		РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
		
		ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
		
		Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
			
			Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
				
				ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
				
				Если ДанныеОбработки = Неопределено Тогда
					
					ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
						URLЗапроса,
						РезультатОтправкиЗапроса);
					
				Иначе
					
					Если ТипЗнч(ДанныеОбработки) = Тип("Массив") Тогда
						
						Если СтатусыКодовМаркировкиКеш = Неопределено Тогда
							ВозвращаемоеЗначение.СтатусыКодовМаркировки = Новый Соответствие;
						Иначе
							ВозвращаемоеЗначение.СтатусыКодовМаркировки = СтатусыКодовМаркировкиКеш;
						КонецЕсли;
						
						Для Каждого ЭлементДанных Из ДанныеОбработки Цикл
							
							ДанныеКодаМаркировки = ЭлементДанных["cisInfo"];
							
							КодОшибки = ЭлементДанных["errorCode"];
							Если КодОшибки = "404" Тогда
								
								ПараметрыКодаМаркировки        = ИнтерфейсМОТПСлужебный.ПараметрыКодаМаркировки();
								ПараметрыКодаМаркировки.Статус = Перечисления.СтатусыКодовМаркировкиМОТП.Неопределен;
								
							Иначе
								
								ПараметрыКодаМаркировки = ИнтерфейсМОТПСлужебный.ПараметрыКодаМаркировки(ДанныеКодаМаркировки, ВидПродукции);
								
							КонецЕсли;
							
							КодМаркировки = ДанныеКодаМаркировки["cis"];
							РезультатПоиска = СоответствиеКодовМаркировки[КодМаркировки];
							Если РезультатПоиска <> Неопределено Тогда
								
								Для Каждого КлючИЗначение Из РезультатПоиска Цикл
									
									СтрокаТаблицы = КлючИЗначение.Ключ;
									
									Если Не ЗначениеЗаполнено(СтрокаТаблицы.ВидПродукции)
										И ЗначениеЗаполнено(ПараметрыКодаМаркировки.ВидПродукции) Тогда
										СтрокаТаблицы.ВидПродукции = ПараметрыКодаМаркировки.ВидПродукции;
									КонецЕсли;
									
									Если Не ЗначениеЗаполнено(СтрокаТаблицы.ВидУпаковки)
										И ЗначениеЗаполнено(ПараметрыКодаМаркировки.ВидУпаковки) Тогда
										СтрокаТаблицы.ВидУпаковки = ПараметрыКодаМаркировки.ВидУпаковки;
									КонецЕсли;
									
									ВозвращаемоеЗначение.СтатусыКодовМаркировки.Вставить(
										СтрокаТаблицы, ПараметрыКодаМаркировки);
									
								КонецЦикла;
								
							КонецЕсли;
							
						КонецЦикла;
					
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
				
				Возврат ВозвращаемоеЗначение;
				
			ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 404 Тогда
				
				Если СтатусыКодовМаркировкиКеш = Неопределено Тогда
					ВозвращаемоеЗначение.СтатусыКодовМаркировки = Новый Соответствие;
				Иначе
					ВозвращаемоеЗначение.СтатусыКодовМаркировки = СтатусыКодовМаркировкиКеш;
				КонецЕсли;
				
				Для Каждого СтрокаТаблицы Из МассивИсходныхСтрок Цикл
					
					ПараметрыКодаМаркировки = ИнтерфейсМОТПСлужебный.ПараметрыКодаМаркировки();
					
					Если ИнтеграцияИСКлиентСервер.ЭтоПродукцияМОТП(СтрокаТаблицы.ВидПродукции) Тогда
						ПараметрыКодаМаркировки.Статус = Перечисления.СтатусыКодовМаркировкиМОТП.Неопределен;
					Иначе
						ПараметрыКодаМаркировки.Статус = Перечисления.СтатусыКодовМаркировкиИСМП.Неопределен;
					КонецЕсли;
					
					ВозвращаемоеЗначение.СтатусыКодовМаркировки.Вставить(СтрокаТаблицы, ПараметрыКодаМаркировки);
					
				КонецЦикла;
				
			Иначе
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			КонецЕсли;
			
		ИначеЕсли ВозвращаемоеЗначение.РезультатОтправкиЗапроса.КодСостояния = 404 Тогда
			
			Если СтатусыКодовМаркировкиКеш = Неопределено Тогда
				ВозвращаемоеЗначение.СтатусыКодовМаркировки = Новый Соответствие;
			Иначе
				ВозвращаемоеЗначение.СтатусыКодовМаркировки = СтатусыКодовМаркировкиКеш;
			КонецЕсли;
			
			Для Каждого СтрокаТаблицы Из МассивИсходныхСтрок Цикл
				
				ПараметрыКодаМаркировки = ИнтерфейсМОТПСлужебный.ПараметрыКодаМаркировки();
				
				Если ИнтеграцияИСКлиентСервер.ЭтоПродукцияМОТП(СтрокаТаблицы.ВидПродукции) Тогда
					ПараметрыКодаМаркировки.Статус = Перечисления.СтатусыКодовМаркировкиМОТП.Неопределен;
				Иначе
					ПараметрыКодаМаркировки.Статус = Перечисления.СтатусыКодовМаркировкиИСМП.Неопределен;
				КонецЕсли;
				
				ВозвращаемоеЗначение.СтатусыКодовМаркировки.Вставить(СтрокаТаблицы, ПараметрыКодаМаркировки);
				
			КонецЦикла;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Проверяет коды пачек, блоков и логистических упаковок на принадлежность к серой зоне.
//
// Параметры:
// 	МассивИсходныхСтрок       - Массив из Структура, СтрокаТаблицыЗначений - Массив кодов маркировки.
// 	СтатусыКодовМаркировкиКеш - Соответствие, Неопределено                - Кеш статусов кодов маркировки.
// 	Организация               - ОпределяемыйТип.Организация               - Организация.
// Возвращаемое значение:
//	Структура - Описание:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	* РезультатОтправкиЗапроса - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* СостояниеСеройЗоны - Соответствие Из КлючИЗначение:
//		* Ключ - Структура - Исходная строка
//		* Значение - Структура - Состояние серой зоны.
// * ТекстОшибки - Строка - Текст сообщения об ошибке.
Функция ПроверитьСеруюЗонуКодовМаркировки(ДанныеДляЗапроса, Организация = Неопределено) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии(Организация));
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("СостояниеСеройЗоны",             Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ЛогистическиеУпаковкиСоСкобками = Новый Соответствие;
	ЛогистическиеУпаковкиБезСкобок  = Новый Соответствие;
	
	ПараметрыНормализацииТабакЛогистическаяУпаковка = РазборКодаМаркировкиИССлужебныйКлиентСервер.ПараметрыНормализацииКодаМаркировки();
	ПараметрыНормализацииТабакЛогистическаяУпаковка.ИмяСвойстваКодМаркировки = "Штрихкод";
	ПараметрыНормализацииТабакЛогистическаяУпаковка.НачинаетсяСоСкобки       = Ложь;
	
	ПакетыКодовМаркировки = Новый Массив;
	ПакетКодовМаркировки  = Неопределено;
	
	Если ТипЗнч(ДанныеДляЗапроса) = Тип("Структура")
		Или ТипЗнч(ДанныеДляЗапроса) = Тип("СтрокаТаблицыЗначений") 
		Или ТипЗнч(ДанныеДляЗапроса) = Тип("СтрокаДереваЗначений") Тогда
		
		МассивИсходныхСтрок = Новый Массив();
		МассивИсходныхСтрок.Добавить(ДанныеДляЗапроса);
		
	Иначе
		
		МассивИсходныхСтрок = ДанныеДляЗапроса;
		
	КонецЕсли;
	
	Для Каждого СтрокаКодаМаркировки Из МассивИсходныхСтрок Цикл
		
		Если ПакетКодовМаркировки = Неопределено
			Или ПакетКодовМаркировки.Количество() >= 25 Тогда
			ПакетКодовМаркировки = Новый Массив();
			ПакетыКодовМаркировки.Добавить(ПакетКодовМаркировки);
		КонецЕсли;
		
		Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			
			КодМаркировкиБезСкобок = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
				СтрокаКодаМаркировки, СтрокаКодаМаркировки.ВидПродукции, ПараметрыНормализацииТабакЛогистическаяУпаковка);
			
			Если КодМаркировкиБезСкобок <> СтрокаКодаМаркировки.Штрихкод Тогда
				
				// Формирование копии элемента без скобок
				СтрокаКодаМаркировкиБезСкобок = ШтрихкодированиеИС.НоваяСтруктураОбработкиШтрихкода(, СтрокаКодаМаркировки.ВидПродукции);
				ЗаполнитьЗначенияСвойств(СтрокаКодаМаркировкиБезСкобок, СтрокаКодаМаркировки);
				СтрокаКодаМаркировкиБезСкобок.Штрихкод = КодМаркировкиБезСкобок;
				ШтрихкодированиеМОТП.РассчитатьХэшСуммуНормализации(
					СтрокаКодаМаркировкиБезСкобок,
					СтрокаКодаМаркировкиБезСкобок.ДанныеРазбора);
				ЛогистическиеУпаковкиБезСкобок.Вставить(СтрокаКодаМаркировкиБезСкобок, СтрокаКодаМаркировки);
				ЛогистическиеУпаковкиСоСкобками.Вставить(СтрокаКодаМаркировки, СтрокаКодаМаркировкиБезСкобок);
				
				ПакетКодовМаркировки.Добавить(СтрокаКодаМаркировкиБезСкобок);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ПакетКодовМаркировки.Добавить(СтрокаКодаМаркировки);
		
	КонецЦикла;
	
	СостояниеСеройЗоны = Новый Соответствие;
	
	Для Каждого ПакетКодовМаркировки Из ПакетыКодовМаркировки Цикл
		
		РезультатЗапросаСтатусовКодовМаркировок = ПроверитьСеруюЗонуКодовМаркировкиПакетно(
			ПакетКодовМаркировки, СостояниеСеройЗоны, Организация);
		
		ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатЗапросаСтатусовКодовМаркировок.РезультатОтправкиЗапроса;
		
		Если РезультатЗапросаСтатусовКодовМаркировок.ТребуетсяОбновлениеКлючаСессии Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки                    = РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки;
			ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			
			Возврат ВозвращаемоеЗначение;
			
		ИначеЕсли ЗначениеЗаполнено(РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки) Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки = РезультатЗапросаСтатусовКодовМаркировок.ТекстОшибки;
			
			Возврат ВозвращаемоеЗначение;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ПакетКодовМаркировки Из ПакетыКодовМаркировки Цикл
		
		Для Каждого СтрокаКодаМаркировки Из ПакетКодовМаркировки Цикл
			
			Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
				
				СтрокаКодаМаркировкиБезСкобок  = ЛогистическиеУпаковкиСоСкобками.Получить(СтрокаКодаМаркировки);
				СтрокаКодаМаркировкиСоСкобками = ЛогистическиеУпаковкиБезСкобок.Получить(СтрокаКодаМаркировки);
				
				Если СтрокаКодаМаркировкиБезСкобок <> Неопределено
					Или СтрокаКодаМаркировкиСоСкобками <> Неопределено Тогда
					
					// Обрабатываем из пары только код со скобками
					Если СтрокаКодаМаркировкиБезСкобок = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					
					СтрокаКодаМаркировкиСоСкобками = СтрокаКодаМаркировки;
					
					ДанныеСтатусаБезСкобок  = СостояниеСеройЗоны[СтрокаКодаМаркировкиБезСкобок];
					ДанныеСтатусаСоСкобками = СостояниеСеройЗоны[СтрокаКодаМаркировкиСоСкобками];
					
					Если ДанныеСтатусаСоСкобками <> Неопределено Тогда
						ДанныеСтатуса = ДанныеСтатусаСоСкобками;
					Иначе
						ДанныеСтатуса = ДанныеСтатусаБезСкобок;
					КонецЕсли;
					
					СостояниеСеройЗоны[СтрокаКодаМаркировки] = ДанныеСтатуса;
					СостояниеСеройЗоны.Удалить(СтрокаКодаМаркировкиБезСкобок);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ВозвращаемоеЗначение.СостояниеСеройЗоны = СостояниеСеройЗоны;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Проверяет коды пачек, блоков и логистических упаковок на принадлежность к серой зоне.
//
// Параметры:
// 	МассивИсходныхСтрок       - Массив из Структура, СтрокаТаблицыЗначений - Массив кодов маркировки.
// 	СтатусыКодовМаркировкиКеш - Соответствие, Неопределено                - Кеш статусов кодов маркировки.
// 	Организация               - ОпределяемыйТип.Организация               - Организация.
// Возвращаемое значение:
//	Структура - Описание:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	* РезультатОтправкиЗапроса - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* СостояниеСеройЗоны - Соответствие Из КлючИЗначение:
//		* Ключ - Структура - Исходная строка
//		* Значение - Структура - Состояние серой зоны.
// * ТекстОшибки - Строка - Текст сообщения об ошибке.
Функция ПроверитьСеруюЗонуКодовМаркировкиПакетно(МассивИсходныхСтрок, СостояниеСеройЗоныКеш = Неопределено, Организация = Неопределено) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии(Организация));
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("СостояниеСеройЗоны",             Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	НормализованныеКодыМаркировки = Новый Массив;
	СоответствиеКодовМаркировки   = Новый Соответствие;
	
	ПараметрыНормализации = РазборКодаМаркировкиИССлужебныйКлиентСервер.ПараметрыНормализацииКодаМаркировки();
	ПараметрыНормализации.ИмяСвойстваКодМаркировки = "Штрихкод";
	ПараметрыНормализации.ВключатьМРЦ = Ложь;
	
	Для Каждого СтрокаТаблицы Из МассивИсходныхСтрок Цикл
		
		Если СтрокаТаблицы.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			НормализованныйКодМаркировки = СтрокаТаблицы.Штрихкод;
		Иначе
			НормализованныйКодМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
				СтрокаТаблицы, СтрокаТаблицы.ВидПродукции, ПараметрыНормализации);
		КонецЕсли;
		
		Если НормализованныеКодыМаркировки.Найти(НормализованныйКодМаркировки) = Неопределено Тогда
			НормализованныеКодыМаркировки.Добавить(НормализованныйКодМаркировки);
			СоответствиеКодовМаркировки.Вставить(НормализованныйКодМаркировки, Новый Массив);
		КонецЕсли;
		
		СоответствиеКодовМаркировки[НормализованныйКодМаркировки].Добавить(СтрокаТаблицы);
		
	КонецЦикла;
	
	URLЗапроса = "api/v3/true-api/cises/gz/info";
	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(
		URLЗапроса, НормализованныеКодыМаркировки, КлючСессии,
		"POST", ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов(""));
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				Если ТипЗнч(ДанныеОбработки) = Тип("Массив") Тогда
					
					Если СостояниеСеройЗоныКеш = Неопределено Тогда
						ВозвращаемоеЗначение.СостояниеСеройЗоны = Новый Соответствие;
					Иначе
						ВозвращаемоеЗначение.СостояниеСеройЗоны = СостояниеСеройЗоныКеш;
					КонецЕсли;
					
					Для Каждого ЭлементМассива Из ДанныеОбработки Цикл
						
						КодОшибки = ЭлементМассива["errorCode"];
						Если КодОшибки = "404" Тогда
							Продолжить;
						КонецЕсли;
						
						ЭлементДанных = ЭлементМассива["info"];
						
						Значение = ЭлементДанных["packageType"];
						Если Значение <> Неопределено Тогда
							ВидУпаковки = ИнтерфейсИСМПСлужебный.ВидУпаковки(Значение, Перечисления.ВидыПродукцииИС.Табак);
						Иначе
							ВидУпаковки = Неопределено;
						КонецЕсли;
						
						СостояниеСеройЗоны = Новый Структура;
						СостояниеСеройЗоны.Вставить("ВидУпаковки",       ВидУпаковки);
						СостояниеСеройЗоны.Вставить("ВСеройЗоне",        Ложь);
						СостояниеСеройЗоны.Вставить("СодержитСерыеКоды", Ложь);
						СостояниеСеройЗоны.Вставить("КоличествоПачек",            0);
						СостояниеСеройЗоны.Вставить("КоличествоБлоков",           0);
						СостояниеСеройЗоны.Вставить("КоличествоВложенныхЕдиниц",  0);
						
						Значение = ЭлементДанных["inGrayZone"];
						Если Значение <> Неопределено Тогда
							СостояниеСеройЗоны.ВСеройЗоне = Значение;
						КонецЕсли;
						
						Значение = ЭлементДанных["containsGrayCodes"];
						Если Значение <> Неопределено Тогда
							СостояниеСеройЗоны.СодержитСерыеКоды = Значение;
						КонецЕсли;
						
						Значение = ЭлементДанных["gzPacks"];
						Если Значение <> Неопределено Тогда
							СостояниеСеройЗоны.КоличествоПачек = Значение;
						КонецЕсли;
						
						Значение = ЭлементДанных["gzBlocks"];
						Если Значение <> Неопределено Тогда
							СостояниеСеройЗоны.КоличествоБлоков = Значение;
						КонецЕсли;
						
						Значение = ЭлементДанных["aggCount"];
						Если Значение <> Неопределено Тогда
							СостояниеСеройЗоны.КоличествоВложенныхЕдиниц = Значение;
						КонецЕсли;
						
						КодМаркировки = ЭлементДанных["code"];
						Для Каждого СтрокаТаблицы Из СоответствиеКодовМаркировки[КодМаркировки] Цикл
							
							ВозвращаемоеЗначение.СостояниеСеройЗоны.Вставить(
								СтрокаТаблицы, СостояниеСеройЗоны);
							
						КонецЦикла;
						
					КонецЦикла;
				
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			
			Возврат ВозвращаемоеЗначение;
			
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 404 Тогда
			
			Если СостояниеСеройЗоныКеш = Неопределено Тогда
				ВозвращаемоеЗначение.СостояниеСеройЗоны = Новый Соответствие;
			Иначе
				ВозвращаемоеЗначение.СостояниеСеройЗоны = СостояниеСеройЗоныКеш;
			КонецЕсли;
			
			Для Каждого СтрокаТаблицы Из МассивИсходныхСтрок Цикл
				
				СостояниеСеройЗоны = Новый Структура;
				СостояниеСеройЗоны.Вставить("ВидУпаковки",       СтрокаТаблицы.ВидУпаковки);
				СостояниеСеройЗоны.Вставить("ВСеройЗоне",        Ложь);
				СостояниеСеройЗоны.Вставить("СодержитСерыеКоды", Ложь);
				СостояниеСеройЗоны.Вставить("КоличествоПачек",            0);
				СостояниеСеройЗоны.Вставить("КоличествоБлоков",           0);
				СостояниеСеройЗоны.Вставить("КоличествоВложенныхЕдиниц",  0);
				
				ВозвращаемоеЗначение.СостояниеСеройЗоны.Вставить(СтрокаТаблицы, СостояниеСеройЗоны);
				
			КонецЦикла;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	ИначеЕсли ВозвращаемоеЗначение.РезультатОтправкиЗапроса.КодСостояния = 404 Тогда
		
		Если СостояниеСеройЗоныКеш = Неопределено Тогда
			ВозвращаемоеЗначение.СостояниеСеройЗоны = Новый Соответствие;
		Иначе
			ВозвращаемоеЗначение.СостояниеСеройЗоны = СостояниеСеройЗоныКеш;
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из МассивИсходныхСтрок Цикл
			
			СостояниеСеройЗоны = Новый Структура;
			СостояниеСеройЗоны.Вставить("ВидУпаковки",       СтрокаТаблицы.ВидУпаковки);
			СостояниеСеройЗоны.Вставить("ВСеройЗоне",        Ложь);
			СостояниеСеройЗоны.Вставить("СодержитСерыеКоды", Ложь);
			СостояниеСеройЗоны.Вставить("КоличествоПачек",            0);
			СостояниеСеройЗоны.Вставить("КоличествоБлоков",           0);
			СостояниеСеройЗоны.Вставить("КоличествоВложенныхЕдиниц",  0);
			
			ВозвращаемоеЗначение.СостояниеСеройЗоны.Вставить(СтрокаТаблицы, СостояниеСеройЗоны);
			
		КонецЦикла;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполнить запрос данных об агрегации кодов маркировки в ИС МОТП.
// В результате успешного выполнения запроса в ответе вернется информация о составе кода агрегата.
// В запросе следует указывать только один код маркировки.
// 
// Параметры:
// 	СтрокаКодаМаркировки - Структура, СтрокаТаблицыЗначений - Строка кода маркировки.
// Возвращаемое значение:
//	 Структура - Вложенные коды упаковок:
//		* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//		* РезультатОтправкиЗапроса - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//		* ТекстОшибки - Строка - Текст ошибки
//		* ДанныеОбАгрегации - Массив из Структура:
//			* КодМаркировки - Строка                                        - Код маркировки.
//			* Статус        - ПеречислениеСсылка.СтатусыКодовМаркировкиМОТП - Статус кода маркировки.
//			* ИНН           - Строка                                        - ИНН владельца кода маркировки.
Функция ЗапроситьДанныеОбАгрегацииКодовМаркировки(СтрокаКодаМаркировки, Организация = Неопределено) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии(Организация));
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	ВозвращаемоеЗначение.Вставить("ДанныеОбАгрегации",              Неопределено);
	ВозвращаемоеЗначение.Вставить("СодержимоеНедоступно",           Ложь);
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ПараметрыНормализации = РазборКодаМаркировкиИССлужебныйКлиентСервер.ПараметрыНормализацииКодаМаркировки();
	ПараметрыНормализации.ИмяСвойстваКодМаркировки = "Штрихкод";
	ПараметрыНормализации.ВключатьМРЦ = Ложь;
	
	Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
		НормализованныйКодМаркировки = СтрокаКодаМаркировки.Штрихкод;
	Иначе
		НормализованныйКодМаркировки = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(СтрокаКодаМаркировки, СтрокаКодаМаркировки.ВидПродукции, ПараметрыНормализации);
	КонецЕсли;
	
	ТелоЗапроса = Новый Массив;
	ТелоЗапроса.Добавить(НормализованныйКодМаркировки);
	
	URLЗапроса = "api/v3/true-api/cises/aggregated/list?pg=tobacco";
	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(
		URLЗапроса, ТелоЗапроса, КлючСессии,
		"POST", ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов(""));
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.ДанныеОбАгрегации = ДанныеОбработки;
				
			КонецЕсли;
		
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			
			Возврат ВозвращаемоеЗначение;
			
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 403 Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			ВозвращаемоеЗначение.СодержимоеНедоступно = Истина;
			
			Возврат ВозвращаемоеЗначение;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполнить запрос данных об агрегации кодов маркировки в ИС МОТП.
// В результате успешного выполнения запроса в ответе вернется информация о составе кода агрегата.
// В запросе следует указывать только один код маркировки.
// 
// Параметры:
// 	СтрокаКодаМаркировки - Структура - Строка кода маркировки.
// Возвращаемое значение:
//	 Структура - Вложенные коды упаковок:
//		* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//		* РезультатОтправкиЗапроса - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//		* ТекстОшибки - Строка - Текст ошибки
//		* ДанныеОбАгрегации - Массив из Структура:
//			* КодМаркировки - Строка                                        - Код маркировки.
//			* Статус        - ПеречислениеСсылка.СтатусыКодовМаркировкиМОТП - Статус кода маркировки.
//			* ИНН           - Строка                                        - ИНН владельца кода маркировки.
Функция ЗапроситьДанныеОбАгрегацииКодовМаркировкиПакетно(ПакетКодовМаркировки, Организация = Неопределено) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии(Организация));
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	ВозвращаемоеЗначение.Вставить("ДанныеОбАгрегации",              Неопределено);
	ВозвращаемоеЗначение.Вставить("СодержимоеНедоступно",           Ложь);
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ПараметрыНормализации = РазборКодаМаркировкиИССлужебныйКлиентСервер.ПараметрыНормализацииКодаМаркировки();
	ПараметрыНормализации.ИмяСвойстваКодМаркировки = "Штрихкод";
	ПараметрыНормализации.ВключатьМРЦ = Ложь;
	
	ИсходныеСтроки = Новый Соответствие();
	ВидыПродукции  = Новый Соответствие();
	ТелоЗапроса    = Новый Массив;
	
	Для Каждого ЭлементПакета Из ПакетКодовМаркировки Цикл
		
		ТелоЗапроса.Добавить(ЭлементПакета.ИсходныйШтрихкод);
		ИсходныеСтроки.Вставить(ЭлементПакета.Штрихкод, ЭлементПакета);
		
		Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(ЭлементПакета.ВидПродукции) Тогда
			ВидыПродукции.Вставить(Перечисления.ВидыПродукцииИС.Табак);
		ИначеЕсли ЭлементПакета.ВидПродукции = Перечисления.ВидыПродукцииИС.УпакованнаяВода Тогда
			ВидыПродукции.Вставить(Перечисления.ВидыПродукцииИС.УпакованнаяВода);
		ИначеЕсли ЭлементПакета.ВидПродукции = Перечисления.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС
			Или ЭлементПакета.ВидПродукции = Перечисления.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС Тогда
			ВидыПродукции.Вставить(Перечисления.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС);
		КонецЕсли;
		
	КонецЦикла;
	
	ЗначенияПараметровТоварнойГруппы = Новый Массив();
	Для Каждого КлючИЗначение Из ВидыПродукции Цикл
		ЗначенияПараметровТоварнойГруппы.Добавить(ИнтерфейсИСМПСлужебный.ТоварнаяГруппа(КлючИЗначение.Ключ));
	КонецЦикла;
	ЗначениеПараметраТоварнаяГруппа = СтрСоединить(ЗначенияПараметровТоварнойГруппы, ",");
	
	ПараметрыЗапроса = Новый Массив();
	Если ЗначениеЗаполнено(ЗначениеПараметраТоварнаяГруппа) Тогда
		ПараметрыЗапроса.Добавить(
			СтрШаблон(
				"pg=%1",
				ЗначениеПараметраТоварнаяГруппа));
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		"api/v3/true-api/cises/aggregated/list%1",
		ИнтерфейсИСМП.ПараметрыЗапроса(ПараметрыЗапроса));
		
	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(
		URLЗапроса, ТелоЗапроса, КлючСессии,
		"POST", ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов(""));
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.ДанныеОбАгрегации = ДанныеОбработки;
				
			КонецЕсли;
		
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			
			Возврат ВозвращаемоеЗначение;
			
		ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 403 Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			ВозвращаемоеЗначение.СодержимоеНедоступно = Истина;
			
			Возврат ВозвращаемоеЗначение;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// В результате успешного выполнения запроса в ответе вернется информация о максимальной
// розничной цене табачной продукции, если она установлена.
// 
// Параметры:
// 	ДанныеДляЗапроса - Массив из Структура,СтрокаТаблицыЗначений,СтрокаДереваЗначений,
// 	Структура,СтрокаТаблицыЗначений,СтрокаДереваЗначений - Коллекция или элемент коллекции.
// Возвращаемое значение:
//	Структура - Описание:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	* РезультатОтправкиЗапроса - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* ДанныеПродукции - Соответствие - где:
//   * Ключ - Структура,СтрокаТаблицыЗначений,СтрокаДереваЗначений - Переданное значение в параметре ДанныеДляЗапроса,
//   * Значение - Структура - данные о статусе и МРЦ:
//		** Статус - ПеречислениеСсылка.СтатусыКодовМаркировкиМОТП - Статус кода.
//		** МРЦ  - Число  - МРЦ для кода маркировки. Если указано -1, то МРЦ не ограничено.
//		** Наименование - Строка - Наименование продукции.
// * ТекстОшибки - Строка - Текст сообщения об ошибке.
Функция ЗапроситьМРЦДляКодаМаркировки(ДанныеДляЗапроса) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("ДанныеПродукции",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
		
	РезультатЗапросаСтатусов = ЗапроситьСтатусыКодовМаркировки(ДанныеДляЗапроса);
	
	Если РезультатЗапросаСтатусов.СтатусыКодовМаркировки <> Неопределено Тогда
		
		ВозвращаемоеЗначение.ДанныеПродукции = РезультатЗапросаСтатусов.СтатусыКодовМаркировки;
		
	ИначеЕсли РезультатЗапросаСтатусов.ТребуетсяОбновлениеКлючаСессии Тогда
		
		ВозвращаемоеЗначение.ТекстОшибки = РезультатЗапросаСтатусов.ТекстОшибки;
		ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
		
		Возврат ВозвращаемоеЗначение;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = РезультатЗапросаСтатусов.ТекстОшибки;
		
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Запрос списка кодов происходит в несколько этапов:
// 1. Формирование заказа на список кодов.
// Фильтр указания типа упаковки:
// 	Короб - box
// 	Блок  - block
// 	Пачка - pack
// 2. Проверка статуса заказа на формирование списка кодов маркировки участника.
// 3. Получение результата заказа на формирование списка кодов маркировки участника.
// 
// После успешного завершения по адресу результата заказа будет доступен архив со списком кодов,
// принадлежащих участнику, сформировавший запрос.
// Файл будет доступен для скачивание в течение часа, после чего будет удален.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация.
// 	GTIN - ОпределяемыйТип.GTIN - GTIN
// 	ТипУпаковки - Строка - Для получения списка логистических упаковок: "box", для блоков: "black", для пачек "pack".
// Возвращаемое значение:
//	Структура - Описание:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	* РезультатОтправкиЗапроса - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* ИдентификаторЗапроса - Строка - Идентификатор запроса.
//	* ТекстОшибки - Строка - Текст сообщения об ошибке.
Функция СоздатьЗаказНаФормированиеСпискаКодовМаркировкиУчастника(Организация, GTIN, ТипУпаковки = Неопределено) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии(Организация));
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("ИдентификаторЗаказа",            Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ТелоЗапроса = Новый Структура;
	ТелоЗапроса.Вставить("gtin", GTIN);
	Если ЗначениеЗаполнено(ТипУпаковки) Тогда
		ТелоЗапроса.Вставить("packageType", ТипУпаковки);
	КонецЕсли;
	
	URLЗапроса = "api/v3/true-api/cises/my";
	РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(URLЗапроса, ТелоЗапроса, КлючСессии, "POST", ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов(""));
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ВозвращаемоеЗначение.ИдентификаторЗаказа = РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);

			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			КонецЕсли;

			Возврат ВозвращаемоеЗначение;

		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область РеестрУчастников

// Выполнить запрос статуса регистрации в МОТП по ИНН.
// В результате успешного выполнения запроса в ответе вернется
// информация о статусе регистрации в ИС МОТП, запрошенного ИНН.
// Сценарий использования: Проверка статуса регистрации перед передачей заявки о регистрации участника.
// 
// Параметры:
// 	ИНН - Строка - ИНН участника.
// Возвращаемое значение:
//	Структура - Структура со свойствами:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Необходимость обновления ключа сессии.
//	* РезультатОтправкиЗапроса       - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* Статус                         - ПеречислениеСсылка.СтатусыУчастниковМОТП - Статус участника.
//  * ТекстОшибки                    - Строка - Текст сообщения об ошибке.
Функция ЗапроситьСтатусРегистрацииУчастника(ИНН) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии());
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("Статус",                         Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ПустаяСтрока(ИНН) Тогда
		ВызватьИсключение НСтр("ru = 'Внутренняя ошибка: В метод ЗапроситьСтатусРегистрацииУчастника не передан ИНН'");
	КонецЕсли;
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		"v3/participants/%1",
		ИНН);
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, КлючСессии, ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов());
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.Статус = ИнтерфейсМОТПСлужебный.СтатусУчастника(ДанныеОбработки.status);
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);

			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			КонецЕсли;

			Возврат ВозвращаемоеЗначение;
			
		КонецЕсли;

	Иначе

		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);

	КонецЕсли;

	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполнить запрос списка ИНН контрагентов организации.
// В результате успешного выполнения запроса в ответе вернется список, состоящий из ИНН контрагентов.
// Организация должна быть авторизована в ИС МОТП.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация, для которой запрашивается список ИНН контрагентов.
// 
// Возвращаемое значение:
//	Структура - Структура со свойствами:
//	* ТребуетсяОбновлениеКлючаСессии - Булево    - Необходимость обновления ключа сессии.
//	* РезультатОтправкиЗапроса       - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON)
//	* ИННКонтрагентов                - Массив    - Список ИНН контрагентов.
//  * ТекстОшибки                    - Строка    - Текст сообщения об ошибке.
Функция ЗапроситьСписокКонтрагентовУчастника(Организация) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии(Организация));
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("ИННКонтрагентов",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	URLЗапроса = "v3/partners/my";
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, КлючСессии, ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов());
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ВозвращаемоеЗначение.ИННКонтрагентов = ДанныеОбработки;
				
			КонецЕсли;

		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);

			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			КонецЕсли;

			Возврат ВозвращаемоеЗначение;

		КонецЕсли;

	Иначе

		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область РеестрПродукции

// Выполнить запрос списка продукции.
// В результате успешного выполнения запроса в ответе вернется список продукции,
// содержащие id - идентификатор продукта, gtin - международный товарный идентификатор и producerINN - ИНН производителя.
// 
// Возвращаемое значение:
//	Структура - Структура со свойствами:
//	* ТребуетсяОбновлениеКлючаСессии - Булево          - Необходимость обновления ключа сессии.
//	* РезультатОтправкиЗапроса       - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* ПродукцияОрганизации           - ТаблицаЗначений - Список продукции организации:
//		** GTIN                - Строка - GTIN товара.
//		** ИННПроизводителя    - Строка - ИНН производителя.
//		** ТипУпаковки         - Строка - Тип упаковки товара.
//		** КоличествоВложенных - Число  - Количество вложенных единиц.
//  * ТекстОшибки                    - Строка          - Текст сообщения об ошибке.
Функция ЗапроситьСписокПродукции(НомерСтраницы = 0) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии());
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("ПродукцияОрганизации",           Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	URLЗапроса = СтрШаблон("api/v3/true-api/product/gtin?pg=tobacco&limit=100&page=%1", НомерСтраницы);
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, КлючСессии, ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов(""));
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ПродукцияОрганизации = Новый ТаблицаЗначений;
				ПродукцияОрганизации.Колонки.Добавить("GTIN", Метаданные.ОпределяемыеТипы.GTIN.Тип);
				
				Для Каждого ЭлементМассива Из ДанныеОбработки.gtins Цикл
					
					СтрокаТЧ      = ПродукцияОрганизации.Добавить();
					СтрокаТЧ.GTIN = ЭлементМассива;
					
				КонецЦикла;
				
				ВозвращаемоеЗначение.ПродукцияОрганизации = ПродукцияОрганизации;
				
			КонецЕсли;

		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			КонецЕсли;

			Возврат ВозвращаемоеЗначение;

		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполнить запрос карточки продукции по идентификатору.
// В результате успешного выполнения запроса в ответе вернется информация о продукте и ее производителе.
// Адрес URL запроса заканчивается идентификатором продукта из реестра продукции.
// 
// Параметры:
//	МассивGTIN   - Массив из Строка                   - GTIN'ы товаров.
//	ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции.
// Возвращаемое значение:
//	Структура - Структура со свойствами:
//	* ТребуетсяОбновлениеКлючаСессии - Булево          - Необходимость обновления ключа сессии.
//	* РезультатОтправкиЗапроса       - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* ДанныеПродукции - Структура - Реквизиты продукции:
//		** Идентификатор - Строка - Идентификатор продукции.
//		** Наименование - Строка - Идентификатор продукции.
//		** НаименованиеПолное - Строка - Идентификатор продукции.
//		** GTIN - Строка - Идентификатор продукции.
//		** ТорговаяМарка - Строка - Идентификатор продукции.
//		** ТипУпаковки - Строка - Идентификатор продукции.
//		** КоличествоВложенныхЕдиниц - Строка - Идентификатор продукции.
//		** Производители - Массив Из Структура - Массив производителей:
//			*** Наименование - Строка - Наименование производителя.
//			*** НаименованиеПолное - Строка - Полное наименование организации.
//			*** ИНН - Строка - ИНН.
//			*** КПП - Строка - КПП.
//			*** ФИО - Строка - ФИО.
//			*** Адрес - Строка - Фактический адрес организации.
//		** ВыполненаСинхронизацияGS1 - Булево - Признак выполненной синхронизации с GS1.
//  * ТекстОшибки - Строка - Текст сообщения об ошибке.
Функция ЗапроситьКарточкуПродукции(МассивGTIN, ВидПродукции) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии());
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("ДанныеПродукции",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если МассивGTIN.Количество() = 1 Тогда
		
		URLЗапроса       = СтрШаблон("v3/products/%1", МассивGTIN[0]);
		РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(
			URLЗапроса,
			КлючСессии,
			ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов());
		
	Иначе
		
		ДанныеЗапроса = Новый Структура();
		ДанныеЗапроса.Вставить("gtins", МассивGTIN);
		
		URLЗапроса       = "v3/products/list";
		РезультатЗапроса = ИнтеграцияИСМП.ОтправитьДанныеВСервис(
			URLЗапроса,
			ДанныеЗапроса,
			КлючСессии,
			"POST",
			ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов());
		
	КонецЕсли;

	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеРезультата = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеРезультата = Неопределено Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				Если МассивGTIN.Количество() = 1 Тогда
					ДанныеОбработки = Новый Массив;
					ДанныеОбработки.Добавить(ДанныеРезультата);
				Иначе
					ДанныеОбработки = ДанныеРезультата
				КонецЕсли;
				
				ДанныеПродукции = Новый Соответствие();
				
				Для Каждого СтрокаДанных Из ДанныеОбработки Цикл
					
					СтрокаПродукции = Новый Структура;
					СтрокаПродукции.Вставить("GTIN",             СтрокаДанных.gtin);
					СтрокаПродукции.Вставить("Наименование",     Неопределено);
					СтрокаПродукции.Вставить("ВидПродукции",     ВидПродукции);
					СтрокаПродукции.Вставить("ИННПроизводителя", Неопределено);
					СтрокаПродукции.Вставить("ТипУпаковки",      Неопределено);
					СтрокаПродукции.Вставить("ТорговаяМарка",    Неопределено);
					СтрокаПродукции.Вставить("КоличествоВложенныхЕдиниц", СтрокаДанных.innerUnitCount);
					
					СтрокаДанных.Свойство("productName", СтрокаПродукции.Наименование);
					СтрокаДанных.Свойство("brand",       СтрокаПродукции.ТорговаяМарка);
					СтрокаДанных.Свойство("packageType", СтрокаПродукции.ТипУпаковки);
					СтрокаДанных.Свойство("producerInn", СтрокаПродукции.ИННПроизводителя);
					
					Если СтрокаДанных.Свойство("packageType") И ЗначениеЗаполнено(СтрокаДанных.packageType) Тогда
						СтрокаПродукции.ТипУпаковки = ИнтерфейсИСМПСлужебный.ВидУпаковки(СтрокаДанных.packageType, ВидПродукции);
					КонецЕсли;
					
					ДанныеПродукции.Вставить(СтрокаДанных.gtin, СтрокаПродукции);
					
				КонецЦикла;
				
				ВозвращаемоеЗначение.ДанныеПродукции = ДанныеПродукции;
				
			КонецЕсли;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);

			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			КонецЕсли;

			Возврат ВозвращаемоеЗначение;
			
		КонецЕсли;

	ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 404 Тогда

		ВозвращаемоеЗначение.ДанныеПродукции = Новый Соответствие();

	Иначе

		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);

	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область РеестрДокументов

// В результате успешного выполнения запрос в ответе вернется содержание документа.
// В качестве идентификатора следует указывать имя файла документа без расширения
// 
// Параметры:
// 	Идентификатор - Строка - Имя файла документа без расширения.
// Возвращаемое значение:
//	Структура - Описание:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	* РезультатОтправкиЗапроса       - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакФайл).
//	* ИмяФайла                       - Строка - Имя временного файла с содержимым документа.
//	* ТекстОшибки                    - Строка - Текст сообщения об ошибке.
Функция ЗапроситьДокументПоИдентификатору(Идентификатор, ИмяВременногоФайла) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии());
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("ИмяФайла",                       Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		"v3/documents/%1/body",
		Идентификатор);
	
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, КлючСессии, ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов());
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакФайл(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ВозвращаемоеЗначение.ИмяФайла = РезультатОтправкиЗапроса.ИмяФайла;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);

			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			КонецЕсли;

			Возврат ВозвращаемоеЗначение;

		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

// Проверка статуса заказа участника.
// 	В процессе выполнения          - IN PROGRESS
// 	Задание выполнено              - SUCCESS
// 	При выполнении возникла ошибка - ERROR
// 	Результат устарел              - RESULT_EXPIRED
// 
// Параметры:
// 	ИдентификаторЗаказа - Строка - Идентификатор заказа.
// Возвращаемое значение:
//	Структура - Описание:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	* РезультатОтправкиЗапроса - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* СтатусЗаказа - Строка - Статус заказа.
//	* ТекстОшибки - Строка - Текст сообщения об ошибке.
Функция ЗапроситьСтатусЗаказа(ИдентификаторЗаказа) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии());
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("СтатусЗаказа",                   Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		"api/v3/true-api/orders/%1/status",
		ИдентификаторЗаказа);
		
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, КлючСессии, ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов());
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ВозвращаемоеЗначение.СтатусЗаказа = РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);

			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			КонецЕсли;

			Возврат ВозвращаемоеЗначение;

		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Получение результата заказа на формирование списка кодов маркировки участника.
// 
// Параметры:
// 	ИдентификаторЗаказа - Строка - Идентификатор заказа на формирование списка кодов маркировки участника.
// 	ИмяВременногоФайла  - Строка - Имя временного файла
// Возвращаемое значение:
//	Структура - Описание:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	* РезультатОтправкиЗапроса       - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакФайл).
//	* РезультатЗаказа - Строка       - Имя файла с результатом заказа.
//	* ТекстОшибки                    - Строка - Текст сообщения об ошибке.
Функция ЗапроситьРезультатЗаказа(ИдентификаторЗаказа, ИмяВременногоФайла) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии());
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатЗаказа",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		"api/v3/true-api/orders/%1/result",
		ИдентификаторЗаказа);
	
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, КлючСессии, ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов());
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакФайл(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ВозвращаемоеЗначение.РезультатЗаказа = РезультатОтправкиЗапроса.ИмяФайла;
			
		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);

			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			КонецЕсли;

			Возврат ВозвращаемоеЗначение;

		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Запросить цепочки движения кода маркировки.
// В результате успешного выполнения запроса в ответе вернется информация
// о движении кода маркировки. Каждый участник видит производителя продукции,
// своего продавца и текущего владельца. Если данных участника, сформировавшего запрос,
// нет в цепочки движения, то будет виден только производитель и текущий владелец.
// Если код выведен из оборота, то вместо текущего владельца будет соответствующая информация.
// 
// Параметры:
// 	КодМаркировки - Строка - Код маркировки.
// Возвращаемое значение:
//	Структура - Описание:
//	* ТребуетсяОбновлениеКлючаСессии - Булево - Признак необходимости обновления ключа сессии.
//	* РезультатОтправкиЗапроса - (См. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
//	* ИсторияДвижения - Массив Из Структура - История движения кода маркировки в МОТП:
//		** ДатаОперации                 - Дата - Дата операции
//		** ТипОперации                  - ПеречислениеСсылка.ТипыОперацийДвиженияКодовМаркировкиМОТП - Тип операции движения в МОТП
//		** ГрузоотправительИНН          - Строка - ИНН грузоотправителя
//		** ГрузоотправительНаименование - Строка - Наименование грузоотправителя
//		** ГрузополучательИНН           - Строка  - ИНН грузополучателя
//		** ГрузополучательНаименование  - Строка - Наименование грузополучателя
//		** ИдентификаторДокумента       - Строка - Идентификатор документа
//		** ТипДокумента                 - ПеречислениеСсылка.ТипыДокументовМОТП - Тип документа
//		** СтатусДокумента              - Строка - Статус документа
//	* ДанныеПродукции - Структура - Сведения о продукции:
//		** ИННПроизводителя - Строка - ИНН производителя.
//		** НаименованиеПроизводителя - Строка - Наименование производителя продукции.
//		** ИННВладельца - Строка - ИНН владельца продукции.
//		** НаименованиеВладельца - Строка - Наименование владельца продукции.
//	* ТекстОшибки - Строка - Текст сообщения об ошибке.
Функция ЗапроситьИсториюДвиженияКодаМаркировки(КодМаркировки) Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПСлужебный.ПроверитьОбновитьКлючСессии(
		ИнтерфейсМОТПКлиентСервер.ПараметрыЗапросаКлючаСессии());
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессии", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",       Неопределено);
	ВозвращаемоеЗначение.Вставить("ДанныеПродукции",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ИсторияДвижения",                Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                    "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		"api/v3/true-api/cises/%1/history",
		КодироватьСтроку(КодМаркировки, СпособКодированияСтроки.КодировкаURL));
	РезультатЗапроса = ИнтеграцияИСМП.ПолучитьДанныеИзСервиса(URLЗапроса, КлючСессии, ИнтерфейсМОТПКлиентСервер.ПараметрыОтправкиHTTPЗапросов());
	
	РезультатОтправкиЗапроса = ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ИнтерфейсМОТПСлужебный.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			Иначе
				
				ДанныеПродукции = Новый Структура;
				ДанныеПродукции.Вставить("ИННПроизводителя",          ДанныеОбработки.producerInn);
				ДанныеПродукции.Вставить("НаименованиеПроизводителя", ДанныеОбработки.producerName);
				ДанныеПродукции.Вставить("ИННВладельца",              ДанныеОбработки.ownerInn);
				ДанныеПродукции.Вставить("НаименованиеВладельца",     ДанныеОбработки.ownerName);
				
				ВозвращаемоеЗначение.ДанныеПродукции = ДанныеПродукции;
				
				ИсторияДвижения = Новый ТаблицаЗначений();
				ИсторияДвижения.Колонки.Добавить("ДатаОперации",                 Новый ОписаниеТипов("Дата"));
				ИсторияДвижения.Колонки.Добавить("ТипОперации",                  Новый ОписаниеТипов("ПеречислениеСсылка.ТипыОперацийДвиженияКодовМаркировкиМОТП"));
				ИсторияДвижения.Колонки.Добавить("ГрузоотправительИНН",          Новый ОписаниеТипов("Строка"));
				ИсторияДвижения.Колонки.Добавить("ГрузоотправительНаименование", Новый ОписаниеТипов("Строка"));
				ИсторияДвижения.Колонки.Добавить("ГрузополучательИНН",           Новый ОписаниеТипов("Строка"));
				ИсторияДвижения.Колонки.Добавить("ГрузополучательНаименование",  Новый ОписаниеТипов("Строка"));
				ИсторияДвижения.Колонки.Добавить("ИдентификаторДокумента",       Новый ОписаниеТипов("Строка"));
				ИсторияДвижения.Колонки.Добавить("ТипДокумента",                 Новый ОписаниеТипов("ПеречислениеСсылка.ТипыДокументовМОТП"));
				ИсторияДвижения.Колонки.Добавить("СтатусДокумента",              Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыДокументовМОТП"));
				
				ИсторияДвижения.Колонки.Добавить("КодРодительскойУпаковки",      Новый ОписаниеТипов("Строка"));
				ИсторияДвижения.Колонки.Добавить("ТипРодительскойУпаковки",      Новый ОписаниеТипов("Строка"));
				
				Для Каждого ЭлементИстории Из ДанныеОбработки.history Цикл
					
					СтрокаТЧ = ИсторияДвижения.Добавить();
					
					СтрокаТЧ.ДатаОперации = ИнтеграцияИС.ДатаИзСтрокиUNIX(ЭлементИстории.operationDate);
					СтрокаТЧ.ТипОперации  = ИнтерфейсМОТПСлужебный.ТипОперацииДвиженияКодаМаркировки(ЭлементИстории.type);
					
					Если ЭлементИстории.Свойство("participant1Inn") Тогда
						СтрокаТЧ.ГрузоотправительИНН          = Формат(ЭлементИстории.participant1Inn, "ЧГ=0");
						СтрокаТЧ.ГрузоотправительНаименование = ЭлементИстории.participant1Name;
					КонецЕсли;
					
					Если ЭлементИстории.Свойство("participant2Inn") Тогда
						СтрокаТЧ.ГрузополучательИНН           = Формат(ЭлементИстории.participant2Inn, "ЧГ=0");
						СтрокаТЧ.ГрузополучательНаименование  = ЭлементИстории.participant2Name;
					КонецЕсли;
					
					Если ЭлементИстории.Свойство("docId") Тогда
						
						СтрокаТЧ.ИдентификаторДокумента = ЭлементИстории.docId;
						СтрокаТЧ.ТипДокумента           = ИнтерфейсМОТПСлужебный.ТипДокумента(ЭлементИстории.documentType);
						СтрокаТЧ.СтатусДокумента        = ИнтерфейсМОТПСлужебный.СтатусДокумента(ЭлементИстории.documentStatus);
					
					КонецЕсли;
					
					Если ЭлементИстории.Свойство("aggregationCis") Тогда
						
						СтрокаТЧ.ИдентификаторДокумента = ЭлементИстории.aggregationCis;
						СтрокаТЧ.ТипДокумента           = ЭлементИстории.parentCisPackageType;
					
					КонецЕсли;
					
				КонецЦикла;
				
				ВозвращаемоеЗначение.ИсторияДвижения = ИсторияДвижения;
				
			КонецЕсли;

		Иначе
			
			ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);

			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессии = Истина;
			КонецЕсли;

			Возврат ВозвращаемоеЗначение;

		КонецЕсли;

	Иначе

		ВозвращаемоеЗначение.ТекстОшибки = ИнтерфейсМОТПСлужебный.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);

	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПараметрыПостроенияДерева(ПараметрыСканирования) Экспорт
	
	ПараметрыДерева = Новый Структура();
	ПараметрыДерева.Вставить("ВариантПолученияМРЦ",            "ВычислениеИЗапрос");
	ПараметрыДерева.Вставить("Организация",                    Неопределено);
	ПараметрыДерева.Вставить("ЭтоПроверкаКодовМаркировкиИСМП", Ложь);
	ПараметрыДерева.Вставить("УчитыватьМРЦ",                   ИнтеграцияИСМПКлиентСерверПовтИсп.УчитыватьМРЦ());
	ПараметрыДерева.Вставить("ИспользуетсяСераяЗона",          Ложь);
	ПараметрыДерева.Вставить("ПроверятьДублиКодовМаркировки",  Неопределено);
	
	Если ПараметрыСканирования <> Неопределено Тогда
		
		Если ПараметрыСканирования.Свойство("ВариантПолученияМРЦ") Тогда
			ПараметрыДерева.ВариантПолученияМРЦ = ПараметрыСканирования.ВариантПолученияМРЦ;
		КонецЕсли;
		
		ПараметрыДерева.Организация                    = ПараметрыСканирования.Организация;
		ПараметрыДерева.ЭтоПроверкаКодовМаркировкиИСМП = ПараметрыСканирования.ЭтоПроверкаКодовМаркировкиИСМП;
		
		Если ПараметрыСканирования.Свойство("ДопустимыПроверкиСеройЗоныМОТП") Тогда
			ПараметрыДерева.ИспользуетсяСераяЗона = ПараметрыСканирования.ДопустимыПроверкиСеройЗоныМОТП;
		КонецЕсли;
		
		Если ПараметрыСканирования.Свойство("ПроверятьДублиКодовМаркировки")
			И ПараметрыДерева.ЭтоПроверкаКодовМаркировкиИСМП Тогда
			ПараметрыДерева.ПроверятьДублиКодовМаркировки = ПараметрыСканирования.ПроверятьДублиКодовМаркировки;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПараметрыДерева;
	
КонецФункции

// Параметры заполнения дерева упаковок.
// Параметры:
// 	ИсходныеПараметры = Структура, Неопределено - Исходные параметры.
// Возвращаемое значение:
// 	Структура - Описание:
// * ВариантПолученияМРЦ    - Строка - Вариант получения МРЦ.
// * Детализация            - ПеречислениеСсылка.ДетализацияСтруктурыХраненияИС - Деталиция упаковок.
// * Кеш                    - Структура - 
// * КодыМаркировкиУпаковок - Соответствие -
// * ДанныеВерхнегоУровня   - СтрокаДереваЗначений, Структура - 
// * ДеревоУпаковок         - ДеревоЗначений, СтрокаДереваЗначений -
// * ПараметрыДерева        - см. ПараметрыПостроенияДерева
Функция ПараметрыЗаполненияДереваУпаковок(ИсходныеПараметры = Неопределено) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ДеревоУпаковок");
	Результат.Вставить("ДанныеВерхнегоУровня");
	Результат.Вставить("КодыМаркировкиУпаковок");
	Результат.Вставить("Кеш");
	Результат.Вставить("Детализация");
	Результат.Вставить("ВариантПолученияМРЦ");
	Результат.Вставить("СоответствиеСтрокДерева");
	Результат.Вставить("ДанныеОбАгрегации");
	Результат.Вставить("СтатусыКодовМаркировки");
	Результат.Вставить("ДополнительныеКолонки");
	Результат.Вставить("ДополнительныеКолонкиДляВложенныхСтрокДерева");
	Результат.Вставить("ПоляВДанныхСтатуса");
	Результат.Вставить("ПоляВДанныхСтатусаДляВложенныхСтрокДерева");
	Результат.Вставить("ВидПродукции");
	Результат.Вставить("ПараметрыДерева");
	Результат.Вставить("ЭталонСтроки");
	
	Если ИсходныеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Результат, ИсходныеПараметры);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Запрашивает МРЦ по строкам дерева.
// 
// Параметры:
// 	ЗапрашиваемыеСтроки - Массив из СтрокаДереваЗначений - Запрашиваемые данные.
Процедура ЗапросМРЦПоСтрокамДерева(ЗапрашиваемыеСтроки) Экспорт
	
	Если ЗапрашиваемыеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РезутатЗапросаСтатусовДляМРЦ = ИнтерфейсМОТП.ЗапроситьМРЦДляКодаМаркировки(ЗапрашиваемыеСтроки);
	
	Если РезутатЗапросаСтатусовДляМРЦ.ДанныеПродукции <> Неопределено Тогда
		
		ДанныеПродукции = РезутатЗапросаСтатусовДляМРЦ.ДанныеПродукции;
		
		Для Каждого СтрокаЗапроса Из ЗапрашиваемыеСтроки Цикл
			
			ДанныеСтроки = ДанныеПродукции.Получить(СтрокаЗапроса);
			Если ДанныеСтроки <> Неопределено Тогда
				СтрокаЗапроса.МРЦ = ДанныеСтроки.МРЦ;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует МРЦ стройкой с лидирующими нулями.
// 
// Параметры:
//  МРЦ   - Число - Максимальная розничная цена.
//  Длина - Число - Минимальная длинна итогового значения.
// Возвращаемое значение:
//  Строка - МРЦ блока.
Функция ЗначениеМРЦСтрокой(МРЦ, Длина = 6) Экспорт
	
	СтрокаМРЦ = Формат(МРЦ * 100, "ЧГ=0;"); // МРЦ в копейках
	Пока СтрДлина(СтрокаМРЦ) < Длина Цикл
		СтрокаМРЦ = "0" + СтрокаМРЦ;
	КонецЦикла;
	
	Возврат СтрокаМРЦ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеПараметрыНабораПакетов()
	
	ПараметрыНабораПакетов = Новый Структура();
	ПараметрыНабораПакетов.Вставить("ПараметрыПоВидуПродукции", Новый Соответствие());
	ПараметрыНабораПакетов.Вставить("НаборыПакетов",            Новый Массив());
	
	Возврат ПараметрыНабораПакетов;
	
КонецФункции

Функция ДобавлятьУпаковкуВДерево(СтрокаКодаМаркировки, Детализация, ДанныеВерхнегоУровня)
	
	Добавлять = Истина;
	
	Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская Тогда
		
		Если Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ПалетыСМонотоварнымиКоробами
			Или Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.КоробаСГрупповымиУпаковками Тогда
			Если ДанныеВерхнегоУровня.ВидУпаковки <> Перечисления.ВидыУпаковокИС.Логистическая Тогда
				Добавлять = Ложь;
			КонецЕсли;
		КонецЕсли;
	
	Иначе
			// В детализации Пачки - пропускаются все упаковки
		Если (Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ПотребительскиеУпаковки)
			// В детализации Блоки с пачками - пропускаются все упаковки кроме блоков
			Или (Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковки
				И СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая)
			// В детализации Блоки с пачками - пропускаются все упаковки кроме блоков
			Или (Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ГрупповыеУпаковкиСПотребительскими
				И СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая)
			Или (Детализация = Перечисления.ДетализацияСтруктурыХраненияИС.ПалетыСМонотоварнымиКоробами
				И Не ПустаяСтрока(ДанныеВерхнегоУровня.GTIN)) Тогда
			
			Добавлять = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Добавлять;
	
КонецФункции

Функция ЗаполненВидУпаковкиПоДаннымСтатуса(СтрокаКодаМаркировки, ПараметрыКодаМаркировки)
	
	Если ПараметрыКодаМаркировки = Неопределено
		Или Не ЗначениеЗаполнено(ПараметрыКодаМаркировки.ВидУпаковки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не РазборКодаМаркировкиИССлужебныйКлиентСервер.ВидУпаковкиСоответствуетРазбору(
		СтрокаКодаМаркировки.ВидПродукции, ПараметрыКодаМаркировки.ВидУпаковки, СтрокаКодаМаркировки.ДанныеРазбора) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтрокаКодаМаркировки.ВидУпаковки               = ПараметрыКодаМаркировки.ВидУпаковки;
	СтрокаКодаМаркировки.ДанныеРазбора.ВидУпаковки = ПараметрыКодаМаркировки.ВидУпаковки;
	
	ШтрихкодированиеМОТП.РассчитатьХэшСуммуНормализации(
		СтрокаКодаМаркировки,
		СтрокаКодаМаркировки.ДанныеРазбора);
	
	Возврат Истина;
	
КонецФункции

Функция ТребуетсяЗапросВложенныхСтатусов(СтрокаКодаМаркировки, ВложеннаяСтрокаКодаМаркировки, ПараметрыДерева, ДанныеСтатуса)
	
	Если Не ЗначениеЗаполнено(ВложеннаяСтрокаКодаМаркировки.ВидПродукции) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(ВложеннаяСтрокаКодаМаркировки.ВидПродукции) Тогда
		Если ПараметрыДерева.УчитыватьМРЦ Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВложеннаяСтрокаКодаМаркировки.ВидУпаковки) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ДанныеСтатуса <> Неопределено
		И (ДанныеСтатуса.Статус = Перечисления.СтатусыКодовМаркировкиИСМП.Разагрегирован
			Или ДанныеСтатуса.Статус = Перечисления.СтатусыКодовМаркировкиМОТП.Разагрегирован)
		И (СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
			Или СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ДобавитьСтрокуВНаборПакетовЗапросов(СтрокаДанных, ПараметрыНабораПакетов)
	
	ПараметрыПакетов = ПараметрыНабораПакетов.ПараметрыПоВидуПродукции.Получить(СтрокаДанных.ВидПродукции);
	
	Если ПараметрыПакетов = Неопределено Тогда
		
		Для Каждого КлючИЗначение Из ПараметрыНабораПакетов.ПараметрыПоВидуПродукции Цикл
			
			Если (ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(КлючИЗначение.Ключ)
				И ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(СтрокаДанных.ВидПродукции))
				Или (ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(КлючИЗначение.Ключ)
				И ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(СтрокаДанных.ВидПродукции)) Тогда
				
				ПараметрыПакетов = КлючИЗначение.Значение;
				ПараметрыНабораПакетов.ПараметрыПоВидуПродукции.Вставить(СтрокаДанных.ВидПродукции, ПараметрыПакетов);
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПараметрыПакетов = Неопределено Тогда
		
		ПараметрыПакетов = Новый Структура();
		ПараметрыПакетов.Вставить("ВидПродукцииПодсистемы", СтрокаДанных.ВидПродукции);
		ПараметрыПакетов.Вставить("ПакетКодовМаркировки",   Неопределено);
		ПараметрыПакетов.Вставить("ПакетыКодовМаркировки",  Новый Массив);
		Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(ПараметрыПакетов.ВидПродукцииПодсистемы) Тогда
			ПараметрыПакетов.Вставить("КоличествоВПакете", 1000);
		Иначе
			ПараметрыПакетов.Вставить("КоличествоВПакете", 25);
		КонецЕсли;
		
		ПараметрыНабораПакетов.ПараметрыПоВидуПродукции.Вставить(СтрокаДанных.ВидПродукции, ПараметрыПакетов);
		ПараметрыНабораПакетов.НаборыПакетов.Добавить(ПараметрыПакетов);
		
	КонецЕсли;
	
	Если ПараметрыПакетов.ПакетКодовМаркировки = Неопределено
		Или ПараметрыПакетов.ПакетКодовМаркировки.Количество() >= ПараметрыПакетов.КоличествоВПакете Тогда
		
		ПараметрыПакетов.ПакетКодовМаркировки = Новый Массив;
		ПараметрыПакетов.ПакетыКодовМаркировки.Добавить(ПараметрыПакетов.ПакетКодовМаркировки);
		
	КонецЕсли;
	
	ПараметрыПакетов.ПакетКодовМаркировки.Добавить(СтрокаДанных);
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуДанныхДереваПоСтрокеКодаМаркировки(СтрокаДерева, СтрокаКодаМаркировки)
	
	СтрокаДерева.Штрихкод                = СтрокаКодаМаркировки.Штрихкод;
	СтрокаДерева.ТипШтрихкода            = СтрокаКодаМаркировки.ТипШтрихкода;
	СтрокаДерева.ВидУпаковки             = СтрокаКодаМаркировки.ВидУпаковки;
	СтрокаДерева.ВидПродукции            = СтрокаКодаМаркировки.ВидПродукции;
	СтрокаДерева.ХэшСуммаНормализации    = СтрокаКодаМаркировки.ХэшСуммаНормализации;
	СтрокаДерева.НормализованныйШтрихкод = СтрокаКодаМаркировки.НормализованныйШтрихкод;
	СтрокаДерева.СоставКодаМаркировки    = СтрокаКодаМаркировки.СоставКодаМаркировки;
	СтрокаДерева.GTIN                    = СтрокаКодаМаркировки.GTIN;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуДереваПоДополнительнымКолонкам(СтрокаДерева, ДанныеСтатуса, ДополнительныеКолонки, ДополнительныеПоляВДанных);
	
	Если ДополнительныеКолонки = Неопределено
		Или ДополнительныеКолонки.Количество() = 0
		Или ДанныеСтатуса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокПолей = ДополнительныеПоляВДанных.Получить(СтрокаДерева.ВидПродукции);
	Если СписокПолей = Неопределено Тогда
		
		МассивПолей = Новый Массив();
		Для Каждого КлючИЗначение Из ДополнительныеКолонки Цикл
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеСтатуса, КлючИЗначение.Ключ)
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаДерева, КлючИЗначение.Ключ) Тогда
				МассивПолей.Добавить(КлючИЗначение.Ключ);
			КонецЕсли;
		КонецЦикла;
		СписокПолей = СтрСоединить(МассивПолей, ",");
		ДополнительныеПоляВДанных.Вставить(СтрокаДерева.ВидПродукции, СписокПолей);
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтрокаДерева, ДанныеСтатуса, СписокПолей);
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуДереваСпецификойПоСоставуКода(СтрокаДерева, СтрокаКодаМаркировки, ПараметрыЗаполнения, ЭтоФиктивнаяСтрока = Ложь);
	
	ПараметрыДерева     = ПараметрыЗаполнения.ПараметрыДерева;
	ВариантПолученияМРЦ = ПараметрыЗаполнения.ВариантПолученияМРЦ;
	Кеш                 = ПараметрыЗаполнения.Кеш;
	
	Если СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая Тогда
		
		Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(СтрокаДерева.ВидПродукции) Тогда
			СтрокаДерева.КоличествоБлоков = 1;
		КонецЕсли;
		
	ИначеЕсли СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская Тогда
		
		Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(СтрокаДерева.ВидПродукции) Тогда
			СтрокаДерева.КоличествоПачек = 1;
		ИначеЕсли ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(СтрокаДерева.ВидПродукции) Тогда
			СтрокаДерева.КоличествоПотребительскихУпаковок = 1;
		КонецЕсли;
		
	КонецЕсли;
	
	// МРЦ
	Если СтрокаДерева.ВидПродукции = Перечисления.ВидыПродукцииИС.Табак
		И ПараметрыДерева.УчитыватьМРЦ
		И Не ЗначениеЗаполнено(СтрокаДерева.МРЦ)
		И (СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
			Или СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская) Тогда
			
		Если СтрокаКодаМаркировки.СоставКодаМаркировки <> Неопределено Тогда
			СтрокаКодаМаркировки.СоставКодаМаркировки.Свойство("МРЦ", СтрокаДерева.МРЦ);
		КонецЕсли;
		
		Если Не ЭтоФиктивнаяСтрока
			И Не ЗначениеЗаполнено(СтрокаДерева.МРЦ) Тогда
			Если ВариантПолученияМРЦ = "ВычислениеИЗапрос" Тогда
				Кеш.ЗапросИУстановкаМРЦ.Добавить(СтрокаДерева);
			ИначеЕсли ВариантПолученияМРЦ = "ВычислениеОтРодителя" Тогда
				Кеш.ВычислениеМРЦ.Добавить(СтрокаДерева);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтрокаДерева.GTIN)
		И СтрокаКодаМаркировки.СоставКодаМаркировки <> Неопределено Тогда
		СтрокаКодаМаркировки.СоставКодаМаркировки.Свойство("GTIN", СтрокаДерева.GTIN);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДеревоУпаковокРекурсивно(ПараметрыЗаполнения)
	
	ДеревоУпаковок         = ПараметрыЗаполнения.ДеревоУпаковок;
	ДанныеВерхнегоУровня   = ПараметрыЗаполнения.ДанныеВерхнегоУровня;
	КодыМаркировкиУпаковок = ПараметрыЗаполнения.КодыМаркировкиУпаковок;
	Кеш                    = ПараметрыЗаполнения.Кеш;
	Детализация            = ПараметрыЗаполнения.Детализация;
	ДанныеОбАгрегации      = ПараметрыЗаполнения.ДанныеОбАгрегации;
	СтатусыКодовМаркировки = ПараметрыЗаполнения.СтатусыКодовМаркировки;
	ВидПродукции           = ПараметрыЗаполнения.ВидПродукции;
	ЭталонСтроки           = ПараметрыЗаполнения.ЭталонСтроки;
	ПараметрыДерева        = ПараметрыЗаполнения.ПараметрыДерева;
	
	ДополнительныеКолонкиОбщие = ПараметрыЗаполнения.ДополнительныеКолонки;
	ПоляВДанныхСтатусаОбщие    = ПараметрыЗаполнения.ПоляВДанныхСтатуса;
	
	ДополнительныеКолонкиДляВложенныхСтрокДерева = ПараметрыЗаполнения.ДополнительныеКолонкиДляВложенныхСтрокДерева;
	ПоляВДанныхСтатусаДляВложенныхСтрокДерева    = ПараметрыЗаполнения.ПоляВДанныхСтатусаДляВложенныхСтрокДерева;
	
	КоличествоПотребительскихУпаковок = 0;
	КоличествоГрупповыхУпаковок       = 0;
	
	Если ТипЗнч(КодыМаркировкиУпаковок) <> Тип("Число") Тогда
		
		Для Каждого ЗначениеВложенногоКода Из КодыМаркировкиУпаковок Цикл
			
			СтрокаКодаМаркировки = ЗначениеВложенногоКода.Ключ;
			ДанныеСтатуса        = СтатусыКодовМаркировки[СтрокаКодаМаркировки];
			КодМаркировки        = СтрокаКодаМаркировки.Штрихкод;
			
			Если ТипЗнч(ЗначениеВложенногоКода.Значение) = Тип("Структура") Тогда
				Если ДанныеСтатуса = Неопределено Тогда
					ДанныеСтатуса = ЗначениеВложенногоКода.Значение;
				КонецЕсли;
			КонецЕсли;
			
			Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская Тогда // Не имеет вложенных кодов
				ВложенныеКодыМаркировки = Новый Соответствие;
			Иначе
				ВложенныеКодыМаркировки = ДанныеОбАгрегации[СтрокаКодаМаркировки];
			КонецЕсли;
			
			Если ДанныеСтатуса = Неопределено Тогда
				ИсходныеДанныеСтатуса = ДанныеВерхнегоУровня;
				ДополнительныеКолонки = ДополнительныеКолонкиДляВложенныхСтрокДерева;
				ПоляВДанныхСтатуса    = ПоляВДанныхСтатусаДляВложенныхСтрокДерева;
			Иначе
				ИсходныеДанныеСтатуса = ДанныеСтатуса;
				ДополнительныеКолонки = ДополнительныеКолонкиОбщие;
				ПоляВДанныхСтатуса    = ПоляВДанныхСтатусаОбщие;
			КонецЕсли;
			
			Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая
				Или СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая Тогда // Логистическая упаковка или блок
				
				Если ДобавлятьУпаковкуВДерево(СтрокаКодаМаркировки, Детализация, ДанныеВерхнегоУровня) Тогда
					
					Если ПараметрыДерева.ПроверятьДублиКодовМаркировки <> "НеПроверять" Тогда
						ИнтерфейсИСМПСлужебный.ПроверитьИсправитьДублиСтрокДереваУпаковок(КодМаркировки, Кеш.КодыМаркировки);
					КонецЕсли;
					
					СтрокаДерева = ДеревоУпаковок.Строки.Добавить();
					
					ЗаполнитьСтрокуДанныхДереваПоСтрокеКодаМаркировки(
						СтрокаДерева,
						СтрокаКодаМаркировки);
					
					ЗаполнитьСтрокуДереваПоДополнительнымКолонкам(
						СтрокаДерева,
						ИсходныеДанныеСтатуса,
						ДополнительныеКолонки,
						ПоляВДанныхСтатуса);
					
					ЗаполнитьСтрокуДереваСпецификойПоСоставуКода(
						СтрокаДерева,
						СтрокаКодаМаркировки,
						ПараметрыЗаполнения);
					
					ПараметрыЗаполненияВложенные = ПараметрыЗаполненияДереваУпаковок(ПараметрыЗаполнения);
					ПараметрыЗаполненияВложенные.ДеревоУпаковок         = СтрокаДерева;
					ПараметрыЗаполненияВложенные.ДанныеВерхнегоУровня   = СтрокаДерева;
					ПараметрыЗаполненияВложенные.КодыМаркировкиУпаковок = ВложенныеКодыМаркировки;
					
					Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
						И ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(СтрокаДерева.ВидПродукции) Тогда
						
						ПараметрыЗаполненияВложенные.ВариантПолученияМРЦ = "ВычислениеОтРодителя";
						
					КонецЕсли;
					
					ЗаполнитьДеревоУпаковокРекурсивно(ПараметрыЗаполненияВложенные);
					
					Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(СтрокаКодаМаркировки.ВидПродукции) Тогда
						КоличествоПотребительскихУпаковок = КоличествоПотребительскихУпаковок  + СтрокаДерева.КоличествоПачек;
						КоличествоГрупповыхУпаковок       = КоличествоГрупповыхУпаковок        + СтрокаДерева.КоличествоБлоков;
					Иначе
						КоличествоПотребительскихУпаковок = КоличествоПотребительскихУпаковок  + СтрокаДерева.КоличествоПотребительскихУпаковок;
					КонецЕсли;
					
					Кеш.КодыМаркировки.Вставить(СтрокаКодаМаркировки.Штрихкод, СтрокаДерева);
					ОбновитьКешGTIN(СтрокаДерева, Кеш.GTIN, ПараметрыДерева);
					
				Иначе
					
					ДанныеСтрокиВместоДанныеВерхнегоУровня = ОбщегоНазначения.СкопироватьРекурсивно(ЭталонСтроки);
					
					ЗаполнитьСтрокуДанныхДереваПоСтрокеКодаМаркировки(
						ДанныеСтрокиВместоДанныеВерхнегоУровня,
						СтрокаКодаМаркировки);
					
					ЗаполнитьСтрокуДереваПоДополнительнымКолонкам(
						ДанныеСтрокиВместоДанныеВерхнегоУровня,
						ИсходныеДанныеСтатуса,
						ДополнительныеКолонки,
						ПоляВДанныхСтатуса);
					
					ЗаполнитьСтрокуДереваСпецификойПоСоставуКода(
						ДанныеСтрокиВместоДанныеВерхнегоУровня,
						СтрокаКодаМаркировки,
						ПараметрыЗаполнения,
						Истина);
					
					ПараметрыЗаполненияВложенные = ПараметрыЗаполненияДереваУпаковок(ПараметрыЗаполнения);
					ПараметрыЗаполненияВложенные.ДеревоУпаковок         = ДеревоУпаковок;
					ПараметрыЗаполненияВложенные.ДанныеВерхнегоУровня   = ДанныеСтрокиВместоДанныеВерхнегоУровня;
					ПараметрыЗаполненияВложенные.КодыМаркировкиУпаковок = ВложенныеКодыМаркировки;
					
					Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая
						И ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(ДанныеСтрокиВместоДанныеВерхнегоУровня.ВидПродукции) Тогда
						
						ПараметрыЗаполненияВложенные.ВариантПолученияМРЦ = "ВычислениеОтРодителя";
						
					КонецЕсли;
					
					ЗаполнитьДеревоУпаковокРекурсивно(ПараметрыЗаполненияВложенные);
					
					Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(СтрокаКодаМаркировки.ВидПродукции) Тогда
						КоличествоПотребительскихУпаковок  = КоличествоПотребительскихУпаковок  + ДанныеСтрокиВместоДанныеВерхнегоУровня.КоличествоПачек;
						КоличествоГрупповыхУпаковок        = КоличествоГрупповыхУпаковок        + ДанныеСтрокиВместоДанныеВерхнегоУровня.КоличествоБлоков;
						Если СтрокаКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая Тогда
							КоличествоГрупповыхУпаковок = КоличествоГрупповыхУпаковок + 1;
						КонецЕсли;
					Иначе
						КоличествоПотребительскихУпаковок  = КоличествоПотребительскихУпаковок  + ДанныеСтрокиВместоДанныеВерхнегоУровня.КоличествоПотребительскихУпаковок;
					КонецЕсли;
					
					ОбновитьКешGTIN(ДанныеСтрокиВместоДанныеВерхнегоУровня, Кеш.GTIN, ПараметрыДерева);
					
				КонецЕсли;
				
			Иначе
				
				Если ПараметрыДерева.ПроверятьДублиКодовМаркировки <> "НеПроверять" Тогда
					ИнтерфейсИСМПСлужебный.ПроверитьИсправитьДублиСтрокДереваУпаковок(КодМаркировки, Кеш.КодыМаркировки);
				КонецЕсли;
				
				Если ДобавлятьУпаковкуВДерево(СтрокаКодаМаркировки, Детализация, ДанныеВерхнегоУровня) Тогда
					
					Если ДанныеСтатуса = Неопределено Тогда
						ИсходныеДанныеСтатуса = ДанныеВерхнегоУровня;
						ДополнительныеКолонки = ДополнительныеКолонкиДляВложенныхСтрокДерева;
						ПоляВДанныхСтатуса    = ПоляВДанныхСтатусаДляВложенныхСтрокДерева;
					Иначе
						ИсходныеДанныеСтатуса = ДанныеСтатуса;
						ДополнительныеКолонки = ДополнительныеКолонкиОбщие;
						ПоляВДанныхСтатуса    = ПоляВДанныхСтатусаОбщие;
					КонецЕсли;
					
					СтрокаДерева = ДеревоУпаковок.Строки.Добавить();
					
					ЗаполнитьСтрокуДанныхДереваПоСтрокеКодаМаркировки(
						СтрокаДерева,
						СтрокаКодаМаркировки);
					
					ЗаполнитьСтрокуДереваПоДополнительнымКолонкам(
						СтрокаДерева,
						ИсходныеДанныеСтатуса,
						ДополнительныеКолонки,
						ПоляВДанныхСтатуса);
					
					ЗаполнитьСтрокуДереваСпецификойПоСоставуКода(
						СтрокаДерева,
						СтрокаКодаМаркировки,
						ПараметрыЗаполнения);
					
					Кеш.КодыМаркировки.Вставить(КодМаркировки, СтрокаДерева);
					ОбновитьКешGTIN(СтрокаДерева, Кеш.GTIN, ПараметрыДерева);
					
				Иначе
					
					ОбновитьКешGTIN(СтрокаКодаМаркировки, Кеш.GTIN, ПараметрыДерева, Истина);
					
				КонецЕсли;
				
				КоличествоПотребительскихУпаковок = КоличествоПотребительскихУпаковок + 1;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		КоличествоПотребительскихУпаковок = КодыМаркировкиУпаковок;
		КоличествоГрупповыхУпаковок       = 0;
		
	КонецЕсли;
	
	Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(ВидПродукции) Тогда
		ДанныеВерхнегоУровня.КоличествоПачек  = ДанныеВерхнегоУровня.КоличествоПачек  + КоличествоПотребительскихУпаковок;
		ДанныеВерхнегоУровня.КоличествоБлоков = ДанныеВерхнегоУровня.КоличествоБлоков + КоличествоГрупповыхУпаковок;
	Иначе
		ДанныеВерхнегоУровня.КоличествоПотребительскихУпаковок = ДанныеВерхнегоУровня.КоличествоПотребительскихУпаковок + КоличествоПотребительскихУпаковок;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьКешGTIN(СтрокаДерева, КешGTIN, ПараметрыДерева, ЭтоСтрокаКодаМаркировки = Ложь)
	
	Если ПустаяСтрока(СтрокаДерева.GTIN) Тогда
		Возврат;
	КонецЕсли;
	
	МРЦ = Неопределено;
	Если ПараметрыДерева.УчитыватьМРЦ
		И ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(СтрокаДерева.ВидПродукции) Тогда
		Если ЭтоСтрокаКодаМаркировки Тогда
			МРЦ = СтрокаДерева.СоставКодаМаркировки.МРЦ;
		Иначе
			МРЦ = СтрокаДерева.МРЦ;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("GTIN", СтрокаДерева.GTIN);
	Если МРЦ <> Неопределено Тогда
		ПараметрыОтбора.Вставить("МРЦ", МРЦ);
	КонецЕсли;
	
	НайденныеСтроки = КешGTIN.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество() = 0 Тогда
		НоваяСтрокаКешаGTIN = КешGTIN.Добавить();
		НоваяСтрокаКешаGTIN.GTIN = СтрокаДерева.GTIN;
		Если МРЦ <> Неопределено Тогда
			НоваяСтрокаКешаGTIN.МРЦ = МРЦ;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти