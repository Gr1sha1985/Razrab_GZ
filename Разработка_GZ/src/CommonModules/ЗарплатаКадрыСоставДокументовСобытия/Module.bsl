#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьСоставСотрудниковДокументаПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДокумента = ЗарплатаКадрыСоставДокументов.ОписаниеОбъектаПоИсточнику(Источник);
	Если ОписаниеДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СоставДокумента = ЗарплатаКадрыСоставДокументов.СоставОбъекта(Источник, ОписаниеДокумента);
	
	Если ОписаниеДокумента.ЗаполнятьФизическиеЛицаПоСотрудникам Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьФизическиеЛицаПоСотрудникамВДокументе(Источник, ОписаниеДокумента);
	КонецЕсли;

	Если ОписаниеДокумента.ЗаполнятьТабличнуюЧастьФизическиеЛицаДокумента Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьТабличнуюЧастьФизическиеЛицаДокумента(Источник, ОписаниеДокумента, СоставДокумента.ФизическиеЛица);
	КонецЕсли;
	
	Если ОписаниеДокумента.ИспользоватьКраткийСостав Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьКраткийСоставДокумента(Источник, ОписаниеДокумента, СоставДокумента.СотрудникиФизическиеЛица);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСоставСотрудниковДокументаПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДокумента = ЗарплатаКадрыСоставДокументов.ОписаниеОбъектаПоИсточнику(Источник);
	Если ОписаниеДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументСсылка = Источник.Ссылка;
	СоставДокумента = ЗарплатаКадрыСоставДокументов.СоставОбъекта(Источник, ОписаниеДокумента);
	
	Если ОписаниеДокумента.ИспользоватьОграничениеДоступа Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьРегистрФизическиеЛицаДокументов(ДокументСсылка, СоставДокумента.ФизическиеЛица);
	КонецЕсли;
	
	Если ОписаниеДокумента.ЗаполнятьСоставДокументов Тогда
		ЗарплатаКадрыСоставДокументов.ЗаполнитьРегистрСоставДокументовЗарплатаКадры(ДокументСсылка, СоставДокумента.СотрудникиФизическиеЛица, Источник.Дата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
