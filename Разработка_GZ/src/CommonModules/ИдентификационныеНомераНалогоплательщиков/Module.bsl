// Приказ ФНС России от 29.06.2012 N ММВ-7-6/435@
// "Об утверждении Порядка и условий присвоения, применения, а также изменения идентификационного номера налогоплательщика"

Функция МинимальнаяДлинаИНН() Экспорт
	
	// ММВ-7-6/435@
	// 1. Структура идентификационного номера налогоплательщика (далее - ИНН) представляет собой:
	// 1) для организации - десятизначный цифровой код:
	// 2) для физического лица - двенадцатизначный цифровой код:
	
	Возврат 10;
	
КонецФункции

Функция МаксимальнаяДлинаИНН() Экспорт
	
	// ММВ-7-6/435@
	// 1. Структура идентификационного номера налогоплательщика (далее - ИНН) представляет собой:
	// 1) для организации - десятизначный цифровой код:
	// 2) для физического лица - двенадцатизначный цифровой код:
	
	Возврат 12;
	
КонецФункции

Функция ЭтоИННЮридическогоЛица(ИНН) Экспорт
	
	// ММВ-7-6/435@
	// 1. Структура идентификационного номера налогоплательщика (далее - ИНН) представляет собой:
	// 1) для организации - десятизначный цифровой код:
	
	Возврат СтрДлина(ИНН) = МинимальнаяДлинаИНН();
	
КонецФункции

Функция ЭтоИННФизическогоЛица(ИНН) Экспорт
	
	// ММВ-7-6/435@
	// 1. Структура идентификационного номера налогоплательщика (далее - ИНН) представляет собой:
	// 2) для физического лица - двенадцатизначный цифровой код:
	
	Возврат СтрДлина(ИНН) = МаксимальнаяДлинаИНН();
	
КонецФункции

Процедура УдалитьИзНаименованияИНН(Наименование, ИНН) Экспорт
	
	ЭлементыНаименования = СтрРазделить(Наименование, " ", Ложь);
	Если ЭлементыНаименования.Количество() < 3 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭлементыНаименования[0] <> "ИНН" Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭлементыНаименования[1] <> ИНН 
		И Не ОбщегоНазначенияБПКлиентСервер.ТолькоНулиВСтроке(ЭлементыНаименования[1]) Тогда
		Возврат;
	КонецЕсли;
	
	// Удалим часть наименования, включающую первое и второе слова.
	Для ИндексСлова = 0 По 1 Цикл
		Слово = ЭлементыНаименования[ИндексСлова];
		НачалоСлова  = СтрНайти(Наименование, Слово);
		КонецСлова   = НачалоСлова + СтрДлина(Слово); // Точнее, это позиция разделителя за концом слова
		Наименование = СокрЛ(Сред(Наименование, КонецСлова + 1));// Так как слов больше 2, то заведомо не выйдем за конец строки
	КонецЦикла;
	
КонецПроцедуры

Функция ТолькоСимволы09AZ(ПроверяемоеЗначение)
	
	РезультатПроверки = Истина;
	
	Код0 = КодСимвола("0");
	Код9 = КодСимвола("9");
	КодA = КодСимвола("A");
	КодZ = КодСимвола("Z");
	
	Для Инд = 1 По 2 Цикл
		ПроверяемыйСимвол = Сред(ПроверяемоеЗначение, Инд, 1);
		КодПроверяемогоСимвола = КодСимвола(ПроверяемыйСимвол);
		
		КодВДиапазонах = Код0 <= КодПроверяемогоСимвола И КодПроверяемогоСимвола <= Код9
		             ИЛИ КодA <= КодПроверяемогоСимвола И КодПроверяемогоСимвола <= КодZ;
		
		Если НЕ КодВДиапазонах Тогда
			РезультатПроверки = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция ПроверитьСоответствиеТребованиямИНН(Знач ИНН, Знач ЭтоЮрЛицо) Экспорт
	Перем ТекстОшибкиФорматнаяПроверка;
	
	РезультатПроверки = Новый Структура("СоответствуетТребованиям, ЭтоЮрЛицо, ОписаниеОшибки", Истина, ЭтоЮрЛицо, "");
	
	Если ЗначениеЗаполнено(ИНН) Тогда
		
		ИНН = СокрП(ИНН);
		
		ЭтоИННФизическогоЛица  = ЭтоИННФизическогоЛица(ИНН);
		ЭтоИННЮридическогоЛица = ЭтоИННЮридическогоЛица(ИНН);
		
		Если РезультатПроверки.ЭтоЮрЛицо И НЕ ЭтоИННЮридическогоЛица Тогда
			РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'ИНН юридического лица должен состоять из 10 цифр'");
			
			Если ЭтоИННФизическогоЛица Тогда
				// Ошибка в определении вида контрагента, однако ИНН может быть все равно корректным - просто надо изменить вид.
				РезультатПроверки.ЭтоЮрЛицо = Ложь;
			Иначе
				РезультатПроверки.СоответствуетТребованиям = Ложь;
			КонецЕсли; 
		ИначеЕсли НЕ РезультатПроверки.ЭтоЮрЛицо И НЕ ЭтоИННФизическогоЛица Тогда
			РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'ИНН физического лица должен состоять из 12 цифр'");
			
			Если ЭтоИННЮридическогоЛица Тогда
				// Ошибка в определении вида контрагента, однако ИНН может быть все равно корректным - просто надо изменить вид.
				РезультатПроверки.ЭтоЮрЛицо = Истина;
			Иначе
				РезультатПроверки.СоответствуетТребованиям = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если РезультатПроверки.СоответствуетТребованиям Тогда
			Если Лев(ИНН, 2) = "00" Тогда
				РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'Первые две цифры ИНН не могут быть ""00""'");
				РезультатПроверки.СоответствуетТребованиям = Ложь;
			Иначе
				РезультатПроверки.СоответствуетТребованиям = РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, РезультатПроверки.ЭтоЮрЛицо, ТекстОшибкиФорматнаяПроверка);
				
				Если НЕ РезультатПроверки.СоответствуетТребованиям Тогда
					РезультатПроверки.ОписаниеОшибки = ТекстОшибкиФорматнаяПроверка;
				КонецЕсли; 
				
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		РезультатПроверки.СоответствуетТребованиям = Ложь;
		// В этом случае описание ошибки не выводим,
		// так как описание ошибки из этой функции обычно используется для вывода на форме,
		// а о незаполненном ИНН на форме сообщаем другими средствами.
		
	КонецЕсли;
	
	Возврат РезультатПроверки;
КонецФункции

Функция ПроверитьСоответствиеТребованиямКПП(Знач КПП, Знач ЭтоЮрЛицо, Знач ОбособленноеПодразделение, Знач ЭтоКонтрагент = Истина) Экспорт
	
	РезультатПроверки = ПроверитьСоответствиеТребованиямФорматаКПП(КПП, ЭтоЮрЛицо);
	
	Если НЕ РезультатПроверки.СоответствуетТребованиям Тогда
		Возврат РезультатПроверки;
	КонецЕсли;
	
	КодПричиныПостановкиНаУчет = КодПричиныПостановкиНаУчет(КПП);
	
	// Проверяем только числовые значения
	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(КодПричиныПостановкиНаУчет) Тогда
		ПризнакОбособленногоПодразделения = ЭтоПричинаПостановкиНаУчетОбособленногоПодразделения(КодПричиныПостановкиНаУчет);
		ПризнакГоловногоПодразделения     = ЭтоПричинаПостановкиНаУчетОрганизацииВЦелом(КодПричиныПостановкиНаУчет);
		
		// Проверки на обособленное и головное подразделение должны носить рекомендательный характер,
		// так как список кодов причин поставки на учет не публикуется
		Если ОбособленноеПодразделение и ПризнакГоловногоПодразделения Тогда 
			РезультатПроверки.СоответствуетТребованиям = Истина;
			РезультатПроверки.ОписаниеОшибки = СтрШаблон(
				НСтр("ru = 'Возможно, введен КПП юридического лица. Измените вид %1'"), 
				?(ЭтоКонтрагент, НСтр("ru = 'контрагента'"), НСтр("ru = 'организации'")));
			Возврат РезультатПроверки;
		ИначеЕсли НЕ ОбособленноеПодразделение И ПризнакОбособленногоПодразделения Тогда  
			РезультатПроверки.СоответствуетТребованиям = Истина;
			РезультатПроверки.ОписаниеОшибки = СтрШаблон(
				НСтр("ru = 'Возможно, введен КПП обособленного подразделения. Измените вид %1'"), 
				?(ЭтоКонтрагент, НСтр("ru = 'контрагента'"), НСтр("ru = 'организации'")));
			Возврат РезультатПроверки;
		ИначеЕсли НЕ ПризнакГоловногоПодразделения и НЕ ПризнакОбособленногоПодразделения Тогда
			РезультатПроверки.СоответствуетТребованиям = Истина;
			РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'Возможно, КПП не соответствует формату'");
			Возврат РезультатПроверки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция ПроверитьСоответствиеТребованиямФорматаКПП(Знач КПП, Знач ЭтоЮрЛицо) Экспорт
	
	РезультатПроверки = Новый Структура("СоответствуетТребованиям, ОписаниеОшибки", Истина, "");
	
	Если НЕ ЗначениеЗаполнено(КПП) Тогда
		РезультатПроверки.СоответствуетТребованиям = Ложь;
		Возврат РезультатПроверки;
	КонецЕсли;
	
	Если НЕ ЭтоЮрЛицо Тогда
		РезультатПроверки.СоответствуетТребованиям = Ложь;
		Возврат РезультатПроверки;
	КонецЕсли;
	
	КПП = СокрП(КПП);
	
	// ММВ-7-6/435
	// 4. Структура КПП представляет собой девятизначный код:
	// NNNNPPXXX
	// 5. КПП состоит из следующей последовательности символов слева направо:
	// 1) NNNN (4 знака) - код налогового органа, который осуществил постановку на учет организации по месту ее нахождения,
	// 2) PP (2 знака) - причина постановки на учет (учета сведений).
	// Символ P представляет собой цифру или заглавную букву латинского алфавита от A до Z.
	// 3) XXX (3 знака) - порядковый номер постановки на учет (учета сведений) в налоговом органе по соответствующему основанию.
	
	Если СтрДлина(КПП) <> 9 Тогда
		РезультатПроверки.СоответствуетТребованиям = Ложь;
		РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'КПП должен состоять из 9 символов'");
		Возврат РезультатПроверки;
	КонецЕсли;
	
	Если СтрНачинаетсяС(КПП, "00") Тогда
		РезультатПроверки.СоответствуетТребованиям = Ложь;
		РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'КПП не должен начинаться на 00'");
		Возврат РезультатПроверки;
	КонецЕсли;

	КодПричиныПостановкиНаУчет = КодПричиныПостановкиНаУчет(КПП);
	
	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Лев(КПП, 4))           // NNNN
		ИЛИ НЕ ТолькоСимволы09AZ(КодПричиныПостановкиНаУчет)                       // PP
		ИЛИ НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Прав(КПП, 3)) Тогда // XXX
		РезультатПроверки.СоответствуетТребованиям = Ложь;
		РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'КПП должен состоять из цифр и символов A-Z (в позициях 5 и 6)'");
		Возврат РезультатПроверки;
	ИначеЕсли КодПричиныПостановкиНаУчет = "00" Тогда 
		РезультатПроверки.СоответствуетТребованиям = Ложь;
		РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'Код причины постановки на учет (5 и 6 символ) не может быть 00. Опечатка?'");
		Возврат РезультатПроверки;
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

#Область ПричиныПостановкиНаУчет

Функция ЭтоПричинаПостановкиНаУчетОрганизацииВЦелом(КодПричиныПостановки) Экспорт
	
	// В письме от 13 апреля 2012 г. N 03-07-09/35 Минфин отмечает, что 
	// при заполнении счетов-фактур по товарам, реализованным обособленными подразделениями организаций, 
	// указывается КПП соответствующего обособленного подразделения организации;
	// Соответственно, при приобретении товаров обособленными подразделениями организации 
	// указывается КПП соответствующего обособленного подразделения.
	// 
	// В соответствии с этой моделью поведения в платежных поручениях организации также могут указывать
	// КПП обособленного подразделения.
	//
	// Организации встают на учет в налоговых органах по месту нахождения обособленных подразделений
	// с присвоением КПП.
	//
	// При этом причина постановки в составе КПП указывается кодом в соответствии с ведомственный справочником
	// "Причины постановки на учет налогоплательщиков - организаций в налоговых органах" (СППУНО).
	// 
	// Обособленное подразделение может получить КПП как в связи с регистрацией по месту нахождения обособленного подразделения,
	// так и по другим основаниям, например по месту нахождения имущества этого обособленного подразделения.
	// 
	// По некоторым из этих других оснований КПП может быть присвоен и организации в целом.
	// Однако, можно предположить, что такие КПП организация не будет указывать в документах контрагентам,
	// до тех пор, пока речь не идет об обособленных подразделениях.
	//
	// С учетом этого, в случаях, когда указан КПП с кодом причины, указывающем на то, что в данном случае
	// речь точно не идет об обособленном подразделении, считаем, что речь идет об организации в целом.
	//
	// Если указан другой КПП - то есть, указывающий на филиал, представительство либо предполагающий двоякое толкование,
	// считаем, что речь идет об обособленном подразделении.
	
	// СППУНО не публикуется.
	// Однако, отдельные значения кодов раскрываются в письмах ФНС.
	// Чаще всего применяется письмо от 2 июня 2008 г. N ЧД-6-6/396@
	// Больше всего кодов приведено в письме от 25 декабря 2014 г. N ММВ-7-1/674@
	// См. также письма
	// от 26 февраля 2015 г. N СА-4-14/2973@
	// от 28 ноября 2014 г. N СА-4-14/24715
	// от 18 февраля 2011 г. N ПА-4-6/2688@
	
	Возврат КодПричиныПостановки = "01"  // Постановка на учет в налоговом органе российской организации в качестве налогоплательщика по месту ее нахождения
		Или КодПричиныПостановки = "30"  // Постановка на учет российской организации - налогового агента, не учтенной в качестве налогоплательщика (по месту ее нахождения)
		Или КодПричиныПостановки = "37"  // Постановка (восстановление  с  ранее  присвоенным  ИНН)  на учет организации,  ранее  состоявшей  на учете,  в связи с  внесением  в ЕГРЮЛ    записи    об    отмене    судом    решения    о    прекращении (ликвидации) юридического лица
		Или КодПричиныПостановки = "40"  // Учет в  налоговом органе  организации  при возникновении у нее обязанности по уплате отдельных видов налогов, предусмотренных действующим законодательством о налогах и сборах, в случае, когда организация осуществляет деятельность вне места своего нахождения сроком   менее   1    месяца,   не   создавая   при   этом   обособленное подразделение и не имея недвижимое имущество или транспортные средства по месту осуществления деятельности
		Или КодПричиныПостановки = "41"  // Учет в налоговом органе организации, уплачивающей отдельные виды налогов, в случае отсутствия у нее на территории этого налогового органа оснований для постановки на учет (СК-6-09/49)
		Или КодПричиныПостановки = "51"  // Постановка на учёт отделений иностранных организаций
		Или КодПричиныПостановки = "52"  // Постановка на учёт отделений иностранных организаций в Российской Федерации, созданных филиалом этой иностранной организации в иностранном государстве
		Или КодПричиныПостановки = "63"  // Постановка на учёт международных организаций
		Или КодПричиныПостановки = "75"  // Постановка на учёт иностранных и международных организаций, имеющих космические объекты в Российской Федерации
		Или КодПричиныПостановки = "89"  // Постановка на учет в налоговом органе иностранных интернет-компаний, занимающихся оказанием электронных услуг
		Или ЭтоПричинаПостановкиНаУчетКрупнейшегоНалогоплательщика(КодПричиныПостановки);
	
КонецФункции

Функция ЭтоПричинаПостановкиНаУчетОбособленногоПодразделения(КодПричиныПостановкиНаУчет)
	
	Возврат КодПричиныПостановкиНаУчет = "02" //Постановка на учёт налогоплательщика — российской организации по месту нахождения ее обособленного подразделения в зависимости от вида подразделения 
		Или КодПричиныПостановкиНаУчет = "03" //Постановка на учёт налогоплательщика — российской организации по месту нахождения ее филиала, не исполняющего обязанности организации по уплате налогов и сборов 
		Или КодПричиныПостановкиНаУчет = "04" //Постановка на учёт налогоплательщика — российской организации по месту нахождения ее обособленного подразделения в зависимости от вида подразделения 
		Или КодПричиныПостановкиНаУчет = "05" //Постановка на учёт налогоплательщика — российской организации по месту нахождения ее обособленного подразделения в зависимости от вида подразделения
		Или КодПричиныПостановкиНаУчет = "30" //Российская организация — налоговый агент, не учтенная в качестве налогоплательщика
		Или КодПричиныПостановкиНаУчет = "31" //Постановка на учёт налогоплательщика — российской организации по месту нахождения обособленного подразделения, в отношении которого не проведена процедура оформления в соответствии с пунктом 3 статьи 55 Гражданского кодекса Российской Федерации, исполняющего обязанности организации по уплате налогов и сборов 
		Или КодПричиныПостановкиНаУчет = "32" //Постановка на учёт налогоплательщика — российской организации по месту нахождения обособленного подразделения, в отношении которого не проведена процедура оформления в соответствии с пунктом 3 статьи 55 Гражданского кодекса Российской Федерации, не исполняющего обязанности организации по уплате налогов и сборов 
		Или КодПричиныПостановкиНаУчет = "43" //Постановка на учёт российской организации по месту нахождения ее филиала (аналогично старым кодам «02», «03» — Письмо Минфина РФ от 02.06.2008 № ЧД-6-6/396@ «О применении кодов справочника СППУНО») 
		Или КодПричиныПостановкиНаУчет = "44" //Постановка на учёт российской организации по месту нахождения ее представительства (аналогично старым кодам «04», «05» — Письмо Минфина РФ от 02.06.2008 № ЧД-6-6/396@ «О применении кодов справочника СППУНО») 
		Или КодПричиныПостановкиНаУчет = "45" //Постановка на учёт российской организации по месту нахождения ее обособленного подразделения (аналогично старым кодам «31», «32» — Письмо Минфина РФ от 02.06.2008 № ЧД-6-6/396@ «О применении кодов справочника СППУНО») 
		Или КодПричиныПостановкиНаУчет = "60" //Постановка на учёт посольств иностранных государств
		Или КодПричиныПостановкиНаУчет = "61" //Постановка на учёт консульств иностранных государств
		Или КодПричиныПостановкиНаУчет = "62" //Постановка на учёт представительств, приравненных к дипломатическим
		Или КодПричиныПостановкиНаУчет = "70" //Постановка на учёт иностранных и международных организаций, имеющих недвижимое имущество в Российской Федерации, за исключением транспортных средств, относящихся к недвижимому имуществу
		Или КодПричиныПостановкиНаУчет = "71" //Постановка на учёт иностранных и международных организаций, имеющих транспортные средства в Российской Федерации, не относящиеся к недвижимому имуществу
		Или КодПричиныПостановкиНаУчет = "72" //Постановка на учёт иностранных и международных организаций, имеющих морские транспортные средства в Российской Федерации
		Или КодПричиныПостановкиНаУчет = "73" //Постановка на учёт иностранных и международных организаций, имеющих речные транспортные средства в Российской Федерации
		Или КодПричиныПостановкиНаУчет = "74" //Постановка на учёт иностранных и международных организаций, имеющих воздушные транспортные средства в Российской Федерации
		Или КодПричиныПостановкиНаУчет = "80" //Учет иностранных и международных организаций в связи с открытием в банках рублевых счетов типа «Т» (текущие)
		Или КодПричиныПостановкиНаУчет = "81" //Учет иностранных и международных организаций в связи с открытием счетов в банках типа «И» (инвестиционные)
		Или КодПричиныПостановкиНаУчет = "82" //учёт иностранных и международных организаций в связи с открытием счетов в банках типа «С» (специальные)
		Или КодПричиныПостановкиНаУчет = "83" //Учет иностранных и международных организаций в связи с открытием в банках счетов типа «Т» (текущие) в иностранной валюте
		Или КодПричиныПостановкиНаУчет = "84" //Учет иностранных и международных организаций в связи с открытием корреспондентских счетов в банках 
		Или КодПричиныПостановкиНаУчет = "91" //Некоторые филиалы и представительства и международных организаций из реестра РАФП (аккредитованных филиалов и представительств)
		Или КодПричиныПостановкиНаУчет = "92";//Некоторые филиалы и представительства и международных организаций из реестра РАФП (аккредитованных филиалов и представительств)
	
КонецФункции

Функция ЭтоПричинаПостановкиНаУчетКрупнейшегоНалогоплательщика(КодПричиныПостановки) Экспорт
	
	Возврат КодПричиныПостановки = "50"; // Постановка на учет в налоговом органе российской организации в качестве крупнейшего налогоплательщика (ММ-3-09/553@)
	
КонецФункции

Функция КодПричиныПостановкиНаУчет(КПП) Экспорт
	
	// ММВ-7-6/435
	// 4. Структура КПП представляет собой девятизначный код:
	// NNNNPPXXX
	// 5. КПП состоит из следующей последовательности символов слева направо:
	// 2) PP (2 знака) - причина постановки на учет (учета сведений).
	// Символ P представляет собой цифру или заглавную букву латинского алфавита от A до Z.
	// Числовое значение символов PP может принимать значение:
	// для российской организации от 01 до 50 (01 - по месту ее нахождения);
	// для иностранной организации от 51 до 99;
	
	Если КППКорректен(КПП) Тогда
		Возврат Сред(КПП, 5, 2);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция КППКорректен(КПП) Экспорт
	
	// ММВ-7-6/435
	// 4. Структура КПП представляет собой девятизначный код
	
	Возврат ТипЗнч(КПП) = Тип("Строка") И Не ПустаяСтрока(КПП) И СтрДлина(КПП) = 9;
	
КонецФункции

Функция КПППоУмолчанию(ИНН) Экспорт
	
	Если ПустаяСтрока(ИНН) Или Не ЭтоИННЮридическогоЛица(ИНН) Тогда
		Возврат "";
	КонецЕсли;
	
	// ММВ-7-6/435
	// NNNNPPXXX
	// NNNN      - код налогового органа, который осуществил постановку на учет организации по месту ее нахождения,
	//     01    - Постановка на учет в налоговом органе российской организации в качестве налогоплательщика по месту ее нахождения
	//       001 - Порядковый номер постановки на учет (учета сведений) в налоговом органе по соответствующему основанию
	
	Возврат Лев(ИНН, 4) + "01001";
	
КонецФункции

#КонецОбласти
