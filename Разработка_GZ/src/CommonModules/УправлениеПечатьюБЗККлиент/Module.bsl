#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.Печать

// Клиентский обработчик команды печати. Формирует печатную форму.
// См. УправлениеПечатью.СоздатьКоллекциюКомандПечати, колонка Обработчик
// В отличие от стандартного обработчика, добавляет в заголовок формы печати представление документа.
//
// Параметры:
//  КомандаПечати - Структура - Команда печати. 
//       Свойства соответствуют колонкам таблицы, возвращаемой функцией УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//       В том числе:
//       * Форма                   - ФормаКлиентскогоПриложения - Форма, из которой вызвана команда печати.
//       * Идентификатор           - Строка - Идентификатор команды печати, 
//                                            по которому менеджер печати определяет печатную форму, 
//                                            которую необходимо сформировать.
//       * МенеджерПечати          - Строка - Имя объекта, в модуле менеджера которого располагается процедура Печать,
//                                            формирующая табличные документы для этой команды.
//       * ОбъектыПечати           - Массив           - Массив ссылок выбранных объектов.
//       * ДополнительныеПараметры - Структура        - Дополнительные параметры печати.
//
// Возвращаемое значение:
//  Неопределено - никак в дальнейшем не используется.
//
Функция ВыполнитьКомандуПечати(КомандаПечати) Экспорт
	
	Если КомандаПечати.Свойство("ЗаголовокФормы") И ЗначениеЗаполнено(КомандаПечати.ЗаголовокФормы) Тогда
		ПараметрыПечати = Новый Структура("ЗаголовокФормы", КомандаПечати.ЗаголовокФормы);
	Иначе
		
		Если ТипЗнч(КомандаПечати.ОбъектыПечати) = Тип("Массив") И КомандаПечати.ОбъектыПечати.Количество() = 1 Тогда 
			ПараметрыПечати = Новый Структура("ЗаголовокФормы", КомандаПечати.ОбъектыПечати[0]);
		Иначе
			ПараметрыПечати = Новый Структура;
		КонецЕсли;
		
	КонецЕсли;
	
	Если КомандаПечати.Свойство("ДополнительныеПараметры") Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", КомандаПечати.ДополнительныеПараметры);
	КонецЕсли;
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		КомандаПечати.МенеджерПечати, 
		КомандаПечати.Идентификатор, 
		КомандаПечати.ОбъектыПечати,
		КомандаПечати.Форма, 
		ПараметрыПечати);
	
КонецФункции

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#КонецОбласти

