
#Область СлужебныйПрограммныйИнтерфейс

// Специфические алгоритмы чтения данных.

// Помещает в переданный МенеджерВременныхТаблиц таблицу 
//	ВТКадровыхДанных, которая содержит следующие поля:
//		Сотрудник - СправочникСсылка.Сотрудники
//		ДатаНачала - дата
//		Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления
//		Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//		Должность - СправочникСсылка.Должности
//		ЯвляетсяФармацевтом - булево.
//		ПрименяемыйЛьготныйТерриториальныйТариф - СправочникСсылка.ВидыТарифовСтраховыхВзносов.
//		ЯвляетсяРаботникомСДосрочнойПенсией - ПеречислениеСсылка.ВидыРаботСДосрочнойПенсией
//		ЯвляетсяПрокурором - булево.
//		ЯвляетсяВоеннослужащим - булево.
//		РаботаетВСтуденческомОтряде - булево.
//		ЯвляетсяЧленомЛетногоЭкипажа - булево.
//		ЯвляетсяШахтером - булево
//		КоэффициентУчетаСтроки - число от 0 до 1.
//      	 
// Параметры:
//		Организация -
//		ПериодРегистрации - дата -
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - содержит вр. таблицу ВТНачисления с полями:
//				Сотрудник - СправочникСсылка.Сотрудники
//				ДатаНачала - дата -
//				Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления -
//				ПодразделениеОрганизации - СправочникСсылка.ПодразделенияОрганизаций
//		УточнятьПериодКадровыхДанных - булево - если выставлен в Истина, период будет скорректирован с учетом дат приема и
//		                                        увольнения, для работающих постоянно - выставлен на конец месяца.
//
Процедура СформироватьВТКадровыхДанных(Организация, ПериодРегистрации, МенеджерВременныхТаблиц, УточнятьПериодКадровыхДанных = Истина) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Скорректируем если требуется, переданные даты для чтения кадровых данных сотрудников.
	Если УточнятьПериодКадровыхДанных Тогда
		
		КадровыеДанные = "ДатаПриема, ДатаУвольнения";
		ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
			МенеджерВременныхТаблиц, "ВТНачисления", "Сотрудник,ДатаНачала");
		КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Ложь, КадровыеДанные);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокНачислений.Сотрудник,
		|	ВЫБОР
		|		КОГДА СписокНачислений.ДатаНачала < ДанныеОСотруднике.ДатаПриема
		|			ТОГДА ДанныеОСотруднике.ДатаПриема
		|		КОГДА ДанныеОСотруднике.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				И КОНЕЦПЕРИОДА(СписокНачислений.ДатаНачала, МЕСЯЦ) > ДанныеОСотруднике.ДатаУвольнения
		|			ТОГДА ДанныеОСотруднике.ДатаУвольнения
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(СписокНачислений.ДатаНачала, МЕСЯЦ), ДЕНЬ)
		|	КОНЕЦ КАК Период,
		|	СписокНачислений.ДатаНачала КАК ИсходныйПериод
		|ПОМЕСТИТЬ ВТВременнаяТаблица
		|ИЗ
		|	ВТНачисления КАК СписокНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ДанныеОСотруднике
		|		ПО СписокНачислений.Сотрудник = ДанныеОСотруднике.Сотрудник
		|			И СписокНачислений.ДатаНачала = ДанныеОСотруднике.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТКадровыеДанныеСотрудников";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокНачислений.Сотрудник,
		|	СписокНачислений.ДатаНачала КАК Период,
		|	СписокНачислений.ДатаНачала КАК ИсходныйПериод
		|ПОМЕСТИТЬ ВТВременнаяТаблица
		|ИЗ
		|	ВТНачисления КАК СписокНачислений";
		
	КонецЕсли;
	Запрос.Выполнить();

	// Читаем кадровые данные сотрудников, влияющие на исчисление взносов.
	
	КадровыеДанные = "Должность, ЯвляетсяФармацевтом, ПрименяемыйЛьготныйТерриториальныйТариф, ЯвляетсяРаботникомСДосрочнойПенсией, ЯвляетсяПрокурором, ЯвляетсяВоеннослужащим, РаботаетВСтуденческомОтряде, ЯвляетсяЧленомЛетногоЭкипажа, ЯвляетсяШахтером";
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		МенеджерВременныхТаблиц,
		"ВТВременнаяТаблица");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Ложь, КадровыеДанные);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНачислений.Сотрудник КАК Сотрудник,
	|	СписокНачислений.ДатаНачала КАК ДатаНачала,
	|	СписокНачислений.Начисление КАК Начисление,
	|	СписокНачислений.ПодразделениеОрганизации КАК Подразделение,
	|	ДанныеОСотруднике.Должность КАК Должность,
	|	ДанныеОСотруднике.ЯвляетсяФармацевтом КАК ЯвляетсяФармацевтом,
	|	ДанныеОСотруднике.ПрименяемыйЛьготныйТерриториальныйТариф КАК ПрименяемыйЛьготныйТерриториальныйТариф,
	|	ДанныеОСотруднике.ЯвляетсяРаботникомСДосрочнойПенсией КАК ЯвляетсяРаботникомСДосрочнойПенсией,
	|	ДанныеОСотруднике.ЯвляетсяПрокурором КАК ЯвляетсяПрокурором,
	|	ДанныеОСотруднике.ЯвляетсяВоеннослужащим КАК ЯвляетсяВоеннослужащим,
	|	ДанныеОСотруднике.РаботаетВСтуденческомОтряде КАК РаботаетВСтуденческомОтряде,
	|	ДанныеОСотруднике.ЯвляетсяЧленомЛетногоЭкипажа КАК ЯвляетсяЧленомЛетногоЭкипажа,
	|	ДанныеОСотруднике.ЯвляетсяШахтером КАК ЯвляетсяШахтером,
	|	1 КАК КоэффициентУчетаСтроки
	|ПОМЕСТИТЬ ВТКадровыхДанных
	|ИЗ
	|	ВТНачисления КАК СписокНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВременнаяТаблица КАК УточненныеПериоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ДанныеОСотруднике
	|			ПО УточненныеПериоды.Сотрудник = ДанныеОСотруднике.Сотрудник
	|				И УточненныеПериоды.Период = ДанныеОСотруднике.Период
	|		ПО СписокНачислений.Сотрудник = УточненныеПериоды.Сотрудник
	|			И СписокНачислений.ДатаНачала = УточненныеПериоды.ИсходныйПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКадровыеДанныеСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВременнаяТаблица";
	Запрос.Выполнить();
	
КонецПроцедуры

// Помещает в переданный МенеджерВременныхТаблиц таблицу 
//	ВТНачисленияСДаннымиУчета, которая содержит следующие поля:
//		Сотрудник - СправочникСсылка.Сотрудники
//		ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//		ДатаНачала - дата
//		ДатаПолученияДохода - дата.
//		Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления
//		ВидДохода - СправочникСсылка.ВидыДоходовПоСтраховымВзносам -
//		Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//		ЯвляетсяДоходомФармацевта - булево.
//		ЛьготныйТерриториальныйТариф - СправочникСсылка.ВидыТарифовСтраховыхВзносов.
//		ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией - ПеречислениеСсылка.ВидыРаботСДосрочнойПенсией
//		КлассУсловийТруда - ПеречислениеСсылка.КлассыУсловийТрудаПоРезультатамСпециальнойОценки
//		ОблагаетсяВзносамиНаДоплатуЛетчикам - булево.
//		ОблагаетсяВзносамиНаДоплатуШахтерам - булево.
//		Сумма - число 
//		Скидка - число 
//      	 
// Параметры:
//		Организация -
//		ПериодРегистрации - дата -
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - содержит 
//			вр. таблицу ВТНачисления с полями:
//				Сотрудник -
//				ДатаНачала - дата -
//				Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления -
//				Подразделение - 
//				СуммаДохода - 
//				СуммаВычетаВзносы -
//      	        и, возможно, другими
//			вр. таблицу ВТСДаннымиУчета, с полями:
//				Сотрудник - СправочникСсылка.Сотрудники
//				ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//				ДатаНачала - дата
//				Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления
//				ВидДохода - СправочникСсылка.ВидыДоходовПоСтраховымВзносам -
//				Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//				ЯвляетсяДоходомФармацевта - булево.
//				ЛьготныйТерриториальныйТариф - СправочникСсылка.ВидыТарифовСтраховыхВзносов.
//				ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией - ПеречислениеСсылка.ВидыРаботСДосрочнойПенсией
//				КлассУсловийТруда - ПеречислениеСсылка.КлассыУсловийТрудаПоРезультатамСпециальнойОценки
//				ОблагаетсяВзносамиНаДоплатуЛетчикам - булево.
//				ОблагаетсяВзносамиНаДоплатуШахтерам - булево.
//				КоэффициентУчетаСтроки - число от 0 до 1.
//
Процедура СформироватьВТНачисленияСДаннымиУчета(Организация, ПериодРегистрации, МенеджерВременныхТаблиц, ИспользоватьОсобыеУсловияТруда = Ложь) Экспорт 

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаПолученияДохода", УчетСтраховыхВзносовКлиентСервер.ДатаПолученияДохода(ПериодРегистрации));
	Запрос.УстановитьПараметр("ИспользоватьОсобыеУсловияТруда", ИспользоватьОсобыеУсловияТруда);
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	ВременнаяТаблица.Сотрудник,
	|	ВременнаяТаблица.Начисление,
	|	ВременнаяТаблица.ПодразделениеОрганизации КАК Подразделение,
	|	ВременнаяТаблица.ДатаНачала,
	|	ДанныеУчета.ВидДохода,
	|	ДанныеУчета.ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ДанныеУчета.ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ДанныеУчета.ЯвляетсяДоходомФармацевта,
	|	ДанныеУчета.ЛьготныйТерриториальныйТариф,
	|	ДанныеУчета.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеУчета.КлассУсловийТруда,
	|	ВЫРАЗИТЬ(СУММА(ВременнаяТаблица.СуммаДохода * ЕСТЬNULL(ДанныеУчета.КоэффициентУчетаСтроки, 1)) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДанныеУчета.ВидДохода.ОблагаетсяВзносамиЧастично
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ВременнаяТаблица.СуммаВычетаВзносы * ЕСТЬNULL(ДанныеУчета.КоэффициентУчетаСтроки, 1)) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Скидка,
	|	ДанныеУчета.ФизическоеЛицо,
	|	ИСТИНА КАК РаспределятьЕНВД,
	|	НЕОПРЕДЕЛЕНО КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТНачисленияСДаннымиУчета
	|ИЗ
	|	ВТНачисления КАК ВременнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСДаннымиУчета КАК ДанныеУчета
	|		ПО ВременнаяТаблица.Сотрудник = ДанныеУчета.Сотрудник
	|			И ВременнаяТаблица.Начисление = ДанныеУчета.Начисление
	|			И ВременнаяТаблица.ПодразделениеОрганизации = ДанныеУчета.Подразделение
	|			И ВременнаяТаблица.ДатаНачала = ДанныеУчета.ДатаНачала
	|			И &ИспользоватьОсобыеУсловияТруда
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблица.Сотрудник,
	|	ВременнаяТаблица.Начисление,
	|	ВременнаяТаблица.ПодразделениеОрганизации,
	|	ВременнаяТаблица.ДатаНачала,
	|	ДанныеУчета.ВидДохода,
	|	ДанныеУчета.ВидДохода.ОблагаетсяВзносамиЧастично,
	|	ДанныеУчета.ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ДанныеУчета.ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ДанныеУчета.ЯвляетсяДоходомФармацевта,
	|	ДанныеУчета.ЛьготныйТерриториальныйТариф,
	|	ДанныеУчета.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеУчета.КлассУсловийТруда,
	|	ДанныеУчета.ФизическоеЛицо";
	
	Если ИспользоватьОсобыеУсловияТруда Тогда
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&ИспользоватьОсобыеУсловияТруда", "ВременнаяТаблица.УсловияТруда = ДанныеУчета.УсловияТруда")
	Иначе
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&ИспользоватьОсобыеУсловияТруда", "ИСТИНА")
	КонецЕсли;
	Запрос.Выполнить();
	
КонецПроцедуры

// Помещает в переданный МенеджерВременныхТаблиц таблицу 
//	ВТСДаннымиУчета, которая содержит следующие поля
//		Сотрудник - СправочникСсылка.Сотрудники
//		ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//		ДатаНачала - дата
//		Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления
//		ВидДохода - СправочникСсылка.ВидыДоходовПоСтраховымВзносам -
//		Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//		ЯвляетсяДоходомФармацевта - булево.
//		ЛьготныйТерриториальныйТариф - СправочникСсылка.ВидыТарифовСтраховыхВзносов.
//		ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией - ПеречислениеСсылка.ВидыРаботСДосрочнойПенсией
//		КлассУсловийТруда - ПеречислениеСсылка.КлассыУсловийТрудаПоРезультатамСпециальнойОценки
//		ОблагаетсяВзносамиНаДоплатуЛетчикам - булево.
//		ОблагаетсяВзносамиНаДоплатуШахтерам - булево.
//		КоэффициентУчетаСтроки - число от 0 до 1.
//      	 
// Параметры:
//		Организация -
//		ПериодРегистрации - дата -
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - содержит
//			вр. таблицу ВТНачисления с полями:
//				Сотрудник - СправочникСсылка.Сотрудники
//				ДатаНачала - дата
//				Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления
//				ПодразделениеОрганизации - СправочникСсылка.ПодразделенияОрганизаций
//			вр. таблицу ВТКадровыхДанных с полями:
//				Сотрудник - СправочникСсылка.Сотрудники
//				ДатаНачала - дата
//				Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления
//				Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//				Должность - СправочникСсылка.Должности
//				ЯвляетсяФармацевтом - булево.
//				ПрименяемыйЛьготныйТерриториальныйТариф - СправочникСсылка.ВидыТарифовСтраховыхВзносов.
//				ЯвляетсяРаботникомСДосрочнойПенсией - ПеречислениеСсылка.ВидыРаботСДосрочнойПенсией
//				ЯвляетсяПрокурором - булево.
//				ЯвляетсяВоеннослужащим - булево.
//				РаботаетВСтуденческомОтряде - булево.
//				ЯвляетсяЧленомЛетногоЭкипажа - булево.
//				ЯвляетсяШахтером - булево.
//				КоэффициентУчетаСтроки - число от 0 до 1.
//
Процедура СформироватьВТСДаннымиУчета(Организация, ПериодРегистрации, МенеджерВременныхТаблиц) Экспорт 

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ПериодРегистрации < УчетСтраховыхВзносовКлиентСервер.ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР() Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка) КАК Должность,
		|	ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка) КАК КлассУсловийТруда,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период
		|ПОМЕСТИТЬ ВТКлассыУсловийТрудаПоДолжностямСрезПоследних";
		Запрос.Выполнить();
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеОСотруднике.Должность,
		|	ДанныеОСотруднике.ДатаНачала КАК Период
		|ПОМЕСТИТЬ ВТДолжности
		|ИЗ
		|	ВТКадровыхДанных КАК ДанныеОСотруднике";
		Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
		Запрос.Выполнить();
		
		ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
		ПараметрыПостроения.ВсеЗаписи = Истина;
		УчетСтраховыхВзносов.ДобавитьОтборПриЧтенииПериодическихДанных(ПараметрыПостроения, КонецМесяца(ПериодРегистрации), Ложь);
	
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
			"КлассыУсловийТрудаПоДолжностям",
			Запрос.МенеджерВременныхТаблиц,
			Ложь,
			ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
				"ВТДолжности",
				"Должность"),
			ПараметрыПостроения);
		
		Запрос.Текст = 
		"УНИЧТОЖИТЬ ВТДолжности";
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНачислений.Начисление,
	|	ДанныеОСотруднике.ЯвляетсяПрокурором,
	|	ДанныеОСотруднике.ЯвляетсяВоеннослужащим,
	|	ДанныеОСотруднике.РаботаетВСтуденческомОтряде
	|ПОМЕСТИТЬ ВТСписокНачислений
	|ИЗ
	|	ВТНачисления КАК СписокНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыхДанных КАК ДанныеОСотруднике
	|		ПО СписокНачислений.Сотрудник = ДанныеОСотруднике.Сотрудник
	|			И СписокНачислений.ДатаНачала = ДанныеОСотруднике.ДатаНачала
	|			И СписокНачислений.Начисление = ДанныеОСотруднике.Начисление
	|			И СписокНачислений.ПодразделениеОрганизации = ДанныеОСотруднике.Подразделение";
	Запрос.Выполнить();
	
	УчетСтраховыхВзносов.СформироватьВТСоответствиеВидовДоходаНачислениям(МенеджерВременныхТаблиц, ПериодРегистрации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНачислений.Сотрудник,
	|	СписокНачислений.ДатаНачала,
	|	СписокНачислений.Начисление,
	|	СписокНачислений.ПодразделениеОрганизации
	|ПОМЕСТИТЬ ВТДляОпределенияДанныхУчета
	|ИЗ
	|	ВТНачисления КАК СписокНачислений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНачислений.Сотрудник,
	|	СписокНачислений.Начисление,
	|	СписокНачислений.ДатаНачала КАК ДатаНачала,
	|	СписокНачислений.ПодразделениеОрганизации КАК Подразделение,
	|	ЕСТЬNULL(ВидыДоходаПоНачислениям.ВидДохода, ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.ПустаяСсылка)) КАК ВидДохода,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЕСТЬNULL(ДанныеОСотруднике.ЯвляетсяЧленомЛетногоЭкипажа, ЛОЖЬ) КАК ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ЕСТЬNULL(ДанныеОСотруднике.ЯвляетсяШахтером, ЛОЖЬ) КАК ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ЕСТЬNULL(ДанныеОСотруднике.ЯвляетсяФармацевтом, ЛОЖЬ) КАК ЯвляетсяДоходомФармацевта,
	|	ЕСТЬNULL(ДанныеОСотруднике.ПрименяемыйЛьготныйТерриториальныйТариф, ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ПустаяСсылка)) КАК ЛьготныйТерриториальныйТариф,
	|	ЕСТЬNULL(ДанныеОСотруднике.ЯвляетсяРаботникомСДосрочнойПенсией, ЗНАЧЕНИЕ(Перечисление.ВидыРаботСДосрочнойПенсией.ПустаяСсылка)) КАК ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ЕСТЬNULL(КлассыУсловийТрудаПоДолжностям.КлассУсловийТруда, ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка)) КАК КлассУсловийТруда,
	|	ЕСТЬNULL(СУММА(ДанныеОСотруднике.КоэффициентУчетаСтроки), 1) КАК КоэффициентУчетаСтроки
	|ПОМЕСТИТЬ ВТСДаннымиУчета
	|ИЗ
	|	ВТДляОпределенияДанныхУчета КАК СписокНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыхДанных КАК ДанныеОСотруднике
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКлассыУсловийТрудаПоДолжностямСрезПоследних КАК КлассыУсловийТрудаПоДолжностям
	|			ПО ДанныеОСотруднике.Должность = КлассыУсловийТрудаПоДолжностям.Должность
	|				И ДанныеОСотруднике.ДатаНачала = КлассыУсловийТрудаПоДолжностям.Период
	|		ПО СписокНачислений.Сотрудник = ДанныеОСотруднике.Сотрудник
	|			И СписокНачислений.ДатаНачала = ДанныеОСотруднике.ДатаНачала
	|			И СписокНачислений.Начисление = ДанныеОСотруднике.Начисление
	|			И СписокНачислений.ПодразделениеОрганизации = ДанныеОСотруднике.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыДоходаПоНачислениям КАК ВидыДоходаПоНачислениям
	|		ПО СписокНачислений.Начисление = ВидыДоходаПоНачислениям.Начисление
	|			И (ДанныеОСотруднике.ЯвляетсяВоеннослужащим = ВидыДоходаПоНачислениям.ЯвляетсяВоеннослужащим)
	|			И (ДанныеОСотруднике.ЯвляетсяПрокурором = ВидыДоходаПоНачислениям.ЯвляетсяПрокурором)
	|			И (ДанныеОСотруднике.РаботаетВСтуденческомОтряде = ВидыДоходаПоНачислениям.РаботаетВСтуденческомОтряде)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО СписокНачислений.Сотрудник = Сотрудники.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОСотруднике.ЯвляетсяЧленомЛетногоЭкипажа,
	|	ДанныеОСотруднике.ЯвляетсяШахтером,
	|	ДанныеОСотруднике.ПрименяемыйЛьготныйТерриториальныйТариф,
	|	ДанныеОСотруднике.ЯвляетсяРаботникомСДосрочнойПенсией,
	|	СписокНачислений.Сотрудник,
	|	СписокНачислений.Начисление,
	|	СписокНачислений.ПодразделениеОрганизации,
	|	СписокНачислений.ДатаНачала,
	|	Сотрудники.ФизическоеЛицо,
	|	ЕСТЬNULL(ДанныеОСотруднике.ЯвляетсяФармацевтом, ЛОЖЬ),
	|	ЕСТЬNULL(КлассыУсловийТрудаПоДолжностям.КлассУсловийТруда, ЗНАЧЕНИЕ(Перечисление.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.ПустаяСсылка)),
	|	ЕСТЬNULL(ВидыДоходаПоНачислениям.ВидДохода, ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСписокНачислений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВидыДоходаПоНачислениям
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДляОпределенияДанныхУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКлассыУсловийТрудаПоДолжностямСрезПоследних";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Помещает в переданный МенеджерВременныхТаблиц таблицу 
//	ВТКлассыУсловийТруда, которая содержит следующие поля:
//		Организация - СправочникСсылка.Организация
//		Период - дата
//		КлассУсловийТруда - ПеречислениеСсылка.КлассыУсловийТрудаПоРезультатамСпециальнойОценки
//		ВидыРаботСДосрочнойПенсией - ПеречислениеСсылка.ВидыРаботСДосрочнойПенсией
//
// Параметры:
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//
Процедура СоздатьВТКлассыУсловийТруда(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КлассыПоДолжностям.Период,
	|	КлассыПоДолжностям.КлассУсловийТруда,
	|	КлассыПоДолжностям.Должность.ВзимаютсяВзносыЗаЗанятыхНаРаботахСДосрочнойПенсией КАК ВидыРаботСДосрочнойПенсией,
	|	Организации.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТКлассыУсловийТруда
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КлассыУсловийТрудаПоДолжностям КАК КлассыПоДолжностям
	|		ПО (ИСТИНА)";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Возвращает список доступных тарифов страховых взносов в зависимости
// от настроек учета по организации.
//
// Параметры: 
// 	ВидОрганизации - ПеречислениеСсылка.ЮридическоеФизическоеЛицо - 
//	ПрименяетсяУСН - булево - определяет, применяется ли для организации УСН.
//	ПоказыватьТарифыПрименяемыеПоОтдельнымДоходам - булево
//
// Возвращаемое значение:
//	Массив значений типа СправочникСсылка.ВидыТарифовСтраховыхВзносов.
//
Функция ДоступныеТарифыСтраховыхВзносов(ВидОрганизации, ПрименяетсяУСН, ПоказыватьТарифыПрименяемыеПоОтдельнымДоходам) Экспорт
	ДоступныеТарифы = Новый Массив;
	
	Если ВидОрганизации = Неопределено Тогда
		Если ПрименяетсяУСН = Неопределено Тогда
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежим);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СельХозПроизводители);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ITОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииРазработчикиЭлектроннойПродукции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.Мультипликаторы);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СМИ);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.НекоммерческиеОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.БлаготворительныеОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТехникоВнедренческойЗоны);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникПроектаИнновационныйЦентрСколково);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентОсобойЗоныВКалининградскойОбласти);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентПортаВладивосток);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТерриторииОпережающегоСоциальноЭкономическогоРазвития);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникОсобойЗоныВКрыму);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ХозОбществаБюджетныхВУЗов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииОказывающиеИнжиниринговыеУслуги);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ЕСХН);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.НародныеХудожественныеПромыслы);
		ИначеЕсли ПрименяетсяУСН Тогда
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежим);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ITОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииРазработчикиЭлектроннойПродукции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.Мультипликаторы);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СМИ);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.НекоммерческиеОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.БлаготворительныеОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТехникоВнедренческойЗоны);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникПроектаИнновационныйЦентрСколково);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентОсобойЗоныВКалининградскойОбласти);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникОсобойЗоныВКрыму);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТерриторииОпережающегоСоциальноЭкономическогоРазвития);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентПортаВладивосток);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ХозОбществаБюджетныхВУЗов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииОказывающиеИнжиниринговыеУслуги);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ЕСХН);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.НародныеХудожественныеПромыслы);
		Иначе
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СельХозПроизводители);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ITОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииРазработчикиЭлектроннойПродукции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.Мультипликаторы);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СМИ);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТехникоВнедренческойЗоны);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникПроектаИнновационныйЦентрСколково);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентОсобойЗоныВКалининградскойОбласти);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникОсобойЗоныВКрыму);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТерриторииОпережающегоСоциальноЭкономическогоРазвития);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентПортаВладивосток);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ХозОбществаБюджетныхВУЗов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииОказывающиеИнжиниринговыеУслуги);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.НародныеХудожественныеПромыслы);
		КонецЕсли;	
	ИначеЕсли ВидОрганизации = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		Если ПрименяетсяУСН = Неопределено Тогда
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежим);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СельХозПроизводители);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ITОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииРазработчикиЭлектроннойПродукции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.Мультипликаторы);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СМИ);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.НекоммерческиеОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.БлаготворительныеОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТехникоВнедренческойЗоны);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникПроектаИнновационныйЦентрСколково);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентОсобойЗоныВКалининградскойОбласти);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТерриторииОпережающегоСоциальноЭкономическогоРазвития);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентПортаВладивосток);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ХозОбществаБюджетныхВУЗов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникОсобойЗоныВКрыму);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииОказывающиеИнжиниринговыеУслуги);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ЕСХН);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.НародныеХудожественныеПромыслы);
		ИначеЕсли ПрименяетсяУСН Тогда
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежим);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ITОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииРазработчикиЭлектроннойПродукции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.Мультипликаторы);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СМИ);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.НекоммерческиеОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.БлаготворительныеОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТехникоВнедренческойЗоны);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникПроектаИнновационныйЦентрСколково);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентОсобойЗоныВКалининградскойОбласти);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникОсобойЗоныВКрыму);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТерриторииОпережающегоСоциальноЭкономическогоРазвития);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентПортаВладивосток);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ХозОбществаБюджетныхВУЗов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииОказывающиеИнжиниринговыеУслуги);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ЕСХН);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.НародныеХудожественныеПромыслы);
		Иначе
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СельХозПроизводители);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ITОрганизации);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииРазработчикиЭлектроннойПродукции);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.Мультипликаторы);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СМИ);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТехникоВнедренческойЗоны);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникПроектаИнновационныйЦентрСколково);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентОсобойЗоныВКалининградскойОбласти);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникОсобойЗоныВКрыму);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТерриторииОпережающегоСоциальноЭкономическогоРазвития);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентПортаВладивосток);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ХозОбществаБюджетныхВУЗов);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацииОказывающиеИнжиниринговыеУслуги);
			ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.НародныеХудожественныеПромыслы);
		КонецЕсли;	
	ИначеЕсли ПрименяетсяУСН = Неопределено Тогда	
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежим);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СельХозПроизводители);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникОсобойЗоныВКрыму);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТерриторииОпережающегоСоциальноЭкономическогоРазвития);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентПортаВладивосток);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТехникоВнедренческойЗоны);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СМИ);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ЕСХН);
	ИначеЕсли ПрименяетсяУСН Тогда
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежим);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникОсобойЗоныВКрыму);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТерриторииОпережающегоСоциальноЭкономическогоРазвития);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентПортаВладивосток);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТехникоВнедренческойЗоны);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СМИ);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ЕСХН);
	Иначе
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.УчастникОсобойЗоныВКрыму);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТерриторииОпережающегоСоциальноЭкономическогоРазвития);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентПортаВладивосток);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.РезидентТехникоВнедренческойЗоны);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СМИ);
		ДоступныеТарифы.Добавить(Справочники.ВидыТарифовСтраховыхВзносов.СельХозПроизводители);
	КонецЕсли;
	
	Если ПоказыватьТарифыПрименяемыеПоОтдельнымДоходам Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыТарифовСтраховыхВзносов.Ссылка
		|ИЗ
		|	Справочник.ВидыТарифовСтраховыхВзносов КАК ВидыТарифовСтраховыхВзносов
		|ГДЕ
		|	ВидыТарифовСтраховыхВзносов.ПрименяетсяПоОтдельнымДоходам
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидыТарифовСтраховыхВзносов.РеквизитДопУпорядочивания";
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ДоступныеТарифы.Добавить(Выборка.Ссылка)
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДоступныеТарифы;	
	
КонецФункции

#КонецОбласти
