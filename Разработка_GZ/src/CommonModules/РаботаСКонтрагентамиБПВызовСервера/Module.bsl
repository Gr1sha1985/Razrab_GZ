////////////////////////////////////////////////////////////////////////////////
// Работа с контрагентами в конфигурации "Бухгалтерия предприятия".
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция ДанныеВыбораКонтрагента(Параметры, ОкончаниеВводаТекста = Ложь) Экспорт

	ДанныеВыбора = Справочники.Контрагенты.ПолучитьДанныеВыбора(Параметры);
	Если ДанныеВыбора.Количество() = 0 Тогда
		СтрокаПоиска = СокрЛП(Параметры.СтрокаПоиска);
		ДлинаСтроки  = СтрДлина(СтрокаПоиска);
		ЭтоСтрокаИНН = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаПоиска);
		Если ЭтоСтрокаИНН Тогда
			МассивПодстрок   = Новый Массив;
			МассивПодстрок.Добавить(НСтр("ru='Создать: ИНН '"));
			ИНН = Лев(СтрокаПоиска, 12);
			ЭтоКорректныйИНН = (ДлинаСтроки = 10
				И РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Истина, ""))
				ИЛИ (ДлинаСтроки = 12
				И РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Ложь, ""));
			Если ЭтоКорректныйИНН Тогда
				ПолужирныйШрифт = Новый Шрифт(, , Истина);
				МассивПодстрок.Добавить(
					Новый ФорматированнаяСтрока(ИНН, ПолужирныйШрифт, ЦветаСтиля.РезультатУспехЦвет));
			Иначе
				МассивПодстрок.Добавить(ИНН);
			КонецЕсли;
			СколькоВопросов = ?(ЭтоКорректныйИНН ИЛИ ДлинаСтроки >= 12, 0, 
				?(ДлинаСтроки < 10, 10 - ДлинаСтроки, 12 - ДлинаСтроки));
			ЗнакиВопроса = Лев("????????????", СколькоВопросов);
			ЛишниеЦифры  = Сред(СтрокаПоиска, 13);
			МассивПодстрок.Добавить(
				Новый ФорматированнаяСтрока(ЗнакиВопроса + ЛишниеЦифры, , ЦветаСтиля.ЦветТекстаНекорректногоКонтрагента));
			ПредставлениеНового = Новый ФорматированнаяСтрока(МассивПодстрок);
		Иначе
			ПредставлениеНового = СтрШаблон(НСтр("ru='Создать: %1'"), СтрокаПоиска);
		КонецЕсли;
		ДанныеВыбора.Добавить(СтрокаПоиска, ПредставлениеНового);
		Если ОкончаниеВводаТекста Тогда
			// Добавляем вторую строку, чтобы в этом режиме не срабатывал автовыбор единственной строки.
			ДанныеВыбора.Добавить(""); 
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеВыбора;

КонецФункции 

#КонецОбласти

