// См. РаботаСФайламиПереопределяемый.ПриОтправкеФайловПочтой
Процедура ПриОтправкеФайловПочтой(ПараметрыОтправки, ФайлыДляОтправки, ВладелецФайлов, УникальныйИдентификатор) Экспорт
	
	Получатель = "";
	Вложения = Новый Массив;
	ТолькоОдинФайл = ФайлыДляОтправки.Количество() = 1;
	
	МетаданныеВладельцаФайла = ВладелецФайлов.Метаданные();
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Контрагент", МетаданныеВладельцаФайла) Тогда
		Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецФайлов, "Контрагент");
		Получатель = ОтправкаПочтовыхСообщений.АдресаЭлектроннойПочты(Контрагент);
	КонецЕсли;
	
	Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ВладелецФайлов)) И МетаданныеВладельцаФайла.ДлинаНомера > 0 Тогда
		
		ШаблонТемаПисьма = НСтр("ru='%1, %2 к документу %3 %4 от %5'");
		НомерДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецФайлов, "Номер");
		ДатаДокумента  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецФайлов, "Дата");
		ТемаПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТемаПисьма,
			?(ТолькоОдинФайл, НСтр("ru='Файл'"), НСтр("ru='Файлы'")),
			?(ТолькоОдинФайл, НСтр("ru='присоединенный'"), НСтр("ru='присоединенные'")),
			ТипЗнч(ВладелецФайлов),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(НомерДокумента, Истина, Истина),
			Формат(ДатаДокумента, "ДЛФ=DD"));
		
	Иначе
		
		ШаблонТемаПисьма = НСтр("ru='%1, %2 к %3'");
		ТемаПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТемаПисьма,
			?(ТолькоОдинФайл, НСтр("ru='Файл'"), НСтр("ru='Файлы'")),
			?(ТолькоОдинФайл, НСтр("ru='присоединенный'"), НСтр("ru='присоединенные'")),
			ТипЗнч(ВладелецФайлов));
		
	КонецЕсли;
	
	ШаблонТекстаПисьма = НСтр("ru='К письму %1 %2:'");
	ТекстПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаПисьма,
		?(ТолькоОдинФайл, НСтр("ru='присоединен'"), НСтр("ru='присоединены'")),
		?(ТолькоОдинФайл, НСтр("ru='файл'"), НСтр("ru='файлы'")));
	
	Для Каждого ФайлДляОтправки Из ФайлыДляОтправки Цикл
		ПараметрыФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
		ПараметрыФайла.ИдентификаторФормы = УникальныйИдентификатор;
		ПараметрыФайла.ПолучатьСсылкуНаДвоичныеДанные = Истина;
		ДанныеФайла = РаботаСФайлами.ДанныеФайла(ФайлДляОтправки, ПараметрыФайла);
		ОписаниеФайла = Новый Структура;
		ОписаниеФайла.Вставить("Представление"            , ДанныеФайла.ИмяФайла);
		ОписаниеФайла.Вставить("АдресВоВременномХранилище", ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
		Вложения.Добавить(ОписаниеФайла);
		ИмяФайлаДляПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='- %1'"), ДанныеФайла.ИмяФайла);
		ТекстПисьма = ТекстПисьма + Символы.ПС + ИмяФайлаДляПисьма;
	КонецЦикла;
	
	ПараметрыОтправки.Получатель = Получатель;
	ПараметрыОтправки.Тема       = ТемаПисьма;
	ПараметрыОтправки.Текст      = ОтправкаПочтовыхСообщений.ПодготовитьТекстПисьма(ТекстПисьма);
	
	ОтправкаПочтовыхСообщений.ДополнитьПараметрыПисьма(ПараметрыОтправки);
	
КонецПроцедуры
