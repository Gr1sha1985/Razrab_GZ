////////////////////////////////////////////////////////////////////////////////
// КадровыйУчетКлиентСервер: методы кадрового учета, работающие на стороне 
//							клиента и сервера.
//  
////////////////////////////////////////////////////////////////////////////////
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает полное наименование сотрудника из ФИО с учетом уточнений наименований физлица и сотрудника.
//
// Параметры:
//   Фамилия  - Строка
//   Имя      - Строка
//   Отчество - Строка
//   УточнениеНаименованияФизЛица    - Строка
//   УточнениеНаименованияСотрудника - Строка
//
Функция ПолноеНаименованиеСотрудника(Фамилия, Имя, Отчество, УточнениеНаименованияФизЛица, УточнениеНаименованияСотрудника = "") Экспорт
	
	ЧастиНаименования = Новый Массив;
	
	ЧастиНаименования.Добавить(Фамилия);
	
	Если ЗначениеЗаполнено(Имя) Тогда
		ЧастиНаименования.Добавить(Имя);
	КонецЕсли;
	Если ЗначениеЗаполнено(Отчество) Тогда
		ЧастиНаименования.Добавить(Отчество);
	КонецЕсли;
	Если ЗначениеЗаполнено(УточнениеНаименованияФизЛица) Тогда
		ЧастиНаименования.Добавить(УточнениеНаименованияФизЛица);
	КонецЕсли;
	Если ЗначениеЗаполнено(УточнениеНаименованияСотрудника) Тогда
		ЧастиНаименования.Добавить(УточнениеНаименованияСотрудника);
	КонецЕсли;
	
	Возврат СтрСоединить(ЧастиНаименования, " ")
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик события ОбработкаПолученияДанныхВыбора перечисления ВидыЗанятости.
//
// Параметры:
//   ДанныеВыбора         - СписокЗначений
//   Параметры            - Структура
//   СтандартнаяОбработка - Булево
//
Процедура ОбработкаПолученияДанныхВыбораВидовЗанятости(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	КадровыйУчетКлиентСерверВнутренний.ОбработкаПолученияДанныхВыбораВидовЗанятости(ДанныеВыбора, Параметры, СтандартнаяОбработка);
	
КонецПроцедуры

// Проверяет СНИЛС на заполненность, исключая из СНИЛСа служебные символы-разделители.
//
// Параметры:
//		СНИЛС	- Строка
//
// Возвращаемое значение:
//		Булево
//
Функция СНИЛСЗаполнен(Знач СНИЛС) Экспорт
	
	Возврат НЕ ПустаяСтрока(СтрЗаменить(СНИЛС, "-", ""));
	
КонецФункции

// Рационального числа a/b для b <= 100
// Например, 0,125 = 1/8
// Параметры: 
//  ДесятичноеЧисло - число, представление для которого 
//       необходимо получить 
//  Числитель, Знаменатель - в параметры помещаются числитель и знаменатель дроби
//  Точность - точность переданного десятичного числа. Не обязательный, по умолчанию - 20
// Возвращаемое значение: Истина, если представление получено, Ложь - если не получено.
Функция ПредставлениеРациональногоЧисла(ДесятичноеЧисло, Числитель, Знаменатель, Точность = 20) Экспорт
	
	ПредставлениеНайдено = Ложь;
	Для ЗнаменательРасчетный = 1 По 100 Цикл
		
		ЧислительРасчетный = ДесятичноеЧисло * ЗнаменательРасчетный;
		Если Окр(ЧислительРасчетный, 0) = Окр(ЧислительРасчетный, Точность - 2) Тогда
			ЧислительРасчетный = Окр(ЧислительРасчетный, 0);
			ПредставлениеНайдено = Истина;
			Прервать;
		КонецЕсли; 
		
	КонецЦикла;
	
	Если ПредставлениеНайдено Тогда
		Числитель = ЧислительРасчетный;
		Знаменатель = ЗнаменательРасчетный;
	КонецЕсли; 
	
	Возврат ПредставлениеНайдено;
		
КонецФункции

Функция ТочностьКоличестваСтавок(Знач КоличествоСтавок) Экспорт
	
	Если КоличествоСтавок = Цел(КоличествоСтавок) Тогда
		Точность = 0;
	Иначе
		
		Для Точность = 1 По 19 Цикл
			
			Если КоличествоСтавок = Цел(КоличествоСтавок * Pow(10, Точность)) / Pow(10, Точность) Тогда
				Точность = Точность;
				Прервать;
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Точность;
	
КонецФункции

Функция ПредставлениеКоличестваСтавок(Знач КоличествоСтавок) Экспорт
	
	ПредставлениеКоличестваСтавок = "";
	
	Если КоличествоСтавок = 1 Тогда
		ПредставлениеКоличестваСтавок = "1";
	Иначе
		
		Если ЗначениеЗаполнено(КоличествоСтавок) Тогда
			
			ЦелаяЧасть = Цел(КоличествоСтавок);
			Если ЦелаяЧасть <> КоличествоСтавок Тогда
				
				Числитель = 1;
				Знаменатель = 1;
				Если ПредставлениеРациональногоЧисла(КоличествоСтавок - ЦелаяЧасть, Числитель, Знаменатель) Тогда
					
					Если Знаменатель > 1 Тогда
						
						ПредставлениеКоличестваСтавок = Строка(Числитель) + "/" + Строка(Знаменатель);
						Если ЦелаяЧасть > 0 Тогда
							ПредставлениеКоличестваСтавок = Строка(ЦелаяЧасть) + " (" + ПредставлениеКоличестваСтавок + ")"
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли; 
				
			КонецЕсли; 
			
		КонецЕсли; 
	
	КонецЕсли;
	
	Если ПустаяСтрока(ПредставлениеКоличестваСтавок) Тогда
		ПредставлениеКоличестваСтавок = Формат(КоличествоСтавок, "ЧН=");
	КонецЕсли;
	
	Возврат ПредставлениеКоличестваСтавок;
	
КонецФункции

#КонецОбласти
