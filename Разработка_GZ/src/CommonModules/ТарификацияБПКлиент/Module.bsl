#Область ПрограммныйИнтерфейс

// Оповещает пользователя о том, что его действие заблокировано
// ограничением тарифа и записывает информацию об этом в журнал регистрации.
//
Процедура ОповеститьОбОграниченииТарифа(ИмяОбъекта) Экспорт
	
	ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
		СобытиеЖурналаРегистрацииСообщение(),
		,
		Строка(ИмяОбъекта),
		,
		Истина);
	
	ТекстПредупреждения = ТарификацияБПКлиентСервер.ПолучитьТекстПредупреждения();
	ПоказатьПредупреждение(
		,
		ТекстПредупреждения,
		,
		НСтр("ru='Ограничение тарифа'"));
	
КонецПроцедуры

// Открывает форму выбора тарифа.
// 
// Параметры:
//  - Владелец (обязательный) - форма или элемент управления другой формы.
//  - ПараметрыФормы (необязательный) - Структура - параметры открытия формы
//    выбора тарифа со следюущими ключами.
//    - ОткрыватьФормуОплаты - Булево - признак открытия формы оплаты сервиса
//      после выбора тарифа. По умолчанию Истина.
//    - ЗаголовокСсылкиВозвратаКВладельцу - Строка - текст заголовка
//      ссылки возврата к форме-владельцу. По умолчанию не указан.
//      Если параметр не задан, в форме оплаты отображается заголовок ссылки,
//      определенный в метаданных формы.
//
Процедура ОткрытьФормуВыбораТарифа(Владелец, ПараметрыФормы = Неопределено) Экспорт
	
	Если ПараметрыФормы = Неопределено Тогда
		ПараметрыФормы = Новый Структура;
	КонецЕсли;
	
	Если НЕ ПараметрыФормы.Свойство("ОткрыватьФормуОплаты") Тогда
		ПараметрыФормы.Вставить("ОткрыватьФормуОплаты", Истина);
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ОплатаСервисаБП.Форма.ВыборТарифа", ПараметрыФормы,
		Владелец,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СобытиеЖурналаРегистрацииСообщение()
	
	КодЯзыка = ОбщегоНазначенияКлиент.КодОсновногоЯзыка();
	ШаблонТекста = НСтр("ru='%1.Вызов функции'", КодЯзыка);
	Возврат СтрШаблон(ШаблонТекста, ТарификацияБПКлиентСервер.ГруппаСобытийЖурналаРегистрации());
	
КонецФункции

#КонецОбласти
