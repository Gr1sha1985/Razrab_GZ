
#Область СлужебныйПрограммныйИнтерфейс

Процедура ПроверкаРайонногоКоэффициентаПередЗаписью(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = Источник.Метаданные().Реквизиты;
	Если Реквизиты.Найти("РайонныйКоэффициент") <> НеОпределено Тогда
		Если Источник.РайонныйКоэффициент < 1 Тогда
			Источник.РайонныйКоэффициент = 1;
		КонецЕсли;
	КонецЕсли;
	
	Если Реквизиты.Найти("РайонныйКоэффициентРФ") <> НеОпределено Тогда
		Если Источник.РайонныйКоэффициентРФ < 1 Тогда
			Источник.РайонныйКоэффициентРФ = 1;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарплатаКадрыОрганизацииПередЗаписью(Источник, Отказ) Экспорт
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Источник.ОбособленноеПодразделение Тогда
		Если ЗначениеЗаполнено(Источник.Ссылка) Тогда
			Источник.ГоловнаяОрганизация = Источник.Ссылка;
		Иначе
			Если Источник.ПолучитьСсылкуНового().Пустая() Тогда
				Источник.УстановитьСсылкуНового(Справочники.Организации.ПолучитьСсылку());
			КонецЕсли;
			Источник.ГоловнаяОрганизация = Источник.ПолучитьСсылкуНового();
		КонецЕсли;
	КонецЕсли;
	Если Не Источник.ПрименятьРайонныйКоэффициент Тогда
		Источник.РайонныйКоэффициент = 1;
		Источник.РайонныйКоэффициентРФ = 1;
	КонецЕсли;
	
	Если Источник.ЭтоНовый() Тогда
		Источник.ДополнительныеСвойства.Вставить("ЭтоНовый", Истина);
	КонецЕсли;
	
	Источник.ДополнительныеСвойства.Вставить("ЕстьОбособленныеПодразделения", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "ЕстьОбособленныеПодразделения"));
	
	ПерсонифицированныйУчетКлиентСервер.УстановитьФорматРегистрационногоНомераПФР(Источник.РегистрационныйНомерПФР);
	
КонецПроцедуры

Процедура ЗарплатаКадрыОрганизацииПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	// Пишем функциональную опцию в зависимости от использования 
	// начисления зарплаты.
	ИспользоватьНачислениеЗарплаты = Константы.ИспользоватьНачислениеЗарплаты.Получить();
	ЗаписиФО = РегистрыСведений.НастройкиЗарплатаКадры.СоздатьНаборЗаписей();
	ЗаписиФО.Отбор.Организация.Установить(Источник.Ссылка);
	ЗаписиФО.Прочитать();
	Если ЗаписиФО.Количество() = 0 Тогда
		Строка = ЗаписиФО.Добавить();
	Иначе
		Строка = ЗаписиФО[0];
	КонецЕсли;
	ПрименятьСевернуюНадбавку = ИспользоватьНачислениеЗарплаты И Источник.ПрименятьСевернуюНадбавку;
	ПрименятьРайонныйКоэффициент = ИспользоватьНачислениеЗарплаты И Источник.ПрименятьРайонныйКоэффициент;
	
	ОбновлятьПланВидовРасчета = Строка.ПрименятьСевернуюНадбавку <> ПрименятьСевернуюНадбавку 
		Или Строка.ПрименятьРайонныйКоэффициент <> ПрименятьРайонныйКоэффициент;
	
	// При изменении применения районного коэффициента или северной надбавки
	// а также при изменении признака наличия обособленных подразделений.
	Если ОбновлятьПланВидовРасчета 
		Или Источник.ДополнительныеСвойства.ЕстьОбособленныеПодразделения <> Источник.ЕстьОбособленныеПодразделения Тогда
		// Пишем значения настроек организации.
		Строка.Организация = Источник.Ссылка;
		Строка.ПрименятьСевернуюНадбавку = ИспользоватьНачислениеЗарплаты И Источник.ПрименятьСевернуюНадбавку;
		Строка.ПрименятьРайонныйКоэффициент = ИспользоватьНачислениеЗарплаты И Источник.ПрименятьРайонныйКоэффициент;
		
		УстановитьПривилегированныйРежим(Истина);
		
		ЗаписиФО.Записать();
		Если ОбновлятьПланВидовРасчета Тогда
			// Обновляем состав начислений.
			РасчетЗарплаты.СформироватьВидыРасчетаРКиСН();
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	ЗарплатаКадры.УстановитьРеквизитыВПодчиненныхПодразделениях(Источник);
	
	Если Источник.ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодразделенияОрганизацийПередЗаписью(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ОбработкаЗаписиРодителя") И Источник.ДополнительныеСвойства.ОбработкаЗаписиРодителя Тогда
		Возврат;
	КонецЕсли;
	
	Источник.ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Владелец, "ГоловнаяОрганизация");
	
	Если Источник.ЭтоНовый() Или Не Источник.ОбособленноеПодразделение Тогда
		
		Если ЗначениеЗаполнено(Источник.РегистрацияВНалоговомОргане) 
			И Источник.ОбособленноеПодразделение Тогда
			СписокПолей = "РайонныйКоэффициент,РайонныйКоэффициентРФ";
		Иначе
			СписокПолей = "РегистрацияВНалоговомОргане,РайонныйКоэффициент,РайонныйКоэффициентРФ";
		КонецЕсли;
			
		Если ЗначениеЗаполнено(Источник.Родитель) Тогда
			ИсточникСведений = Источник.Родитель;
			ПрименяетсяРайонныйКоэффициент = Истина;
			УстановитьПрименяемыйЛьготныйТерриториальныйТариф = Истина;
			СписокПолей = СписокПолей + ",ПрименяемыйЛьготныйТерриториальныйТариф";
		Иначе
			ИсточникСведений = Источник.Владелец;
			ПрименяетсяРайонныйКоэффициент = Неопределено;
			УстановитьПрименяемыйЛьготныйТерриториальныйТариф = Ложь;
			СписокПолей = СписокПолей + ",ПрименятьРайонныйКоэффициент";
		КонецЕсли;
		
		РеквизитыИсточникаСведений = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсточникСведений, СписокПолей);
		
		Если РеквизитыИсточникаСведений.Свойство("РегистрацияВНалоговомОргане") Тогда
			Источник.РегистрацияВНалоговомОргане = РеквизитыИсточникаСведений.РегистрацияВНалоговомОргане;
		КонецЕсли;
		
		Если Не Источник.ОбособленноеПодразделение Или Источник.РайонныйКоэффициент < 1 Тогда
			
			Если ПрименяетсяРайонныйКоэффициент = Неопределено Тогда
				РеквизитыИсточникаСведений.Свойство("ПрименятьРайонныйКоэффициент",ПрименяетсяРайонныйКоэффициент);
			КонецЕсли;
			
			Если ПрименяетсяРайонныйКоэффициент Тогда
				Источник.РайонныйКоэффициент = РеквизитыИсточникаСведений.РайонныйКоэффициент;
				Источник.РайонныйКоэффициентРФ = РеквизитыИсточникаСведений.РайонныйКоэффициентРФ;
			КонецЕсли;
			
		КонецЕсли;
		
		Если УстановитьПрименяемыйЛьготныйТерриториальныйТариф Тогда
			Источник.ПрименяемыйЛьготныйТерриториальныйТариф = РеквизитыИсточникаСведений.ПрименяемыйЛьготныйТерриториальныйТариф;
		КонецЕсли;
		
	КонецЕсли;
	
	Источник.ДополнительныеСвойства.Вставить("ОбособленноеПодразделение", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "ОбособленноеПодразделение"));
	
КонецПроцедуры

Процедура УстановитьЗначенияРеквизитовПодчиненныхПодразделенийПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ОбработкаЗаписиРодителя") И Источник.ДополнительныеСвойства.ОбработкаЗаписиРодителя Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадры.УстановитьРеквизитыВПодчиненныхПодразделениях(Источник);
	
КонецПроцедуры

Процедура РегистрыУчетаНалоговВзносовСФОТПередЗаписью(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаНабора Из Источник Цикл
		СтрокаНабора.Период = НачалоДня(СтрокаНабора.Период)
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьПодчиненностьОрганизации(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Источник.ЭтоНовый() Тогда
		
		РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка, "ГоловнаяОрганизация,ЕстьОбособленныеПодразделения,ИндивидуальныйПредприниматель");
		
		ПредыдущаяГоловнаяОрганизация = РеквизитыОрганизации.ГоловнаяОрганизация;
		Если ЗначениеЗаполнено(ПредыдущаяГоловнаяОрганизация) И Источник.ГоловнаяОрганизация <> ПредыдущаяГоловнаяОрганизация Тогда
			
			ЗарплатаКадры.ПроверитьВозможностьСменыГоловнойОрганизации(Источник.Ссылка, Отказ);
			
			Если НЕ Отказ Тогда
				Источник.ДополнительныеСвойства.Вставить("ИзмененаГоловнаяОрганизация", ПредыдущаяГоловнаяОрганизация);
			КонецЕсли;
			
		КонецЕсли;
		
		ПредыдущееЕстьОбособленныеПодразделения = РеквизитыОрганизации.ЕстьОбособленныеПодразделения;
		Если ПредыдущееЕстьОбособленныеПодразделения И Не Источник.ЕстьОбособленныеПодразделения Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ГоловнаяОрганизация", Источник.Ссылка);
			
			Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	Организации.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Организации КАК Организации
				|ГДЕ
				|	Организации.Ссылка <> &ГоловнаяОрганизация
				|	И Организации.ГоловнаяОрганизация = &ГоловнаяОрганизация";
			
			УстановитьПривилегированныйРежим(Истина);
			РезультатЗапроса = Запрос.Выполнить();
			УстановитьПривилегированныйРежим(Ложь);
			
			Если Не РезультатЗапроса.Пустой() Тогда
				
				ТекстИсключенияЗаписи = СтрШаблон(
					НСтр("ru = 'Не удалось отключить наличие филиалов (обособленных подразделений).
						|В базе данных есть филиалы (обособленные подразделения) организации ""%1"".'"),
					Источник.Наименование);
				
				ВызватьИсключение ТекстИсключенияЗаписи;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Источник.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо
			И Источник.ИндивидуальныйПредприниматель <> РеквизитыОрганизации.ИндивидуальныйПредприниматель Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ГоловнаяОрганизация", Источник.Ссылка);
			Запрос.УстановитьПараметр("ФизическоеЛицо", Источник.ИндивидуальныйПредприниматель);
			
			Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	Сотрудники.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Сотрудники КАК Сотрудники
				|ГДЕ
				|	Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация
				|	И Сотрудники.ФизическоеЛицо = &ФизическоеЛицо";
			
			Если Не Запрос.Выполнить().Пустой() Тогда
				ЗарплатаКадры.СообщитьОНевозможностиИППриниматьНаРаботуСамогоСебя();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПодчиненностьОрганизации(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущаяГоловнаяОрганизация = Неопределено;
	Если НЕ Источник.ДополнительныеСвойства.Свойство("ИзмененаГоловнаяОрганизация", ПредыдущаяГоловнаяОрганизация) Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Источник.Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ПредыдущаяГоловнаяОрганизация);
	
	Если ПредыдущаяГоловнаяОрганизация = Источник.Ссылка Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Сотрудники.Ссылка
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация";
			
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				СотрудникОбъект = Выборка.Ссылка.ПолучитьОбъект();
				
				Попытка 
					СотрудникОбъект.Заблокировать();
				Исключение
					ТекстИсключенияЗаписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось изменить головную организацию ""%1"".
							|Возможно, сотрудник редактируется другим пользователем'"),
						СотрудникОбъект.Наименование);			
					ВызватьИсключение ТекстИсключенияЗаписи;
				КонецПопытки;
		
				СотрудникОбъект.ГоловнаяОрганизация = Источник.ГоловнаяОрганизация;
				СотрудникОбъект.Записать();
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Подразделения.Ссылка
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК Подразделения
		|ГДЕ
		|	Подразделения.Владелец = &Организация
		|	И Подразделения.ГоловнаяОрганизация <> &ГоловнаяОрганизация";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ПодразделениеОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Попытка 
				ПодразделениеОбъект.Заблокировать();
			Исключение
				ТекстИсключенияЗаписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось изменить головную организацию ""%1"".
						|Возможно, подразделение редактируется другим пользователем'"),
					ПодразделениеОбъект.Наименование);			
				ВызватьИсключение ТекстИсключенияЗаписи;
			КонецПопытки;
		
			ПодразделениеОбъект.ГоловнаяОрганизация = Источник.ГоловнаяОрганизация;
			ПодразделениеОбъект.Записать();
			
		КонецЦикла;
		
	КонецЕсли; 
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

Процедура ЗапретитьРедактированиеОбщихДанныхНаборЗаписейПередЗаписью(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверяем, если...
	// - набор пустой, 
	// - и хотя бы одно его ведущее измерение заполнено ссылкой на несуществующий (удаленный) объект, 
	// ..то пропускаем, т.е. снимаем запрет.
	Если Источник.Количество() = 0 Тогда
		Для Каждого Измерение Из Источник.Метаданные().Измерения Цикл
			Если Измерение.Ведущее Тогда
				ЭлементОтбора = Источник.Отбор.Найти(Измерение.Имя);
				Если ЭлементОтбора <> Неопределено И ЭлементОтбора.Использование = Истина Тогда
					Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ЭлементОтбора.Значение)) И Не ОбщегоНазначения.СсылкаСуществует(ЭлементОтбора.Значение) Тогда
						 Возврат;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ЗаписьОбщихДанных") Тогда 
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ПодборИзКлассификатора") Тогда 
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru='Редактирование общих данных запрещено.'");
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	
КонецПроцедуры

Процедура ЗапретитьРедактированиеОбщихДанныхОбъектПередЗаписью(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ЗаписьОбщихДанных") Тогда 
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ПодборИзКлассификатора") Тогда 
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru='Редактирование общих данных запрещено.'");
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	
КонецПроцедуры

Процедура ПроверитьЗаписьПоУмолчаниюРегистраСведений(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ДатаОтсчетаПериодическихСведений = ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведений();
	
	Для каждого Запись Из Источник Цикл
		
		Если Запись.Период < ДатаОтсчетаПериодическихСведений Тогда
			
			МетаданныеРегистра = Источник.Метаданные();
			Если МетаданныеРегистра.ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Месяц Тогда
				МинимальныйДопустимыйПериод = КонецМесяца(ДатаОтсчетаПериодическихСведений) + 1;
			Иначе
				МинимальныйДопустимыйПериод = КонецДня(ДатаОтсчетаПериодическихСведений) + 1;
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Новые значения ""%1"" можно установить не ранее чем с %2'"),
					МетаданныеРегистра.Имя,
					Формат(МинимальныйДопустимыйПериод, "ДЛФ=DD")),
				,
				"Объект.Период",
				,
				Отказ);
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьЗначениеРеквизитаОрганизацияПриОднофирменномУчетеОбработкаЗаполнения(Источник, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт

	ЗарплатаКадры.ЗаполнитьЗначениеРеквизитаОрганизацияПриОднофирменномУчете(Источник);
	
КонецПроцедуры

Процедура ЗаполнитьЗначениеРеквизитаОрганизацияСправочникаПриОднофирменномУчетеБазоваяОбработкаЗаполнения(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	ЗарплатаКадры.ЗаполнитьЗначениеРеквизитаОрганизацияПриОднофирменномУчете(Источник);
	
КонецПроцедуры

// Обработчик подписки на событие ПроверитьУникальностьЕстественногоКлюча.
// Предназначена для обеспечения уникальности естественных ключей объектов.
//	
Процедура ПроверитьУникальностьЕстественногоКлюча(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат
	КонецЕсли;
	
	Если Источник.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;	
	
	МетаданныеИсточника = Источник.Метаданные(); 
	МенеджерИсточника = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеИсточника.ПолноеИмя());
	ПоляЕстественногоКлюча = МенеджерИсточника.ПоляЕстественногоКлюча();	
	
	ПредставлениеТипа = МетаданныеИсточника.ПредставлениеОбъекта;
	Если ПустаяСтрока(ПредставлениеТипа) Тогда
		ПредставлениеТипа = МетаданныеИсточника.Представление();
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИсточника.Ссылка КАК Ссылка
	|ИЗ
	|	#ТаблицаИсточника КАК ТаблицаИсточника
	|ГДЕ
	|	ТаблицаИсточника.Ссылка <> &Источник
	|	И НЕ ТаблицаИсточника.ПометкаУдаления
	|	И &Условие";
	Запрос.УстановитьПараметр("Источник", Источник.Ссылка);
	
	ИменаКлючей = "";
	ПредставлениеКлючей = "";
	Условие = "";
	Итерация = 0;
	Для Каждого ПолеЕстественногоКлюча Из ПоляЕстественногоКлюча Цикл 
		
		ЗнакПрепинания = ?(Итерация = 0, "", ", ");
		ЛогическаяФункция = ?(Итерация = 0, "", " И ");

		ИменаКлючей = ИменаКлючей + ЗнакПрепинания + ПолеЕстественногоКлюча;
		ПредставлениеКлючей = ПредставлениеКлючей + ЗнакПрепинания + Источник[ПолеЕстественногоКлюча];
		
		Условие = Условие + ЛогическаяФункция + "ТаблицаИсточника."+ПолеЕстественногоКлюча +" = &"+ПолеЕстественногоКлюча;
		Запрос.УстановитьПараметр(ПолеЕстественногоКлюча, Источник[ПолеЕстественногоКлюча]);
		
		Итерация = Итерация + 1;
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаИсточника", МетаданныеИсточника.ПолноеИмя());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", Условие);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат
	КонецЕсли;	
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		?(ПоляЕстественногоКлюча.Количество() = 1,
			НСтр("ru='Уже существует %1 с таким значением поля %2 (%3)'"),
			НСтр("ru='Уже существует %1 с таким значением полей %2 (%3)'")),
		НРег(ПредставлениеТипа),
		ИменаКлючей,
		ПредставлениеКлючей);
		
	ВызватьИсключение ТекстСообщения	
	
КонецПроцедуры

Процедура УстановитьНастройкиЗарплатаКадрыПередЗаписью(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	// Пишем функциональную опцию в зависимости от использования 
	// начисления зарплаты.
	
	// Получим данные об организациях.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	Организации.ПрименятьСевернуюНадбавку,
	|	Организации.ПрименятьРайонныйКоэффициент
	|ИЗ
	|	РегистрСведений.НастройкиЗарплатаКадры КАК НастройкиЗарплатаКадры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО НастройкиЗарплатаКадры.Организация = Организации.Ссылка");
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЗаписиФО = РегистрыСведений.НастройкиЗарплатаКадры.СоздатьНаборЗаписей();
	Пока Выборка.Следующий() Цикл
		Строка = ЗаписиФО.Добавить();
		Строка.Организация = Выборка.Организация;
		Строка.ПрименятьСевернуюНадбавку = Источник.Значение И Выборка.ПрименятьСевернуюНадбавку;
		Строка.ПрименятьРайонныйКоэффициент = Источник.Значение И Выборка.ПрименятьРайонныйКоэффициент;
	КонецЦикла;
	ЗаписиФО.Записать();									
	
КонецПроцедуры

Процедура УстановитьДатуДокументаЗарплатаКадрыОбработкаЗаполнения(Источник, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	ИменаПодсистем = "ЗарплатаКадрыПодсистемы,ЗарплатаКадрыПриложения,ЗарплатаКадрыРасширеннаяПодсистемы,ЗарплатаКадрыКорпоративнаяПодсистемы";
	ИсточникВходитВПодсистемуЗарплатаКадры = ЗарплатаКадры.ОбъектМетаданныхВключенВПодсистемы(Источник.Метаданные().ПолноеИмя(), ИменаПодсистем);
	Если ИсточникВходитВПодсистемуЗарплатаКадры И НЕ ЗначениеЗаполнено(Источник.Дата) Тогда
		Источник.Дата = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьИсториюРегистрацийВНалоговомОргане(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", Источник.Ссылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсторияРегистрацийВНалоговомОргане.Период,
		|	ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница
		|ИЗ
		|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
		|ГДЕ
		|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.ИсторияРегистрацийВНалоговомОргане.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(Выборка.СтруктурнаяЕдиница);
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


