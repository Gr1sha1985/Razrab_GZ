////////////////////////////////////////////////////////////////////////////////
// СписаниеСРасчетногоСчетаФормы: клиентские процедуры и функции, 
// вызываемые из форм документа "Списание с расчетного счета".
//  
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

Процедура ПередЗакрытием(Форма, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка) Экспорт
	
	Если ЗавершениеРаботы И Форма.Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Форма.Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("Форма", Форма);
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Форма, Отказ, ПараметрыЗаписи) Экспорт
	
	Объект = Форма.Объект;
	
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		Если Объект.НеПодтвержденоВыпискойБанка Тогда
			Отказ = Истина;
			ТекстВопроса = НСтр("ru = 'Проведение документа возможно только после установки флага ""Подтверждено выпиской банка"".
				|Записать документ без проведения?'");
			
			ДополнительныеПараметры = Новый Структура();
			ДополнительныеПараметры.Вставить("Форма", Форма);
			ДополнительныеПараметры.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);
			
			Оповещение = Новый ОписаниеОповещения("ВопросПередЗаписьюЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
		Иначе
			КлючеваяОперация = "ПроведениеСписаниеСРасчетногоСчета";
			ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗаписи(Форма, ПараметрыЗаписи) Экспорт
	
	Объект = Форма.Объект;
	
	// Оповестим оплачиваемые документы выплаты зарплаты
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП") Тогда
		Для каждого СтрокаПлатеж Из Объект.ПеречислениеЗаработнойПлаты Цикл
			Оповестить("ИзменениеОплатыВедомости",, СтрокаПлатеж.Ведомость);
			Если Объект.Проведен Тогда
				Оповестить("ОбновитьФорму",, СтрокаПлатеж.Ведомость);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВидыОперацииУплатаНалога = Новый Массив;
	ВидыОперацииУплатаНалога.Добавить(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога"));
	ВидыОперацииУплатаНалога.Добавить(
		ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц"));
	
	// Оповестим оплачиваемые документы выплаты зарплаты
	Если Объект.Проведен И ВидыОперацииУплатаНалога.Найти(Объект.ВидОперации) <> Неопределено Тогда
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ЗадачиБухгалтера"));
	КонецЕсли;
	
	Оповестить("ОбновитьФорму", Форма.ВладелецФормы, Объект.Ссылка);
	ОповеститьОбИзменении(Тип("ДокументСсылка.ПлатежноеПоручение"));
	
	Если ПараметрыЗаписи.Свойство("ОплаченныеСчета") И ПараметрыЗаписи.ОплаченныеСчета.Количество() > 0 Тогда
		ОповеститьОбИзменении(Тип("ДокументСсылка.СчетНаОплатуПоставщика"));
		Оповестить("ИзмененСтатусДокументов", ПараметрыЗаписи.ОплаченныеСчета, Объект.Ссылка);
	КонецЕсли;
	
	Оповестить("ИзменениеВыписки");
	
	Если ПараметрыЗаписи.Свойство("ОповеститьОбИзмененииСтатусаПредупрежденияЗагрузкиВыписки")
		И ПараметрыЗаписи.ОповеститьОбИзмененииСтатусаПредупрежденияЗагрузкиВыписки Тогда
		ОповеститьОбИзмененииПредупрежденияПриЗагрузкеВыписки(Объект.Ссылка);
	КонецЕсли;
	
	СписаниеСРасчетногоСчетаФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура ОбработкаНавигационнойСсылки(Форма, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка) Экспорт
	РасходныйКассовыйОрдерФормыКлиент.ОбработкаНавигационнойСсылки(Форма, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
КонецПроцедуры
 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

Процедура ВидОперацииАвтоПодбор(ВидОперации, ДанныеВыбора, Ожидание, СтандартнаяОбработка, ЭтоПлатежноеПоручение = Ложь) Экспорт
	
	Если Ожидание = 0 Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщику"));
		ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю"));
		Если НЕ ЭтоПлатежноеПоручение Тогда
			ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.КомиссияБанка"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ВидОперации) И ДанныеВыбора.НайтиПоЗначению(ВидОперации) = Неопределено Тогда
			ДанныеВыбора.Добавить(ВидОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВедомостьЗарплатаРаботникуНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	Если Форма.УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",  Объект.Организация);
		ПараметрыФормы.Вставить("ВидМестаВыплаты", ПредопределенноеЗначение("Перечисление.ВидыМестВыплатыЗарплаты.БанковскийСчет"));
		ПараметрыФормы.Вставить("СпособРасчетов",  ПредопределенноеЗначение("Перечисление.СпособыРасчетовСФизическимиЛицами.ОплатаТруда"));
		ОткрытьФорму("Документ.ВедомостьНаВыплатуЗарплаты.ФормаВыбора",
			Новый Структура("ПараметрыОтбораСписка, ТекущаяСтрока", ПараметрыФормы, Объект.ПлатежнаяВедомость), Элемент);
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
		ОткрытьФорму("Документ.ВедомостьНаВыплатуЗарплатыВБанк.ФормаВыбора",
			Новый Структура("Отбор, ТекущаяСтрока", ПараметрыФормы, Объект.ПлатежнаяВедомость), Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВедомостьПоДоговоруПодрядаНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",   Объект.Организация);
	ПараметрыФормы.Вставить("СпособВыплаты", СписаниеСРасчетногоСчетаФормыВызовСервера.СпособВыплатыПоДоговоруПодряда());
	ОткрытьФорму("Документ.ВедомостьНаВыплатуЗарплатыВБанк.ФормаВыбора",
		Новый Структура("Отбор, ТекущаяСтрока", ПараметрыФормы, Объект.ПлатежнаяВедомость), Элемент);
	
КонецПроцедуры

Процедура НадписьПлатежноеПоручениеНажатие(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	Параметры = Форма.Параметры;
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Параметры.Ключ) ИЛИ Форма.Модифицированность Тогда
		ОбъектЗаписан = Форма.Записать();
		
		Если НЕ ОбъектЗаписан Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Форма.ПлатежноеПоручение) Тогда
		ПоказатьЗначение(, Форма.ПлатежноеПоручение);
	Иначе
		Если Объект.ПометкаУдаления Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Нельзя вводить платежное поручение на основании документа, помеченного на удаление!'"));
			Возврат;
		КонецЕсли;
		
		ПараметрыСписания = Новый Структура("Основание, ВидОперации", Объект.Ссылка, Объект.ВидОперации);
		ОткрытьФорму("Документ.ПлатежноеПоручение.ФормаОбъекта", ПараметрыСписания, Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура НалогАвтоПодбор(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	Если СтрДлина(Текст) > 13 Тогда
		
		СтрокаПоискаПоКБК = СтрЗаменить(ПараметрыПолученияДанных.СтрокаПоиска, " ", "");
		
		Если СтрДлина(СтрокаПоискаПоКБК) <= 20 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаПоискаПоКБК) Тогда
			
			// Строка поиска похожа на КБК. Код включает код подвида доходов.
			ДанныеВыбора = СписаниеСРасчетногоСчетаФормыВызовСервера.ДанныеВыбораНалогаПоКБК(СтрокаПоискаПоКБК);
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НалогОкончаниеВводаТекста(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка) Экспорт
	
	Если СтрДлина(Текст) > 13 Тогда
		
		СтрокаПоискаПоКБК = СтрЗаменить(ПараметрыПолученияДанных.СтрокаПоиска, " ", "");
		
		Если СтрДлина(СтрокаПоискаПоКБК) <= 20 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаПоискаПоКБК) Тогда
			
			// Строка поиска похожа на КБК. Код включает код подвида доходов.
			ДанныеВыбора = СписаниеСРасчетногоСчетаФормыВызовСервера.ДанныеВыбораНалогаПоКБК(СтрокаПоискаПоКБК);
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НалоговыйПериодРегулирование(Форма, Элемент, Направление, СтандартнаяОбработка) Экспорт
	
	ИзменениеПериодаУплаты(Форма, "Объект.НалоговыйПериод", "НалоговыйПериодСтрока", Направление);
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

Процедура НалоговыйПериодНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(Объект.НалоговыйПериод) Тогда
		ТекущийПериод = Объект.НалоговыйПериод;
	Иначе
		ТекущийПериод = Объект.Дата;
	КонецЕсли;
	
	ВидПериода        = "Месяц";
	НарастающимИтогом = Ложь;
	
	Если ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоТорговыйСбор(Форма.ВидНалога) Тогда
		ВидПериода = "Квартал";
	ИначеЕсли ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоНалогУСН(Форма.ВидНалога)
		Или ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоНДФЛ_ИП(Форма.ВидНалога) Тогда
		ВидПериода        = "Квартал";
		НарастающимИтогом = Истина;
	КонецЕсли;
	
	Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность." + ВидПериода);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НачалоПериода", ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, ТекущийПериод));
	ПараметрыФормы.Вставить("КонецПериода",  ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, ТекущийПериод));
	ПараметрыФормы.Вставить("НарастающимИтогом", НарастающимИтогом);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода" + ВидПериода,
		ПараметрыФормы, Элемент,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ПодразделениеОрганизацииПриИзменении(Форма, Элемент) Экспорт
	
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(Объект.ПодразделениеОрганизации) Тогда
		СписаниеСРасчетногоСчетаФормыКлиентСервер.УстановитьПодразделениеДт(Форма);
	Иначе
		Объект.ПодразделениеДт = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодтвержденоВыпискойБанкаПриИзменении(Форма, Элемент) Экспорт
	
	Объект = Форма.Объект;
	
	Объект.НеПодтвержденоВыпискойБанка = НЕ Форма.ПодтвержденоВыпискойБанка;
	
КонецПроцедуры

Процедура ПорядокОтраженияБУНажатие(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	
	ПараметрыФормы.Вставить("Период",      Объект.Дата);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("Счет",        Объект.СчетУчетаРасчетовСКонтрагентом);
	ПараметрыФормы.Вставить("Субконто1",   Объект.СубконтоДт1);
	ПараметрыФормы.Вставить("Субконто2",   Объект.СубконтоДт2);
	ПараметрыФормы.Вставить("Субконто3",   Объект.СубконтоДт3);
	ПараметрыФормы.Вставить("Направление", "Дт");
	
	Если Объект.СчетУчетаРасчетовСКонтрагентом = ПредопределенноеЗначение(
			"ПланСчетов.Хозрасчетный.НДСНалоговогоАгента") Тогда
		ПараметрыФормы.Вставить("АдресТаблицыНалоговыйАгентНДСВХранилище", 
			СписаниеСРасчетногоСчетаФормыВызовСервера.ПоместитьРасшифровкуНалоговыйАгентНДСВХранилище(Объект));
	КонецЕсли;
		
	ПараметрыФормы.Вставить("СуммаПоПлатежномуПоручению", Объект.СуммаДокумента);
	ПараметрыФормы.Вставить("НазначениеПлатежа",          Объект.НазначениеПлатежа);
	ПараметрыФормы.Вставить("ДокументСсылка",             Объект.Ссылка);
	
	ОткрытьФорму("ОбщаяФорма.ПорядокОтраженияБУ", ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура ПрочееРасходыУСНПриИзменении(Форма, Элемент) Экспорт
	
	ОграничитьПринимаемыеРасходыДляУСН(Форма);
	ОграничитьНДСДляУСН(Форма);
	
	СписаниеСРасчетногоСчетаФормыКлиентСервер.УправлениеЭлементамиОтраженияВУСН(Форма);
	
КонецПроцедуры

Процедура ПрочееНДСУСНПриИзменении(Форма, Элемент) Экспорт
	ОграничитьНДСДляУСН(Форма);
КонецПроцедуры

Процедура СубконтоДтПрочиеПриИзменении(Форма, Элемент, НомерСубконто, Постфикс = "") Экспорт
	
	ПриИзмененииСубконто(Форма, НомерСубконто, Постфикс);
	
КонецПроцедуры

Процедура СубконтоДтПрочиеНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка, Постфикс = "") Экспорт
	
	НачалоВыбораСубконто(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка, Постфикс);
	
КонецПроцедуры

Процедура СчетУчетаЗадержкаЗарплатыПриИзменении(Форма, Элемент) Экспорт
	
	Объект = Форма.Объект;
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
		Форма, Форма.Объект, СписаниеСРасчетногоСчетаФормыКлиентСервер.ПараметрыУстановкиСвойствСубконто(Форма, "ЗадержкаЗарплаты"));
	
КонецПроцедуры

Процедура СубконтоДтЗадержкаЗарплатыПриИзменении(Форма, Элемент, НомерСубконто, Постфикс = "") Экспорт
	
	ПриИзмененииСубконто(Форма, НомерСубконто, Постфикс);
	
КонецПроцедуры

Процедура СубконтоДтЗадержкаЗарплатыНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка, Постфикс = "") Экспорт
	
	НачалоВыбораСубконто(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка, Постфикс);
	
КонецПроцедуры

Процедура СтатьяДвиженияДенежныхСредствПриИзменении(Форма, Элемент) Экспорт
	
	Объект = Форма.Объект;
	
	ТекущиеДанные = Новый Структура("СтатьяДвиженияДенежныхСредств, ВидПлатежаПоКредитамЗаймам",
		Объект.СтатьяДвиженияДенежныхСредств);
	Если ТекущиеДанные.СтатьяДвиженияДенежныхСредств <> Форма.СвойстваПлатежа.СтатьяДвиженияДенежныхСредств Тогда
		ПодсказкаОНастройкеСтатьяДДС(Форма, ТекущиеДанные);
		Форма.СвойстваПлатежа.СтатьяДвиженияДенежныхСредств = ТекущиеДанные.СтатьяДвиженияДенежныхСредств;
	КонецЕсли;
	
КонецПроцедуры

Процедура НалоговыйПериодОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Если ВыбранноеЗначение = Неопределено ИЛИ ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	ВидПериода = "Месяц";
	Если ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоТорговыйСбор(Форма.ВидНалога)
			ИЛИ ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоНалогУСН(Форма.ВидНалога)
			Или ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоНДФЛ_ИП(Форма.ВидНалога) Тогда
		ВидПериода = "Квартал";
	КонецЕсли;
	Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность." + ВидПериода);
	
	ВыбранныйПериод = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ВыбранноеЗначение, "КонецПериода", Объект.Дата);
	
	НалоговыйПериод = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Периодичность, ВыбранныйПериод);
	НалоговыйПериодСтрока
		= СписаниеСРасчетногоСчетаФормыКлиентСервер.ПредставлениеПериодаУплатыНалога(НалоговыйПериод, Форма.ВидНалога);
	
	Форма.НалоговыйПериодСтрока = НалоговыйПериодСтрока;
	
	Если Объект.НалоговыйПериод = НалоговыйПериод Тогда
		// Ничего не изменилось, выбор не нужно обрабатывать.
		СтандартнаяОбработка = Ложь;
	Иначе
		ВыбранноеЗначение        = НалоговыйПериодСтрока;
		Объект.НалоговыйПериод   = НалоговыйПериод;
	КонецЕсли;
	
КонецПроцедуры

Процедура НомерЧекаНПДНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Организация", Форма.Объект.Организация);
	
	ОткрытьФорму("Документ.ПоступлениеНаРасчетныйСчет.ФормаВыбора", ПараметрыОтбора, Форма);
	
КонецПроцедуры

Процедура НомерЧекаНПДАвтоПодбор(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрокаТекст = Текст;
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		ТекущаяСтрокаТекст = Объект.НомерЧекаНПД;
	КонецЕсли;
	
	ДанныеВыбора = ЧекиНПДВызовСервера.ДанныеВыбораЧековДляАннулирования(
		ТекущаяСтрокаТекст, Объект.Организация, "ПоступлениеНаРасчетныйСчет");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

Процедура РеестрУплатыНДФЛ(Форма, Команда) Экспорт
	
	Объект = Форма.Объект;
	
	ОчиститьСообщения();
	
	Если НЕ Объект.Проведен ИЛИ Форма.Модифицированность Тогда
		ТекстСообщения = НСтр("ru = 'Печать реестра уплаты НДФЛ возможна только после проведения документа.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраНДФЛ(Новый Структура("МенеджерПечати, Ссылка, Форма",
		"Документ.СписаниеСРасчетногоСчета", Объект.Ссылка, Форма));
	
КонецПроцедуры

#КонецОбласти

#Область РасшифровкаПлатежа

Процедура РасшифровкаПлатежаПриНачалеРедактирования(Форма, Элемент, НоваяСтрока, Копирование) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	СтрокаПлатеж = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если НоваяСтрока И Не Копирование Тогда
		ЗаполнитьЗначенияСвойств(СтрокаПлатеж, Форма.СвойстваПлатежа);
		Если Форма.ОтражениеДоходаВидимость Тогда
			СтрокаПлатеж.ОтражениеДоходаПредставление = БанкИКассаФормыКлиентСервер.ВариантОтраженияДоходовПредставление(
				СтрокаПлатеж.ПорядокОтраженияДохода,
				БанкИКассаФормыКлиентСервер.ВариантыОтраженияДоходов(Форма));
		КонецЕсли;
	КонецЕсли;
	
	Если НоваяСтрока И Форма.ЕстьРасчетыВУсловныхЕдиницах Тогда
		СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьНадписиВРасшифровке(СтрокаПлатеж);
	КонецЕсли;
	
КонецПроцедуры

Процедура РасшифровкаПлатежаПередУдалением(Форма, Элемент, Отказ) Экспорт
	
	Если СписаниеСРасчетногоСчетаФормыКлиентСервер.ФормаРасшифровкиПлатежа(Форма) Тогда
		РасшифровкаПлатежа = Форма.РасшифровкаПлатежа;
	Иначе
		РасшифровкаПлатежа = Форма.Объект.РасшифровкаПлатежа;
	КонецЕсли;
	
	Отказ = РасшифровкаПлатежа.Количество() = 1;
	
КонецПроцедуры

Процедура РасшифровкаПлатежаДоговорКонтрагентаОткрытие(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ОткрытьДоговорКонтрагента(Форма);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаДоговорКонтрагентаОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	ПараметрыДоговора = Новый Структура;
	ПараметрыДоговора.Вставить("Организация", Объект.Организация);
	ПараметрыДоговора.Вставить("Владелец", Объект.Контрагент);
	ПараметрыДоговора.Вставить("ВидДоговора", ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"));
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
КонецПроцедуры

Процедура РасшифровкаПлатежаДоговорКонтрагентаАвтоПодбор(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)Экспорт
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ЗаполнитьСписокВыбора(Элемент, Текст, Форма.ПредлагатьНовыйДоговор, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаСтатьяДвиженияДенежныхСредствПриИзменении(Форма, Элемент) Экспорт
	
	ТекущиеДанные = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	
	Если ТекущиеДанные.СтатьяДвиженияДенежныхСредств <> Форма.СвойстваПлатежа.СтатьяДвиженияДенежныхСредств Тогда
		СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, ТекущиеДанные);
		ПодсказкаОНастройкеСтатьяДДС(Форма, ТекущиеДанные);
		Форма.СвойстваПлатежа.СтатьяДвиженияДенежныхСредств = ТекущиеДанные.СтатьяДвиженияДенежныхСредств;
	КонецЕсли;
	
КонецПроцедуры

Процедура РасшифровкаПлатежаВидПлатежаПоКредитамЗаймамПриИзменении(Форма, Элемент) Экспорт
	
	Объект = Форма.Объект;
	
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	
	Если СтрокаПлатеж.ВидПлатежаПоКредитамЗаймам <> Форма.СвойстваПлатежа.ВидПлатежаПоКредитамЗаймам
		ИЛИ НЕ ЗначениеЗаполнено(СтрокаПлатеж.СтатьяДвиженияДенежныхСредств) Тогда
		
		Форма.СвойстваПлатежа.ВидПлатежаПоКредитамЗаймам = СтрокаПлатеж.ВидПлатежаПоКредитамЗаймам;
		
		Если СтрокаПлатеж.ВидПлатежаПоКредитамЗаймам = ПредопределенноеЗначение("Перечисление.ВидыПлатежейПоКредитамЗаймам.ПогашениеДолга") Тогда
			СтрокаПлатеж.СтатьяДвиженияДенежныхСредств =
				УчетДенежныхСредствВызовСервера.СтатьяДДСПоУмолчанию("ПогашениеДолга");
		ИначеЕсли СтрокаПлатеж.ВидПлатежаПоКредитамЗаймам = ПредопределенноеЗначение("Перечисление.ВидыПлатежейПоКредитамЗаймам.УплатаПроцентов") Тогда
			СтрокаПлатеж.СтатьяДвиженияДенежныхСредств =
				УчетДенежныхСредствВызовСервера.СтатьяДДСПоУмолчанию("УплатаПроцентов");
		КонецЕсли;
		
		Форма.СвойстваПлатежа.СтатьяДвиженияДенежныхСредств = СтрокаПлатеж.СтатьяДвиженияДенежныхСредств;
		
	КонецЕсли;
	
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьОтражениеСтрокиВУСН(Форма, СтрокаПлатеж);
	РасходыУСНПриИзменении(Форма, СтрокаПлатеж);
	
	ПараметрыЗаполненияСчетовУчета = СписаниеСРасчетногоСчетаФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"РасшифровкаПлатежа.ВидПлатежаПоКредитамЗаймам", Объект, СтрокаПлатеж);
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(ПараметрыЗаполненияСчетовУчета);
	
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, СтрокаПлатеж);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаСпособПогашенияЗадолженностиПриИзменении(Форма, Элемент) Экспорт
	
	Объект = Форма.Объект;
	
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	Если СтрокаПлатеж.СпособПогашенияЗадолженности <> ПредопределенноеЗначение("Перечисление.СпособыПогашенияЗадолженности.ПоДокументу") Тогда
		СтрокаПлатеж.Сделка = Неопределено;
	КонецЕсли;
	
	ПараметрыЗаполненияСчетовУчета = СписаниеСРасчетногоСчетаФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"РасшифровкаПлатежа.СпособПогашенияЗадолженности", Объект, СтрокаПлатеж);
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(ПараметрыЗаполненияСчетовУчета);
	
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, СтрокаПлатеж);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаСделкаНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	СтандартнаяОбработка = Ложь;
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю") Тогда
		СчетУчета = СтрокаПлатеж.СчетУчетаРасчетовПоАвансам;
	Иначе
		СчетУчета = СтрокаПлатеж.СчетУчетаРасчетовСКонтрагентом;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указана дата.'"));
		Возврат;
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указана организация.'"));
		Возврат;
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указан контрагент.'"));
		Возврат;
	ИначеЕсли Форма.ВестиУчетПоДоговорам И НЕ ЗначениеЗаполнено(СтрокаПлатеж.ДоговорКонтрагента) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указан договор.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("Дата",               Объект.Дата);
	ПараметрыОбъекта.Вставить("ДоговорКонтрагента", СтрокаПлатеж.ДоговорКонтрагента);
	ПараметрыОбъекта.Вставить("Контрагент",         Объект.Контрагент);
	ПараметрыОбъекта.Вставить("Организация",        Объект.Организация);
	ПараметрыОбъекта.Вставить("ОстаткиОбороты",     "Кт");
	ПараметрыОбъекта.Вставить("ТипыДокументов",     "Метаданные.Документы.СписаниеСРасчетногоСчета.ТабличныеЧасти.РасшифровкаПлатежа.Реквизиты.Сделка.Тип");
	Если ЗначениеЗаполнено(СчетУчета) Тогда
		ПараметрыОбъекта.Вставить("РежимОтбораДокументов", ПредопределенноеЗначение("Перечисление.РежимОтбораДокументов.ПоОстаткам"));
		ПараметрыОбъекта.Вставить("СчетУчета",             СчетУчета);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ПараметрыОбъекта", ПараметрыОбъекта);
	ОткрытьФорму("Документ.ДокументРасчетовСКонтрагентом.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаКурсВзаиморасчетовПриИзменении(Форма, Элемент) Экспорт
	
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	СписаниеСРасчетногоСчетаФормыКлиентСервер.РассчитатьСуммуВзаиморасчетов(Форма, СтрокаПлатеж);
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, СтрокаПлатеж);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаКурсВзаиморасчетовНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	Если НЕ ЗначениеЗаполнено(СтрокаПлатеж.ДоговорКонтрагента) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для выбора курса взаиморасчетов нужно выбрать договор контрагента.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыДокумента = Новый Структура("ДоговорКонтрагента, КурсВзаиморасчетов, КратностьВзаиморасчетов, Дата");
	ЗаполнитьЗначенияСвойств(ПараметрыДокумента, СтрокаПлатеж);
	ПараметрыДокумента.Дата = Объект.Дата;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтрокаПлатеж", СтрокаПлатеж);
	ДополнительныеПараметры.Вставить("Форма",        Форма);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("РасшифровкаПлатежаКурсВзаиморасчетовНачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Общаяформа.ФормаВводаКурсаИКратности", ПараметрыДокумента,,,,, ОповещениеОЗакрытии);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаСуммаВзаиморасчетовПриИзменении(Форма, Элемент) Экспорт
	
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьПустыеКурсыВалют(Форма, СтрокаПлатеж);
	
	Если СтрокаПлатеж.СуммаВзаиморасчетов <> 0 И Форма.КратностьДокумента <> 0 Тогда
		СуммаРуб = СтрокаПлатеж.СуммаПлатежа * Форма.КурсДокумента / Форма.КратностьДокумента;
		СтрокаПлатеж.КурсВзаиморасчетов = СуммаРуб / СтрокаПлатеж.СуммаВзаиморасчетов * СтрокаПлатеж.КратностьВзаиморасчетов;
	КонецЕсли;
	
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, СтрокаПлатеж);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаСтавкаНДСПриИзменении(Форма, Элемент) Экспорт
	
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ПересчитатьСуммуНДС(СтрокаПлатеж);
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, СтрокаПлатеж);
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ПоказатьПредупреждениеПечатьЧека(Форма);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаСчетУчетаРасчетовСКонтрагентомПриИзменении(Форма, Элемент) Экспорт
	
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьОтражениеСтрокиВУСН(Форма, СтрокаПлатеж);
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, СтрокаПлатеж);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаНДСУСНПриИзменении(Форма, Элемент) Экспорт
	
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	ОграничитьСуммуНДСДляУСНПоСтроке(СтрокаПлатеж);
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, СтрокаПлатеж);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаРасходыУСНПриИзменении(Форма, Элемент) Экспорт
	
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	
	РасходыУСНПриИзменении(Форма, СтрокаПлатеж);
	
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, СтрокаПлатеж);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаОтражениеДоходаПредставлениеПриИзменении(Форма, Элемент) Экспорт
	
	СтрокаПлатеж = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	
	БанкИКассаФормыКлиент.ОтражениеДоходаПриИзменении(Форма,
		СтрокаПлатеж.ПорядокОтраженияДохода,
		СтрокаПлатеж.ОтражениеДоходаПредставление,
		Форма.СвойстваПлатежа.ПорядокОтраженияДохода);
	
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, СтрокаПлатеж);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаОтражениеДоходаПредставлениеОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	БанкИКассаФормыКлиент.ОтражениеДоходаОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ИнициализироватьСвойстваПлатежа(Форма) Экспорт
	
	Если Форма.СвойстваПлатежа = Неопределено Тогда
		Форма.СвойстваПлатежа = СписаниеСРасчетногоСчетаФормыКлиентСервер.ПустаяСтруктураСвойстваПлатежа();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьДоговорКонтрагента(Форма)
	
	Объект = Форма.Объект;
	
	ТекущиеДанные = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	
	ЗначенияЗаполнения = Новый Структура;
	ВидыДоговора   = УчетДенежныхСредствКлиентСервер.ОпределитьВидДоговораСКонтрагентом(Объект.ВидОперации);
	ЗначенияЗаполнения.Вставить("Организация", Объект.Организация);
	ЗначенияЗаполнения.Вставить("Владелец", Объект.Контрагент);
	ЗначенияЗаполнения.Вставить("ВидДоговора", Новый ФиксированныйМассив(ВидыДоговора));
	ЗначенияЗаполнения.Вставить("ОплатаВВалюте", Форма.ОплатаВВалюте);
	Если Форма.ОплатаВВалюте ИЛИ НЕ СписаниеСРасчетногоСчетаФормыКлиентСервер.ДокументМожетБытьОформленВВалюте(Объект.ВидОперации) Тогда
		ЗначенияЗаполнения.Вставить("ВалютаВзаиморасчетов", Объект.ВалютаДокумента);
		ЗначенияЗаполнения.Вставить("Валютный", Форма.ОплатаВВалюте);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.ДоговорКонтрагента);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаОбъекта", ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура РасшифровкаПлатежаДоговорКонтрагентаПриИзменении(Форма, Элемент) Экспорт
	
	ТекущиеДанные = СписаниеСРасчетногоСчетаФормыКлиентСервер.ТекущиеДанныеРасшифровкаПлатежа(Форма);
	ПодсказкаОНастройкеДоговор(Форма, ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ПеречислениеЗаработнойПлаты

Процедура ПеречислениеЗаработнойПлатыПриНачалеРедактирования(Форма, Элемент, НоваяСтрока, Копирование) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если НоваяСтрока И НЕ Копирование Тогда
		
		ТекущиеДанные = Элементы.ПеречислениеЗаработнойПлаты.ТекущиеДанные;
		
		СписаниеСРасчетногоСчетаФормыКлиентСервер.УстановитьПустоеЗначениеПеречислениеЗаработнойПлатыВедомость(
			Форма, ТекущиеДанные.Ведомость);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеречислениеЗаработнойПлатыПриОкончанииРедактирования(Форма, Элемент, НоваяСтрока, ОтменаРедактирования) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.ПеречислениеЗаработнойПлаты.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущаяВедомость = ТекущиеДанные.Ведомость;
			Если ПроверитьДублиВедомостей(Форма, ТекущаяВедомость) Тогда
				СписаниеСРасчетногоСчетаФормыКлиентСервер.УстановитьПустоеЗначениеПеречислениеЗаработнойПлатыВедомость(
					Форма, ТекущиеДанные.Ведомость);
				ТекущиеДанные.СуммаПлатежа = 0;
				ОтменаРедактирования = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеречислениеЗаработнойПлатыВедомостьНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	Если Форма.УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",     Объект.Организация);
		ПараметрыФормы.Вставить("ВидМестаВыплаты", ПредопределенноеЗначение("Перечисление.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект"));
		ПараметрыФормы.Вставить("СпособРасчетов",  ПредопределенноеЗначение("Перечисление.СпособыРасчетовСФизическимиЛицами.ОплатаТруда"));
		ОткрытьФорму("Документ.ВедомостьНаВыплатуЗарплаты.ФормаВыбора", Новый Структура("ПараметрыОтбораСписка", ПараметрыФормы), Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеречислениеЗаработнойПлатыВедомостьОчистка(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	Элементы = Форма.Элементы;

	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ПеречислениеЗаработнойПлаты.ТекущиеДанные;
	СписаниеСРасчетногоСчетаФормыКлиентСервер.УстановитьПустоеЗначениеПеречислениеЗаработнойПлатыВедомость(
		Форма, ТекущиеДанные.Ведомость);
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

Процедура ПеречислениеЗаработнойПлатыВедомостьОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Если ПроверитьДублиВедомостей(Форма, ВыбранноеЗначение, "ПеречислениеЗаработнойПлаты") Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПеречислениеДепонентов

Процедура ПеречислениеДепонентовПриНачалеРедактирования(Форма, Элемент, НоваяСтрока, Копирование) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если НоваяСтрока И НЕ Копирование Тогда
	
		ТекущиеДанные = Элементы.ПеречислениеДепонентов.ТекущиеДанные;
		
		СписаниеСРасчетногоСчетаФормыКлиентСервер.УстановитьПустоеЗначениеПеречислениеДепонентовВедомость(
			Форма, ТекущиеДанные.Ведомость);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеречислениеДепонентовПриОкончанииРедактирования(Форма, Элемент, НоваяСтрока, ОтменаРедактирования) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.ПеречислениеДепонентов.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущаяВедомость = ТекущиеДанные.Ведомость;
			Если ПроверитьДублиВедомостей(Форма, ТекущаяВедомость, "ПеречислениеДепонентов") Тогда
				СписаниеСРасчетногоСчетаФормыКлиентСервер.УстановитьПустоеЗначениеПеречислениеДепонентовВедомость(
					Форма, ТекущиеДанные.Ведомость);
				ТекущиеДанные.СуммаПлатежа = 0;
				ОтменаРедактирования = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеречислениеДепонентовВедомостьНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	Если Форма.УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",     Объект.Организация);
		ПараметрыФормы.Вставить("ВидМестаВыплаты", ПредопределенноеЗначение("Перечисление.ВидыМестВыплатыЗарплаты.Касса"));
		ПараметрыФормы.Вставить("СпособРасчетов",  ПредопределенноеЗначение("Перечисление.СпособыРасчетовСФизическимиЛицами.ОплатаТруда"));
		
		ОткрытьФорму("Документ.ВедомостьНаВыплатуЗарплаты.ФормаВыбора", Новый Структура("ПараметрыОтбораСписка", ПараметрыФормы), Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеречислениеДепонентовВедомостьОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Если ПроверитьДублиВедомостей(Форма, ВыбранноеЗначение, "ПеречислениеДепонентов") Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов

Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыЗаписи = Новый Структура("Закрыть", Истина);
		Если Форма.Записать(ПараметрыЗаписи) Тогда
			Форма.Закрыть();
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Форма.Модифицированность = Ложь;
		Форма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВопросПередЗаписьюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	ИсходныеПараметрыЗаписи = ДополнительныеПараметры.ПараметрыЗаписи;
	Объект = Форма.Объект;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		РежимЗаписи = ?(Объект.Проведен, РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписиДокумента.Запись);
		ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписи);
		Форма.Записать(ПараметрыЗаписи);
		Если ИсходныеПараметрыЗаписи.Свойство("Закрыть") Тогда
			Форма.Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура РасшифровкаПлатежаКурсВзаиморасчетовНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	СтрокаПлатеж = ДополнительныеПараметры.СтрокаПлатеж;
	Форма        = ДополнительныеПараметры.Форма;
	
	СтруктураКурсаИКратности = РезультатЗакрытия;
	
	Если СтруктураКурсаИКратности <> Неопределено Тогда
		СтрокаПлатеж.КурсВзаиморасчетов      = СтруктураКурсаИКратности.КурсВалюты;
		СтрокаПлатеж.КратностьВзаиморасчетов = СтруктураКурсаИКратности.КратностьВалюты;
	КонецЕсли;
	
	СписаниеСРасчетногоСчетаФормыКлиентСервер.РассчитатьСуммуВзаиморасчетов(Форма, СтрокаПлатеж);
	СписаниеСРасчетногоСчетаФормыКлиентСервер.ЗаполнитьЗначенияТекущиеДанныеРасшифровкаПлатежа(Форма, СтрокаПлатеж);
	
	Форма.Модифицированность = Истина;
	Форма.ОбновитьОтображениеДанных();
	
КонецПроцедуры

Процедура ПодсказкаОНастройкеСтатьяДДСЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") И ЗначениеЗаполнено(РезультатВыполнения) Тогда
		СписаниеСРасчетногоСчетаФормыВызовСервера.УстановитьСтатьюДДСПоУмолчанию(РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодсказкаОНастройкеДоговорЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") И ЗначениеЗаполнено(РезультатВыполнения) Тогда
		СписаниеСРасчетногоСчетаФормыВызовСервера.УстановитьДоговорУмолчанию(РезультатВыполнения.Договор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодсказкаОНастройкеДоговор(Форма, ТекущиеДанные)
	
	Объект = Форма.Объект;
	
	Если Не СписаниеСРасчетногоСчетаФормыКлиентСервер.ФормаДокументаОднострочная(Форма)
		И СписаниеСРасчетногоСчетаФормыКлиентСервер.ЕстьРасшифровкаПлатежа(Форма) Тогда
		Если СписаниеСРасчетногоСчетаФормыКлиентСервер.ФормаРасшифровкиПлатежа(Форма) Тогда
			РасшифровкаПлатежа = Форма.РасшифровкаПлатежа;
		Иначе
			РасшифровкаПлатежа = Форма.Объект.РасшифровкаПлатежа;
		КонецЕсли;
		
		Если РасшифровкаПлатежа.Количество() > 1 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ДоговорКонтрагента) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПлатежа = Новый Структура("Организация, Контрагент, ДоговорКонтрагента, ВидОперации, ОплатаВВалюте, ВалютаДокумента",
		Объект.Организация,
		Объект.Контрагент,
		ТекущиеДанные.ДоговорКонтрагента,
		Объект.ВидОперации,
		Форма.ОплатаВВалюте,
		Объект.ВалютаДокумента);
	Если Не СписаниеСРасчетногоСчетаФормыВызовСервера.НастроитьОсновнойДоговор(ПараметрыПлатежа) Тогда
		// Настройка для этого элемента уже существует.
		Возврат;
	КонецЕсли;
	
	ПараметрыДоговор = Новый Структура;
	ПараметрыДоговор.Вставить("Настройка",    "Договор");
	ПараметрыДоговор.Вставить("Договор",      ТекущиеДанные.ДоговорКонтрагента);
	ПараметрыДоговор.Вставить("ВидОперации",  Объект.ВидОперации);
	ПараметрыДоговор.Вставить("ТипДокумента", Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
	ПараметрыНастройки = Новый Структура("ПараметрыНастройки", ПараметрыДоговор);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодсказкаОНастройкеДоговорЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ФормаПодсказкаОНастройке", ПараметрыНастройки, Форма,,,, ОписаниеОповещения);
	
КонецПроцедуры

Процедура ПодсказкаОНастройкеСтатьяДДС(Форма, ТекущиеДанные)
	
	Объект = Форма.Объект;
	
	ПредыдущееЗначение = Форма.СвойстваПлатежа.СтатьяДвиженияДенежныхСредств;
	
	Если Не СписаниеСРасчетногоСчетаФормыКлиентСервер.ФормаДокументаОднострочная(Форма)
		И СписаниеСРасчетногоСчетаФормыКлиентСервер.ЕстьРасшифровкаПлатежа(Форма) Тогда
		Если СписаниеСРасчетногоСчетаФормыКлиентСервер.ФормаРасшифровкиПлатежа(Форма) Тогда
			РасшифровкаПлатежа = Форма.РасшифровкаПлатежа;
		Иначе
			РасшифровкаПлатежа = Форма.Объект.РасшифровкаПлатежа;
		КонецЕсли;
		
		Если РасшифровкаПлатежа.Количество() > 1 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.СтатьяДвиженияДенежныхСредств)
		Или ЗначениеЗаполнено(ПредыдущееЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстОперации = Неопределено;
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога") Тогда
		Если ЗначениеЗаполнено(Объект.Налог)
			И УчетДенежныхСредствКлиентСервер.ЭтоУплатаНалога(Объект.ВидНалоговогоОбязательства) Тогда
			Если УчетДенежныхСредствКлиентСервер.ЭтоУплатаНалогаНаПрибыль(Форма.ВидНалога) Тогда
				КонтекстОперации = "НалогНаПрибыль";
			Иначе
				КонтекстОперации = Объект.ВидОперации;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли УчетДенежныхСредствКлиентСервер.ЭтоВозвратКредитаЗайма(Объект.ВидОперации) Тогда
		Если ТекущиеДанные.ВидПлатежаПоКредитамЗаймам = ПредопределенноеЗначение("Перечисление.ВидыПлатежейПоКредитамЗаймам.ПогашениеДолга") Тогда
			КонтекстОперации = "ПогашениеДолга";
		ИначеЕсли ТекущиеДанные.ВидПлатежаПоКредитамЗаймам = ПредопределенноеЗначение("Перечисление.ВидыПлатежейПоКредитамЗаймам.УплатаПроцентов") Тогда
			КонтекстОперации = "УплатаПроцентов";
		КонецЕсли;
	Иначе
		Если Объект.СчетУчетаРасчетовСКонтрагентом = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПрочиеРасходы") тогда
			// В большинстве случаев это услуги банка.
			КонтекстОперации = "КомиссияБанка";
		Иначе
			КонтекстОперации = Объект.ВидОперации;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КонтекстОперации) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не СписаниеСРасчетногоСчетаФормыВызовСервера.НастроитьСтатьяДДСДляОперации(
			ТекущиеДанные.СтатьяДвиженияДенежныхСредств, КонтекстОперации) Тогда
		// Настройка для этого элемента уже существует.
		Возврат;
	КонецЕсли;
	
	ПараметрыСтатьяДДС = Новый Структура;
	ПараметрыСтатьяДДС.Вставить("Настройка", "СтатьяДДС");
	ПараметрыСтатьяДДС.Вставить("ПредставлениеСтатьяДДС", НСтр("ru = 'Статью расходов'"));
	ПараметрыСтатьяДДС.Вставить("СтатьяДДС", ТекущиеДанные.СтатьяДвиженияДенежныхСредств);
	ПараметрыСтатьяДДС.Вставить("ВидОперации", Объект.ВидОперации);
	ПараметрыСтатьяДДС.Вставить("КонтекстОперации", КонтекстОперации);
	ПараметрыНастройки = Новый Структура("ПараметрыНастройки", ПараметрыСтатьяДДС);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодсказкаОНастройкеСтатьяДДСЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ФормаПодсказкаОНастройке", ПараметрыНастройки, Форма,,,, ОписаниеОповещения);
	
КонецПроцедуры

Функция ПроверитьДублиВедомостей(Форма, ТекущаяВедомость, ТабличнаяЧасть = "ПеречислениеЗаработнойПлаты")
	
	Элементы = Форма.Элементы;
	
	ЕстьДублиВедомостей = Ложь;
	
	Если СтрНайти(Форма.ИмяФормы, "ФормаДокумента") = 0 Тогда
		ДанныеТЧ = Форма[ТабличнаяЧасть];
	Иначе
		ДанныеТЧ = Форма.Объект[ТабличнаяЧасть];
	КонецЕсли;
	
	СтрокаПоиска    = Новый Структура("Ведомость", ТекущаяВедомость);
	НайденныеСтроки = ДанныеТЧ.НайтиСтроки(СтрокаПоиска);
	Если НайденныеСтроки.Количество() <> 0 Тогда
		Если НайденныеСтроки.Количество() = 1 Тогда
			Если Элементы[ТабличнаяЧасть].ТекущиеДанные.НомерСтроки <> НайденныеСтроки[0].НомерСтроки Тогда
				ЕстьДублиВедомостей = Истина;
			КонецЕсли;
		Иначе
			ЕстьДублиВедомостей = Истина;
		КонецЕсли;
		
		Если ЕстьДублиВедомостей Тогда
			ТекстСообщения = НСтр("ru = 'Указанная ведомость уже выбрана в документе!'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЕстьДублиВедомостей;
	
КонецФункции

Процедура ИзменениеПериодаУплаты(Форма, ПутьРеквизита, ПутьРеквизитаПредставления, Направление)
	
	Множитель = 1;
	Если ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоТорговыйСбор(Форма.ВидНалога)
			ИЛИ ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоНалогУСН(Форма.ВидНалога)
			Или ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоНДФЛ_ИП(Форма.ВидНалога) Тогда
		Множитель = 3;
	КонецЕсли;
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьРеквизита);
	Значение = ДобавитьМесяц(Значение, Множитель * Направление);
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, ПутьРеквизита, Значение);
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, ПутьРеквизитаПредставления,
		СписаниеСРасчетногоСчетаФормыКлиентСервер.ПредставлениеПериодаУплатыНалога(Значение, Форма.ВидНалога));
	
КонецПроцедуры

Процедура ПриИзмененииСубконто(Форма, НомерСубконто, Постфикс = "")
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСубконто(
		Форма, Форма.Объект, НомерСубконто, СписаниеСРасчетногоСчетаФормыКлиентСервер.ПараметрыУстановкиСвойствСубконто(Форма, Постфикс));
	
КонецПроцедуры

Процедура НачалоВыбораСубконто(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка, Постфикс = "")
	
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		Форма.Объект, СписаниеСРасчетногоСчетаФормыКлиентСервер.ПараметрыУстановкиСвойствСубконто(Форма, Постфикс));
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(Форма, Элемент, СтандартнаяОбработка, ДанныеОбъекта);
	
КонецПроцедуры

#КонецОбласти

#Область УСН

Процедура ОграничитьПринимаемыеРасходыДляУСН(Форма)
	
	Объект = Форма.Объект;
	
	МаксРасход = Объект.СуммаДокумента;
	Если Форма.ОплатаВВалюте Тогда
		МаксРасход = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаДокумента,
			Объект.ВалютаДокумента, Форма.ВалютаРегламентированногоУчета,
			Форма.КурсДокумента,      1,
			Форма.КратностьДокумента, 1);
	КонецЕсли;
	
	Объект.Графа7_УСН = Мин(Объект.Графа7_УСН, МаксРасход);
	
	Если Объект.Графа7_УСН = 0 Тогда
		Объект.РасходыЕНВД_УСН = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОграничитьПринимаемыеРасходыДляУСНПоСтроке(Форма, СтрокаПлатеж)
	
	Объект = Форма.Объект;

	МаксРасход = СтрокаПлатеж.СуммаПлатежа;
	
	Если Форма.ОплатаВВалюте Тогда
		МаксРасход = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(СтрокаПлатеж.СуммаПлатежа,
			Объект.ВалютаДокумента, Форма.ВалютаРегламентированногоУчета,
			Форма.КурсДокумента,      1,
			Форма.КратностьДокумента, 1);
	КонецЕсли;
	
	СтрокаПлатеж.РасходыУСН = Мин(СтрокаПлатеж.РасходыУСН, МаксРасход);
	
КонецПроцедуры

Процедура ОграничитьНДСДляУСН(Форма)
	
	Объект = Форма.Объект;
	
	Если Объект.НДС_УСН > Объект.Графа7_УСН Тогда
		Объект.НДС_УСН = Объект.Графа7_УСН;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОграничитьСуммуНДСДляУСНПоСтроке(СтрокаПлатеж)
	
	Если СтрокаПлатеж.НДСУСН > СтрокаПлатеж.РасходыУСН Тогда
		СтрокаПлатеж.НДСУСН = СтрокаПлатеж.РасходыУСН;
	КонецЕсли;
	
КонецПроцедуры

Процедура РасходыУСНПриИзменении(Форма, СтрокаПлатеж)
	
	ОграничитьПринимаемыеРасходыДляУСНПоСтроке(Форма, СтрокаПлатеж);
	ОграничитьСуммуНДСДляУСНПоСтроке(СтрокаПлатеж);
	
	Если СтрокаПлатеж.РасходыУСН = 0 Тогда
		СтрокаПлатеж.РаспределятьРасходыУСН = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПредупрежденияПриЗагрузкеВыписки

Процедура ОповеститьОбИзмененииПредупрежденияПриЗагрузкеВыписки(Документ) Экспорт
	
	Оповестить("ИзмененоПредупреждениеПриЗагрузкеВыписки", Неопределено, Документ);
	
КонецПроцедуры

#КонецОбласти
