#Область СлужебныйПрограммныйИнтерфейс

// Изменяет свойства элемента, устанавливаемые после редактирования фиксированных данных
//
// Параметры:
//  Форма            - ФормаКлиентскогоПриложения
//  ДанныеСправки    - ДанныеФормыСтруктура
//  ПутьКДанным      - Строка - путь к данным редактируемого элемента
//
Процедура ПриИзмененииФиксируемогоЗначения(Форма, ДанныеСправки, ПутьКДанным) Экспорт
	ДанныеСправки["Фикс" + ПутьКДанным] = Истина;
	СправкиНДФЛФормыКлиентСервер.УстановитьСвойстваЭлементовСФиксациейДанных(Форма, ДанныеСправки, Форма.ДокументПроведен);
	СправкиНДФЛФормыКлиентСервер.УстановитьИнфонадписьИсправления(Форма.ИнфоНадписьИсправления, ДанныеСправки, Форма.ДокументПроведен);
КонецПроцедуры	

// Изменяет свойства элементов удостоверения личности, устанавливаемые после редактирования фиксированных данных
//
// Параметры:
//  Форма            - ФормаКлиентскогоПриложения
//  ДанныеСправки    - ДанныеФормыСтруктура
//
Процедура ПриИзмененииУдостоверенияЛичности(Форма, ДанныеСправки) Экспорт
	ПриИзмененииФиксируемогоЗначения(Форма, ДанныеСправки, "ВидДокумента");
	ПриИзмененииФиксируемогоЗначения(Форма, ДанныеСправки, "СерияДокумента");
	ПриИзмененииФиксируемогоЗначения(Форма, ДанныеСправки, "НомерДокумента");
КонецПроцедуры	

// Изменяет свойства элемента гражданство, устанавливаемые после редактирования фиксированных данных
//
// Параметры:
//  Форма            - ФормаКлиентскогоПриложения
//  ДанныеСправки    - ДанныеФормыСтруктура
//
Процедура ГражданствоПриИзменении(Форма, ДанныеСправки) Экспорт
	ПриИзмененииФиксируемогоЗначения(Форма, ДанныеСправки, "Гражданство");
	СправкиНДФЛФормыКлиентСервер.УстановитьПризнакНаличияГражданства(Форма, ДанныеСправки);
КонецПроцедуры	

// Изменяет свойства элементов лиц без гражданства, устанавливаемые после редактирования фиксированных данных
//
// Параметры:
//  Форма            - ФормаКлиентскогоПриложения
//  ДанныеСправки    - ДанныеФормыСтруктура
//
Процедура ЛицБезГражданстваПриИзменении(Форма, ДанныеСправки) Экспорт
	Если Форма.ЛицоБезГражданства = 1 Тогда
		ДанныеСправки.Гражданство = ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
		Форма.Элементы.Гражданство.Доступность = Ложь;
	Иначе
		Форма.Элементы.Гражданство.Доступность = Истина;	
	КонецЕсли;
	
	ПриИзмененииФиксируемогоЗначения(Форма, ДанныеСправки, "Гражданство");	
КонецПроцедуры	

// Меняет измененные значения контролируемых полей на значения, полученные из учетных данных
//
// Параметры:
//  РедактируемыеДанные                  - ДанныеФормыСтруктура - структура аналогична реквизитам документа СправкиНДФЛ
//  СтарыеЗначенияКонтролируемыхПолей    - Соответствие содержит:
//    * ИТОГИ                       - Соответствие содержит:
//      ** Итоги                    - Соответствие см. УчетНДФЛ.ПоляИтоговСправки2НДФЛ
//    * СведенияОВычетах            - Соответствие содержит:
//      ** СведенияОВычетах          - Соответствие содержит:
//         *** КодВычета             - СправочникСсылка.ВидыВычетовНДФЛ
//         *** СуммаВычета           - Число
//    * СведенияОДоходах            - Соответствие содержит:
//      ** СведенияОДоходах         - Соответствие содержит:
//         *** СуммаДохода            - Число
//         *** МесяцНалоговогоПериода - Число
//         *** КодДохода              - СправочникСсылка.ВидыДоходовНДФЛ
//         *** КодВычета              - СправочникСсылка.ВидыВычетовНДФЛ
//         *** СуммаВычета            - Число
//    * Уведомление                 - Соответствие содержит:
//      ** Уведомление              - Соответствие содержит:
//         *** ДатаУведомленияАвансовыеПлатежи                - Дата
//         *** НомерУведомленияАвансовыеПлатежи               - Строка
//         *** КодНалоговогоОрганаУведомленияАвансовыеПлатежи - Строка
//    * УведомленияНОоПравеНаВычеты - Соответствие содержит:
//      ** УведомленияНОоПравеНаВычеты - Соответствие содержит:
//         *** ДатаУведомления                - Дата
//         *** НомерУведомления               - Строка
//         *** КодНалоговогоОрганаУведомления - Строка
//         *** ГруппаВычета                   - ПеречислениеСсылка.ГруппыВычетовПоНДФЛ
//  КонтролируемыеПоля                   - ФиксированнаяСтруктура с ключами:
//    * ИТОГИ                       - ФиксированныйМассив из Строк
//    * СведенияОВычетах            - ФиксированныйМассив из Строк
//    * СведенияОДоходах            - ФиксированныйМассив из Строк
//    * Уведомление                 - ФиксированныйМассив из Строк
//    * УведомленияНОоПравеНаВычеты - ФиксированныйМассив из Строк
//  Раздел                          - Строка
//
Процедура УстановитьСтарыеЗначенияКонтролируемыхПолей(РедактируемыеДанные, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, Раздел) Экспорт
	КонтролируемыеПоляРаздела = КонтролируемыеПоля[Раздел];
	
	Если РедактируемыеДанные = Неопределено Тогда
		СтарыеЗначенияКонтролируемыхПолейРаздела = Новый Соответствие;
		СтарыеЗначенияКонтролируемыхПолей.Вставить(Раздел, СтарыеЗначенияКонтролируемыхПолейРаздела);
		
		Для Каждого Поле Из КонтролируемыеПоляРаздела Цикл
			СтарыеЗначенияКонтролируемыхПолейРаздела.Вставить(Поле, Неопределено);		
		КонецЦикла;		
	Иначе			
		СтарыеЗначенияКонтролируемыхПолейРаздела = СтарыеЗначенияКонтролируемыхПолей.Получить(Раздел);
		Если СтарыеЗначенияКонтролируемыхПолейРаздела = Неопределено Тогда
			СтарыеЗначенияКонтролируемыхПолейРаздела = Новый Соответствие;
			СтарыеЗначенияКонтролируемыхПолей.Вставить(Раздел, СтарыеЗначенияКонтролируемыхПолейРаздела);
		КонецЕсли;
		
		Для Каждого Поле Из КонтролируемыеПоляРаздела Цикл
			СтарыеЗначенияКонтролируемыхПолейРаздела.Вставить(Поле, РедактируемыеДанные[Поле]);		
		КонецЦикла;		
	КонецЕсли;	
КонецПроцедуры	

// Запоминает старые значения редактируемых полей
// Параметры:
//  Форма                                - ФормаКлиентскогоПриложения
//  ДанныеСправки    - ДанныеФормыСтруктура, структура, аналогичная реквизитам документа СправкаНДФЛ
//  РедактируемыеДанные                  - ДанныеФормыСтруктура - структура аналогична реквизитам документа СправкиНДФЛ
//  СтарыеЗначенияКонтролируемыхПолей    - Соответствие содержит:
//    * ИТОГИ                       - Соответствие содержит:
//      ** Итоги                    - Соответствие см. УчетНДФЛ.ПоляИтоговСправки2НДФЛ
//    * СведенияОВычетах            - Соответствие содержит:
//      ** СведенияОВычетах          - Соответствие содержит:
//         *** КодВычета   - СправочникСсылка.ВидыВычетовНДФЛ
//         *** СуммаВычета - Число
//    * СведенияОДоходах            - Соответствие содержит:
//      ** СведенияОДоходах         - Соответствие содержит:
//         *** СуммаДохода            - Число
//         *** МесяцНалоговогоПериода - Число
//         *** КодДохода              - СправочникСсылка.ВидыДоходовНДФЛ
//         *** КодВычета              - СправочникСсылка.ВидыВычетовНДФЛ
//         *** СуммаВычета            - Число
//    * Уведомление                 - Соответствие содержит:
//      ** Уведомление              - Соответствие содержит:
//         *** ДатаУведомленияАвансовыеПлатежи                - Дата
//         *** НомерУведомленияАвансовыеПлатежи               - Строка
//         *** КодНалоговогоОрганаУведомленияАвансовыеПлатежи - Строка
//    * УведомленияНОоПравеНаВычеты - Соответствие содержит:
//      ** УведомленияНОоПравеНаВычеты - Соответствие содержит:
//         *** ДатаУведомления                - Дата
//         *** НомерУведомления               - Строка
//         *** КодНалоговогоОрганаУведомления - Строка
//         *** ГруппаВычета                   - ПеречислениеСсылка.ГруппыВычетовПоНДФЛ
//  КонтролируемыеПоля                   - ФиксированнаяСтруктура с ключами:
//    * ИТОГИ                       - ФиксированныйМассив из Строк
//    * СведенияОВычетах            - ФиксированныйМассив из Строк
//    * СведенияОДоходах            - ФиксированныйМассив из Строк
//    * Уведомление                 - ФиксированныйМассив из Строк
//    * УведомленияНОоПравеНаВычеты - ФиксированныйМассив из Строк
//  Раздел                          - Строка
//
Процедура ПриОкончанииРедактирования(Форма, ДанныеСправки, РедактируемыеДанные, СтарыеЗначенияКонтролируемыхПолей, КонтролируемыеПоля, Раздел) Экспорт
	Если РедактируемыеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	КонтролируемыеПоляРаздела = КонтролируемыеПоля[Раздел];	
	СтарыеЗначенияКонтролируемыхПолейРаздела = СтарыеЗначенияКонтролируемыхПолей[Раздел];
	
	Для Каждого Поле Из КонтролируемыеПоляРаздела Цикл
		Если РедактируемыеДанные[Поле] <> СтарыеЗначенияКонтролируемыхПолейРаздела[Поле] Тогда
			Если Раздел = "Уведомление" Тогда
				ДанныеСправки.ФиксУведомление = Истина	
			Иначе	
				ДанныеСправки.ФиксНалоги = Истина;
			КонецЕсли;	
		КонецЕсли;	
		СтарыеЗначенияКонтролируемыхПолейРаздела.Вставить(Поле, РедактируемыеДанные[Поле]);
	КонецЦикла;	
КонецПроцедуры	

// Открывает форму справочника "Регистрации в налоговом органе"
//
// Параметры:
//  Форма                - ФормаКлиентскогоПриложения
//  СтандартнаяОбработка - Булево
//
Процедура РегистрацияВНалоговомОрганеОткрытие(Форма, СтандартнаяОбработка) Экспорт
	Если ЗначениеЗаполнено(Форма.Объект.РегистрацияВНалоговомОргане) Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОткрытия = Новый Структура("Ключ", Форма.Объект.РегистрацияВНалоговомОргане);
		
		ОткрытьФорму("Справочник.РегистрацииВНалоговомОргане.ФормаОбъекта", ПараметрыОткрытия);
	КонецЕсли;	
КонецПроцедуры	

#КонецОбласти