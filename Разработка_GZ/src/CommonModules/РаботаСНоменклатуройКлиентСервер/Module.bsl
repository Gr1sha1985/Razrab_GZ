#Область ПрограммныйИнтерфейс

// Функция возвращает содержание услуги с учетом периодичности.
// Например, "Сопровождение 1С за сентябрь 2016"
//
// Параметры:
//    НаименованиеПолное - Строка - Полное наименование номенклатуры.
//    Периодичность - Перечисление.Периодичность - Поддерживаются месяц, квартал и год.
//    Дата - Дата - Дата для формирования содержания.
//
Функция СодержаниеУслуги(Знач НаименованиеПолное, Знач ПериодичностьУслуги, Знач Дата, Знач УказыватьГод = Истина) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПериодичностьУслуги) ИЛИ ПустаяСтрока(НаименованиеПолное) Тогда
		Возврат НаименованиеПолное;
	КонецЕсли;
	
	ШаблонСтроки = НСтр("ru='за %1'");
	ЧастиСодержания = Новый Массив;
	ЧастиСодержания.Добавить(НаименованиеПолное);
	
	ФорматДаты = "";
	Если ПериодичностьУслуги = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		ФорматДаты = "MMMM %1";
	ИначеЕсли ПериодичностьУслуги = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		ФорматДаты = "к ""квартал"" %1";
	ИначеЕсли ПериодичностьУслуги = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		ФорматДаты = "%1";
	КонецЕсли;
	
	ФорматДаты = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ФорматДаты, ?(УказыватьГод, "yyyy ""г.""", ""));
	
	Если НЕ ПустаяСтрока(ФорматДаты) Тогда
		ЧастиСодержания.Добавить(СтрШаблон(ШаблонСтроки, НРег(Формат(Дата, "Л=ru_RU; ДФ='"+ФорматДаты+"'"))));
	КонецЕсли; 
	
	Возврат СтрСоединить(ЧастиСодержания, " ");
	
КонецФункции

#КонецОбласти