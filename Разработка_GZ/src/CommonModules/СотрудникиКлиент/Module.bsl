////////////////////////////////////////////////////////////////////////////////
// СотрудникиКлиент: методы, обслуживающие работу формы сотрудника.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Выводит модальное окно, содержащее пояснение к предупреждающим полям.
Процедура ПояснениеНажатие(Элемент, СтандартнаяОбработка = Ложь) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Подсказка = Элемент.Подсказка;
	
	Если Не ПустаяСтрока(Элемент.Заголовок) И Элемент.Заголовок <> "Ошибка" И Элемент.Заголовок <> "Почему?" И Элемент.Заголовок <> Подсказка Тогда
	
		Подсказка = СтрПолучитьСтроку(Элемент.Заголовок, 1) + Символы.ПС + Подсказка;
	
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Подсказка) Тогда
		ПоказатьПредупреждение(, Подсказка);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПроверитьКонфликтыВидаЗанятостиССуществующимиСотрудниками(Сотрудник, ФизическоеЛицо, Организация, ВидЗанятости, ДатаПриема) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Сотрудник)
		И ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		
		ТекстСообщенияОКонфликте = СотрудникиВызовСервера.СообщениеОКонфликтеВидаЗанятостиНовогоСотрудникаССуществующими(Сотрудник, ФизическоеЛицо, Организация, ВидЗанятости, ДатаПриема);
		Если Не ПустаяСтрока(ТекстСообщенияОКонфликте) Тогда
			ПоказатьПредупреждение(, ТекстСообщенияОКонфликте, , НСтр("ru = 'Внимание'"));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру параметров формы ФормыФизическиеЛицаСПохожимиДанными, справочника ФизическиеЛица.
// Ключи параметров Фамилия, Имя, Отчество, ФизическоеЛицоУникально, ДанныеФизическихЛиц, ДанныеФизическихЛицДоступны,
// ВозможнаПроверкаПоИНН и ВозможнаПроверкаПоСНИЛС заполняются из параметра РезультатыПроверки.
//
// Параметры:
//	РезультатыПроверки	- Структура, содержит результаты проверки уникальности физического лица
//							см. СотрудникиФормы.ПодобратьСписокФизЛиц
// 
// Возвращаемое значение:
//	Структура	- Содержит параметры открытия формы:
//					* ЗаголовокФормы				- Строка
//					* ТекстИнформационнойНадписи	- Строка
//					* Фамилия						- Строка
//					* Имя							- Строка
//					* Отчество						- Строка
//					* ФизическоеЛицоУникально		- Булево
//					* ДанныеФизическихЛиц			- Массив
//					* ДанныеФизическихЛицДоступны	- Булево
//					* ВозможнаПроверкаПоИНН			- Булево
//					* ВозможнаПроверкаПоСНИЛС		- Булево
//					* СкрытьКомандуДругойЧеловек	- Булево
//					* УчитыватьОтрицательныйВыбор	- Булево
//
Функция ПараметрыОткрытияФормыФизическиеЛицаСПохожимиДанными(РезультатыПроверки) Экспорт
	
	ПараметрыОткрытия = Новый Структура(
		"ЗаголовокФормы,
		|ТекстИнформационнойНадписи,
		|Фамилия,
		|Имя,
		|Отчество,
		|ФизическоеЛицоУникально,
		|ДанныеФизическихЛиц,
		|ДанныеФизическихЛицДоступны,
		|ВозможнаПроверкаПоИНН,
		|ВозможнаПроверкаПоСНИЛС,
		|СкрытьКомандуДругойЧеловек,
		|УчитыватьОтрицательныйВыбор,
		|КлючНазначенияИспользования");
	
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, РезультатыПроверки);
	ПараметрыОткрытия.СкрытьКомандуДругойЧеловек = Ложь;
	ПараметрыОткрытия.УчитыватьОтрицательныйВыбор = Ложь;
	ПараметрыОткрытия.КлючНазначенияИспользования = ?(
		ПараметрыОткрытия.ДанныеФизическихЛиц.Количество() < 2, "БезСписка", "СоСписком");
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

// Стартует длительную операцию по заполнению панели "История работы" в форме, связанной с физическими лицами.
// 
// Параметры:
// 	Форма - УправляемаяФорма
// 	ФизическиеЛица - Массив из СправочникСсылка.ФизическиеЛица.
//
Процедура НачатьЗаполнениеИсторииВзаимоотношений(Форма, ФизическиеЛица) Экспорт
	
	УстановитьСостояниеОжиданияЗаполненияИсторииВзаимоотношений(Форма, Истина);
	
	ДлительнаяОперация = СотрудникиВызовСервера.ДлительнаяОперацияЗаполненияИсторииВзаимоотношенийНаСервере(
		Форма.УникальныйИдентификатор, 
		ФизическиеЛица);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("Подключаемый_ПослеЗавершенияПолученияИсторииВзаимоотношений", Форма);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

// Инициирует открытие формы для редактирования места рождения физического лица.
//
// Параметры:
//  Форма				 - УправляемаяФорма					 - форма, в которой редактируются данные физического лица.
//  Элемент				 - ПолеФормы						 - поле ввода, в котором размещается представление места рождения.
//  СтандартнаяОбработка - Булево							 - признак стандартной обработки, который будет установлен в Ложь.
//  МестоРождения		 - Строка							 - данные места рождения, которые будет отредактированы.
//  ОповещениеЗавершения - ОписаниеОповещения, Неопределено	 - необязательный, если указано,
//  	будет выполнен после завершения редактирования, в качестве параметра передается признак Булево:
//  	Истина, если место рождения изменено, Ложь - в противном случае.
//
Процедура НачатьРедактированиеМестаРожденияФизическогоЛица(
	Форма, Элемент, СтандартнаяОбработка, МестоРождения, ОповещениеЗавершения = Неопределено) Экспорт	
	ФизическиеЛицаМестоРожденияНачалоВыбора(Форма, Элемент, СтандартнаяОбработка, МестоРождения, ОповещениеЗавершения);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОткрытьФормуСогласиеНаОбработкуПерсональныхДанных(ПараметрыПечати) Экспорт
	
	Если ПараметрыПечати.ОбъектыПечати.Количество() > 0 Тогда
		
		ПараметрыОткрытия = СотрудникиВызовСервера.ПараметрыОткрытияСогласияНаОбработкуПерсональныхДанных(ПараметрыПечати.ОбъектыПечати[0]);
		ОткрытьФорму("Документ.СогласиеНаОбработкуПерсональныхДанных.ФормаОбъекта", ПараметрыОткрытия);
		
	КонецЕсли;
	
КонецФункции

Процедура ОформитьНаОсновании(Форма, СотрудникСсылка, ИмяКоманды, ПараметрыОткрытияФормы = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(СотрудникСсылка) Тогда 
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("СотрудникСсылка", СотрудникСсылка);
	ДополнительныеПараметры.Вставить("ИмяКоманды", ИмяКоманды);
	ДополнительныеПараметры.Вставить("ЗаписатьДанные", Истина);
	ДополнительныеПараметры.Вставить("ПараметрыОткрытияФормы", ПараметрыОткрытияФормы);
	
	// Если данные сотрудника еще не записаны, предложим записать.
	Если Форма.Модифицированность Или Форма.Параметры.Свойство("Ключ") И Форма.Параметры.Ключ.Пустая() Тогда
		
		ОформитьНаОснованииПродолжение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
		
	Иначе 
		
		ДополнительныеПараметры.ЗаписатьДанные = Ложь;
		ОформитьНаОснованииЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОформитьНаОснованииПродолжение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если ДополнительныеПараметры.ЗаписатьДанные Тогда
		Оповещение = Новый ОписаниеОповещения("ОформитьНаОснованииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		Форма.ЗаписатьНаКлиенте(Ложь, Оповещение);
	Иначе 
		ПараметрыЗаписи = Новый Структура;
		ОформитьНаОснованииЗавершение(ПараметрыЗаписи, ДополнительныеПараметры);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОформитьНаОснованииЗавершение(ПараметрыЗаписи, ДополнительныеПараметры) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	СотрудникСсылка = ДополнительныеПараметры.СотрудникСсылка;
	ИмяКоманды = ДополнительныеПараметры.ИмяКоманды;
	
	Если ДополнительныеПараметры.ЗаписатьДанные И Не Форма.Записать(ПараметрыЗаписи) Тогда
		Возврат;
	КонецЕсли; 
	
	УстановитьДополнительныйПараметрПоИмениКомандыВводаНаОсновании(ИмяКоманды, ДополнительныеПараметры);
	
	ПолноеИмяОбъекта = ПолноеИмяОбъектаМетаданныхПоИмениКомандыВводаНаОсновании(ИмяКоманды);
	
	Если СтрНачинаетсяС(ПолноеИмяОбъекта, "Документы.") Тогда
		ПутьКФормеОбъекта = СтрЗаменить(ПолноеИмяОбъекта, "Документы.", "Документ.") + ".ФормаОбъекта";
	Иначе
		ПутьКФормеОбъекта = СтрЗаменить(ПолноеИмяОбъекта, "Справочники.", "Справочник.") + ".ФормаОбъекта";
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	Если ДополнительныеПараметры.Свойство("ПараметрыОткрытияФормы")
		И ЗначениеЗаполнено(ДополнительныеПараметры.ПараметрыОткрытияФормы) Тогда
		ПараметрыОткрытияФормы = ДополнительныеПараметры.ПараметрыОткрытияФормы;
		Для каждого КлючИЗначение Из ПараметрыОткрытияФормы Цикл
			ПараметрыФормы.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Основание", СотрудникСсылка);
	ОткрытьФорму(ПутьКФормеОбъекта, ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура УстановитьДополнительныйПараметрПоИмениКомандыВводаНаОсновании(ИмяКоманды, ДополнительныеПараметры)
	
	НомерСимвола = СтрНайти(ИмяКоманды, "__");
	Если НомерСимвола > 0 Тогда
		
		ИдентификаторВидаДокументаСтрока = Прав(ИмяКоманды, 36);
		ИмяКоманды = Лев(ИмяКоманды, НомерСимвола - 1);
		
		Если ДополнительныеПараметры.ПараметрыОткрытияФормы = Неопределено Тогда
			ДополнительныеПараметры.ПараметрыОткрытияФормы = Новый Структура;
		КонецЕсли;
		ДополнительныеПараметры.ПараметрыОткрытияФормы.Вставить("ДополнительныйПараметр", Новый УникальныйИдентификатор(СтрЗаменить(ИдентификаторВидаДокументаСтрока, "_", "-")));
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает полное имя объекта метаданных по имени команды ввода на основании.
//
Функция ПолноеИмяОбъектаМетаданныхПоИмениКомандыВводаНаОсновании(ИмяКоманды)
	
	ПолноеИмяОбъектаМетаданных = СтрЗаменить(ИмяКоманды, "КомандаВводаНаОсновании_", "");
	ПозицияПодчеркивания = СтрНайти(ПолноеИмяОбъектаМетаданных, "_");
	
	Если ПозицияПодчеркивания = 0 Тогда
		Возврат ПолноеИмяОбъектаМетаданных;
	КонецЕсли;
	
	Возврат Лев(ПолноеИмяОбъектаМетаданных, ПозицияПодчеркивания - 1) + "." + Сред(ПолноеИмяОбъектаМетаданных, ПозицияПодчеркивания + 1);
	
КонецФункции

#Область ОбработчикиСобытийФормыСотрудника

Процедура СотрудникиПриОткрытии(Форма) Экспорт
	
	Если Форма.Параметры.Ключ.Пустая()
		И НЕ ЗначениеЗаполнено(Форма.ФизическоеЛицо.Ссылка)
		И НЕ ПустаяСтрока(Форма.ФизическоеЛицо.ФИО) Тогда

		ПриИзмененииФИОСотрудника(Форма);

	КонецЕсли; 
	
КонецПроцедуры

Процедура СотрудникиПриЗакрытии(Форма) Экспорт
	
	Если Форма.СозданиеНового И НЕ Форма.Параметры.Ключ.Пустая() Тогда
		Оповестить("СозданСотрудник", Форма.СотрудникСсылка, Форма.ВладелецФормы);
	КонецЕсли; 
	
КонецПроцедуры

Процедура СотрудникиПослеЗаписи(Форма, ПараметрыЗаписи) Экспорт
	
	ЛичныеДанныеФизическогоЛицаПослеЗаписи(Форма, Форма.ФизическоеЛицоСсылка);
	ОповеститьОбИзмененииДанныхФизическогоЛица(Форма, Форма.ФизическоеЛицо);
	
	// Оповестить об изменении физического лица при записи из формы Сотрудника.
	ОповеститьОбИзменении(Форма.ФизическоеЛицоСсылка);
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ФизическоеЛицо", Форма.ФизическоеЛицо.Ссылка);
	ПараметрыОповещения.Вставить("Сотрудник", Форма.Сотрудник.Ссылка);
	
	Если Не Форма.ИспользоватьКадровыйУчет Тогда
		Оповестить("ИзменениеДанныхМестаРаботы", ПараметрыОповещения, Форма);
	КонецЕсли;
	
	Оповестить("Запись_Сотрудники", ПараметрыОповещения);
	
	Форма.ВыполненаКомандаСменыФИО = Ложь;
	Форма.Элементы.ФИО.ТолькоПросмотр = Ложь;
	
КонецПроцедуры

Процедура СотрудникиОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	СотрудникиКлиентВнутренний.СотрудникиОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

Процедура СотрудникиПередЗаписью(Форма, Отказ, ПараметрыЗаписи, ОповещениеЗавершения = Неопределено, ЗакрытьПослеЗаписи = Истина) Экспорт
	
	СотрудникиКлиентВнутренний.СотрудникиПередЗаписью(Форма, Отказ, ПараметрыЗаписи, ОповещениеЗавершения, ЗакрытьПослеЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормыФизическогоЛица

Процедура ФизическиеЛицаПриОткрытии(Форма) Экспорт
	
	Если Форма.Параметры.Ключ.Пустая()
		И НЕ ПустаяСтрока(Форма.ФизическоеЛицо.ФИО) Тогда
		ПриИзмененииФИОФизическогоЛица(Форма);
	КонецЕсли; 

КонецПроцедуры

Процедура ФизическиеЛицаОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	СотрудникиКлиентВнутренний.ФизическиеЛицаОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

Процедура ФизическиеЛицаПередЗаписью(Форма, Отказ, ПараметрыЗаписи, ОповещениеЗавершения = Неопределено, ЗакрытьПослеЗаписи = Истина) Экспорт
	
	СотрудникиКлиентВнутренний.ФизическиеЛицаПередЗаписью(Форма, Отказ, ПараметрыЗаписи, ОповещениеЗавершения, ЗакрытьПослеЗаписи);
	
КонецПроцедуры

Процедура ФизическиеЛицаПослеЗаписи(Форма, ПараметрыЗаписи) Экспорт

    ЛичныеДанныеФизическогоЛицаПослеЗаписи(Форма, Форма.ФизическоеЛицоСсылка);

    ФизическоеЛицо = Форма.ФизическоеЛицо;
    ОповеститьОбИзмененииДанныхФизическогоЛица(Форма, ФизическоеЛицо);

    Форма.ВыполненаКомандаСменыФИО = Ложь;
    Форма.Элементы.ФИО.ТолькоПросмотр = Ложь;

    ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ПроверитьПараметрыПодключенияК1СОтчетности(Форма.ФизическоеЛицоСсылка, Форма);

КонецПроцедуры

Процедура ФизическиеЛицаСтраницыПриСменеСтраницы(Форма, Элемент, ТекущаяСтраница) Экспорт
	ПриПереходеНаСтраницуИстория(Форма, Элемент, ТекущаяСтраница);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедуры

// Оповещает о событии "ИзменениеДанныхФизическогоЛица". Используется для обновления данных физических лиц,
// хранящихся в документах и редактируемых в формах этих документов. Кроме форм документов ЗК, обрабатывается
// в формах БП.
//
// Параметры:
//	Форма			- ФормаКлиентскогоПриложения
//	ФизическоеЛицо	- СправочникОбъект.ФизическиеЛица
//
Процедура ОповеститьОбИзмененииДанныхФизическогоЛица(Форма, ФизическоеЛицо)
	
	ДанныеФизическогоЛица = Новый Структура;
	ДанныеФизическогоЛица.Вставить("ФизическоеЛицо", ФизическоеЛицо.Ссылка);
	ДанныеФизическогоЛица.Вставить("Фамилия", "");
	ДанныеФизическогоЛица.Вставить("Имя", "");
	ДанныеФизическогоЛица.Вставить("Отчество", "");
	ДанныеФизическогоЛица.Вставить("Пол", ФизическоеЛицо.Пол);
	ДанныеФизическогоЛица.Вставить("ДатаРождения", ФизическоеЛицо.ДатаРождения);
	ДанныеФизическогоЛица.Вставить("МестоРождения", ФизическоеЛицо.МестоРождения);
	ДанныеФизическогоЛица.Вставить("МестоРожденияПредставление", "");
	ДанныеФизическогоЛица.Вставить("Гражданство", "");
	ДанныеФизическогоЛица.Вставить("СтраховойНомерПФР", ФизическоеЛицо.СтраховойНомерПФР);
	ДанныеФизическогоЛица.Вставить("ИНН", ФизическоеЛицо.ИНН);
	ДанныеФизическогоЛица.Вставить("ВидДокумента");
	ДанныеФизическогоЛица.Вставить("Серия", "");
	ДанныеФизическогоЛица.Вставить("Номер", "");
	ДанныеФизическогоЛица.Вставить("КемВыдан", "");
	ДанныеФизическогоЛица.Вставить("ДатаВыдачи", '00010101');
	ДанныеФизическогоЛица.Вставить("КодПодразделения", "");
	ДанныеФизическогоЛица.Вставить("ПредставлениеДокумента", "");
	ДанныеФизическогоЛица.Вставить("АдресРегистрации", "");
	ДанныеФизическогоЛица.Вставить("АдресРегистрацииПредставление", "");
	ДанныеФизическогоЛица.Вставить("АдресФактический", "");
	ДанныеФизическогоЛица.Вставить("АдресФактическийПредставление", "");
	ДанныеФизическогоЛица.Вставить("АдресДляИнформирования", "");
	ДанныеФизическогоЛица.Вставить("АдресДляИнформированияПредставление", "");
	ДанныеФизическогоЛица.Вставить("ИностранныйАдрес", "");
	ДанныеФизическогоЛица.Вставить("Телефоны", "");
	ДанныеФизическогоЛица.Вставить("ТелефонРабочий", "");
										
	Если Форма.ДоступенПросмотрДанныхФизическихЛиц Тогда
											
		ДанныеФизическогоЛица.Фамилия 		= Форма.ФИОФизическихЛиц.Фамилия;
		ДанныеФизическогоЛица.Имя 			= Форма.ФИОФизическихЛиц.Имя;
		ДанныеФизическогоЛица.Отчество 		= Форма.ФИОФизическихЛиц.Отчество;
		ДанныеФизическогоЛица.Гражданство 	= Форма.ГражданствоФизическихЛиц.Страна;

		ДанныеФизическогоЛица.ВидДокумента 	= Форма.ДокументыФизическихЛиц.ВидДокумента;
		ДанныеФизическогоЛица.Серия 		= Форма.ДокументыФизическихЛиц.Серия;
		ДанныеФизическогоЛица.Номер 		= Форма.ДокументыФизическихЛиц.Номер;
		ДанныеФизическогоЛица.КемВыдан 		= Форма.ДокументыФизическихЛиц.КемВыдан;
		ДанныеФизическогоЛица.ДатаВыдачи 	= Форма.ДокументыФизическихЛиц.ДатаВыдачи;
		ДанныеФизическогоЛица.КодПодразделения 	= Форма.ДокументыФизическихЛиц.КодПодразделения;
		ДанныеФизическогоЛица.ПредставлениеДокумента 	= Форма.ДокументыФизическихЛиц.Представление;
		
	КонецЕсли;
										
	СтруктураОтбора = Новый Структура("Вид", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица"));
	
	СтрокиАдреса = ФизическоеЛицо.КонтактнаяИнформация.НайтиСтроки(СтруктураОтбора);
	
	Если СтрокиАдреса.Количество() > 0 Тогда
		ДанныеФизическогоЛица.АдресРегистрации = СтрокиАдреса[0].ЗначенияПолей;
		ДанныеФизическогоЛица.АдресРегистрацииПредставление = СтрокиАдреса[0].Представление;	
	КонецЕсли;	
	
	СтруктураОтбора = Новый Структура("Вид", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица"));
	
	СтрокиАдреса = ФизическоеЛицо.КонтактнаяИнформация.НайтиСтроки(СтруктураОтбора);
	
	Если СтрокиАдреса.Количество() > 0 Тогда
		ДанныеФизическогоЛица.АдресФактический = СтрокиАдреса[0].ЗначенияПолей;
		ДанныеФизическогоЛица.АдресФактическийПредставление = СтрокиАдреса[0].Представление;	
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Вид", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресДляИнформированияФизическиеЛица"));
	
	СтрокиАдреса = ФизическоеЛицо.КонтактнаяИнформация.НайтиСтроки(СтруктураОтбора);
	
	Если СтрокиАдреса.Количество() > 0 Тогда
		ДанныеФизическогоЛица.АдресДляИнформирования = СтрокиАдреса[0].ЗначенияПолей;
		ДанныеФизическогоЛица.АдресДляИнформированияПредставление = СтрокиАдреса[0].Представление;	
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Вид", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонДомашнийФизическиеЛица"));

	СтрокиАдреса = ФизическоеЛицо.КонтактнаяИнформация.НайтиСтроки(СтруктураОтбора);
	
	Если СтрокиАдреса.Количество() > 0 Тогда
		ДанныеФизическогоЛица.Телефоны = СтрокиАдреса[0].Представление;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Вид", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица"));

	СтрокиАдреса = ФизическоеЛицо.КонтактнаяИнформация.НайтиСтроки(СтруктураОтбора);
	
	Если СтрокиАдреса.Количество() > 0 Тогда
		ДанныеФизическогоЛица.ТелефонРабочий = СтрокиАдреса[0].Представление;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Вид", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресЗаПределамиРФФизическиеЛица"));

	СтрокиАдреса = ФизическоеЛицо.КонтактнаяИнформация.НайтиСтроки(СтруктураОтбора);
	
	Если СтрокиАдреса.Количество() > 0 Тогда
		ДанныеФизическогоЛица.ИностранныйАдрес = СтрокиАдреса[0].Представление;
	КонецЕсли;

	ДанныеФизическогоЛица.МестоРожденияПредставление = ПерсонифицированныйУчетКлиентСервер.ПредставлениеМестаРождения(ФизическоеЛицо.МестоРождения);

	Оповестить("ИзменениеДанныхФизическогоЛица", ДанныеФизическогоЛица, ФизическоеЛицо.Ссылка);
	
	Оповестить("Запись_ФизическиеЛица", , ФизическоеЛицо.Ссылка);
	
КонецПроцедуры

Функция ЗаблокироватьФизическоеЛицоПриРедактировании(Форма, СообщатьОНевозможностиБлокировки = Истина, ИзФормыСотрудника = Истина) Экспорт
	
	Если НЕ Форма.ФизическоеЛицоЗаблокировано Тогда
		
		Если НЕ СотрудникиВызовСервера.ЗаблокироватьФизическоеЛицоПриРедактированииНаСервере(Форма.ФизическоеЛицоСсылка, Форма.ФизическоеЛицоВерсияДанных, Форма.УникальныйИдентификатор) Тогда
			
			Если СообщатьОНевозможностиБлокировки Тогда
				
				Если ЗарплатаКадрыВызовСервера.СБазойРаботаетЕдинственныйПользователь() Тогда
					ПоказатьПредупреждение(, НСтр("ru = 'Не удается внести изменения в личные данные сотрудника. Возможно, уже открыта форма с личными данными.'"));
				Иначе
					ПоказатьПредупреждение(, НСтр("ru = 'Не удается внести изменения в личные данные сотрудника. Возможно, уже открыта форма с личными данными или личные данные редактируются другим пользователем.'"));
				КонецЕсли;
				
			КонецЕсли;
			
			Форма.ФизическоеЛицоЗаблокированоВДругойФорме = Истина;
			Форма.УстановитьРежимТолькоПросмотраЛичныхДанных();
			
			// Заблокировать не удалось - обновить данные физлица.
			Если ИзФормыСотрудника Тогда
				Форма.ПрочитатьДанныеСвязанныеСФизлицом();
			КонецЕсли;
			
			Возврат Ложь;
			
		Иначе
			Форма.ФизическоеЛицоЗаблокировано = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ЗаблокироватьСотрудникаПриРедактировании(Форма) Экспорт
	
	Если Не Форма.СотрудникЗаблокирован Тогда
		
		Если НЕ СотрудникиВызовСервера.ЗаблокироватьСотрудникаПриРедактированииНаСервере(Форма.СотрудникСсылка, Форма.Сотрудник.ВерсияДанных, Форма.УникальныйИдентификатор) Тогда
			
			Если ЗарплатаКадрыВызовСервера.СБазойРаботаетЕдинственныйПользователь() Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Не удается внести изменения в личные данные сотрудника. Возможно, уже открыта форма с личными данными.'"));
			Иначе
				ПоказатьПредупреждение(, НСтр("ru = 'Не удается внести изменения в личные данные сотрудника. Возможно, уже открыта форма с личными данными или личные данные редактируются другим пользователем.'"));
			КонецЕсли;
			
			Форма.СотрудникЗаблокированВДругойФорме = Истина;
			Форма.ТолькоПросмотр = Истина;
			Форма.УстановитьРежимТолькоПросмотраЛичныхДанных();
			
			Возврат Ложь;
			
		Иначе
			Форма.СотрудникЗаблокирован = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура СформироватьНаименованиеСотрудника(Форма) Экспорт
	Форма.Сотрудник.Наименование = КадровыйУчетКлиентСервер.ПолноеНаименованиеСотрудника(Форма.ФИОФизическихЛиц.Фамилия, Форма.ФИОФизическихЛиц.Имя, Форма.ФИОФизическихЛиц.Отчество, Форма.ФизическоеЛицо.УточнениеНаименования, Форма.Сотрудник.УточнениеНаименования);
	Форма.СотрудникНаименование  = СотрудникиКлиентСервер.ПредставлениеСотрудникаПоДаннымФормыСотрудника(Форма);;
	ИзменитьНаименованиеФизическогоЛица(Форма);
	Оповестить("ИзмененЗаголовокФормыСотрудника", Форма.Сотрудник.Наименование, Форма);
КонецПроцедуры

Процедура ИзменитьНаименованиеФизическогоЛица(Форма)
	
	Если Форма.ДоступноДобавлениеИзменениеДанныхФизическихЛиц Тогда
		Форма.ФизическоеЛицо.Наименование = КадровыйУчетКлиентСервер.ПолноеНаименованиеСотрудника(Форма.ФИОФизическихЛиц.Фамилия, Форма.ФИОФизическихЛиц.Имя, Форма.ФИОФизическихЛиц.Отчество, Форма.ФизическоеЛицо.УточнениеНаименования);
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьНаименованиеФизическогоЛица(Форма) Экспорт
	ИзменитьНаименованиеФизическогоЛица(Форма);
	Оповестить("ИзмененЗаголовокФормыСотрудника", Форма.ФизическоеЛицо.Наименование, Форма);
КонецПроцедуры

Процедура ДополнитьПредставлениеСотрудникаПриИзменении(Форма) Экспорт
	СотрудникиКлиентСервер.УстановитьДоступностьУточненияНаименования(Форма);
	Если НЕ Форма.ДополнятьПредставление Тогда
		Форма.Сотрудник.УточнениеНаименования = "";
	КонецЕсли;
	СформироватьНаименованиеСотрудника(Форма);
КонецПроцедуры

Процедура ДополнитьПредставлениеФизическогоЛицаПриИзменении(Форма) Экспорт
	
	СотрудникиКлиентСервер.УстановитьДоступностьУточненияНаименования(Форма);
	Если НЕ Форма.ДополнятьПредставление Тогда
		Форма.ФизическоеЛицо.УточнениеНаименования = "";
	КонецЕсли;
	
	СформироватьНаименованиеФизическогоЛица(Форма);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОбслуживанияЛичныхДанныхФизическогоЛица

Процедура ЛичныеДанныеФизическогоЛицаПослеЗаписи(Форма, ФизическоеЛицоСсылка)
	
	Если Форма.ИзмененыЛичныеДанные Тогда
		
		Оповестить("ИзменениеЛичныхДанных", ФизическоеЛицоСсылка, Форма);
		Форма.ИзмененыЛичныеДанные = Ложь;
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ЛичныеДанныеФизическогоЛицаОбработкаОповещения(ИмяСобытия, Параметр, Источник, Форма) Экспорт
	
	Если ИмяСобытия = "ИзменениеЛичныхДанных"
		И Параметр = Форма.ФизическоеЛицоСсылка
		И Источник <> Форма Тогда
		
		Форма.Прочитать();
		
	КонецЕсли;
	
КонецПроцедуры	

Процедура СотрудникиИННПриИзменении(Форма, Элемент) Экспорт
	ЗаблокироватьФизическоеЛицоПриРедактировании(Форма); 
	ФизическиеЛицаИННПриИзменении(Форма, Элемент);
КонецПроцедуры

Процедура СотрудникиСтраховойНомерПФРПриИзменении(Форма, Элемент) Экспорт
	ЗаблокироватьФизическоеЛицоПриРедактировании(Форма); 
	ФизическиеЛицаСтраховойНомерПФРПриИзменении(Форма, Элемент);
КонецПроцедуры

Процедура ФизическиеЛицаИННПриИзменении(Форма, Элемент) Экспорт
	СотрудникиКлиентВнутренний.ФизическиеЛицаИННПриИзменении(Форма, Элемент);
КонецПроцедуры

Процедура ФизическиеЛицаСтраховойНомерПФРПриИзменении(Форма, Элемент) Экспорт
	СотрудникиКлиентВнутренний.ФизическиеЛицаСтраховойНомерПФРПриИзменении(Форма, Элемент);
КонецПроцедуры

Процедура ФизическиеЛицаМестоРожденияНачалоВыбора(Форма, Элемент, СтандартнаяОбработка, МестоРождения, ОповещениеЗавершения = Неопределено) Экспорт	
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Представление", МестоРождения);
	
	Оповещение = Новый ОписаниеОповещения("ФизическиеЛицаМестоРожденияНачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("ОбщаяФорма.ВводМестаРождения", ПараметрыФормы, , , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

Процедура ФизическиеЛицаМестоРожденияНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт	
	
	Если Результат <> Неопределено Тогда
		
		Форма = ДополнительныеПараметры.Форма;
		Элемент = ДополнительныеПараметры.Элемент;
		ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
		
		Форма.ФизическоеЛицо.МестоРождения = Результат;
		Форма[Элемент.Имя] = ПерсонифицированныйУчетКлиентСервер.ПредставлениеМестаРождения(Результат);
		Форма.Модифицированность = Истина;

		Если ОповещениеЗавершения <> Неопределено Тогда
			МестоРожденияИзменено = Результат <> Неопределено;
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, МестоРожденияИзменено);
		Иначе
			Форма.ОбновитьОтображениеДанных();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВводДокументовСправочниковОткрытиеЖурналовУчетНДФЛ

Процедура ВвестиДокументПрекращениеСтандартныхВычетовНДФЛ(ФизическоеЛицо, Организация) Экспорт
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Сотрудник", ФизическоеЛицо);
	ЗначенияЗаполнения.Вставить("Организация", Организация);
	ЗначенияЗаполнения.Вставить("Месяц", НачалоМесяца(ОбщегоНазначенияКлиент.ДатаСеанса()));

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ИзФормыСотрудника", Истина);
	
	ОткрытьФорму("Документ.ПрекращениеСтандартныхВычетовНДФЛ.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

Процедура ВвестиНовоеЗаявлениеНаПредоставлениеСтандартныхВычетов(ФизическоеЛицо, Организация) Экспорт
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Сотрудник", ФизическоеЛицо);
	ЗначенияЗаполнения.Вставить("Организация", Организация);
	ЗначенияЗаполнения.Вставить("Месяц", НачалоМесяца(ОбщегоНазначенияКлиент.ДатаСеанса()));

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ИзФормыСотрудника", Истина);
	
	ОткрытьФорму("Документ.ЗаявлениеНаПредоставлениеСтандартныхВычетовПоНДФЛ.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

Процедура ВвестиНовоеУведомлениеОПравеНаИмущественныйВычет(ФизическоеЛицо, Организация) Экспорт
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Сотрудник", ФизическоеЛицо);
	ЗначенияЗаполнения.Вставить("Организация", Организация);
	ЗначенияЗаполнения.Вставить("ПрименятьВычетыС", НачалоМесяца(ОбщегоНазначенияКлиент.ДатаСеанса()));
	ЗначенияЗаполнения.Вставить("НалоговыйПериод", Год(ОбщегоНазначенияКлиент.ДатаСеанса()));

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ИзФормыСотрудника", Истина);
	
	ОткрытьФорму("Документ.УведомлениеОПравеНаИмущественныйВычетДляНДФЛ.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

Процедура ОткрытьФормуВводаДоходовСПредыдущегоМестаРаботы(ФизическоеЛицо, ГоловнаяОрганизация) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФизическоеЛицо", ФизическоеЛицо);
	ПараметрыФормы.Вставить("ГоловнаяОрганизация", ГоловнаяОрганизация);
	
	ОткрытьФорму("Справочник.Сотрудники.Форма.ДоходыСПредыдущегоМестаРаботы", ПараметрыФормы, , , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОткрытьФормуВводаВычетовСПредыдущегоМестаРаботы(ФизическоеЛицо, ГоловнаяОрганизация) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФизическоеЛицо", ФизическоеЛицо);
	ПараметрыФормы.Вставить("ГоловнаяОрганизация", ГоловнаяОрганизация);
	
	ОткрытьФорму("Справочник.Сотрудники.Форма.ВычетыСПредыдущегоМестаРаботы", ПараметрыФормы, , , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОткрытьЖурналЗаявленийНаВычеты(ФизическоеЛицо) Экспорт
	
	Отбор = Новый Структура("Сотрудник", ФизическоеЛицо);
	
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	
	ОткрытьФорму("ЖурналДокументов.ЗаявленияНаВычеты.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбслуживанияДанныхОФИО

Процедура ПриИзмененииФИО(Форма, ФизическоеЛицо, МенеджерЗаписиФИО)
	
	ФИО = СокрЛП(ФизическоеЛицо.ФИО);
	Пока СтрНайти(ФИО, "  ") > 0 Цикл
		ФИО = СтрЗаменить(ФИО, "  ", " ");
	КонецЦикла;
	
	СтруктураФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(ФИО);
	
	ФизическоеЛицо.Инициалы = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ИнициалыПоИмениОтчеству(
		СтруктураФИО.Имя, СтруктураФИО.Отчество);
	
	МенеджерЗаписиФИО.Фамилия = СтруктураФИО.Фамилия;
	МенеджерЗаписиФИО.Имя = СтруктураФИО.Имя;
	МенеджерЗаписиФИО.Отчество = СтруктураФИО.Отчество;
	МенеджерЗаписиФИО.Инициалы = ФизическоеЛицо.Инициалы;
	
	ОкончаниеФИО = СтрЗаменить(ФИО, СокрЛП(МенеджерЗаписиФИО.Фамилия + " " + МенеджерЗаписиФИО.Имя + " " + МенеджерЗаписиФИО.Отчество), "");
	Если Не ПустаяСтрока(ОкончаниеФИО) Тогда
		МенеджерЗаписиФИО.Отчество = МенеджерЗаписиФИО.Отчество + " " + СокрЛП(ОкончаниеФИО);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(МенеджерЗаписиФИО.Период) Тогда
		
		Если ЗначениеЗаполнено(ФизическоеЛицо.ДатаРождения) Тогда
			МенеджерЗаписиФИО.Период = ФизическоеЛицо.ДатаРождения;
		Иначе
			МенеджерЗаписиФИО.Период = ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведений();
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МенеджерЗаписиФИО.Отчество) И Не ЗначениеЗаполнено(ФизическоеЛицо.Пол) Тогда
		ФизическоеЛицо.Пол = СотрудникиКлиентСервер.ОпределитьПолПоОтчеству(МенеджерЗаписиФИО.Отчество);
	КонецЕсли;
	
	СформироватьНаименованиеФизическогоЛица(Форма);
	
	СотрудникиКлиентСервер.УстановитьВидимостьПолейФИО(Форма);
	
КонецПроцедуры

Процедура УстановитьТолькоПросмотрФИО(Элемент, ИзменениеЗаднимЧислом, ПериодНачалаДействия, ПериодОкончанияДействия) 
	
	Если ИзменениеЗаднимЧислом Тогда 
		
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСТР("ru = 'Изменение ФИО выполнено ""задним числом"". Введенное данные действуют с %1 по %2.
				|Редактирование ФИО непосредственно в форме сейчас недоступно и будет доступно только после записи.'"),
				Формат(ПериодНачалаДействия, "ДЛФ=D"),
				Формат(ПериодОкончанияДействия, "ДЛФ=D"));
			
		ПоказатьПредупреждение(, ТекстПредупреждения);
		
		Элемент.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьФИОФизическогоЛица(Форма, ОповещениеЗавершения = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура("Форма, ОповещениеЗавершения", Форма, ОповещениеЗавершения);
	
	СтруктураФИО = Новый Структура("Фамилия,Имя,Отчество,Инициалы");
	ЗаполнитьЗначенияСвойств(СтруктураФИО, Форма.ФИОФизическихЛиц);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Фамилия", ?(СтруктураФИО.Фамилия = Неопределено, "", СтруктураФИО.Фамилия));
	ПараметрыФормы.Вставить("Имя", ?(СтруктураФИО.Имя = Неопределено, "", СтруктураФИО.Имя));
	ПараметрыФормы.Вставить("Отчество", ?(СтруктураФИО.Отчество = Неопределено, "", СтруктураФИО.Отчество));
	ПараметрыФормы.Вставить("Инициалы", ?(СтруктураФИО.Инициалы = Неопределено, "", СтруктураФИО.Инициалы));
	ПараметрыФормы.Вставить("ДатаИзменения", ОбщегоНазначенияКлиент.ДатаСеанса());
	
	Оповещение = Новый ОписаниеОповещения("ИзменитьФИОФизическогоЛицаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("ОбщаяФорма.СменаФИО", ПараметрыФормы, Форма, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ИзменитьФИОФизическогоЛицаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	МенеджерЗаписиФИО = Форма.ФИОФизическихЛиц;
	
	Если Результат <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(Форма.ФИОФизическихЛиц, Результат);
		
		// нажали ОК
		Если Результат.ДатаИзменения >= МенеджерЗаписиФИО.Период Тогда
			// Изменим имя только если это - хронологически последняя запись регистра ФИО.
			НовоеНаименование = Результат.Фамилия + " " + Результат.Имя + " " + Результат.Отчество;
		Иначе
			// Вернем прежнее имя если это - ввод хронологически не последней записи регистра ФИО.
			НовоеНаименование = МенеджерЗаписиФИО.Фамилия + " " + МенеджерЗаписиФИО.Имя + " " + МенеджерЗаписиФИО.Отчество;
		КонецЕсли;
		
		Результат.Вставить("НовоеНаименование", НовоеНаименование);
		Результат.Вставить("ИзменениеЗаднимЧислом", Результат.ДатаИзменения < МенеджерЗаписиФИО.Период И МенеджерЗаписиФИО.Период > ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведений());
		Результат.Вставить("ДатаТекущейЗаписи",
			?(Не ЗначениеЗаполнено(МенеджерЗаписиФИО.Период) Или МенеджерЗаписиФИО.Период = ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведений(),
				ОбщегоНазначенияКлиент.ДатаСеанса(), МенеджерЗаписиФИО.Период));
		
		МенеджерЗаписиФИО.Фамилия = Результат.Фамилия;
		МенеджерЗаписиФИО.Имя = Результат.Имя;
		МенеджерЗаписиФИО.Отчество = Результат.Отчество;
		МенеджерЗаписиФИО.Инициалы = Результат.Инициалы;
		МенеджерЗаписиФИО.Период = Результат.ДатаИзменения;
		
		Форма.ВыполненаКомандаСменыФИО = Истина;
		Форма.ФИОФизическихЛицНоваяЗапись = Истина;
		Форма.Модифицированность = Истина;
		
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Результат);
		КонецЕсли;
		
	Иначе
		
		Форма.ФИОФизическихЛицНоваяЗапись = Ложь;
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Результат);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьФИОСотрудника(Форма, ОповещениеЗавершения = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура("Форма, ОповещениеЗавершения", Форма, ОповещениеЗавершения);
	
	Оповещение = Новый ОписаниеОповещения("ИзменитьФИОСотрудникаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ИзменитьФИОФизическогоЛица(Форма, Оповещение);
	
КонецПроцедуры

Процедура ИзменитьФИОСотрудникаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	
	Если Результат <> Неопределено Тогда
		Если Не Результат.ИзменениеЗаднимЧислом И ЗаблокироватьФизическоеЛицоПриРедактировании(Форма) Тогда
			Форма.ФизическоеЛицо.ФИО = Результат.НовоеНаименование;
			СформироватьНаименованиеСотрудника(Форма);
		КонецЕсли;
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриИзмененииФИОФизическогоЛица(Форма) Экспорт
	
	ПриИзмененииФИО(Форма, Форма.ФизическоеЛицо, Форма.ФИОФизическихЛиц);
	
	Если Форма.Параметры.Ключ.Пустая() Тогда
		// При создании нового физического лица попробуем проверить по введенным ФИО
		// существующее физическое лицо.
		ПроверитьОднофамильцев(Форма, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриИзмененииФИОСотрудника(Форма) Экспорт
	
	Если НЕ ЗаблокироватьФизическоеЛицоПриРедактировании(Форма) Тогда
		Возврат;
	КонецЕсли;
	
	ПриИзмененииФИО(Форма, Форма.ФизическоеЛицо, Форма.ФИОФизическихЛиц);
	
	ДополнительныеПараметры = Новый Структура("Форма", Форма);
	
	Если Форма.Параметры.Ключ.Пустая() Тогда
		// При создании нового сотрудника попробуем подобрать по введенным ФИО
		// существующее физическое лицо.
		Оповещение = Новый ОписаниеОповещения("ПриИзмененииФИОСотрудникаЗавершение", ЭтотОбъект, ДополнительныеПараметры); 
		ПроверитьОднофамильцев(Форма, Истина, Оповещение);
	Иначе 
		ПриИзмененииФИОСотрудникаЗавершение(Неопределено, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриИзмененииФИОСотрудникаЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	Форма.ПроверяютсяОднофамильцы = Ложь;
	СформироватьНаименованиеСотрудника(Форма);
	
КонецПроцедуры

Процедура УстановитьФизическоеЛицоВФормеСотрудника(Форма, ФизическоеЛицоСсылка) Экспорт
	Если ФизическоеЛицоСсылка <> Неопределено Тогда
		Если НЕ ФизическоеЛицоСсылка.Пустая() Тогда
			Форма.Сотрудник.ФизическоеЛицо = ФизическоеЛицоСсылка;
			Форма.ФизическоеЛицоСсылка     = ФизическоеЛицоСсылка;
			Форма.ОбновитьДанныеФизическогоЛицаНаСервере();
			СформироватьНаименованиеСотрудника(Форма);
			СотрудникиКлиентСервер.УстановитьВидЗанятостиНовогоСотрудника(Форма);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьОднофамильцев(Форма, ИзФормыСотрудника, ОповещениеЗавершения = Неопределено)
	
	Если Форма.ПроверяютсяОднофамильцы Тогда
		Возврат;
	КонецЕсли;
	
	Если (Форма.ИННУказанПравильно ИЛИ ПустаяСтрока(Форма.ФизическоеЛицо.ИНН))
		И (Форма.СНИЛСУказанПравильно ИЛИ НЕ КадровыйУчетКлиентСервер.СНИЛСЗаполнен(Форма.ФизическоеЛицо.СтраховойНомерПФР))
		И (НЕ ПустаяСтрока(Форма.ФизическоеЛицо.ИНН) ИЛИ КадровыйУчетКлиентСервер.СНИЛСЗаполнен(Форма.ФизическоеЛицо.СтраховойНомерПФР)) Тогда
		
		Если СотрудникиВызовСервера.ИННИСНИЛСУникальны(Форма.ФизическоеЛицо.ИНН, Форма.ФизическоеЛицо.СтраховойНомерПФР) Тогда
			
			Если ОповещениеЗавершения <> Неопределено Тогда 
				ВыполнитьОбработкуОповещения(ОповещениеЗавершения);
			КонецЕсли;
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураПроверкиОднофамильцев = СотрудникиВызовСервера.ПодобратьСписокФизЛиц(
		Форма.ФизическоеЛицоСсылка,
		Форма.ФИОФизическихЛиц.Фамилия,
		Форма.ФИОФизическихЛиц.Имя,
		Форма.ФИОФизическихЛиц.Отчество);
	
	ДополнительныеПараметры = Новый Структура;	
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("ИзФормыСотрудника", ИзФормыСотрудника);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Если НЕ СтруктураПроверкиОднофамильцев.ФизическоеЛицоУникально Тогда
		
		ПараметрыОткрытия = ПараметрыОткрытияФормыФизическиеЛицаСПохожимиДанными(СтруктураПроверкиОднофамильцев);
		
		Форма.ПроверяютсяОднофамильцы = Истина;
		Если СтруктураПроверкиОднофамильцев.ДанныеФизическихЛиц.Количество() > 0 Тогда
			
			Оповещение = Новый ОписаниеОповещения("ПроверитьОднофамильцевДанныеФизическихЛиц", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму(
				"Справочник.ФизическиеЛица.Форма.ФизическиеЛицаСПохожимиДанными", 
				ПараметрыОткрытия, 
				Форма, 
				Форма.ФизическоеЛицоСсылка, , , 
				Оповещение);
			
		Иначе
			
			Если СтруктураПроверкиОднофамильцев.ВозможнаПроверкаПоИНН
				ИЛИ СтруктураПроверкиОднофамильцев.ВозможнаПроверкаПоСНИЛС Тогда
				
				Оповещение = Новый ОписаниеОповещения("ПроверитьОднофамильцевИНН_СНИЛС", ЭтотОбъект, ДополнительныеПараметры);
				ОткрытьФорму("Справочник.ФизическиеЛица.Форма.ПроверкаУникальностиПоИННиСНИЛС", ПараметрыОткрытия,
					Форма, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				
			Иначе
				
				ТекстПредупреждения = НСтр("ru='Найден человек с похожим именем.
					|Обратитесь к администратору информационной системы для устранения проблемы.'");
					
				ПоказатьПредупреждение(, ТекстПредупреждения);
				Форма.Модифицированность = Ложь;
				Форма.Закрыть();
				
				Если ОповещениеЗавершения <> Неопределено Тогда 
					ВыполнитьОбработкуОповещения(ОповещениеЗавершения);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьОднофамильцевДанныеФизическихЛиц(РезультатВыбора, ДополнительныеПараметры) Экспорт 
	
	Форма = ДополнительныеПараметры.Форма;
	ИзФормыСотрудника = ДополнительныеПараметры.ИзФормыСотрудника;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	
	Форма.ПроверяютсяОднофамильцы = Ложь;
	
	Если РезультатВыбора <> Неопределено Тогда
		
		Если ИзФормыСотрудника Тогда
			УстановитьФизическоеЛицоВФормеСотрудника(Форма, РезультатВыбора);
		Иначе
			
			Форма.Модифицированность = Ложь;
			Форма.Закрыть();
			
			Если Форма.РежимВыбора Тогда
				Оповестить("СозданоФизическоеЛицо", РезультатВыбора, Форма.ВладелецФормы);
			Иначе
				ОткрытьФорму("Справочник.ФизическиеЛица.ФормаОбъекта", Новый Структура("Ключ", РезультатВыбора));
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьОднофамильцевИНН_СНИЛС(РезультатПроверки, ДополнительныеПараметры) Экспорт 
	
	Форма = ДополнительныеПараметры.Форма;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	
	Форма.ПроверяютсяОднофамильцы = Ложь;
	
	Если РезультатПроверки = Неопределено Тогда
		
		ТекстПредупреждения = НСтр("ru = 'Проверка уникальности не пройдена.
			|Обратитесь к администратору информационной системы для устранения проблемы.'");
		
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Форма.Модифицированность = Ложь;
		Форма.Закрыть();
		
	Иначе
		
		ЗаполнитьЗначенияСвойств(Форма.ФизическоеЛицо, РезультатПроверки);
		
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения);
	КонецЕсли;
	
КонецПроцедуры

Процедура СотрудникИзменилФИОНажатие(Форма) Экспорт
	
	ДополнительныеПараметры = Новый Структура("Форма", Форма);
	
	Оповещение = Новый ОписаниеОповещения("СотрудникИзменилФИОНажатиеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ИзменитьФИОСотрудника(Форма, Оповещение);
	
КонецПроцедуры

Процедура СотрудникИзменилФИОНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если Результат <> Неопределено Тогда 
		УстановитьТолькоПросмотрФИО(Форма.Элементы.ФИО, Результат.ИзменениеЗаднимЧислом, Форма.ФИОФизическихЛиц.Период, Результат.ДатаТекущейЗаписи);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ФизическоеЛицоИзменилФИОНажатие(Форма) Экспорт
	
	ДополнительныеПараметры = Новый Структура("Форма", Форма);
	
	Оповещение = Новый ОписаниеОповещения("ФизическоеЛицоИзменилФИОНажатиеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ИзменитьФИОФизическогоЛица(Форма, Оповещение);
	
КонецПроцедуры

Процедура ФизическоеЛицоИзменилФИОНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если Результат <> Неопределено Тогда
		Если Не Результат.ИзменениеЗаднимЧислом Тогда
			Форма.ФизическоеЛицо.ФИО = Результат.НовоеНаименование;
			СформироватьНаименованиеФизическогоЛица(Форма);
		КонецЕсли; 
		УстановитьТолькоПросмотрФИО(Форма.Элементы.ФИО, Результат.ИзменениеЗаднимЧислом, Форма.ФИОФизическихЛиц.Период, Результат.ДатаТекущейЗаписи);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеОтображениемИДоступностьюДокументаУдостоверяющегоЛичность

Процедура ДокументыФизическихЛицВидДокументаПриИзменении(Форма) Экспорт
	СотрудникиКлиентВнутренний.ДокументыФизическихЛицВидДокументаПриИзменении(Форма);
КонецПроцедуры

Процедура ДокументыФизическихЛицСерияПриИзменении(Форма, Элемент) Экспорт
	СотрудникиКлиентВнутренний.ДокументыФизическихЛицСерияПриИзменении(Форма, Элемент);
КонецПроцедуры

Процедура ДокументыФизическихЛицНомерПриИзменении(Форма, Элемент) Экспорт
	СотрудникиКлиентВнутренний.ДокументыФизическихЛицНомерПриИзменении(Форма, Элемент);
КонецПроцедуры

Процедура ДокументыФизическихЛицВидДокументаНачалоВыбора(Форма, Элемент, СтандартнаяОбработка) Экспорт
	Если НЕ ЗначениеЗаполнено(Форма.ДокументыФизическихЛиц.ВидДокумента) Тогда
		Форма.ДокументыФизическихЛиц.ВидДокумента = ПредопределенноеЗначение("Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ");
	КонецЕсли; 
КонецПроцедуры

Процедура ОткрытьСписокВсехДокументовФизическогоЛица(Форма, ФизическоеЛицоСсылка) Экспорт
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Физлицо", ФизическоеЛицоСсылка);
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Отбор", СтруктураОтбора);
	ПараметрыОткрытияФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	ПараметрыОткрытияФормы.Вставить("ИзФормыРедактированияЛичныхДанных", Истина);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	
	ОткрытьФорму("РегистрСведений.ДокументыФизическихЛиц.Форма.ДокументыФизическогоЛица", ПараметрыОткрытияФормы, Форма);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаМодифицированностиГражданстваИнвалидностиУдостоверенияЛичности

Процедура ЗапроситьРежимИзмененияГражданства(ФормаИсточник, ДатаИзменения, Отказ, ОповещениеЗавершения = Неопределено) Экспорт
	
	Если ФормаИсточник.ДоступенПросмотрДанныхФизическихЛиц Тогда
		
		ТекстКнопкиДа = НСтр("ru = 'Изменилось гражданство'");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru =  'При редактировании изменены сведения о гражданстве. 
							|Если исправлена прежняя запись о гражданстве (она была ошибочной), нажмите ""Исправлена ошибка"".
							|Если у сотрудника изменилось гражданство с %1, нажмите ""%2""'"), 
				Формат(ДатаИзменения, "ДФ='д ММММ гггг ""г""'"),
				ТекстКнопкиДа);
		
		РедактированиеПериодическихСведенийКлиент.ЗапроситьРежимИзмененияРегистра(ФормаИсточник, "ГражданствоФизическихЛиц", ТекстВопроса, ТекстКнопкиДа, Отказ, ОповещениеЗавершения);
		
	Иначе 
		
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗапроситьРежимИзмененияУдостоверенияЛичности(ФормаИсточник, ДатаИзменения, Отказ, ОповещениеЗавершения = Неопределено) Экспорт
	
	Если ФормаИсточник.ДоступенПросмотрДанныхФизическихЛиц Тогда
		
		ТекстКнопкиДа = НСтр("ru = 'Изменился документ, удостоверяющий личность'");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru =  'При редактировании изменены сведения о документе, удостоверяющем личность.
							|Если исправлена прежняя запись о документе (она была ошибочной), нажмите ""Исправлена ошибка"".
							|Если у сотрудника изменился документ, удостоверяющий личность с %1, нажмите'") + " ""%2""",
				Формат(ДатаИзменения, "ДФ='д ММММ гггг ""г""'"),
				ТекстКнопкиДа);
		
		РедактированиеПериодическихСведенийКлиент.ЗапроситьРежимИзмененияРегистра(ФормаИсточник, "ДокументыФизическихЛиц", ТекстВопроса, ТекстКнопкиДа, Отказ, ОповещениеЗавершения);
		
	Иначе 
		
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ПросмотрИстории

Процедура СотрудникиОткрытьФормуРедактированияИстории(ИмяРегистра, Форма) Экспорт
	ФизлицоЗаблокировано = ЗаблокироватьФизическоеЛицоПриРедактировании(Форма, Ложь);
	Если ИмяРегистра = "ДокументыФизическихЛиц" Тогда
		СотрудникиКлиентСервер.ОбновитьНаборЗаписейИсторииДокументыФизическихЛиц(Форма, Форма.ФизическоеЛицоСсылка);
	КонецЕсли;
	РедактированиеПериодическихСведенийКлиент.ОткрытьИсторию(ИмяРегистра, Форма.ФизическоеЛицоСсылка, Форма, Форма.ТолькоПросмотр ИЛИ НЕ ФизлицоЗаблокировано);
КонецПроцедуры

Процедура ОткрытьФормуРедактированияИстории(ИмяРегистра, ВедущийОбъект, Форма) Экспорт
	ТолькоПросмотрИстории = Форма.ТолькоПросмотр;
	Если Не ТолькоПросмотрИстории Тогда
		Попытка
			Форма.ЗаблокироватьДанныеФормыДляРедактирования();
			ТолькоПросмотрИстории = Ложь;
		Исключение
			ТолькоПросмотрИстории = Истина;
		КонецПопытки
	КонецЕсли; 
	Если ИмяРегистра = "ДокументыФизическихЛиц" Тогда
		СотрудникиКлиентСервер.ОбновитьНаборЗаписейИсторииДокументыФизическихЛиц(Форма, ВедущийОбъект);
	КонецЕсли;
	РедактированиеПериодическихСведенийКлиент.ОткрытьИсторию(ИмяРегистра, ВедущийОбъект, Форма, ТолькоПросмотрИстории);
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработкиИнформацииОМестахРаботы

Процедура ОткрытьФормуСпискаМестРаботыФизическогоЛица(ФизическоеЛицо, Владелец) Экспорт
	
	Отбор = Новый Структура("ФизическоеЛицо", ФизическоеЛицо);
	
	ОткрытьФорму("Справочник.Сотрудники.ФормаСписка", Новый Структура("Отбор", Отбор), Владелец); 
	
КонецПроцедуры	

Процедура ОткрытьМестоРаботы(Форма) Экспорт
	
	ПараметрыОткрытияФормыСотрудника = Новый Структура;
	ПараметрыОткрытияФормыСотрудника.Вставить("ФизическоеЛицо", Форма.ФизическоеЛицоСсылка);
	ПараметрыОткрытияФормыСотрудника.Вставить("ДоступныТолькоДанныеСотрудника", Истина);
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ОткрытиеФормыЭлементаСправочникаСотрудники");
	
	ОткрытьФорму("Справочник.Сотрудники.ФормаОбъекта", ПараметрыОткрытияФормыСотрудника);
	
КонецПроцедуры

Процедура ОткрытьФормуСотрудника(Форма, Команда) Экспорт 
	
	СоответствиеКомандСотрудникам = Форма.СоответствиеКомандСотрудникам;
	
	Сотрудник = СоответствиеКомандСотрудникам.Получить(Команда.Имя);
	Если Сотрудник <> Неопределено Тогда 
		
		ПараметрыОткрытияФормыСотрудника = Новый Структура;
		ПараметрыОткрытияФормыСотрудника.Вставить("Ключ", Сотрудник);
		ПараметрыОткрытияФормыСотрудника.Вставить("ДоступныТолькоДанныеСотрудника", Истина);
		
		ОценкаПроизводительностиКлиент.ЗамерВремени("ОткрытиеФормыЭлементаСправочникаСотрудники");
		
		ОткрытьФорму("Справочник.Сотрудники.ФормаОбъекта", ПараметрыОткрытияФормыСотрудника);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МеханизмВстраиванияФорм

Процедура ЗарегистрироватьОткрытиеФормы(Форма, ИмяФормы) Экспорт
	
	Если ТипЗнч(Форма.ВладелецФормы.ОткрытыеФормы) <> Тип("Соответствие") Тогда
		Форма.ВладелецФормы.ОткрытыеФормы = Новый Соответствие;
	КонецЕсли; 
	
	Форма.ВладелецФормы.ОткрытыеФормы.Вставить(ИмяФормы, Форма);
	
КонецПроцедуры

Процедура СохранитьДанныеФорм(Форма, Отказ, ЗакрытьФорму) Экспорт
	
	Если НЕ Форма.Параметры.Ключ.Пустая()
		И ТипЗнч(Форма.ОткрытыеФормы) = Тип("Соответствие") Тогда
		
		Для каждого ОткрытаяФорма Из Форма.ОткрытыеФормы Цикл
			
			ДополнительнаяФорма = ОткрытаяФорма.Значение;
			Если ДополнительнаяФорма.Открыта() Тогда
				ДополнительнаяФорма.СохранитьИЗакрытьНаКлиенте(ЗакрытьФорму);
			КонецЕсли; 
			
			Если ЗакрытьФорму И ДополнительнаяФорма.Открыта() Тогда
				Отказ = Истина;
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьЗаписи(Форма, Отказ) Экспорт
	
	Если НЕ Форма.Модифицированность Тогда
		
		Если ТипЗнч(Форма.ОткрытыеФормы) = Тип("Соответствие") Тогда
			
			Для каждого ОткрытаяФорма Из Форма.ОткрытыеФормы Цикл
				
				ДополнительнаяФорма = ОткрытаяФорма.Значение;
				Если НЕ ДополнительнаяФорма.ТолькоПросмотр И ДополнительнаяФорма.Открыта() Тогда
					
					ДополнительнаяФорма.Закрыть();
					Если ДополнительнаяФорма.Открыта() Тогда
						ДополнительнаяФорма.АктивиЗировать();
						Отказ = Истина;
					КонецЕсли; 
					
				КонецЕсли; 
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли; 

КонецПроцедуры

Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если Источник = Форма.ВладелецФормы Тогда
		
		Если ИмяСобытия = "ИзмененЗаголовокФормыСотрудника" Тогда
			
			Форма.Заголовок = Параметр;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСДополнительнымиФормами

Процедура ОткрытьДополнительнуюФорму(ОписаниеДополнительнойФормы, Форма, ИзФормыСотрудника = Ложь, ОписаниеОповещения = Неопределено) Экспорт
	
	ПараметрыФормы = ПараметрыДополнительнойФормы(ОписаниеДополнительнойФормы, Форма, ИзФормыСотрудника);
	ОткрытьФорму(ОписаниеДополнительнойФормы.ИмяФормы, ПараметрыФормы, Форма, , , , ОписаниеОповещения);
	
КонецПроцедуры

Функция ПараметрыДополнительнойФормы(ОписаниеДополнительнойФормы, Форма, ИзФормыСотрудника) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресВХранилище", "");
	
	Для каждого КлючевойРеквизит Из ОписаниеДополнительнойФормы.КлючевыеРеквизиты Цикл
		ПараметрыФормы.Вставить(КлючевойРеквизит.Ключ);
		Если ЗначениеЗаполнено(КлючевойРеквизит.Значение) Тогда
			Попытка
				Данные = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, КлючевойРеквизит.Значение);
			Исключение
				Данные = Неопределено;
			КонецПопытки;
			ПараметрыФормы[КлючевойРеквизит.Ключ] = Данные;
		КонецЕсли; 
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Форма, , "Заголовок");
	ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыФормы.Вставить("ИзФормыСотрудника", ИзФормыСотрудника);
	
	ПараметрыФормы.Вставить(
		"АдресВХранилище",
		Форма.АдресДанныхДополнительнойФормыНаСервере(ОписаниеДополнительнойФормы));
	
	Возврат ПараметрыФормы;
	
КонецФункции

Процедура ПроверитьРежимТолькоПросмотраДополнительнойФормы(Форма, ПроверятьБлокировкуСотрудника, ПроверятьБлокировкуФизическогоЛица = Истина) Экспорт
	
	Если Форма.ТолькоПросмотр Тогда
		УстановитьРежимТолькоПросмотраДополнительнойФормы(Форма);
	Иначе
		
		Если ПроверятьБлокировкуСотрудника Тогда
			
			Если Форма.ВладелецФормы.СотрудникЗаблокированВДругойФорме Тогда
				УстановитьРежимТолькоПросмотраДополнительнойФормы(Форма);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не Форма.ТолькоПросмотр
			И ПроверятьБлокировкуФизическогоЛица
			И Форма.ВладелецФормы.ФизическоеЛицоЗаблокированоВДругойФорме Тогда
			
			УстановитьРежимТолькоПросмотраДополнительнойФормы(Форма);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьРежимТолькоПросмотраДополнительнойФормы(Форма) Экспорт
	
	Форма.ТолькоПросмотр = Истина;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ФормаОк",
		"Доступность",
		Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ФормаОтмена",
		"КнопкаПоУмолчанию",
		Истина);
	
КонецПроцедуры

Функция ЗаблокироватьОбъектВФормеВладельцеДополнительнойФормы(Форма, БлокироватьСотрудника, БлокироватьФизическоеЛицо = Истина) Экспорт
	
	ОбъектЗаблокирован = Истина;
	
	Если БлокироватьСотрудника Тогда
		
		Если Не ЗаблокироватьСотрудникаПриРедактировании(Форма.ВладелецФормы) Тогда
			
			УстановитьРежимТолькоПросмотраДополнительнойФормы(Форма);
			Форма.Модифицированность = Ложь;
			
			ОбъектЗаблокирован = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбъектЗаблокирован
		И БлокироватьФизическоеЛицо
		И Не ЗаблокироватьФизическоеЛицоПриРедактировании(Форма.ВладелецФормы, , БлокироватьСотрудника) Тогда
		
		УстановитьРежимТолькоПросмотраДополнительнойФормы(Форма);
		Форма.Модифицированность = Ложь;
		
		ОбъектЗаблокирован = Ложь;
		
	КонецЕсли;
	
	Возврат ОбъектЗаблокирован;
	
КонецФункции

Функция ЗаблокироватьОбъектВФормеВладельцеПриРедактированииИстории(Форма) Экспорт
	
	Возврат ЗарплатаКадрыКлиент.ЗаблокироватьОбъектВФормеВладельцеПриРедактированииИстории(Форма)
	
КонецФункции

#КонецОбласти

#Область ИсторияВзаимоотношенийСКомпанией

Процедура ПриПереходеНаСтраницуИстория(Форма, Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница.Имя <> "ГруппаИстория" Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьИсториюВзаимоотношений(Форма);

КонецПроцедуры

Процедура ЗаполнитьИсториюВзаимоотношений(Форма)
	
	Если Форма.Параметры.Ключ.Пустая() Тогда
		Возврат;
	КонецЕсли;

	Если Форма.ИсторияЗаполнена Тогда
		Возврат;
	КонецЕсли;
	
	НачатьЗаполнениеИсторииВзаимоотношений(Форма, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Форма.Параметры.Ключ));
	
КонецПроцедуры

Процедура ЗавершитьЗаполнениеИсторииВзаимоотношений(Форма, Результат, ДополнительныеПараметры) Экспорт

	УстановитьСостояниеОжиданияЗаполненияИсторииВзаимоотношений(Форма, Ложь);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Форма.ИсторияЗаполнена = Истина;

КонецПроцедуры

Процедура УстановитьСостояниеОжиданияЗаполненияИсторииВзаимоотношений(Форма, Включить)
	
	УстановитьСтраницу = Форма.Элементы.ГруппаИсторияЗаполнена;
	Если Включить Тогда
		УстановитьСтраницу = Форма.Элементы.ГруппаИсторияЗаполняется;
	КонецЕсли;

	Если Форма.Элементы.ГруппаИсторияСтраницы.ТекущаяСтраница <> УстановитьСтраницу Тогда
		Форма.Элементы.ГруппаИсторияСтраницы.ТекущаяСтраница = УстановитьСтраницу;
	КонецЕсли;

КонецПроцедуры

Процедура ОткрытьФормуЭлементаИсторииВзаимоотношений(Форма, Ссылка) Экспорт
	
	СтандартнаяОбработка = Истина;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсоналаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодборПерсоналаКлиент");
		МодульПодборПерсоналаКлиент.ПриОткрытииФормыЭлементаИсторииВзаимоотношений(Ссылка, Форма, СтандартнаяОбработка);
	КонецЕсли; 

	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Ссылка);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
	
	ОткрытьФорму(
		"Справочник.Сотрудники.ФормаОбъекта", 
		ПараметрыФормы, 
		Форма, , , , , 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#КонецОбласти

#КонецОбласти
