#Область ПрограммныйИнтерфейс

// Возвращает значение функциональной опции ДоступнаИнтеграцияСПлатформойСамозанятые.
//
// Возвращаемое значение:
//   Булево.
//
Функция ДоступнаИнтеграцияСПлатформойСамозанятые() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ДоступнаИнтеграцияСПлатформойСамозанятые");
	
КонецФункции

// Запускает длительную операцию по проверке аннулирования чека в сервисе Мой налог.
//
// Параметры:
//	СсылкаНаДокумент - ДокументСсылка - ссылка на документ, в котором запускается проверка.
//	ПараметрыОжиданияРезультата - Структура - параметры результата ожидания.
//	Идентификатор - УникальныйИдентификатор - уникальный идентификатор документа.
//
// Возвращаемое значение:
//   ДлительнаяОперация - Структура.
//
Функция ЗапуститьПроверкуРезультатаАннулированияЧекаВФоне(СсылкаНаДокумент, ПараметрыОжиданияРезультата, Идентификатор) Экспорт
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Ссылка", СсылкаНаДокумент);
	ПараметрыПроцедуры.Вставить("ПараметрыОжиданияРезультата", ПараметрыОжиданияРезультата);
	
	ДлительнаяОперация = 
		РегистрыСведений.ЧекиНПД.ЗапуститьПроверкуРезультатаАннулированияЧекаВФоне(
			ПараметрыПроцедуры, Идентификатор);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

// Запускает длительную операцию по аннулированию чека в сервисе Мой налог.
//
// Параметры:
//	ПараметрыАннулированияЧека - Структура - параметры аннулирования чека.
//	Идентификатор - УникальныйИдентификатор - уникальный идентификатор документа.
//
// Возвращаемое значение:
//   ДлительнаяОперация - Структура.
//
Функция ЗапуститьАннулированиеЧекаВФоне(ПараметрыАннулированияЧека, Идентификатор) Экспорт

	ДлительнаяОперация = 
		РегистрыСведений.ЧекиНПД.ЗапуститьАннулированиеЧекаВФоне(ПараметрыАннулированияЧека,
			Идентификатор);
			
	Возврат ДлительнаяОперация;
	
КонецФункции

// Возвращает подходящие данные выбора по номеру чека.
//
// Параметры:
//   СтрокаПоиска - Строка - часть номера чека, введенного пользователем.
//   Организация - СправочникСсылка.Организации - ссылка на организацию.
//   ИмяДокументаОснования - Строка - ограничение поиска чека по имени документа-основания.
//
// Возвращаемое значение:
//   СписокЗначений из Строка - список из номеров чеков и их представлений.
//
Функция ДанныеВыбораЧековДляАннулирования(СтрокаПоиска, Организация, ИмяДокументаОснования) Экспорт
	
	Возврат РегистрыСведений.ЧекиНПД.ДанныеВыбораЧековДляАннулирования(СтрокаПоиска, Организация, ИмяДокументаОснования);
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НачатьОбновлениеСтатусовОфлайнЧековНПД(Знач Организация) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ДоступнаИнтеграцияСПлатформойСамозанятые")
		Или Не УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(Организация, ТекущаяДатаСеанса()) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление статусов офлайн чеков НПД.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"ЧекиНПД.НачатьОбновлениеСтатусовОфлайнЧековНПД",
		Организация,
		ПараметрыВыполнения);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

Функция ПолучитьРезультатОбновленияСтатусовОфлайнЧеков(Запрос) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление статусов офлайн чеков НПД.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ДополнительныйРезультат = Запрос.ДополнительныйРезультат;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатформойСамозанятые.ПолучитьРезультатВыполненияВФоне",
		Запрос,
		ПараметрыВыполнения);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

// Изменяет статусы офлайн чеков по данным из сервиса и возвращает массив из обновленных чеков.
//
// Параметры:
//   Организация - СправочникСсылка.Организации - ссылка на организацию.
//   АдресСтатусовЧеков - Строка - адрес временного хранилища, 
//                                 содержащего таблицу ИнтеграцияСПлатформойСамозанятые.НовыйСтатусыЧеков()
//
// Возвращаемое значение:
//   Массив из Строка - массив номеров обновленных чеков.
//
Функция ИзменитьСтатусыОфлайнЧеков(Организация, АдресСтатусовЧеков) Экспорт
	
	Возврат ЧекиНПД.ИзменитьСтатусыОфлайнЧеков(Организация, АдресСтатусовЧеков);
	
КонецФункции

#КонецОбласти
