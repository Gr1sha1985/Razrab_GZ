////////////////////////////////////////////////////////////////////////////////
// Подсистема "Индивидуальный предприниматель"
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает дату начала действия прогрессивной шкалы НДФЛ
//
// Возвращаемое значение:
//   Дата
//
Функция ДатаНачалаПрогрессивнойШкалы() Экспорт
	
	Возврат Дата(2021, 1, 1);
	
КонецФункции

// Возвращает сумму налоговой базы налогоплательщика для применения повышенной ставки НДФЛ
//
// Возвращаемое значение:
//   Число
//
Функция ГраницаНалоговойБазыДляПримененияПовышеннойСтавки() Экспорт
	
	Возврат 5000000; // п. 1 ст. 224 НК РФ
	
КонецФункции

// Возвращает ставки налога НДФЛ, указанные в НК РФ
//
// Параметры:
//   Период - Дата - Дата, для которой определяются ставки налога
//
// Возвращаемое значение:
//   Структура - ставки налогов
//    * СтавкаНДФЛ           - Число - абзац 2 п. 1 ст. 224 НК РФ
//    * СтавкаНДФЛПовышенная - Число - абзац 3 п. 1 ст. 224 НК РФ
//
Функция НалоговыеСтавкиПоУмолчанию(Период = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СтавкаНДФЛ", 13);
	Результат.Вставить("СтавкаНДФЛПовышенная", Неопределено);
	
	Если Период = Неопределено Или Период >= ДатаНачалаПрогрессивнойШкалы() Тогда
		Результат.СтавкаНДФЛПовышенная = 15;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает сумму налога на доходы физических лиц
//
// Параметры:
//   НалоговаяБаза - Число - сумма налоговой базы
//   Период        - Дата - Дата, для которой рассчитывается сумма налога
//
// Возвращаемое значение:
//   Число
//
Функция СуммаНалога(НалоговаяБаза, Период = Неопределено) Экспорт
	
	НалоговыеСтавки = НалоговыеСтавкиПоУмолчанию(Период);
	ПредельнаяСуммаНалоговойБазы = ГраницаНалоговойБазыДляПримененияПовышеннойСтавки();
	
	НалоговаяБазаСвышеПредела = НалоговаяБаза - ПредельнаяСуммаНалоговойБазы;
	
	Если НалоговыеСтавки.СтавкаНДФЛПовышенная <> Неопределено И НалоговаяБазаСвышеПредела > 0 Тогда
		
		Результат = ПредельнаяСуммаНалоговойБазы * НалоговыеСтавки.СтавкаНДФЛ / 100
			+ НалоговаяБазаСвышеПредела * НалоговыеСтавки.СтавкаНДФЛПовышенная / 100;
	Иначе
		Результат = НалоговаяБаза * НалоговыеСтавки.СтавкаНДФЛ / 100;
	КонецЕсли;
	
	Возврат Окр(Результат, 0);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти