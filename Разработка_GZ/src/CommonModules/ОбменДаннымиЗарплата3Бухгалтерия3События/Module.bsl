////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обмен Зарплата 3 Бухгалтерия 3"
// Серверные процедуры и функции, обслуживающие подписки на события.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьНастройкиИспользованияОбменаЗарплата3Бухгалтерия3ПриЗаписи(Источник, Отказ) Экспорт
	
	// Проверка значения свойства ОбменДанными.Загрузка отсутствует по причине того, что в расположенным ниже коде,
	// реализована логика, которая должна выполняться в том числе при установке этого свойства равным Истина
	// (на стороне кода, который выполняет попытку записи в данный план обмена).

	
	ИмяПланаОбмена = ОбменДаннымиПовтИсп.ПолучитьИмяПланаОбмена(Источник.Ссылка);
	
	Если ИмяПланаОбмена <> "ОбменЗарплата3Бухгалтерия3" Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("Загрузка") Или Источник.ДополнительныеСвойства.Свойство("ПолучениеСообщенияОбмена") Тогда
		Возврат;
	КонецЕсли;
	
	ИсточникСсылка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка, "НомерОтправленного, НомерПринятого");
	Если ИсточникСсылка.НомерОтправленного <> Источник.НомерОтправленного Или ИсточникСсылка.НомерПринятого <> Источник.НомерПринятого Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ИспользованиеОбменаЗарплата3Бухгалтерия3ПоОрганизациям.СоздатьНаборЗаписей();
	КонстантаМенеджер = Константы.ИспользоватьОбменЗарплата3Бухгалтерия3ПоВсемОрганизациям;
	
	СинхронизацияДанныхЗарплатаКадры.ОбновитьНастройкиИспользованияОбменаДанными(ИмяПланаОбмена, КонстантаМенеджер, НаборЗаписей);
	
КонецПроцедуры

Процедура ЗаписатьНастройкиИспользованияОбменаЗарплата3Бухгалтерия3ПередУдалением(Источник, Отказ) Экспорт
	
	// Проверка значения свойства ОбменДанными.Загрузка отсутствует по причине того, что в расположенным ниже коде,
	// реализована логика, которая должна выполняться в том числе при установке этого свойства равным Истина
	// (на стороне кода, который выполняет попытку записи в данный план обмена).
	
	ИмяПланаОбмена = ОбменДаннымиПовтИсп.ПолучитьИмяПланаОбмена(Источник.Ссылка);
	
	Если ИмяПланаОбмена <> "ОбменЗарплата3Бухгалтерия3" Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ОтключитьМеханизмРегистрацииОбъектов") Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ИспользованиеОбменаЗарплата3Бухгалтерия3ПоОрганизациям.СоздатьНаборЗаписей();
	КонстантаМенеджер = Константы.ИспользоватьОбменЗарплата3Бухгалтерия3ПоВсемОрганизациям;
	
	СинхронизацияДанныхЗарплатаКадры.ОбновитьНастройкиИспользованияОбменаДанными(ИмяПланаОбмена, КонстантаМенеджер, НаборЗаписей, Источник.Ссылка);
	
КонецПроцедуры

#КонецОбласти
