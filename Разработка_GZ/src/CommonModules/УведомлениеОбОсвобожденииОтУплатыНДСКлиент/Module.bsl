#Область ПрограммныйИнтерфейс

// Процедура вызывается при создании формы уведомления об освобождении от уплаты НДС.
// Вместо стандартной формы уведомления открываем форму выбора периода.
// После выбора периода будет открыта специальная форма уведомления
// с дополнительными доработками - списком листов, баннером, заполнением.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма 1с-отчетности
//   ПараметрыУведомления - Структура - параметры, передаваемые в форму уведомления - содержит -
//     * UIDФорма1СОтчетность - Строка 
//     * ВидУведомления - ПеречислениеСсылка.ВидыУведомленийОСпецрежимахНалогообложения
//                - Уведомление об освобождении от уплаты НДС
//     * Данные - Неопределено
//     * Организация - СправочникСсылка.Организации
//
Процедура ПриВыбореВидаУведомления(Форма, ПараметрыУведомления) Экспорт

	Если ПараметрыУведомления.ВидУведомления = 
		ПредопределенноеЗначение("Перечисление.ВидыУведомленийОСпецрежимахНалогообложения.ОсвобождениеОтУплатыНДС") Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗакрытииФормыПараметров",
			ЭтотОбъект, ПараметрыУведомления);
		ПараметрыДляОткрытияФормы = Новый Структура("Организация", ПараметрыУведомления.Организация);

		ОткрытьФорму("ОбщаяФорма.ФормаЗаполненияПараметровУведомления",
			ПараметрыДляОткрытияФормы, Форма, , , , ОписаниеОповещения);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Процедура инициирует открытие новой формы уведомления об освобождении от уплаты НДС
//
// Параметры:
//   РезультатЗакрытия - Дата - дата на которую создается уведомление
//   ДополнительныеПараметры - Структура - параметры, передаваемые в форму уведомления - содержит -
//     * UIDФорма1СОтчетность - Строка 
//     * ВидУведомления - ПеречислениеСсылка.ВидыУведомленийОСпецрежимахНалогообложения
//                - Уведомление об освобождении от уплаты НДС
//     * Данные - Неопределено
//     * Организация - СправочникСсылка.Организации
//
Процедура ПриЗакрытииФормыПараметров(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Если РезультатЗакрытия <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("ДатаНачало", РезультатЗакрытия);
		СписокЛистов = УведомлениеОбОсвобожденииОтУплатыНДСВызовСервера.ПодготовитьСписокЛистов(
			ДополнительныеПараметры.Организация, РезультатЗакрытия);
		ДополнительныеПараметры.Вставить("СписокЛистовДерева", СписокЛистов);
		ОткрытьФорму("Отчет.РегламентированноеУведомлениеБумажныеФормы.Форма.ФормаОсвобождениеНДС",
			ДополнительныеПараметры);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

