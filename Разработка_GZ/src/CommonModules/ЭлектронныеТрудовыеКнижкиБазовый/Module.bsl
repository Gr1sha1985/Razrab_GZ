#Область СлужебныеПроцедурыИФункции

Функция РазрядКатегорияВидимость() Экспорт
	
	Возврат Ложь;
	
КонецФункции

Функция КодПоРееструДолжностейВидимость() Экспорт
	
	Возврат Ложь;
	
КонецФункции

Функция ИменаКадровыхДанныхСотрудниковДляНачалаУчета() Экспорт
	
	Возврат "Подразделение,Должность,ВидЗанятости,ВидСобытия,РабочееМестоПериодРегистрации,РабочееМестоРегистратор,ФИОПолные,ДатаПриема,ДатаУвольнения";
	
КонецФункции

Процедура УточнитьЗапросПолученияДанныхНаНачалоУчета(Запрос) Экспорт
	
	Запрос.УстановитьПараметр("РазрядКатегория", "");
	
КонецПроцедуры

Функция КодДолжностиПоРееструГосударственнойСлужбы(Должность) Экспорт
	
	Возврат "";
	
КонецФункции

Функция МероприятияСотрудникаДо2020Года(Сотрудник, Организация) Экспорт
	
	ДанныеСотрудника = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолучения.Организация = Организация;
	ПараметрыПолучения.НачалоПериода = ЭлектронныеТрудовыеКнижки.ДатаНачалаУчета() - 1;
	ПараметрыПолучения.ОкончаниеПериода = ПараметрыПолучения.НачалоПериода;
	ПараметрыПолучения.КадровыеДанные = ЭлектронныеТрудовыеКнижки.ИменаКадровыхДанныхСотрудниковДляНачалаУчета();
	ПараметрыПолучения.СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник);
	ПараметрыПолучения.КадровыеДанные = "ВидЗанятости";
	
	ЭлектронныеТрудовыеКнижки.УстановитьОтборПараметровПолученияСотрудниковОрганизации(ПараметрыПолучения);
	
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолучения, "ВТДляДанныхМероприятийДо2020Года");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК НачалоПериода,
		|	СотрудникиОрганизации.Период КАК ОкончаниеПериода,
		|	СотрудникиОрганизации.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТОтборСотрудниковДляПолученияМероприятийДо2020Года
		|ИЗ
		|	ВТДляДанныхМероприятийДо2020Года КАК СотрудникиОрганизации";
	
	Запрос.Выполнить();
	
	ПараметрыПолучения = КадровыйУчет.ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице(
		"ВТОтборСотрудниковДляПолученияМероприятийДо2020Года", "Сотрудник", "НачалоПериода", "ОкончаниеПериода");
	
	КадровыйУчет.СоздатьВТРабочиеМестаСотрудников(
		Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолучения, "ВТДанныеДляМероприятий");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеДляМероприятий.Период КАК ДатаМероприятия,
		|	ДанныеДляМероприятий.ФизическоеЛицо КАК Сотрудник,
		|	ДанныеДляМероприятий.Подразделение КАК Подразделение,
		|	ДанныеДляМероприятий.Должность КАК Должность,
		|	ВЫБОР
		|		КОГДА СотрудникиОрганизации.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЯвляетсяСовместителем,
		|	ВЫБОР
		|		КОГДА ДанныеДляМероприятий.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Прием)
		|		КОГДА ДанныеДляМероприятий.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Перемещение)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Перевод)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.ПустаяСсылка)
		|	КОНЕЦ КАК ВидМероприятия,
		|	ДанныеДляМероприятий.ДокументОснование КАК ДокументОснование,
		|	ДанныеДляМероприятий.Сотрудник КАК СотрудникЗаписи
		|ИЗ
		|	ВТДанныеДляМероприятий КАК ДанныеДляМероприятий
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДляДанныхМероприятийДо2020Года КАК СотрудникиОрганизации
		|		ПО ДанныеДляМероприятий.Сотрудник = СотрудникиОрганизации.Сотрудник
		|ГДЕ
		|	ДанныеДляМероприятий.ВидСобытия В (ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием), ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Перемещение))
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаМероприятия,
		|	ВидМероприятия";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеМероприятияСотрудника = ЭлектронныеТрудовыеКнижки.ПустаяСтруктураЗаписиОТрудовойДеятельности();
		ЗаполнитьЗначенияСвойств(ДанныеМероприятияСотрудника, Выборка);
		
		Если ДанныеМероприятияСотрудника.ВидМероприятия = Перечисления.ВидыМероприятийТрудовойДеятельности.Прием Тогда
			ДанныеМероприятияСотрудника.НаименованиеДокументаОснования = ЭлектронныеТрудовыеКнижки.НаименованиеДокументаПоВидуДокументаСобытия(Организация, "ПриемНаРаботу");
		ИначеЕсли ДанныеМероприятияСотрудника.ВидМероприятия = Перечисления.ВидыМероприятийТрудовойДеятельности.Перевод Тогда
			ДанныеМероприятияСотрудника.НаименованиеДокументаОснования = ЭлектронныеТрудовыеКнижки.НаименованиеДокументаПоВидуДокументаСобытия(Организация, "КадровыйПеревод");
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
			
			Если ДанныеМероприятияСотрудника.НомерДокументаОснования = Неопределено
				И ЗначениеЗаполнено(Выборка.ДокументОснование) Тогда
				
				РеквизитыПриказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Выборка.ДокументОснование, "Дата,Номер,НомерПриказа");
				Если РеквизитыПриказа <> Неопределено Тогда
					ДанныеМероприятияСотрудника.НомерДокументаОснования = ЗарплатаКадрыОтчеты.НомерНаПечать(РеквизитыПриказа.Номер, РеквизитыПриказа.НомерПриказа);
					ДанныеМероприятияСотрудника.ДатаДокументаОснования = РеквизитыПриказа.Дата;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеСотрудника.Добавить(ДанныеМероприятияСотрудника);
		
	КонецЦикла;
	
	Возврат ДанныеСотрудника;
	
КонецФункции

Функция ПредставлениеРазрядаКатегории(РазрядКатегория) Экспорт
	
	Возврат СокрЛП(РазрядКатегория);
	
КонецФункции

Процедура ЗаполнитьТипыОснованийУвольненияДляРегистрацииМероприятийТрудовойДеятельности(СписокОснований) Экспорт
	
	СписокОснований.Добавить(Тип("СправочникСсылка.ОснованияУвольнения"));
	
КонецПроцедуры

#КонецОбласти