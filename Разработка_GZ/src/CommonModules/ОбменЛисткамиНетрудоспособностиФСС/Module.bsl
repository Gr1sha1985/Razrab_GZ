#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает пространство имен пакета XDTO для обмена ЭЛН с ФСС.
//
// Возвращаемое значение:
//   Строка - Пространство имен пакета XDTO для обмена ЭЛН с ФСС.
//
Функция ПространствоИмен() Экспорт
	Возврат "http://www.fss.ru/integration/types/eln/ins/v01";
КонецФункции

// Возвращает версию формата обмена ЭЛН с ФСС.
//
// Возвращаемое значение:
//   Строка - Версия формата обмена ЭЛН с ФСС.
//
Функция Версия() Экспорт
	Возврат "2.0";
КонецФункции

// Проверяет что причина нетрудоспособности - карантин и что есть сведения об электронном листке нетрудоспособности.
//
// Возвращаемое значение:
//    Булево - Истина если причина нетрудоспособности - карантин и есть сведения об ЭЛН.
//
Функция ЭтоКарантинныйЭЛН(ПричинаНетрудоспособности, НомерЛН) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Возврат ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.Карантин
		И РегистрыСведений.СведенияОбЭЛН.ЭтоЭЛН(НомерЛН);
КонецФункции

// Проверяет по номеру ЛН что это ЭЛН и в регистре есть его исходный XML.
Функция ДоступенИсходныйXML(НомерЛН, ГоловнаяОрганизация) Экспорт
	Возврат РегистрыСведений.СведенияОбЭЛН.ДоступенИсходныйXML(НомерЛН, ГоловнаяОрганизация);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодготовкаНешифрованногоЗапроса

// Формирует параметры получения электронного листка нетрудоспособности.
//
// Параметры:
//   БольничныйЛист - ДокументОбъект.БольничныйЛист, ДанныеФормыСтруктура - Больничный.
//
// Возвращаемое значение:
//   Структура - Результат выгрузки в XML.
//       * Организация - СправочникСсылка.Организации - Организация, для которой получается ЭЛН.
//       * РегистрационныйНомерФСС - Строка - Рег. номер организации, для которой получается ЭЛН.
//       * ТекстXML - Строка - Сведения, необходимые для получения ЭЛН в формате XML.
//
Функция ВыгрузитьЗапросДляПолученияЭЛН(БольничныйЛист) Экспорт
	Отказ = Ложь;
	ДанныеБольничного = ДанныеБольничногоДляПолученияЭЛН(БольничныйЛист, Отказ);
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПакетXDTO = ФабрикаXDTO.Пакеты.Получить(ПространствоИмен());
	getPrivateLNDataRequest = ФабрикаXDTO.Создать(ПакетXDTO.КорневыеСвойства.Получить("getPrivateLNDataRequest").Тип);
	
	getPrivateLNDataRequest.lnCode = ДанныеБольничного.НомерЛисткаНетрудоспособности;
	getPrivateLNDataRequest.regNum = ДанныеБольничного.РегистрационныйНомерДляОбменаФСС;
	getPrivateLNDataRequest.snils  = ДанныеБольничного.СНИЛС;
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("UTF-8");
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, getPrivateLNDataRequest, "getPrivateLNDataRequest");
	
	Результат = Новый Структура("Организация, РегистрационныйНомерФСС, ТекстXML");
	Результат.Организация             = БольничныйЛист.Организация;
	Результат.РегистрационныйНомерФСС = ДанныеБольничного.РегистрационныйНомерДляОбменаФСС;
	Результат.ТекстXML                = ЗаписьXML.Закрыть();
	
	Возврат Результат;
КонецФункции

// Формирует параметры отправки реестра электронных листков нетрудоспособности.
//
// Параметры:
//   Документ - ДокументОбъект.РеестрДанныхЭЛНЗаполняемыхРаботодателем, ДанныеФормыСтруктура - Реестр.
//   ПомещатьВФайл - Булево - Истина, если результат надо поместить в файл и вернуть адрес во временном хранилище.
//   ИдентификаторФормы - УникальныйИдентификатор - Идентификатор формы. Обязательный, если ПомещатьВФайл = Истина.
//   ВнутреннийФормат - Булево - Истина, если реестр требуется выгрузить в сокращенном формате для БРО.
//   ИсключатьПринятые - Булево - Истина, если реестр отправляется в сервис и необходимо учитывать состояние строк.
//       Ложь, если выгрузка в файл и требуются все строки, без учета их состояния.
//
// Возвращаемое значение:
//   Структура - Результат выгрузки в XML.
//       * Организация - СправочникСсылка.Организации - Организация, для которой отправляется реестр ЭЛН.
//       * РегистрационныйНомерФСС - Строка - Рег. номер организации, для которой отправляется реестр ЭЛН.
//       * ТекстXML - Строка - Данные реестра в формате XML (электронное представление),
//                             либо пустая строка если ПомещатьВФайл = Истина.
//       * Адрес - Строка - Адрес временного хранилища, по которому размещены двоичные данные файла реестра в формате XML,
//                          либо пустая строка если ПомещатьВФайл = Ложь.
//
Функция ВыгрузитьЗапросДляОтправкиРеестраЭЛН(Документ, ПомещатьВФайл, ИдентификаторФормы, ВнутреннийФормат, ИсключатьПринятые) Экспорт
	РегистрационныйНомерДляОбменаФСС = УчетПособийСоциальногоСтрахованияКлиентСервер.РегистрационныйНомерДляОбменаФСС(Документ);
	
	ПакетXDTO = ФабрикаXDTO.Пакеты.Получить(ПространствоИмен());
	
	ParseReestrFileRequest = ФабрикаXDTO.Создать(ПакетXDTO.КорневыеСвойства.Получить("prParseReestrFileRequest").Тип);
	
	Request = ФабрикаXDTO.Создать(ParseReestrFileRequest.Свойства().Получить("request").Тип);
	Request.regNum = РегистрационныйНомерДляОбменаФСС;
	
	XMLFile = ФабрикаXDTO.Создать(Request.Свойства().Получить("pXmlFile").Тип);
	Rowset = ФабрикаXDTO.Создать(XMLFile.Свойства().Получить("rowset").Тип);
	
	Rowset.version          = Версия();
	Rowset.software         = Лев("1С:" + Метаданные.Синоним, 80);
	Rowset.version_software = Лев(Метаданные.Версия, 15);
	
	Rowset.author = Строка(Документ.РеестрСоставил);
	Rowset.phone  = Строка(Документ.ТелефонСоставителя);
	Rowset.email  = Строка(Документ.АдресЭлектроннойПочтыСоставителя);
	
	ФИО = Новый Структура("Руководитель, ГлавныйБухгалтер", Документ.Руководитель, Документ.ГлавныйБухгалтер);
	ЗаполнитьПолныеФИО(ФИО);
	
	ТипСтроки = Rowset.Свойства().Получить("row").Тип;
	
	Для Каждого ДанныеЛН Из Документ.ДанныеЭЛН Цикл
		
		Если ИсключатьПринятые И ДанныеЛН.Состояние = Перечисления.СостоянияЭЛНВРеестрахЭЛН.ПринятФСС Тогда
			Продолжить;
		КонецЕсли;
		
		Row = Rowset.row.Добавить(ФабрикаXDTO.Создать(ТипСтроки));
		
		Row.lnCode   = ДанныеЛН.НомерЛисткаНетрудоспособности;
		Row.snils    = УчетПособийСоциальногоСтрахованияКлиентСервер.СНИЛСВФорматеФСС(ДанныеЛН.СНИЛС);
		Row.employer = Строка(Документ.Организация);
		Row.emplFlag = ?(ДанныеЛН.ВидЗанятости = Перечисления.ВидыЗанятости.ОсновноеМестоРаботы, 1, 0);
		
		УстановитьЗначениеЕслиЗаполнено(Row.innPerson,       ДанныеЛН.ИНН);
		УстановитьЗначениеЕслиЗаполнено(Row.emplRegNo,       Документ.РегистрационныйНомерФСС);
		УстановитьЗначениеЕслиЗаполнено(Row.emplRegNo2,      Документ.ДополнительныйКодФСС);
		УстановитьЗначениеЕслиЗаполнено(Row.approve2,        ФИО.ГлавныйБухгалтер);
		УстановитьЗначениеЕслиЗаполнено(Row.baseAvgSal,      ДанныеЛН.БазаДляРасчетаСреднегоЗаработка);
		УстановитьЗначениеЕслиЗаполнено(Row.baseAvgDailySal, ДанныеЛН.СреднийДневнойЗаработок);
		УстановитьЗначениеЕслиЗаполнено(Row.calcCondition1,  ДанныеЛН.УсловияИсчисленияКод1);
		УстановитьЗначениеЕслиЗаполнено(Row.calcCondition2,  ДанныеЛН.УсловияИсчисленияКод2);
		УстановитьЗначениеЕслиЗаполнено(Row.calcCondition3,  ДанныеЛН.УсловияИсчисленияКод3);
		
		УстановитьЗначение(Row.emplParentNo,   Документ.КодПодчиненностиФСС);
		УстановитьЗначение(Row.approve1,       ФИО.Руководитель);
		УстановитьЗначение(Row.form1Dt,        ДанныеЛН.ДатаАктаН1);
		УстановитьЗначение(Row.returnDateEmpl, ДанныеЛН.ДатаНачалаРаботы);
		УстановитьЗначение(Row.dt1Ln,          ДанныеЛН.ДатаНачалаОплаты);
		УстановитьЗначение(Row.dt2Ln,          ДанныеЛН.ДатаОкончанияОплаты);
		
		РазностьСтажей = УчетПособийСоциальногоСтрахования.ПодсчитатьРазностьСтажейВГодахИМесяцах(
			ДанныеЛН.СтажРасширенныйЛет,
			ДанныеЛН.СтажРасширенныйМесяцев,
			ДанныеЛН.СтажЛет,
			ДанныеЛН.СтажМесяцев);
		Если РазностьСтажей.РазностьЛет <> 0 Или РазностьСтажей.РазностьМесяцев <> 0 Тогда
			Row.insurYy = ДанныеЛН.СтажРасширенныйЛет;
			Row.insurMm = ДанныеЛН.СтажРасширенныйМесяцев;
			Row.notInsurYy = РазностьСтажей.РазностьЛет;
			Row.notInsurMm = РазностьСтажей.РазностьМесяцев;
		Иначе
			Row.insurYy = ДанныеЛН.СтажЛет;
			Row.insurMm = ДанныеЛН.СтажМесяцев;
			Row.notInsurYy = Неопределено;
			Row.notInsurMm = Неопределено;
		КонецЕсли;
		
		Row.emplPayment = ДанныеЛН.СуммаОплатыЗаСчетРаботодателя;
		Row.fssPayment  = ДанныеЛН.СуммаОплатыЗаСчетФСС;
		Row.payment     = ДанныеЛН.СуммаОплатыЗаСчетРаботодателя + ДанныеЛН.СуммаОплатыЗаСчетФСС;
		
		Если ДанныеЛН.Исправление Тогда
			УстановитьЗначениеЕслиЗаполнено(Row.correctionReason, ДанныеЛН.КодПричиныИсправления);
			УстановитьЗначениеЕслиЗаполнено(Row.correctionNote,   СокрЛП(ДанныеЛН.ОписаниеПричиныИсправления));
		КонецЕсли;
		
	КонецЦикла;
	
	XMLFile.rowset = Rowset;
	Request.pXmlFile = XMLFile;
	ParseReestrFileRequest.request = Request;
	
	Если ВнутреннийФормат Тогда
		ТекстXML = СериализацияБЗК.ОбъектXDTOВСтрокуXML(Rowset, "rowset", Ложь);
	Иначе
		ПакетXDTO = ФабрикаXDTO.Пакеты.Получить("http://schemas.xmlsoap.org/soap/envelope/");
		Envelope = ФабрикаXDTO.Создать(ПакетXDTO.КорневыеСвойства.Получить("Envelope").Тип);
		Envelope.Body = ФабрикаXDTO.Создать(Envelope.Свойства().Получить("Body").Тип);
		Envelope.Body.Добавить(ФормаXML.Элемент, ПространствоИмен(), "prParseReestrFileRequest", ParseReestrFileRequest);
		ТекстXML = СериализацияБЗК.ОбъектXDTOВСтрокуXML(Envelope, "Envelope", Истина);
	КонецЕсли;
	
	Результат = Новый Структура("Организация, РегистрационныйНомерФСС, ТекстXML, Адрес");
	Результат.Организация             = Документ.Организация;
	Результат.РегистрационныйНомерФСС = РегистрационныйНомерДляОбменаФСС;
	Результат.ТекстXML                = ТекстXML;
	
	Если ПомещатьВФайл Тогда
		ДвоичныеДанные = ЗарплатаКадры.СтрокаВДвоичныеДанные(ТекстXML, "UTF-8", Истина);
		Результат.Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ИдентификаторФормы);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ДанныеБольничногоДляПолученияЭЛН(БольничныйЛист, Отказ)
	Результат = Новый Структура("НомерЛисткаНетрудоспособности, РегистрационныйНомерДляОбменаФСС, СНИЛС");
	
	// Номер больничного листа.
	Результат.НомерЛисткаНетрудоспособности = БольничныйЛист.НомерЛисткаНетрудоспособности;
	
	// Регистрационный номер ФСС организации (страхователя).
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БольничныйЛист.Организация, "РегистрационныйНомерФСС, ДополнительныйКодФСС");
	Результат.РегистрационныйНомерДляОбменаФСС = УчетПособийСоциальногоСтрахованияКлиентСервер.РегистрационныйНомерДляОбменаФСС(РеквизитыОрганизации);
	
	// СНИЛС сотрудника на которого оформлен листок нетрудоспособности.
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, БольничныйЛист.Сотрудник, "СтраховойНомерПФР");
	Если КадровыеДанные.Количество() > 0 Тогда
		Результат.СНИЛС = УчетПособийСоциальногоСтрахованияКлиентСервер.СНИЛСВФорматеФСС(
			КадровыеДанные[0].СтраховойНомерПФР);
	КонецЕсли;
	
	// Проверка результатов.
	Если Не ЗначениеЗаполнено(Результат.НомерЛисткаНетрудоспособности) Тогда
		Текст = НСтр("ru = 'Не заполнен номер листка нетрудоспособности'");
		ОбщегоНазначения.СообщитьПользователю(Текст, , "НомерЛисткаНетрудоспособности", "Объект", Отказ);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Результат.РегистрационныйНомерДляОбменаФСС) Тогда
		Текст = НСтр("ru = 'У организации не заполнен регистрационный номер ФСС'");
		ОбщегоНазначения.СообщитьПользователю(Текст, , "Организация", "Объект", Отказ);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Результат.СНИЛС) Тогда
		Текст = НСтр("ru = 'У сотрудника не указан СНИЛС'");
		ОбщегоНазначения.СообщитьПользователю(Текст, , "Сотрудник", "Объект", Отказ);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Получает полные ФИО физических лиц.
//
// Параметры:
//   СтруктураФИО - Структура - По указанным ключам структуры будут размещены полные ФИО.
//       * Ключ - Строка - Имя ключа, по которому будет размещено полное ФИО.
//       * Значение - СправочникСсылка.ФизическиеЛица - Ссылка физ. лица, для которого требуется получить ФИО.
//           После выполнения процедуры превращается в тип "Строка".
//
Процедура ЗаполнитьПолныеФИО(СтруктураФИО)
	МассивФизическихЛиц = Новый Массив;
	Для Каждого КлючИЗначение Из СтруктураФИО Цикл
		МассивФизическихЛиц.Добавить(КлючИЗначение.Значение);
	КонецЦикла;
	
	ТаблицаКадровыхДанных = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, МассивФизическихЛиц, "ФИОПолные");
	
	Для Каждого КлючИЗначение Из СтруктураФИО Цикл
		КадровыеДанныеФизическогоЛица = ТаблицаКадровыхДанных.Найти(КлючИЗначение.Значение, "ФизическоеЛицо");
		Если КадровыеДанныеФизическогоЛица <> Неопределено Тогда
			ФИО = КадровыеДанныеФизическогоЛица.ФИОПолные;
		ИначеЕсли ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			ФИО = Строка(КлючИЗначение.Значение);
		Иначе
			ФИО = "";
		КонецЕсли;
		СтруктураФИО.Вставить(КлючИЗначение.Ключ, ФИО);
	КонецЦикла;
КонецПроцедуры

Процедура УстановитьЗначениеЕслиЗаполнено(ИзменяемоеЗначение, Значение)
	Если ЗначениеЗаполнено(Значение) Тогда
		ИзменяемоеЗначение = Значение;
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьЗначение(ИзменяемоеЗначение, Значение)
	Если ЗначениеЗаполнено(Значение) Тогда
		ИзменяемоеЗначение = Значение;
	Иначе
		ИзменяемоеЗначение = Неопределено;
	КонецЕсли;
КонецПроцедуры

Функция КодыПричинИсправления(Список = Неопределено) Экспорт
	Если ТипЗнч(Список) <> Тип("СписокЗначений") Тогда
		Список = Новый СписокЗначений;
	КонецЕсли;
	
	Список.Добавить("01", НСтр("ru = 'Работником представлены дополнительные сведения для расчета'"));
	Список.Добавить("02", НСтр("ru = 'Работником представлено свидетельство ИНН'"));
	Список.Добавить("03", НСтр("ru = 'Изменены регистрационные данные работодателя/сведения о должностных лицах работодателя'"));
	Список.Добавить("04", НСтр("ru = 'Уточнены условия труда работника/условия исчисления пособия (включая Акт ф. Н-1)'"));
	Список.Добавить("05", НСтр("ru = 'Выявлены ошибки в расчете пособия/подсчете страхового стажа'"));
	Список.Добавить("06", НСтр("ru = 'Ошибка оператора'"));
	
	Для Каждого Элемент Из Список Цикл
		Элемент.Представление = Элемент.Значение + ". " + Элемент.Представление;
	КонецЦикла;
	
	Возврат Список;
КонецФункции

#КонецОбласти

#Область ЧтениеРасшифрованногоОтвета

Функция ЗагрузитьОтветСервисаФСС(Документ, Операция, АдресРасшифрованногоОтвета, ЗагрузкаИзФайла = Ложь) Экспорт
	Результат = Новый Структура("Отказ, ТекстСообщенияФСС");
	Результат.Отказ = Ложь;
	Результат.ТекстСообщенияФСС = "";
	
	ИсходныйXML = Неопределено;
	СтруктураDOM = Неопределено;
	Попытка
		ИсходныйXML = СериализацияБЗК.СтрокаXMLИзВременногоХранилища(АдресРасшифрованногоОтвета);
		СтруктураDOM = СериализацияБЗК.СтруктураDOM(ИсходныйXML);
	Исключение
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Ошибка при получении содержимого XML: %1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗарплатаКадрыОтображениеОшибок.СообщитьПользователю(ТекстОшибки);
	КонецПопытки;
	Если СтруктураDOM = Неопределено Тогда
		РасшифрованныйОтвет = ПолучитьИзВременногоХранилища(АдресРасшифрованногоОтвета);
		СтруктураDOM = СериализацияБЗК.СтруктураDOM(РасшифрованныйОтвет);
	КонецЕсли;
	
	УдалитьИзВременногоХранилища(АдресРасшифрованногоОтвета);
	
	УзелСообщения = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'mess']");
	Если УзелСообщения = Неопределено Тогда
		УзелСообщения = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'MESS']");
	КонецЕсли;
	Если УзелСообщения <> Неопределено Тогда
		ТекстовоеСодержимое = СокрЛП(Строка(УзелСообщения.ТекстовоеСодержимое));
		Если ТекстовоеСодержимое <> "" Тогда
			Результат.ТекстСообщенияФСС = НСтр("ru = 'При обмене с ФСС возникли ошибки. Ответ ФСС:'") + Символы.ПС + ТекстовоеСодержимое;
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		Если Операция = "getPrivateLNData" Тогда
			
			УзелЭЛН = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'responseRow']");
			Если УзелЭЛН = Неопределено Тогда
				УзелЭЛН = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'ROW']");
				Если УзелЭЛН = Неопределено Тогда
					ВызватьИсключение ?(ПустаяСтрока(Результат.ТекстСообщенияФСС), НСтр("ru = 'Узел ""responseRow"" не найден'"), Результат.ТекстСообщенияФСС);
				КонецЕсли;
			КонецЕсли;
			
			СтрокаXML = СериализацияБЗК.ОбъектDOMВСтрокуXML(УзелЭЛН);
			ОбъектЭЛН = СериализацияБЗК.ОбъектXDTOИзСтрокиXML(СтрокаXML);
			
			УзелОтвета = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'fileOperationsLnUserGetPrivateLNDataOut']");
			Если УзелОтвета = Неопределено Тогда
				УзелОтвета = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'FileOperationsLnUserGetPrivateLNDataOut']");
			КонецЕсли;
			УзелКодаОтвета = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "*[local-name() = 'status']", УзелОтвета);
			Если УзелКодаОтвета = Неопределено Тогда
				УзелКодаОтвета = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "*[local-name() = 'STATUS']", УзелОтвета);
			КонецЕсли;
			КодОтвета = ?(УзелКодаОтвета = Неопределено, "", СокрЛП(УзелКодаОтвета.ТекстовоеСодержимое));
			Если КодОтвета <> "1" И ЗначениеЗаполнено(Результат.ТекстСообщенияФСС) Тогда
				ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Результат.Отказ, Результат.ТекстСообщенияФСС, "");
				Возврат Результат;
			КонецЕсли;
			
			Если ОбъектЭЛН.Свойства().Получить("lnCode") = Неопределено
				И ОбъектЭЛН.Свойства().Получить("LN_CODE") <> Неопределено Тогда
				ДанныеИзСервиса = ОтветСервисаПолученияЭЛНСтаройВерсии(ОбъектЭЛН, Результат.Отказ);
			Иначе
				ДанныеИзСервиса = ОтветСервисаПолученияЭЛН(ОбъектЭЛН, Результат.Отказ);
			КонецЕсли;
			
			Если Результат.Отказ Тогда
				Возврат Результат;
			КонецЕсли;
			
			Если ТипЗнч(Документ) = Тип("Структура") Тогда
				// Метод вызван только для актуализации хеша ЭЛН.
				РегистрыСведений.СведенияОбЭЛН.ЗаписатьПервичныеСведения(
					ДанныеИзСервиса.НомерЛН,
					ДанныеИзСервиса.ХешЭЛН,
					ДанныеИзСервиса.СостояниеЭЛНВФСС,
					Документ.Организация,
					Неопределено,
					ИсходныйXML);
			Иначе
				// Метод вызван для заполнения больничного.
				ЗагрузитьРезультатПолученияЭЛН(Документ, ДанныеИзСервиса, ИсходныйXML, Результат.Отказ);
			КонецЕсли;
			
		ИначеЕсли Операция = "prParseReestrFile" Тогда
			
			УзелRowset = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'infoRowset']");
			Если УзелRowset = Неопределено Тогда
				УзелRow = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'infoRow']");
				Если УзелRow = Неопределено Тогда
					ВызватьИсключение СокрП(НСтр("ru = 'Узел ""infoRowset"" не найден'") + Символы.ПС + Результат.ТекстСообщенияФСС);
				КонецЕсли;
				УзелRowset = УзелRow.РодительскийУзел;
			КонецЕсли;
			
			СтрокаXML = СериализацияБЗК.ОбъектDOMВСтрокуXML(УзелRowset);
			ОбъектRowset = СериализацияБЗК.ОбъектXDTOИзСтрокиXML(СтрокаXML);
			ЭлементыRow = СериализацияБЗК.СписокXDTO(ОбъектRowset, "infoRow");
			
			ЗагрузитьРезультатОтправкиРеестраЭЛН(Документ, ЭлементыRow, Результат);
			
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Ошибка при загрузке ответа ФСС по операции ""%1"":
				|%2
				|
				|XML ответа:
				|%3'"),
			Операция,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке),
			ИсходныйXML);
		ЗарплатаКадрыОтображениеОшибок.СообщитьПользователю(ТекстОшибки);
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область ЗагрузкаЭЛН

Функция ОтветСервисаПолученияЭЛН(ОбъектЭЛН, Отказ)
	ИменаПолейИзСервиса = 
	"approve1,
	|approve2,
	|baseAvgDailySal,
	|baseAvgSal,
	|birthday,
	|calcCondition1,
	|calcCondition2,
	|calcCondition3,
	|calcCondition4,
	|date1,
	|date2,
	|dt1Ln,
	|dt2Ln,
	|duplicateFlag,
	|emplFlag,
	|emplParentNo,
	|emplPayment,
	|emplRegNo,
	|emplRegNo2,
	|employer,
	|form1Dt,
	|fssPayment,
	|gender,
	|hospitalBreach,
	|hospitalDt1,
	|hospitalDt2,
	|innPerson,
	|insurMm,
	|insurYy,
	|lnCode,
	|lnDate,
	|lnHash,
	|lnResult,
	|lnState,
	|lpuAddress,
	|lpuName,
	|lpuOgrn,
	|mseDt1,
	|mseDt2,
	|mseDt3,
	|mseInvalidGroup,
	|name,
	|notInsurMm,
	|notInsurYy,
	|patronymic,
	|payment,
	|pregn12wFlag,
	|prevLnCode,
	|primaryFlag,
	|reason1,
	|reason2,
	|reason3,
	|returnDateEmpl,
	|returnrDateEmpl,
	|snils,
	|surname,
	|voucherNo,
	|voucherOgrn";
	
	ДанныеИзСервиса = Новый Структура(ИменаПолейИзСервиса);
	ЗаполнитьЗначенияСвойств(ДанныеИзСервиса, ОбъектЭЛН);
	
	// Базовые проверки спецификации.
	БазовыеПоля = СтрРазделить("snils, lnCode", ", ", Ложь);
	Для Каждого ИмяПоля Из БазовыеПоля Цикл
		Если Не ЗначениеЗаполнено(ДанныеИзСервиса[ИмяПоля]) Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Файл не соответствует спецификации: Не заполнено поле ""%1"".'"), ИмяПоля);
			ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Отказ, ТекстОшибки, ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	ДанныеИзСервиса.Вставить("НомерЛН", ЗначениеXMLТипаСтрока(ДанныеИзСервиса.lnCode));
	ДанныеИзСервиса.Вставить("ДатаЛН", СериализацияБЗК.ЗначениеXML(ДанныеИзСервиса.lnDate, Тип("Дата")));
	ДанныеИзСервиса.Вставить("ХешЭЛН", ЗначениеXMLТипаСтрока(ДанныеИзСервиса.lnHash));
	
	ДанныеИзСервиса.Вставить("СНИЛС", УчетПособийСоциальногоСтрахованияКлиентСервер.СНИЛСВФорматеИБ(ДанныеИзСервиса.snils));
	ДанныеИзСервиса.Вставить("Фамилия",  ЗначениеXMLТипаСтрока(ДанныеИзСервиса.surname));
	ДанныеИзСервиса.Вставить("Имя",      ЗначениеXMLТипаСтрока(ДанныеИзСервиса.name));
	ДанныеИзСервиса.Вставить("Отчество", ЗначениеXMLТипаСтрока(ДанныеИзСервиса.patronymic));
	
	ДанныеИзСервиса.Вставить("ЯвляетсяПродолжениемБолезни", Не СериализацияБЗК.ЗначениеXML(ДанныеИзСервиса.primaryFlag, Тип("Булево")));
	ДанныеИзСервиса.Вставить("НомерПервичногоЛисткаНетрудоспособности", Строка(ДанныеИзСервиса.prevLnCode));
	
	ДанныеИзСервиса.Вставить("ПериодыНетрудоспособности", СериализацияБЗК.СписокXDTO(ОбъектЭЛН, "treatPeriods.treatFullPeriod"));
	ДанныеИзСервиса.Вставить("ПериодыУходаЗаРодственниками", СериализацияБЗК.СписокXDTO(ОбъектЭЛН, "servData.servFullData"));
	
	СостояниеЭЛНВФСС = Перечисления.СостоянияЭЛНВФСС.НайтиПоКодуФСС(ДанныеИзСервиса.lnState);
	ДанныеИзСервиса.Вставить("СостояниеЭЛНВФСС", СостояниеЭЛНВФСС);
	
	Возврат ДанныеИзСервиса;
КонецФункции

Функция ОтветСервисаПолученияЭЛНСтаройВерсии(ОбъектЭЛН, Отказ)
	ИменаПолейСтарогоФормата = 
	"APPROVE1,
	|APPROVE2,
	|BASE_AVG_DAILY_SAL,
	|BASE_AVG_SAL,
	|BIRTHDAY,
	|BOZ_FLAG,
	|CALC_CONDITION1,
	|CALC_CONDITION2,
	|CALC_CONDITION3,
	|CALC_CONDITION4,
	|DATE1,
	|DATE2,
	|DT1_LN,
	|DT2_LN,
	|DUPLICATE_FLAG,
	|EMPL_FLAG,
	|EMPL_PARENT_NO,
	|EMPL_PAYMENT,
	|EMPL_REG_NO,
	|EMPL_REG_NO2,
	|EMPLOYER,
	|FORM1_DT,
	|FSS_PAYMENT,
	|GENDER,
	|HOSPITAL_BREACH,
	|HOSPITAL_DT1,
	|HOSPITAL_DT2,
	|INN_PERSON,
	|INSUR_MM,
	|INSUR_YY,
	|LN_CODE,
	|LN_DATE,
	|LN_HASH,
	|LN_RESULT,
	|LN_STATE,
	|LPU_ADDRESS,
	|LPU_EMPL_FLAG,
	|LPU_EMPLOYER,
	|LPU_NAME,
	|LPU_OGRN,
	|MSE_DT1,
	|MSE_DT2,
	|MSE_DT3,
	|MSE_INVALID_GROUP,
	|NAME,
	|NOT_INSUR_MM,
	|NOT_INSUR_YY,
	|PARENT_CODE,
	|PATRONIMIC,
	|PAYMENT,
	|PREGN12W_FLAG,
	|PREV_LN_CODE,
	|PRIMARY_FLAG,
	|REASON1,
	|REASON2,
	|REASON3,
	|RETURN_DATE_EMPL,
	|SERV1_AGE,
	|SERV1_FIO,
	|SERV1_MM,
	|SERV1_RELATION_CODE,
	|SERV2_AGE,
	|SERV2_FIO,
	|SERV2_MM,
	|SERV2_RELATION_CODE,
	|SNILS,
	|SURNAME,
	|VOUCHER_NO,
	|VOUCHER_OGRN";
	
	ДанныеВСтаромФормате = Новый Структура(ИменаПолейСтарогоФормата);
	ЗаполнитьЗначенияСвойств(ДанныеВСтаромФормате, ОбъектЭЛН);
	
	ДанныеИзСервиса = Новый Структура;
	ДанныеИзСервиса.Вставить("approve1", ДанныеВСтаромФормате.APPROVE1);
	ДанныеИзСервиса.Вставить("approve2", ДанныеВСтаромФормате.APPROVE2);
	ДанныеИзСервиса.Вставить("baseAvgDailySal", ДанныеВСтаромФормате.BASE_AVG_DAILY_SAL);
	ДанныеИзСервиса.Вставить("baseAvgSal", ДанныеВСтаромФормате.BASE_AVG_SAL);
	ДанныеИзСервиса.Вставить("birthday", ДанныеВСтаромФормате.BIRTHDAY);
	ДанныеИзСервиса.Вставить("calcCondition1", ДанныеВСтаромФормате.CALC_CONDITION1);
	ДанныеИзСервиса.Вставить("calcCondition2", ДанныеВСтаромФормате.CALC_CONDITION2);
	ДанныеИзСервиса.Вставить("calcCondition3", ДанныеВСтаромФормате.CALC_CONDITION3);
	ДанныеИзСервиса.Вставить("calcCondition4", ДанныеВСтаромФормате.CALC_CONDITION4);
	ДанныеИзСервиса.Вставить("date1", ДанныеВСтаромФормате.DATE1);
	ДанныеИзСервиса.Вставить("date2", ДанныеВСтаромФормате.DATE2);
	ДанныеИзСервиса.Вставить("dt1Ln", ДанныеВСтаромФормате.DT1_LN);
	ДанныеИзСервиса.Вставить("dt2Ln", ДанныеВСтаромФормате.DT2_LN);
	ДанныеИзСервиса.Вставить("duplicateFlag", ДанныеВСтаромФормате.DUPLICATE_FLAG);
	ДанныеИзСервиса.Вставить("emplFlag", ДанныеВСтаромФормате.EMPL_FLAG);
	ДанныеИзСервиса.Вставить("emplParentNo", ДанныеВСтаромФормате.EMPL_PARENT_NO);
	ДанныеИзСервиса.Вставить("emplPayment", ДанныеВСтаромФормате.EMPL_PAYMENT);
	ДанныеИзСервиса.Вставить("emplRegNo", ДанныеВСтаромФормате.EMPL_REG_NO);
	ДанныеИзСервиса.Вставить("emplRegNo2", ДанныеВСтаромФормате.EMPL_REG_NO2);
	ДанныеИзСервиса.Вставить("employer", ДанныеВСтаромФормате.EMPLOYER);
	ДанныеИзСервиса.Вставить("form1Dt", ДанныеВСтаромФормате.FORM1_DT);
	ДанныеИзСервиса.Вставить("fssPayment", ДанныеВСтаромФормате.FSS_PAYMENT);
	ДанныеИзСервиса.Вставить("gender", ДанныеВСтаромФормате.GENDER);
	ДанныеИзСервиса.Вставить("hospitalBreach", Неопределено);
	ДанныеИзСервиса.Вставить("hospitalDt1", ДанныеВСтаромФормате.HOSPITAL_DT1);
	ДанныеИзСервиса.Вставить("hospitalDt2", ДанныеВСтаромФормате.HOSPITAL_DT2);
	ДанныеИзСервиса.Вставить("innPerson", ДанныеВСтаромФормате.INN_PERSON);
	ДанныеИзСервиса.Вставить("insurMm", ДанныеВСтаромФормате.INSUR_MM);
	ДанныеИзСервиса.Вставить("insurYy", ДанныеВСтаромФормате.INSUR_YY);
	ДанныеИзСервиса.Вставить("lnCode", ДанныеВСтаромФормате.LN_CODE);
	ДанныеИзСервиса.Вставить("lnDate", ДанныеВСтаромФормате.LN_DATE);
	ДанныеИзСервиса.Вставить("lnHash", ДанныеВСтаромФормате.LN_HASH);
	ДанныеИзСервиса.Вставить("lnResult", Неопределено);
	ДанныеИзСервиса.Вставить("lnState", ДанныеВСтаромФормате.LN_STATE);
	ДанныеИзСервиса.Вставить("lpuAddress", ДанныеВСтаромФормате.LPU_ADDRESS);
	ДанныеИзСервиса.Вставить("lpuName", ДанныеВСтаромФормате.LPU_NAME);
	ДанныеИзСервиса.Вставить("lpuOgrn", ДанныеВСтаромФормате.LPU_OGRN);
	ДанныеИзСервиса.Вставить("mseDt1", ДанныеВСтаромФормате.MSE_DT1);
	ДанныеИзСервиса.Вставить("mseDt2", ДанныеВСтаромФормате.MSE_DT2);
	ДанныеИзСервиса.Вставить("mseDt3", ДанныеВСтаромФормате.MSE_DT3);
	ДанныеИзСервиса.Вставить("mseInvalidGroup", ДанныеВСтаромФормате.MSE_INVALID_GROUP);
	ДанныеИзСервиса.Вставить("name", ДанныеВСтаромФормате.NAME);
	ДанныеИзСервиса.Вставить("notInsurMm", ДанныеВСтаромФормате.NOT_INSUR_MM);
	ДанныеИзСервиса.Вставить("notInsurYy", ДанныеВСтаромФормате.NOT_INSUR_YY);
	ДанныеИзСервиса.Вставить("patronymic", ДанныеВСтаромФормате.PATRONIMIC);
	ДанныеИзСервиса.Вставить("payment", ДанныеВСтаромФормате.PAYMENT);
	ДанныеИзСервиса.Вставить("pregn12wFlag", Неопределено);
	ДанныеИзСервиса.Вставить("prevLnCode", ДанныеВСтаромФормате.PREV_LN_CODE);
	ДанныеИзСервиса.Вставить("primaryFlag", ДанныеВСтаромФормате.PRIMARY_FLAG);
	ДанныеИзСервиса.Вставить("reason1", ДанныеВСтаромФормате.REASON1);
	ДанныеИзСервиса.Вставить("reason2", ДанныеВСтаромФормате.REASON2);
	ДанныеИзСервиса.Вставить("reason3", ДанныеВСтаромФормате.REASON3);
	ДанныеИзСервиса.Вставить("returnDateEmpl", ДанныеВСтаромФормате.RETURN_DATE_EMPL);
	ДанныеИзСервиса.Вставить("returnrDateEmpl", "");
	ДанныеИзСервиса.Вставить("snils", ДанныеВСтаромФормате.SNILS);
	ДанныеИзСервиса.Вставить("surname", ДанныеВСтаромФормате.SURNAME);
	ДанныеИзСервиса.Вставить("voucherNo", ДанныеВСтаромФормате.VOUCHER_NO);
	ДанныеИзСервиса.Вставить("voucherOgrn", ДанныеВСтаромФормате.VOUCHER_OGRN);
	
	// Адаптация поля pregn12wFlag.
	Если ДанныеВСтаромФормате.PREGN12W_FLAG = "1" Тогда
		ДанныеИзСервиса.Вставить("pregn12wFlag", "true");
	ИначеЕсли ДанныеВСтаромФормате.PREGN12W_FLAG = "0" Тогда
		ДанныеИзСервиса.Вставить("pregn12wFlag", "false");
	КонецЕсли;
	
	// Адаптация вложенных полей lnResult.
	Если ДанныеВСтаромФормате.LN_RESULT <> Неопределено Тогда
		ЗначенияВСтаромФормате = Новый Структура("MSE_RESULT, OTHER_STATE_DT, RETURN_DATE_LPU, NEXT_LN_CODE");
		ЗаполнитьЗначенияСвойств(ЗначенияВСтаромФормате, ДанныеВСтаромФормате.LN_RESULT);
		
		Значения = Новый Структура;
		Значения.Вставить("mseResult", ЗначенияВСтаромФормате.MSE_RESULT);
		Значения.Вставить("otherStateDt", ЗначенияВСтаромФормате.OTHER_STATE_DT);
		Значения.Вставить("returnDateLpu", ЗначенияВСтаромФормате.RETURN_DATE_LPU);
		Значения.Вставить("nextLnCode", ЗначенияВСтаромФормате.NEXT_LN_CODE);
		
		ДанныеИзСервиса.Вставить("lnResult", Значения);
	КонецЕсли;
	
	// Адаптация вложенных полей hospitalBreach.
	Если ДанныеВСтаромФормате.HOSPITAL_BREACH <> Неопределено Тогда
		ЗначенияВСтаромФормате = Новый Структура("HOSPITAL_BREACH_CODE, HOSPITAL_BREACH_DT");
		ЗаполнитьЗначенияСвойств(ЗначенияВСтаромФормате, ДанныеВСтаромФормате.HOSPITAL_BREACH);
		
		Значения = Новый Структура;
		Значения.Вставить("hospitalBreachCode", ЗначенияВСтаромФормате.HOSPITAL_BREACH_CODE);
		Значения.Вставить("hospitalBreachDt", ЗначенияВСтаромФормате.HOSPITAL_BREACH_DT);
		
		ДанныеИзСервиса.Вставить("hospitalBreach", Значения);
	КонецЕсли;
	
	// Адаптация вложенных полей treatPeriods.treatFullPeriod.
	ДатаНачала    = '39991231235959';
	ДатаОкончания = '00010101';
	ДатаНачалаXML    = "";
	ДатаОкончанияXML = "";
	ПериодыНетрудоспособности = Новый Массив;
	ПериодыНетрудоспособностиВСтаромФормате = СериализацияБЗК.СписокXDTO(ОбъектЭЛН, "TREAT_PERIODS.TREAT_FULL_PERIOD");
	Для Каждого СтрокаТаблицыПолныхПериодов Из ПериодыНетрудоспособностиВСтаромФормате Цикл
		
		СвойстваПериодаВСтаромФормате = Новый Структура("TREAT_CHAIRMAN, TREAT_CHAIRMAN_ROLE, TREAT_PERIOD");
		ЗаполнитьЗначенияСвойств(СвойстваПериодаВСтаромФормате, СтрокаТаблицыПолныхПериодов);
		
		ПодробностиПериода = Неопределено;
		Если СвойстваПериодаВСтаромФормате.TREAT_PERIOD <> Неопределено Тогда
			
			ПодробностиПериодаВСтаромФормате = Новый Структура("TREAT_DT1, TREAT_DT2, TREAT_DOCTOR_ROLE, TREAT_DOCTOR");
			ЗаполнитьЗначенияСвойств(ПодробностиПериодаВСтаромФормате, СвойстваПериодаВСтаромФормате.TREAT_PERIOD);
			
			ПодробностиПериода = Новый Структура;
			ПодробностиПериода.Вставить("treatDt1",        ПодробностиПериодаВСтаромФормате.TREAT_DT1);
			ПодробностиПериода.Вставить("treatDt2",        ПодробностиПериодаВСтаромФормате.TREAT_DT2);
			ПодробностиПериода.Вставить("treatDoctorRole", ПодробностиПериодаВСтаромФормате.TREAT_DOCTOR_ROLE);
			ПодробностиПериода.Вставить("treatDoctor",     ПодробностиПериодаВСтаромФормате.TREAT_DOCTOR);
			
			// Для заполнения обязательных полей servDt1 и servDt2 требуется вычислить начало и окончание нетрудоспособности.
			Дата1 = СериализацияБЗК.ЗначениеXML(ПодробностиПериода.treatDt1, Тип("Дата"));
			Дата2 = СериализацияБЗК.ЗначениеXML(ПодробностиПериода.treatDt2, Тип("Дата"));
			Если ЗначениеЗаполнено(Дата1) И Дата1 < ДатаНачала Тогда
				ДатаНачала    = Дата1;
				ДатаНачалаXML = ПодробностиПериода.treatDt1;
				Если ДатаОкончания < ДатаНачала Тогда
					ДатаОкончания    = ДатаНачала;
					ДатаОкончанияXML = ДатаНачалаXML;
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(Дата2) И Дата2 > ДатаОкончания Тогда
				ДатаОкончания    = Дата2;
				ДатаОкончанияXML = ПодробностиПериода.treatDt2;
			КонецЕсли;
			
		КонецЕсли;
		
		СвойстваПериода = Новый Структура;
		СвойстваПериода.Вставить("treatChairman",     СвойстваПериодаВСтаромФормате.TREAT_CHAIRMAN);
		СвойстваПериода.Вставить("treatChairmanRole", СвойстваПериодаВСтаромФормате.TREAT_CHAIRMAN_ROLE);
		СвойстваПериода.Вставить("treatPeriod",       ПодробностиПериода);
		
		ПериодыНетрудоспособности.Добавить(СвойстваПериода);
	КонецЦикла;
	
	// Адаптация вложенных полей servData.servFullData.
	ПериодыУходаЗаРодственниками = Новый Массив;
	Для НомерРодственника = 1 По 2 Цикл
		Префикс = "SERV" + НомерРодственника + "_";
		Если ЗначениеЗаполнено(ДанныеВСтаромФормате[Префикс + "FIO"])
			Или ЗначениеЗаполнено(ДанныеВСтаромФормате[Префикс + "AGE"])
			Или ЗначениеЗаполнено(ДанныеВСтаромФормате[Префикс + "MM"]) Тогда
			
			Если ЗначениеЗаполнено(ДанныеВСтаромФормате[Префикс + "FIO"]) Тогда
				ЧастиИмени = ФизическиеЛицаКлиентСервер.ЧастиИмени(ДанныеВСтаромФормате[Префикс + "FIO"]);
			Иначе
				ЧастиИмени = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
			КонецЕсли;
			
			ЗначенияПолей = Новый Структура;
			ЗначенияПолей.Вставить("servAge",          ДанныеВСтаромФормате[Префикс + "AGE"]);
			ЗначенияПолей.Вставить("servMm",           ДанныеВСтаромФормате[Префикс + "MM"]);
			ЗначенияПолей.Вставить("servRelationCode", ДанныеВСтаромФормате[Префикс + "RELATION_CODE"]);
			ЗначенияПолей.Вставить("servDt1",          ДатаНачалаXML);
			ЗначенияПолей.Вставить("servDt2",          ДатаОкончанияXML);
			ЗначенияПолей.Вставить("treatmentType",    Неопределено);
			ЗначенияПолей.Вставить("surname",          ЧастиИмени.Фамилия);
			ЗначенияПолей.Вставить("name",             ЧастиИмени.Имя);
			ЗначенияПолей.Вставить("patronymic",       ЧастиИмени.Отчество);
			ЗначенияПолей.Вставить("birthday",         Неопределено);
			ЗначенияПолей.Вставить("reason1",          Неопределено);
			ЗначенияПолей.Вставить("snils",            Неопределено);
			
			ПериодыУходаЗаРодственниками.Добавить(ЗначенияПолей);
		КонецЕсли;
	КонецЦикла;
	
	// Базовые проверки спецификации.
	БазовыеПоля = СтрРазделить("snils, lnCode", ", ", Ложь);
	Для Каждого ИмяПоля Из БазовыеПоля Цикл
		Если Не ЗначениеЗаполнено(ДанныеИзСервиса[ИмяПоля]) Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Файл не соответствует спецификации: Не заполнено поле ""%1"".'"), ИмяПоля);
			ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Отказ, ТекстОшибки, ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	ДанныеИзСервиса.Вставить("НомерЛН", ЗначениеXMLТипаСтрока(ДанныеИзСервиса.lnCode));
	ДанныеИзСервиса.Вставить("ДатаЛН", СериализацияБЗК.ЗначениеXML(ДанныеИзСервиса.lnDate, Тип("Дата")));
	ДанныеИзСервиса.Вставить("ХешЭЛН", ЗначениеXMLТипаСтрока(ДанныеИзСервиса.lnHash));
	
	ДанныеИзСервиса.Вставить("СНИЛС", УчетПособийСоциальногоСтрахованияКлиентСервер.СНИЛСВФорматеИБ(ДанныеИзСервиса.snils));
	ДанныеИзСервиса.Вставить("Фамилия",  ЗначениеXMLТипаСтрока(ДанныеИзСервиса.surname));
	ДанныеИзСервиса.Вставить("Имя",      ЗначениеXMLТипаСтрока(ДанныеИзСервиса.name));
	ДанныеИзСервиса.Вставить("Отчество", ЗначениеXMLТипаСтрока(ДанныеИзСервиса.patronymic));
	
	ДанныеИзСервиса.Вставить("ЯвляетсяПродолжениемБолезни", Не СериализацияБЗК.ЗначениеXML(ДанныеИзСервиса.primaryFlag, Тип("Булево")));
	ДанныеИзСервиса.Вставить("НомерПервичногоЛисткаНетрудоспособности", Строка(ДанныеИзСервиса.prevLnCode));
	
	ДанныеИзСервиса.Вставить("ПериодыНетрудоспособности", ПериодыНетрудоспособности);
	ДанныеИзСервиса.Вставить("ПериодыУходаЗаРодственниками", ПериодыУходаЗаРодственниками);
	
	СостояниеЭЛНВФСС = Перечисления.СостоянияЭЛНВФСС.НайтиПоКодуФСС(ДанныеИзСервиса.lnState);
	ДанныеИзСервиса.Вставить("СостояниеЭЛНВФСС", СостояниеЭЛНВФСС);
	
	Возврат ДанныеИзСервиса;
КонецФункции

Функция РазрешенаЗагрузкаЭЛН(СостояниеВФСС, КодПричиныНетрудоспособности, НомерЛН)
	// Не оформленные и аннулированные ЭЛН опасно загружать.
	Если Не ЗначениеЗаполнено(СостояниеВФСС)
		Или СостояниеВФСС = Перечисления.СостоянияЭЛНВФСС.Аннулирован Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Карантинные ЭЛН 65+ разрешено загружать почти в любых состояниях (для авансовых выплат за счет ФСС).
	Если КодПричиныНетрудоспособности = "03" И СтрНачинаетсяС(НомерЛН, "9990") Тогда
		Возврат Истина;
	КонецЕсли;
	
	// ЭЛН разрешено загружать только когда они закрыты медицинской организацией.
	Если СостояниеВФСС = Перечисления.СостоянияЭЛНВФСС.Закрыт
		Или СостояниеВФСС = Перечисления.СостоянияЭЛНВФСС.НаправленНаМСЭ
		Или СостояниеВФСС = Перечисления.СостоянияЭЛНВФСС.ДополненДаннымиМСЭ Тогда
		Возврат Истина;
	КонецЕсли;
	
	// ЭЛН 2.0 действует сразу для всех мест работы - и по основному месту работы и по совместительству.
	Если СостояниеВФСС = Перечисления.СостоянияЭЛНВФСС.ПринятРеестрЭЛН
		Или СостояниеВФСС = Перечисления.СостоянияЭЛНВФСС.ПринятРеестрПВСО
		Или СостояниеВФСС = Перечисления.СостоянияЭЛНВФСС.ВыплаченФСС Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Открытые ЭЛН загружать не следует.
	Возврат Ложь;
КонецФункции

Процедура ЗагрузитьРезультатПолученияЭЛН(Документ, ДанныеИзСервиса, ИсходныйXML, Отказ)
	ПроверитьВозможностьЗагрузкиЭЛН(Документ, ДанныеИзСервиса, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	// Перед записью хеша необходимо определить организацию и физлицо.
	ЗаполнитьПоПервичномуБольничномуЛисту(Документ, ДанныеИзСервиса);
	ЗаполнитьСотрудникаПоСНИЛС(Документ, ДанныеИзСервиса, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьДанныеЭЛН(Документ, ДанныеИзСервиса);
	РегистрыСведений.СведенияОбЭЛН.ЗаписатьПервичныеСведения(
		ДанныеИзСервиса.НомерЛН,
		ДанныеИзСервиса.ХешЭЛН,
		ДанныеИзСервиса.СостояниеЭЛНВФСС,
		Документ.Организация,
		Документ.ФизическоеЛицо,
		ИсходныйXML);
	ПроверитьФИОСотрудника(Документ, ДанныеИзСервиса);
КонецПроцедуры

Процедура ПроверитьВозможностьЗагрузкиЭЛН(Документ, ДанныеИзСервиса, Отказ) Экспорт
	// Проверка состояния.
	Если Не ЗначениеЗаполнено(ДанныеИзСервиса.СостояниеЭЛНВФСС) Тогда
		ТекстОшибки = НСтр("ru = 'Файл не соответствует спецификации: В поле ""%1"" недокументированное значение: ""%2"".'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, "lnState", ДанныеИзСервиса.lnState);
		ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Отказ, ТекстОшибки, "lnState");
	ИначеЕсли Не РазрешенаЗагрузкаЭЛН(ДанныеИзСервиса.СостояниеЭЛНВФСС, ДанныеИзСервиса.reason1, ДанныеИзСервиса.НомерЛН) Тогда
		ТекстОшибки = НСтр("ru = 'Листок нетрудоспособности не может быть загружен, его состояние: ""%1"".'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ДанныеИзСервиса.СостояниеЭЛНВФСС);
		ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Отказ, ТекстОшибки, "lnState");
	КонецЕсли;
	
	// Проверка соответствия номера ЛН в больничном с номером ЭЛН.
	Если ЗначениеЗаполнено(Документ.НомерЛисткаНетрудоспособности) Тогда
		Если СтрСравнить(Документ.НомерЛисткаНетрудоспособности, ДанныеИзСервиса.НомерЛН) <> 0 Тогда
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Номер листка нетрудоспособности в больничном (%1) не совпадает с номером ЭЛН (%2).'"),
				Документ.НомерЛисткаНетрудоспособности,
				ДанныеИзСервиса.НомерЛН);
			ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Отказ, ТекстОшибки, "НомерЛисткаНетрудоспособности");
		КонецЕсли;
	КонецЕсли;
	
	// Если в документе заполнен сотрудник, то необходимо только проверить соответствие СНИЛСов.
	Если ЗначениеЗаполнено(Документ.Сотрудник) Тогда
		Документ.ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ.Сотрудник, "ФизическоеЛицо");
		СНИЛСВДокументе = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ.ФизическоеЛицо, "СтраховойНомерПФР");
		Если ЗначениеЗаполнено(СНИЛСВДокументе) Тогда
			Если СтрСравнить(ДанныеИзСервиса.СНИЛС, СНИЛСВДокументе) <> 0 Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'СНИЛС сотрудника ""%1"" в больничном (%2) не соответствует СНИЛСу в ЭЛН (%3).'"),
					Строка(Документ.Сотрудник),
					СНИЛСВДокументе,
					ДанныеИзСервиса.СНИЛС);
				ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Отказ, ТекстОшибки, "Сотрудник");
			КонецЕсли;
		Иначе
			РезультатПоиска = ФизическиеЛицаЗарплатаКадры.ФизическоеЛицоПоСНИЛСИлиФИО(ДанныеИзСервиса.СНИЛС, "", "", "");
			ФизическоеЛицо  = РезультатПоиска.ФизическоеЛицо;
			Если ЗначениеЗаполнено(ФизическоеЛицо) И Документ.ФизическоеЛицо <> ФизическоеЛицо Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'В документе выбран сотрудник ""%1"" с пустым СНИЛС, а по СНИЛСу в ЭЛН (%2) найден сотрудник ""%3"".'"),
					Документ.ФизическоеЛицо,
					ДанныеИзСервиса.СНИЛС,
					ФизическоеЛицо);
				ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Отказ, ТекстОшибки, "Сотрудник");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоПервичномуБольничномуЛисту(Документ, ДанныеИзСервиса) Экспорт
	
	Документ.НомерПервичногоЛисткаНетрудоспособности = ДанныеИзСервиса.НомерПервичногоЛисткаНетрудоспособности;
	Документ.ЯвляетсяПродолжениемБолезни             = ДанныеИзСервиса.ЯвляетсяПродолжениемБолезни;
	
	Если ЗначениеЗаполнено(Документ.НомерПервичногоЛисткаНетрудоспособности)
		И Документ.ЯвляетсяПродолжениемБолезни Тогда
		Документ.ЗаполнитьПоПервичномуБольничномуЛисту(Документ.НомерПервичногоЛисткаНетрудоспособности);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСотрудникаПоСНИЛС(Документ, ДанныеИзСервиса, Отказ)
	// В документе не заполнен сотрудник и это первичный больничный лист.
	Если ЗначениеЗаполнено(Документ.Сотрудник) Тогда
		Возврат;
	КонецЕсли;
	
	// В качестве даты начала события (нетрудоспособности) можно взять дату ЭЛН.
	Если ЗначениеЗаполнено(ДанныеИзСервиса.ДатаЛН) Тогда
		ДатаНачалаСобытия = ДанныеИзСервиса.ДатаЛН;
	Иначе
		ДатаНачалаСобытия = Документ.Дата;
	КонецЕсли;
	
	РезультатПоиска = НайтиСотрудникаПоСНИЛС(
		Документ.Организация,
		ДатаНачалаСобытия,
		ДанныеИзСервиса.СНИЛС,
		ДанныеИзСервиса.Фамилия,
		ДанныеИзСервиса.Имя,
		ДанныеИзСервиса.Отчество);
	
	Если РезультатПоиска.Успех Тогда
		ЗаполнитьЗначенияСвойств(Документ, РезультатПоиска, "Сотрудник, ФизическоеЛицо, Организация");
	Иначе
		Отказ = Истина;
	КонецЕсли;
	Если ЗначениеЗаполнено(РезультатПоиска.ТекстОшибки) Тогда
		ОбщегоНазначения.СообщитьПользователю(РезультатПоиска.ТекстОшибки);
	КонецЕсли;
КонецПроцедуры

Функция НайтиСотрудникаПоСНИЛС(Организация, ДатаНачалаСобытия, СНИЛС, Фамилия = "", Имя = "", Отчество = "")
	Результат = Новый Структура("Успех, ТекстОшибки, ФизическоеЛицо, Сотрудник, Организация");
	Результат.Успех = Ложь;
	Результат.ТекстОшибки = "";
	
	// 1. Поиск физического лица по СНИЛСу.
	РезультатПоиска = ФизическиеЛицаЗарплатаКадры.ФизическоеЛицоПоСНИЛСИлиФИО(СНИЛС, Фамилия, Имя, Отчество);
	ЗаполнитьЗначенияСвойств(Результат, РезультатПоиска, "ФизическоеЛицо, ТекстОшибки");
	Если Не ЗначениеЗаполнено(Результат.ФизическоеЛицо) Тогда
		Возврат Результат;
	КонецЕсли;
	
	// 2. Поиск сотрудников организаций по физическому лицу.
	ПриПоискеСотрудникаОрганизации(Результат, Организация, ДатаНачалаСобытия);
	
	Возврат Результат;
КонецФункции

Процедура ПриПоискеСотрудникаОрганизации(Результат, ПредполагаемаяОрганизация, ДатаНачалаСобытия)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// 255-ФЗ, Глава 2, Статья 5, абзац 2.
	// Работодатель обязан оплатить больничный лист бывшему работнику, если соблюдены 2 условия:
	// - Период временной нетрудоспособности наступил в течение 30 дней после увольнения;
	// - Бывший работник предоставил больничный лист работодателю в течение полугода после увольнения.
	НачалоПериода = ДобавитьМесяц(ДатаНачалаСобытия, -6);
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудников.СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Результат.ФизическоеЛицо);
	ПараметрыПолученияСотрудников.НачалоПериода       = НачалоПериода;
	ПараметрыПолученияСотрудников.ОкончаниеПериода    = ДатаНачалаСобытия;
	ПараметрыПолученияСотрудников.КадровыеДанные      = "Организация, ДатаУвольнения, ВидЗанятости";
	УстановитьПривилегированныйРежим(Истина);
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыПолученияСотрудников);
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА СотрудникиОрганизации.ДатаУвольнения = &ПустаяДата
	|				ТОГДА &МаксимальнаяДата
	|			ИНАЧЕ СотрудникиОрганизации.ДатаУвольнения
	|		КОНЕЦ) КАК ДатаУвольнения,
	|	СотрудникиОрганизации.Организация КАК Организация,
	|	СотрудникиОрганизации.ВидЗанятости КАК ВидЗанятости
	|ПОМЕСТИТЬ ВТДатыУвольненияИзОрганизаций
	|ИЗ
	|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
	|ГДЕ
	|	(СотрудникиОрганизации.ДатаУвольнения = &ПустаяДата
	|			ИЛИ СотрудникиОрганизации.ДатаУвольнения >= &НачалоПериода)
	|	И (СотрудникиОрганизации.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|			ИЛИ СотрудникиОрганизации.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизации.Организация,
	|	СотрудникиОрганизации.ВидЗанятости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыУвольненияИзОрганизаций.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ДатыУвольненияИзОрганизаций.Организация В (&ДоступныеФилиалы)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьПравоПросмотраОрганизации,
	|	ВЫБОР
	|		КОГДА ДатыУвольненияИзОрганизаций.ДатаУвольнения >= &ДатаНачалаСобытия
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Работает,
	|	ДатыУвольненияИзОрганизаций.ДатаУвольнения КАК ДатаУвольнения,
	|	СотрудникиОрганизации.Сотрудник КАК Сотрудник
	|ИЗ
	|	ВТДатыУвольненияИзОрганизаций КАК ДатыУвольненияИзОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК СотрудникиОрганизации
	|		ПО ДатыУвольненияИзОрганизаций.Организация = СотрудникиОрганизации.Организация
	|			И ДатыУвольненияИзОрганизаций.ВидЗанятости = СотрудникиОрганизации.ВидЗанятости
	|			И (ВЫБОР
	|				КОГДА СотрудникиОрганизации.ДатаУвольнения = &ПустаяДата
	|					ТОГДА &МаксимальнаяДата
	|				ИНАЧЕ СотрудникиОрганизации.ДатаУвольнения
	|			КОНЕЦ = ДатыУвольненияИзОрганизаций.ДатаУвольнения)";
	
	Запрос.УстановитьПараметр("НачалоПериода",     НачалоПериода);
	Запрос.УстановитьПараметр("ДатаНачалаСобытия", ДатаНачалаСобытия);
	Запрос.УстановитьПараметр("ПустаяДата",        '00010101');
	Запрос.УстановитьПараметр("МаксимальнаяДата",  '39991231235959');
	
	ДоступныеФилиалы = ЗарплатаКадры.ДоступныеОрганизации().Филиалы;
	Если ДоступныеФилиалы = Неопределено Тогда
		// Полные права.
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДатыУвольненияИзОрганизаций.Организация В (&ДоступныеФилиалы)", "ИСТИНА");
	Иначе
		// Не полные права.
		Запрос.УстановитьПараметр("ДоступныеФилиалы", ДоступныеФилиалы);
	КонецЕсли;
	
	ВсеОрганизации = Запрос.Выполнить().Выгрузить();
	Если ВсеОрганизации.Найти(Истина, "Работает") <> Неопределено
		И ВсеОрганизации.Найти(Ложь, "Работает") <> Неопределено Тогда
		ПодходящиеОрганизации = ВсеОрганизации.Скопировать(Новый Структура("Работает", Истина));
	Иначе
		ПодходящиеОрганизации = ВсеОрганизации;
	КонецЕсли;
	
	КоличествоПодходящих = ПодходящиеОрганизации.Количество();
	Если КоличествоПодходящих = 0 Тогда
		Результат.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сотрудник ""%1"" на %2 не принят на работу.'"),
			Результат.ФизическоеЛицо,
			Формат(ДатаНачалаСобытия, "ДЛФ=D"));
		Возврат;
	КонецЕсли;
	
	Если КоличествоПодходящих = 1 Тогда
		НайденнаяСтрока = ПодходящиеОрганизации[0];
	Иначе
		Если ЗначениеЗаполнено(ПредполагаемаяОрганизация) Тогда
			НайденнаяСтрока = ПодходящиеОрганизации.Найти(ПредполагаемаяОрганизация, "Организация");
		Иначе
			НайденнаяСтрока = Неопределено;
		КонецЕсли;
		Если НайденнаяСтрока = Неопределено Тогда
			ПредставлениеОрганизаций = ПодходящиеОрганизации.ВыгрузитьКолонку("Организация");
			Если ДоступныеФилиалы <> Неопределено Тогда
				// Не полные права.
				ВГраница = ПредставлениеОрганизаций.ВГраница();
				Для Индекс = 0 По ВГраница Цикл
					Если ДоступныеФилиалы.Найти(ПредставлениеОрганизаций[Индекс]) = Неопределено Тогда
						ПредставлениеОрганизаций[Индекс] = НСтр("ru = '<Недостаточно прав>'");
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Результат.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Требуется выбрать организацию, т.к. сотрудник ""%1"" на %2 работает в нескольких организациях: %3.'"),
				Результат.ФизическоеЛицо,
				Формат(ДатаНачалаСобытия, "ДЛФ=D"),
				СтрСоединить(ПредставлениеОрганизаций, ", "));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не НайденнаяСтрока.ЕстьПравоПросмотраОрганизации Тогда
		Результат.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Недостаточно прав для загрузки ЭЛН сотрудника ""%1"" организации ""%2"".'"),
			Результат.ФизическоеЛицо,
			НайденнаяСтрока.Организация);
		Возврат;
	КонецЕсли;
	
	Результат.Успех       = Истина;
	Результат.Организация = НайденнаяСтрока.Организация;
	Результат.Сотрудник   = НайденнаяСтрока.Сотрудник;
КонецПроцедуры

Процедура ЗаполнитьДанныеЭЛН(Документ, ДанныеИзСервиса)
	
	Документ.НомерЛисткаНетрудоспособности = ДанныеИзСервиса.НомерЛН;
	
	СоответствиеПолей = СоответствиеПолейЭЛН();
	Для Каждого СвойствоДокумента Из СоответствиеПолей Цикл
		Документ[СвойствоДокумента.Ключ] = СериализацияБЗК.ЗначениеXML(ДанныеИзСервиса[СвойствоДокумента.Значение], ТипЗнч(Документ[СвойствоДокумента.Ключ]));
	КонецЦикла;
	
	Если ДанныеИзСервиса.lnResult <> Неопределено Тогда
		Значения = Новый Структура("mseResult, otherStateDt, returnDateLpu, nextLnCode");
		ЗаполнитьЗначенияСвойств(Значения, ДанныеИзСервиса.lnResult);
		ЗаполнитьЗначениеИзСтрокиXML(Документ.НовыйСтатусНетрудоспособного,     Значения.mseResult,     Тип("Строка"));
		ЗаполнитьЗначениеИзСтрокиXML(Документ.ДатаНовыйСтатусНетрудоспособного, Значения.otherStateDt,  Тип("Дата"));
		ЗаполнитьЗначениеИзСтрокиXML(Документ.ПриступитьКРаботеС,               Значения.returnDateLpu, Тип("Дата"));
		ЗаполнитьЗначениеИзСтрокиXML(Документ.НомерЛисткаПродолжения,           Значения.nextLnCode,    Тип("Строка"));
	КонецЕсли;
	
	Если ДанныеИзСервиса.hospitalBreach <> Неопределено Тогда
		Значения = Новый Структура("hospitalBreachCode, hospitalBreachDt");
		ЗаполнитьЗначенияСвойств(Значения, ДанныеИзСервиса.hospitalBreach);
		ЗаполнитьЗначениеИзСтрокиXML(Документ.КодНарушенияРежима,  Значения.hospitalBreachCode, Тип("Строка"));
		ЗаполнитьЗначениеИзСтрокиXML(Документ.ДатаНарушенияРежима, Значения.hospitalBreachDt,   Тип("Дата"));
	КонецЕсли;
	
	// Анализ периодов ухода за родственниками.
	ИменаПолей = "servAge, servMm, servRelationCode, servDt1, servDt2, treatmentType, surname, name, patronymic, birthday, reason1, snils";
	КодыПричин = Новый Массив;
	ПоискРодственников = Новый Соответствие;
	ПорядокРодственников = Новый Массив;
	Для Каждого УходЗаРодственником Из ДанныеИзСервиса.ПериодыУходаЗаРодственниками Цикл
		ЗначенияПолей = Новый Структура(ИменаПолей);
		ЗаполнитьЗначенияСвойств(ЗначенияПолей, УходЗаРодственником);
		// Получение реквизитов.
		ДатаНачала     = СериализацияБЗК.ЗначениеXML(ЗначенияПолей.servDt1, Тип("Дата"));
		ДатаОкончания  = СериализацияБЗК.ЗначениеXML(ЗначенияПолей.servDt2, Тип("Дата"));
		ВозрастЛет     = СериализацияБЗК.ЗначениеXML(ЗначенияПолей.servAge, Тип("Число"));
		ВозрастМесяцев = СериализацияБЗК.ЗначениеXML(ЗначенияПолей.servMm,  Тип("Число"));
		КодПричины = ЗначениеXMLТипаСтрока(ЗначенияПолей.reason1);
		ТипЛечения = ЗначениеXMLТипаСтрока(ЗначенияПолей.treatmentType);
		КодСвязи   = ЗначениеXMLТипаСтрока(ЗначенияПолей.servRelationCode);
		СНИЛС      = ВРег(ЗначениеXMLТипаСтрока(ЗначенияПолей.snils));
		Фамилия    = ТРег(ЗначениеXMLТипаСтрока(ЗначенияПолей.surname));
		Имя        = ТРег(ЗначениеXMLТипаСтрока(ЗначенияПолей.name));
		Отчество   = ТРег(ЗначениеXMLТипаСтрока(ЗначенияПолей.patronymic));
		// Получение вторичной информации.
		Если ТипЗнч(ВозрастЛет) <> Тип("Число") Тогда
			ВозрастЛет = 0;
		КонецЕсли;
		Если ТипЗнч(ВозрастМесяцев) <> Тип("Число") Тогда
			ВозрастМесяцев = 0;
		КонецЕсли;
		Если ВозрастЛет > 0 Или ВозрастМесяцев > 0 Тогда
			ПредставлениеВозраста = ЗарплатаКадрыКлиентСервер.ПредставлениеЛет(ВозрастЛет)
				+ " "
				+ ЗарплатаКадрыКлиентСервер.ПредставлениеМесяцев(ВозрастМесяцев);
		Иначе
			ПредставлениеВозраста = "";
		КонецЕсли;
		Если ЗначениеЗаполнено(Фамилия) Или ЗначениеЗаполнено(Имя) Или ЗначениеЗаполнено(Отчество) Тогда
			ФИО = СокрП(Фамилия + " " + Имя + " " + Отчество);
		Иначе
			ФИО = "";
		КонецЕсли;
		// Проверка спецификации.
		Если Не ЗначениеЗаполнено(ДатаНачала) Или Не ЗначениеЗаполнено(ДатаОкончания) Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru = 'В ЭЛН не заполнен период ухода за %1, код связи: %2.'"), ФИО, КодСвязи);
			ЗарплатаКадрыОтображениеОшибок.СообщитьПользователю(ТекстОшибки);
			Продолжить;
		КонецЕсли;
		// Наполнение массива причин.
		Если ЗначениеЗаполнено(КодПричины) Тогда
			ОбщегоНазначенияБЗК.ДобавитьЗначениеВМассив(КодыПричин, КодПричины);
		КонецЕсли;
		// Регистрация дней ухода.
		ДниУхода = ОбщегоНазначенияБЗК.МассивДатИзПериода(ДатаНачала, ДатаОкончания);
		Если ДниУхода.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		Ключ1 = ФИО + КодСвязи + СНИЛС + ПредставлениеВозраста;
		Ключ2 = ФИО + КодСвязи;
		Ключ3 = ФИО;
		Родственник = ПоискРодственников[Ключ1];
		Если Родственник = Неопределено Тогда
			Родственник = ПоискРодственников[Ключ2];
			Если Родственник = Неопределено Тогда
				Родственник = ПоискРодственников[Ключ3];
			КонецЕсли;
		КонецЕсли;
		Если Родственник = Неопределено Тогда
			Родственник = Новый Структура("ДниУхода, ВозрастЛет, ВозрастМесяцев, КодПричины, ТипЛечения, КодСвязи, СНИЛС, Фамилия, Имя, Отчество, ФИО, ПредставлениеВозраста");
			Родственник.ДниУхода       = ДниУхода;
			Родственник.ВозрастЛет     = ВозрастЛет;
			Родственник.ВозрастМесяцев = ВозрастМесяцев;
			Родственник.КодПричины     = КодПричины;
			Родственник.ТипЛечения     = ТипЛечения;
			Родственник.КодСвязи       = КодСвязи;
			Родственник.СНИЛС          = СНИЛС;
			Родственник.Фамилия        = Фамилия;
			Родственник.Имя            = Имя;
			Родственник.Отчество       = Отчество;
			Родственник.ФИО            = ФИО;
			Родственник.ПредставлениеВозраста = ПредставлениеВозраста;
			ПорядокРодственников.Добавить(Родственник);
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Родственник.ДниУхода, ДниУхода, Истина);
			Если Не ЗначениеЗаполнено(Родственник.ПредставлениеВозраста) Тогда
				Родственник.ВозрастЛет     = ВозрастЛет;
				Родственник.ВозрастМесяцев = ВозрастМесяцев;
				Родственник.ПредставлениеВозраста = ПредставлениеВозраста;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Родственник.КодПричины) Тогда
				Родственник.КодПричины = КодПричины;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Родственник.ТипЛечения) Тогда
				Родственник.ТипЛечения = ТипЛечения;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Родственник.КодСвязи) Тогда
				Родственник.КодСвязи = КодСвязи;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Родственник.СНИЛС) Тогда
				Родственник.СНИЛС = СНИЛС;
			КонецЕсли;
		КонецЕсли;
		ПоискРодственников.Вставить(Ключ1, Родственник);
		ПоискРодственников.Вставить(Ключ2, Родственник);
		ПоискРодственников.Вставить(Ключ3, Родственник);
	КонецЦикла;
	
	ДанныеИзСервиса.Вставить("ПорядокРодственников", ПорядокРодственников);
	
	ПредставленияРодственников = Новый Массив;
	Номер = 0;
	Для Каждого Родственник Из ПорядокРодственников Цикл
		Номер = Номер + 1;
		СписокДней = Новый СписокЗначений;
		СписокДней.ЗагрузитьЗначения(Родственник.ДниУхода);
		СписокДней.СортироватьПоЗначению(НаправлениеСортировки.Возр);
		ПериодыУхода = ОбщегоНазначенияБЗК.ПериодыИзМассиваДат(СписокДней.ВыгрузитьЗначения());
		ПредставленияПериодов = Новый Массив;
		Для Каждого ПериодУхода Из ПериодыУхода Цикл
			ПредставленияПериодов.Добавить(ОбщегоНазначенияБЗК.НаименованиеПериода(
				ПериодУхода.ДатаНачала,
				ПериодУхода.ДатаОкончания));
		КонецЦикла;
		ПредставленияРодственников.Добавить(СтрШаблон(
			НСтр("ru = '№ %1: %2, уход %3, причина нетрудоспособности: %4, возраст: %5, код родственной связи: %6.'"),
			Номер,
			Родственник.ФИО,
			СтрСоединить(ПредставленияПериодов, ", "),
			Родственник.КодПричины,
			Родственник.ПредставлениеВозраста,
			Родственник.КодСвязи));
		Если Номер > 2 Тогда
			Продолжить;
		КонецЕсли;
		// Заполнение реквизитов в шапке документа.
		Документ["ПоУходуВозрастЛет" + Номер]       = Родственник.ВозрастЛет;
		Документ["ПоУходуВозрастМесяцев" + Номер]   = Родственник.ВозрастМесяцев;
		Документ["ПоУходуРодственнаяСвязь" + Номер] = Родственник.КодСвязи;
		Документ["ПоУходуФИО" + Номер]              = Родственник.ФИО;
	КонецЦикла;
	
	// Очистка отсутствующих периодов ухода за родственниками.
	Пока Номер < 2 Цикл
		Номер = Номер + 1;
		Документ["ПоУходуВозрастЛет" + Номер]       = 0;
		Документ["ПоУходуВозрастМесяцев" + Номер]   = 0;
		Документ["ПоУходуРодственнаяСвязь" + Номер] = "";
		Документ["ПоУходуФИО" + Номер]              = "";
	КонецЦикла;
	
	// Вывод списка родственников.
	Если ПредставленияРодственников.Количество() > 2 Тогда
		ТекстОшибки = НСтр("ru = 'В ЭЛН более 2 родственников, за которыми осуществляется уход:'");
		ПредставленияРодственников.Вставить(0, ТекстОшибки);
		ЗарплатаКадрыОтображениеОшибок.СообщитьПользователю(СтрСоединить(ПредставленияРодственников, Символы.ПС));
	КонецЕсли;
	
	// Заполнение кода причины нетрудоспособности в шапке.
	Если Не ЗначениеЗаполнено(Документ.КодПричиныНетрудоспособности) Тогда
		Если КодыПричин.Количество() > 0 Тогда
			Документ.КодПричиныНетрудоспособности = КодыПричин[0];
		Иначе
			ТекстОшибки = НСтр("ru = 'В ЭЛН не указана причина нетрудоспособности. Требуется выбрать вручную.'");
			ЗарплатаКадрыОтображениеОшибок.СообщитьПользователю(ТекстОшибки, "ПричинаНетрудоспособности");
		КонецЕсли;
	ИначеЕсли КодыПричин.Найти(Документ.КодПричиныНетрудоспособности) = Неопределено Тогда
		КодыПричин.Вставить(0, Документ.КодПричиныНетрудоспособности);
	КонецЕсли;
	
	// Проверка количества различных кодов причин нетрудоспособности.
	Если КодыПричин.Количество() > 1 Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'В ЭЛН несколько причин нетрудоспособности: %1.'"),
			СтрСоединить(КодыПричин, ", "));
		ЗарплатаКадрыОтображениеОшибок.СообщитьПользователю(ТекстОшибки, "ПричинаНетрудоспособности");
	КонецЕсли;
	
	// Заполнение периодов нетрудоспособности.
	Номер = 0;
	Для Каждого ЭлементПериода Из ДанныеИзСервиса.ПериодыНетрудоспособности Цикл
		СвойстваПериода = Новый Структура("treatChairman, treatChairmanRole, treatPeriod");
		ЗаполнитьЗначенияСвойств(СвойстваПериода, ЭлементПериода);
		Если СвойстваПериода.treatPeriod = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Номер = Номер + 1;
		
		ПодробностиПериода = Новый Структура("treatDt1, treatDt2, treatDoctorRole, treatDoctor");
		ЗаполнитьЗначенияСвойств(ПодробностиПериода, СвойстваПериода.treatPeriod);
		
		Документ["ОсвобождениеДатаНачала" + Номер]     = СериализацияБЗК.ЗначениеXML(ПодробностиПериода.treatDt1, Тип("Дата"));
		Документ["ОсвобождениеДатаОкончания" + Номер]  = СериализацияБЗК.ЗначениеXML(ПодробностиПериода.treatDt2, Тип("Дата"));
		Документ["ОсвобождениеФИОВрача" + Номер]       = ПодробностиПериода.treatDoctor;
		Документ["ОсвобождениеДолжностьВрача" + Номер] = ПодробностиПериода.treatDoctorRole;
		Документ["ОсвобождениеФИОВрачаПредседателяВК" + Номер]       = СвойстваПериода.treatChairman;
		Документ["ОсвобождениеДолжностьВрачаПредседателяВК" + Номер] = СвойстваПериода.treatChairmanRole;
		
		Если Номер = 3 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Очистка отсутствующих периодов нетрудоспособности.
	Пока Номер < 2 Цикл
		Номер = Номер + 1;
		Документ["ОсвобождениеДатаНачала" + Номер]                   = '00010101';
		Документ["ОсвобождениеДатаОкончания" + Номер]                = '00010101';
		Документ["ОсвобождениеФИОВрача" + Номер]                     = "";
		Документ["ОсвобождениеДолжностьВрача" + Номер]               = "";
		Документ["ОсвобождениеФИОВрачаПредседателяВК" + Номер]       = "";
		Документ["ОсвобождениеДолжностьВрачаПредседателяВК" + Номер] = "";
	КонецЦикла;
	
	ЗаполнитьФлагПостановкиНаУчетВРанниеСрокиБеременности(Документ, ДанныеИзСервиса);
	
	ЗаполнитьМедицинскуюОрганизацию(Документ);
	
	ЗаполнитьДаты(Документ);
	
	ЗаполнитьПричинуНетрудоспособностиИСлучайУходаЗаРебенком(Документ);
	
	Документы.БольничныйЛист.ПослеЗагрузкиЭЛН(Документ, ДанныеИзСервиса);
	
КонецПроцедуры

Процедура ЗаполнитьЗначениеИзСтрокиXML(Приемник, ЗначениеВФорматеXML, ТипЗначения)
	Значение = СериализацияБЗК.ЗначениеXML(ЗначениеВФорматеXML, ТипЗначения);
	Если ЗначениеЗаполнено(Значение) Тогда
		Приемник = Значение;
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьФИОСотрудника(Документ, ДанныеИзСервиса)
	
	// Проверка кадровых данных сотрудника.
	КадровыеДанныеСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(
		Истина,
		Документ.Сотрудник,
		"Фамилия, Имя, Отчество, ДатаРождения",
		ДанныеИзСервиса.ДатаЛН);
	Найденные = КадровыеДанныеСотрудников.НайтиСтроки(Новый Структура("Сотрудник", Документ.Сотрудник));
	Если Найденные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КадровыеДанныеСотрудника = Найденные[0];
	
	Если    СтрСравнить(ДанныеИзСервиса.Фамилия,  СокрЛП(КадровыеДанныеСотрудника.Фамилия))  <> 0
		Или СтрСравнить(ДанныеИзСервиса.Имя,      СокрЛП(КадровыеДанныеСотрудника.Имя))      <> 0
		Или СтрСравнить(ДанныеИзСервиса.Отчество, СокрЛП(КадровыеДанныеСотрудника.Отчество)) <> 0 Тогда
		ТекстОшибки = НСтр("ru = 'ФИО указанные в листке нетрудоспособности (%1 %2 %3) не совпадают с ФИО сотрудника.'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ДанныеИзСервиса.Фамилия, ДанныеИзСервиса.Имя, ДанныеИзСервиса.Отчество);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	ДанныеЛНДатаРождения = СериализацияБЗК.ЗначениеXML(ДанныеИзСервиса.birthday, Тип("Дата"));
	Если ДанныеЛНДатаРождения <> КадровыеДанныеСотрудника.ДатаРождения Тогда
		ТекстОшибки = НСтр("ru = 'Дата рождения указанная в листке нетрудоспособности (%1) не совпадает с датой рождения сотрудника.'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Формат(ДанныеЛНДатаРождения, "ДЛФ=D"));
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает соответствие, в котором ключ - имя поля больничного, а значение - имя поля ЭЛН.
Функция СоответствиеПолейЭЛН()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("НомерПервичногоЛисткаНетрудоспособности",      "prevLnCode");
	СоответствиеПолей.Вставить("ПредоставленДубликатЛисткаНетрудоспособности", "duplicateFlag");
	СоответствиеПолей.Вставить("ДатаВыдачиЛисткаНетрудоспособности",           "lnDate");
	
	СоответствиеПолей.Вставить("НаименованиеМедицинскойОрганизации", "lpuName");
	СоответствиеПолей.Вставить("АдресМедицинскойОрганизации",        "lpuAddress");
	СоответствиеПолей.Вставить("ОГРНМедицинскойОрганизации",         "lpuOgrn");
	
	СоответствиеПолей.Вставить("КодПричиныНетрудоспособности",               "reason1");
	СоответствиеПолей.Вставить("ДополнительныйКодПричиныНетрудоспособности", "reason2");
	СоответствиеПолей.Вставить("ВторойКодПричиныНетрудоспособности",         "reason3");
	
	СоответствиеПолей.Вставить("ДатаИзмененияКодаПричиныНетрудоспособности", "date1");
	СоответствиеПолей.Вставить("ДатаОкончанияПутевки",                       "date2");
	
	СоответствиеПолей.Вставить("НомерПутевки",   "voucherNo");
	СоответствиеПолей.Вставить("ОГРН_Санатория", "voucherOgrn");
	
	СоответствиеПолей.Вставить("ПериодНахожденияВСтационареСРебенкомС",  "hospitalDt1");
	СоответствиеПолей.Вставить("ПериодНахожденияВСтационареСРебенкомПо", "hospitalDt2");
	
	СоответствиеПолей.Вставить("ДатаНаправленияВБюроМСЭ",      "mseDt1");
	СоответствиеПолей.Вставить("ДатаРегистрацииДокументовМСЭ", "mseDt2");
	СоответствиеПолей.Вставить("ДатаОсвидетельствованияМСЭ",   "mseDt3");
	СоответствиеПолей.Вставить("ГруппаИнвалидности",           "mseInvalidGroup");
	
	Возврат СоответствиеПолей;
	
КонецФункции

// Заполнение поля ПоставленаНаУчетВРанниеСрокиБеременности.
Процедура ЗаполнитьФлагПостановкиНаУчетВРанниеСрокиБеременности(ДокументОбъект, ДанныеИзСервиса) Экспорт
	ФлагПостановки = СериализацияБЗК.ЗначениеXML(ДанныеИзСервиса.pregn12wFlag, Тип("Булево"));
	Если ФлагПостановки = Истина Тогда
		ДокументОбъект.ПоставленаНаУчетВРанниеСрокиБеременности = Перечисления.ПостановкаНаУчетВРанниеСрокиБеременности.Поставлена;
	ИначеЕсли ФлагПостановки = Ложь Тогда
		ДокументОбъект.ПоставленаНаУчетВРанниеСрокиБеременности = Перечисления.ПостановкаНаУчетВРанниеСрокиБеременности.НеПоставлена;
	Иначе
		ДокументОбъект.ПоставленаНаУчетВРанниеСрокиБеременности = Перечисления.ПостановкаНаУчетВРанниеСрокиБеременности.НеИзвестно;
	КонецЕсли;
КонецПроцедуры

// Заполнение медицинской организации.
Процедура ЗаполнитьМедицинскуюОрганизацию(Документ)
	Если Не ЗначениеЗаполнено(Документ.ОГРНМедицинскойОрганизации) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МедицинскиеОрганизации.Ссылка
	|ИЗ
	|	Справочник.МедицинскиеОрганизации КАК МедицинскиеОрганизации
	|ГДЕ
	|	МедицинскиеОрганизации.ОГРН = &ОГРН";
	Запрос.УстановитьПараметр("ОГРН", Документ.ОГРНМедицинскойОрганизации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Документ.МедицинскаяОрганизация = Выборка.Ссылка;
	Иначе
		МедОрганизация = Справочники.МедицинскиеОрганизации.СоздатьЭлемент();
		МедОрганизация.Наименование = Документ.НаименованиеМедицинскойОрганизации;
		МедОрганизация.ОГРН         = Документ.ОГРНМедицинскойОрганизации;
		МедОрганизация.Адрес        = Документ.АдресМедицинскойОрганизации;
		МедОрганизация.Записать();
		Документ.МедицинскаяОрганизация = МедОрганизация.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

// Заполнение дат оплаты и освобождения от работы.
Процедура ЗаполнитьДаты(Документ)
	
	Если ЗначениеЗаполнено(Документ.ОсвобождениеДатаНачала1) Тогда
		Документ.ДатаНачала    = Документ.ОсвобождениеДатаНачала1;
		Документ.ДатаОкончания = Документ.ОсвобождениеДатаОкончания1;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Документ.ОсвобождениеДатаНачала2)
		И ЗначениеЗаполнено(Документ.ОсвобождениеДатаОкончания2) Тогда
		Если ЗначениеЗаполнено(Документ.ДатаНачала) Тогда
			Документ.ДатаНачала = Мин(Документ.ДатаНачала, Документ.ОсвобождениеДатаНачала2);
		Иначе
			Документ.ДатаНачала = Документ.ОсвобождениеДатаНачала2;
		КонецЕсли;
		Документ.ДатаОкончания = Макс(Документ.ДатаОкончания, Документ.ОсвобождениеДатаОкончания2);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Документ.ОсвобождениеДатаНачала3)
		И ЗначениеЗаполнено(Документ.ОсвобождениеДатаОкончания3) Тогда
		Если ЗначениеЗаполнено(Документ.ДатаНачала) Тогда
			Документ.ДатаНачала = Мин(Документ.ДатаНачала, Документ.ОсвобождениеДатаНачала3);
		Иначе
			Документ.ДатаНачала = Документ.ОсвобождениеДатаНачала3;
		КонецЕсли;
		Документ.ДатаОкончания = Макс(Документ.ДатаОкончания, Документ.ОсвобождениеДатаОкончания3);
	КонецЕсли;
	
	Документ.ДатаНачалаОплаты    = Документ.ДатаНачала;
	Документ.ДатаОкончанияОплаты = Документ.ДатаОкончания;
	
КонецПроцедуры

// Заполняет причину нетрудоспособности и случай ухода за ребенком.
Процедура ЗаполнитьПричинуНетрудоспособностиИСлучайУходаЗаРебенком(Документ)
	Стационар = ЗначениеЗаполнено(Документ.ПериодНахожденияВСтационареСРебенкомС);
	
	Если Документ.КодПричиныНетрудоспособности = "01"
		Или Документ.КодПричиныНетрудоспособности = "02" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ОбщееЗаболевание;
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "03" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.Карантин;
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "04"
		Или Документ.КодПричиныНетрудоспособности = "07" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ТравмаНаПроизводстве;
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "05" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоБеременностиИРодам;
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "06" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.Протезирование;
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "08" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПособиеПриДолечивании;
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "09"
		Или (Не ЗначениеЗаполнено(Документ.КодПричиныНетрудоспособности)
			И ЗначениеЗаполнено(Документ.ПоУходуФИО1)) Тогда
		
		ПоУходуВозраст1 = Документ.ПоУходуВозрастЛет1 + Окр(Документ.ПоУходуВозрастМесяцев1 / 12, 1);
		ПоУходуВозраст2 = Документ.ПоУходуВозрастЛет2 + Окр(Документ.ПоУходуВозрастМесяцев2 / 12, 1);
		
		Если ПоУходуВозраст2 > 0 И ПоУходуВозраст2 < ПоУходуВозраст1 Тогда
			НаименьшийВозрастПоУходу = ПоУходуВозраст2;
		Иначе
			НаименьшийВозрастПоУходу = ПоУходуВозраст1;
		КонецЕсли;
		
		Если НаименьшийВозрастПоУходу = 0 Тогда
			Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаРебенком;
		ИначеЕсли НаименьшийВозрастПоУходу < 7 Тогда
			Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаРебенком;
			Документ.СлучайУходаЗаБольнымРебенком = ?(Стационар,
				Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо7миЛетВСтационаре,
				Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо7миЛетАмбулаторно);
		ИначеЕсли НаименьшийВозрастПоУходу >= 7 И НаименьшийВозрастПоУходу < 15 Тогда
			Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаРебенком;
			Документ.СлучайУходаЗаБольнымРебенком = ?(Стационар,
				Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо15тиЛетВСтационаре,
				Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо15тиЛетАмбулаторно);
		ИначеЕсли НаименьшийВозрастПоУходу >= 15 Тогда
			Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаВзрослым;
		КонецЕсли;
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "10"
		Или Документ.КодПричиныНетрудоспособности = "11" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ОбщееЗаболевание;
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "12" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаРебенком;
		Документ.СлучайУходаЗаБольнымРебенком = ?(Стационар,
			Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо7миЛетПриОсобомЗаболеванииВСтационаре,
			Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо7миЛетПриОсобомЗаболеванииАмбулаторно);
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "13" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаРебенком;
		Документ.СлучайУходаЗаБольнымРебенком = ?(Стационар,
			Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуЗаРебенкомИнвалидомВСтационаре,
			Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуЗаРебенкомИнвалидомАмбулаторно);
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "14" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаРебенком;
		Документ.СлучайУходаЗаБольнымРебенком = ?(Стационар,
			Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо15тиЛетПриПоствакцинальномОсложненииВСтационаре,
			Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо15тиЛетПриПоствакцинальномОсложненииАмбулаторно);
		
	ИначеЕсли Документ.КодПричиныНетрудоспособности = "15" Тогда
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаРебенком;
		Документ.СлучайУходаЗаБольнымРебенком = Перечисления.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо15тиЛетПриВИЧ;
		
	Иначе
		Документ.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ОбщееЗаболевание;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаРеестровЭЛН

Процедура ЗагрузитьРезультатОтправкиРеестраЭЛН(Документ, ЭлементыRow, Результат)
	ТребуетсяВывестиТекстСообщенияФСС = ЗначениеЗаполнено(Результат.ТекстСообщенияФСС);
	Для Каждого Row Из ЭлементыRow Цикл
		ТребуетсяАктуализацияХеша = Ложь;
		ТекстыОшибок = Новый Массив;
		
		НомерЛН = Row.lnCode;
		ЭЛНПринят = (Строка(Row.status) = "1");
		
		Хеш = Неопределено;
		Если Row.Свойства().Получить("lnHash") <> Неопределено Тогда
			Хеш = СериализацияБЗК.ЗначениеXML(Row.lnHash, Тип("Строка"));
		КонецЕсли;
		
		Если Не ЭЛНПринят Тогда
			Errors = СериализацияБЗК.СписокXDTO(Row, "errors.error");
			Для Каждого Error Из Errors Цикл
				ТекстыОшибок.Добавить(СтрШаблон("[%1] %2", СокрЛП(Error.errCode), СокрЛП(Error.errMess)));
				Если СтрСравнить(Error.errCode, "ERR_013") = 0 Тогда
					// Отказ не включается, т.к. будет предпринята попытка зачитать актуальные хеши и повторная попытка отправки.
					ТребуетсяАктуализацияХеша = Истина;
					ТекстыОшибок.Добавить(НСтр("ru = 'Будет предпринята попытка актуализации Хэша ЭЛН'")); // АПК:1036 "Хэш ЭЛН" - термин ФСС.
				КонецЕсли;
				Если ТребуетсяВывестиТекстСообщенияФСС Тогда
					ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Результат.Отказ, Результат.ТекстСообщенияФСС, "");
					ТребуетсяВывестиТекстСообщенияФСС = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		СтрокаДанныхЭЛН = Документ.ДанныеЭЛН.Найти(НомерЛН, "НомерЛисткаНетрудоспособности");
		Если СтрокаДанныхЭЛН = Неопределено Тогда
			ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(Результат.Отказ, СтрСоединить(ТекстыОшибок, Символы.ПС), "");
			ЗарплатаКадрыОтображениеОшибок.СообщитьОбОшибке(
				Результат.Отказ,
				СтрШаблон(НСтр("ru = 'ЭЛН № %1 не найден в документе.'"), НомерЛН),
				"НомерЛисткаНетрудоспособности",
				"ДанныеЭЛН",
				0,
				"Объект");
		Иначе
			СтрокаДанныхЭЛН.ТребуетсяАктуализацияХеша = ТребуетсяАктуализацияХеша;
			СтрокаДанныхЭЛН.КомментарийКСостоянию = СтрСоединить(ТекстыОшибок, Символы.ПС);
			Если ЭЛНПринят Тогда
				СтрокаДанныхЭЛН.Состояние = Перечисления.СостоянияЭЛНВРеестрахЭЛН.ПринятФСС;
			Иначе
				СтрокаДанныхЭЛН.Состояние = Перечисления.СостоянияЭЛНВРеестрахЭЛН.НеПринятФСС;
			КонецЕсли;
		КонецЕсли;
		
		РегистрыСведений.СведенияОбЭЛН.ПриЗагрузкеОтветаФССПоРеестру(НомерЛН, Документ, СтрокаДанныхЭЛН, Хеш);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ВыгрузкаЭЛН

Функция ОписаниеФайлаЭЛН(НомерЛН, ГоловнаяОрганизация, ИдентификаторФормы) Экспорт
	Результат = Новый Структура("Адрес, ИмяБезРасширения");
	
	ТекстXML = РегистрыСведений.СведенияОбЭЛН.ИсходныйXML(НомерЛН, ГоловнаяОрганизация);
	Если Не ЗначениеЗаполнено(ТекстXML) Тогда
		Возврат Неопределено
	КонецЕсли;
	Если СтрСравнить(Лев(СокрЛ(ТекстXML), 5), "<?xml") = 0 Тогда
		Кодировка = СериализацияБЗК.КодировкаXML(ТекстXML);
	Иначе
		ТекстXML = "<?xml version='1.0' encoding='UTF-8'?>" + Символы.ПС + ТекстXML;
		Кодировка = "UTF-8";
	КонецЕсли;
	
	ДвоичныеДанные = ЗарплатаКадры.СтрокаВДвоичныеДанные(ТекстXML, Кодировка, Истина);
	Результат.Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ИдентификаторФормы);
	
	Попытка
		СтруктураDOM = СериализацияБЗК.СтруктураDOM(ТекстXML);
		УзелЗапроса = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'requestId']");
		Если УзелЗапроса = Неопределено Тогда
			УзелЗапроса = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'REQUEST_ID']");
		КонецЕсли;
		Если УзелЗапроса <> Неопределено Тогда
			Результат.ИмяБезРасширения = УзелЗапроса.ТекстовоеСодержимое;
		КонецЕсли;
	Исключение
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Ошибка при получении содержимого XML: %1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗарплатаКадрыОтображениеОшибок.СообщитьПользователю(ТекстОшибки);
	КонецПопытки;
	Если Не ЗначениеЗаполнено(Результат.ИмяБезРасширения) Тогда
		Результат.ИмяБезРасширения = "getPrivateLNData_" + НомерЛН;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область XML

// Преобразует строку XML (полученную из текста элемента или значения атрибута XML) в значение типа Строка.
//
// Параметры:
//   * ЗначениеXML - Строка, Неопределено, ОбъектXDTO - Значение свойства объекта, полученное из XML.
//
// Возвращаемое значение:
//   Строка
//
Функция ЗначениеXMLТипаСтрока(ЗначениеXML)
	ТипЗначенияXML = ТипЗнч(ЗначениеXML);
	Если ТипЗначенияXML = Тип("Строка") Тогда
		Возврат СокрЛП(ЗначениеXML);
	ИначеЕсли ЗначениеXML = Неопределено Или ТипЗначенияXML = Тип("ОбъектXDTO") Тогда
		Возврат "";
	Иначе
		Возврат СокрЛП(Строка(ЗначениеXML));
	КонецЕсли;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
