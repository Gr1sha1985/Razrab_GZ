#Область ПрограммныйИнтерфейс

Функция ЕстьПравоНаИзменениеДанныхИСвойствТранзакций() Экспорт
	
	Возврат (ПравоДоступа("Изменение", Метаданные.Справочники.ТранзакцииОбменаСБанками)
		И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СвойстваТранзакцийОбменаСБанками));
	
КонецФункции

Функция ДоступнаОблачнаяКриптография() Экспорт
	
	Возврат УниверсальныйОбменСБанками.ДоступнаОблачнаяКриптография();
	
КонецФункции

Функция СервисТранзакции(Транзакция) Экспорт
	
	Документооборот = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Транзакция, "Документооборот");
	Сервис = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документооборот, "Сервис");
	Возврат Сервис;
	
КонецФункции

Функция ТранзакцииПоПредмету(Предмет, Знач Отбор = Неопределено) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ТранзакцииПоПредмету(Предмет, Отбор);
	
КонецФункции

Функция ОрганизацияТранзакции(Транзакция) Экспорт
	
	Если ТипЗнч(Транзакция) = Тип("Массив") Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Транзакция, "Организация");
	Иначе
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Транзакция, "Организация");
	КонецЕсли;
	
КонецФункции

Функция ДатаТранспортаТранзакции(Транзакция) Экспорт
	
	Если ТипЗнч(Транзакция) = Тип("Массив") Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Транзакция, "ДатаТранспорта");
	Иначе
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Транзакция, "ДатаТранспорта");
	КонецЕсли;
	
КонецФункции

// См. УниверсальныйОбменСБанками.ПротоколОбработкиОтчетностиБанком
Функция ПротоколОбработкиОтчетностиБанком(ПредметИлиТранзакция, ТипыДокументовПротокола, ТипыТранзакцийПротокола = Неопределено) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ПротоколОбработкиОтчетностиБанком(ПредметИлиТранзакция, ТипыДокументовПротокола, ТипыТранзакцийПротокола);
	
КонецФункции

Функция ПредметТранзакции(Транзакция) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ПредметТранзакции(Транзакция);
	
КонецФункции

Функция ОбновитьДанныеРесурсаВЖурналеОперацийСФайлами(Идентификатор, ИсходноеИмяФайла, ИдентификаторАрхива, ИмяРесурса, Знач Данные, Распаковать = Ложь) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Выполнено", Ложь);
	Результат.Вставить("ОписаниеОшибки", "");
	Результат.Вставить("РаспакованныеДанные", Неопределено);
	
	// Выбираем перечень файлов в пакете.
	Выборка = УниверсальныйОбменСБанками.
				ВыбратьДанныеВЖурналеОперацийСФайламиПоИдентификатору(Идентификатор, ИсходноеИмяФайла, ИдентификаторАрхива);
	Если Выборка.Количество() = 0 Тогда
		Результат.ОписаниеОшибки = НСтр("ru='Данные о вложении не найдены.'");
		Возврат Результат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ЖурналОперацийСФайламиОбменаСБанками.СоздатьМенеджерЗаписи();
	
	Выборка.Следующий();
	МенеджерЗаписи.Идентификатор       = Идентификатор;
	МенеджерЗаписи.ИсходноеИмяФайла    = Выборка.ИсходноеИмяФайла;
	МенеджерЗаписи.Предмет             = Выборка.Предмет;
	МенеджерЗаписи.Организация         = Выборка.Организация;
	МенеджерЗаписи.ИдентификаторАрхива = ИдентификаторАрхива;
	МенеджерЗаписи.Прочитать();
	
	Попытка
		Если Распаковать Тогда
			// Распаковка сжатого файла.
			РезультатРаспаковки = УниверсальныйОбменСБанками.
				РаспаковатьДвоичныеДанные(Данные, ИсходноеИмяФайла);
			Если НЕ РезультатРаспаковки.Выполнено Тогда
				Результат.ОписаниеОшибки = РезультатРаспаковки.ОписаниеОшибки;
				Возврат Результат;
			КонецЕсли;
			Данные = РезультатРаспаковки.РаспакованныеДанные;
			Результат.РаспакованныеДанные = Данные;
		КонецЕсли;
		МенеджерЗаписи[ИмяРесурса] = Новый ХранилищеЗначения(Данные, Новый СжатиеДанных(9));
		Если ИмяРесурса = "Подпись" И Данные <> Неопределено Тогда
			МенеджерЗаписи.ПодписьВыполнена = Истина;
			МенеджерЗаписи.ПодписьНеДействительна = Ложь;
		КонецЕсли;
		МенеджерЗаписи.Записать();
	Исключение
		Результат.ОписаниеОшибки = УниверсальныйОбменСБанкамиКлиентСервер.ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке());
		Возврат Результат;
	КонецПопытки;
	
	Результат.Выполнено = Истина;
	Возврат Результат;
	
КонецФункции

Функция СтруктураДанныхСертификатаИзДвоичныхДанных(ДанныеСертификата) Экспорт
	
	СертификатКриптографии = Новый СертификатКриптографии(ДанныеСертификата);
	Возврат УниверсальныйОбменСБанкамиКлиентСервер.СертификатКриптографииВСтуктуру(СертификатКриптографии);
	
КонецФункции

Функция СтрокаОтпечаткаВДвоичныеДанные(Строка) Экспорт
	
	Возврат УниверсальныйОбменСБанками.СтрокаОтпечаткаВДвоичныеДанные(Строка);
	
КонецФункции

Функция ТекущаяДатаНаСервере() Экспорт
	
	Возврат УниверсальныйОбменСБанками.ТекущаяДатаНаСервере();
	
КонецФункции

Функция СертификатШифрованияБанков(Сервис, СписокБанков = Неопределено) Экспорт
	
	Сертификаты = УниверсальныйОбменСБанками.СертификатШифрованияБанков(Сервис, СписокБанков);
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Сертификаты);
	
КонецФункции

Функция СертификатыПодписиБанка(Банк) Экспорт
	
	Сертификаты = УниверсальныйОбменСБанками.СертификатыПодписиБанка(Банк);
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Сертификаты);
	
КонецФункции

Функция СертификатАвторизацииБанка(Банк) Экспорт
	
	Возврат УниверсальныйОбменСБанками.СертификатАвторизацииБанка(Банк);
	
КонецФункции

// Получает сертификат, сопоставленный организации.
//
// Параметры:
//  Сервис - ПеречислениеСсылка.СервисыОбменаСБанками - сервис, для которого запрашивается сертификат.
//  Организация  - СправочникСсылка.Организации - организация, которой сопоставлен сертификат.
//
// Возвращаемое значение:
//   ОпределяемыйТип.ОтпечатокСертификатаДляОбменаСБанками - строковое значение отпечатка сертификата.
//      Если сертификат для организации и сервиса не задан, возвращается пустая строка.
//
Функция СертификатОрганизации(Знач Сервис, Знач Организация) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СертификатыОрганизацийДляОбменаСБанками.СертификатОтпечаток КАК Сертификат
	|ИЗ
	|	РегистрСведений.СертификатыОрганизацийДляОбменаСБанками КАК СертификатыОрганизацийДляОбменаСБанками
	|ГДЕ
	|	СертификатыОрганизацийДляОбменаСБанками.Организация = &Организация
	|	И СертификатыОрганизацийДляОбменаСБанками.Сервис = &Сервис");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Сервис", Сервис);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ?(Выборка.Следующий(), Выборка.Сертификат, "");
	
КонецФункции

// Получает признак сохранения сертификата, сопоставленного организации.
//
// Параметры:
//  Сервис - ПеречислениеСсылка.СервисыОбменаСБанками - сервис, для которого запрашивается сертификат.
//  Организация  - СправочникСсылка.Организации - организация, которой сопоставлен сертификат.
//
// Возвращаемое значение:
//   Булево, Неопределено - значение признака ЗапомнитьСертификат. Если нет данных по организации и сервису, то возвращается Ложь.
//
Функция ПризнакЗапомнитьСертификатОрганизации(Знач Сервис, Знач Организация) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СертификатыОрганизацийДляОбменаСБанками.ЗапомнитьСертификат КАК ЗапомнитьСертификат
	|ИЗ
	|	РегистрСведений.СертификатыОрганизацийДляОбменаСБанками КАК СертификатыОрганизацийДляОбменаСБанками
	|ГДЕ
	|	СертификатыОрганизацийДляОбменаСБанками.Организация = &Организация
	|	И СертификатыОрганизацийДляОбменаСБанками.Сервис = &Сервис");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Сервис", Сервис);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ?(Выборка.Следующий(), Выборка.ЗапомнитьСертификат, Ложь);
	
КонецФункции

// Сопоставляет сертификат для организации.
//
// Параметры:
//  Организация  - СправочникСсылка.Организации - организация, которой сопоставлен сертификат.
//  ОпределяемыйТип.ОтпечатокСертификата - хеш-сумма от данных сертификата.
//
// Возвращаемое значение:
//   Булево      - если Истина, то сертификат успешно сопоставлен; иначе Ложь.
//
Функция СохранитьСертификатОрганизации(Знач Сервис, Знач Организация, Знач ОтпечатокСертификата, ЗапомнитьВыбор = Неопределено) Экспорт
	
	Запись = РегистрыСведений.СертификатыОрганизацийДляОбменаСБанками.СоздатьМенеджерЗаписи();
	Запись.Сервис = Сервис;
	Запись.Организация = Организация;
	Запись.Прочитать();
	Запись.Сервис = Сервис;
	Запись.Организация = Организация;
	Запись.СертификатОтпечаток = ОтпечатокСертификата;
	Если ЗапомнитьВыбор <> Неопределено Тогда
		Запись.ЗапомнитьСертификат = ЗапомнитьВыбор;
	КонецЕсли;
	
	Запись.Записать(Истина);
	
	Возврат Истина;
	
КонецФункции

Функция РаспаковатьДвоичныеДанные(Данные, ИмяФайла, Идентификатор = Неопределено) Экспорт
	
	Возврат УниверсальныйОбменСБанками.РаспаковатьДвоичныеДанные(Данные, ИмяФайла, Идентификатор);
	
КонецФункции

Функция НайтиСертификатБанкаТранзакцииПоОтпечатку(Транзакция, Отпечаток) Экспорт
	
	СертификатыБанка = УниверсальныйОбменСБанками.СертификатыПодписиБанка(Транзакция.Банк);
	ОтпечатокСтрока = УниверсальныйОбменСБанкамиКлиентСервер.ДвоичныеДанныеВСтроку(Отпечаток);
	СертификатНайден = СертификатыБанка.Найти(ОтпечатокСтрока, "СертификатОтпечаток") <> Неопределено;
	
	Возврат СертификатНайден;
	
КонецФункции

Функция ПолучитьДанныеХранилища(Хранилище) Экспорт
	
	Возврат Хранилище.Получить();
	
КонецФункции

Функция ОпределитьТипТранзакцииПоСсылке(Сервис, ТипТранзакцииОбменСБанками) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ОпределитьТипТранзакцииПоСсылке(Сервис, ТипТранзакцииОбменСБанками);
	
КонецФункции

Функция ОпределитьТипДокументаПоСсылке(Сервис, ВидДокументовОбменСБанками) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ОпределитьТипДокументаПоСсылке(Сервис, ВидДокументовОбменСБанками);
	
КонецФункции

Функция ОпределитьСервисПоСсылке(СсылкаНаСервис) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ОпределитьСервисПоСсылке(СсылкаНаСервис);
	
КонецФункции

Функция ОпределитьДокументооборотПоСсылке(СсылкаДокументооборот) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ОпределитьТипДокументооборотаПоСсылке(СсылкаДокументооборот);
	
КонецФункции

Функция ЭтоОблачныйСертификатПользователя(Отпечаток) Экспорт
	
	Возврат УниверсальныйОбменСБанкамиПовтИсп.ЭтоОблачныйСертификатПользователя(Отпечаток);
	
КонецФункции

Функция ПоместитьДанныеТранзакцииВЖурналОперацийСФайлами(Транзакция) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ПоместитьДанныеТранзакцииВЖурналОперацийСФайлами(Транзакция);
	
КонецФункции

Функция ПоместитьДанныеЖурналаОперацийСФайламиВДанныеТранзакции(Транзакция, Идентификатор) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ПоместитьДанныеЖурналаОперацийСФайламиВДанныеТранзакции(Транзакция, Идентификатор);
	
КонецФункции

Функция РасшифроватьИПроверитьПодписьВЖурналеОперацийСФайлами(Идентификатор, Транзакция) Экспорт
	
	Возврат УниверсальныйОбменСБанками.РасшифроватьИПроверитьПодписьВЖурналеОперацийСФайлами(Идентификатор, Транзакция);
	
КонецФункции

Функция ОбработатьРезультатРасшифровкиТранзакции(Транзакция, РезультатРасшифровки) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ОбработатьРезультатРасшифровкиТранзакции(Транзакция, РезультатРасшифровки);
	
КонецФункции

// Используется для получения параметров, используемых при отладке функциональности.
//
// Параметры:
//  Параметр     - Строка - имя настройки, хранящейся в константе ПараметрыТестированияУниверсальногоОбменаСБанками.
//
// Возвращаемое значение:
//   Булево или иное - значение особого параметра, задействующего анализируемую отладку.
//
Функция ПолучитьПараметрТестирования(Параметр) Экспорт
	
	Возврат  УниверсальныйОбменСБанками.ПолучитьПараметрТестирования(Параметр);
	
КонецФункции

Функция РеквизитыОрганизации(Организация, Реквизиты) Экспорт
	
	Возврат УниверсальныйОбменСБанками.РеквизитыОрганизации(Организация, Реквизиты);
	
КонецФункции

// См. УниверсальныйОбменСБанками.ДокументооборотыПоПредмету.
Функция ДокументооборотыПоПредмету(Предмет, Отбор = Неопределено, ВключаяПомеченныеНаУдаление = Ложь) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ДокументооборотыПоПредмету(Предмет, Отбор, ВключаяПомеченныеНаУдаление);
	
КонецФункции

Функция ПоследнийДокументооборотПоПредмету(Предмет, Знач Отбор = Неопределено, ВключаяПомеченныеНаУдаление = Ложь) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ПоследнийДокументооборотПоПредмету(Предмет, Отбор, ВключаяПомеченныеНаУдаление);
	
КонецФункции

#Область ПрограммныйИнтерфейсДляПотребителей

// См. УниверсальныйОбменСБанками.ПометитьНаУдалениеТранзакцию
Процедура ПометитьНаУдалениеТранзакции(Транзакции) Экспорт
	
	УниверсальныйОбменСБанками.ПометитьНаУдалениеТранзакции(Транзакции);
	
КонецПроцедуры

Функция ТранзакцииТребующиеРасшифровки(Сервис = Неопределено, Организация = Неопределено, СписокТранзакций = Неопределено) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ТранзакцииТребующиеРасшифровки(Сервис, Организация, СписокТранзакций);
	
КонецФункции

Функция НайтиОблачныйСертификатВХранилище(Отпечаток) Экспорт
	
	Возврат УниверсальныйОбменСБанками.НайтиОблачныйСертификатВХранилище(Отпечаток);
	
КонецФункции

Функция ОтправитьТранзакцииВБанк(Сервис, Транзакции) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ОтправитьТранзакцииНаСервер(Сервис, Транзакции);
	
КонецФункции

Функция ВременныеДанныеТранзакции(Идентификатор, СписокФайлов = Неопределено, ИсходныеДанные = Ложь, Результаты = Ложь, Подписи = Ложь, РезультатыПослеРасшифровки = Ложь) Экспорт
	
	Результат = УниверсальныйОбменСБанками.ВременныеДанныеТранзакции(Идентификатор, СписокФайлов, ИсходныеДанные, Результаты, Подписи);
	Результат = ОбщегоНазначения.ТаблицаЗначенийВМассив(Результат);
	Возврат Результат;
	
КонецФункции

Функция ПараметрыСоздатьТранзакцииИзДанныхЖурналаОперацийСФайлами() Экспорт
	
	Возврат УниверсальныйОбменСБанками.ПараметрыСоздатьТранзакцииИзДанныхЖурналаОперацийСФайлами();;
	
КонецФункции

Функция СоздатьТранзакцииИзДанныхЖурналаОперацийСФайлами(Параметры) Экспорт
	
	Возврат УниверсальныйОбменСБанками.СоздатьТранзакцииИзДанныхЖурналаОперацийСФайлами(Параметры);
	
КонецФункции

// Возвращает текст сообщения для пользователя о том, что необходимо проверить доступ к серверу обмена с банками.
//
// Параметры:
//	ИсходныйТекстОшибки - Строка - Исходное представление ошибки для пользователя.
//
// Возвращаемое значение:
//	Строка - Подробное сообщение для пользователя с рекомендацией по проверке доступа.
//
Функция ПодготовитьТекстОшибкиОбновленияДанныхСервиса(ИсходныйТекстОшибки) Экспорт

	ПараметрыСервераОбменаСБанками = УниверсальныйОбменСБанками.ПараметрыСервераОбменаСБанками();
	
	АдресСервиса = ПараметрыСервераОбменаСБанками.АдресСервиса;
	ПортСервиса  = ПараметрыСервераОбменаСБанками.ПортСервиса;

	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Если ОбщегоНазначения.КлиентПодключенЧерезВебСервер() Тогда
			// Если клиент подключен через веб-сервер, то надо проверять на том компьютере, где запущен веб-сервер.
			ШаблонСообщения = НСтр("ru = 'Возможно, заблокирован доступ к ресурсу %1 (порт %2) на веб-сервере, где опубликована база данных.'");
		Иначе
			ШаблонСообщения = НСтр("ru = 'Возможно, заблокирован доступ к ресурсу %1 (порт %2) на вашем компьютере.'");
		КонецЕсли;
	Иначе
		// Серверная база 
		ШаблонСообщения = НСтр("ru = 'Возможно, заблокирован доступ к ресурсу %1 (порт %2) на сервере 1С:Предприятия.'");
	КонецЕсли;

	Подстроки = Новый Массив;
	Подстроки.Добавить(НСтр("ru = 'В процессе обновления данных сервиса возникла ошибка:'"));
	Подстроки.Добавить(ИсходныйТекстОшибки);
	Подстроки.Добавить(Символы.ПС);
	Подстроки.Добавить(СтрШаблон(ШаблонСообщения, АдресСервиса, Формат(ПортСервиса, "ЧГ=0")));

	Возврат СтрСоединить(Подстроки, Символы.ПС);

КонецФункции

#КонецОбласти

// Получает список архивов, которые требуется создать перед формированием транзакции.
//
// Параметры:
//  Идентификатор - Строка - идентификатор транзакции.
//
// Возвращаемое значение:
//   Массив - список архивов.
//
Функция ИдентификаторыСоздаваемыхАрхивов(Знач Идентификатор) Экспорт
	
	ЗапросАрхивов = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЖурналОперацийСФайламиОбменаСБанками.ИдентификаторАрхива КАК ИдентификаторАрхива
	|ИЗ
	|	РегистрСведений.ЖурналОперацийСФайламиОбменаСБанками КАК ЖурналОперацийСФайламиОбменаСБанками
	|ГДЕ
	|	ЖурналОперацийСФайламиОбменаСБанками.Идентификатор = &Идентификатор
	|	И ЖурналОперацийСФайламиОбменаСБанками.ИдентификаторАрхива <> """"");
	ЗапросАрхивов.УстановитьПараметр("Идентификатор", Идентификатор);
	
	Возврат ЗапросАрхивов.Выполнить().Выгрузить().ВыгрузитьКолонку("ИдентификаторАрхива");
	
КонецФункции

Функция ПодготовитьАрхивыВЖурналеОперацийСФайлами(Идентификатор, ПараметрыПрогрессаВыполнения) Экспорт
	
	Возврат УниверсальныйОбменСБанками.ПодготовитьАрхивыВЖурналеОперацийСФайлами(Идентификатор, "", ПараметрыПрогрессаВыполнения);
	
КонецФункции

#КонецОбласти