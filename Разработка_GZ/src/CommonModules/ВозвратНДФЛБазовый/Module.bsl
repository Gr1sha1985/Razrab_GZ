#Область СлужебныеПроцедурыИФункции

Функция УдержанияПоРабочимМестам(РеквизитыДляПроведения) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(РеквизитыДляПроведения.Месяц));
	
	КадровыйУчет.СоздатьВТОсновныеСотрудникиФизическихЛиц(Запрос.МенеджерВременныхТаблиц, Ложь, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РеквизитыДляПроведения.ФизическоеЛицо), РеквизитыДляПроведения.Организация, КонецМесяца(РеквизитыДляПроведения.Месяц));
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Сотрудники.Сотрудник КАК Сотрудник,
	               |	&КонецМесяца КАК Период
	               |ПОМЕСТИТЬ ВТПериодыСотрудников
	               |ИЗ
	               |	ВТОсновныеСотрудникиФизическихЛиц КАК Сотрудники";
	
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТПериодыСотрудников");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Ложь, "Подразделение");
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДанныеОСотруднике.Сотрудник КАК Сотрудник,
	               |	ДанныеОСотруднике.Подразделение КАК Подразделение
	               |ИЗ
	               |	ВТКадровыеДанныеСотрудников КАК ДанныеОСотруднике";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(РеквизитыДляПроведения, Выборка);
	КонецЕсли;
	
	УдержанияПоРабочимМестам = УчетНачисленнойЗарплаты.ТаблицаРаспределенияПоРабочимМестам();
	ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
	СтатьяРасходов = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВозвратНДФЛСуммыВозврата.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	               |	СУММА(ВозвратНДФЛСуммыВозврата.Налог) КАК Сумма
	               |ИЗ
	               |	Документ.ВозвратНДФЛ.СуммыВозврата КАК ВозвратНДФЛСуммыВозврата
	               |ГДЕ
	               |	ВозвратНДФЛСуммыВозврата.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВозвратНДФЛСуммыВозврата.РегистрацияВНалоговомОргане";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = УдержанияПоРабочимМестам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, РеквизитыДляПроведения);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.СтатьяРасходов = СтатьяРасходов;
		НоваяСтрока.ВидДоходаИсполнительногоПроизводства = Перечисления.ВидыДоходовИсполнительногоПроизводства.ЗарплатаВознаграждения;
	КонецЦикла;
	
	Возврат УдержанияПоРабочимМестам;
	
КонецФункции

Процедура ОбработкаПроверкиЗаполнения(Документ, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти