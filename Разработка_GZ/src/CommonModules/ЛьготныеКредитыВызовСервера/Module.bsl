
#Область СлужебныйПрограммныйИнтерфейс

#Область ПредупреждениеОКонтактахБанка

Функция ЗаявкиДляПоказа() Экспорт
	
	Возврат Документы.ЗаявкиНаЛьготныйКредит.ЗаявкиДляПоказа();
	
КонецФункции

Функция НужноПоказатьПредупреждениеОКонтактахБанка() Экспорт
	
	Возврат Документы.ЗаявкиНаЛьготныйКредит.НужноПоказатьПредупреждениеОКонтактахБанка();
	
КонецФункции

#КонецОбласти

#Область ПредупреждениеОНеправильномСтатусе

Функция НужноПоказатьПредупреждениеОНеправильномСтатусе(ЭтоСменаОдобренныхНаОтклоненные) Экспорт
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.Документы.ЗаявкиНаЛьготныйКредит) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	КлючОбъекта = Документы.ЗаявкиНаЛьготныйКредит.КлючОбъектаЗаявкиСНеправильнымСтатусом_БольшеНеПоказывать(
		ЭтоСменаОдобренныхНаОтклоненные);
		
	БольшеНеНапоминать = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта);
	Если БольшеНеНапоминать = Истина Тогда  // Может быть Неопределено
		Возврат Ложь;
	КонецЕсли;
	
	Заявки = Документы.ЗаявкиНаЛьготныйКредит.ЗаявкиСНеправильнымиСтатусамиИзКэша(ЭтоСменаОдобренныхНаОтклоненные);
	
	Возврат Заявки.Количество() > 0;
	
КонецФункции

Функция СтатусыУжеОбновленыИзСервиса() Экспорт
	
	Ключ_ЭтоСменаОдобренныхНаОтклоненные = Документы.ЗаявкиНаЛьготныйКредит.КлючОбъектаЗаявкиСНеправильнымСтатусом(Истина);
	Заявки_ОдобренныхНаОтклоненные = ХранилищеОбщихНастроек.Загрузить(Ключ_ЭтоСменаОдобренныхНаОтклоненные);
	
	Ключ_НедоставленныеВБанк = Документы.ЗаявкиНаЛьготныйКредит.КлючОбъектаЗаявкиСНеправильнымСтатусом(Ложь);
	Заявки_НедоставленныеВБанк = ХранилищеОбщихНастроек.Загрузить(Ключ_НедоставленныеВБанк);
	
	УжеОбновлены = 
		Заявки_ОдобренныхНаОтклоненные <> Неопределено
		ИЛИ Заявки_НедоставленныеВБанк <> Неопределено;
		
	Возврат УжеОбновлены;
	
КонецФункции

Функция НачатьОпределениеЗаявокВНеправильномСтатусе() Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Документы.ЗаявкиНаЛьготныйКредит.ОпределитьЗаявкиВНеправильномСтатусеВФоне",
		"", 
		ПараметрыВыполнения);

КонецФункции
	
Процедура ИсправитьСтатусыИЗаписатьЗаявкиСНеправильнымСтатусомВКэш(ЗаявкиВНеправильномСтатусе) Экспорт
	
	ЗаявкиИзмененныеСОдобренныхНаОтклоненные = Новый Массив;
	ЗаявкиНедоставленныеВБанк = Новый Массив;
	
	Для каждого Заявка Из ЗаявкиВНеправильномСтатусе Цикл
		
		Попытка
			ЗаявкаОбъект = Заявка.Ссылка.ПолучитьОбъект();
			ЗаявкаОбъект.Состояние = Заявка.НовыйСтатус;
			ЗаявкаОбъект.ТребуетВнимания = Ложь;
			ЗаявкаОбъект.ОдобреннаяСуммаКредита = 0;
			ЗаявкаОбъект.Записать();
			
			Если Заявка.НовыйСтатус = Перечисления.СостоянияЛьготныхЗаявок.ОтклоненоБанком Тогда
				ЗаявкиИзмененныеСОдобренныхНаОтклоненные.Добавить(Заявка.Ссылка);
			ИначеЕсли Заявка.НовыйСтатус = Перечисления.СостоянияЛьготныхЗаявок.ОтправленоВФНС Тогда
				ЗаявкиНедоставленныеВБанк.Добавить(Заявка.Ссылка);
			КонецЕсли;
			
		Исключение
			
			КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
			ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Заявки на льготный кредит'", КодОсновногоЯзыка),
				УровеньЖурналаРегистрации.Ошибка,
				,
				,
				ИнформацияОбОшибке);
		
		КонецПопытки;
	
	КонецЦикла;
	
	Документы.ЗаявкиНаЛьготныйКредит.ЗаписатьЗаявкиСНеправильнымиСтатусамиВКэш(ЗаявкиИзмененныеСОдобренныхНаОтклоненные, Истина);
	Документы.ЗаявкиНаЛьготныйКредит.ЗаписатьЗаявкиСНеправильнымиСтатусамиВКэш(ЗаявкиНедоставленныеВБанк, Ложь);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НачатьПолучениеБанков() Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = Получение списка банков-участников'");
	ПараметрыВыполнения.ОжидатьЗавершение = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Документы.ЗаявкиНаЛьготныйКредит.ПолучитьСсылкуНаБанкиВФоне", 
		"", 
		ПараметрыВыполнения);

КонецФункции

#КонецОбласти
