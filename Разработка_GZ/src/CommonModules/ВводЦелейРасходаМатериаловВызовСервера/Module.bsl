////////////////////////////////////////////////////////////////////////////////
// Ввод целей расхода материалов
// Обеспечивает быстрое создание элемента без открытия формы
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Переопределяет данные выбора при вводе по строке (события: Автоподбор, ОкончаниеВводаТекста)
//
// Параметры:
//  Параметры			 - Структура - содержит параметры поиска, которые будут переданы в метод ПолучитьДанныеВыбора.
//  ОкончаниеВводаТекста - Булево - истина, если обращение из события ОкончаниеВводаТекста
// Возвращаемое значение:
//   ДанныеВыбора- список значений данных выбора
//
Функция ДанныеВыбораЦелиРасхода(Параметры, ОкончаниеВводаТекста = Ложь) Экспорт

	ДанныеВыбора = Справочники.ЦелиРасходаМатериалов.ПолучитьДанныеВыбора(Параметры);
	Если ДанныеВыбора.Количество() = 0 Тогда
		СтрокаПоиска = СокрЛП(Параметры.СтрокаПоиска);
		ПредставлениеНового = СтрШаблон(НСтр("ru='Создать: %1'"), СтрокаПоиска);
		ДанныеВыбора.Добавить(СтрокаПоиска, ПредставлениеНового);
		Если ОкончаниеВводаТекста Тогда
			// Добавляем вторую строку, чтобы в этом режиме не срабатывал автовыбор единственной строки.
			ДанныеВыбора.Добавить(""); 
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеВыбора;

КонецФункции 

// Создает элемент справочника цели расхода при вводе по строке
//
// Параметры:
//  Наименование - Строка - наименование, указанное при вводе
//
// Возвращаемое значение:
//   СправочникСсылка.ЦелиРасходаМатериалов - ссылка на цель расхода или Неопределено при неудачной записи
//
Функция СоздатьЭлементСправочника(Наименование) Экспорт
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Наименование", Наименование);

	ЭлементСправочника = Справочники.ЦелиРасходаМатериалов.СоздатьЭлемент();
	ЭлементСправочника.Заполнить(ДанныеЗаполнения);
	
	Попытка
		ЭлементСправочника.Записать();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ЭлементСправочника.Ссылка;
	
КонецФункции

#КонецОбласти

