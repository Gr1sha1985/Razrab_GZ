// Универсальные механизмы интеграции ИС (ЕГАИС, ГИСМ, ВЕТИС, ...)

#Область ПрограммныйИнтерфейс

#Область ФормыДокументовИС

Процедура ПослеЗаписиВФормеОбъектаДокументаИС(Форма, Объект, ИмяПодсистемы, ПараметрыЗаписи) Экспорт
	
	Если ПараметрыЗаписи.Свойство("ПредыдущийДокументОснование")
	 И ПараметрыЗаписи.ПредыдущийДокументОснование <> Объект.ДокументОснование Тогда
		
		ПараметрыЗаписи.Вставить("Основание", ПараметрыЗаписи.ПредыдущийДокументОснование);
		
		Оповестить(
			ИнтеграцияИСКлиентСервер.ИмяСобытияИзмененОбъект(ИмяПодсистемы, Форма),
			ПараметрыЗаписи,
			Объект.Ссылка);
		
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("Основание", Объект.ДокументОснование);
	
	Оповестить(
		ИнтеграцияИСКлиентСервер.ИмяСобытияИзмененОбъект(ИмяПодсистемы, Форма),
		ПараметрыЗаписи,
		Объект.Ссылка);
	
КонецПроцедуры

Функция ОбработкаОповещенияВФормеСпискаДокументовИС(Форма, ИмяПодсистемы, ИмяСобытия, Параметр, Источник) Экспорт
	
	ОбновитьСписок = Ложь;
	
	Если ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияИзмененОбъект(ИмяПодсистемы, Форма) Тогда
		
		ОбновитьСписок = Истина;
	
	ИначеЕсли ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияИзмененоСостояние(ИмяПодсистемы) Тогда
		
		ТипЗначения = ТипЗнч(Параметр.Ссылка);
		ТипДокумента = Тип("ДокументСсылка." + ИнтеграцияИСКлиентСервер.ИмяОбъектаИзИмениФормы(Форма, Ложь));
		
		Если ТипЗначения = Тип("Массив") Тогда
			
			Для Каждого ЭлементДанных Из Параметр.Ссылка Цикл
				Если ТипЗнч(ЭлементДанных) = ТипДокумента Тогда
					ОбновитьСписок = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			ОбновитьСписок = (ТипЗначения = ТипДокумента);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = ИнтеграцияИСКлиентСервер.ИмяСобытияВыполненОбмен(ИмяПодсистемы) Тогда
		
		ОжидаемоеСвойство = ИнтеграцияИСКлиентСервер.ИмяСвойстваОбновлятьСтатусВФормахДокументов(ИмяПодсистемы);
		
		Если (ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство(ОжидаемоеСвойство)) Тогда
			
			Параметр.Свойство(ОжидаемоеСвойство, ОбновитьСписок);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбновитьСписок Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "Список") Тогда
			Форма.Элементы.Список.Обновить();
		КонецЕсли;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "СписокКОформлению") Тогда
			Форма.Элементы.СписокКОформлению.Обновить();
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОбновитьСписок;
	
КонецФункции

Процедура ПредставлениеСохраненногоВыбораОбработкаНавигационнойСсылки(Форма, НавигационнаяСсылкаФорматированнойСтроки) Экспорт
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СброситьСохраненныеДанныеВыбораПоМаркируемойПродукции" Тогда
		
		Форма.СохраненВыборПоМаркируемойПродукции = Ложь;
		Форма.ДанныеВыбораПоМаркируемойПродукции  = Неопределено;
		ШтрихкодированиеИСКлиентСервер.ОтобразитьСохраненныйВыборПоМаркируемойПродукции(Форма);
		Форма.Модифицированность = Истина;
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьНоменклатуру" Тогда
		
		ПоказатьЗначение(, Форма.ДанныеВыбораПоМаркируемойПродукции.Номенклатура);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьХарактеристику" Тогда
		
		ПоказатьЗначение(, Форма.ДанныеВыбораПоМаркируемойПродукции.Характеристика);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьСерию" Тогда
		
		ПоказатьЗначение(, Форма.ДанныеВыбораПоМаркируемойПродукции.Серия);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьШаблонЭтикетки" Тогда
		
		ПоказатьЗначение(, Форма.ДанныеВыбораПоМаркируемойПродукции.ШаблонЭтикетки);
		
	Иначе
		
		ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаФорматированнойСтроки);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФормыДокументовОснований

Функция ОбработкаОповещенияВФормеДокументаОснования(Форма, Объект, ИмяСобытия, Параметр, Источник) Экспорт
	
	СобытиеОбработано = Ложь;
	ИнтегрируемыеПодсистемы = ИнтеграцияИСКлиентСервер.ИнтегрируемыеПодсистемыВФормеДокументаОснования(Форма);
	
	Если НЕ ЗначениеЗаполнено(ИнтегрируемыеПодсистемы) Тогда
		Возврат СобытиеОбработано;
	КонецЕсли;
	
	// Вызовем обработки оповещения всех подсистем.
	Для Каждого КлючИЗначение Из ИнтегрируемыеПодсистемы Цикл
		
		ИмяПодсистемы = КлючИЗначение.Ключ;
		
		Если НЕ ИнтеграцияИСКлиентСервер.ЭтоИмяСобытияОповещения(ИмяСобытия, ИмяПодсистемы) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПодсистемаСуществует(ИмяПодсистемы) Тогда
			
			МодульИнтеграцииКлиент = ОбщийМодуль(ИмяПодсистемы);
			
			МестоВызова = Новый Структура;
			МестоВызова.Вставить("Форма", Форма);
			МестоВызова.Вставить("Объект", Объект);
			
			Событие = Новый Структура;
			Событие.Вставить("Имя",        ИмяПодсистемы);
			Событие.Вставить("Параметр",   Параметр);
			Событие.Вставить("Источник",   Источник);
			Событие.Вставить("Обработано", Ложь);
			
			МодульИнтеграцииКлиент.ОбработкаОповещенияВФормеДокументаОснования(МестоВызова, Событие);
			
		КонецЕсли;
		
		Если Событие.Обработано Тогда
			СобытиеОбработано = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СобытиеОбработано;
	
КонецФункции

Процедура ОбработкаНавигационнойСсылкиВФормеДокументаОснования(Форма, Объект,
			Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка) Экспорт
	
	СобытиеОбработано = Ложь;
	ИнтегрируемыеПодсистемы = ИнтеграцияИСКлиентСервер.ИнтегрируемыеПодсистемыВФормеДокументаОснования(Форма);
	
	Если НЕ ЗначениеЗаполнено(ИнтегрируемыеПодсистемы) Тогда
		Возврат;
	КонецЕсли;
	
	// Определим имя подсистемы по имени элемента управления.
	//  Поскольку элемент управления есть и ведет сюда считаем что подсистема установлена
	ИмяПодсистемы = ИмяИнтегрируемойПодсистемыПоИмениЭлементаФормы(ИнтегрируемыеПодсистемы, Элемент);
	
	Если НЕ ИнтеграцияИСКлиентСервер.ЭтоИмяКомандыНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, ИмяПодсистемы) Тогда
		
		// Имя команды сформировано не функцией ИнтеграцияИСКлиентСервер.ИмяКоманды...()
		УточнениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Некорректное имя команды навигационной ссылки: ""%1""'"),
			НавигационнаяСсылкаФорматированнойСтроки);
		
		ВызватьИсключение ИнтеграцияИСКлиентСервер.ТекстОшибки(ПредставлениеПодсистемы(ИмяПодсистемы), УточнениеОшибки); // некорректное имя команды навигационной ссылки
		
	КонецЕсли;
	
	// Вызовем обработку навигационной ссылки подсистемы.
	МодульИнтеграцииКлиент = ОбщийМодуль(ИмяПодсистемы);
	МодульИнтеграцииКлиент.ОбработкаНавигационнойСсылкиВФормеДокументаОснования(
		Форма,
		Объект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка,
		СобытиеОбработано);
	
КонецПроцедуры

Процедура ОбновитьПолеИнтеграцииВФормеДокументаОснования(Форма, Подсистема) Экспорт
	
	ИмяРеквизитаОбъект = Форма.ПараметрыИнтеграцииГосИС.Получить(Подсистема.Имя).ИмяРеквизитаФормыОбъект;
	
	ТекстНадписи = "";
	ПараметрыИнтеграции = Форма.ПараметрыИнтеграцииГосИС.Получить(СтрШаблон("%1.ДокументОснование", Подсистема.Имя));
	Если ПараметрыИнтеграции <> Неопределено И ЗначениеЗаполнено(ПараметрыИнтеграции.ИмяРеквизитаФормы) Тогда
		ИмяРеквизитаФормы = ПараметрыИнтеграции.ИмяРеквизитаФормы;
		ТекстНадписи = Подсистема.МодульВызовСервера.ТекстНадписиПоляИнтеграцииВФормеДокументаОснования(Форма[ИмяРеквизитаОбъект].Ссылка);
		Форма[ИмяРеквизитаФормы] = ТекстНадписи;
		Если ЗначениеЗаполнено(ПараметрыИнтеграции.ИмяЭлементаФормы) Тогда
			Форма.Элементы[ПараметрыИнтеграции.ИмяЭлементаФормы].Видимость = ЗначениеЗаполнено(ТекстНадписи);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СерииНоменклатуры

// Функция проверяет необходимость обновления статусов серий в строке.
//
// Параметры:
//  Форма                  - ФормаКлиентскогоПриложения - форма документа, в которой инициировано указание серий;
//  Элемент                - ТаблицаФормы     - таблица формы, отображающая ТЧ товаров;
//  КэшированныеЗначения   - Произвольный     - кэшированные значения формы;
//  ПараметрыУказанияСерий - Структура        - параметры указания серий таблицы;
//  Удаление               - Булево           - признак удаления строки
// 
// Возвращаемое значение:
//  Булево - необходимо обновить статусы серий.
//
Функция НеобходимоОбновитьСтатусыСерий(Форма, Элемент, КэшированныеЗначения, ПараметрыУказанияСерий = "", Удаление = Ложь) Экспорт
	
	Обновить = Ложь;
	ИнтеграцияИСКлиентПереопределяемый.УстановитьОбновитьСтатусыСерий(Обновить, Форма, Элемент, КэшированныеЗначения, ПараметрыУказанияСерий, Удаление);
	Возврат Обновить;
	
КонецФункции

Процедура ОткрытьПодборСерий(Форма, ПараметрыУказанияСерий = "", Текст, СтандартнаяОбработка, ТекущиеДанные = Неопределено) Экспорт
	
	ПодборНеВыполнен = Ложь;

	ИнтеграцияИСКлиентПереопределяемый.ЗаполнитьДляУказанияСерийНуженСерверныйВызов(
		ПодборНеВыполнен,
		Форма,
		?(ПараметрыУказанияСерий = "",Форма.ПараметрыУказанияСерий, ПараметрыУказанияСерий),
		Текст,
		ТекущиеДанные,
		СтандартнаяОбработка);
	
	Если ПодборНеВыполнен Тогда
		
		ТекстИсключения = НСтр("ru = 'Ошибка при попытке указать серии - 
			| в этом документе для указания серий нужен контекстный серверный вызов.'");
		ВызватьИсключение ТекстИсключения;
	
	КонецЕсли;
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаВСпискахДокументов

// Функция проверяет что
//  * в динамическом списке формы из которого производится выбор есть строки
//  * пользователь не выбрал строки группировок.
// Параметры:
//  ДинамическийСписок         - ТаблицаФормы - динамический список формы
//  ПоТекущейСтроке            - Булево       - переключатель единичного и множественного выбора
//  СообщатьОшибкуПользователю - Булево       - необходимость вывести "сообщение по умолчанию",
//                                              если Ложь то взаимодействие с пользователем оставляется для вызывающей функции.
//
// Возвращаемое значение:
//  Булево - признак корректности выбора
Функция ВыборСтрокиДинамическогоСпискаКорректен(ДинамическийСписок, ПоТекущейСтроке = Ложь, СообщатьОшибкуПользователю = Истина) Экспорт
	
	ТипГруппировка = Тип("СтрокаГруппировкиДинамическогоСписка");
	Результат = Ложь;
	
	Если ПоТекущейСтроке Тогда
		
		Если ДинамическийСписок.ТекущиеДанные <> Неопределено И ТипЗнч(ДинамическийСписок.ТекущаяСтрока) <> ТипГруппировка Тогда
			Возврат Истина;
		КонецЕсли;
		
	Иначе
		
		Если ДинамическийСписок.ВыделенныеСтроки.Количество() Тогда
			Результат = Истина;
			Для Каждого Элемент Из ДинамическийСписок.ВыделенныеСтроки Цикл
				Результат = Результат И (ТипЗнч(Элемент) <> ТипГруппировка);
			КонецЦикла;
			
			Если Результат Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если СообщатьОшибкуПользователю Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Операция недоступна для выбранного объекта'"),,
			ДинамическийСписок.Имя);
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Функция возвращает данные выделенных строк (или колонки из них) исключая строки группировки
//
// Параметры:
//  ДинамическийСписок - ТаблицаФормы - имя элемента формы связанного с динамическим списком для выбора
//  Колонка            - Строка       - вернуть результат в виде ДанныеСтроки[ИмяКолонки]
//                     - Неопределено - вернуть результат в виде массива выделенных строк
//
// Возвращаемое значение:
//  Массив - данные выделенных строк списка
Функция ВыделенныеЭлементыСпискаБезГрупп(ДинамическийСписок, Колонка = Неопределено) Экспорт
	
	ТипГруппировка = Тип("СтрокаГруппировкиДинамическогоСписка");
	Результат = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из ДинамическийСписок.ВыделенныеСтроки Цикл
		Если ТипЗнч(ВыделеннаяСтрока) <> ТипГруппировка Тогда
			Если Колонка = Неопределено Тогда
				Результат.Добавить(ВыделеннаяСтрока);
			Иначе
				Результат.Добавить(ДинамическийСписок.ДанныеСтроки(ВыделеннаяСтрока)[Колонка]);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

// Заполняет массив документов по которым должен быть установлен архивный статус и отдает для архивирования
//
// Параметры:
//   Форма                    - ФормаКлиентскогоПриложения - источник данных команды
//   СсылкаДинамическийСписок - ТаблицаФормы     - динамический список формы (при вызове из формы списка)
//                            - ДокументСсылка   - ссылка на архивируемый документ (при вызове из формы документа)
//   МодульОбработки          - ОбщийМодуль      - обработчик архивирования
//
Процедура АрхивироватьДокументы(Форма, СсылкаДинамическийСписок, МодульОбработки) Экспорт
	
	ОчиститьСообщения();
	
	Если ТипЗнч(СсылкаДинамическийСписок) = Тип("ТаблицаФормы") Тогда
		
		Если Не ВыборСтрокиДинамическогоСпискаКорректен(СсылкаДинамическийСписок) Тогда
			Возврат;
		КонецЕсли; 
	
		ДокументыКАрхивированию = ВыделенныеЭлементыСпискаБезГрупп(СсылкаДинамическийСписок);
		ТекстВопроса = НСтр("ru = 'Подтвердите операцию: по выделенным документам дальнейшие действия не требуются'");
		
	Иначе
		
		Если Не ЗначениеЗаполнено(СсылкаДинамическийСписок) Тогда
			Возврат;
		КонецЕсли;
		
		ДокументыКАрхивированию = Новый Массив;
		ДокументыКАрхивированию.Добавить(СсылкаДинамическийСписок);
		ТекстВопроса = НСтр("ru = 'Подтвердите операцию: по текущему документу дальнейшие действия не требуются'");
		
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДокументыКАрхивированию", ДокументыКАрхивированию);
	// Используется в механизме оповещений
	ДополнительныеПараметры.Вставить("Контекст",                СсылкаДинамическийСписок);
	ДополнительныеПараметры.Вставить("ДальнейшееДействие",      НСтр("ru = 'Архивирование документов'"));
	ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении", Неопределено);
	ДополнительныеПараметры.Вставить("ИдентификаторВладельца",  Форма.УникальныйИдентификатор);
	
	//@skip-warning одноименные обработчики архивирования ГосИС
	ПоказатьВопрос(
		Новый ОписаниеОповещения("АрхивироватьДокументы", МодульОбработки, ДополнительныеПараметры),
		ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

// Заполняет массив распоряжений по которым должен быть установлен архивный статус оформления и отдает для архивирования
//
// Параметры:
//   Форма              - ФормаКлиентскогоПриложения - источник данных команды
//   ДинамическийСписок - ТаблицаФормы     - динамический список распоряжений на форме
//   МодульОбработки    - ОбщийМодуль      - обработчик архивирования
//   ПустаяСсылка       - ДокументСсылка   - пустой документ ГосИС (источник данных "Документ")
//   Колонка            - Строка           - имя колонки "Документ-основание" (источник данных "Основание")
//
Процедура АрхивироватьРаспоряжения(Форма, ДинамическийСписок, МодульОбработки, ПустаяСсылка, Колонка = "ДокументОснование") Экспорт
	
	ОчиститьСообщения();
	
	Если Не ВыборСтрокиДинамическогоСпискаКорректен(ДинамическийСписок) Тогда
		Возврат;
	КонецЕсли;
	
	Распоряжения = ВыделенныеЭлементыСпискаБезГрупп(ДинамическийСписок, Колонка);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Распоряжения", Распоряжения);
	ДополнительныеПараметры.Вставить("ПустаяСсылка", ПустаяСсылка);
	// Используется в механизме оповещений
	ДополнительныеПараметры.Вставить("Контекст",                ДинамическийСписок);
	ДополнительныеПараметры.Вставить("ДальнейшееДействие",      НСтр("ru = 'Архивирование распоряжений на оформление'"));
	ДополнительныеПараметры.Вставить("ОповещениеПриЗавершении", Неопределено);
	ДополнительныеПараметры.Вставить("ИдентификаторВладельца",  Форма.УникальныйИдентификатор);
	
	//@skip-warning одноименные обработчики архивирования ГосИС
	ПоказатьВопрос(
		Новый ОписаниеОповещения("АрхивироватьРаспоряжения", МодульОбработки, ДополнительныеПараметры),
		НСтр("ru = 'Подтвердите действие: по выделенным документам не требуется оформление документов'"),
		РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

// Открывает распоряжение из списка документов к оформлению
//
// Параметры:
//   ДинамическийСписок   - ТаблицаФормы - динамический список распоряжений на форме
//   СтандартнаяОбработка - Булево       - признак стандартной обработки выбора
//   Колонка              - Строка       - имя колонки "Документ-основание" (источник данных "Основание")
//
Процедура ОткрытьРаспоряжение(ДинамическийСписок, СтандартнаяОбработка, Колонка = "ДокументОснование") Экспорт
	
	ОчиститьСообщения();
	
	Если Не ВыборСтрокиДинамическогоСпискаКорректен(ДинамическийСписок, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = ДинамическийСписок.ТекущиеДанные;
	ПоказатьЗначение( ,ТекущиеДанные[Колонка]);
	
КонецПроцедуры

// Открывает отчет о расхождениях при оформлении распоряжение из списка документов к оформлению
//
// Параметры:
//   ДинамическийСписок   - ТаблицаФормы - динамический список распоряжений на форме
//   ИмяОтчета            - Строка       - имя открываемого отчета
//   Колонка              - Строка       - имя колонки "Документ-основание" (источник данных "Основание")
//
Процедура ОткрытьОтчетОРасхожденияхИзРаспоряжений(ДинамическийСписок, ИмяОтчета, Колонка = "ДокументОснование") Экспорт
	
	ОчиститьСообщения();
	
	Если Не ВыборСтрокиДинамическогоСпискаКорректен(ДинамическийСписок) Тогда
		Возврат;
	КонецЕсли;
	
	Распоряжения = ВыделенныеЭлементыСпискаБезГрупп(ДинамическийСписок, Колонка);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Отбор", Новый Структура("ДокументОснование", Распоряжения));
	ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму(СтрШаблон("Отчет.%1.Форма", ИмяОтчета), ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаКодовМаркировки

// Открывает форму загрузки кодов маркировки из таблицы.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма источник события.
// 	ДоступнаИерархия - Булево - Признак доступности иерархии.
// 	Заголовок - Строка - Заголовок открываемой формы.
//
Процедура ОткрытьФормуЗагрузкиКодовМаркировки(Форма, ДоступнаИерархия, Заголовок) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Заголовок",               Заголовок);
	ПараметрыОткрытия.Вставить("ИспользоватьКодУпаковки", ДоступнаИерархия);
	
	ОткрытьФорму("ОбщаяФорма.ЗагрузкаКодовМаркировкиИС", ПараметрыОткрытия, Форма, Форма.УникальныйИдентификатор);
	
КонецПроцедуры

// Возвращает признак того, что владелец формы является формой загрузки кодов маркировки.
// 
// Параметры:
// 	Форма - УправляемаяФомра - форма-источник события.
// 	Владелец - ФормаКлиентскогоПриложения - владелец формы для проверки.
// Возвращаемое значение:
// 	Булево - Признак формы загрузки кодов маркировки.
//
Функция ЭтоЗагрузкаКодовМаркировки(Форма, Владелец) Экспорт
	
	Возврат ТипЗнч(Форма) = Тип("ФормаКлиентскогоПриложения")
		И Форма.ИмяФормы = "ОбщаяФорма.ЗагрузкаКодовМаркировкиИС"
		И Форма.ВладелецФормы = Владелец;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область РаботаСИнтерфейсом

// Разворачивает на форме дерево или его группу со всеми вложенными группами
//
// Параметры:
//  СтрокаДерева - ДеревоЗначений, СтрокаДереваЗначений - разворачиваемая корневая группа
//  ЭлементФормы - ТаблицаФормы - связанный с деревом элемент управляемой формы
//
Процедура РазвернутьДеревоРекурсивно(СтрокаДерева, ЭлементФормы) Экспорт
	
	КоллекцияЭлементов = СтрокаДерева.ПолучитьЭлементы();
	Для каждого Элемент Из КоллекцияЭлементов Цикл
		
		ЭлементФормы.Развернуть(Элемент.ПолучитьИдентификатор());
		РазвернутьДеревоРекурсивно(Элемент, ЭлементФормы);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПрисоединенныеФайлы

// Открывает форму сообщения обмена с государственной информационной системой
//
// Параметры:
//  Форма           - ФормаКлиентскогоПриложения - источник события
//  ИмяПодсистемы   - Строка - краткое имя библиотеки
//  ВыбраннаяСтрока - Произвольный - выбранная строка таблицы
//  ПоказатьГруппу  - Булево - действие при выборе группы 
//   * показать или вывести сообщение "Операция не может быть выполнена"
//
Процедура ПоказатьСообщенияОперации(Форма, ИмяПодсистемы, ВыбраннаяСтрока, ПоказатьГруппу = Истина) Экспорт
	
	ДанныеСтроки = Форма.Элементы.ДеревоФайлов.ДанныеСтроки(ВыбраннаяСтрока);
	
	Если ДанныеСтроки = Неопределено Тогда
		ПоказатьПредупреждение(,ИнтеграцияИСКлиентСервер.ТекстКомандаНеМожетБытьВыполнена());
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеСтроки.Файл) Тогда
		
		ИмяФормы = СтрШаблон("Справочник.%1ПрисоединенныеФайлы.Форма.ФормаЗапросОтвет", ИмяПодсистемы);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ДанныеСтроки.Файл);
		ПараметрыФормы.Вставить("Заголовок", СтрШаблон(НСтр("ru = 'Сообщения операции: %1'"), ДанныеСтроки.Операция));
		
		ОткрытьФорму(
			ИмяФормы,
			ПараметрыФормы,
			Форма,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ПоказатьГруппу И ЗначениеЗаполнено(ДанныеСтроки.Документ) Тогда
		
		ПоказатьЗначение(,ДанныеСтроки.Документ);
		
	Иначе
		
		ПоказатьПредупреждение(,ИнтеграцияИСКлиентСервер.ТекстКомандаНеМожетБытьВыполнена());
		
	КонецЕсли;
	
КонецПроцедуры

// Проверить возможность пользовательского действия по текущей строке
//
// Параметры:
//  Форма              - ФормаКлиентскогоПриложения - форма в которой происходит проверка
//  Действие           - Строка - дальнейшее действие (группа)
//  ДальнейшееДействие - Произвольный - фактическое дальнейшее действие (исходящий)
// Возвращаемое значение:
//  Булево - Истина если передача данных сообщения допустима
//
Функция ПроверитьВозможностьДействия(Форма, Действие, ДальнейшееДействие) Экспорт
	
	ТекущиеДанные = Форма.Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(,ИнтеграцияИСКлиентСервер.ТекстКомандаНеМожетБытьВыполнена());
		Возврат Ложь;
	КонецЕсли;
	
	ДальнейшееДействие = Неопределено;
	Для Каждого ЭлементСписка Из ТекущиеДанные.ДальнейшиеДействия Цикл
		Если Форма.Действия[Действие].Найти(ЭлементСписка.Значение) <> Неопределено Тогда
			ДальнейшееДействие = ЭлементСписка.Значение;
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	ПоказатьПредупреждение(,ИнтеграцияИСКлиентСервер.ТекстКомандаНеМожетБытьВыполнена());
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ИнтегрируемыеПодсистемы

Процедура ОткрытьФормуСозданияДокумента(ПолноеИмяДокумента, ДокументОснование = Неопределено, Владелец = Неопределено, ОписаниеОповещения = Неопределено) Экспорт
	
	ПараметрыФормы = Новый Структура("Основание", ДокументОснование);
	
	ОткрытьФорму(
		ПолноеИмяДокумента + ".Форма.ФормаДокумента",
		ПараметрыФормы,
		Владелец,,,,
		ОписаниеОповещения);
	
КонецПроцедуры

Процедура ОткрытьФормуСозданияДокументаСДополнительнымиПараметрами(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Основание = Новый Структура;
	Основание.Вставить("ИмяФильтра",      ДополнительныеПараметры.ИмяФильтра);
	Основание.Вставить("ЗначениеФильтра", ВыбранныйЭлемент.Значение);
	Основание.Вставить("Основание",       ДополнительныеПараметры.ДокументОснование);
	
	ПараметрыФормы = Новый Структура("Основание", Основание);
	
	ОткрытьФорму(
		ДополнительныеПараметры.ПолноеИмяДокумента + ".Форма.ФормаДокумента",
		ПараметрыФормы,
		ДополнительныеПараметры.Владелец,,,,
		ДополнительныеПараметры.ОписаниеОповещения);
	
КонецПроцедуры

Процедура ВыбратьИзСпискаИОткрытьФормуСозданияДокумента(ПараметрыВыбораИзСписка) Экспорт
	
	СписокДляВыбора = Новый СписокЗначений;
	СписокДляВыбора.ЗагрузитьЗначения(ПараметрыВыбораИзСписка.ОбъектыДляВыбора);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолноеИмяДокумента", ПараметрыВыбораИзСписка.ПолноеИмяДокумента);
	ДополнительныеПараметры.Вставить("ДокументОснование",  ПараметрыВыбораИзСписка.ДокументОснование);
	ДополнительныеПараметры.Вставить("Владелец",           ПараметрыВыбораИзСписка.Владелец);
	ДополнительныеПараметры.Вставить("ИмяФильтра",         ПараметрыВыбораИзСписка.ИмяФильтра);
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ПараметрыВыбораИзСписка.ОписаниеОповещения);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьФормуСозданияДокументаСДополнительнымиПараметрами",
		ЭтотОбъект,
		ДополнительныеПараметры);
		
	ПараметрыВыбораИзСписка.Владелец.ПоказатьВыборИзСписка(ОписаниеОповещения, СписокДляВыбора);
	
КонецПроцедуры

#КонецОбласти

Процедура СкопироватьШтрихКодВБуферОбмена(ЭлементФормы, Знач Штрихкод) Экспорт
	
	Попытка
		HTMLДокумент = ЭлементФормы.document;
		ВременноеПолеВвода = HTMLДокумент.createElement("INPUT");
		АктивныйЭлемент = HTMLДокумент.activeElement;
		ВременноеПолеВвода.value = Штрихкод;
		HTMLДокумент.body.appendChild(ВременноеПолеВвода);
		ВременноеПолеВвода.select();
		HTMLДокумент.execCommand("copy");
		HTMLДокумент.body.removeChild(ВременноеПолеВвода);
		АктивныйЭлемент.focus();
		
		ТекстСообщения = НСтр("ru = 'Штрихкод скопирован в буфер обмена.'");
		ПоказатьОповещениеПользователя(,,ТекстСообщения,БиблиотекаКартинок.УспешнаяПроверкаНаличияГосИС);
	Исключение
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(Штрихкод);
	КонецПопытки;

КонецПроцедуры

#Область ДальнейшиеДействия

// Инициализировать структура данных для подготовки сообщений к передаче
//
//  Параметры:
//   ДинамическийСписок - ТаблицаФормы - Динамический список.
//   ДальнейшееДействие - ПеречислениеСсылка - Дальнейшее действие.
//   ОповещениеПриЗавершении - ОписаниеОповещения - Оповещение при завершении проведения документов.
// Возвращаемое значение:
// 	Структура - обязательные параметры для подготовки сообщений к передаче:
// * ОповещениеПриЗавершении 
// * ПричинаНедоступности - Строка - Причина недоступности
// * ДействиеНедоступно - Булево - Признак недоступности действия.
// * ДальнейшееДействие - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействию* - Дальнейшее действие.
// * ДинамическийСписок - ТаблицаФормы - Динамический список
// * РеквизитыДокументов - Соответствие Из КлючИЗначение:
//    * Ключ - ДокументСсылка - Документ
//    * Значение - Структура - Реквизиты документа
// * НепроведенныеДокументы - Массив Из ДокументСсылка - Непроведенные документы.
// * МассивДокументов - Массив Из ДокументСсылка - Документы.
Функция СтруктураПодготовкиСообщенийКПередаче(ДинамическийСписок, ДальнейшееДействие, ОповещениеПриЗавершении) Экспорт
	
	Контекст = Новый Структура;
	Контекст.Вставить("МассивДокументов",        Новый Массив);
	Контекст.Вставить("НепроведенныеДокументы",  Новый Массив);
	Контекст.Вставить("РеквизитыДокументов",     Новый Соответствие);
	Контекст.Вставить("ДинамическийСписок",      ДинамическийСписок);
	Контекст.Вставить("ДальнейшееДействие",      ДальнейшееДействие);
	Контекст.Вставить("ДействиеНедоступно",      Ложь);
	Контекст.Вставить("ПричинаНедоступности",    "");
	Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	
	Возврат Контекст;
	
КонецФункции

// Процедура - Определить доступность действий
//
// Параметры:
//  Контекст                       - (См. СтруктураПодготовкиСообщенийКПередаче).
//  ИменаКолонокДальнейшиеДействия - Массив Из Строка - Имена колонок с дальнейшими действиями.
//  ИменаРеквизитов                - Структура,Строка,Неопределено - Имена реквизитов для сохранения в РеквизитыДокументов.
//
Процедура ОпределитьДоступностьДействий(Контекст, ИменаКолонокДальнейшиеДействия, ИменаРеквизитов = Неопределено) Экспорт
	
	ТипГруппировка = Тип("СтрокаГруппировкиДинамическогоСписка");
	
	Для Каждого ВыделеннаяСтрока Из Контекст.ДинамическийСписок.ВыделенныеСтроки Цикл
		
		Если ТипЗнч(ВыделеннаяСтрока) = ТипГруппировка Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеСтроки = Контекст.ДинамическийСписок.ДанныеСтроки(ВыделеннаяСтрока);
		ПроверкаПройдена = Ложь;
		Для Каждого ИмяКолонки Из ИменаКолонокДальнейшиеДействия Цикл
			ПроверкаПройдена = ПроверкаПройдена Или (ДанныеСтроки[ИмяКолонки] = Контекст.ДальнейшееДействие);
		КонецЦикла;
		Если ПроверкаПройдена Тогда 
			Контекст.МассивДокументов.Добавить(ДанныеСтроки.Ссылка);
			Если Не ДанныеСтроки.Проведен Тогда
				Контекст.НепроведенныеДокументы.Добавить(ДанныеСтроки.Ссылка);
			КонецЕсли;
		КонецЕсли;
		Если ИменаРеквизитов <> Неопределено Тогда
			РеквизитыДокумента = Новый Структура;
			Если ТипЗнч(ИменаРеквизитов) = Тип("Структура") Тогда
				Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
					РеквизитыДокумента.Вставить(ИмяРеквизита, ДанныеСтроки[ИмяРеквизита]);
				КонецЦикла;
			Иначе
				РеквизитыДокумента.Вставить(ИменаРеквизитов, ДанныеСтроки[ИменаРеквизитов]);
			КонецЕсли;
			Контекст.РеквизитыДокументов.Вставить(ДанныеСтроки.Ссылка, РеквизитыДокумента);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура - Подготовить сообщения к передаче
//
// Параметры:
//  Контекст - Структура - см СтруктураПодготовкиСообщенийКПередаче()
//
Процедура ПодготовитьСообщенияКПередаче(Контекст) Экспорт
	
	Если Контекст.МассивДокументов.Количество() = 0
		Или Контекст.ДействиеНедоступно Тогда
		
		Если Контекст.ДействиеНедоступно И ЗначениеЗаполнено(Контекст.ПричинаНедоступности) Тогда
			ПользовательскоеПредупреждение = Контекст.ПричинаНедоступности;
		ИначеЕсли Контекст.ДинамическийСписок.ВыделенныеСтроки.Количество() = 1 Тогда
			ПользовательскоеПредупреждение = НСтр("ru = 'Команда не может быть выполнена для указанного документа.'");
		Иначе
			ПользовательскоеПредупреждение = НСтр("ru = 'Команда не может быть выполнена для указанных документов.'");
		КонецЕсли;
		
		ПоказатьПредупреждение(,ПользовательскоеПредупреждение);
		
		Возврат;
		
	КонецЕсли;
	
	Если Контекст.НепроведенныеДокументы.Количество() > 0 Тогда
		
		Если Контекст.НепроведенныеДокументы.Количество() = 1 Тогда
			ТекстВопроса = НСтр("ru = 'Для выполнения команды необходимо предварительно провести документ. Выполнить проведение документа и продолжить?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Для выполнения команды необходимо предварительно провести документы. Выполнить проведение документов и продолжить?'");
		КонецЕсли;
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Продолжить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПодготовитьСообщенияКПередачеПодтверждениеПроведения", ИнтеграцияИСКлиент, Контекст),
			ТекстВопроса,
			Кнопки);
		
		Возврат;
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.ОповещениеПриЗавершении, Контекст);
	
КонецПроцедуры

// Только для внутреннего использования.
// 
// Параметры:
// 	ЭлементФормы - ПолФормы - Элемент формы
// Возвращаемое значение:
// 	Неопределено - Элемент формы
Функция ПолучитьФормуПоЭлементуФормы(ЭлементФормы) Экспорт
	
	Если ТипЗнч(ЭлементФормы) = Тип("ФормаКлиентскогоПриложения") Тогда
		Возврат ЭлементФормы;
	ИначеЕсли ЭлементФормы.Родитель = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ПолучитьФормуПоЭлементуФормы(ЭлементФормы.Родитель);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ДиалогПроведенияДокументов

// Только для внутреннего использования.
Процедура ПодготовитьСообщенияКПередачеПодтверждениеПроведения(Ответ, Контекст) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ДанныеОНепроведенныхДокументах = ОбщегоНазначенияВызовСервера.ПровестиДокументы(Контекст.НепроведенныеДокументы);
	ШаблонСообщения = НСтр("ru = 'Документ %1 не проведен: %2'");
	НепроведенныеДокументы = Новый Массив;
	Для Каждого ИнформацияОДокументе Из ДанныеОНепроведенныхДокументах Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				Строка(ИнформацияОДокументе.Ссылка),
				ИнформацияОДокументе.ОписаниеОшибки),
				ИнформацияОДокументе.Ссылка);
		НепроведенныеДокументы.Добавить(ИнформацияОДокументе.Ссылка);
	КонецЦикла;
	
	Контекст.МассивДокументов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(Контекст.МассивДокументов, НепроведенныеДокументы);
	
	// Оповещаем открытые формы о том, что были проведены документы.
	ТипыПроведенныхДокументов = Новый Соответствие;
	Для Каждого ПроведенныйДокумент Из Контекст.МассивДокументов Цикл
		ТипыПроведенныхДокументов.Вставить(ТипЗнч(ПроведенныйДокумент));
	КонецЦикла;
	Для Каждого Тип Из ТипыПроведенныхДокументов Цикл
		ОповеститьОбИзменении(Тип.Ключ);
	КонецЦикла;
	
	Если НепроведенныеДокументы.Количество() > 0 Тогда
		
		Если Контекст.НепроведенныеДокументы.Количество() = НепроведенныеДокументы.Количество() Тогда
			
			Если Контекст.НепроведенныеДокументы.Количество() = 1 Тогда
				ТекстПредупреждения = НСтр("ru = 'Не удалось провести документ'");
			Иначе
				ТекстПредупреждения = НСтр("ru = 'Не удалось провести документы'");
			КонецЕсли;
			
			ПоказатьПредупреждение(, ТекстПредупреждения);
			
		Иначе
			
			ТекстДиалога = НСтр("ru = 'Не удалось провести один или несколько документов.'");
			
			КнопкиДиалога = Новый СписокЗначений;
			Если Контекст.МассивДокументов.Количество() = 0 Тогда
				КнопкиДиалога.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'ОК'"));
			Иначе
				ТекстДиалога = ТекстДиалога + " " + НСтр("ru = 'Продолжить?'");
				КнопкиДиалога.Добавить(КодВозвратаДиалога.Пропустить, НСтр("ru = 'Продолжить'"));
				КнопкиДиалога.Добавить(КодВозвратаДиалога.Отмена);
			КонецЕсли;
			
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ПодготовитьСообщенияКПередачеПослеПодтверждениеПродолжения", ЭтотОбъект, Контекст),
				ТекстДиалога,
				КнопкиДиалога);
		
		КонецЕсли; 
		
	Иначе
		
		ВыполнитьОбработкуОповещения(Контекст.ОповещениеПриЗавершении, Контекст);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьСообщенияКПередачеПослеПодтверждениеПродолжения(РезультатВопроса, Контекст) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Пропустить Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОповещениеПриЗавершении, Контекст);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Формирует и выводит сообщение, которое может быть связано с элементом 
// управления формы.
//
// Параметры:
//  ИдентификаторНазначения    - УникальныйИдентификатор, Неопределено - уникальный идентификатор формы для показа сообщения.
//  ТекстСообщенияПользователю - Строка - текст сообщения.
//  КлючДанных                 - ЛюбаяСсылка - объект или ключ записи информационной базы, к которому это сообщение относится.
//  Поле                       - Строка - наименование реквизита формы.
//  ПутьКДанным                - Строка - путь к данным (путь к реквизиту формы).
//  Отказ                      - Булево - выходной параметр, всегда устанавливается в значение Истина.
//
// Пример:
// 	см ОбщегоНазначенияКлиентСервер.СообщитьПользователю.
//
Процедура СообщитьПользователюВФорму(
		Знач ИдентификаторНазначения = Неопределено,
		Знач ТекстСообщенияПользователю,
		Знач КлючДанных = Неопределено,
		Знач Поле = "",
		Знач ПутьКДанным = "",
		Отказ = Ложь) Экспорт
	
	Сообщение = Новый СообщениеПользователю;
	Если ИдентификаторНазначения <> Неопределено Тогда
		Сообщение.ИдентификаторНазначения = ИдентификаторНазначения;
	КонецЕсли;
	
	Сообщение.Текст = ТекстСообщенияПользователю;
	Сообщение.Поле = Поле;
	
	Сообщение.КлючДанных = КлючДанных;
	
	Если НЕ ПустаяСтрока(ПутьКДанным) Тогда
		Сообщение.ПутьКДанным = ПутьКДанным;
	КонецЕсли;
		
	Сообщение.Сообщить();
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСМетаданными

Функция ПодсистемаСуществует(КраткоеИмяПодсистемы)
	
	ПолноеИмяПодсистемы = ИнтеграцияИСКлиентСервер.ПолноеИмяПодсистемы(КраткоеИмяПодсистемы);
	Возврат ОбщегоНазначенияКлиент.ПодсистемаСуществует(ПолноеИмяПодсистемы);
	
КонецФункции

Функция ОбщийМодуль(КраткоеИмяПодсистемы)
	
	ИмяМодуля = ИнтеграцияИСКлиентСервер.МодульКлиент(КраткоеИмяПодсистемы);
	Возврат ОбщегоНазначенияКлиент.ОбщийМодуль(ИмяМодуля);
	
КонецФункции

Функция ПредставлениеПодсистемы(ИмяПодсистемы)
	
	Если НЕ ЗначениеЗаполнено(ИмяПодсистемы) Тогда
		Представление = НСтр("ru = '<Интеграция>'");
	Иначе
		ИмяМодуля = ИнтеграцияИСКлиентСервер.МодульКлиентСервер(ИмяПодсистемы);
		Представление = ОбщегоНазначенияКлиент.ОбщийМодуль(ИмяМодуля).ПредставлениеПодсистемы();
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

Функция ИмяИнтегрируемойПодсистемыПоИмениЭлементаФормы(ИнтегрируемыеПодсистемы, Элемент)
	
	ИмяПодсистемы = "";
	
	Для Каждого КлючИЗначение Из ИнтегрируемыеПодсистемы Цикл
		
		Если Элемент.Имя = ИнтеграцияИСКлиентСервер.ИмяПоляИнтеграцииВФормеДокументаОснования(КлючИЗначение.Ключ) Тогда
			ИмяПодсистемы = КлючИЗначение.Ключ;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(ИмяПодсистемы) Тогда
		
		УточнениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось определить имя интегрируемой подсистемы по имени элемента формы ""%1""'"),
			Элемент.Имя);
			
		ВызватьИсключение ИнтеграцияИСКлиентСервер.ТекстОшибки(, УточнениеОшибки); // неизвестное имя интегрируемой подсистемы
		
	КонецЕсли;
	
	Возврат ИмяПодсистемы;
	
КонецФункции

#КонецОбласти

#КонецОбласти
