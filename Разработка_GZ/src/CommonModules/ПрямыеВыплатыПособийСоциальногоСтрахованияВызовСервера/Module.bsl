
#Область СлужебныеПроцедурыИФункции

// Возвращает описание файла реестра прямых выплат.
//
// Параметры:
//   Реестры - Массив из ДокументСсылка.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий - Реестры ПВСО.
//
// Возвращаемое значение:
//   Структура - Описание ошибок.
//       * ДокументыСОшибками - Массив из ДокументСсылка.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий
//       * ТекстОшибки - Строка
//       * Сообщения - ФиксированныйМассив из СообщениеПользователю
//
Функция ПроверитьРеестры(Знач Реестры) Экспорт
	Результат = Новый Структура("ДокументыСОшибками, ТекстОшибки, Сообщения");
	Результат.ДокументыСОшибками = Новый Массив;
	
	Для Каждого ДокументСсылка Из Реестры Цикл
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		Если Не ДокументОбъект.ПроверитьЗаполнениеПоРегламентуФСС() Тогда
			Результат.ДокументыСОшибками.Добавить(ДокументСсылка);
		КонецЕсли;
	КонецЦикла;
	
	Результат.Сообщения = ПолучитьСообщенияПользователю(Истина);
	
	Возврат Результат;
КонецФункции

// Возвращает описание файла реестра прямых выплат.
//
// Параметры:
//   Реестры - Массив из ДокументСсылка.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий - Реестры ПВСО.
//   УникальныйИдентификатор - УникальныйИдентификатор, Неопределено - Уникальный идентификатор формы,
//       к которому следует привязать временное хранилище файла.
//
// Возвращаемое значение:
//   Массив из ОписаниеПередаваемогоФайла - Описания файлов.
//
Функция ФайлыРеестровПрямыхВыплат(Знач Реестры, Знач УникальныйИдентификатор) Экспорт
	Результат = Новый Массив;
	Для Каждого СсылкаРеестра Из Реестры Цикл
		ОписаниеФайла = Документы.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий.ФайлРеестра(
			СсылкаРеестра,
			УникальныйИдентификатор);
		Результат.Добавить(Новый ОписаниеПередаваемогоФайла(
			ОписаниеФайла.ИмяФайлаСРасширением, ОписаниеФайла.АдресДвоичныхДанных));
	КонецЦикла;
	Возврат Результат;
КонецФункции

#КонецОбласти


