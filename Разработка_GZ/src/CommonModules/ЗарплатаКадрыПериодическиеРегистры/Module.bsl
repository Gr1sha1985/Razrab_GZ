
#Область ПрограммныйИнтерфейс 

#Область ПолучениеДанныхПериодическихРегистров 

// Возвращает таблицу записей регистра.
//
// Параметры:
//		ИмяРегистра 				- Строка - имя периодического регистра сведений, как  оно
//										задано в конфигураторе.
//		МенеджерВременныхТаблиц 	- МенеджерВременныхТаблиц
//		ТолькоРазрешенные 			- Булево
//		ОписаниеФильтра 			- Структура - см. функцию ОписаниеФильтраДляСоздатьВТИмяРегистра.
//		ПараметрыПостроения 		- Структура - см. функцию ПараметрыПостроенияДляСоздатьВТИмяРегистра.
//
// Возвращаемое значение:
//		ТаблицаЗначений
//
Функция ТаблицаВТИмяРегистра(Знач ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено) Экспорт
	РезультатЗапроса = ВыполнитьЗапросПолученияДвиженийРегистра(ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, "");
	Возврат РезультатЗапроса.Выгрузить();
КонецФункции

// Возвращает таблицу среза последних регистра.
//
// Параметры:
//		ИмяРегистра 				- Строка - имя периодического регистра сведений, как  оно
//										задано в конфигураторе.
//		МенеджерВременныхТаблиц 	- МенеджерВременныхТаблиц
//		ТолькоРазрешенные 			- Булево
//		ОписаниеФильтра 			- Структура - см. функцию ОписаниеФильтраДляСоздатьВТИмяРегистра.
//		ПараметрыПостроения 		- Структура - см. функцию ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез.
//
// Возвращаемое значение:
//		ТаблицаЗначений
//					
Функция ТаблицаВТИмяРегистраСрезПоследних(Знач ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено) Экспорт
	РезультатЗапроса = ВыполнитьЗапросПолученияСрезаРегистра(ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, Истина, "");
	Возврат РезультатЗапроса.Выгрузить();	
КонецФункции

// Создает временную таблицу записей регистра в менеджере временных таблиц, переданном в качестве параметра.
//
// Параметры:
//		ИмяРегистра 				- Строка - имя периодического регистра сведений, как  оно
//										задано в конфигураторе.
//		МенеджерВременныхТаблиц 	- МенеджерВременныхТаблиц
//		ТолькоРазрешенные 			- Булево
//		ОписаниеФильтра 			- Структура - см. функцию ОписаниеФильтраДляСоздатьВТИмяРегистра.
//		ПараметрыПостроения 		- Структура - см. функцию ПараметрыПостроенияДляСоздатьВТИмяРегистра.
//		ИмяРезультирующейТаблицы 	- Строка - имя создаваемой временной таблицы, если не задано, то
//										имя результирующей таблицы будет сформировано каК ВТ<ИмяРегистра>.
//					
Процедура СоздатьВТИмяРегистра(Знач ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяРезультирующейТаблицы = Неопределено) Экспорт
	ВыполнитьЗапросПолученияДвиженийРегистра(ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяРезультирующейТаблицы)	
КонецПроцедуры

// Возвращает таблицу периодов регистра 
//
// Параметры:
//		ИмяРегистра 				- Строка - имя периодического регистра сведений, как  оно
//										задано в конфигураторе.
//		МенеджерВременныхТаблиц 	- МенеджерВременныхТаблиц
//		ТолькоРазрешенные 			- Булево
//		ОписаниеФильтра 			- Структура - см. функцию ОписаниеФильтраДляСоздатьВТИмяРегистра.
//		ПараметрыПостроения 		- Структура - см. функцию ПараметрыПостроенияДляСоздатьВТИмяРегистра.
//		
// Возвращаемое значение: 
//	ТаблицаЗначений
//
Функция ТаблицаВТИмяРегистраПериоды(Знач ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено) Экспорт
	РезультатЗапроса = ВыполнитьЗапросПолученияПериодовРегистра(ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, "");	
	Возврат РезультатЗапроса.Выгрузить(); 
КонецФункции

// Создает временную таблицу периодов регистра в менеджере временных таблиц, переданном в качестве параметра.
//
// Параметры:
//		ИмяРегистра 				- Строка - имя периодического регистра сведений, как  оно
//										задано в конфигураторе.
//		МенеджерВременныхТаблиц 	- МенеджерВременныхТаблиц
//		ТолькоРазрешенные 			- Булево
//		ОписаниеФильтра 			- Структура - см. функцию ОписаниеФильтраДляСоздатьВТИмяРегистра.
//		ПараметрыПостроения 		- Структура - см. функцию ПараметрыПостроенияДляСоздатьВТИмяРегистра.
//		ИмяРезультирующейТаблицы 	- Строка - имя создаваемой временной таблицы, если не задано, то
//										имя результирующей таблицы будет сформировано как ВТ<ИмяРегистра>Периоды.
//					
Процедура СоздатьВТИмяРегистраПериоды(Знач ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяРезультирующейТаблицы = Неопределено) Экспорт
	ВыполнитьЗапросПолученияПериодовРегистра(ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяРезультирующейТаблицы);	
КонецПроцедуры

// Создает временную таблицу среза первых регистра в менеджере временных таблиц, переданном в качестве параметра.
//
// Параметры:
//		ИмяРегистра 				- Строка - имя периодического регистра сведений, как  оно
//										задано в конфигураторе.
//		МенеджерВременныхТаблиц 	- МенеджерВременныхТаблиц
//		ТолькоРазрешенные 			- Булево
//		ОписаниеФильтра 			- Структура - см. функцию ОписаниеФильтраДляСоздатьВТИмяРегистра.
//		ПараметрыПостроения 		- Структура - см. функцию ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез.
//		ИмяРезультирующейТаблицы 	- Строка - имя создаваемой временной таблицы, если не задано, то
//										имя результирующей таблицы будет сформировано каК ВТ<ИмяРегистра>.
//					
Процедура СоздатьВТИмяРегистраСрезПервых(Знач ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяРезультирующейТаблицы = Неопределено) Экспорт
	ВыполнитьЗапросПолученияСрезаРегистра(ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, Ложь, ИмяРезультирующейТаблицы);	
КонецПроцедуры

// Создает временную таблицу среза последних регистра в менеджере временных таблиц, переданном в качестве параметра.
//
// Параметры:
//		ИмяРегистра 				- Строка - имя периодического регистра сведений, как  оно
//										задано в конфигураторе.
//		МенеджерВременныхТаблиц 	- МенеджерВременныхТаблиц
//		ТолькоРазрешенные 			- Булево
//		ОписаниеФильтра 			- Структура - см. функцию ОписаниеФильтраДляСоздатьВТИмяРегистра.
//		ПараметрыПостроения 		- Структура - см. функцию ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез.
//		ИмяРезультирующейТаблицы 	- Строка - имя создаваемой временной таблицы, если не задано, то
//										имя результирующей таблицы будет сформировано каК ВТ<ИмяРегистра>.
//					
Процедура СоздатьВТИмяРегистраСрезПоследних(Знач ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяРезультирующейТаблицы = Неопределено) Экспорт
	ВыполнитьЗапросПолученияСрезаРегистра(ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, Истина, ИмяРезультирующейТаблицы);	
КонецПроцедуры

// Возвращает запрос представления ВТИмяРегистра.
//
// Параметры:
//		ИмяРегистра - Строка - Имя регистра как задано в конфигураторе.
//		ТолькоРазрешенные - Булево
//		ОписаниеФильтра - Структура - см. функцию ОписаниеФильтраДляСоздатьВТИмяРегистра.
//		ПараметрыПостроения - Структура - см.ПараметрыПостроенияПредставленияВТТаблицаРегистра.
//		ИмяСоздаваемойТаблицы - Строка - если не указано, запрос будет создавать временную
//					таблицу ВТ<ИмяРегистра>
//
// Возвращаемое значение:
//		Запрос
//
Функция ЗапросВТИмяРегистра(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяСоздаваемойТаблицы = Неопределено) Экспорт
	Если ИмяСоздаваемойТаблицы = Неопределено Тогда
		ИмяСоздаваемойТаблицы = "ВТ" + ИмяРегистра;
	КонецЕсли;	
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	КонецЕсли;	
	
	Запрос = Неопределено;	
	ПриПолученииЗапросаВТИмяРегистра(Запрос, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);

	Если Запрос <> Неопределено Тогда
		Возврат Запрос;
	КонецЕсли;	
		
	ОписаниеПакета = НовыйОписаниеПакетаЗапросовКРегистру();
	ДобавитьЗапросВТИмяРегистра(ОписаниеПакета, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);
	
	Возврат ЗапросПоОписаниюПакета(ОписаниеПакета, ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД);
КонецФункции	

// Возвращает запрос представления ВТИмяРегистраСрез.
//
// Параметры:
//		ИмяРегистра 			- Строка - Имя регистра как задано в конфигураторе.
//		ТолькоРазрешенные 		- Булево
//		ПараметрыПостроения 	- Структура - см.ПараметрыПостроенияПредставленияВТИмяРегистраСрез.
//		ОписаниеФильтра - Структура - см. функцию ОписаниеФильтраДляСоздатьВТИмяРегистра.
//		СрезПоследних			- Булево - если Истина - срез последних, если Ложь - Срез первых.
//		ИмяСоздаваемойТаблицы 	- Строка - если не указано, запрос будет создавать временную таблицу.
//									ВТ<ИмяРегистра>СрезПоследних или ВТ<ИмяРегистра>СрезПервых
//
// Возвращаемое значение:
//		Запрос
//
Функция ЗапросВТИмяРегистраСрез(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, СрезПоследних = Истина, ИмяСоздаваемойТаблицы = Неопределено) Экспорт
	Если ИмяСоздаваемойТаблицы = Неопределено Тогда
		ИмяСоздаваемойТаблицы = "ВТ" + ИмяРегистра + ?(СрезПоследних,"СрезПоследних","СрезПервых");
	КонецЕсли;
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	КонецЕсли;
	
	Запрос = Неопределено;	
	ПриПолученииЗапросаВТИмяРегистраСрез(Запрос, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних, ИмяСоздаваемойТаблицы);

	Если Запрос <> Неопределено Тогда
		Возврат Запрос;
	КонецЕсли;	
		
	ОписаниеПакета = НовыйОписаниеПакетаЗапросовКРегистру();
	ДобавитьЗапросВТИмяРегистраСрез(ОписаниеПакета, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних, ИмяСоздаваемойТаблицы);
	
	Возврат ЗапросПоОписаниюПакета(ОписаниеПакета, ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД);
КонецФункции	

// Возвращает запрос представления ВТИмяРегистраПериоды. 
// Получение периодов по интервальному регистру сведений.
//
// Параметры:
//		ИмяРегистра 			- Строка - Имя регистра как задано в конфигураторе.
//		ТолькоРазрешенные 		- Булево
//		ОписаниеФильтра 		- Структура - см. функцию ОписаниеФильтраДляСоздатьВТИмяРегистра.
//		ПараметрыПостроения 	- Структура - см.ПараметрыПостроенияПредставленияВТИмяРегистраСрез.
//		ИмяСоздаваемойТаблицы 	- Строка - если не указано, запрос будет создавать временную таблицу.
//									ВТ<ИмяРегистра>Периоды
//
// Возвращаемое значение:
//		Запрос
//
Функция ЗапросВТПериодыИмяРегистра(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяСоздаваемойТаблицы = Неопределено) Экспорт
	Если ИмяСоздаваемойТаблицы = Неопределено Тогда
		ИмяСоздаваемойТаблицы = "ВТ" + ИмяРегистра + "Периоды";
	КонецЕсли;
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистраПериоды();
	КонецЕсли;
	
	ОписаниеПакета = НовыйОписаниеПакетаЗапросовКРегистру();
	ДобавитьЗапросВТПериодыИмяРегистра(ОписаниеПакета, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);
	
	Возврат ЗапросПоОписаниюПакета(ОписаниеПакета, ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД);
КонецФункции

// Возвращает описание источника данных - временная таблица.
//
// Параметры:
//		ТаблицаФильтра 		- Строка - имя временной таблицы
//					   		- ТаблицаЗначений
//								содержат обязательные колонки:
//									"Период" (для получения представлений среза первых или
//										среза последних)
//									"ДатаНачала" и "ДатаОкончания" (для получения таблицы регистра).
//		ИзмеренияФильтра 	- Строка - имена колонок таблицы равные именам измерений регистра,
//								по которым устанавливается фильтр, для колонок с именами отличающимися
//								от имен измерений, в значениях ключа "СоответствиеИзмеренийРегистраИзмерениямФильтра",
//								указывается соответствие имени измерения регистра имени колонки таблицы фильтра.
//								Допускается не указывать, когда ТаблицаФильтра имеет тип ТаблицаЗначений.
//		ДополнительныеПоляФильтра 		- Строка
//							- Массив - имена колонок таблицы, по которым не устанавливается фильтр,
//								но которые должны участвовать в результате запроса.
//
// Возвращаемое значение:
//		Структура - см. НовыйОписаниеФильтраДляСоздатьВТИмяРегистра.
//
Функция ОписаниеФильтраДляСоздатьВТИмяРегистра(Знач ТаблицаФильтра, ИзмеренияФильтра = "", ДополнительныеПоляФильтра = "") Экспорт
	Если ТипЗнч(ТаблицаФильтра) = Тип("Строка") Тогда
		Возврат ОписаниеФильтраДляСоздатьВТИмяРегистраПоВременнойТаблице(ТаблицаФильтра, ИзмеренияФильтра, ДополнительныеПоляФильтра);
	Иначе
		Если Не ЗначениеЗаполнено(ИзмеренияФильтра) Тогда
			ИзмеренияФильтра = Новый Массив;
			Для Каждого Колонка Из ТаблицаФильтра.Колонки Цикл
				Если ВРег(Колонка.Имя) <> ВРег("Период") 
					И ВРег(Колонка.Имя) <> ВРег("ДатаНачала")
					И ВРег(Колонка.Имя) <> ВРег("ДатаОкончания") Тогда
					
					ИзмеренияФильтра.Добавить(Колонка.Имя);
				КонецЕсли;	
			КонецЦикла;	
		КонецЕсли;
		Возврат ОписаниеФильтраДляСоздатьВТИмяРегистраПоТаблицеЗначений(ТаблицаФильтра, ИзмеренияФильтра, ДополнительныеПоляФильтра);
	КонецЕсли;	
КонецФункции

// Возвращает описание источника данных - временная таблица.
//
// Параметры:
//		ТаблицаФильтра 			- ТаблицаЗначений -	содержат обязательные колонки:
//										"Период" (для получения представлений среза первых или
//											среза последних)
//										"ДатаНачала" и "ДатаОкончания" (для получения таблицы регистра).
//		Измерения 				- Строка - имена колонок таблицы равные именам измерений регистра,
//									по которым устанавливается фильтр, для колонок с именами отличающимися
//									от имен измерений, в значениях ключа "СоответствиеИзмеренийРегистраИзмерениямФильтра",
//									указывается соответствие имени измерения регистра имени колонки таблицы фильтра.
//									Допускается не указывать, когда ТаблицаФильтра имеет тип ТаблицаЗначений.
//		ДополнительныеПоля 		- Строка
//								- Массив - имена колонок таблицы, по которым не устанавливается фильтр,
//									но которые должны участвовать в результате запроса.
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//		ИмяВТФильтр 			- Строка - имя создаваемой временной таблицы фильтра
//
// Возвращаемое значение:
//		Структура - см. НовыйОписаниеФильтраДляСоздатьВТИмяРегистра.
//
Функция ОписаниеФильтраДляСоздатьВТИмяРегистраПоТаблицеЗначений(ТаблицаФильтра, Измерения = "", ДополнительныеПоля = "", МенеджерВременныхТаблиц = Неопределено, ИмяВТФильтр = Неопределено) Экспорт	
	ОписаниеФильтра = НовыйОписаниеФильтраДляСоздатьВТИмяРегистра(Измерения, ДополнительныеПоля);
	
	ЗначениеФильтра = ФильтрСписокЗначенийПоТаблицеЗначений(ТаблицаФильтра, ОписаниеФильтра);
	
	Если ЗначениеФильтра = Неопределено Тогда
		Если МенеджерВременныхТаблиц <> Неопределено 
			И ИмяВТФильтр <> Неопределено Тогда
			
			ЗначениеФильтра = ФильтрВременнаяТаблицаПоТаблицеЗначений(МенеджерВременныхТаблиц, ТаблицаФильтра, ИмяВТФильтр);
		Иначе
			ЗначениеФильтра = ФильтрТаблицаЗначений(ТаблицаФильтра);	
		КонецЕсли;	
	КонецЕсли;	
	
	ОписаниеФильтра.ЗначениеФильтра = ЗначениеФильтра;	
	
	Возврат ОписаниеФильтра;
КонецФункции

// Возвращает описание источника данных - временная таблица.
//
// Параметры:
//		ИмяВТФильтр 			- Строка - имя временной таблицы
//		Измерения 				- Строка - имена колонок таблицы равные именам измерений регистра,
//									по которым устанавливается фильтр, для колонок с именами отличающимися
//									от имен измерений, в значениях ключа "СоответствиеИзмеренийРегистраИзмерениямФильтра",
//									указывается соответствие имени измерения регистра имени колонки таблицы фильтра.
//									Допускается не указывать, когда ТаблицаФильтра имеет тип ТаблицаЗначений.
//		ДополнительныеПоля 		- Строка
//								- Массив - имена колонок таблицы, по которым не устанавливается фильтр,
//									но которые должны участвовать в результате запроса.
//
// Возвращаемое значение:
//		Структура - см. НовыйОписаниеФильтраДляСоздатьВТИмяРегистра.
//
Функция ОписаниеФильтраДляСоздатьВТИмяРегистраПоВременнойТаблице(ИмяВТФильтр, Измерения = "", ДополнительныеПоля = "") Экспорт
	ОписаниеФильтра = НовыйОписаниеФильтраДляСоздатьВТИмяРегистра(Измерения, ДополнительныеПоля);
	ОписаниеФильтра.ЗначениеФильтра = ФильтрВременнаяТаблица(ИмяВТФильтр);
	
	Возврат ОписаниеФильтра;
КонецФункции

// Возвращает описание источника данных - временная таблица.
//
// Параметры:
//		ОписаниеПериода 		- Структура - см. ОписаниеПериодаДляСоздатьВТИмяРегистра
//		Измерение 				- Строка - имена колонок таблицы равные именам измерений регистра,
//									по которым устанавливается фильтр, для колонок с именами отличающимися
//									от имен измерений, в значениях ключа "СоответствиеИзмеренийРегистраИзмерениямФильтра",
//									указывается соответствие имени измерения регистра имени колонки таблицы фильтра.
//									Допускается не указывать, когда ТаблицаФильтра имеет тип ТаблицаЗначений.
//		ЗначениеИзмерения 		- Произвольный - значение измерения.
//
// Возвращаемое значение:
//		Структура - см. НовыйОписаниеФильтраДляСоздатьВТИмяРегистра.
//
Функция ОписаниеФильтраДляСоздатьВТИмяРегистраПоЗначению(ОписаниеПериода, Измерение = "", ЗначениеИзмерения = Неопределено) Экспорт
	Если ЗначениеЗаполнено(Измерение) И ЗначениеИзмерения = Неопределено Тогда
		ВызватьИсключение НСТР("ru = 'Не передано значение измерения'");
	КонецЕсли;	
	
	СписокЗначенийИзмерения = Новый Массив;
	СписокЗначенийИзмерения.Добавить(ЗначениеИзмерения);
	
	ОписаниеФильтра = НовыйОписаниеФильтраДляСоздатьВТИмяРегистра(Измерение);
	ОписаниеФильтра.ЗначениеФильтра = ФильтрСписокЗначений(ОписаниеПериода, СписокЗначенийИзмерения);	
	
	Возврат ОписаниеФильтра;
КонецФункции

// Возвращает описание источника данных - временная таблица.
//
// Параметры:
//		ОписаниеПериода 		- Структура - см. ОписаниеПериодаДляСоздатьВТИмяРегистра
//		Измерение 				- Строка - имена колонок таблицы равные именам измерений регистра,
//									по которым устанавливается фильтр, для колонок с именами отличающимися
//									от имен измерений, в значениях ключа "СоответствиеИзмеренийРегистраИзмерениямФильтра",
//									указывается соответствие имени измерения регистра имени колонки таблицы фильтра.
//									Допускается не указывать, когда ТаблицаФильтра имеет тип ТаблицаЗначений.
//		СписокЗначенийИзмерения	- Массив - значения измерений.
//
// Возвращаемое значение:
//		Структура - см. НовыйОписаниеФильтраДляСоздатьВТИмяРегистра.
//
Функция ОписаниеФильтраДляСоздатьВТИмяРегистраПоСпискуЗначений(ОписаниеПериода, Измерение = "", СписокЗначенийИзмерения = Неопределено) Экспорт
	Если ЗначениеЗаполнено(Измерение) И СписокЗначенийИзмерения = Неопределено Тогда
		ВызватьИсключение НСТР("ru = 'Не передан список значений измерения'");
	КонецЕсли;	
		
	ОписаниеФильтра = НовыйОписаниеФильтраДляСоздатьВТИмяРегистра(Измерение);
	ОписаниеФильтра.ЗначениеФильтра = ФильтрСписокЗначений(ОписаниеПериода, СписокЗначенийИзмерения);	
	
	Возврат ОписаниеФильтра;
КонецФункции

// Возвращает параметры построения для СоздатьВТИмяРегистра.
//
// Возвращаемое значение:
//		Структура:
//			* ИндексироватьПо 							- Неопределено - см. функцию ПараметрыПостроенияВТИмяРегистра.
//			* Отборы 									- Массив - см. функцию ПараметрыПостроенияВТИмяРегистра.
//			* ВключатьЗаписиНаНачалоПериода 			- Булево - если истина в результирующую таблицу будут
//															включены значения по измерениям фильтра на начало периода
//															(применимо при построении запросов к регистрам с периодичностью.
//															Секунда, День, Месяц, Квартал или Год).
//			* ИмяВременнойТаблицыЗаписейНаНачалоПериода - Строка - задает имя временной таблицы записей на начало периода.
//			* ИспользуемоеИмяВременнойТаблицыЗаписейНаНачалоПериода - Строка - по окончании работы метода ЗапросВТИмяРегистра
//			                                                          будет сохранено имя временной таблицы, содержащей записи
//			                                                          на начало периода, если задано значение
//			                                                          ИмяВременнойТаблицыЗаписейНаНачалоПериода, то будет
//			                                                          содержать значение
//			                                                          ИмяВременнойТаблицыЗаписейНаНачалоПериода.
//			* ОтборыЗаписейНаНачалоПериода 				- Массив - описания отбора (см.ДобавитьВКоллекциюОтбор), применяется к таблице
//															формирующий записи на начало периода, при формировании результирующей таблицы.
//
Функция ПараметрыПостроенияДляСоздатьВТИмяРегистра() Экспорт
	
	ПараметрыПостроения = ПараметрыПостроенияВТИмяРегистра();
	ПараметрыПостроения.Вставить("ВключатьЗаписиНаНачалоПериода", Ложь);
	ПараметрыПостроения.Вставить("ИмяВременнойТаблицыЗаписейНаНачалоПериода", "");
	ПараметрыПостроения.Вставить("ИспользуемоеИмяВременнойТаблицыЗаписейНаНачалоПериода", "");
	ПараметрыПостроения.Вставить("ОтборыЗаписейНаНачалоПериода", Неопределено);
	
	Возврат ПараметрыПостроения;
	
КонецФункции

// Возвращает параметры построения для СоздатьВТИмяРегистраПериоды.
//
// Возвращаемое значение:
//		Структура - содержит структуры со свойствами:
//			* ИндексироватьПо 							- Неопределено - см. функцию ПараметрыПостроенияВТИмяРегистра.
//			* Отборы 									- Массив - см. функцию ПараметрыПостроенияВТИмяРегистра.
//			* ВключатьЗаписиНаНачалоПериода 			- Булево - если истина в результирующую таблицу будут
//															включены значения по измерениям фильтра на начало периода
//															(применимо при построении запросов к регистрам с периодичностью.
//															Секунда, День, Месяц, Квартал или Год).
//			* ИмяВременнойТаблицыЗаписейНаНачалоПериода - Строка - задает имя временной таблицы записей на начало периода.
//			* ИспользуемоеИмяВременнойТаблицыЗаписейНаНачалоПериода - Строка - по окончании работы метода ЗапросВТИмяРегистра
//			                                                          будет сохранено имя временной таблицы, содержащей записи
//			                                                          на начало периода, если задано значение
//			                                                          ИмяВременнойТаблицыЗаписейНаНачалоПериода, то будет
//			                                                          содержать значение
//			                                                          ИмяВременнойТаблицыЗаписейНаНачалоПериода.
//			* ОтборыЗаписейНаНачалоПериода 				- Массив - описания отбора (см.ДобавитьВКоллекциюОтбор), применяется к таблице
//															формирующий записи на начало периода, при формировании результирующей таблицы.
//
Функция ПараметрыПостроенияДляСоздатьВТИмяРегистраПериоды() Экспорт
	
	ПараметрыПостроения = ПараметрыПостроенияВТИмяРегистра();
	
	Возврат ПараметрыПостроения;
	
КонецФункции

// Возвращает параметры построения для СоздатьВТИмяРегистраСрез.
//
// Возвращаемое значение:
//		Структура:
//			* ИндексироватьПо 			- Неопределено 	- см. функцию ПараметрыПостроенияВТИмяРегистра.
//			* Отборы 					- Массив 		- см. функцию ПараметрыПостроенияВТИмяРегистра.
//			* ВсеЗаписи 				- Булево - если Истина в результирующую таблицу будут включены
//											записи таблицы фильтра, для которых нет записей в регистре.
//			* ВключаяГраницу 			- Булево - если Ложь из результирующей таблицы будут исключены
//											записи с периодами равными ограничивающим.
//			* ОтборыПрименяемыеКСрезу - Массив - коллекции, аналогичных коллекции ключа Отборы, но применяемый
//											к полученному срезу.
//
Функция ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез() Экспорт
	
	ПараметрыПостроения = ПараметрыПостроенияВТИмяРегистра();
	ПараметрыПостроения.Вставить("ВсеЗаписи", Ложь);
	ПараметрыПостроения.Вставить("ВключаяГраницу", Истина);
	ПараметрыПостроения.Вставить("ОтборыПрименяемыеКСрезу", Новый Массив);
	
	Возврат ПараметрыПостроения;
	
КонецФункции

#КонецОбласти

#Область ИнтервальныеРегистры

// Формирует движения интервального регистра сведений по изменениям первичного регистра
//
// Параметры:
//	ИмяРегистра - Строка - Имя регистра сведений, имеющего интервальную версию.
//	НаборЗаписей - НаборЗаписей - набор записей регистра сведений.
//
Процедура СформироватьДвиженияИнтервальногоРегистраПоИзменениям(ИмяРегистра, НаборЗаписей) Экспорт
	
	ПараметрыПостроения = ПараметрыПостроенияИнтервальногоРегистра();
	ЭтоНовыйНабор = ЭтоНовыйНабор(НаборЗаписей);
	ПараметрыПостроения.ПолноеПереформирование = Не ЭтоНовыйНабор;
	
	ТаблицаИзменившихсяДанныхНабора = НаборЗаписей.ТаблицаИзменившихсяДанныхНабора();
	
	Если ТаблицаИзменившихсяДанныхНабора.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеИнтервальногоРегистра = РегистрыСведений[ИмяРегистра].ОписаниеИнтервальногоРегистра();
	
	Если Не ЭтоНовыйНабор Тогда
		ТаблицаИзменившихсяДанныхНабора.Свернуть(ОписаниеИнтервальногоРегистра.ИзмеренияРасчета);
	КонецЕсли;
	
	ПараметрыПостроения.ТаблицаНабора = НаборЗаписей.Выгрузить();
	ПараметрыПостроения.ТаблицаНабора.Индексы.Добавить(ОписаниеИнтервальногоРегистра.ОсновноеИзмерение);
	ПараметрыПостроения.ТаблицаФильтра = ТаблицаИзменившихсяДанныхНабора;
	
	ИнтервальныеРегистрыБЗК.СформироватьДвиженияИнтервальногоРегистра(ИмяРегистра, ПараметрыПостроения);
	
КонецПроцедуры	

// Обновляет движения интервального регистра сведений
//		КадроваяИсторияСотрудников
//
// Параметры:
//	ИмяРегистра - Строка - Имя регистра сведений, имеющего интервальную версию.
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
Процедура ОбновитьДвиженияИнтервальногоРегистра(ИмяРегистра, МенеджерВременныхТаблиц) Экспорт
	
	ПараметрыПостроения = ПараметрыПостроенияИнтервальногоРегистра();
	ПараметрыПостроения.ПолноеПереформирование = Истина;
	ПараметрыПостроения.РежимЗагрузки = Истина;
	ПараметрыПостроения.ФильтрВВидеВТ = Истина;
	ПараметрыПостроения.ТаблицаФильтра = МенеджерВременныхТаблиц;
	ИнтервальныеРегистрыБЗК.СформироватьДвиженияИнтервальногоРегистра(ИмяРегистра, ПараметрыПостроения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает максимально возможную дату для интервального регистра.
//
// Возвращаемое значение:
//		Дата
Функция МаксимальнаяДата() Экспорт
	
	Возврат Дата(3999, 12, 31, 23, 59, 59);
	
КонецФункции

// Возвращает максимально возможную дату для интервального регистра строкой для запроса.
//
// Возвращаемое значение:
//		Строка
Функция МаксимальнаяДатаСтрокой() Экспорт
	
	Возврат "ДАТАВРЕМЯ(" + Формат(МаксимальнаяДата(), "ДФ='гггг, ММ, дд, ЧЧ, мм, сс'; ДП=") + ")";
	
КонецФункции

#Область ИнтервальныеРегистры

Функция ОписаниеИнтервальногоРегистра() Экспорт
	
	ПараметрыПостроения = Новый Структура;
	ПараметрыПостроения.Вставить("ОсновноеИзмерение", Неопределено);
	ПараметрыПостроения.Вставить("ИзмеренияРасчета", Неопределено);
	ПараметрыПостроения.Вставить("ПараметрыНаследованияРесурсов", Новый Структура);
	
	Возврат ПараметрыПостроения;
	
КонецФункции

Функция ПараметрыНаследованияРесурсов(ИмяРегистра) Экспорт
	
	ИмяИнтервальногоРегистра = ИнтервальныеРегистрыБЗК.ИмяИнтервальногоРегистра(ИмяРегистра);
	ОписаниеРегистра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеРегистра(ИмяИнтервальногоРегистра);
	ПараметрыРесурсов = Новый Структура;
	
	Для каждого Ресурс Из ОписаниеРегистра.Ресурсы Цикл
	
		Если Ресурс = "ПериодПредыдущейЗаписи" Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыРесурсов.Вставить(Ресурс);
		ПараметрыНаследования = Новый Структура("ПравилоНаследования, Значение", ПравилоНаследованияПустое());
		ПараметрыРесурсов[Ресурс] = ПараметрыНаследования;
		
	КонецЦикла;
	
	Возврат ПараметрыРесурсов;
	
КонецФункции

Функция ПравилоНаследованияПустое() Экспорт
	Возврат "Пустое";
КонецФункции

Функция ПравилоНаследованияФиксированное() Экспорт
	Возврат "Фиксированное";
КонецФункции

Функция ПравилоНаследованияНаследование() Экспорт
	Возврат "Наследование";
КонецФункции

#КонецОбласти

#Область КонтрольИзмененияДанныхРегистров

// Подготавливает данные для контроля изменений
//
// Параметры:
//	НаборЗаписейРегистра - Набор записей регистра
Процедура КонтрольИзмененийПередЗаписью(НаборЗаписейРегистра) Экспорт
		
	Если Не СинхронизацияДанныхЗарплатаКадры.НеобходимоОбновлениеВторичныхДанных(НаборЗаписейРегистра) Тогда
		Возврат;
	КонецЕсли;
	
	Если НаборЗаписейРегистра.ДополнительныеСвойства.Свойство("КэшТаблицаИзменившихсяДанных") Тогда
		НаборЗаписейРегистра.ДополнительныеСвойства.Удалить("КэшТаблицаИзменившихсяДанных");
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	НаборЗаписейРегистра.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицИзмененияРегистра", МенеджерВременныхТаблиц);
	
	СоздатьВТСтарыйНаборЗаписей(НаборЗаписейРегистра, МенеджерВременныхТаблиц, ИмяВТСтарыйНаборРегистра(НаборЗаписейРегистра.Метаданные().Имя));
	
КонецПроцедуры

// Формирует таблицу изменений регистра
//
// Параметры:
//	НаборЗаписейРегистра - Набор записей регистра
Процедура КонтрольИзмененийПриЗаписи(НаборЗаписейРегистра) Экспорт
		
	Запрос = ЗапросВТИзмененияВНаборе(НаборЗаписейРегистра.Метаданные().Имя, НаборЗаписейРегистра.Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = НаборЗаписейРегистра.ДополнительныеСвойства.МенеджерВременныхТаблицИзмененияРегистра;
	КэшТаблицаИзменившихсяДанных = Запрос.Выполнить().Выгрузить();
	НаборЗаписейРегистра.ДополнительныеСвойства.Вставить("КэшТаблицаИзменившихсяДанных", КэшТаблицаИзменившихсяДанных);
	
КонецПроцедуры

// Возвращает признак сформированности таблицы изменений
//
// Параметры:
//	НаборЗаписейРегистра - Набор записей регистра
Функция ТаблицаИзменившихсяДанныхНабораСформирована(НаборЗаписейРегистра) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(НаборЗаписейРегистра) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат НаборЗаписейРегистра.ДополнительныеСвойства.Свойство("КэшТаблицаИзменившихсяДанных");
	
КонецФункции

// Возвращает таблицу изменений регистра
//
// Параметры:
//	НаборЗаписейРегистра - Набор записей регистра
Функция ТаблицаИзменившихсяДанныхНабора(НаборЗаписейРегистра) Экспорт
	КэшТаблицаИзменившихсяДанных = Неопределено;	
	
	Если НаборЗаписейРегистра.ДополнительныеСвойства.Свойство("КэшТаблицаИзменившихсяДанных", КэшТаблицаИзменившихсяДанных) Тогда
		Возврат КэшТаблицаИзменившихсяДанных.Скопировать();
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает соответствие изменений массива наборов регистров
//
// Параметры:
//	НаборыИсточникДанных - Массив наборов записей регистров
Функция ИзмененияВНаборахИсточниковВторичныхДанных(НаборыИсточникДанных) Экспорт
	ИзмененияВНаборах = Новый Соответствие;
	
	Для Каждого Набор Из НаборыИсточникДанных Цикл
		ИмяРегистра = Набор.Метаданные().Имя; 
		
		ТаблицаИзмененийВНаборе = Набор.ТаблицаИзменившихсяДанныхНабора();
		
		Если ТаблицаИзмененийВНаборе <> Неопределено
			И ТаблицаИзмененийВНаборе.Количество() > 0 Тогда
						
			ИзмененияВНаборах.Вставить(ИмяРегистра, ТаблицаИзмененийВНаборе);
			
		КонецЕсли;	
	КонецЦикла;		

	Возврат ИзмененияВНаборах;	
КонецФункции

#КонецОбласти

#Область РаботаСФильтромЗапроса 

Функция ОписаниеИспользованиеФильтра(ПсевдонимТаблицыФильтра = "ИзмеренияДаты", ПсевдонимТаблицыРегистра = "РегистрСведений", ФильтрВВидеВТПринудительно = Ложь) Экспорт
	ОписаниеИспользованияФильтра = НовыйОписаниеИспользованияФильтра();
	ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра = ПсевдонимТаблицыФильтра;
	ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра = ПсевдонимТаблицыРегистра;
	Если ФильтрВВидеВТПринудительно Тогда
		ОписаниеИспользованияФильтра.ФильтрВВидеВТ = Истина;
	КонецЕсли;	
	
	Возврат ОписаниеИспользованияФильтра;
КонецФункции	

Функция ИнициализироватьИспользованиеФильтра(ОписаниеИспользованияФильтра, ОписаниеФильтра, ОписаниеРегистра, ПоляПериода, ОператорЗапроса, ПостфиксИменПараметров = "", ПолучатьВсеЗаписи = Ложь) Экспорт
	ОписаниеИспользованияФильтра.ПостфиксИменПараметров = ПостфиксИменПараметров;
	ОписаниеИспользованияФильтра.ОператорЗапроса = ОператорЗапроса;
	ОписаниеИспользованияФильтра.ВсеЗаписи = ПолучатьВсеЗаписи;
	
	Если Не ОписаниеИспользованияФильтра.ФильтрВВидеВТ Тогда	
		ОписаниеИспользованияФильтра.ФильтрВВидеВТ = ИспользоватьВТФильтр(ОписаниеФильтра, ПолучатьВсеЗаписи);
	КонецЕсли;	
	//
	Если ЗначениеЗаполнено(ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра) Тогда
		ОписаниеСоединенияСТаблицейФильтра = ОписаниеСоединенияСТаблицейФильтра(ОператорЗапроса, ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра, ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра);
	                                                                                                                      
		Если ОписаниеСоединенияСТаблицейФильтра = Неопределено Тогда
			ВызватьИсключение НСтр("ru = 'Не возможно установить фильтр в запрос получения данных регистра'");	
		КонецЕсли;	
		ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи = ТекстУсловия(ОписаниеСоединенияСТаблицейФильтра.Условия);
		ОписаниеСоединенияСТаблицейФильтра.Условия.Очистить();
	КонецЕсли;	                                                   
	
	Для Каждого Измерение Из ОписаниеФильтра.ИзмеренияФильтра Цикл
		ПолеФильтра = ПолеТаблицыФильтра(ОписаниеФильтра, Измерение);
		ОписаниеИспользованияФильтра.ИзмеренияФильтра.Вставить(Измерение, ПолеФильтра);

		Если ОписаниеРегистра.ИзмеренияДляПоиска[ВРег(Измерение)] <> Неопределено Тогда 
			ОписаниеИспользованияФильтра.ИзмеренияСвязи.Вставить(Измерение, ПолеФильтра);		
		КонецЕсли;	
	КонецЦикла;		
	
	ПоляПериодовМассив = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПоляПериода, ",", Истина, Истина);
	
	Для Каждого ПолеПериода Из ПоляПериодовМассив Цикл
		ПолеФильтра = ПолеТаблицыФильтра(ОписаниеФильтра, ПолеПериода);
		ОписаниеИспользованияФильтра.ПоляПериодаФильтра.Вставить(ПолеПериода, ПолеФильтра);
	КонецЦикла;	
	
	Для Каждого ДополнительноеПоле Из ОписаниеФильтра.ДополнительныеПоляФильтра Цикл
		Если ОписаниеИспользованияФильтра.ФильтрВВидеВТ Тогда
			ОписаниеИспользованияФильтра.ДополнительныеПоля.Вставить(ДополнительноеПоле, ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра + "." + ДополнительноеПоле);
		Иначе
			ОписаниеИспользованияФильтра.ДополнительныеПоля.Вставить(ДополнительноеПоле, ДополнительноеПоле + ПостфиксИменПараметров);
		КонецЕсли;	
	КонецЦикла;	
КонецФункции	

Процедура УстановитьВыражениеПериодаВТекстШаблонаУсловияСвязи(ОписаниеИспользованияФильтра, ЗаменяемыйТекст, ОписаниеВыраженияПериода) Экспорт
	
	ТекстВыраженияПоляПериод = ВыражениеПоляПериод(ОписаниеВыраженияПериода);
	ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи = СтрЗаменить(
																ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи,
																ЗаменяемыйТекст,
																ТекстВыраженияПоляПериод);
КонецПроцедуры	

Процедура УстановитьВыражениеПолейФильтраВУсловияхСвязиСФильтром(ОписаниеИспользованияФильтра, ЗаменяемыеПоля)  Экспорт
	МассивПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЗаменяемыеПоля, ",", , Истина);
	Для Каждого Поле Из МассивПолей Цикл
		ВыражениеПоля = ВыраженияПоляФильтра(ОписаниеИспользованияФильтра, Поле);
		ЗаменяемыйТекст = ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра + "." + Поле;
		ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи = СтрЗаменить(ОписаниеИспользованияФильтра, ЗаменяемыйТекст, ВыражениеПоля);
	КонецЦикла;	
КонецПроцедуры	

Процедура УстановитьВыражениеПолейФильтраВУсловияхСоединения(ОператорЗапроса, ОписаниеИспользованияФильтра, ПсевдонимПрисоединяемойТаблицы, ЗаменяемыеПоля)  Экспорт
	МассивПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЗаменяемыеПоля, ",", , Истина);
	Для Каждого Поле Из МассивПолей Цикл
		ВыражениеПоля = ВыраженияПоляФильтра(ОписаниеИспользованияФильтра, Поле);
		ЗаменяемыйТекст = ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра + "." + Поле;
		ЗаменитьТекстВУсловииСоединения(ОператорЗапроса, ПсевдонимПрисоединяемойТаблицы, ЗаменяемыйТекст, ВыражениеПоля);
	КонецЦикла;	
КонецПроцедуры	
	
Процедура ЗаменитьТекстВУсловииСвязиСФильтром(ОписаниеИспользованияФильтра, ЗаменяемыйТекст, НовыйТекст) Экспорт
	ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи 
		= СтрЗаменить(ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи, ЗаменяемыйТекст, НовыйТекст);	
КонецПроцедуры

Процедура УстановитьФильтрВОписаниеПакетаЗапросовКРегистру(ОписаниеПакета, ОписаниеФильтра, ОписаниеИспользованияФильтра, ПараметрыПостроения = Неопределено) Экспорт
	ПриемникУсловийСвязи  = Неопределено;
	
	Если ОписаниеИспользованияФильтра.ФильтрВВидеВТ Тогда
		ОписаниеСоединения = ОписаниеСоединенияСТаблицейФильтра(
								ОписаниеИспользованияФильтра.ОператорЗапроса, 
								ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра, 
								ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра);
								
		Если ОписаниеСоединения <> Неопределено Тогда
									
			ПриемникУсловийСвязи = ОписаниеСоединения.Условия;	
										
			Если  ОписаниеИспользованияФильтра.ВсеЗаписи 
				И ОписаниеСоединения.ВедущаяТаблица = ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра Тогда
			
				ОписаниеСоединения.ТипСоединения = "ЛЕВОЕ";
			КонецЕсли;	
		КонецЕсли;	
							
		ИмяВТФильтр = ИнициализироватьВТФильтр(ОписаниеПакета, ОписаниеФильтра, ОписаниеИспользованияФильтра);
		
		ЗаменитьТаблицуВОператореЗапроса(ОписаниеИспользованияФильтра.ОператорЗапроса, ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра, ИмяВТФильтр);
	Иначе	
		УдалитьТаблицуИзОператораЗапроса(ОписаниеИспользованияФильтра.ОператорЗапроса, ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра);
			
		Для Каждого ОписаниеЗначенияПериода Из ОписаниеИспользованияФильтра.ОписаниеВычисляемыхПараметровПериода Цикл
			ПолеПериод = ПолеТаблицыФильтра(ОписаниеФильтра, ОписаниеЗначенияПериода.ИмяИсходногоПоля);
			Значение = ЗначениеПоляПериод(ОписаниеЗначенияПериода, ОписаниеФильтра.ЗначениеФильтра.ОписаниеПериода[ПолеПериод]);
			ОписаниеПакета.Параметры.Вставить(ОписаниеЗначенияПериода.ИмяПоля, Значение);
		КонецЦикла;	
		
		Для Каждого ДополнительноеПоле Из ОписаниеИспользованияФильтра.ДополнительныеПоля Цикл
			ОписаниеПакета.Параметры.Вставить(ДополнительноеПоле.Значение, ОписаниеФильтра.ЗначениеФильтра.ДополнительныеПоля[ДополнительноеПоле.Ключ]);
		КонецЦикла;	
		
		Если ЗначениеЗаполнено(ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра) Тогда 
			ОписаниеСоединения = ОписаниеСоединенияПоПрисоединяемойТаблице(ОписаниеИспользованияФильтра.ОператорЗапроса, ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра);
			Если ОписаниеСоединения <> Неопределено Тогда
				ПриемникУсловийСвязи = ОписаниеСоединения.Условия;
			Иначе	
				ПриемникУсловийСвязи = ОписаниеИспользованияФильтра.ОператорЗапроса.Отбор;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
	УстановитьПараметрыИзмеренияФильтра(ОписаниеПакета, ОписаниеФильтра, ОписаниеИспользованияФильтра);

	Если ПриемникУсловийСвязи <> Неопределено Тогда
		УстановитьУсловияСвязиСФильтром(ОписаниеПакета, ОписаниеИспользованияФильтра, ПриемникУсловийСвязи, ОписаниеФильтра);
	КонецЕсли;	
	
	УстановитьЭлементыОтбораСКД(ОписаниеИспользованияФильтра, ПараметрыПостроения);	
КонецПроцедуры	

Функция ВыражениеИзмерениеФильтра(ОписаниеИспользованияФильтра, Измерение) Экспорт
	Если ОписаниеИспользованияФильтра.ФильтрВВидеВТ Тогда
		ИмяПоляФильтра =  ОписаниеИспользованияФильтра.ИзмеренияФильтра[Измерение];
		Возврат ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра + "." + ИмяПоляФильтра;
	Иначе
		Возврат "&" + ИмяПараметраЭлементаФильтра(Измерение, ОписаниеИспользованияФильтра.ПостфиксИменПараметров);
	КонецЕсли;	
КонецФункции	

#КонецОбласти

#Область ОписаниеПолейПериода

Функция ОписаниеПоляПериода(ИмяПоля, ПсевдонимТаблицы = Неопределено) Экспорт
	ОписаниеФильтраПоПериоду = Новый Структура;
	ОписаниеФильтраПоПериоду.Вставить("ПсевдонимТаблицы", ПсевдонимТаблицы);
	ОписаниеФильтраПоПериоду.Вставить("ИмяПоля", ИмяПоля);
	ОписаниеФильтраПоПериоду.Вставить("Кратность", "СЕКУНДА");
	ОписаниеФильтраПоПериоду.Вставить("Сдвиг", 0);
	ОписаниеФильтраПоПериоду.Вставить("ВариантПриведенияПериода", "НАЧАЛОПЕРИОДА");
	ОписаниеФильтраПоПериоду.Вставить("КратностьСдвига");
	ОписаниеФильтраПоПериоду.Вставить("ПустоеЗначениеКакМаксимальное", Ложь);	
	ОписаниеФильтраПоПериоду.Вставить("ИмяИсходногоПоля");

	Возврат ОписаниеФильтраПоПериоду;
КонецФункции	

Функция ДобавитьОписаниеПоляПериодФильтра(ОписаниеИспользованияФильтра, ИмяИсходногоПоля, ПсевдонимВычисляемогоПоля) Экспорт
	ИмяПоляТаблицыФильтра = ОписаниеИспользованияФильтра.ПоляПериодаФильтра[ИмяИсходногоПоля];	
	
	Если ОписаниеИспользованияФильтра.ФильтрВВидеВТ Тогда
		ОписаниеПоля = ОписаниеПоляПериода(ИмяПоляТаблицыФильтра, ОписаниеИспользованияФильтра.ПсевдонимТаблицыФильтра);	
	Иначе	
		ИмяПараметра = ИмяПараметраЭлементаФильтра(ПсевдонимВычисляемогоПоля, ОписаниеИспользованияФильтра.ПостфиксИменПараметров);
		ОписаниеПоля = ОписаниеПоляПериода(ИмяПараметра);
		ОписаниеПоля.ИмяИсходногоПоля = ИмяПоляТаблицыФильтра;
	КонецЕсли;	
	
	ОписаниеИспользованияФильтра.ОписаниеВычисляемыхПараметровПериода.Добавить(ОписаниеПоля);
	                                                           
	Возврат ОписаниеПоля;
КонецФункции	

Функция ВыражениеПоляПериод(ОписаниеПоляПериода) Экспорт		
	Если ОписаниеПоляПериода.ПсевдонимТаблицы = Неопределено Тогда
		Возврат "&" + ОписаниеПоляПериода.ИмяПоля;
	КонецЕсли;	
	
	Если ОписаниеПоляПериода.ПустоеЗначениеКакМаксимальное Тогда
		ВыражениеПериодШаблон = "
						        |ВЫБОР
						        |	КОГДА &Дата_ = ДАТАВРЕМЯ(1, 1, 1)
						        |		ТОГДА ДАТАВРЕМЯ(3999, 12, 31, 23, 59, 59)
						        |	ИНАЧЕ &ДатаПриведенная_
						        |КОНЕЦ";
						  
		ВыражениеДата = ОписаниеПоляПериода.ПсевдонимТаблицы + "." + ОписаниеПоляПериода.ИмяПоля;
		ВыражениеПериодШаблон = СтрЗаменить(ВыражениеПериодШаблон, "&Дата_", ВыражениеДата);  
		
		ВыражениеПериод = ВыражениеДата;
	Иначе
		ВыражениеПериод = ОписаниеПоляПериода.ПсевдонимТаблицы + "." + ОписаниеПоляПериода.ИмяПоля;
	КонецЕсли;	
		
	Если ОписаниеПоляПериода.Кратность <> "СЕКУНДА" Тогда
		ВыражениеПериод = ОписаниеПоляПериода.ВариантПриведенияПериода + "(" + ВыражениеПериод + "," + ОписаниеПоляПериода.Кратность + ")";
	КонецЕсли;	
	
	КратностьСдвига = ?(ОписаниеПоляПериода.КратностьСдвига = Неопределено, ОписаниеПоляПериода.Кратность, ОписаниеПоляПериода.КратностьСдвига); 
	
	Если ОписаниеПоляПериода.Сдвиг <> 0 Тогда
		ВыражениеПериод = "ДОБАВИТЬКДАТЕ(" + ВыражениеПериод + "," + КратностьСдвига + "," + Формат(ОписаниеПоляПериода.Сдвиг, "ЧГ=") + ")";
	КонецЕсли;	
	
	Если ОписаниеПоляПериода.ПустоеЗначениеКакМаксимальное Тогда
		Возврат СтрЗаменить(ВыражениеПериодШаблон, "&ДатаПриведенная_", ВыражениеПериод);
	Иначе	
		Возврат ВыражениеПериод;
	КонецЕсли;	
КонецФункции

Функция ОписаниеПериодаДляСоздатьВТИмяРегистра() Экспорт
	ОписаниеПериода = Новый Структура;
	ОписаниеПериода.Вставить("Период");
	ОписаниеПериода.Вставить("ДатаНачала");
	ОписаниеПериода.Вставить("ДатаОкончания");
	
	Возврат ОписаниеПериода;
КонецФункции

#КонецОбласти 

#Область ПостроениеМоделиЗапроса

Функция НовыйОписаниеПакетаЗапросовКРегистру() Экспорт
	
	ОписаниеПакетаЗапросовКРегистру = Новый Структура;
	ОписаниеПакетаЗапросовКРегистру.Вставить("ОписаниеЗапросаИнициализацииФильтра");
	ОписаниеПакетаЗапросовКРегистру.Вставить("ЗапросУничтоженияФильтра");
	ОписаниеПакетаЗапросовКРегистру.Вставить("ЗапросыПолученияДанных", Новый Массив);
	ОписаниеПакетаЗапросовКРегистру.Вставить("Параметры", Новый Структура);
	
	Возврат ОписаниеПакетаЗапросовКРегистру;
	
КонецФункции

Функция ОписаниеЗапросаПоТексту(ТекстЗапроса) Экспорт
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	Возврат ОписаниеЗапросаПоЗапросуПакета(СхемаЗапроса.ПакетЗапросов[0]);
КонецФункции

#КонецОбласти

#Область РаботаСМодельюЗапроса

// Описание
// 
// Параметры:
// 	ОписаниеПакетаЗапросовКРегистру - Структура - Описание:
//   * ОписаниеЗапросаИнициализацииФильтра 
//   * ЗапросУничтоженияФильтра 
//   * ЗапросыПолученияДанных - Массив -
//   * Параметры - Структура -
// 	ВыводитьЭлементыСКД - Булево - Описание
// Возвращаемое значение:
// 	Запрос - Запрос -
Функция ЗапросПоОписаниюПакета(ОписаниеПакетаЗапросовКРегистру, ВыводитьЭлементыСКД = Истина) Экспорт
	Запрос = Новый Запрос;
	
	Для Каждого Параметр Из ОписаниеПакетаЗапросовКРегистру.Параметры Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	Разделитель = Символы.ПС + ";" + Символы.ПС;
	
	СтрокиПакетаЗапросов = Новый Массив;
	
	Если ОписаниеПакетаЗапросовКРегистру.ОписаниеЗапросаИнициализацииФильтра <> Неопределено Тогда
		ОписаниеЗапросаВМассивСтрок(СтрокиПакетаЗапросов, ОписаниеПакетаЗапросовКРегистру.ОписаниеЗапросаИнициализацииФильтра, ВыводитьЭлементыСКД);
		СтрокиПакетаЗапросов.Добавить(Разделитель);
	КонецЕсли;	
	
	НомерЗапроса = 0;
	Для Каждого ОписаниеЗапроса Из ОписаниеПакетаЗапросовКРегистру.ЗапросыПолученияДанных Цикл
		НомерЗапроса = НомерЗапроса + 1;
		Если ТипЗнч(ОписаниеЗапроса) = Тип("Запрос") Тогда
			СтрокиПакетаЗапросов.Добавить(ОписаниеЗапроса.Текст);
		Иначе	
			ОписаниеЗапросаВМассивСтрок(СтрокиПакетаЗапросов, ОписаниеЗапроса, ВыводитьЭлементыСКД);
		КонецЕсли;	
		Если НомерЗапроса <> ОписаниеПакетаЗапросовКРегистру.ЗапросыПолученияДанных.Количество() Тогда
			СтрокиПакетаЗапросов.Добавить(Разделитель);
		КонецЕсли;
	КонецЦикла;	
	
	Если ОписаниеПакетаЗапросовКРегистру.ЗапросУничтоженияФильтра <> Неопределено Тогда
		СтрокиПакетаЗапросов.Добавить(Разделитель);
		СтрокиПакетаЗапросов.Добавить(ОписаниеПакетаЗапросовКРегистру.ЗапросУничтоженияФильтра.Текст);
	КонецЕсли;	
		
	Запрос.Текст = СтрСоединить(СтрокиПакетаЗапросов);
	
	Возврат Запрос;
	
КонецФункции	

Функция ТекстЗапросаПоОписанию(ОписаниеЗапроса) Экспорт
	СтрокиЗапроса = Новый Массив;
	
	ОписаниеЗапросаВМассивСтрок(СтрокиЗапроса, ОписаниеЗапроса, Ложь);
	
	Возврат СтрСоединить(СтрокиЗапроса);
КонецФункции	

Процедура УстановитьОтборВОператорЗапросаДанныхРегистра(ОператорВыбратьЗапроса, Отборы, ПараметрыЗапроса, ПостфиксПараметров, СчПараметров = 1, Предикат = "", ПсевдонимРегистра = "РегистрСведений", УстанавливатьОтборПоИсключаемымРегистраторам = Истина, УстанавливатьОтборВУсловияСоединения = Ложь) Экспорт
	Если Отборы = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ОписаниеСоединения = ОписаниеСоединенияСТаблицейФильтра(ОператорВыбратьЗапроса, "ИзмеренияДаты", ПсевдонимРегистра);
	
	ПриемникУсловий = Неопределено;
	Если ОписаниеСоединения <> Неопределено И УстанавливатьОтборВУсловияСоединения Тогда
		ПриемникУсловий = ОписаниеСоединения.Условия;
	КонецЕсли;	
	
	ВыраженияПолей = ВыраженияПолейОператораЗапроса(ОператорВыбратьЗапроса);
		
	Для Каждого СтруктураОтбора Из Отборы Цикл	
		Если ЭтоЭлементОтбораПоИсключаемомуРегистратору(СтруктураОтбора)
			И Не УстанавливатьОтборПоИсключаемымРегистраторам Тогда
			
			Продолжить;
		КонецЕсли;	
		
		Если ТипЗнч(СтруктураОтбора.ПравоеЗначение) = Тип("Строка") Тогда
			ОписаниеПараметра = СтруктураОтбора.ПравоеЗначение;
		Иначе
			ИмяПараметра = УникальноеИмяПараметраЗапроса(ПостфиксПараметров, СчПараметров);
			ОписаниеПараметра = "&" + ИмяПараметра;
			ПараметрыЗапроса.Вставить(ИмяПараметра, СтруктураОтбора.ПравоеЗначение);
		КонецЕсли;
		
		Если Не СтруктураОтбора.Свойство("ОтносительныйПуть") Или СтруктураОтбора.ОтносительныйПуть Тогда
			ВыражениеЛевогоЗначения = ВыраженияПолей[НРег(СтруктураОтбора.ЛевоеЗначение)];
			Если ВыражениеЛевогоЗначения = Неопределено И ПсевдонимРегистра <> Неопределено Тогда
				ВыражениеЛевогоЗначения = ПсевдонимРегистра + "." + СтруктураОтбора.ЛевоеЗначение;	
			КонецЕсли;	
			
			ТекстУсловия = Предикат + ВыражениеЛевогоЗначения + " " + СтруктураОтбора.ВидСравнения + "(" + ОписаниеПараметра + ")";				
		Иначе
			ТекстУсловия = Предикат + СтруктураОтбора.ЛевоеЗначение + " " + СтруктураОтбора.ВидСравнения + " " + ОписаниеПараметра + " ";					
		КонецЕсли;
		Если ПриемникУсловий = Неопределено Тогда
			ДобавитьУсловие(ОператорВыбратьЗапроса, ТекстУсловия);
		Иначе
			ПриемникУсловий.Добавить(ТекстУсловия);
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры		

Функция УстановитьПараметрОтбораПоИсключаемомуРегистратору(ОписаниеПакета, ИсключаемыйРегистратор, Постфикс) Экспорт
	Если ТипЗнч(ИсключаемыйРегистратор) = Тип("Строка") Тогда
		ИмяПараметраИсключаемыеРегистраторы = ИсключаемыйРегистратор;
	Иначе
		ИмяПараметраИсключаемыеРегистраторы = ИмяПараметраИсключаемыйРегистратор(Постфикс);
		Если ТипЗнч(ИсключаемыйРегистратор) = Тип("Массив") Или ТипЗнч(ИсключаемыйРегистратор) = Тип("СписокЗначений") Тогда
			ЗначениеПараметра = ИсключаемыйРегистратор;
		Иначе
			ЗначениеПараметра = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИсключаемыйРегистратор);
		КонецЕсли;
		ОписаниеПакета.Параметры.Вставить(ИмяПараметраИсключаемыеРегистраторы, ЗначениеПараметра);
	КонецЕсли;	

	Возврат ИмяПараметраИсключаемыеРегистраторы;
КонецФункции

Процедура ДобавитьДополнительныеПоляПоОписаниюИспользованияФильтра(ОписаниеЗапроса, ИндексОператора, ОписаниеИспользованияФильтра, ВключатьВГруппировку = Ложь) Экспорт
	Для Каждого ДополнительноеПоле Из ОписаниеИспользованияФильтра.ДополнительныеПоля Цикл
		ВыражениеПоля = ВыражениеДопПоляФильтраПоОписаниюИспользованияФильтра(ДополнительноеПоле.Ключ, ОписаниеИспользованияФильтра);
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, ИндексОператора, ВыражениеПоля, ДополнительноеПоле.Ключ);
		Если ВключатьВГруппировку Тогда
			ДобавитьГруппировку(ОписаниеЗапроса.Операторы[ИндексОператора], ВыражениеПоля);
		КонецЕсли;		
	КонецЦикла;	
КонецПроцедуры	

Процедура ДобавитьПостоянныеПоляВОписаниеЗапроса(ОписаниеПакетаЗапросов, ОписаниеЗапроса, ПостоянныеПоля, ПостфиксИменаПараметров) Экспорт
	Если ПостоянныеПоля = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Для Каждого Поле Из ПостоянныеПоля Цикл
		ИмяПараметра = ИмяПараметраЭлементаФильтра(Поле.Ключ, ПостфиксИменаПараметров);
		ОписаниеПакетаЗапросов.Параметры.Вставить(ИмяПараметра, Поле.Значение);
		
		ВыражениеПоля = "&" + ИмяПараметра; 
		ИндексОператора = 0;
		Для Каждого Оператор Из ОписаниеЗапроса.Операторы Цикл
			ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, ИндексОператора, ВыражениеПоля, Поле.Ключ);
		КонецЦикла;
	КонецЦикла;	
КонецПроцедуры	

Процедура УстановитьПсевдонимыПолей(ОписаниеЗапроса, ПараметрыПостроения) Экспорт
	Если ПараметрыПостроения.СоответствиеПсевдонимовПолей = Неопределено 
		И ПараметрыПостроения.СоответствиеПсевдонимовПолейСКД = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если ПараметрыПостроения.СоответствиеПсевдонимовПолей <> Неопределено Тогда
		Для Каждого Псевдоним Из ПараметрыПостроения.СоответствиеПсевдонимовПолей Цикл
			ИндексКолонки = ОписаниеЗапроса.Колонки.Найти(Псевдоним.Ключ);
			Если ИндексКолонки <> Неопределено Тогда
				ОписаниеЗапроса.Колонки[ИндексКолонки] = Псевдоним.Значение;	
				
				Для Каждого ОператорЗапроса Из ОписаниеЗапроса.Операторы Цикл
					ОператорЗапроса.ПсевдонимыПолей[ИндексКолонки] = Псевдоним.Значение;
				КонецЦикла;				
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;	
	
	Для Каждого ОператорЗапрос Из ОписаниеЗапроса.Операторы Цикл
		Для Каждого ОписаниеПоляСКД Из ОператорЗапрос.ВыбираемыеПоляСКД Цикл
			Если ПараметрыПостроения.СоответствиеПсевдонимовПолей <> Неопределено 
				И ПараметрыПостроения.СоответствиеПсевдонимовПолей[ОписаниеПоляСКД.Поле] <> Неопределено Тогда
				
				ОписаниеПоляСКД.Поле = ПараметрыПостроения.СоответствиеПсевдонимовПолей[ОписаниеПоляСКД.Поле];
			КонецЕсли;	
						
			Если ПараметрыПостроения.СоответствиеПсевдонимовПолейСКД <> Неопределено 
				И ПараметрыПостроения.СоответствиеПсевдонимовПолейСКД[ОписаниеПоляСКД.Поле] <> Неопределено Тогда
					
				ОписаниеПоляСКД.ПсевдонимСКД = ПараметрыПостроения.СоответствиеПсевдонимовПолейСКД[ОписаниеПоляСКД.Поле];
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;	
КонецПроцедуры	

Процедура ДобавитьПоляИндексированияВОписаниеЗапроса(ОписаниеЗапроса, ПоляИндексирования) Экспорт
	Если ПоляИндексирования = Неопределено Тогда
		
		Возврат;
	КонецЕсли;
	
	Для Каждого Поле Из ПоляИндексирования Цикл
		ОписаниеЗапроса.ПоляИндексирования.Добавить(Поле);
	КонецЦикла;	
КонецПроцедуры	

Процедура ДобавитьПоляУпорядочиванияВОписаниеЗапроса(ОписаниеЗапроса, ПоляУпорядочивания) Экспорт
	Если ТипЗнч(ПоляУпорядочивания) = Тип("Массив") Тогда
		Для Каждого Поле Из ПоляУпорядочивания Цикл
			ОписаниеЗапроса.Порядок.Добавить(Поле);
		КонецЦикла;	
	Иначе
		ОписаниеЗапроса.Порядок.Добавить(ПоляУпорядочивания);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаменитьТаблицуВОператореЗапроса(ОператорЗапроса, ПсевдонимЗаменяемойТаблицы, ИмяНовойТаблицы) Экспорт
	ОписаниеТаблицы = ОператорЗапроса.ИсточникСоответствие[ПсевдонимЗаменяемойТаблицы];
	ОписаниеТаблицы.Имя = ИмяНовойТаблицы;
КонецПроцедуры	

Процедура УдалитьТаблицуИзОператораЗапроса(ОператорЗапроса, ПсевдонимУдаляемойТаблицы) Экспорт
	ОписаниеТаблицы = ОператорЗапроса.ИсточникСоответствие[ПсевдонимУдаляемойТаблицы];
	ОператорЗапроса.ИсточникСоответствие.Удалить(ПсевдонимУдаляемойТаблицы);
	ИндексУдаляемогоЭлемента = ОператорЗапроса.Таблицы.Найти(ОписаниеТаблицы);
	ОператорЗапроса.Таблицы.Удалить(ИндексУдаляемогоЭлемента);
	                     
	УдаляемыеСоединения = ОператорЗапроса.Соединения.НайтиСтроки(Новый Структура("ПрисоединяемаяТаблица", ПсевдонимУдаляемойТаблицы));
	Для Каждого УдаляемаяСтрока Из УдаляемыеСоединения Цикл
		ОператорЗапроса.Соединения.Удалить(УдаляемаяСтрока);
	КонецЦикла;	
	
	УдаляемыеСоединения = ОператорЗапроса.Соединения.НайтиСтроки(Новый Структура("ВедущаяТаблица", ПсевдонимУдаляемойТаблицы));
	Для Каждого УдаляемаяСтрока Из УдаляемыеСоединения Цикл
		ОператорЗапроса.Соединения.Удалить(УдаляемаяСтрока);
	КонецЦикла;	
КонецПроцедуры	

Процедура ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, ИндексОператораЗапроса, Выражение, Псевдоним, ДобавлятьПолеВРасширениеСКД = Истина) Экспорт
	ИндексПоля = ОписаниеЗапроса.Колонки.Найти(Псевдоним);
	
	Если ИндексПоля = Неопределено Тогда
		ОписаниеЗапроса.Колонки.Добавить(Псевдоним);
		ИндексЗапроса = 0;
		Для Каждого Запрос Из ОписаниеЗапроса.Операторы Цикл
			Если ИндексЗапроса = ИндексОператораЗапроса Тогда
				ОписаниеЗапроса.Операторы[ИндексЗапроса].ВыбираемыеПоля.Добавить(Выражение);
			Иначе
				ОписаниеЗапроса.Операторы[ИндексЗапроса].ВыбираемыеПоля.Добавить("NULL");
			КонецЕсли;	
			Запрос.ПсевдонимыПолей.Добавить(Псевдоним);
			ИндексЗапроса = ИндексЗапроса + 1;
		КонецЦикла;		
	Иначе
		ОписаниеЗапроса.Операторы[ИндексОператораЗапроса].ВыбираемыеПоля[ИндексПоля] = Выражение;
	КонецЕсли;	
	
	Если ДобавлятьПолеВРасширениеСКД И ИндексОператораЗапроса = 0 Тогда
		ОписаниеЗапроса.Операторы[ИндексОператораЗапроса].ВыбираемыеПоляСКД.Добавить(Новый Структура("Поле, ПсевдонимСКД", Псевдоним));					
	КонецЕсли;	
КонецПроцедуры	

Процедура ДобавитьУсловие(ОператорЗапроса, Условия) Экспорт
	Если ТипЗнч(Условия) = Тип("Массив") Тогда
		Для Каждого ТекстУсловия Из Условия Цикл
			ОператорЗапроса.Отбор.Добавить(ТекстУсловия);	
		КонецЦикла;
	Иначе	
		ОператорЗапроса.Отбор.Добавить(Условия);
	КонецЕсли;	
КонецПроцедуры	

Процедура ДобавитьУсловиеСоединения(ОператорЗапроса, ПсевдонимСоединяемойТаблицы, Условия) Экспорт
	ОписаниеСоединения = ОператорЗапроса.Соединения.НайтиСтроки(Новый Структура("ПрисоединяемаяТаблица", ПсевдонимСоединяемойТаблицы))[0];
	
	Если ТипЗнч(Условия) = Тип("Строка") Тогда
		ОписаниеСоединения.Условия.Добавить("(" + Условия + ")");
	Иначе
		Для Каждого СтрокаУсловия Из Условия Цикл
			ОписаниеСоединения.Условия.Добавить("(" + СтрокаУсловия + ")");
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры	

Процедура ЗаменитьТекстВУсловииЗапроса(ОператорЗапроса, ЗаменяемыйТекст, НовыйТекст) Экспорт
	Для ИндексУсловия = 0 По ОператорЗапроса.Отбор.Количество() - 1 Цикл
		ОператорЗапроса.Отбор[ИндексУсловия] = СтрЗаменить(ОператорЗапроса.Отбор[ИндексУсловия], ЗаменяемыйТекст, НовыйТекст);	
	КонецЦикла;		
КонецПроцедуры

Функция ОписаниеТаблицыЗапросаПоПсевдониму(ОператорЗапроса, ПсевдонимТаблицы) Экспорт
	Возврат ОператорЗапроса.ИсточникСоответствие[ПсевдонимТаблицы];
КонецФункции	

Процедура ЗаменитьВедущуюТаблицуВСоединении(ОператорЗапроса, ПсевдонимСоединяемойТаблицы, ПсевдонимНовойВедущейТаблицы) Экспорт
	ОписаниеСоединения = ОператорЗапроса.Соединения.НайтиСтроки(Новый Структура("ПрисоединяемаяТаблица", ПсевдонимСоединяемойТаблицы))[0];
	ОписаниеСоединения.ВедущаяТаблица = ПсевдонимНовойВедущейТаблицы;
КонецПроцедуры	

Процедура ОчиститьУсловияСоединения(ОператорЗапроса, ПсевдонимСоединяемойТаблицы) Экспорт
	ОписаниеСоединения = ОператорЗапроса.Соединения.НайтиСтроки(Новый Структура("ПрисоединяемаяТаблица", ПсевдонимСоединяемойТаблицы))[0];	
	ОписаниеСоединения.Условия.Очистить();
КонецПроцедуры	

Процедура УстановитьТипСоединения(ОператорЗапроса, ПсевдонимСоединяемойТаблицы, ТипСоединения) Экспорт
	ОписаниеСоединения = ОператорЗапроса.Соединения.НайтиСтроки(Новый Структура("ПрисоединяемаяТаблица", ПсевдонимСоединяемойТаблицы))[0];
	ОписаниеСоединения.ТипСоединения = ТипСоединения;
КонецПроцедуры	

#КонецОбласти

#Область ПараметрыПостроенияЗапросов

Функция ИсключаемыеРегистраторы(ПараметрыПостроения) Экспорт 
	ИсключаемыеРегистраторы = Новый Массив;
	Для Каждого Отбор Из ПараметрыПостроения.Отборы Цикл                    
		Если ЭтоЭлементОтбораПоИсключаемомуРегистратору(Отбор) Тогда			
			Если ТипЗнч(Отбор.ПравоеЗначение) = Тип("Массив") Тогда
				Для Каждого ЭлементОтбора Из Отбор.ПравоеЗначение Цикл		
					Если Не ЭлементОтбора.Пустая() Тогда
						ИсключаемыеРегистраторы.Добавить(ЭлементОтбора);
					КонецЕсли; 	
				КонецЦикла;
			Иначе
				ИсключаемыеРегистраторы.Добавить(Отбор.ПравоеЗначение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	
	Возврат ИсключаемыеРегистраторы;
КонецФункции	

Функция ФормироватьСПериодичностьДень(ПараметрыПостроения, ОписаниеРегистра) Экспорт 
	
	Возврат ПараметрыПостроения.ФормироватьСПериодичностьДень
		И ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Секунда;
		
КонецФункции
	
Функция ВключатьЗаписиНаНачалоПериода(ПараметрыПостроения, ОписаниеРегистра) Экспорт 
	
	Возврат ПараметрыПостроения.ВключатьЗаписиНаНачалоПериода
		И (ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Секунда
			Или ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.День
			Или ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Месяц
			Или ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Квартал
			Или ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Год);
			
КонецФункции

#КонецОбласти

#Область ОбменДанными

Процедура ОбновитьИнтервальныйРегистрПослеЗагрузкиПервичныхДанных(ИмяРегистра, ЗависимыеДанные) Экспорт
	
	Если ЗависимыеДанные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеИнтервальногоРегистра = РегистрыСведений[ИмяРегистра].ОписаниеИнтервальногоРегистра();
	КлючиИзменившихсяДанных = Неопределено;
	
	Для Каждого СтрокаТаблицы Из ЗависимыеДанные Цикл
		Если ИмяРегистра <> СтрокаТаблицы.ВедущиеМетаданные.Имя Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого НаборЗаписей Из СтрокаТаблицы.ВедущиеДанные Цикл
			ТаблицаИзменившихсяДанныхНабора = НаборЗаписей.ТаблицаИзменившихсяДанныхНабора();
			Если ТаблицаИзменившихсяДанныхНабора.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ТаблицаИзменившихсяДанныхНабора.Свернуть(ОписаниеИнтервальногоРегистра.ИзмеренияРасчета);
			Если КлючиИзменившихсяДанных = Неопределено Тогда
				КлючиИзменившихсяДанных = ТаблицаИзменившихсяДанныхНабора.Скопировать();
			Иначе
				ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаИзменившихсяДанныхНабора, КлючиИзменившихсяДанных);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если КлючиИзменившихсяДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КлючиИзменившихсяДанных.Свернуть(ОписаниеИнтервальногоРегистра.ИзмеренияРасчета);
	
	ПараметрыПостроения = ПараметрыПостроенияИнтервальногоРегистра();
	ПараметрыПостроения.ПолноеПереформирование = Истина;
	ПараметрыПостроения.ТаблицаФильтра = КлючиИзменившихсяДанных;
	
	ИнтервальныеРегистрыБЗК.СформироватьДвиженияИнтервальногоРегистра(ИмяРегистра, ПараметрыПостроения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыПостроенияИнтервальногоРегистра()
	
	ПараметрыПостроения = Новый Структура;
	ПараметрыПостроения.Вставить("ПолноеПереформирование", Ложь);
	ПараметрыПостроения.Вставить("РежимЗагрузки", Ложь);
	ПараметрыПостроения.Вставить("ТаблицаНабора", Неопределено);
	ПараметрыПостроения.Вставить("ТаблицаФильтра", Неопределено);
	ПараметрыПостроения.Вставить("ФильтрВВидеВТ", Ложь);
	
	Возврат ПараметрыПостроения;
	
КонецФункции

// Возвращает признак того, что набор записей новый
//
// Параметры:
//	НаборЗаписейРегистра - Набор записей регистра
Функция ЭтоНовыйНабор(НаборЗаписейРегистра)
	
	Возврат НаборЗаписейРегистра.ДополнительныеСвойства.Свойство("ЭтоНовыйНабор")
		И НаборЗаписейРегистра.ДополнительныеСвойства.ЭтоНовыйНабор;
	
КонецФункции

Функция ВыражениеДопПоляФильтраПоОписаниюИспользованияФильтра(ИмяДопПоля, ОписаниеИспользованиеФильтра) Экспорт
	Если ОписаниеИспользованиеФильтра.ФильтрВВидеВТ Тогда
		Возврат ОписаниеИспользованиеФильтра.ДополнительныеПоля[ИмяДопПоля];
	Иначе
		Возврат "&" + ОписаниеИспользованиеФильтра.ДополнительныеПоля[ИмяДопПоля];
	КонецЕсли;	
КонецФункции

Функция ВыраженияПолейОператораЗапроса(ОператорЗапроса)
	ВыраженияПолей = Новый Соответствие;
	Для ИндексПоля = 0 По ОператорЗапроса.ВыбираемыеПоля.Количество() - 1 Цикл
		ВыраженияПолей.Вставить(НРег(ОператорЗапроса.ПсевдонимыПолей[ИндексПоля]), ОператорЗапроса.ВыбираемыеПоля[ИндексПоля]);			
	КонецЦикла;	
	
	Возврат ВыраженияПолей;
КонецФункции

Функция ВыражениеПоляПоПсевдониму(ОператорЗапроса, ПсевдонимПоля) Экспорт
	Для ИндексПоля = 0 По ОператорЗапроса.ВыбираемыеПоля.Количество() - 1 Цикл
		Если НРег(ОператорЗапроса.ПсевдонимыПолей[ИндексПоля]) = НРег(ПсевдонимПоля) Тогда
			Возврат ОператорЗапроса.ВыбираемыеПоля[ИндексПоля];	
		КонецЕсли;	
	КонецЦикла;		
	
	Возврат Неопределено;
КонецФункции	

Функция ОписаниеСоединенияПоПрисоединяемойТаблице(ОператорЗапроса, ПсевдонимСоединяемойТаблицы)
	НайденныеСтроки = ОператорЗапроса.Соединения.НайтиСтроки(Новый Структура("ПрисоединяемаяТаблица", ПсевдонимСоединяемойТаблицы));
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		Возврат НайденныеСтроки[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
КонецФункции	

Процедура ДобавитьГруппировку(ОператорЗапроса, ВыражениеГруппировки) Экспорт
	ОператорЗапроса.Группировка.Добавить(ВыражениеГруппировки);	
КонецПроцедуры

Процедура ЗаменитьТекстВУсловииСоединения(ОператорЗапроса, ПсевдонимСоединяемойТаблицы, ЗаменяемыйТекст, НовыйТекст) Экспорт
	ОписаниеСоединения = ОператорЗапроса.Соединения.НайтиСтроки(Новый Структура("ПрисоединяемаяТаблица", ПсевдонимСоединяемойТаблицы))[0];
	
	Для ИндексУсловия = 0 По ОписаниеСоединения.Условия.Количество() - 1 Цикл
		ОписаниеСоединения.Условия[ИндексУсловия] = СтрЗаменить(ОписаниеСоединения.Условия[ИндексУсловия], ЗаменяемыйТекст, НовыйТекст);	
	КонецЦикла;		
КонецПроцедуры

Процедура УдалитьКолонкуИзОписаниеЗапроса(ОписаниеЗапроса, ИмяУдаляемойКолонки) Экспорт
	ИндексПоля = ОписаниеЗапроса.Колонки.Найти(ИмяУдаляемойКолонки);
	ОписаниеЗапроса.Колонки.Удалить(ИндексПоля);
	
	Для Каждого Запрос Из ОписаниеЗапроса.Операторы Цикл
		Запрос.ВыбираемыеПоля.Удалить(ИндексПоля);
		Запрос.ПсевдонимыПолей.Удалить(ИндексПоля);
	КонецЦикла;	
КонецПроцедуры

Функция ВыражениеДопПоляФильтраПоПсевдонимуИсточника(ИмяДопПоля, ПсевдонимТаблицыИсточника)
	Возврат ПсевдонимТаблицыИсточника + "." + ИмяДопПоля;		
КонецФункции	

Процедура ДобавитьДополнительныеПоляПоПсевдонимуИсточника(ОписаниеЗапроса, ИндексОператора, ПсевдонимТаблицыИсточника, ДополнительныеПоляФильтра, ВключатьВГруппировку = Ложь) Экспорт
	Для Каждого ДобавляемоеПоле Из ДополнительныеПоляФильтра Цикл
		ВыражениеПоля = ВыражениеДопПоляФильтраПоПсевдонимуИсточника(ДобавляемоеПоле, ПсевдонимТаблицыИсточника);
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, ИндексОператора, ВыражениеПоля, ДобавляемоеПоле);
		Если ВключатьВГруппировку Тогда
			ДобавитьГруппировку(ОписаниеЗапроса.Операторы[ИндексОператора], ВыражениеПоля);
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры	

Функция ОписаниеЗапросаПакетаПоИмениВТ(ОписаниеПакетаЗапросов, ИмяСоздаваемойВТ) Экспорт
	Для Каждого ЗапросПакета Из ОписаниеПакетаЗапросов.ЗапросыПолученияДанных Цикл
		Если ТипЗнч(ЗапросПакета) = Тип("Структура")
			И ЗапросПакета.ТаблицаДляПомещения = ИмяСоздаваемойВТ Тогда
			
			Возврат ЗапросПакета;
		КонецЕсли;
	КонецЦикла;	
	
	Возврат Неопределено;
КонецФункции	

Процедура ДобавитьЗапросУничтоженияВТ(ОписаниеПакетаЗапросов, ИмяВТ) Экспорт
	Если Ложь Тогда
		ОписаниеПакетаЗапросов = Новый Массив;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Добавить(Запрос);
	
	Запрос.Текст = "УНИЧТОЖИТЬ " + ИмяВТ;
КонецПроцедуры

#Область Интеграция

Процедура ПриПолученииЗапросаВТИмяРегистра(Запрос, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы)
	ЗарплатаКадрыПериодическиеРегистрыВнутренний.ПриПолученииЗапросаВТИмяРегистра(Запрос, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);	
КонецПроцедуры	

Процедура ПриПолученииЗапросаВТИмяРегистраСрез(Запрос, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних, ИмяСоздаваемойТаблицы)
	ЗарплатаКадрыПериодическиеРегистрыВнутренний.ПриПолученииЗапросаВТИмяРегистраСрез(Запрос, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних, ИмяСоздаваемойТаблицы);	
КонецПроцедуры	

#КонецОбласти

#Область ИнтервальныеРегистры

Процедура СоздатьВТРегистраторыДляОбновления(ИмяРегистра, МенеджерВременныхТаблиц, ИзмеренияОтбора, НаборЗаписей) Экспорт
	
	Запрос = Новый Запрос;
	НаборЗаписей.ДополнительныеСвойства.Вставить("ОбновлятьУвольнение", Истина);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрСведений.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТРегистраторыДляОбновления
		|ИЗ
		|	РегистрСведений.#ИмяРегистра КАК РегистрСведений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТаблицаОтбора КАК ТаблицаОтбора
		|		ПО РегистрСведений.ИзмеренияОтбора_ = ТаблицаОтбора.ИзмеренияОтбора_
		|ГДЕ
		|	РегистрСведений.Регистратор <> &Регистратор
		|	И РегистрСведений.ВидСобытия = &ВидСобытия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТТаблицаОтбора";
		
	МассивИзмерений = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИзмеренияОтбора, ",", Истина, Истина);
	МассивСтрокСоединения = Новый Массив;
	Разделитель = Символы.ПС + "			И ";
	Для каждого Измерение Из МассивИзмерений Цикл
	
		МассивСтрокСоединения.Добавить(Разделитель);
		МассивСтрокСоединения.Добавить("РегистрСведений.");
		МассивСтрокСоединения.Добавить(Измерение);
		МассивСтрокСоединения.Добавить(" = ТаблицаОтбора.");
		МассивСтрокСоединения.Добавить(Измерение);
	
	КонецЦикла; 
	МассивСтрокСоединения.Удалить(0);
	ТекстСоединения = СтрСоединить(МассивСтрокСоединения);
	Запрос.УстановитьПараметр("Регистратор", НаборЗаписей.Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ВидСобытия", Перечисления.ВидыКадровыхСобытий.Увольнение);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяРегистра", ИмяРегистра);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "РегистрСведений.ИзмеренияОтбора_ = ТаблицаОтбора.ИзмеренияОтбора_", ТекстСоединения);
	
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	Выборка.Следующий();
	Если Выборка.Количество = 0 Тогда
		
		НаборЗаписей.ДополнительныеСвойства.ОбновлятьУвольнение = Ложь;
		Запрос.Текст = "УНИЧТОЖИТЬ ВТРегистраторыДляОбновления";
		Запрос.Выполнить();
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиВозвратныйРегистрВИнтервальныйРегистрСведений(ИмяРегистра, ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрСведений.ИзмеренияОтбора_
		|ПОМЕСТИТЬ ВТОтборДляПереформирования
		|ИЗ
		|	РегистрСведений.#ИмяРегистра КАК РегистрСведений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.#ИмяРегистраИнтервальный КАК РегистрСведенийИнтервальный
		|		ПО РегистрСведений.ИзмеренияОтбора_ = РегистрСведенийИнтервальный.ИзмеренияОтбора_
		|ГДЕ
		|	РегистрСведенийИнтервальный.ДатаНачала ЕСТЬ NULL
		|;
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОтборДляПереформирования.*
		|ИЗ
		|	ВТОтборДляПереформирования КАК ОтборДляПереформирования";
	
	ОписаниеИнтервальногоРегистра = РегистрыСведений[ИмяРегистра].ОписаниеИнтервальногоРегистра();
	ИзмеренияРасчета = ОписаниеИнтервальногоРегистра.ИзмеренияРасчета;
	МассивИзмерений = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИзмеренияРасчета, ",", Истина, Истина);
	МассивСтрокИзмерений = Новый Массив;
	МассивСтрокСоединения = Новый Массив;
	Разделитель = "," + Символы.ПС + Символы.Таб;
	РазделительСоединения = Символы.ПС + "			И ";
	Для каждого Измерение Из МассивИзмерений Цикл
	
		МассивСтрокИзмерений.Добавить(Разделитель);
		МассивСтрокИзмерений.Добавить("РегистрСведений.");
		МассивСтрокИзмерений.Добавить(Измерение);
		
		МассивСтрокСоединения.Добавить(РазделительСоединения);
		МассивСтрокСоединения.Добавить("РегистрСведений.");
		МассивСтрокСоединения.Добавить(Измерение);
		МассивСтрокСоединения.Добавить(" = РегистрСведенийИнтервальный.");
		МассивСтрокСоединения.Добавить(Измерение);
	
	КонецЦикла;
	
	МассивСтрокИзмерений.Удалить(0);
	МассивСтрокСоединения.Удалить(0);
	ТекстИзмеренияОтбора = СтрСоединить(МассивСтрокИзмерений);
	ТекстСоединения = СтрСоединить(МассивСтрокСоединения);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "РегистрСведений.ИзмеренияОтбора_ = РегистрСведенийИнтервальный.ИзмеренияОтбора_", ТекстСоединения);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "РегистрСведений.ИзмеренияОтбора_", ТекстИзмеренияОтбора);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяРегистра", ИмяРегистра);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	Иначе
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	КонецЕсли;
	ОбновитьДвиженияИнтервальногоРегистра(ИмяРегистра, Запрос.МенеджерВременныхТаблиц);

КонецПроцедуры

Процедура ОбновитьРесурсыИнтервальногоРегистра(ИмяРегистра) Экспорт
	ИнтервальныеРегистрыБЗК.ОбновитьРесурсыИнтервальногоРегистра(ИмяРегистра);	
КонецПроцедуры

#КонецОбласти

#Область КонтрольИзмененияДанныхРегистров

Процедура СоздатьВТСтарыйНаборЗаписей(НаборЗаписей, МенеджерВременныхТаблиц, ИмяВТРезультат = "ВТСтарыйНаборЗаписей")
	ТаблицаРегистра = НаборЗаписей.Метаданные().ПолноеИмя();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ ВТСтарыйНаборЗаписей 
		|ИЗ
		|	#ТаблицаРегистра КАК РегистрСведений
		|ГДЕ
		|	РегистрСведений.Регистратор = &Регистратор";
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТСтарыйНаборЗаписей", ИмяВТРезультат);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаРегистра", ТаблицаРегистра);
	Запрос.УстановитьПараметр("Регистратор", НаборЗаписей.Отбор.Регистратор.Значение);
	Результат = Запрос.Выполнить();
	
	СтарыеЗаписи = Результат.Выбрать();
	СтарыеЗаписи.Следующий();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЭтоНовыйНабор", СтарыеЗаписи.Количество = 0);
	
КонецПроцедуры

Функция ИмяВТСтарыйНаборРегистра(ИмяРегистра)
	Возврат "ВТ" + ИмяРегистра + "СтарыйНабор";
КонецФункции	

Функция ЗапросВТИзмененияВНаборе(ИмяРегистра, РегистраторТекущегоНабора)
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос._Измерения,
	|	ВложенныйЗапрос._ДействуетДо,
	|	&ИзменившиесяРесурсы,
	|	&СтарыеЗначениеРесурсов,
	|	&НовыеЗначенияРесурсов,
	|	ВложенныйЗапрос._Реквизиты,
	|	ВЫБОР
	|		КОГДА СУММА(ВложенныйЗапрос.ФлагИзменений) = 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Удаление,
	|	ВЫБОР
	|		КОГДА СУММА(ВложенныйЗапрос.ФлагИзменений) = -1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Добавление
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтарыйНабор.Период КАК Период,
	|		СтарыйНабор._Измерения КАК _Измерения,
	|		СтарыйНабор._Ресурсы КАК _Ресурсы,
	|		СтарыйНабор._Реквизиты КАК _Реквизиты,
	|		СтарыйНабор._ДействуетДо КАК _ДействуетДо,
	|		1 КАК ФлагИзменений,
	|		ЛОЖЬ КАК НовыйНабор
	|	ИЗ
	|		&СтарыйНабор КАК СтарыйНабор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НовыйНабор.Период,
	|		НовыйНабор._Измерения,
	|		НовыйНабор._Ресурсы,
	|		НовыйНабор._Реквизиты,
	|		НовыйНабор._ДействуетДо,
	|		-1,
	|		ИСТИНА
	|	ИЗ
	|		&ТаблицаРегистра КАК НовыйНабор
	|	ГДЕ
	|		НовыйНабор.Регистратор = &Регистратор) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос._Измерения,
	|	ВложенныйЗапрос._ДействуетДо
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ВложенныйЗапрос.ФлагИзменений) <> 0
	|		ИЛИ &УсловиеИзменившиесяРесурсы)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтарыйНабор", ИмяВТСтарыйНаборРегистра(ИмяРегистра));
	
	ОписаниеРегистра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеРегистра(ИмяРегистра, Истина);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаРегистра", ОписаниеРегистра.ТипРегистра + "." + ИмяРегистра);
	Если ОписаниеРегистра.Свойство("Периодичность")
		И ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтарыйНабор.Период", "ДАТАВРЕМЯ(1,1,1)");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НовыйНабор.Период", "ДАТАВРЕМЯ(1,1,1)");
	КонецЕсли;
	
	РазделительПолейЗапроса = "," + Символы.ПС;
	
	ПоляИзмеренияСтарогоНабораЗапроса = Новый Массив;
	ПоляИзмеренияНовогоНабораЗапроса = Новый Массив;
	ПоляИзмеренияВнешнегоЗапроса  = Новый Массив;
	
	ПоляРеквизитовСтарогоНабораЗапроса = Новый Массив;
	ПоляРеквизитовНовогоНабораЗапроса = Новый Массив;
	ПоляРеквизитовВнешнегоЗапроса  = Новый Массив;
	
	Для Каждого Измерение Из ОписаниеРегистра.Измерения Цикл
		ПоляИзмеренияСтарогоНабораЗапроса.Добавить("СтарыйНабор." + Измерение + " КАК " + Измерение);
		ПоляИзмеренияНовогоНабораЗапроса.Добавить("НовыйНабор." + Измерение);
		ПоляИзмеренияВнешнегоЗапроса.Добавить("ВложенныйЗапрос." + Измерение);
	КонецЦикла;
	
	Для Каждого Реквизит Из ОписаниеРегистра.Реквизиты Цикл
		ПоляРеквизитовСтарогоНабораЗапроса.Добавить("СтарыйНабор." + Реквизит + " КАК " + Реквизит);
		ПоляРеквизитовНовогоНабораЗапроса.Добавить("НовыйНабор." + Реквизит);
		Если ОписаниеРегистра.РеквизитыСТипами[Реквизит] = Новый ОписаниеТипов("Строка")
			И (ОписаниеРегистра.РеквизитыСТипами[Реквизит].КвалификаторыСтроки.Длина = 0) Тогда
			ПоляРеквизитовВнешнегоЗапроса.Добавить("МАКСИМУМ(ПОДСТРОКА(ВложенныйЗапрос." + Реквизит + ", 0, 1000)) КАК " + Реквизит);
		Иначе
			ПоляРеквизитовВнешнегоЗапроса.Добавить("МАКСИМУМ(ВложенныйЗапрос." + Реквизит + ") КАК " + Реквизит);
		КонецЕсли;
	КонецЦикла;
	
	Если ОписаниеРегистра.Свойство("Периодичность")
		И ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтарыйНабор.Период", "ДАТАВРЕМЯ(1, 1, 1)");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НовыйНабор.Период", "ДАТАВРЕМЯ(1, 1, 1)");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтарыйНабор._Измерения КАК _Измерения", СтрСоединить(ПоляИзмеренияСтарогоНабораЗапроса, РазделительПолейЗапроса)); 
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НовыйНабор._Измерения", СтрСоединить(ПоляИзмеренияНовогоНабораЗапроса, РазделительПолейЗапроса)); 
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВложенныйЗапрос._Измерения", СтрСоединить(ПоляИзмеренияВнешнегоЗапроса, РазделительПолейЗапроса)); 
	
	Если ОписаниеРегистра.Реквизиты.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтарыйНабор._Реквизиты КАК _Реквизиты", СтрСоединить(ПоляРеквизитовСтарогоНабораЗапроса, РазделительПолейЗапроса)); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НовыйНабор._Реквизиты", СтрСоединить(ПоляРеквизитовНовогоНабораЗапроса, РазделительПолейЗапроса)); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВложенныйЗапрос._Реквизиты", СтрСоединить(ПоляРеквизитовВнешнегоЗапроса, РазделительПолейЗапроса)); 
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтарыйНабор._Реквизиты КАК _Реквизиты,", ""); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НовыйНабор._Реквизиты,", ""); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВложенныйЗапрос._Реквизиты,", ""); 
	КонецЕсли;
	
	Если ОписаниеРегистра.ЕстьВозвратныеСобытия Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтарыйНабор._ДействуетДо КАК _ДействуетДо", "СтарыйНабор.ДействуетДо КАК ДействуетДо"); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НовыйНабор._ДействуетДо", "НовыйНабор.ДействуетДо"); 
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтарыйНабор._ДействуетДо КАК _ДействуетДо", "ДАТАВРЕМЯ(1,1,1) КАК ДействуетДо"); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НовыйНабор._ДействуетДо", "ДАТАВРЕМЯ(1,1,1)"); 
	КонецЕсли;	
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВложенныйЗапрос._ДействуетДо", "ВложенныйЗапрос.ДействуетДо");
	
	ПоляРесурсыСтарогоНабора = Новый Массив;
	ПоляРесурсыНовогоНаборы = Новый Массив;
	ПоляСтарыеЗначенияРесурсов = Новый Массив;
	ПоляНовыеЗначенияРесурсов = Новый Массив;	
	ПоляИзменившиесяРесурсы = Новый Массив;
	ЧастиУсловияИзменившиесяРесурсы = Новый Массив;
	
	ШаблонПоляИзменениеРесурса = 
	"	ВЫБОР
	|		КОГДА МАКСИМУМ(ВложенныйЗапрос._Ресурс) <> МИНИМУМ(ВложенныйЗапрос._Ресурс)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИзменилсяРесурс_Ресурс";
	
	ШаблонУсловияИзмененияРесурса = 
	"	ВЫБОР
	|		КОГДА МАКСИМУМ(ВложенныйЗапрос._Ресурс) <> МИНИМУМ(ВложенныйЗапрос._Ресурс)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ";
	
	ШаблонПоляСтарогоЗначенияРесурса = 
	"	МАКСИМУМ(ВЫБОР
	|				КОГДА НЕ ВложенныйЗапрос.НовыйНабор  
	|					ТОГДА ВложенныйЗапрос._Ресурс
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ) КАК СтароеЗначение_Ресурс";
	
	ШаблонПоляНовогоЗначенияРесурса = 
	"	МАКСИМУМ(ВЫБОР
	|				КОГДА ВложенныйЗапрос.НовыйНабор
	|					ТОГДА ВложенныйЗапрос._Ресурс
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ) КАК НовоеЗначение_Ресурс";

	
	Для Каждого Ресурс Из ОписаниеРегистра.Ресурсы Цикл
		ПоляРесурсыСтарогоНабора.Добавить("СтарыйНабор." + Ресурс + " КАК " + Ресурс);
		ПоляРесурсыНовогоНаборы.Добавить("НовыйНабор." + Ресурс);
		
		ТекстПоляИзменившиесяРесурсы = СтрЗаменить(ШаблонПоляИзменениеРесурса, "_Ресурс", Ресурс);
		ПоляИзменившиесяРесурсы.Добавить(ТекстПоляИзменившиесяРесурсы);
		
		ТекстУсловияИзменившиесяРесурсы = СтрЗаменить(ШаблонУсловияИзмененияРесурса, "_Ресурс", Ресурс);
		ЧастиУсловияИзменившиесяРесурсы.Добавить(ТекстУсловияИзменившиесяРесурсы);
		
		ТекстПоляСтароеЗначениеРесурса = СтрЗаменить(ШаблонПоляСтарогоЗначенияРесурса, "_Ресурс", Ресурс);
		ПоляСтарыеЗначенияРесурсов.Добавить(ТекстПоляСтароеЗначениеРесурса);
		
		ТекстПоляНовоеЗначениеРесурса = СтрЗаменить(ШаблонПоляНовогоЗначенияРесурса, "_Ресурс", Ресурс);
		ПоляНовыеЗначенияРесурсов.Добавить(ТекстПоляНовоеЗначениеРесурса);
	КонецЦикла;	
	
	Если ОписаниеРегистра.ЕстьВозвратныеСобытия Тогда
		Для Каждого Ресурс Из ОписаниеРегистра.ВозвратныеРесурсы Цикл
			
			ПоляРесурсыСтарогоНабора.Добавить("СтарыйНабор." + Ресурс + " КАК " + Ресурс);
			ПоляРесурсыНовогоНаборы.Добавить("НовыйНабор." + Ресурс);
			
			ТекстПоляИзменившиесяРесурсы = СтрЗаменить(ШаблонПоляИзменениеРесурса, "_Ресурс", Ресурс);
			ПоляИзменившиесяРесурсы.Добавить(ТекстПоляИзменившиесяРесурсы);
			
			ТекстУсловияИзменившиесяРесурсы = СтрЗаменить(ШаблонУсловияИзмененияРесурса, "_Ресурс", Ресурс);
			ЧастиУсловияИзменившиесяРесурсы.Добавить(ТекстУсловияИзменившиесяРесурсы);
			
			ТекстПоляСтароеЗначениеРесурса = СтрЗаменить(ШаблонПоляСтарогоЗначенияРесурса, "_Ресурс", Ресурс);
			ПоляСтарыеЗначенияРесурсов.Добавить(ТекстПоляСтароеЗначениеРесурса);
			
			ТекстПоляНовоеЗначениеРесурса = СтрЗаменить(ШаблонПоляНовогоЗначенияРесурса, "_Ресурс", Ресурс);
			ПоляНовыеЗначенияРесурсов.Добавить(ТекстПоляНовоеЗначениеРесурса);
			
			РесурсПоОкончании = Ресурс + "ПоОкончании";
			
			ПоляРесурсыСтарогоНабора.Добавить("СтарыйНабор." + РесурсПоОкончании + " КАК " + РесурсПоОкончании);
			ПоляРесурсыНовогоНаборы.Добавить("НовыйНабор." + РесурсПоОкончании);
			
			ТекстПоляИзменившиесяРесурсы = СтрЗаменить(ШаблонПоляИзменениеРесурса, "_Ресурс", РесурсПоОкончании);
			ПоляИзменившиесяРесурсы.Добавить(ТекстПоляИзменившиесяРесурсы);
			
			ТекстУсловияИзменившиесяРесурсы = СтрЗаменить(ШаблонУсловияИзмененияРесурса, "_Ресурс", РесурсПоОкончании);
			ЧастиУсловияИзменившиесяРесурсы.Добавить(ТекстУсловияИзменившиесяРесурсы);
			
			ТекстПоляСтароеЗначениеРесурса = СтрЗаменить(ШаблонПоляСтарогоЗначенияРесурса, "_Ресурс", РесурсПоОкончании);
			ПоляСтарыеЗначенияРесурсов.Добавить(ТекстПоляСтароеЗначениеРесурса);
			
			ТекстПоляНовоеЗначениеРесурса = СтрЗаменить(ШаблонПоляНовогоЗначенияРесурса, "_Ресурс", РесурсПоОкончании);
			ПоляНовыеЗначенияРесурсов.Добавить(ТекстПоляНовоеЗначениеРесурса);
			
		КонецЦикла;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтарыйНабор._Ресурсы КАК _Ресурсы", СтрСоединить(ПоляРесурсыСтарогоНабора, РазделительПолейЗапроса));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НовыйНабор._Ресурсы", СтрСоединить(ПоляРесурсыНовогоНаборы, РазделительПолейЗапроса));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИзменившиесяРесурсы", СтрСоединить(ПоляИзменившиесяРесурсы, РазделительПолейЗапроса));	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтарыеЗначениеРесурсов", СтрСоединить(ПоляСтарыеЗначенияРесурсов, РазделительПолейЗапроса));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НовыеЗначенияРесурсов", СтрСоединить(ПоляНовыеЗначенияРесурсов, РазделительПолейЗапроса));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеИзменившиесяРесурсы", СтрСоединить(ЧастиУсловияИзменившиесяРесурсы, " ИЛИ "));
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Регистратор", РегистраторТекущегоНабора);
	
	Возврат Запрос;
КонецФункции	

#КонецОбласти

#Область РаботаСФильтромЗапроса 

Функция НовыйОписаниеФильтраДляСоздатьВТИмяРегистра(Измерения = "", ДополнительныеПоля = "")
	СтруктураФильтра = Новый Структура;	
	СтруктураФильтра.Вставить("СоответствиеИзмеренийРегистраИзмерениямФильтра", Новый Соответствие);
	СтруктураФильтра.Вставить("ИзмеренияФильтра");
	СтруктураФильтра.Вставить("ДополнительныеПоляФильтра", Новый Массив);
	СтруктураФильтра.Вставить("ЗначениеФильтра");
	
	Если ТипЗнч(Измерения) = Тип("Массив") Тогда
		СтруктураФильтра.ИзмеренияФильтра = Измерения;
	Иначе
		СтруктураФильтра.ИзмеренияФильтра = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Измерения, ",",,Истина);
	КонецЕсли;	
	
	Если ТипЗнч(ДополнительныеПоля) = Тип("Массив") Тогда
		СтруктураФильтра.ДополнительныеПоляФильтра = ДополнительныеПоля;
	Иначе
		СтруктураФильтра.ДополнительныеПоляФильтра = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ДополнительныеПоля, ",",,Истина);
	КонецЕсли;	
	
	Возврат СтруктураФильтра;	
КонецФункции	

Функция НовыйОписаниеИспользованияФильтра()
	ОписаниеИспользованияФильтра = Новый Структура;
	ОписаниеИспользованияФильтра.Вставить("ПсевдонимТаблицыФильтра");
	ОписаниеИспользованияФильтра.Вставить("ПсевдонимТаблицыРегистра");
	ОписаниеИспользованияФильтра.Вставить("ПостфиксИменПараметров");
	ОписаниеИспользованияФильтра.Вставить("ТекстШаблонаУсловийСвязи", "");
	ОписаниеИспользованияФильтра.Вставить("УсловияСвязи", Новый Массив);
	ОписаниеИспользованияФильтра.Вставить("ИзмеренияФильтра", Новый Структура);
	ОписаниеИспользованияФильтра.Вставить("ПоляПериодаФильтра", Новый Структура);
	ОписаниеИспользованияФильтра.Вставить("ДополнительныеПоля", Новый Структура);
	ОписаниеИспользованияФильтра.Вставить("ИзмеренияСвязи", Новый Структура);
	ОписаниеИспользованияФильтра.Вставить("ОператорЗапроса");
	ОписаниеИспользованияФильтра.Вставить("ФильтрВВидеВТ", Ложь);
	ОписаниеИспользованияФильтра.Вставить("ВсеЗаписи", Ложь);
	ОписаниеИспользованияФильтра.Вставить("ОписаниеВычисляемыхПараметровПериода", Новый Массив);
	
	Возврат ОписаниеИспользованияФильтра;
КонецФункции


Функция ФильтрВременнаяТаблицаПоТаблицеЗначений(МенеджерВременныхТаблиц, ТаблицаФильтра, ИмяВТФильтр)
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, ТаблицаФильтра, ИмяВТФильтр, Истина);	
	
	Возврат ФильтрВременнаяТаблица(ИмяВТФильтр, МенеджерВременныхТаблиц);
КонецФункции

Функция ОписаниеСоединенияСТаблицейФильтра(ОператорЗапроса, ПсевдонимТаблицыФильтра, ПсевдонимТаблицыРегистра)
	СтруктураПоиска = Новый Структура("ВедущаяТаблица, ПрисоединяемаяТаблица");
	СтруктураПоиска.ВедущаяТаблица = ПсевдонимТаблицыФильтра;
	СтруктураПоиска.ПрисоединяемаяТаблица = ПсевдонимТаблицыРегистра;
	
	НайденныеСоединения = ОператорЗапроса.Соединения.НайтиСтроки(СтруктураПоиска);
	
	Если НайденныеСоединения.Количество() = 0 Тогда
		СтруктураПоиска.ВедущаяТаблица = ПсевдонимТаблицыРегистра;
		СтруктураПоиска.ПрисоединяемаяТаблица = ПсевдонимТаблицыФильтра;	
		
		НайденныеСоединения = ОператорЗапроса.Соединения.НайтиСтроки(СтруктураПоиска);
	КонецЕсли;	
	
	Если НайденныеСоединения.Количество() = 1 Тогда
		Возврат НайденныеСоединения[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
КонецФункции	

Функция ИнициализироватьВТФильтр(ОписаниеПакетаЗапросов, ОписаниеФильтра, ОписаниеИспользованияФильтра)
	Если ОписаниеФильтра.ЗначениеФильтра.Тип = ТипФильтраВременнаяТаблица()Тогда 
		ИмяВТФильтр = ОписаниеФильтра.ЗначениеФильтра.ИмяВременнойТаблицы;	
	ИначеЕсли ОписаниеПакетаЗапросов.ОписаниеЗапросаИнициализацииФильтра = Неопределено Тогда
		ИмяВТФильтр = "ВТФильтрДля" + ОписаниеИспользованияФильтра.ПостфиксИменПараметров; 
		УстановитьОписаниеЗапросаИнициализацииВТФильтр(ОписаниеПакетаЗапросов, ОписаниеФильтра, ОписаниеИспользованияФильтра.ПоляПериодаФильтра, ИмяВТФильтр, ОписаниеИспользованияФильтра.ПостфиксИменПараметров);
		
		ТекстЗапросаУничтожения = "УНИЧТОЖИТЬ " + ИмяВТФильтр;
		ОписаниеПакетаЗапросов.ЗапросУничтоженияФильтра = Новый Запрос(ТекстЗапросаУничтожения);	
	Иначе
		ДополнитьОписаниеЗапросаИнициализацииВТФильтр(ОписаниеПакетаЗапросов.ОписаниеЗапросаИнициализацииФильтра, ОписаниеИспользованияФильтра.ПоляПериодаФильтра);
		ИмяВТФильтр = ИмяВТТаблицыЗначенийФильтра(ОписаниеПакетаЗапросов);	
	КонецЕсли;	
	
	Возврат ИмяВТФильтр;
КонецФункции	

Процедура УстановитьУсловияСвязиСФильтром(ОписаниеПакетаЗапросов, ОписаниеИспользованияФильтра, ПриемникУсловийСвязи, ОписаниеФильтра)
	Если ЗначениеЗаполнено(ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи) Тогда
		ПриемникУсловийСвязи.Добавить(ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи);
	КонецЕсли;	
	
	Для Каждого ИзмерениеФильтра Из ОписаниеИспользованияФильтра.ИзмеренияСвязи Цикл
		ВыражениеПоляФильтра = ВыражениеИзмерениеФильтра(ОписаниеИспользованияФильтра, ИзмерениеФильтра.Ключ);
		Если ОписаниеИспользованияФильтра.ФильтрВВидеВТ Тогда			
			Если ПриемникУсловийСвязи <> Неопределено Тогда
				ТекстУсловия = ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра + "." + ИзмерениеФильтра.Ключ
									+ " = " + ВыражениеПоляФильтра;	
				ПриемникУсловийСвязи.Добавить(ТекстУсловия);
			КонецЕсли;						
		Иначе
			ИмяПараметра = СтрЗаменить(ВыражениеПоляФильтра, "&", "");
			Если ПриемникУсловийСвязи <> Неопределено Тогда
				Если ОписаниеИспользованияФильтра.ВсеЗаписи Тогда
					ТекстУсловия = ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра + "." + ИзмерениеФильтра.Ключ
									+ " = " + ВыражениеПоляФильтра;		
				Иначе
					ТекстУсловия = ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра + "." + ИзмерениеФильтра.Ключ
									+ " В (" + ВыражениеПоляФильтра + ")";			
				КонецЕсли;					
								
				ПриемникУсловийСвязи.Добавить(ТекстУсловия);
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;		
КонецПроцедуры	

Функция УстановитьПараметрыИзмеренияФильтра(ОписаниеПакетаЗапросов, ОписаниеФильтра, ОписаниеИспользованияФильтра)
	Если Не ОписаниеИспользованияФильтра.ФильтрВВидеВТ Тогда
		Для Каждого ИзмерениеФильтра Из ОписаниеИспользованияФильтра.ИзмеренияФильтра Цикл
			ВыражениеПоляФильтра = ВыражениеИзмерениеФильтра(ОписаниеИспользованияФильтра, ИзмерениеФильтра.Ключ);
			ИмяПараметра = СтрЗаменить(ВыражениеПоляФильтра, "&", "");
			Если ОписаниеИспользованияФильтра.ВсеЗаписи Тогда 
				ЗначениеПараметра = ?(ОписаниеФильтра.ЗначениеФильтра.ЗначенияИзмерения.Количество() = 0, Неопределено, ОписаниеФильтра.ЗначениеФильтра.ЗначенияИзмерения[0]);
			Иначе	
				ЗначениеПараметра = ОписаниеФильтра.ЗначениеФильтра.ЗначенияИзмерения;
			КонецЕсли;	
			ОписаниеПакетаЗапросов.Параметры.Вставить(ИмяПараметра, ЗначениеПараметра);
		КонецЦикла;	
	КонецЕсли;	
КонецФункции	

Процедура УстановитьЭлементыОтбораСКД(ОписаниеИспользованияФильтра, ПараметрыПостроения = Неопределено)
	Если Не ЗначениеЗаполнено(ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра) Тогда
		Возврат;
	КонецЕсли;	
	
	Если ОписаниеИспользованияФильтра.ФильтрВВидеВТ Тогда
		Для Каждого ИзмерениеФильтра Из ОписаниеИспользованияФильтра.ИзмеренияФильтра Цикл	
			ПсевдонимЭлементаОтбора = Неопределено;
			Если ПараметрыПостроения <> Неопределено Тогда
				ПсевдонимЭлементаОтбора = ПараметрыПостроения.СоответствиеПсевдонимовПолейСКД[ИзмерениеФильтра.Ключ];
			КонецЕсли;	
			ВыражениеПоляОтбора = ОписаниеИспользованияФильтра.ПсевдонимТаблицыРегистра + "." + ИзмерениеФильтра.Ключ + ".*";
			Если ПсевдонимЭлементаОтбора <> Неопределено Тогда
				ВыражениеПоляОтбора = ВыражениеПоляОтбора + " КАК " + ПсевдонимЭлементаОтбора;
				ОписаниеИспользованияФильтра.ОператорЗапроса.ОтборыСКД.Добавить(ВыражениеПоляОтбора);
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
КонецПроцедуры	

Функция ИспользоватьВТФильтр(ОписаниеФильтра, ПолучатьВсеЗаписи)
	Если ОписаниеФильтра.ЗначениеФильтра.Тип = ТипФильтраСписокЗначений() Тогда
		Возврат ПолучатьВсеЗаписи;
	Иначе
		Возврат Истина
	КонецЕсли;	
КонецФункции

Функция ИмяВТТаблицыЗначенийФильтра(ОписаниеПакетаЗапросов)
	Если ОписаниеПакетаЗапросов.ОписаниеЗапросаИнициализацииФильтра = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	Возврат ОписаниеПакетаЗапросов.ОписаниеЗапросаИнициализацииФильтра.ТаблицаДляПомещения;
КонецФункции	

Функция ПолеТаблицыФильтра(ОписаниеФильтра, ИмяПоляОтбораРегистра)
	Если ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра[ИмяПоляОтбораРегистра] <> Неопределено Тогда
		Возврат ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра[ИмяПоляОтбораРегистра];
	Иначе
		Возврат ИмяПоляОтбораРегистра;
	КонецЕсли;	
КонецФункции	

Функция ВсеПоляТаблицыФильтра(ОписаниеФильтра, ПоляПериода = Неопределено)
	Поля = Новый Массив;
	Для Каждого Измерение Из ОписаниеФильтра.ИзмеренияФильтра Цикл
		ПолеФильтра = ПолеТаблицыФильтра(ОписаниеФильтра, Измерение);
		Поля.Добавить(ПолеФильтра);	
	КонецЦикла;	
	
	Для Каждого Поле Из ОписаниеФильтра.ДополнительныеПоляФильтра Цикл
		ПолеФильтра = ПолеТаблицыФильтра(ОписаниеФильтра, Поле);
		Поля.Добавить(ПолеФильтра);	
	КонецЦикла;	
	
	Если ПоляПериода <> Неопределено Тогда
		Для Каждого ПолеПериода Из ПоляПериода Цикл
			Поля.Добавить(ПолеПериода.Значение);
		КонецЦикла;	
	КонецЕсли;	
	
	Возврат Поля;
КонецФункции	

Функция ВыраженияПоляФильтра(ОписаниеИспользованияФильтра, Поле)
	Если ОписаниеИспользованияФильтра.ИзмеренияФильтра.Свойство(Поле)  Тогда
		Возврат ВыражениеИзмерениеФильтра(ОписаниеИспользованияФильтра, Поле);
	КонецЕсли;
		
	Если ОписаниеИспользованияФильтра.ДополнительныеПоля.Свойство(Поле) Тогда
		Возврат ВыражениеДопПоляФильтраПоОписаниюИспользованияФильтра(Поле, ОписаниеИспользованияФильтра);
	КонецЕсли;
	
	Если ОписаниеИспользованияФильтра.ПоляПериодаФильтра.Свойство(Поле) Тогда
		ОписаниеПоляПериод = ДобавитьОписаниеПоляПериодФильтра(ОписаниеИспользованияФильтра, Поле, Поле + "Исходный");
		Возврат ВыражениеПоляПериод(ОписаниеПоляПериод);	
	КонецЕсли;	
КонецФункции	

Процедура УстановитьОписаниеЗапросаИнициализацииВТФильтр(ОписаниеПакетаЗапросов, ОписаниеФильтра, ПоляПериода, ИмяВТФильтр, ПостфиксИменПараметров)
	Если ОписаниеФильтра.ЗначениеФильтра.Тип = ТипФильтраТаблицаЗначений() Тогда
		ТаблицаФильтр = ОписаниеФильтра.ЗначениеФильтра.ТаблицаЗначений;
	Иначе
		ТаблицаФильтр = ФильтрСписокЗначенийВТаблицуЗначений(ОписаниеФильтра, ПоляПериода);
	КонецЕсли;	
	
	ПоляТаблицы = ВсеПоляТаблицыФильтра(ОписаниеФильтра, ПоляПериода);
	
	ШаблонТекстаЗапроса = 
	"ВЫБРАТЬ
	|	&ШаблонПоляФильтра КАК ШаблонПоляФильтра
	|ПОМЕСТИТЬ ВТФильтр
	|ИЗ
	|	&ТаблицаФильтра КАК ТаблицаФильтра";
	
	ОписаниеЗапросаИнициализации = ОписаниеЗапросаПоТексту(ШаблонТекстаЗапроса);
	
	ОписаниеЗапросаИнициализации.ТаблицаДляПомещения = ИмяВТФильтр;
	
	ИмяПараметраТаблица = "ТаблицаФильтра" + ПостфиксИменПараметров;
	ЗаменитьТаблицуВОператореЗапроса(ОписаниеЗапросаИнициализации.Операторы[0], "ТаблицаФильтра", "&" + ИмяПараметраТаблица);
	
	Для Каждого Поле Из ПоляТаблицы Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаИнициализации, 0, "ТаблицаФильтра." + Поле, Поле, Ложь);
	КонецЦикла;	
	
	ОписаниеПакетаЗапросов.Параметры.Вставить(ИмяПараметраТаблица, ТаблицаФильтр);	
	
	ОписаниеПакетаЗапросов.ОписаниеЗапросаИнициализацииФильтра = ОписаниеЗапросаИнициализации;
КонецПроцедуры

Процедура ДополнитьОписаниеЗапросаИнициализацииВТФильтр(ОписаниеЗапросаИнициализации, ПоляПериода)
	Для Каждого Поле Из ПоляПериода Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаИнициализации, 0, Поле.Значение, Поле.Значение, Ложь);	
	КонецЦикла;	
КонецПроцедуры	

Функция ФильтрСписокЗначенийВТаблицуЗначений(ОписаниеФильтра, ПоляПериода)
	
	ТаблицаФильтра = Новый ТаблицаЗначений;
	Если ОписаниеФильтра.ИзмеренияФильтра.Количество() = 1 Тогда 
		ТипыФильтра = Новый Массив;
		
		Для Каждого Значение Из ОписаниеФильтра.ЗначениеФильтра.ЗначенияИзмерения Цикл
			Если ТипыФильтра.Найти(ТипЗнч(Значение)) = Неопределено Тогда
				ТипыФильтра.Добавить(ТипЗнч(Значение));
			КонецЕсли;	
			ТаблицаФильтра.Добавить();
		КонецЦикла;
		
		Если ТипыФильтра.Количество() > 0 Тогда
			ОписаниеТипа = Новый ОписаниеТипов(ТипыФильтра);
		Иначе
			ОписаниеТипа = Новый ОписаниеТипов("Неопределено");
		КонецЕсли;	
		
		ТаблицаФильтра.Колонки.Добавить(ОписаниеФильтра.ИзмеренияФильтра[0], ОписаниеТипа);
		ТаблицаФильтра.ЗагрузитьКолонку(ОписаниеФильтра.ЗначениеФильтра.ЗначенияИзмерения, ОписаниеФильтра.ИзмеренияФильтра[0]);
	Иначе
		ТаблицаФильтра.Добавить();
	КонецЕсли;	
	
	Для Каждого ПолеПериода Из ОписаниеФильтра.ЗначениеФильтра.ОписаниеПериода Цикл
		Если ПолеПериода.Значение <> Неопределено Тогда
			ТаблицаФильтра.Колонки.Добавить(ПолеПериода.Ключ, Новый ОписаниеТипов("Дата"));
			ТаблицаФильтра.ЗаполнитьЗначения(ПолеПериода.Значение, ПолеПериода.Ключ);
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого ДополнительноеПоле Из ОписаниеФильтра.ЗначениеФильтра.ДополнительныеПоля Цикл
		Если ДополнительноеПоле.Значение <> Неопределено Тогда
			ТипЗначения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипЗнч(ДополнительноеПоле.Значение));
			ТаблицаФильтра.Колонки.Добавить(ДополнительноеПоле.Ключ, Новый ОписаниеТипов(ТипЗначения));
			ТаблицаФильтра.ЗаполнитьЗначения(ДополнительноеПоле.Значение, ДополнительноеПоле.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаФильтра;
КонецФункции

Функция ИмяПараметраЭлементаФильтра(ИмяПараметра, ИмяТаблицы)
	Возврат ИмяПараметра + "_" + ИмяТаблицы;	
КонецФункции

Функция ФильтрВременнаяТаблица(ИмяВременнойТаблицы, МенеджерВременныхТаблиц = Неопределено)
	ЗначениеФильтра = Новый Структура;
	ЗначениеФильтра.Вставить("Тип", ТипФильтраВременнаяТаблица());
	ЗначениеФильтра.Вставить("ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	ЗначениеФильтра.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	
	Возврат ЗначениеФильтра;
КонецФункции

Функция ФильтрТаблицаЗначений(ТаблицаФильтра)
	ЗначениеФильтра = Новый Структура;
	ЗначениеФильтра.Вставить("Тип", ТипФильтраТаблицаЗначений());
	ЗначениеФильтра.Вставить("ТаблицаЗначений", ТаблицаФильтра);
	
	Возврат ЗначениеФильтра;
КонецФункции

Функция ФильтрСписокЗначений(ОписаниеПериода, ЗначенияИзмерения = Неопределено, ДополнительныеПоля = Неопределено)
	
	ЗначениеФильтра = Новый Структура;
	ЗначениеФильтра.Вставить("Тип", ТипФильтраСписокЗначений());	
	ЗначениеФильтра.Вставить("ЗначенияИзмерения", Новый Массив);
	ЗначениеФильтра.Вставить("ОписаниеПериода", ОписаниеПериода);
	ЗначениеФильтра.Вставить("ДополнительныеПоля", ДополнительныеПоля);
	
	Если ТипЗнч(ЗначенияИзмерения) = Тип("Массив") 
		Или ТипЗнч(ЗначенияИзмерения) = Тип("СписокЗначений") Тогда
		
		ЗначениеФильтра.ЗначенияИзмерения = ЗначенияИзмерения;
	Иначе
		ЗначениеФильтра.ЗначенияИзмерения.Добавить(ЗначенияИзмерения);
	КонецЕсли;	
			
	Возврат ЗначениеФильтра;
КонецФункции

Функция ФильтрСписокЗначенийПоТаблицеЗначений(ТаблицаФильтра, ОписаниеФильтра)	
	Если ОписаниеФильтра.ИзмеренияФильтра.Количество() > 1 Тогда
		Возврат Неопределено;
	ИначеЕсли ТаблицаФильтра.Количество() > 50 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеПериода = ОписаниеПериодаДляСоздатьВТИмяРегистра();
	
	ПоляГруппировки = Новый Массив;
	Для Каждого Колонка Из ТаблицаФильтра.Колонки Цикл
		Если ОписаниеФильтра.ДополнительныеПоляФильтра.Найти(Колонка.Имя) <> Неопределено Тогда
			ПоляГруппировки.Добавить(Колонка.Имя);	
		ИначеЕсли Колонка.ТипЗначения.СодержитТип(Тип("Дата")) Тогда
			Если ОписаниеПериода.Свойство(Колонка.Имя) Тогда	
				ПоляГруппировки.Добавить(Колонка.Имя);
			Иначе
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;		
	
	ПоляГруппировкиСтрокой = СтрСоединить(ПоляГруппировки, ",");
	
	ТаблицаДляПроверкиУникальности = ТаблицаФильтра.Скопировать(, ПоляГруппировкиСтрокой);
	ТаблицаДляПроверкиУникальности.Свернуть(ПоляГруппировкиСтрокой);
	
	Если ТаблицаДляПроверкиУникальности.Количество() <> 1 Тогда 
		Возврат Неопределено;
	КонецЕсли;
			
	Для Каждого ПолеПериода Из ОписаниеПериода Цикл
		Если ПоляГруппировки.Найти(ПолеПериода.Ключ) <> Неопределено Тогда
			ОписаниеПериода.Вставить(ПолеПериода.Ключ, ТаблицаДляПроверкиУникальности[0][ПолеПериода.Ключ]);	
		КонецЕсли;	
	КонецЦикла;	
	
	СписокЗначенийИзмерения = Новый Массив;
	Для Каждого Измерение Из ОписаниеФильтра.ИзмеренияФильтра Цикл
		СписокЗначенийИзмерения = ТаблицаФильтра.ВыгрузитьКолонку(Измерение);
		Прервать;
	КонецЦикла;	
	
	ДополнительныеПоля = Новый Структура;
	Если ОписаниеФильтра.ДополнительныеПоляФильтра.Количество() > 0 Тогда
		Для Каждого Поле Из ОписаниеФильтра.ДополнительныеПоляФильтра Цикл
			ДополнительныеПоля.Вставить(Поле, ТаблицаДляПроверкиУникальности[0][Поле]);			
		КонецЦикла;	
	КонецЕсли;	
	
	Возврат ФильтрСписокЗначений(ОписаниеПериода, СписокЗначенийИзмерения, ДополнительныеПоля);	
КонецФункции	

Функция ТипФильтраСписокЗначений()
	Возврат "ФильтрСписокЗначений";	
КонецФункции

Функция ТипФильтраТаблицаЗначений()
	Возврат "ФильтрТаблицаЗначений";	
КонецФункции

Функция ТипФильтраВременнаяТаблица()
	Возврат "ФильтрВременнаяТаблица";	
КонецФункции

Функция ОписаниеФильтраДляПолученияЗаписейНаНачалоПериода(ОписаниеПакетаЗапросовПолученияДвижений, ОписаниеФильтраПолученияДвижений, ПолучатьВсеЗаписи = Ложь)
	ОписаниеФильтраСреза = Неопределено;
	
	Если ИспользоватьВТФильтр(ОписаниеФильтраПолученияДвижений, ПолучатьВсеЗаписи) 
		И ОписаниеФильтраПолученияДвижений.ЗначениеФильтра.Тип <> ТипФильтраВременнаяТаблица() Тогда
		
		ИмяВременнойВТФильтр = ИмяВТТаблицыЗначенийФильтра(ОписаниеПакетаЗапросовПолученияДвижений);		
		
		ОписаниеФильтраСреза = ОписаниеФильтраДляСоздатьВТИмяРегистраПоВременнойТаблице(
									ИмяВременнойВТФильтр,
									ОписаниеФильтраПолученияДвижений.ИзмеренияФильтра, 
									ОписаниеФильтраПолученияДвижений.ДополнительныеПоляФильтра);	
	КонецЕсли;	
	
	Если ОписаниеФильтраСреза = Неопределено Тогда
		ОписаниеФильтраСреза = СкопироватьОписаниеФильтра(ОписаниеФильтраПолученияДвижений);
	КонецЕсли;	
	
	ИмяПоляДатаНачала = ПолеТаблицыФильтра(ОписаниеФильтраПолученияДвижений, "ДатаНачала"); 
	ОписаниеФильтраСреза.СоответствиеИзмеренийРегистраИзмерениямФильтра = ОбщегоНазначения.СкопироватьРекурсивно(ОписаниеФильтраПолученияДвижений.СоответствиеИзмеренийРегистраИзмерениямФильтра);
	ОписаниеФильтраСреза.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Период", ИмяПоляДатаНачала);
	
	Возврат ОписаниеФильтраСреза;
КонецФункции	

Функция СкопироватьОписаниеФильтра(КопируемоеОписаниеФильтра)		
	Если КопируемоеОписаниеФильтра.ЗначениеФильтра.Тип = ТипФильтраВременнаяТаблица() Тогда
		
		КопированноеОписание = ОписаниеФильтраДляСоздатьВТИмяРегистраПоВременнойТаблице(
									КопируемоеОписаниеФильтра.ЗначениеФильтра.ИмяВременнойТаблицы,
									КопируемоеОписаниеФильтра.ИзмеренияФильтра,
									КопируемоеОписаниеФильтра.ДополнительныеПоляФильтра);
									
	ИначеЕсли КопируемоеОписаниеФильтра.ЗначениеФильтра.Тип = ТипФильтраТаблицаЗначений() Тогда
		
		КопированноеОписание = ОписаниеФильтраДляСоздатьВТИмяРегистраПоТаблицеЗначений(
									КопируемоеОписаниеФильтра.ЗначениеФильтра.ТаблицаЗначений,
									КопируемоеОписаниеФильтра.ИзмеренияФильтра,
									КопируемоеОписаниеФильтра.ДополнительныеПоляФильтра);
	
	ИначеЕсли КопируемоеОписаниеФильтра.ЗначениеФильтра.Тип = ТипФильтраСписокЗначений() Тогда
		
		КопированноеОписание = ОписаниеФильтраДляСоздатьВТИмяРегистраПоСпискуЗначений(
									КопируемоеОписаниеФильтра.ЗначениеФильтра.ОписаниеПериода,
									КопируемоеОписаниеФильтра.ИзмеренияФильтра[0], 
									КопируемоеОписаниеФильтра.ЗначениеФильтра.ЗначенияИзмерения);		
	КонецЕсли;	
	
	КопированноеОписание.СоответствиеИзмеренийРегистраИзмерениямФильтра = ОбщегоНазначения.СкопироватьРекурсивно(КопируемоеОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра);	
	
	Возврат КопированноеОписание;
КонецФункции	

#КонецОбласти 

#Область ОписаниеПолейПериода

Функция ЗначениеПоляПериод(ОписаниеПоляПериода, ИсходноеЗначение)
	Если ОписаниеПоляПериода.ПустоеЗначениеКакМаксимальное 
		И Не ЗначениеЗаполнено(ИсходноеЗначение) Тогда
		
		Возврат МаксимальнаяДата();
	КонецЕсли;	
	
	ПриведенноеЗначение = ЗначениеПоляПериодПриведенное(ИсходноеЗначение, ОписаниеПоляПериода.ВариантПриведенияПериода, ОписаниеПоляПериода.Кратность);
			
	КратностьСдвига = ?(ОписаниеПоляПериода.КратностьСдвига = Неопределено, ОписаниеПоляПериода.Кратность, ОписаниеПоляПериода.КратностьСдвига); 
		
	Возврат ЗначениеПоляПериодСдвинутое(ПриведенноеЗначение, КратностьСдвига, ОписаниеПоляПериода.Сдвиг)

КонецФункции

Функция ЗначениеПоляПериодПриведенное(ИсходноеЗначение, ВариантПриведенияПериода, Кратность)
	Если ВРег(Кратность) = "СЕКУНДА" Тогда
		Возврат ИсходноеЗначение;
	ИначеЕсли ВРег(Кратность) = "МИНУТА" Тогда
		Если ВРег(ВариантПриведенияПериода) = "НАЧАЛОПЕРИОДА" Тогда
			Возврат НачалоМинуты(ИсходноеЗначение);
		Иначе
			Возврат КонецМинуты(ИсходноеЗначение);
		КонецЕсли;
	ИначеЕсли ВРег(Кратность) = "ЧАС" Тогда
		Если ВРег(ВариантПриведенияПериода) = "НАЧАЛОПЕРИОДА" Тогда
			Возврат НачалоЧаса(ИсходноеЗначение);
		Иначе
			Возврат КонецЧаса(ИсходноеЗначение);
		КонецЕсли;	
	ИначеЕсли ВРег(Кратность) = "ДЕНЬ" Тогда
		Если ВРег(ВариантПриведенияПериода) = "НАЧАЛОПЕРИОДА" Тогда
			Возврат НачалоДня(ИсходноеЗначение);
		Иначе
			Возврат КонецДня(ИсходноеЗначение);
		КонецЕсли;
	ИначеЕсли ВРег(Кратность) = "НЕДЕЛЯ" Тогда
		Если ВРег(ВариантПриведенияПериода) = "НАЧАЛОПЕРИОДА" Тогда
			Возврат НачалоНедели(ИсходноеЗначение);
		Иначе
			Возврат КонецНедели(ИсходноеЗначение);
		КонецЕсли;	
	ИначеЕсли ВРег(Кратность) = "МЕСЯЦ" Тогда
		Если ВРег(ВариантПриведенияПериода) = "НАЧАЛОПЕРИОДА" Тогда
			Возврат НачалоМесяца(ИсходноеЗначение);
		Иначе
			Возврат КонецМесяца(ИсходноеЗначение);
		КонецЕсли;
	ИначеЕсли ВРег(Кратность) = "КВАРТАЛ" Тогда
		Если ВРег(ВариантПриведенияПериода) = "НАЧАЛОПЕРИОДА" Тогда
			Возврат НачалоКвартала(ИсходноеЗначение);
		Иначе
			Возврат КонецКвартала(ИсходноеЗначение);
		КонецЕсли;		
	ИначеЕсли ВРег(Кратность) = "ГОД" Тогда
		Если ВРег(ВариантПриведенияПериода) = "НАЧАЛОПЕРИОДА" Тогда
			Возврат НачалоГода(ИсходноеЗначение);
		Иначе
			Возврат КонецГода(ИсходноеЗначение);
		КонецЕсли;	
	Иначе
		Возврат ИсходноеЗначение;
	КонецЕсли;	
			
КонецФункции	

Функция ЗначениеПоляПериодСдвинутое(ИсходноеЗначение, Кратность, Сдвиг)
	ДлинаСуток = 86400;
	
	Если ВРег(Кратность) = "СЕКУНДА" Тогда
		Возврат ИсходноеЗначение + 1 * Сдвиг;
	ИначеЕсли ВРег(Кратность) = "МИНУТА" Тогда
		Возврат ИсходноеЗначение + 60 * Сдвиг;
	ИначеЕсли ВРег(Кратность) = "ЧАС" Тогда
		Возврат ИсходноеЗначение + 3600 * Сдвиг;
	ИначеЕсли ВРег(Кратность) = "ДЕНЬ" Тогда
		Возврат ИсходноеЗначение + ДлинаСуток * Сдвиг;
	ИначеЕсли ВРег(Кратность) = "НЕДЕЛЯ" Тогда
		Возврат ИсходноеЗначение + ДлинаСуток * 7 * Сдвиг; 
	ИначеЕсли ВРег(Кратность) = "МЕСЯЦ" Тогда
		Возврат ДобавитьМесяц(ИсходноеЗначение, Сдвиг);
	ИначеЕсли ВРег(Кратность) = "КВАРТАЛ" Тогда
		Возврат ДобавитьМесяц(ИсходноеЗначение, 3 * Сдвиг);	
	ИначеЕсли ВРег(Кратность) = "ГОД" Тогда
		Возврат ДобавитьМесяц(ИсходноеЗначение, 12 * Сдвиг);
	Иначе
		Возврат ИсходноеЗначение;
	КонецЕсли;	
			
КонецФункции	

Функция КратностьПериодаРегистра(ОписаниеРегистра, ПараметрыПостроения) Экспорт
	ФормироватьСПериодичностьДень = ФормироватьСПериодичностьДень(ПараметрыПостроения, ОписаниеРегистра);	
	
	Если ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Секунда Тогда
		Если ФормироватьСПериодичностьДень Тогда
			КратностьПериода = "ДЕНЬ";
		Иначе
			КратностьПериода = "СЕКУНДА";
		КонецЕсли;
	ИначеЕсли ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.День Тогда
		КратностьПериода = "ДЕНЬ";
	ИначеЕсли ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Месяц Тогда
		КратностьПериода = "МЕСЯЦ";
	ИначеЕсли ОписаниеРегистра.Периодичность = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Квартал Тогда
		КратностьПериода = "КВАРТАЛ";
	Иначе
		КратностьПериода = "ГОД";
	КонецЕсли;	
	
	Возврат КратностьПериода;
КонецФункции	

#КонецОбласти 

#Область ПостроениеМоделиЗапроса

Функция НовыйОписаниеЗапроса()
	СтруктураШаблонаЗапроса = Новый Структура;
	СтруктураШаблонаЗапроса.Вставить("ВыбиратьРазрешенные", Ложь);
	СтруктураШаблонаЗапроса.Вставить("ТаблицаДляПомещения", "");
	СтруктураШаблонаЗапроса.Вставить("Колонки", Новый Массив);
	СтруктураШаблонаЗапроса.Вставить("Порядок", Новый Массив);
	СтруктураШаблонаЗапроса.Вставить("Операторы", Новый Массив);
	СтруктураШаблонаЗапроса.Вставить("ПоляИндексирования", Новый Массив);
	
	Возврат СтруктураШаблонаЗапроса;	
КонецФункции

Функция НовыйОператорЗапроса()
	Соединения = Новый ТаблицаЗначений;
	Соединения.Колонки.Добавить("ВедущаяТаблица", Новый ОписаниеТипов("Строка"));
	Соединения.Колонки.Добавить("ПрисоединяемаяТаблица", Новый ОписаниеТипов("Строка"));
	Соединения.Колонки.Добавить("ПорядокСоединения", Новый ОписаниеТипов("Число"));
	Соединения.Колонки.Добавить("Условия");
	Соединения.Колонки.Добавить("ТипСоединения", Новый ОписаниеТипов("Строка"));

	Соединения.Индексы.Добавить("ВедущаяТаблица");
	Соединения.Индексы.Добавить("ПрисоединяемаяТаблица");
	Соединения.Индексы.Добавить("ВедущаяТаблица, ПрисоединяемаяТаблица");
	
	ОператорЗапроса = Новый Структура;
	ОператорЗапроса.Вставить("ТипОбъединения");
	ОператорЗапроса.Вставить("ВыбираемыеПоля", Новый Массив);
	ОператорЗапроса.Вставить("ВыбиратьРазличные", Ложь);
	ОператорЗапроса.Вставить("КоличествоЗаписей", 0);
	ОператорЗапроса.Вставить("Таблицы", Новый Массив);
	ОператорЗапроса.Вставить("Соединения", Соединения);
	ОператорЗапроса.Вставить("Отбор", Новый Массив);
	ОператорЗапроса.Вставить("Имеющие", Новый Массив);
	ОператорЗапроса.Вставить("Группировка", Новый Массив);
	ОператорЗапроса.Вставить("ВыбираемыеПоляСКД", Новый Массив);
	ОператорЗапроса.Вставить("ОтборыСКД", Новый Массив);
	ОператорЗапроса.Вставить("ИсточникСоответствие", Новый Соответствие);
	ОператорЗапроса.Вставить("ПсевдонимыПолей", Новый Массив);
	
	Возврат ОператорЗапроса;
КонецФункции

Функция НовыйОписаниеТаблицыЗапроса()
	Возврат Новый Структура("Имя, Псевдоним, Тип");	
КонецФункции

Функция НовыйОписаниеВложенногоЗапроса()
	Возврат Новый Структура("ОписаниеЗапроса, Псевдоним, Тип");	
КонецФункции

// Возвращает описание запроса
// 
// Параметры:
// 	ЗапросВыбораСхемыЗапроса - ЗапросВыбораСхемыЗапроса - ЗапросВыбораСхемыЗапроса
// Возвращаемое значение:
// 	Структура - см. НовыйОписаниеЗапроса() 
Функция ОписаниеЗапросаПоЗапросуПакета(ЗапросВыбораСхемыЗапроса)
	ОписаниеЗапроса = НовыйОписаниеЗапроса();

	ОписаниеЗапроса.ВыбиратьРазрешенные = ЗапросВыбораСхемыЗапроса.ВыбиратьРазрешенные;
	ОписаниеЗапроса.ТаблицаДляПомещения = ЗапросВыбораСхемыЗапроса.ТаблицаДляПомещения;
		
	Для Каждого ВыражениеУпорядочивания Из ЗапросВыбораСхемыЗапроса.Порядок Цикл
		Если ТипЗнч(ВыражениеУпорядочивания.Элемент) = Тип("КолонкаСхемыЗапроса") Тогда
			ЭлементУпорядочивания = ВыражениеУпорядочивания.Элемент.Псевдоним;
		Иначе	
			ЭлементУпорядочивания = Строка(ВыражениеУпорядочивания);
		КонецЕсли;	
			
		Если Не ЭтоВыражениеШаблон(ЭлементУпорядочивания) Тогда
			ОписаниеЗапроса.Порядок.Добавить(ЭлементУпорядочивания + ?(ВыражениеУпорядочивания.Направление = НаправлениеПорядкаСхемыЗапроса.ПоУбыванию, " УБЫВ", ""));
		КонецЕсли;	
	КонецЦикла;	
	
	СтруктураПоискаСоединения = Новый Структура("ПрисоединяемаяТаблица");
	Для Каждого Оператор Из ЗапросВыбораСхемыЗапроса.Операторы Цикл
		СтруктураОбъединяемогоЗапроса = НовыйОператорЗапроса();	
		ОписаниеЗапроса.Операторы.Добавить(СтруктураОбъединяемогоЗапроса);
		
		СтруктураОбъединяемогоЗапроса.ТипОбъединения = ТипОбъединенияСтрокой(Оператор.ТипОбъединения);
		СтруктураОбъединяемогоЗапроса.ВыбиратьРазличные = Оператор.ВыбиратьРазличные;
		СтруктураОбъединяемогоЗапроса.КоличествоЗаписей = Оператор.КоличествоПолучаемыхЗаписей;
			
		Для Каждого ВыражениеГруппировки Из Оператор.Группировка Цикл
			ЭлементГруппировки = Строка(ВыражениеГруппировки);
			Если Не ЭтоВыражениеШаблон(ЭлементГруппировки) Тогда
				ДобавитьГруппировку(СтруктураОбъединяемогоЗапроса, ЭлементГруппировки);
			КонецЕсли;	
		КонецЦикла;	
		
		Для Каждого ВыражениеОтбора Из Оператор.Отбор Цикл
			ЭлементОтбора = Строка(ВыражениеОтбора);
			Если Не ЭтоВыражениеШаблон(ЭлементОтбора) Тогда
				Если ВыражениеОтбора.СодержитАгрегатнуюФункцию() Тогда
					СтруктураОбъединяемогоЗапроса.Имеющие.Добавить(ЭлементОтбора);	
				Иначе	
					ДобавитьУсловие(СтруктураОбъединяемогоЗапроса, ЭлементОтбора);
				КонецЕсли;	
			КонецЕсли;	
		КонецЦикла;	
		
		Для Каждого ИсточникСхемы Из Оператор.Источники Цикл
			Если ТипЗнч(ИсточникСхемы.Источник) = Тип("ВложенныйЗапросСхемыЗапроса") Тогда
				ОписаниеТаблицыЗапроса = ОписаниеВложенногоЗапроса(ИсточникСхемы.Источник.Запрос, ИсточникСхемы.Источник.Псевдоним);		
			Иначе	
				ОписаниеТаблицыЗапроса = ОписаниеТаблицыЗапроса(ИсточникСхемы.Источник.ИмяТаблицы, ИсточникСхемы.Источник.Псевдоним);
			КонецЕсли;	
			СтруктураОбъединяемогоЗапроса.Таблицы.Добавить(ОписаниеТаблицыЗапроса);
			СтруктураОбъединяемогоЗапроса.ИсточникСоответствие.Вставить(ИсточникСхемы.Источник.Псевдоним, ОписаниеТаблицыЗапроса);
			
			ПорядокСоединения = 0;
			Для Каждого Соединение Из ИсточникСхемы.Соединения Цикл
				СтруктураПоискаСоединения.ПрисоединяемаяТаблица = Соединение.Источник.Источник.Псевдоним;
				СтрокиСоединения = СтруктураОбъединяемогоЗапроса.Соединения.НайтиСтроки(СтруктураПоискаСоединения);
				
				Если СтрокиСоединения.Количество() = 0 Тогда
					ПорядокСоединения = ПорядокСоединения + 1;
					
					ОписаниеСоединения = СтруктураОбъединяемогоЗапроса.Соединения.Добавить();
					ОписаниеСоединения.ВедущаяТаблица = ИсточникСхемы.Источник.Псевдоним;
					ОписаниеСоединения.ПрисоединяемаяТаблица = Соединение.Источник.Источник.Псевдоним;
					ОписаниеСоединения.ТипСоединения = Строка(Соединение.ТипСоединения);
					ОписаниеСоединения.Условия = Новый Массив;
					ОписаниеСоединения.ПорядокСоединения = ПорядокСоединения;
				Иначе
					ОписаниеСоединения = СтрокиСоединения[0];	
				КонецЕсли;
				СтрокаУсловия = "(" + Строка(Соединение.Условие) + ")";
				Если Не ЭтоВыражениеШаблон(СтрокаУсловия) Тогда
					ОписаниеСоединения.Условия.Добавить(СтрокаУсловия);
				КонецЕсли;						
			КонецЦикла;	
		КонецЦикла;		
	КонецЦикла;		
	
	ИндексПоля = 0;
	Для Каждого Колонка Из ЗапросВыбораСхемыЗапроса.Колонки Цикл
		ДобавляемыеПоля = Новый Массив;
		ДобавлятьКолонку = Ложь;
		Для Каждого Поле Из Колонка.Поля Цикл
			ПолеСтрокой = Строка(Поле);
			Если ЭтоВыражениеШаблон(ПолеСтрокой) Тогда
				ДобавляемыеПоля.Добавить("NULL");
			Иначе
				ДобавляемыеПоля.Добавить(ПолеСтрокой);	
				ДобавлятьКолонку = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ДобавлятьКолонку Тогда
			Для ИндексОператора = 0 По ДобавляемыеПоля.Количество() - 1 Цикл
				ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, ИндексОператора, ДобавляемыеПоля[ИндексОператора], Колонка.Псевдоним, Ложь);
			КонецЦикла;	
		КонецЕсли;	
	КонецЦикла;	
 
	Возврат ОписаниеЗапроса;
КонецФункции	
	
Функция ОписаниеТаблицыЗапроса(Имя, Псевдоним)
	ОписаниеТаблицыЗапроса = НовыйОписаниеТаблицыЗапроса();
	ОписаниеТаблицыЗапроса.Имя = Имя;
	ОписаниеТаблицыЗапроса.Псевдоним = Псевдоним;
	ОписаниеТаблицыЗапроса.Тип = "ОписаниеТаблицыЗапроса";
	
	Возврат ОписаниеТаблицыЗапроса	
КонецФункции

Функция ОписаниеВложенногоЗапроса(ЗапросПакета, Псевдоним)
	ОписаниеТаблицыЗапроса = НовыйОписаниеВложенногоЗапроса();
	ОписаниеТаблицыЗапроса.ОписаниеЗапроса = ОписаниеЗапросаПоЗапросуПакета(ЗапросПакета);
	ОписаниеТаблицыЗапроса.Псевдоним = Псевдоним;
	ОписаниеТаблицыЗапроса.Тип = "ОписаниеВложенногоЗапроса";
	
	Возврат ОписаниеТаблицыЗапроса	
КонецФункции

Функция ЭтоВыражениеШаблон(ТекстВыражения)	
	Возврат Лев(ТекстВыражения, 7) = "&Шаблон" ИЛИ Лев(ТекстВыражения, 8) = "(&Шаблон"	
КонецФункции	

#КонецОбласти 

#Область РаботаСМодельюЗапроса

Процедура ЗаполнитьИспользуемыеВЗапросеТаблицы(ИспользуемыеТаблицы, ОператорЗапроса, ОписаниеПакетаЗапросов)
	Для Каждого ОписаниеТаблицы Из ОператорЗапроса.Таблицы Цикл
		Если ИспользуемыеТаблицы.Найти(ОписаниеТаблицы.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		ИспользуемыеТаблицы.Добавить(ОписаниеТаблицы.Имя);
		ОписаниеЗапросаФормирующееВТ = ОписаниеЗапросаПакетаПоИмениВТ(ОписаниеПакетаЗапросов, ОписаниеТаблицы.Имя);
		Если ОписаниеЗапросаФормирующееВТ <> Неопределено Тогда
			Для Каждого ОператорФормированияВТ Из ОписаниеЗапросаФормирующееВТ.Операторы Цикл
				ЗаполнитьИспользуемыеВЗапросеТаблицы(ИспользуемыеТаблицы, ОператорФормированияВТ, ОписаниеПакетаЗапросов);
			КонецЦикла;	
		КонецЕсли;			
	КонецЦикла;		
КонецПроцедуры	

#КонецОбласти

#Область ГенерацияТекстЗапросаПоМодели

Процедура ОписаниеЗапросаВМассивСтрок(МассивСтрок, ОписаниеЗапроса, ВыводитьЭлементыСКД = Истина)	
	ИндексОператора = 0;
	Для Каждого ОператорЗапроса Из ОписаниеЗапроса.Операторы Цикл
		МассивСтрок.Добавить(Символы.ПС);
		
		Если ИндексОператора <> 0 Тогда
			МассивСтрок.Добавить(ОператорЗапроса.ТипОбъединения);
			МассивСтрок.Добавить(Символы.ПС);
		КонецЕсли;
		
		ОператорЗапросаВМассивСтрок(ОписаниеЗапроса, ОператорЗапроса, МассивСтрок, ИндексОператора = 0, ВыводитьЭлементыСКД);
		ИндексОператора = ИндексОператора + 1;
	КонецЦикла;	
	
	УпорядочиваниеОписанияЗапросаВМассивСтрок(ОписаниеЗапроса, МассивСтрок);
	
	ИндексированиеОписанияЗапросаВМассивСтрок(ОписаниеЗапроса, МассивСтрок);
КонецПроцедуры	

Процедура УпорядочиваниеОписанияЗапросаВМассивСтрок(ОписаниеЗапроса, МассивСтрок)
	ИндексЭлементаПорядка = 0;
	Если ОписаниеЗапроса.Порядок.Количество() > 0 Тогда
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить("УПОРЯДОЧИТЬ ПО");
		МассивСтрок.Добавить(Символы.ПС);
	КонецЕсли;	
	
	ИндексЭлементаПорядка = 0;
	Для Каждого ЭлементПорядка Из ОписаниеЗапроса.Порядок Цикл
		МассивСтрок.Добавить(ЭлементПорядка);
		
		Если ИндексЭлементаПорядка <> ОписаниеЗапроса.Порядок.Количество() - 1 Тогда			
			МассивСтрок.Добавить(РазделительПолей());
		КонецЕсли;
		
		ИндексЭлементаПорядка = ИндексЭлементаПорядка + 1;	
	КонецЦикла;		
КонецПроцедуры	

Процедура ИндексированиеОписанияЗапросаВМассивСтрок(ОписаниеЗапроса, МассивСтрок)
	Если ЗначениеЗаполнено(ОписаниеЗапроса.ТаблицаДляПомещения)
		И ОписаниеЗапроса.ПоляИндексирования.Количество() > 0 Тогда
		
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить("ИНДЕКСИРОВАТЬ ПО");
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Символы.Таб);
		
		ИндексЭлементаИндекса = 0;
		Для Каждого ЭлементИндекса Из ОписаниеЗапроса.ПоляИндексирования Цикл			
			МассивСтрок.Добавить(ЭлементИндекса);
			
			Если ИндексЭлементаИндекса <> ОписаниеЗапроса.ПоляИндексирования.Количество() - 1 Тогда			
				МассивСтрок.Добавить(РазделительПолей());
			КонецЕсли;	
			
			ИндексЭлементаИндекса = ИндексЭлементаИндекса + 1;	
		КонецЦикла;	
	КонецЕсли;	
КонецПроцедуры	

Процедура ОператорЗапросаВМассивСтрок(ОписаниеЗапроса, ОператорЗапроса, МассивСтрок, ЭтоПервыйОператор, ВыводитьЭлементыСКД)
	МассивСтрок.Добавить("ВЫБРАТЬ");
	
	Если ЭтоПервыйОператор И ОписаниеЗапроса.ВыбиратьРазрешенные Тогда
		МассивСтрок.Добавить(" РАЗРЕШЕННЫЕ");
	КонецЕсли;
	
	МодификаторыОператораЗапросаВМассивСтрок(ОписаниеЗапроса, ОператорЗапроса, МассивСтрок);	
	
	ПоляОператораЗапросаВМассивСтрок(ОписаниеЗапроса, ОператорЗапроса, МассивСтрок);
	
	Если ВыводитьЭлементыСКД И ЭтоПервыйОператор Тогда
		ПоляСКДОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок);
	КонецЕсли;	
	
	МассивСтрок.Добавить(Символы.ПС);
	Если ЭтоПервыйОператор И Не ПустаяСтрока(ОписаниеЗапроса.ТаблицаДляПомещения) Тогда
		МассивСтрок.Добавить("ПОМЕСТИТЬ ");
		МассивСтрок.Добавить(ОписаниеЗапроса.ТаблицаДляПомещения);
		МассивСтрок.Добавить(Символы.ПС);
	КонецЕсли;	
		
	ИсточникиДанныхОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок);
	
	МассивСтрок.Добавить(Символы.ПС);

	УсловияОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок);
	
	Если ВыводитьЭлементыСКД Тогда
		ОтборыСКДОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок);
	КонецЕсли;	
	
	МассивСтрок.Добавить(Символы.ПС);
	
	ГруппировкаОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок);
	
	СекцияИмеющиеОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок);
КонецПроцедуры

Процедура МодификаторыОператораЗапросаВМассивСтрок(ОписаниеЗапроса, ОператорЗапроса, МассивСтрок)	
	Если ОператорЗапроса.ВыбиратьРазличные Тогда
		МассивСтрок.Добавить(" РАЗЛИЧНЫЕ");
	КонецЕсли;	
	
	Если ОператорЗапроса.КоличествоЗаписей <> Неопределено Тогда
		МассивСтрок.Добавить(" ПЕРВЫЕ " + Строка(ОператорЗапроса.КоличествоЗаписей));
	КонецЕсли;	
КонецПроцедуры	

Процедура ПоляОператораЗапросаВМассивСтрок(ОписаниеЗапроса, ОператорЗапроса, МассивСтрок)
	МассивСтрок.Добавить(Символы.ПС);
	МассивСтрок.Добавить(Символы.Таб);
	ИндексПоля = 0;
	Для Каждого Поле Из ОператорЗапроса.ВыбираемыеПоля Цикл
		МассивСтрок.Добавить(Поле);
		МассивСтрок.Добавить(" КАК ");
		МассивСтрок.Добавить(ОписаниеЗапроса.Колонки[ИндексПоля]);
		Если ИндексПоля <> ОператорЗапроса.ВыбираемыеПоля.Количество() - 1 Тогда
			МассивСтрок.Добавить(РазделительПолей(ИндексПоля));
		КонецЕсли;	
		ИндексПоля = ИндексПоля + 1;
	КонецЦикла;	
КонецПроцедуры	

Процедура ПоляСКДОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок)
	Если ОператорЗапроса.ВыбираемыеПоляСКД.Количество() > 0 Тогда
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить("{ВЫБРАТЬ");
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Символы.Таб);
		
		ИндексПоля = 0;
		Для Каждого ОписаниеПоля Из ОператорЗапроса.ВыбираемыеПоляСКД Цикл
			МассивСтрок.Добавить(ОписаниеПоля.Поле);
			Если ЗначениеЗаполнено(ОписаниеПоля.ПсевдонимСКД) Тогда
				МассивСтрок.Добавить(" КАК ");
				МассивСтрок.Добавить(ОписаниеПоля.ПсевдонимСКД);
			КонецЕсли;	
			Если ИндексПоля <> ОператорЗапроса.ВыбираемыеПоляСКД.Количество() - 1 Тогда			
				МассивСтрок.Добавить(РазделительПолей());
			КонецЕсли;	
			
			ИндексПоля = ИндексПоля + 1;	
		КонецЦикла;		
		МассивСтрок.Добавить("}");
	КонецЕсли;		
КонецПроцедуры	

Процедура ИсточникиДанныхОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок)
	Если ОператорЗапроса.Таблицы.Количество() > 0 Тогда
		МассивСтрок.Добавить("ИЗ");
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Символы.Таб);
	КонецЕсли;	
	
	ИндексТаблицы = 0;
	Для Каждого ОписаниеТаблицы Из ОператорЗапроса.Таблицы Цикл
		Если ОператорЗапроса.Соединения.НайтиСтроки(Новый Структура("ПрисоединяемаяТаблица", ОписаниеТаблицы.Псевдоним)).Количество() <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИндексТаблицы <> 0 Тогда
			МассивСтрок.Добавить(",");
			МассивСтрок.Добавить(Символы.ПС);
		КонецЕсли;	
		
		Если ОписаниеТаблицы.Тип = "ОписаниеВложенногоЗапроса" Тогда
			МассивСтрок.Добавить("(");
			ОписаниеЗапросаВМассивСтрок(МассивСтрок, ОписаниеТаблицы.ОписаниеЗапроса, Ложь);	
			МассивСтрок.Добавить(")");
		Иначе	
			МассивСтрок.Добавить(ОписаниеТаблицы.Имя);
		КонецЕсли;	
		
		МассивСтрок.Добавить(" КАК ");
		МассивСтрок.Добавить(ОписаниеТаблицы.Псевдоним);
		
		СтрокиСоединения = ОператорЗапроса.Соединения.НайтиСтроки(Новый Структура("ВедущаяТаблица", ОписаниеТаблицы.Псевдоним));
		Соединения = ОператорЗапроса.Соединения.Скопировать(СтрокиСоединения);
		Соединения.Сортировать("ПорядокСоединения");
		
		Для Каждого ОписаниеСоединения Из Соединения Цикл
			СоединениеВМассивСтрок(МассивСтрок, ОписаниеСоединения, ОператорЗапроса);
		КонецЦикла;	
		
		ИндексТаблицы = ИндексТаблицы + 1;
	КонецЦикла;	
КонецПроцедуры	

Процедура УсловияОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок)
	Если ОператорЗапроса.Отбор.Количество() > 0 Тогда
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить("ГДЕ");
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Символы.Таб);
	КонецЕсли;	
		
	ИндексУсловия = 0;
	Для Каждого Условие Из ОператорЗапроса.Отбор Цикл
		МассивСтрок.Добавить("(");
		МассивСтрок.Добавить(Условие);
		МассивСтрок.Добавить(")");
		Если ИндексУсловия <> ОператорЗапроса.Отбор.Количество() - 1 Тогда			
			МассивСтрок.Добавить(РазделительУсловий());
		КонецЕсли;	
		
		ИндексУсловия = ИндексУсловия + 1;	
	КонецЦикла;		
КонецПроцедуры	

Процедура ГруппировкаОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок)
	Если ОператорЗапроса.Группировка.Количество() > 0 Тогда  
	 	МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить("СГРУППИРОВАТЬ ПО");
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Символы.Таб);
	КонецЕсли;
	
	ИндексГруппировки = 0;
	Для Каждого ЭлементГруппировки Из ОператорЗапроса.Группировка Цикл
		Если ИндексГруппировки <> 0 Тогда			
			МассивСтрок.Добавить(РазделительПолей());
		КонецЕсли;	
		МассивСтрок.Добавить(ЭлементГруппировки);
		
		ИндексГруппировки = ИндексГруппировки + 1;	
	КонецЦикла;			                                              
КонецПроцедуры	

Процедура СекцияИмеющиеОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок)
	Если ОператорЗапроса.Имеющие.Количество() > 0 Тогда  
	 	МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить("ИМЕЮЩИЕ");
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Символы.Таб);
	КонецЕсли;
	
	Индекс = 0;
	Для Каждого ЭлементОтбора Из ОператорЗапроса.Имеющие Цикл
		Если Индекс <> 0 Тогда			
			МассивСтрок.Добавить(РазделительУсловий());
		КонецЕсли;	
		МассивСтрок.Добавить(ЭлементОтбора);
		
		Индекс = Индекс + 1;	
	КонецЦикла;			                                              
КонецПроцедуры	

Процедура СоединениеВМассивСтрок(МассивСтрок, ОписаниеСоединения, ОписаниеОбъединяемогоЗапроса)
	ОписаниеПрисоединяемойТаблицы = ОписаниеОбъединяемогоЗапроса.ИсточникСоответствие[ОписаниеСоединения.ПрисоединяемаяТаблица];
	МассивСтрок.Добавить("
	|	");
	МассивСтрок.Добавить(ОписаниеСоединения.ТипСоединения);
	МассивСтрок.Добавить(" СОЕДИНЕНИЕ ");
	МассивСтрок.Добавить(ОписаниеПрисоединяемойТаблицы.Имя);
	МассивСтрок.Добавить(" КАК ");
	МассивСтрок.Добавить(ОписаниеПрисоединяемойТаблицы.Псевдоним);
	
	СтрокиСоединения = ОписаниеОбъединяемогоЗапроса.Соединения.НайтиСтроки(Новый Структура("ВедущаяТаблица", ОписаниеПрисоединяемойТаблицы.Псевдоним));
	Соединения = ОписаниеОбъединяемогоЗапроса.Соединения.Скопировать(СтрокиСоединения);
	Соединения.Сортировать("ПорядокСоединения");
	
	Для Каждого ОписаниеВложенногоСоединения Из СтрокиСоединения Цикл
		СоединениеВМассивСтрок(МассивСтрок, ОписаниеВложенногоСоединения, ОписаниеОбъединяемогоЗапроса);
	КонецЦикла;	
	
	МассивСтрок.Добавить("
	|	ПО ");
	
	ИндексУсловия = 0;
	Для Каждого Условие Из ОписаниеСоединения.Условия Цикл
		МассивСтрок.Добавить("(");
		МассивСтрок.Добавить(Условие);	
		МассивСтрок.Добавить(")");

		Если ИндексУсловия <> ОписаниеСоединения.Условия.Количество() - 1 Тогда
			МассивСтрок.Добавить(РазделительУсловий());
		КонецЕсли;
		
		ИндексУсловия = ИндексУсловия + 1;
	КонецЦикла;	
	
	Если ОписаниеСоединения.Условия.Количество() = 0 Тогда
		МассивСтрок.Добавить("ИСТИНА");
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОтборыСКДОператораЗапросаВМассивСтрок(ОператорЗапроса, МассивСтрок)
	Если ОператорЗапроса.ОтборыСКД.Количество() > 0 Тогда
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить("{ГДЕ");
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Символы.Таб);
		
		ИндексЭлементаОтбора = 0;
		Для Каждого ЭлементОтбора Из ОператорЗапроса.ОтборыСКД Цикл
			МассивСтрок.Добавить(ЭлементОтбора);
			Если ИндексЭлементаОтбора <> ОператорЗапроса.ОтборыСКД.Количество() - 1 Тогда			
				МассивСтрок.Добавить(РазделительПолей());
			КонецЕсли;	
			
			ИндексЭлементаОтбора = ИндексЭлементаОтбора + 1;	
		КонецЦикла;		
		МассивСтрок.Добавить("}");
	КонецЕсли;		
КонецПроцедуры	

Функция ТекстУсловияСоединения(ОписаниеОператораЗапроса, ПсевдонимСоединяемойТаблицы) Экспорт
	ОписаниеСоединения = ОписаниеОператораЗапроса.Соединения.НайтиСтроки(Новый Структура("ПрисоединяемаяТаблица", ПсевдонимСоединяемойТаблицы))[0];
	
	Возврат ТекстУсловия(ОписаниеСоединения.Условия);
КонецФункции	

Функция ТекстУсловия(Условия)
	Если Условия.Количество() = 0 Тогда
		Возврат "";
	ИначеЕсли Условия.Количество() = 1 Тогда
		Возврат Условия[0];
	Иначе
		Возврат СтрСоединить(Условия, РазделительУсловий());
	КонецЕсли;			
КонецФункции	

Функция ТипОбъединенияСтрокой(ТипОбъединения)
	Если ТипОбъединения = ТипОбъединенияСхемыЗапроса.Объединить Тогда
		Возврат "ОБЪЕДИНИТЬ";
	Иначе
		Возврат "ОБЪЕДИНИТЬ ВСЕ";
	КонецЕсли;	
КонецФункции	

Функция РазделительУсловий()
	Возврат "
			|	И ";	
КонецФункции	

Функция РазделительПолей(ИндексПоля = Неопределено)
	Возврат ",
			|	";	
КонецФункции	

#КонецОбласти 

#Область ПараметрыПостроенияЗапросов 

Функция ПараметрыПостроенияВТИмяРегистра()
	
	ПараметрыПостроения = Новый Структура;
	ПараметрыПостроения.Вставить("ИндексироватьПо");
	ПараметрыПостроения.Вставить("Отборы", Новый Массив);
	ПараметрыПостроения.Вставить("ФормироватьСПериодичностьДень", Истина);
	ПараметрыПостроения.Вставить("ИспользоватьРасширениеЯзыкаЗапросовДляСКД", Истина);
	ПараметрыПостроения.Вставить("ПостоянныеПоля");
	ПараметрыПостроения.Вставить("ИсключаемыеРегистраторы", Ложь);
	ПараметрыПостроения.Вставить("СоответствиеПсевдонимовПолей", Новый Соответствие);
	ПараметрыПостроения.Вставить("СоответствиеПсевдонимовПолейСКД", Новый Соответствие);
	ПараметрыПостроения.Вставить("ИсключатьНеИспользуемыеПоля", Истина);
	
	Возврат ПараметрыПостроения;
	
КонецФункции

Функция ИспользоватьПервичныйРегистр(ПараметрыПостроения, ИмяРегистра, ЭтоСрез = Ложь, ДопустимоеЧислоИсключаемыхРегистраторов = 1)
	
	Если ПараметрыПостроения = Неопределено
		ИЛИ ПараметрыПостроения.Отборы = Неопределено Тогда
		
		Возврат Ложь;
	КонецЕсли; 
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.Найти(ИмяРегистра);
	Для Каждого Отбор Из ПараметрыПостроения.Отборы Цикл
		Если ЭтоСрез
			И МетаданныеРегистра.Ресурсы.Найти(Отбор.ЛевоеЗначение) <> Неопределено 
			И ВРег(Отбор.ЛевоеЗначение) <> ВРег("ЭтоГоловнойСотрудник") Тогда
			
			Возврат Истина;
		КонецЕсли; 
	КонецЦикла;
	
	ИсключаемыеРегистраторы = ИсключаемыеРегистраторы(ПараметрыПостроения);
	
	Для ИндексТекущегоЭлемента = 0 По ИсключаемыеРегистраторы.Количество() - 1 Цикл
		ТекущийЭлемент = ИсключаемыеРегистраторы[ИндексТекущегоЭлемента];
		Если ТекущийЭлемент = Неопределено
			Или Не ТекущийЭлемент.Метаданные().Движения.Содержит(МетаданныеРегистра) Тогда
			ИсключаемыеРегистраторы.Удалить(ИндексТекущегоЭлемента);
		Иначе
			ИндексТекущегоЭлемента = ИндексТекущегоЭлемента + 1;
		КонецЕсли;
	КонецЦикла;	
		
	Если ИсключаемыеРегистраторы.Количество() > 0 Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РегистрСведений.Регистратор) КАК Регистратор
			|ИЗ
			|	РегистрСведений.#ИмяРегистра КАК РегистрСведений
			|ГДЕ
			|	РегистрСведений.Регистратор В(&Регистраторы)
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РегистрСведений.Регистратор) > 0";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяРегистра", ИмяРегистра);
		Запрос.УстановитьПараметр("Регистраторы", ИсключаемыеРегистраторы);
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Выборка.Следующий() Тогда
			
			ПараметрыПостроения.Вставить("ИсключаемыеРегистраторы", Истина);
			Если Выборка.Регистратор > ДопустимоеЧислоИсключаемыхРегистраторов Тогда
				Возврат Истина;
			КонецЕсли; 
			
		Иначе
			ПараметрыПостроения.Вставить("ИсключаемыеРегистраторы", Ложь);
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат Ложь;
	
КонецФункции

Функция ПараметрыПостроенияСрезаПоследнихПоПараметрамПостроения(ПараметрыПостроения, ОписаниеРегистра)
	
	ПараметрыПостроенияСрезаПоследних = ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроенияСрезаПоследних.ФормироватьСПериодичностьДень = ФормироватьСПериодичностьДень(ПараметрыПостроения, ОписаниеРегистра);
	ПараметрыПостроенияСрезаПоследних.ИсключаемыеРегистраторы = ПараметрыПостроения.ИсключаемыеРегистраторы;	
	ПараметрыПостроенияСрезаПоследних.ИсключатьНеИспользуемыеПоля = ПараметрыПостроения.ИсключатьНеИспользуемыеПоля;
	
	Если ПараметрыПостроения.Отборы <> Неопределено Тогда
		
		Для каждого ОписаниеОтбора Из ПараметрыПостроения.Отборы Цикл
			
			Если ОписаниеОтбора.Свойство("ОтносительныйПуть") Тогда
				ОтносительныйПуть = ОписаниеОтбора.ОтносительныйПуть;
			Иначе
				ОтносительныйПуть = Истина;
			КонецЕсли;                   
			
			Если ОтносительныйПуть
				И (ОписаниеРегистра.ИзмеренияДляПоиска.Получить(ВРег(ОписаниеОтбора.ЛевоеЗначение)) <> Неопределено
					Или ВРег(ОписаниеОтбора.ЛевоеЗначение) = ВРег("Регистратор"))Тогда
					
				ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроенияСрезаПоследних.Отборы, ОписаниеОтбора.ЛевоеЗначение, ОписаниеОтбора.ВидСравнения, ОписаниеОтбора.ПравоеЗначение, ОтносительныйПуть);
				
			Иначе
				ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроенияСрезаПоследних.ОтборыПрименяемыеКСрезу, ОписаниеОтбора.ЛевоеЗначение, ОписаниеОтбора.ВидСравнения, ОписаниеОтбора.ПравоеЗначение, ОтносительныйПуть);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ПараметрыПостроенияСрезаПоследних;
	
КонецФункции

Функция ЭтоЭлементОтбораПоИсключаемомуРегистратору(ЭлементОтбора)
	Если ВРег(ЭлементОтбора.ЛевоеЗначение) = ВРег("Регистратор")  
		И (ЭлементОтбора.ВидСравнения = "<>" 
		ИЛИ ВРег(ЭлементОтбора.ВидСравнения) = "НЕ В") Тогда
		
		Возврат Истина;	
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции	

#КонецОбласти

#Область ВспомогательныеМетоды 

Функция ИмяВспомогательнойВТДоступныеЗаписи(ИмяРегистра, ПараметрыПостроения)
	
	Возврат "ВТДоступныеЗаписи" + ИмяРегистра;
	
КонецФункции

Функция ИмяВспомогательнойВТПериодыСрезаНаНачалоДня(ИмяРегистра, ПараметрыПостроения)
	
	Возврат "ВТПериодыСрезаНаНачалоДня" + ИмяРегистра;
	
КонецФункции

Функция ИмяПараметраИсключаемыйРегистратор(Постфикс)
	Возврат "ИсключаемыйРегистратор" + Постфикс;	
КонецФункции	

Функция УникальноеИмяПараметраЗапроса(Знач ИмяТаблицы, НомерПараметра = Неопределено) Экспорт
	
	УникальноеИмяПараметра = ИмяТаблицы + "_Параметр";
	
	Если ТипЗнч(НомерПараметра) = Тип("Число") Тогда
		
		УникальноеИмяПараметра = УникальноеИмяПараметра + Формат(НомерПараметра, "ЧГ=");
		НомерПараметра = НомерПараметра + 1;
		
	КонецЕсли;
	
	Возврат УникальноеИмяПараметра;
	
КонецФункции

Функция ИмяВспомогательнойВТПериодыСреза(ИмяРегистра)
	
	Возврат "ВТПериодыСреза" + ИмяРегистра;
	
КонецФункции

Функция ДоступенИнтервальныйРегистрСведений(Знач ИмяРегистра)
	Возврат Метаданные.РегистрыСведений.Найти(ИнтервальныеРегистрыБЗК.ИмяИнтервальногоРегистра(ИмяРегистра)) <> Неопределено;
КонецФункции

Функция ИмяСоздаваемойТаблицыСрезПоследнихВПараметрахПостроения(ИмяРегистра, ПараметрыПостроения) Экспорт
	
	Если ПустаяСтрока(ПараметрыПостроения.ИмяВременнойТаблицыЗаписейНаНачалоПериода) Тогда
		ИмяСоздаваемойТаблицыСрезПоследних = "ВТ" + ИмяРегистра + "СрезПоследних";
	Иначе
		ИмяСоздаваемойТаблицыСрезПоследних = ПараметрыПостроения.ИмяВременнойТаблицыЗаписейНаНачалоПериода;
	КонецЕсли; 
	
	ПараметрыПостроения.ИспользуемоеИмяВременнойТаблицыЗаписейНаНачалоПериода = ИмяСоздаваемойТаблицыСрезПоследних;
	
	Возврат ИмяСоздаваемойТаблицыСрезПоследних;
	
КонецФункции

// Проверяет возможность использования интервального регистра сведений
//
// Параметры:
//		ИмяРегистра - Строка - Имя проверяемого регистра.
//
// Возвращаемое значение:
//		Булево
Функция ИспользоватьИнтервальныйРегистрСведений(Знач ИмяРегистра)
	
	Возврат Метаданные.РегистрыСведений.Найти(ИнтервальныеРегистрыБЗК.ИмяИнтервальногоРегистра(ИмяРегистра)) <> Неопределено;
	
КонецФункции

#КонецОбласти

#Область МетодыПолученияДанных

Функция ВыполнитьЗапросПолученияДвиженийРегистра(Знач ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяРезультирующейТаблицы = Неопределено)	
	Если ИмяРезультирующейТаблицы = Неопределено Тогда
		ИмяРезультирующейТаблицы = "ВТ" + ИмяРегистра;
	КонецЕсли;
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	КонецЕсли;	
	
	ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД = Ложь;
	
	Запрос = Неопределено;	
	ПриПолученииЗапросаВТИмяРегистра(Запрос, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяРезультирующейТаблицы);

	Если Запрос <> Неопределено Тогда
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	    РезультатЗапроса = Запрос.Выполнить();
	ИначеЕсли ИспользоватьИнтервальныйРегистрСведений(ИмяРегистра)
		Или ПривилегированныйРежим()
		Или ПараметрыПостроения.ВключатьЗаписиНаНачалоПериода Тогда
		
		Запрос = ЗапросВТИмяРегистра(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяРезультирующейТаблицы);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		РезультатЗапроса = Запрос.Выполнить();
	Иначе	
		ИмяВТДоступныеЗаписи = ИмяВспомогательнойВТДоступныеЗаписи(ИмяРегистра, ПараметрыПостроения);
		Запрос = ЗапросВТДоступныеЗаписиИмяРегистра(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ИмяВТДоступныеЗаписи, ПараметрыПостроения);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.Выполнить();
		
		Запрос = ЗапросВТТаблицаРегистра(ИмяРегистра, Ложь, ОписаниеФильтра, ИмяВТДоступныеЗаписи, ПараметрыПостроения, ИмяРезультирующейТаблицы);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();                                
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат РезультатЗапроса;
КонецФункции	

Функция ВыполнитьЗапросПолученияСрезаРегистра(Знач ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, СрезПоследних = Истина, ИмяРезультирующейТаблицы = Неопределено)	
	Если ИмяРезультирующейТаблицы = Неопределено Тогда
		ИмяРезультирующейТаблицы = "ВТ" + ИмяРегистра + ?(СрезПоследних,"СрезПоследних","СрезПервых");
	КонецЕсли;
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	КонецЕсли;	
	
	ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД = Ложь;
	
	Запрос = Неопределено;	
	ПриПолученииЗапросаВТИмяРегистраСрез(Запрос, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних, ИмяРезультирующейТаблицы);

	Если Запрос <> Неопределено Тогда 
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		РезультатЗапроса = Запрос.Выполнить();
	ИначеЕсли ПривилегированныйРежим()
		Или (ИспользоватьИнтервальныйРегистрСведений(ИмяРегистра)
		И СрезПоследних) Тогда
		
		Запрос = ЗапросВТИмяРегистраСрез(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних, ИмяРезультирующейТаблицы);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		РезультатЗапроса = Запрос.Выполнить();
	Иначе
		Запрос = ЗапросВТПериодыСреза(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.Выполнить();
		
		Запрос = ЗапросВТТаблицаСреза(ИмяРегистра, Ложь, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних, ИмяРезультирующейТаблицы);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса =  Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);	
	КонецЕсли;
	
	Возврат РезультатЗапроса;	
КонецФункции

Функция ВыполнитьЗапросПолученияПериодовРегистра(Знач ИмяРегистра, МенеджерВременныхТаблиц, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяРезультирующейТаблицы = Неопределено)
	Если ИмяРезультирующейТаблицы = Неопределено Тогда
		ИмяРезультирующейТаблицы = "ВТ" + ИмяРегистра + "Периоды";
	КонецЕсли;
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистраПериоды()
	КонецЕсли;	
	
	ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД = Ложь;
	
	Запрос = ЗапросВТПериодыИмяРегистра(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяРезультирующейТаблицы);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
	Возврат Запрос.Выполнить();	
КонецФункции	

#КонецОбласти

#Область ФормированиеЗапросовКРегистрам

Функция ЗапросВТДоступныеЗаписиИмяРегистра(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ИмяВТДоступныеЗаписиИмяРегистра, ПараметрыПостроения = Неопределено)
	ОписаниеПакета = НовыйОписаниеПакетаЗапросовКРегистру();
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	КонецЕсли;
	
	ДобавитьЗапросВТДоступныеЗаписиИмяРегистра(ОписаниеПакета, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ИмяВТДоступныеЗаписиИмяРегистра, ПараметрыПостроения);
	
	Возврат ЗапросПоОписаниюПакета(ОписаниеПакета, ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД);
КонецФункции

Функция ЗапросВТТаблицаРегистра(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ИмяВТДоступныеЗаписи, ПараметрыПостроения = Неопределено, ИмяСоздаваемойТаблицы = Неопределено)
	Если ИмяСоздаваемойТаблицы = Неопределено Тогда
		ИмяСоздаваемойТаблицы = "ВТ" + ИмяРегистра;
	КонецЕсли;	
	
	ОписаниеПакета = НовыйОписаниеПакетаЗапросовКРегистру();
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	КонецЕсли;
	
	ДобавитьЗапросВТТаблицаРегистра(ОписаниеПакета, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ИмяВТДоступныеЗаписи, ПараметрыПостроения, ИмяСоздаваемойТаблицы);
	
	Возврат ЗапросПоОписаниюПакета(ОписаниеПакета, ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД);
КонецФункции

Функция ЗапросВТПериодыСреза(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, СрезПоследних = Истина)
	ОписаниеПакета = НовыйОписаниеПакетаЗапросовКРегистру();
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	КонецЕсли;
	
	ДобавитьЗапросВТПериодыСреза(ОписаниеПакета, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних);
	
	Возврат ЗапросПоОписаниюПакета(ОписаниеПакета, ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД);
КонецФункции	

Функция ЗапросВТТаблицаСреза(ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, СрезПоследних = Истина, ИмяСоздаваемойТаблицы = Неопределено)
	ОписаниеПакета = НовыйОписаниеПакетаЗапросовКРегистру();
	
	Если ИмяСоздаваемойТаблицы = Неопределено Тогда
		ИмяСоздаваемойТаблицы = "ВТ" + ИмяРегистра + ?(СрезПоследних,"СрезПоследних","СрезПервых");
	КонецЕсли;
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	КонецЕсли;
	
	ДобавитьЗапросВТТаблицаСреза(ОписаниеПакета, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних, ИмяСоздаваемойТаблицы);
	
	Возврат ЗапросПоОписаниюПакета(ОписаниеПакета, ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД);

КонецФункции	

Процедура ДобавитьЗапросВТИмяРегистра(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяСоздаваемойТаблицы = Неопределено)	
	Если ПараметрыПостроения <> Неопределено Тогда
		ПараметрыПостроения.ИспользуемоеИмяВременнойТаблицыЗаписейНаНачалоПериода = "";
	КонецЕсли; 
	
	ИмяВТДоступныеЗаписи = ИмяВспомогательнойВТДоступныеЗаписи(ИмяРегистра, ПараметрыПостроения);
	Если ДоступенИнтервальныйРегистрСведений(ИмяРегистра) Тогда	
		Если ИспользоватьПервичныйРегистр(ПараметрыПостроения, ИмяРегистра) Тогда
			ИнтервальныеРегистрыБЗК.ДобавитьЗапросВТДвиженияПервичныйРегистр(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);
		Иначе
			ИнтервальныеРегистрыБЗК.ДобавитьЗапросВТДвиженияИмяИнтервальногоРегистра(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);
		КонецЕсли;
		ПараметрыПостроения.ИсключаемыеРегистраторы = Ложь;
	Иначе
		ДобавитьЗапросВТДоступныеЗаписиИмяРегистра(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ИмяВТДоступныеЗаписи, ПараметрыПостроения);
		ДобавитьЗапросВТТаблицаРегистра(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ИмяВТДоступныеЗаписи, ПараметрыПостроения, ИмяСоздаваемойТаблицы);		
	КонецЕсли;	
КонецПроцедуры

Процедура ДобавитьЗапросВТДоступныеЗаписиИмяРегистра(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ИмяВТДоступныеЗаписиИмяРегистра, ПараметрыПостроения = Неопределено)	
	ПостфиксИменПараметров = ИмяВТДоступныеЗаписиИмяРегистра;
	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	КонецЕсли;
	
	ОписаниеРегистра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеРегистраСведений(ИмяРегистра, ОписаниеФильтра.ИзмеренияФильтра, ПараметрыПостроения.ИсключатьНеИспользуемыеПоля);
	ВключатьЗаписиНаНачалоПериода = ВключатьЗаписиНаНачалоПериода(ПараметрыПостроения, ОписаниеРегистра);
		
	ТекстШаблонаЗапросаПолученияЗаписейРегистра = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ШаблонИзмерения КАК ВыбираемыеПоля,
	|	&ШаблонРесурсы КАК Ресурсы,
	|	&ШаблонВозвратныеРесурсы КАК ВозвратныеРесурсы,
	|	&ШаблонРеквизиты КАК Реквизиты,
	|	&ШаблонСтандартныеРеквизиты КАК СтандартныеРеквизиты,
	|	&ШаблонПериод КАК Период,
	|	&ШаблонПериодЗаписи КАК ПериодЗаписи,
	|	&ШаблонЗаписьПериода КАК ЗаписьПериода,
	|	&ШаблонДействуетДо КАК ДействуетДо,
	|	&ШаблонВозвратнаяЗапись КАК ВозвратнаяЗапись
	|ПОМЕСТИТЬ ВТДоступныеЗаписи
	|ИЗ
	|	ВТИзмеренияДаты КАК ИзмеренияДаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #РегистрСведений КАК РегистрСведений
	|		ПО (&ШаблонУсловияСвязиПериоду)
	|			И (&ШаблонУсловиеСвязиИзмерениям)";
		
	ШаблонПоляЭтоВозвратнаяЗапись = "
	|	ВЫБОР
	|		КОГДА РегистрСведений.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1)  
	|			ТОГДА ЛОЖЬ
	|		КОГДА РегистрСведений.ДействуетДо <= &ФильтрДатаОкончанияПриведенная_
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ";
	
	ШаблонПоляЗаписьПериода = "
	|(РегистрСведений.Период >= &ФильтрДатаНачала_
	|	И РегистрСведений.Период <= &ФильтрДатаОкончанияПриведенная_)";
	
	ШаблонУсловияСоединенияПоПериоду = "
	|(РегистрСведений.Период >= &ФильтрДатаНачала_
	|	И (РегистрСведений.Период <= &ФильтрДатаОкончанияПриведенная_)" +
	?(ОписаниеРегистра.ЕстьВозвратныеСобытия, "
	|ИЛИ
	|РегистрСведений.ДействуетДо >= &ФильтрДатаНачала_
	|	И (РегистрСведений.ДействуетДо <= &ФильтрДатаОкончанияПриведенная_)
	|	И РегистрСведений.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1))", 
	")");
			
	ОписаниеЗапроса = ОписаниеЗапросаПоТексту(ТекстШаблонаЗапросаПолученияЗаписейРегистра);
	ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Добавить(ОписаниеЗапроса);
	
	ОписаниеЗапроса.ВыбиратьРазрешенные = ТолькоРазрешенные;
	ОписаниеЗапроса.ТаблицаДляПомещения = ИмяВТДоступныеЗаписиИмяРегистра;

	ОператорЗапроса = ОписаниеЗапроса.Операторы[0];
	
	ОписаниеИспользованияФильтра = ОписаниеИспользованиеФильтра();
	ИнициализироватьИспользованиеФильтра(ОписаниеИспользованияФильтра, ОписаниеФильтра, ОписаниеРегистра, "ДатаНачала, ДатаОкончания", ОператорЗапроса, ПостфиксИменПараметров, Ложь);
	ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи = ШаблонУсловияСоединенияПоПериоду;
	
	ЗаменитьТаблицуВОператореЗапроса(ОператорЗапроса, "РегистрСведений", "РегистрСведений." + ИмяРегистра);
	
	ФильтрДатаНачала = ДобавитьОписаниеПоляПериодФильтра(ОписаниеИспользованияФильтра, "ДатаНачала", "ФильтрДатаНачала");
	ФильтрДатаОкончания = ДобавитьОписаниеПоляПериодФильтра(ОписаниеИспользованияФильтра, "ДатаОкончания", "ФильтрДатаОкончания");
	ФильтрДатаОкончанияПриведенная  = ДобавитьОписаниеПоляПериодФильтра(ОписаниеИспользованияФильтра, "ДатаОкончания", "ФильтрДатаОкончанияПриведенная");

	РегистрПериод = ОписаниеПоляПериода("Период", "РегистрСведений");
	
	ФильтрДатаОкончанияПриведенная.ПустоеЗначениеКакМаксимальное = Истина;
	
	Кратность = КратностьПериодаРегистра(ОписаниеРегистра, ПараметрыПостроения);
	РегистрПериод.Кратность = Кратность;
	ФильтрДатаОкончанияПриведенная.Кратность = Кратность;
	ФильтрДатаОкончанияПриведенная.ВариантПриведенияПериода = "КОНЕЦПЕРИОДА";
	
	Если ВключатьЗаписиНаНачалоПериода Тогда
		ФильтрДатаНачала.Кратность = Кратность;
		ФильтрДатаНачала.Сдвиг = 1;
	ИначеЕсли ФормироватьСПериодичностьДень(ПараметрыПостроения, ОписаниеРегистра) Тогда
		ФильтрДатаНачала.Кратность = Кратность;
	КонецЕсли; 
	
	УстановитьВыражениеПериодаВТекстШаблонаУсловияСвязи(ОписаниеИспользованияФильтра, "&ФильтрДатаНачала_", ФильтрДатаНачала);
	УстановитьВыражениеПериодаВТекстШаблонаУсловияСвязи(ОписаниеИспользованияФильтра, "&ФильтрДатаОкончания_", ФильтрДатаОкончания);
	УстановитьВыражениеПериодаВТекстШаблонаУсловияСвязи(ОписаниеИспользованияФильтра, "&ФильтрДатаОкончанияПриведенная_", ФильтрДатаОкончанияПриведенная);
			
	ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, ВыражениеПоляПериод(РегистрПериод), "Период");
	ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений.Период", "ПериодЗаписи");
		
	ПолеЗаписьПериода = СтрЗаменить(ШаблонПоляЗаписьПериода, "&ФильтрДатаНачала_", ВыражениеПоляПериод(ФильтрДатаНачала));
	ПолеЗаписьПериода = СтрЗаменить(ПолеЗаписьПериода, "&ФильтрДатаОкончанияПриведенная_", ВыражениеПоляПериод(ФильтрДатаОкончанияПриведенная));
	
	ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, ПолеЗаписьПериода, "ЗаписьПериода");
		
	Если ОписаниеРегистра.ЕстьВозвратныеСобытия Тогда				
		ПолеЭтоВозвратнаяЗапись = СтрЗаменить(ШаблонПоляЭтоВозвратнаяЗапись, "&ФильтрДатаОкончания_", ВыражениеПоляПериод(ФильтрДатаОкончания));
		ПолеЭтоВозвратнаяЗапись = СтрЗаменить(ПолеЭтоВозвратнаяЗапись, "&ФильтрДатаОкончанияПриведенная_", ВыражениеПоляПериод(ФильтрДатаОкончанияПриведенная));
		
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, ПолеЭтоВозвратнаяЗапись, "ВозвратнаяЗапись");
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений.ДействуетДо", "ДействуетДо");
	КонецЕсли;                                                             
		
	Для Каждого Измерение Из ОписаниеРегистра.Измерения Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + Измерение, Измерение);
	КонецЦикла;
		
	Для Каждого Ресурс Из ОписаниеРегистра.ВозвратныеРесурсы Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + Ресурс, Ресурс);
		ИмяВозвратногоРесурса = Ресурс + "ПоОкончании";
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + ИмяВозвратногоРесурса, ИмяВозвратногоРесурса);
	КонецЦикла;
	
	Для Каждого Ресурс Из ОписаниеРегистра.Ресурсы Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + Ресурс, Ресурс);
	КонецЦикла;
	
	Для Каждого Реквизит Из ОписаниеРегистра.Реквизиты Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + Реквизит, Реквизит);
	КонецЦикла;
	
	Для Каждого Реквизит Из ОписаниеРегистра.СтандартныеРеквизиты Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + Реквизит, Реквизит);
	КонецЦикла;
		
	УстановитьФильтрВОписаниеПакетаЗапросовКРегистру(ОписаниеПакетаЗапросов, ОписаниеФильтра, ОписаниеИспользованияФильтра, ПараметрыПостроения);
	УстановитьОтборВОператорЗапросаДанныхРегистра(ОператорЗапроса, ПараметрыПостроения.Отборы, ОписаниеПакетаЗапросов.Параметры, ПостфиксИменПараметров);			
	ДобавитьДополнительныеПоляПоОписаниюИспользованияФильтра(ОписаниеЗапроса, 0, ОписаниеИспользованияФильтра);
	
КонецПроцедуры

Процедура ДобавитьЗапросВТТаблицаРегистра(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ИмяВТДоступныеЗаписиИмяРегистра, ПараметрыПостроения = Неопределено, ИмяСоздаваемойТаблицы = Неопределено)	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	КонецЕсли; 

	Если ИмяСоздаваемойТаблицы = Неопределено Тогда
		ИмяСоздаваемойТаблицы = "ВТ" + ИмяРегистра;
	КонецЕсли; 
	
	ОписаниеРегистра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеРегистраСведений(ИмяРегистра, ОписаниеФильтра.ИзмеренияФильтра, ПараметрыПостроения.ИсключатьНеИспользуемыеПоля);
	ФормироватьСПериодичностьДень = ФормироватьСПериодичностьДень(ПараметрыПостроения, ОписаниеРегистра);
	ВключатьЗаписиНаНачалоПериода = ВключатьЗаписиНаНачалоПериода(ПараметрыПостроения, ОписаниеРегистра);	
	
	ШаблонЗапросаВТПредставленияВсеЗаписи = 
	"ВЫБРАТЬ
	|	ДоступныеЗаписи.Период КАК Период,
	|	ДоступныеЗаписи.ПериодЗаписи КАК ПериодЗаписи,
	|	ЛОЖЬ КАК ЭтоВозвратноеСобытие,
	|	ДоступныеЗаписи.ДействуетДо КАК ПериодВозвратногоСобытия,
	|	&ШаблонИзмерения КАК Измерения,
	|	&ШаблонРесурсы КАК Ресурсы,
	|	&ШаблонРеквизиты КАК Реквизиты,
	|	&ШаблонСтандартныеРеквизиты КАК СтандартныеРеквизиты
	|ПОМЕСТИТЬ ВТПредставленияВсеЗаписи
	|ИЗ
	|	ВТДоступныеЗаписи КАК ДоступныеЗаписи
	|ГДЕ
	|	ДоступныеЗаписи.ЗаписьПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоступныеЗаписи.ДействуетДо,
	|	ДоступныеЗаписи.ДействуетДо,
	|	ИСТИНА,
	|	ДАТАВРЕМЯ(3999, 12, 31, 23, 59, 59),
	|	&ШаблонИзмерения,
	|	&ШаблонВозвратныеРесурсы,
	|	&ШаблонРеквизиты,
	|	&ШаблонСтандартныеРеквизиты
	|ИЗ
	|	ВТДоступныеЗаписи КАК ДоступныеЗаписи
	|		{ЛЕВОЕ СОЕДИНЕНИЕ #РегистрСведений КАК РегистрСведенийВспомогательный
	|		ПО ДоступныеЗаписи.ПериодЗаписи < РегистрСведенийВспомогательный.Период
	|			И ДоступныеЗаписи.ДействуетДо >= РегистрСведенийВспомогательный.Период
	|			И (&ШаблонУсловияСвязиПоИзмерениям)}
	|ГДЕ
	|	РегистрСведенийВспомогательный.Период ЕСТЬ NULL
	|	И ДоступныеЗаписи.ВозвратнаяЗапись";
			
	ОписаниеЗапроса = ОписаниеЗапросаПоТексту(ШаблонЗапросаВТПредставленияВсеЗаписи);
	ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Добавить(ОписаниеЗапроса);
	ОписаниеЗапроса.ВыбиратьРазрешенные = ТолькоРазрешенные;
	
	ОператорПолученияОсновныхЗаписей = ОписаниеЗапроса.Операторы[0];
	ОператорПолученияВозвратныхЗаписей = ОписаниеЗапроса.Операторы[1];	
	ЗаменитьТаблицуВОператореЗапроса(ОператорПолученияОсновныхЗаписей, "ДоступныеЗаписи", ИмяВТДоступныеЗаписиИмяРегистра);
	ЗаменитьТаблицуВОператореЗапроса(ОператорПолученияВозвратныхЗаписей, "ДоступныеЗаписи", ИмяВТДоступныеЗаписиИмяРегистра);
		
	Если Не ОписаниеРегистра.ЕстьВозвратныеСобытия Тогда
		ОператорПолученияВозвратныхЗаписей = Неопределено;
		ОписаниеЗапроса.Операторы.Удалить(1);
		
		УдалитьКолонкуИзОписаниеЗапроса(ОписаниеЗапроса, "ЭтоВозвратноеСобытие");
		УдалитьКолонкуИзОписаниеЗапроса(ОписаниеЗапроса, "ПериодВозвратногоСобытия");
	Иначе
		ЗаменитьТаблицуВОператореЗапроса(ОператорПолученияВозвратныхЗаписей, "РегистрСведенийВспомогательный", "РегистрСведений." + ИмяРегистра);	
	КонецЕсли;	 		
	
	Для Каждого Измерение Из ОписаниеРегистра.Измерения Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "ДоступныеЗаписи." + Измерение, Измерение);
		
		Если ОператорПолученияВозвратныхЗаписей <> Неопределено 
			И ВРег(Измерение) <> ВРег("ДатаНачала") И ВРег(Измерение) <> ВРег("ДатаОкончания") Тогда
			
			ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 1, "ДоступныеЗаписи." + Измерение, Измерение);
			ТекстУсловияСвязи = "ДоступныеЗаписи." + Измерение + " = РегистрСведенийВспомогательный." + Измерение; 
			ДобавитьУсловиеСоединения(ОператорПолученияВозвратныхЗаписей, "РегистрСведенийВспомогательный", ТекстУсловияСвязи);
		КонецЕсли;	
	КонецЦикла;
		
	Для Каждого Ресурс Из ОписаниеРегистра.Ресурсы Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "ДоступныеЗаписи." + Ресурс, Ресурс);
	КонецЦикла;

	Для Каждого Ресурс Из ОписаниеРегистра.ВозвратныеРесурсы Цикл
		ИмяРесурсаПоОкончании = Ресурс + "ПоОкончании";
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "ДоступныеЗаписи." + Ресурс, Ресурс);
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 1, "ДоступныеЗаписи." + ИмяРесурсаПоОкончании, Ресурс);	
	КонецЦикла;	
		
	Для Каждого Реквизит Из ОписаниеРегистра.Реквизиты Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "ДоступныеЗаписи." + Реквизит, Реквизит);
		Если ОператорПолученияВозвратныхЗаписей <> Неопределено Тогда
			ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 1, "ДоступныеЗаписи." + Реквизит, Реквизит);
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого Реквизит Из ОписаниеРегистра.СтандартныеРеквизиты Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "ДоступныеЗаписи." + Реквизит, Реквизит);
		Если ОператорПолученияВозвратныхЗаписей <> Неопределено Тогда
			ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 1, "ДоступныеЗаписи." + Реквизит, Реквизит);
		КонецЕсли;	
	КонецЦикла;
	
	Если ФормироватьСПериодичностьДень Тогда
		ИмяВТПредставленияВсеЗаписи = "ВТПредставленияВсеЗаписи" + ИмяРегистра;
		ОписаниеЗапроса.ТаблицаДляПомещения = ИмяВТПредставленияВсеЗаписи;
		
		ДобавитьЗапросПолученияЗаписейСПериодичностьюДень(ОписаниеПакетаЗапросов, ОписаниеРегистра, ПараметрыПостроения, ИмяВТПредставленияВсеЗаписи, ИмяСоздаваемойТаблицы);
		ДобавитьЗапросУничтоженияВТ(ОписаниеПакетаЗапросов, ИмяВТПредставленияВсеЗаписи);
	Иначе
		ОписаниеЗапроса.ТаблицаДляПомещения = ИмяСоздаваемойТаблицы;
	КонецЕсли;	
	
	ОписаниеРезультирующегоЗапроса = ОписаниеЗапросаПакетаПоИмениВТ(ОписаниеПакетаЗапросов, ИмяСоздаваемойТаблицы);
	
	Если ВключатьЗаписиНаНачалоПериода Тогда		
		ОператорПолученияСреза = ДобавитьЗапросПолученияЗаписейНаНачалоПериода(ОписаниеПакетаЗапросов, ТолькоРазрешенные, ОписаниеРегистра, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);	
		// добавим условие что бы отсечь записи среза если срез выполнялся на пустую дату
		ВыраженияПоляПериодСреза = ВыражениеПоляПоПсевдониму(ОператорПолученияСреза, "Период");
		ТекстУсловия = ВыраженияПоляПериодСреза + " <> ДАТАВРЕМЯ(1,1,1)";
		ДобавитьУсловие(ОператорПолученияСреза, ТекстУсловия);
	КонецЕсли;
	
	ОписаниеРезультирующегоЗапроса = ОписаниеЗапросаПакетаПоИмениВТ(ОписаниеПакетаЗапросов, ИмяСоздаваемойТаблицы);
	
	ДобавитьПостоянныеПоляВОписаниеЗапроса(ОписаниеПакетаЗапросов, ОписаниеРезультирующегоЗапроса, ПараметрыПостроения.ПостоянныеПоля, ИмяСоздаваемойТаблицы);
	
	УстановитьПсевдонимыПолей(ОписаниеРезультирующегоЗапроса, ПараметрыПостроения);
	
	ДобавитьЗапросУничтоженияВТ(ОписаниеПакетаЗапросов, ИмяВТДоступныеЗаписиИмяРегистра);
	
	ДобавитьПоляИндексированияВОписаниеЗапроса(ОписаниеРезультирующегоЗапроса, ПараметрыПостроения.ИндексироватьПо);	
КонецПроцедуры

Процедура ДобавитьЗапросПолученияЗаписейСПериодичностьюДень(ОписаниеПакетаЗапросов, ОписаниеРегистра, ПараметрыПостроения, ИмяВТПредставленияВсеЗаписи, ИмяВТРезультат = Неопределено) Экспорт
	
	ШаблонЗапросаВТПериодыСреза = 
	"ВЫБРАТЬ
	|	РегистрСведений.Период КАК Период,
	|	МАКСИМУМ(РегистрСведений.ПериодЗаписи) КАК ПериодЗаписи,
	|	&ШаблонИзмерения КАК Измерения
	|ПОМЕСТИТЬ ВТПериодыСреза
	|ИЗ
	|	ВТПредставленияВсеЗаписи КАК РегистрСведений
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрСведений.Период,
	|	&ШаблонИзмерения
	|";
	
	ШаблонЗапросаПолученияЗаписей = 
	"ВЫБРАТЬ
	|	РегистрСведений.Период КАК Период,
	|	РегистрСведений.ПериодЗаписи КАК ПериодЗаписи,
	|	РегистрСведений.ЭтоВозвратноеСобытие КАК ЭтоВозвратноеСобытие,
	|	РегистрСведений.ПериодВозвратногоСобытия КАК ПериодВозвратногоСобытия,
	|	&ШаблонИзмерения КАК Измерения,
	|	&ШаблонРесурсы КАК Ресурсы,
	|	&ШаблонРеквизиты КАК Реквизиты,
	|	&ШаблонСтандартныеРеквизиты КАК СтандартныеРеквизиты
	|ИЗ
	|	ВТПериодыСреза КАК РегистрСведенийСрез
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПредставленияВсеЗаписи КАК РегистрСведений
	|		ПО РегистрСведенийСрез.Период = РегистрСведений.Период
	|			И РегистрСведенийСрез.ПериодЗаписи = РегистрСведений.ПериодЗаписи
	|			И &ШаблонУсловияСвязиПоИзмерениям";
	

	ИмяВТМаксимальныеПериодыПоДням = ИмяВспомогательнойВТПериодыСрезаНаНачалоДня(ОписаниеРегистра.ИмяРегистра, ПараметрыПостроения);
	
	ОписаниеЗапросаВТПериодыСреза = ОписаниеЗапросаПоТексту(ШаблонЗапросаВТПериодыСреза);
	ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Добавить(ОписаниеЗапросаВТПериодыСреза);
	
	ОписаниеЗапросаВТПериодыСреза.ТаблицаДляПомещения = ИмяВТМаксимальныеПериодыПоДням;
	
	ОператорПолученияМаксимальныхДат = ОписаниеЗапросаВТПериодыСреза.Операторы[0];
	ЗаменитьТаблицуВОператореЗапроса(ОператорПолученияМаксимальныхДат, "РегистрСведений", ИмяВТПредставленияВсеЗаписи);
	
	ОписаниеЗапросаВТРезультат = ОписаниеЗапросаПоТексту(ШаблонЗапросаПолученияЗаписей);
	ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Добавить(ОписаниеЗапросаВТРезультат);
	
	ОператорПолученияЗаписей = ОписаниеЗапросаВТРезультат.Операторы[0]; 
	ЗаменитьТаблицуВОператореЗапроса(ОператорПолученияЗаписей, "РегистрСведений", ИмяВТПредставленияВсеЗаписи);
	ЗаменитьТаблицуВОператореЗапроса(ОператорПолученияЗаписей, "РегистрСведенийСрез", ИмяВТМаксимальныеПериодыПоДням);
	
	Если Не ОписаниеРегистра.ЕстьВозвратныеСобытия Тогда		
		УдалитьКолонкуИзОписаниеЗапроса(ОписаниеЗапросаВТРезультат, "ЭтоВозвратноеСобытие");
		УдалитьКолонкуИзОписаниеЗапроса(ОписаниеЗапросаВТРезультат, "ПериодВозвратногоСобытия");	
	КонецЕсли;	 		
	
	ОператорПолученияМаксимальныхДат.Группировка.Добавить("РегистрСведений.Период");
	Для Каждого Измерение Из ОписаниеРегистра.Измерения Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаВТПериодыСреза, 0, "РегистрСведений." + Измерение, Измерение);
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаВТРезультат, 0, "РегистрСведений." + Измерение, Измерение);
		
		ТекстУсловияСвязи = "РегистрСведенийСрез." + Измерение + " = РегистрСведений." + Измерение; 
		ДобавитьУсловиеСоединения(ОператорПолученияЗаписей, "РегистрСведений", ТекстУсловияСвязи);
		
		ДобавитьГруппировку(ОператорПолученияМаксимальныхДат, "РегистрСведений." + Измерение);
	КонецЦикла;
			
	Для Каждого Ресурс Из ОписаниеРегистра.Ресурсы Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаВТРезультат, 0, "РегистрСведений." + Ресурс, Ресурс);
	КонецЦикла;
	
	Для Каждого Ресурс Из ОписаниеРегистра.ВозвратныеРесурсы Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаВТРезультат, 0, "РегистрСведений." + Ресурс, Ресурс);
	КонецЦикла;
		
	Для Каждого Реквизит Из ОписаниеРегистра.Реквизиты Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаВТРезультат, 0, "РегистрСведений." + Реквизит, Реквизит);
	КонецЦикла;
	
	Для Каждого Реквизит Из ОписаниеРегистра.СтандартныеРеквизиты Цикл                      
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаВТРезультат, 0, "РегистрСведений." + Реквизит, Реквизит);
	КонецЦикла;	
	                                                          
	ОписаниеЗапросаВТРезультат.ТаблицаДляПомещения = ИмяВТРезультат;

	ДобавитьЗапросУничтоженияВТ(ОписаниеПакетаЗапросов, ИмяВТМаксимальныеПериодыПоДням);
	
КонецПроцедуры

Функция ДобавитьЗапросПолученияЗаписейНаНачалоПериода(ОписаниеПакетаЗапросов, ТолькоРазрешенные, ОписаниеРегистра, ОписаниеФильтра,  ПараметрыПостроения, ИмяДополняемойВТ) Экспорт
	ОписаниеФильтраСреза = ОписаниеФильтраДляПолученияЗаписейНаНачалоПериода(ОписаниеПакетаЗапросов, ОписаниеФильтра, Ложь);
	
	ИмяСоздаваемойТаблицыСрезПоследних = ИмяСоздаваемойТаблицыСрезПоследнихВПараметрахПостроения(ОписаниеРегистра.ИмяРегистра, ПараметрыПостроения);
	
	ПараметрыПостроенияСреза = ПараметрыПостроенияСрезаПоследнихПоПараметрамПостроения(ПараметрыПостроения, ОписаниеРегистра);
	
	Если ПараметрыПостроения.ОтборыЗаписейНаНачалоПериода <> Неопределено Тогда
		Для Каждого ОписаниеОтбора Из ПараметрыПостроения.ОтборыЗаписейНаНачалоПериода Цикл
			ОтносительныйПуть = Неопределено;
			ОписаниеОтбора.Свойство("ОтносительныйПуть", ОтносительныйПуть);
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроенияСреза.ОтборыПрименяемыеКСрезу, ОписаниеОтбора.ЛевоеЗначение, ОписаниеОтбора.ВидСравнения, ОписаниеОтбора.ПравоеЗначение, ОтносительныйПуть);
		КонецЦикла;
	КонецЕсли;	
	
	ДобавитьЗапросВТИмяРегистраСрез(ОписаниеПакетаЗапросов, ОписаниеРегистра.ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтраСреза, ПараметрыПостроенияСреза, Истина, ИмяСоздаваемойТаблицыСрезПоследних);
		
	ОписаниеЗапросаПолученияЗаписейНаНачалоПериода = ОписаниеЗапросаПакетаПоИмениВТ(ОписаниеПакетаЗапросов, ИмяСоздаваемойТаблицыСрезПоследних);
	ОписаниеЗапросаПолученияДвижений = ОписаниеЗапросаПакетаПоИмениВТ(ОписаниеПакетаЗапросов, ИмяДополняемойВТ);
		
	КопируемыйОператор = ОписаниеЗапросаПолученияЗаписейНаНачалоПериода.Операторы[0];  
	ДобавляемыйОператор = ОбщегоНазначения.СкопироватьРекурсивно(КопируемыйОператор);	
	ОписаниеЗапросаПолученияДвижений.Операторы.Добавить(ДобавляемыйОператор);
	ДобавляемыйОператор.ТипОбъединения = ТипОбъединенияСхемыЗапроса.ОбъединитьВсе;
	
	ДобавляемыйОператор.ВыбираемыеПоля.Очистить();
	ДобавляемыйОператор.ПсевдонимыПолей.Очистить();
	
	Для Каждого Колонка Из ОписаниеЗапросаПолученияДвижений.Колонки Цикл
		ДобавляемыйОператор.ВыбираемыеПоля.Добавить("NULL");
		ДобавляемыйОператор.ПсевдонимыПолей.Добавить(Колонка);
		ИндексКолонкиКопируемогоЗапроса = ОписаниеЗапросаПолученияЗаписейНаНачалоПериода.Колонки.Найти(Колонка);
		Если ИндексКолонкиКопируемогоЗапроса <> Неопределено Тогда
			ДобавляемоеВыражение = КопируемыйОператор.ВыбираемыеПоля[ИндексКолонкиКопируемогоЗапроса];
		Иначе
			ДобавляемоеВыражение = "NULL";	
		КонецЕсли;	
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапросаПолученияДвижений, ОписаниеЗапросаПолученияДвижений.Операторы.Количество() - 1, ДобавляемоеВыражение, Колонка);  	
	КонецЦикла;	
		
	ИндексУдаляемогоЗапроса = ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Найти(ОписаниеЗапросаПолученияЗаписейНаНачалоПериода);
	ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Удалить(ИндексУдаляемогоЗапроса);	
	
	ИспользуемыеТаблицы = Новый Массив;
	ЗаполнитьИспользуемыеВЗапросеТаблицы(ИспользуемыеТаблицы, КопируемыйОператор, ОписаниеПакетаЗапросов);
	
	ИндексРезультирующегоЗапроса = ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Найти(ОписаниеЗапросаПолученияДвижений);
	ИндексДляВставки = ИндексРезультирующегоЗапроса;
	ИндексЗапроса = ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Количество() - 1;
	Пока ИндексЗапроса <> ИндексРезультирующегоЗапроса Цикл
		ОписаниеЗапроса = ОписаниеПакетаЗапросов.ЗапросыПолученияДанных[ИндексЗапроса];
		Если ТипЗнч(ОписаниеЗапроса) = Тип("Структура") 
			И ЗначениеЗаполнено(ОписаниеЗапроса.ТаблицаДляПомещения)
			И ИспользуемыеТаблицы.Найти(ОписаниеЗапроса.ТаблицаДляПомещения) <> Неопределено Тогда 
			
			ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Удалить(ИндексЗапроса);
			ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Вставить(ИндексДляВставки, ОписаниеЗапроса);
			ИндексРезультирующегоЗапроса = ИндексРезультирующегоЗапроса + 1;
		Иначе
			ИндексЗапроса = ИндексЗапроса - 1;	
		КонецЕсли;		
	КонецЦикла;	
		
	Возврат ДобавляемыйОператор;
КонецФункции	

Процедура ДобавитьЗапросВТИмяРегистраСрез(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, СрезПоследних = Истина, ИмяСоздаваемойТаблицы = Неопределено)
	Если СрезПоследних 
		И ДоступенИнтервальныйРегистрСведений(ИмяРегистра) Тогда
	
		Если ИспользоватьПервичныйРегистр(ПараметрыПостроения, ИмяРегистра, Истина) Тогда
			ИнтервальныеРегистрыБЗК.ДобавитьЗапросВТСрезПервичныйРегистр(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);
		Иначе
			ИнтервальныеРегистрыБЗК.ДобавитьЗапросВТИнтервалыСрез(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);
		КонецЕсли; 	
		ПараметрыПостроения.ИсключаемыеРегистраторы = Ложь;
	Иначе
		ДобавитьЗапросВТПериодыСреза(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних);
		ДобавитьЗапросВТТаблицаСреза(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, СрезПоследних, ИмяСоздаваемойТаблицы);
	КонецЕсли; 	
КонецПроцедуры

Процедура ДобавитьЗапросВТПериодыСреза(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, СрезПоследних = Истина)	
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	КонецЕсли;	
	
	ИмяВТПериодыСреза = ИмяВспомогательнойВТПериодыСреза(ИмяРегистра);
	ПостфиксИменПараметров = ИмяВТПериодыСреза;
	
	ОписаниеРегистра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеРегистраСведений(ИмяРегистра, ОписаниеФильтра.ИзмеренияФильтра, ПараметрыПостроения.ИсключатьНеИспользуемыеПоля);
	ФормироватьСПериодичностьДень = ФормироватьСПериодичностьДень(ПараметрыПостроения, ОписаниеРегистра);
	
	ШаблонЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ШаблонЗаданныйПериод КАК ЗаданныйПериод,
	|	&ШаблонИзмерения КАК Измерения,
	|	&ШаблонАгрегированныйПериод КАК Период
	|ПОМЕСТИТЬ ИмяВТРезультат
	|ИЗ
	|	ВТИзмеренияДаты КАК ИзмеренияДаты
	|		{ВНУТРЕННЕЕ СОЕДИНЕНИЕ #РегистрСведений КАК РегистрСведений
	|		ПО (РегистрСведений.Период < &ФильтрПриведенный)
	|			И (&ШаблонУсловиеСвязиИзмерениям)}
	|
	|СГРУППИРОВАТЬ ПО
	|	&ШаблонПериод";
		
	ОписаниеЗапроса = ОписаниеЗапросаПоТексту(ШаблонЗапроса);
	ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Добавить(ОписаниеЗапроса);
	ОписаниеЗапроса.ВыбиратьРазрешенные = ТолькоРазрешенные;
	ОписаниеЗапроса.ТаблицаДляПомещения = ИмяВТПериодыСреза;
	
	ОператорЗапроса = ОписаниеЗапроса.Операторы[0];
	
	ОписаниеИспользованияФильтра = ОписаниеИспользованиеФильтра();
	ИнициализироватьИспользованиеФильтра(ОписаниеИспользованияФильтра, ОписаниеФильтра, ОписаниеРегистра, "Период", ОператорЗапроса, ПостфиксИменПараметров, ПараметрыПостроения.ВсеЗаписи);
	
	ЗаменитьТаблицуВОператореЗапроса(ОператорЗапроса, "РегистрСведений", "РегистрСведений." + ИмяРегистра);
		                                                      
	ОписаниеПоляПериод = ДобавитьОписаниеПоляПериодФильтра(ОписаниеИспользованияФильтра, "Период", "Период");
	ОписаниеПоляПериодПриведенный = ДобавитьОписаниеПоляПериодФильтра(ОписаниеИспользованияФильтра, "Период", "ПериодПриведенный");
	
	ОписаниеПоляПериодПриведенный.ПустоеЗначениеКакМаксимальное = Истина;
	
	Если СрезПоследних Тогда
		АгрегатнаяФункцияКПериоду = "МАКСИМУМ";
		Если ПараметрыПостроения.ВключаяГраницу Тогда
			ОписаниеПоляПериодПриведенный.ВариантПриведенияПериода = "КОНЕЦПЕРИОДА";
			ОператорСравненияПериода = "<=";
		Иначе
			ОписаниеПоляПериодПриведенный.ВариантПриведенияПериода = "НАЧАЛОПЕРИОДА";
			ОператорСравненияПериода = "<";
		КонецЕсли;		
	Иначе
		АгрегатнаяФункцияКПериоду = "МИНИМУМ";
		Если ПараметрыПостроения.ВключаяГраницу Тогда
			ОписаниеПоляПериодПриведенный.ВариантПриведенияПериода = "КОНЕЦПЕРИОДА";	
			ОператорСравненияПериода = ">=";
		Иначе
			ОписаниеПоляПериодПриведенный.ВариантПриведенияПериода = "КОНЕЦПЕРИОДА";
			ОператорСравненияПериода = ">";
		КонецЕсли;	
	КонецЕсли;
	
	Если ФормироватьСПериодичностьДень Тогда
		ОписаниеПоляПериодПриведенный.Кратность = "ДЕНЬ";
	КонецЕсли;	
	
	ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи = СтрЗаменить(ОписаниеИспользованияФильтра.ТекстШаблонаУсловийСвязи, "<", ОператорСравненияПериода);
	УстановитьВыражениеПериодаВТекстШаблонаУсловияСвязи(ОписаниеИспользованияФильтра, "&ФильтрПриведенный", ОписаниеПоляПериодПриведенный);
		
	ВыражениеПоляАгрегированныйПериод = АгрегатнаяФункцияКПериоду + "(РегистрСведений.Период)";
	ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, ВыражениеПоляАгрегированныйПериод, "Период");
	
	ВыражениеПоляЗаданныйПериод = ВыражениеПоляПериод(ОписаниеПоляПериод);
	ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, ВыражениеПоляЗаданныйПериод, "ЗаданныйПериод");
	
	ДобавитьГруппировку(ОператорЗапроса, ВыражениеПоляЗаданныйПериод);
	Для Каждого Измерение Из ОписаниеРегистра.Измерения Цикл
		Если ПараметрыПостроения.ВсеЗаписи И ОписаниеРегистра.ИзмеренияФильтра.Найти(Измерение) <> НеОпределено Тогда
			ИмяПоляИзмерение = ПолеТаблицыФильтра(ОписаниеФильтра, Измерение);
			ВыражениеИзмерения = ВыражениеИзмерениеФильтра(ОписаниеИспользованияФильтра, Измерение);
		Иначе
			ВыражениеИзмерения = "РегистрСведений." + Измерение;
		КонецЕсли;	

		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, ВыражениеИзмерения, Измерение);	
		ДобавитьГруппировку(ОператорЗапроса, ВыражениеИзмерения);
	КонецЦикла;	
		
	УстановитьФильтрВОписаниеПакетаЗапросовКРегистру(ОписаниеПакетаЗапросов, ОписаниеФильтра, ОписаниеИспользованияФильтра, ПараметрыПостроения);	
	УстановитьОтборВОператорЗапросаДанныхРегистра(ОператорЗапроса, ПараметрыПостроения.Отборы, ОписаниеПакетаЗапросов.Параметры, ПостфиксИменПараметров,,,,,Истина);
	ДобавитьДополнительныеПоляПоОписаниюИспользованияФильтра(ОписаниеЗапроса, 0, ОписаниеИспользованияФильтра, Истина);	
КонецПроцедуры

Процедура ДобавитьЗапросВТТаблицаСреза(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, СрезПоследних = Истина, ИмяСоздаваемойТаблицы = Неопределено)
	Если ПараметрыПостроения = Неопределено Тогда
		ПараметрыПостроения = ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	КонецЕсли;	
	
	ПостфиксИменПараметров = ИмяСоздаваемойТаблицы;
	Если ПостфиксИменПараметров = Неопределено Тогда
		ПостфиксИменПараметров = ИмяРегистра + "Срез";
	КонецЕсли;	
	
	ИмяВТПериодыСреза = ИмяВспомогательнойВТПериодыСреза(ИмяРегистра);
	ОписаниеРегистра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеРегистраСведений(ИмяРегистра, ОписаниеФильтра.ИзмеренияФильтра, ПараметрыПостроения.ИсключатьНеИспользуемыеПоля);
	
	ШаблонЗапроса = 
	"ВЫБРАТЬ
	|	МаксимальныеПериоды.ЗаданныйПериод КАК Период,
	|	РегистрСведений.Период КАК ПериодЗаписи,
	|	&ШаблонЭтоВозвратноеСобытие КАК ЭтоВозвратноеСобытие,
	|	РегистрСведений.ДействуетДо КАК ПериодВозвратногоСобытия,
	|	&ШаблонИзмерения КАК Измерения,
	|	&ШаблонРесурсы КАК Ресурсы,
	|	&ШаблонВозвратныеРесурсы КАК ВозвратныеРесурсы,
	|	&ШаблонРеквизиты КАК Реквизиты	
	|ПОМЕСТИТЬ ВТРезультат
	|ИЗ
	|	ВТПериодыСреза КАК МаксимальныеПериоды
	|		{ЛЕВОЕ СОЕДИНЕНИЕ #РегистрСведений КАК РегистрСведений
	|		ПО (РегистрСведений.Период = МаксимальныеПериоды.Период)
	|			И &ШаблонУсловияСвязиПоИзмерениям}";

	
	ШаблонПоляВозвратноеЗначение = "
	|	ВЫБОР
	|		КОГДА РегистрСведений.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ РегистрСведений.ДействуетДо > &ЗаданныйПериод
	|					И &ЗаданныйПериод <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ЗначениеОбычногоРесурса
	|		ИНАЧЕ &ЗначениеВозвратногоРесурса
	|	КОНЕЦ";
	
	ОписаниеПоляЗаданныйПериод = ОписаниеПоляПериода("ЗаданныйПериод", "МаксимальныеПериоды");
		
	ФормироватьСПериодичностьДень = ФормироватьСПериодичностьДень(ПараметрыПостроения, ОписаниеРегистра);
	Если ФормироватьСПериодичностьДень Тогда
		ОписаниеПоляЗаданныйПериод.Кратность = "ДЕНЬ";
		Если ПараметрыПостроения.ВключаяГраницу Тогда
			ОписаниеПоляЗаданныйПериод.ВариантПриведенияПериода = "КОНЕЦПЕРИОДА";
		Иначе
			ОписаниеПоляЗаданныйПериод.ВариантПриведенияПериода = "НАЧАЛОПЕРИОДА";
		КонецЕсли;
	КонецЕсли;
	
	ШаблонПоляВозвратноеЗначение = СтрЗаменить(ШаблонПоляВозвратноеЗначение, "&ЗаданныйПериод", ВыражениеПоляПериод(ОписаниеПоляЗаданныйПериод));
	
	ОписаниеЗапроса = ОписаниеЗапросаПоТексту(ШаблонЗапроса);
	ОписаниеПакетаЗапросов.ЗапросыПолученияДанных.Добавить(ОписаниеЗапроса);
	ОписаниеЗапроса.ВыбиратьРазрешенные = ТолькоРазрешенные;
	ОписаниеЗапроса.ТаблицаДляПомещения = ИмяСоздаваемойТаблицы;
	
	ОператорЗапроса = ОписаниеЗапроса.Операторы[0];
	ЗаменитьТаблицуВОператореЗапроса(ОператорЗапроса, "МаксимальныеПериоды", ИмяВТПериодыСреза);
	
	Если Не ОписаниеРегистра.ЕстьВозвратныеСобытия Тогда
		УдалитьКолонкуИзОписаниеЗапроса(ОписаниеЗапроса, "ПериодВозвратногоСобытия");
	КонецЕсли;	
	
	ЗаменитьТаблицуВОператореЗапроса(ОператорЗапроса, "РегистрСведений", "РегистрСведений." + ИмяРегистра);
	
	Если ОписаниеРегистра.ЕстьВозвратныеСобытия И СрезПоследних Тогда
		ВыражениеЭтоВозвратнаяЗапись = СтрЗаменить(ШаблонПоляВозвратноеЗначение, "&ЗначениеОбычногоРесурса", "ЛОЖЬ");
		ВыражениеЭтоВозвратнаяЗапись = СтрЗаменить(ВыражениеЭтоВозвратнаяЗапись, "&ЗначениеВозвратногоРесурса", "ИСТИНА");
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, ВыражениеЭтоВозвратнаяЗапись, "ЭтоВозвратноеСобытие");
	КонецЕсли;
	
	УсловияСвязиПоИзмерениям = Новый Массив;
	Для Каждого Измерение Из ОписаниеРегистра.Измерения Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "МаксимальныеПериоды." + Измерение, Измерение);
		
		ТекстУсловияСвязи = "МаксимальныеПериоды." + Измерение + " = РегистрСведений." + Измерение; 
		ДобавитьУсловиеСоединения(ОператорЗапроса, "РегистрСведений", ТекстУсловияСвязи);;	
	КонецЦикла;
		
	Для Каждого Ресурс Из ОписаниеРегистра.Ресурсы Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + Ресурс, Ресурс);
	КонецЦикла;

	ВыраженияРесурсов = Новый Соответствие;
	Для Каждого Ресурс Из ОписаниеРегистра.ВозвратныеРесурсы Цикл
		Если СрезПоследних Тогда
			ВыражениеРесурс = СтрЗаменить(ШаблонПоляВозвратноеЗначение, "&ЗначениеОбычногоРесурса", Ресурс);
			ВыражениеРесурс = СтрЗаменить(ВыражениеРесурс, "&ЗначениеВозвратногоРесурса", Ресурс + "ПоОкончании");
			ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, ВыражениеРесурс, Ресурс);
			ВыраженияРесурсов.Вставить(ВРег(Ресурс), ВыражениеРесурс);
		Иначе
			ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + Ресурс, Ресурс);
			ИмяВозвратногоРесурса = Ресурс + "ПоОкончании";
			ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + ИмяВозвратногоРесурса, ИмяВозвратногоРесурса);
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого Реквизит Из ОписаниеРегистра.Реквизиты Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + Реквизит, Реквизит);
	КонецЦикла;
	
	Для Каждого Реквизит Из ОписаниеРегистра.СтандартныеРеквизиты Цикл
		ДобавитьПолеВОписаниеЗапроса(ОписаниеЗапроса, 0, "РегистрСведений." + Реквизит, Реквизит);
	КонецЦикла;
		
	ДобавитьПостоянныеПоляВОписаниеЗапроса(ОписаниеПакетаЗапросов, ОписаниеЗапроса, ПараметрыПостроения.ПостоянныеПоля, ПостфиксИменПараметров);
	
	УстановитьОтборВОператорЗапросаДанныхРегистра(ОператорЗапроса, ПараметрыПостроения.ОтборыПрименяемыеКСрезу, ОписаниеПакетаЗапросов.Параметры, ИмяРегистра + "ОтборыСреза",,,,,Истина);
		
	ДобавитьДополнительныеПоляПоПсевдонимуИсточника(ОписаниеЗапроса, 0, "МаксимальныеПериоды", ОписаниеФильтра.ДополнительныеПоляФильтра);
	
	УстановитьПсевдонимыПолей(ОписаниеЗапроса, ПараметрыПостроения);
	
	ДобавитьПоляИндексированияВОписаниеЗапроса(ОписаниеЗапроса, ПараметрыПостроения.ИндексироватьПо);	
		
	ДобавитьЗапросУничтоженияВТ(ОписаниеПакетаЗапросов, ИмяВТПериодыСреза);		
КонецПроцедуры

Процедура ДобавитьЗапросВТПериодыИмяРегистра(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения = Неопределено, ИмяСоздаваемойТаблицы = Неопределено)	
	Если ДоступенИнтервальныйРегистрСведений(ИмяРегистра) 
		И Не ИспользоватьПервичныйРегистр(ПараметрыПостроения, ИмяРегистра, Ложь, 0) Тогда
		
		ИнтервальныеРегистрыБЗК.ДобавитьЗапросВТПериодыИмяИнтервальногоРегистра(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);
	Иначе
		ИнтервальныеРегистрыБЗК.ДобавитьЗапросВТПериодыИмяПервичногоРегистра(ОписаниеПакетаЗапросов, ИмяРегистра, ТолькоРазрешенные, ОписаниеФильтра, ПараметрыПостроения, ИмяСоздаваемойТаблицы);
	КонецЕсли; 
	ПараметрыПостроения.ИсключаемыеРегистраторы = Ложь;
КонецПроцедуры

#Область ОбработчикиОбновленияИнформационнойБазы

Процедура УстановитьСпособФормированияИнтервальныхРегистров(ПараметрыОбновления = Неопределено) Экспорт
	Константы.СпособФормированияИнтервальныхРегистров.Установить(Перечисления.СпособыФормированияИнтервальныхРегистров.НеПоддерживатьНесколькоВложенныхПериодическихСобытийИнтервальныхРегистров);
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти
