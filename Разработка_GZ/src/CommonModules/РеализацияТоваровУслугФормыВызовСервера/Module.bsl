
#Область ПрограммныйИнтерфейс
	
Функция СчетНаОплатуПокупателюПредставление(СчетНаОплатуПокупателю) Экспорт
	Если НЕ ЗначениеЗаполнено(СчетНаОплатуПокупателю) Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	РеквизитыСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СчетНаОплатуПокупателю, 
		"Дата, Номер");

	Возврат РеализацияТоваровУслугФормы.ПредставлениеСчетаНаОплату(РеквизитыСчета);
КонецФункции

Функция ОстатокТовараПоСчету(Номенклатура, СчетНаОплатуПокупателю, Регистратор) Экспорт
	МассивСчетов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СчетНаОплатуПокупателю);
	
	РеализацииПоСчету = ОбщегоНазначения.ВыгрузитьКолонку(
		Документы.РеализацияТоваровУслуг.РеализацииПоСчетам(МассивСчетов), 
		"Реализация", Истина);
	
	ЭтаРеализация = РеализацииПоСчету.Найти(Регистратор);
	Если ЭтаРеализация <> Неопределено Тогда
		РеализацииПоСчету.Удалить(ЭтаРеализация);
	КонецЕсли;
	
	// Товары и услуги
	ТоварыУслугиКРеализации = Документы.РеализацияТоваровУслуг.ТоварыУслугиКРеализацииПоСчету(
		СчетНаОплатуПокупателю, РеализацииПоСчету);
		
	СтрокаНоменклатура = ТоварыУслугиКРеализации.Найти(Номенклатура, "Номенклатура");
	Если СтрокаНоменклатура <> Неопределено Тогда
		Возврат СтрокаНОменклатура.Количество;
	Иначе
		Возврат 0;
	КонецЕсли; 
КонецФункции

#КонецОбласти 
