
#Область ПрограммныйИнтерфейс

Процедура ЗаменитьСсылкуВУдалитьСоответствияОбъектовИнформационныхБаз(СтарыйОбъект, НовыйОбъект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(НовыйОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СоответствияОбъектовНовые  = РегистрыСведений.УдалитьСоответствияОбъектовИнформационныхБаз.СоздатьНаборЗаписей();
	СоответствияОбъектовНовые.Отбор.УникальныйИдентификаторИсточника.Установить(НовыйОбъект);
	СоответствияОбъектовНовые.Прочитать();
	Если СоответствияОбъектовНовые.Количество() = 0 Тогда
		
		СоответствияОбъектовСтарые = РегистрыСведений.УдалитьСоответствияОбъектовИнформационныхБаз.СоздатьНаборЗаписей();
		СоответствияОбъектовСтарые.Отбор.УникальныйИдентификаторИсточника.Установить(СтарыйОбъект);
		СоответствияОбъектовСтарые.Прочитать();
		Если СоответствияОбъектовСтарые.Количество()>0 Тогда
		
			СоответствияОбъектовНовые.Загрузить(СоответствияОбъектовСтарые.Выгрузить());
			Для Каждого НастройкаУзла Из СоответствияОбъектовНовые Цикл
				НастройкаУзла.УникальныйИдентификаторИсточника = НовыйОбъект;
				НастройкаУзла.УникальныйИдентификаторИсточникаСтрокой = Строка(НовыйОбъект.УникальныйИдентификатор());
				НастройкаУзла.ТипИсточника = Строка(ТипЗнч(НовыйОбъект));
			КонецЦикла;
			
			СоответствияОбъектовСтарые.Очистить();
			СоответствияОбъектовСтарые.Записать();
			СоответствияОбъектовНовые.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьПереносПлановОбменаБСП() Экспорт
	
	// Обработчики не требуется выполнять в разделенном режиме.
	// Эти обработчики предназначены для обновления планов обмена
	// редакции 2.0, в сервисе такое обновление не возможно.
	// Редакция 2.0 не имеет возможности работать в режиме сервиса.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	СоответствияПлановОбмена = Новый Соответствие;
	
	Если Метаданные.ПланыОбмена.Найти("ОбменУправлениеНебольшойФирмойБухгалтерия30") <> Неопределено Тогда
		СоответствияПлановОбмена.Вставить("УдалитьОбменУправлениеНебольшойФирмойБухгалтерия20", 
			"ОбменУправлениеНебольшойФирмойБухгалтерия30");
	КонецЕсли;
	СоответствияПлановОбмена.Вставить("УдалитьОбменРозницаБухгалтерияПредприятия", 
		"ОбменРозницаБухгалтерияПредприятия30");
	Для Каждого СоответствиеПлановОбмена ИЗ СоответствияПлановОбмена Цикл
		ПеренестиУзлыПланаОбменаБСП(СоответствиеПлановОбмена.Ключ, СоответствиеПлановОбмена.Значение);
	КонецЦикла;
	
	ПланыОбменаУниверсальногоФормата = Новый Массив;
	ПланыОбменаУниверсальногоФормата.Добавить("УдалитьОбменУправлениеТорговлейБухгалтерияПредприятияКОРП");
	ПланыОбменаУниверсальногоФормата.Добавить("УдалитьОбменУправлениеТорговлейБухгалтерияПредприятия");
	Для Каждого ИмяПланаОбмена ИЗ ПланыОбменаУниверсальногоФормата Цикл
		ПеренестиУзлыПланаОбменаБСПВУниверсальныйФормат(ИмяПланаОбмена);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьПереносПлановОбменаУниверсальногоОбмена() Экспорт
	
	ПеренестиУзлыПланаОбменаУТ10();
	
КонецПроцедуры

Процедура ПеренестиНастройкиОбменаДанными(СтарыйУзел, НовыйУзел) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтарыйУзел", СтарыйУзел);
	Запрос.УстановитьПараметр("НовыйУзел", НовыйУзел);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УдалитьНастройкиОбменаДанными.*
	|ИЗ
	|	Справочник.УдалитьНастройкиОбменаДанными КАК УдалитьНастройкиОбменаДанными
	|ГДЕ
	|	УдалитьНастройкиОбменаДанными.УзелИнформационнойБазы = &СтарыйУзел
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиТранспортаОбменаДанными.Корреспондент
	|ИЗ
	|	РегистрСведений.НастройкиТранспортаОбменаДанными КАК НастройкиТранспортаОбменаДанными
	|ГДЕ
	|	НастройкиТранспортаОбменаДанными.Корреспондент = &НовыйУзел";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	СтарыйНастройки = РезультатЗапроса[0].Выгрузить();
	НовыеНастройки = РезультатЗапроса[1].Выгрузить();
	Если СтарыйНастройки.Количество() = 0 ИЛИ
		НовыеНастройки.Количество() > 0 Тогда 
		
		Возврат;
		
	КонецЕсли;
	
	Строка = СтарыйНастройки[0];
	МенеджерЗаписи = РегистрыСведений.НастройкиТранспортаОбменаДанными.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Корреспондент = НовыйУзел;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Корреспондент = НовыйУзел;
	
	МенеджерЗаписи.COMАутентификацияОперационнойСистемы = Строка.АутентификацияWindowsИнформационнойБазыДляПодключения;
	МенеджерЗаписи.COMВариантРаботыИнформационнойБазы = ?(Строка.ТипИнформационнойБазыДляПодключения, 0, 1); // Истина - файловая(0), Ложь - клиент-серверная(1) 
	МенеджерЗаписи.COMИмяИнформационнойБазыНаСервере1СПредприятия = Строка.ИмяИнформационнойБазыНаСервереДляПодключения;
	МенеджерЗаписи.COMИмяПользователя = Строка.ПользовательИнформационнойБазыДляПодключения;
	МенеджерЗаписи.COMИмяСервера1СПредприятия = Строка.ИмяСервераИнформационнойБазыДляПодключения;
	МенеджерЗаписи.COMКаталогИнформационнойБазы = Строка.КаталогИнформационнойБазыДляПодключения;
	
	МенеджерЗаписи.EMAILМаксимальныйДопустимыйРазмерСообщения = Строка.МаксимальныйРазмерОтправляемогоПакетаЧерезПочту;
	МенеджерЗаписи.EMAILСжиматьФайлИсходящегоСообщения = Строка.ВыполнятьАрхивациюФайловОбмена;
	МенеджерЗаписи.EMAILУчетнаяЗапись = Строка.УчетнаяЗаписьПриемаОтправкиСообщений;
	
	МенеджерЗаписи.FILEКаталогОбменаИнформацией = Строка.КаталогОбменаИнформацией;
	МенеджерЗаписи.FILEСжиматьФайлИсходящегоСообщения = Строка.ВыполнятьАрхивациюФайловОбмена;
	
	МенеджерЗаписи.FTPСжиматьФайлИсходящегоСообщения = Строка.ВыполнятьАрхивациюФайловОбмена;
	МенеджерЗаписи.FTPСоединениеМаксимальныйДопустимыйРазмерСообщения = Строка.МаксимальныйРазмерОтправляемогоПолучаемогоПакетаЧерезFTP;
	
	МенеджерЗаписи.FTPСоединениеПассивноеСоединение = Строка.ПассивноеFTPСоединение;
	МенеджерЗаписи.FTPСоединениеПользователь = Строка.ПользовательFTPСоединения;
	МенеджерЗаписи.FTPСоединениеПорт = Строка.ПортFTPСоединения;
	МенеджерЗаписи.FTPСоединениеПуть = Строка.FTPАдресОбмена;
	
	Если ЗначениеЗаполнено(Строка.УчетнаяЗаписьПриемаОтправкиСообщений) Тогда
		МенеджерЗаписи.ВидТранспортаСообщенийОбменаПоУмолчанию = Перечисления.ВидыТранспортаСообщенийОбмена.EMAIL;
	ИначеЕсли Строка.ТипНастройки = Перечисления.УдалитьТипыАвтоматическогоОбменаДанными.ОбменЧерезComСоединение Тогда
		МенеджерЗаписи.ВидТранспортаСообщенийОбменаПоУмолчанию = Перечисления.ВидыТранспортаСообщенийОбмена.COM;
	ИначеЕсли Строка.ТипНастройки = Перечисления.УдалитьТипыАвтоматическогоОбменаДанными.ОбменЧерезFTPРесурс Тогда
		МенеджерЗаписи.ВидТранспортаСообщенийОбменаПоУмолчанию = Перечисления.ВидыТранспортаСообщенийОбмена.FTP;
	ИначеЕсли Строка.ТипНастройки = Перечисления.УдалитьТипыАвтоматическогоОбменаДанными.ОбменЧерезФайловыйРесурс Тогда
		МенеджерЗаписи.ВидТранспортаСообщенийОбменаПоУмолчанию = Перечисления.ВидыТранспортаСообщенийОбмена.FILE;
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НовыйУзел, Строка.ПарольИнформационнойБазыДляПодключения, "COMПарольПользователя");
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НовыйУзел, Строка.ПарольFTPСоединения, "FTPСоединениеПароль");
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НовыйУзел, Строка.ПарольНаОтправку, "ПарольАрхиваСообщенияОбмен");
	
	ТекстПравилПриемника = Строка.ПравилаОбмена.Получить();
	Если ТипЗнч(ТекстПравилПриемника) = Тип("Строка") И СтрДлина(ТекстПравилПриемника) < 1000 
		И НовыйУзел <> ПланыОбмена.ГлавныйУзел() Тогда 
		// одностронний обмен
		НовыйУзелОбъект = НовыйУзел.ПолучитьОбъект();
		НовыйУзелОбъект.РежимВыгрузкиОбъектов = Перечисления.РежимыВыгрузкиОбъектовОбмена.НеВыгружать;
		НовыйУзелОбъект.Записать();
	КонецЕсли;
	
	НастройкаОбмена = Строка.Ссылка.ПолучитьОбъект();
	НастройкаОбмена.УстановитьПометкуУдаления(Истина);
	
	// Перенос сценариев синхронизации данных
	ПеренестиНастройкиВыполненияОбменаДанными(Строка.Ссылка, НовыйУзел, МенеджерЗаписи.ВидТранспортаСообщенийОбменаПоУмолчанию);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПеренестиУзлыПланаОбменаУТ10()
	
	ПланОбменаБП20 = "УдалитьОбменУправлениеТорговлейБухгалтерияКОРП";
	ПланОбменаБП30 = "ОбменУправлениеТорговлей103БухгалтерияПредприятия30";
	
	ИспользоватьСинхронизациюДанных = Ложь;
	
	Запрос = Новый Запрос();
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПланОбменаБП20.Ссылка КАК УзелПланаОбменаБП20,
	|	ПланОбменаБП30.Ссылка КАК УзелПланаОбменаБП30,
	|	ПланОбменаБП20.ВыгрузкаДокументовЗаказПокупателяСчетНаОплатуПокупателю КАК ВыгрузкаДокументов
	|ИЗ
	|	ПланОбмена.[ПланОбменаБП20] КАК ПланОбменаБП20
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланОбмена.[ПланОбменаБП30] КАК ПланОбменаБП30
	|		ПО ПланОбменаБП20.Код = ПланОбменаБП30.Код
	|ГДЕ
	|	ПланОбменаБП20.ПометкаУдаления = ЛОЖЬ И ПланОбменаБП20.Код <> """"";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ПланОбменаБП20]", ПланОбменаБП20);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ПланОбменаБП30]", ПланОбменаБП30);
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Если ЗначениеЗаполнено(Выборка.УзелПланаОбменаБП30)
			И ((Выборка.УзелПланаОбменаБП20 = ПланыОбмена[ПланОбменаБП20].ЭтотУзел()) = (Выборка.УзелПланаОбменаБП30 = ПланыОбмена[ПланОбменаБП30].ЭтотУзел())) Тогда
			
			НовыйУзел = Выборка.УзелПланаОбменаБП30;
			
		Иначе
			
			НовыйУзел = ПолучитьОбъектНовогоУзла(Выборка.УзелПланаОбменаБП20, ПланОбменаБП20, ПланОбменаБП30);
			НовыйУзел.ИспользоватьОтборПоОрганизациям = (НовыйУзел.Организации.Количество() > 0);
			НовыйУзел.РегистрироватьИзменения = НовыйУзел.Ссылка = ПланыОбмена[ПланОбменаБП30].ЭтотУзел();
			
			Если Выборка.ВыгрузкаДокументов = Перечисления.УдалитьВыгрузкаДокументовЗаказПокупателяСчетНаОплатуПокупателю.НеВыгружать Тогда
				НовыйУзел.ВыгрузкаДокументовЗаказПокупателяСчетНаОплатуПокупателю = Перечисления.ВыгрузкаДокументовЗаказПокупателяСчетНаОплатуПокупателю.НеВыгружать;
			ИначеЕсли Выборка.ВыгрузкаДокументов = Перечисления.УдалитьВыгрузкаДокументовЗаказПокупателяСчетНаОплатуПокупателю.СчетНаОплатуПокупателю Тогда
				НовыйУзел.ВыгрузкаДокументовЗаказПокупателяСчетНаОплатуПокупателю = Перечисления.ВыгрузкаДокументовЗаказПокупателяСчетНаОплатуПокупателю.СчетНаОплатуПокупателю;
			ИначеЕсли Выборка.ВыгрузкаДокументов = Перечисления.УдалитьВыгрузкаДокументовЗаказПокупателяСчетНаОплатуПокупателю.ЗаказПокупателя Тогда
				НовыйУзел.ВыгрузкаДокументовЗаказПокупателяСчетНаОплатуПокупателю = Перечисления.ВыгрузкаДокументовЗаказПокупателяСчетНаОплатуПокупателю.ЗаказПокупателя;
			КонецЕсли;	
			
			// по умолчанию считаем, что обмен - двухсторонний, при переносе настроек обмена данными проверим направленность обмена
			НовыйУзел.РежимВыгрузкиОбъектов = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию;
			
			Если Не НовыйУзел.Ссылка = ПланыОбмена.ОбменУправлениеТорговлей103БухгалтерияПредприятия30.ЭтотУзел() Тогда
				НовыйУзел.СкладДляОбменаДаннымиСУТ = Константы.УдалитьСкладДляОбменаДаннымиСУТ.Получить();
			КонецЕсли;
			
			НовыйУзел.Записать();
			НовыйУзел = НовыйУзел.Ссылка;
			
		КонецЕсли;
		
		Если Выборка.УзелПланаОбменаБП20 = ПланыОбмена[ПланОбменаБП20].ЭтотУзел() Тогда
			ЗафиксироватьТранзакцию();
			Продолжить;
		КонецЕсли;
		
		ПеренестиРегистрацииИзмененийДляУзла(Выборка.УзелПланаОбменаБП20, НовыйУзел);
		
		ЗапросФоновогоУзла = Новый Запрос();
		ЗапросФоновогоУзла.Параметры.Вставить("УзелИнформационнойБазы", Выборка.УзелПланаОбменаБП20);
		ЗапросФоновогоУзла.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	УдалитьНастройкиОбменаДанными.УзелФоновогоОбмена
		|ИЗ
		|	Справочник.УдалитьНастройкиОбменаДанными КАК УдалитьНастройкиОбменаДанными
		|ГДЕ
		|	УдалитьНастройкиОбменаДанными.УзелИнформационнойБазы = &УзелИнформационнойБазы";
		ВыборкаФоновогоУзла = ЗапросФоновогоУзла.Выполнить().Выбрать();
		Если ВыборкаФоновогоУзла.Следующий() Тогда
			Если ЗначениеЗаполнено(ВыборкаФоновогоУзла.УзелФоновогоОбмена) Тогда
				ПеренестиРегистрацииИзмененийДляУзла(ВыборкаФоновогоУзла.УзелФоновогоОбмена, НовыйУзел);
			КонецЕсли;
		КонецЕсли;
		
		ПеренестиНастройкиОбменаДанными(Выборка.УзелПланаОбменаБП20, НовыйУзел);
		
		ИспользоватьСинхронизациюДанных = Истина;
		
		ПланыОбмена.УдалитьРегистрациюИзменений(Выборка.УзелПланаОбменаБП20);
		
		Объект = Выборка.УзелПланаОбменаБП20.ПолучитьОбъект();
		Объект.УстановитьПометкуУдаления(Истина);
		
		ЗафиксироватьТранзакцию();
		
	КонецЦикла;
	
	// установить константу синхронизации данных в значение Истина только в случае обработки узлов плана обмена БП30
	Если ИспользоватьСинхронизациюДанных Тогда
		Константы.ИспользоватьСинхронизациюДанных.Установить(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиУзлыПланаОбменаБСП(ПланОбменаБП20, ПланОбменаБП30)
	
	Если Метаданные.ПланыОбмена.Найти(ПланОбменаБП20) = Неопределено
		ИЛИ Метаданные.ПланыОбмена.Найти(ПланОбменаБП30) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПланОбменаБП20.Ссылка КАК УзелПланаОбменаБП20,
	|	ПланОбменаБП30.Ссылка КАК УзелПланаОбменаБП30
	|ИЗ
	|	ПланОбмена.[ПланОбменаБП20] КАК ПланОбменаБП20
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланОбмена.[ПланОбменаБП30] КАК ПланОбменаБП30
	|		ПО ПланОбменаБП20.Код = ПланОбменаБП30.Код
	|ГДЕ
	|	ПланОбменаБП20.ПометкаУдаления = ЛОЖЬ
	|	И ПланОбменаБП20.Код <> """"";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ПланОбменаБП20]", ПланОбменаБП20);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ПланОбменаБП30]", ПланОбменаБП30);
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Если НЕ ЗначениеЗаполнено(Выборка.УзелПланаОбменаБП30) Тогда
			НовыйУзел = СоздатьНовыйУзелПланаОбмена(Выборка.УзелПланаОбменаБП20, ПланОбменаБП20, ПланОбменаБП30);
		Иначе
			НовыйУзел = Выборка.УзелПланаОбменаБП30;
		КонецЕсли;
		
		Если Выборка.УзелПланаОбменаБП20 = ПланыОбмена[ПланОбменаБП20].ЭтотУзел() Тогда
			ЗафиксироватьТранзакцию();
			Продолжить;
		КонецЕсли;
		
		ПеренестиРегистрацииИзмененийДляУзла(Выборка.УзелПланаОбменаБП20, НовыйУзел);
		
		ПеренестиНастройкиТранпортаСообщенийОбмена(Выборка.УзелПланаОбменаБП20, НовыйУзел);
		
		ПеренестиСоответствияОбъектовИнформационныхБаз(Выборка.УзелПланаОбменаБП20, НовыйУзел);
		
		ПеренестиСостоянияОбменовИнформационныхБаз(Выборка.УзелПланаОбменаБП20, НовыйУзел);
		
		Объект = Выборка.УзелПланаОбменаБП20.ПолучитьОбъект();
		Объект.УстановитьПометкуУдаления(Истина);
		ПланыОбмена.УдалитьРегистрациюИзменений(Выборка.УзелПланаОбменаБП20);
		
		ЗафиксироватьТранзакцию();
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьОбъектНовогоУзла(СтарыйУзел, ИмяСтарогоПланаОбмена, ИмяНовогоПланаОбмена)
	
	Если СтарыйУзел = ПланыОбмена[ИмяСтарогоПланаОбмена].ЭтотУзел() Тогда
		НовыйУзел = ПланыОбмена[ИмяНовогоПланаОбмена].ЭтотУзел().ПолучитьОбъект();
		ИсключаяСвойства = "НомерОтправленного, НомерПринятого, ПометкаУдаления";
	Иначе
		НовыйУзел = ПланыОбмена[ИмяНовогоПланаОбмена].СоздатьУзел();
		ИсключаяСвойства = "ПометкаУдаления";
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НовыйУзел, СтарыйУзел, , ИсключаяСвойства);
	Для Каждого ТабличнаяЧасть ИЗ Метаданные.ПланыОбмена[ИмяСтарогоПланаОбмена].ТабличныеЧасти Цикл
		Если Метаданные.ПланыОбмена[ИмяНовогоПланаОбмена].ТабличныеЧасти.Найти(ТабличнаяЧасть.Имя) <> Неопределено Тогда
			Для Каждого СтрокаСтарогоУзла Из СтарыйУзел[ТабличнаяЧасть.Имя] Цикл
				СтрокаНовогоУзла = НовыйУзел[ТабличнаяЧасть.Имя].Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаНовогоУзла, СтрокаСтарогоУзла);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НовыйУзел;
	
КонецФункции

Функция СоздатьНовыйУзелПланаОбмена(СтарыйУзел, ИмяСтарогоПланаОбмена, ИмяНовогоПланаОбмена)
	
	НовыйУзел = ПолучитьОбъектНовогоУзла(СтарыйУзел, ИмяСтарогоПланаОбмена, ИмяНовогоПланаОбмена);
	НовыйУзел.ОбменДанными.Загрузка = Истина;
	НовыйУзел.Записать();
	
	Возврат НовыйУзел.Ссылка;
	
КонецФункции

Процедура ПеренестиРегистрацииИзмененийДляУзла(СтарыйУзел, НовыйУзел)
	
	СоставНовогоПланаОбмена = НовыйУзел.Метаданные().Состав;
	НомерСообщения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтарыйУзел, "НомерОтправленного");
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(СтарыйУзел, НомерСообщения);
	Пока ВыборкаИзменений.Следующий() Цикл
		Объект = ВыборкаИзменений.Получить();
		
		Если Объект = Неопределено Тогда
			Продолжить;
		ИначеЕсли ТипЗнч(Объект) = Тип("УдалениеОбъекта") Тогда
			Если НЕ СоставНовогоПланаОбмена.Содержит(Объект.Ссылка.Метаданные()) Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли НЕ СоставНовогоПланаОбмена.Содержит(Объект.Метаданные()) Тогда
			Продолжить;
		КонецЕсли;
		
		ПланыОбмена.ЗарегистрироватьИзменения(НовыйУзел, Объект);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПеренестиНастройкиТранпортаСообщенийОбмена(СтарыйУзел, НовыйУзел)
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("УзелИнформационнойБазы", СтарыйУзел);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СценарииОбменовДаннымиНастройкиОбмена.Ссылка
	|ИЗ
	|	Справочник.СценарииОбменовДанными.НастройкиОбмена КАК СценарииОбменовДаннымиНастройкиОбмена
	|ГДЕ
	|	СценарииОбменовДаннымиНастройкиОбмена.УзелИнформационнойБазы = &УзелИнформационнойБазы";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СценарийОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Для Каждого СтрокаСценария Из СценарийОбъект.НастройкиОбмена Цикл
			Если СтрокаСценария.УзелИнформационнойБазы = СтарыйУзел Тогда
				СтрокаСценария.УзелИнформационнойБазы = НовыйУзел;
			КонецЕсли;
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СценарийОбъект);
	КонецЦикла;
	
	НастройкиТранспортаОбменаНовогоУзла = РегистрыСведений.НастройкиТранспортаОбменаДанными.СоздатьНаборЗаписей();
	НастройкиТранспортаОбменаНовогоУзла.Отбор.Корреспондент.Установить(НовыйУзел);
	НастройкиТранспортаОбменаНовогоУзла.Прочитать();
	Если НастройкиТранспортаОбменаНовогоУзла.Количество()=0 Тогда
		НастройкиТранспортаОбменаСтарогоУзла = РегистрыСведений.УдалитьНастройкиТранспортаОбмена.СоздатьНаборЗаписей();
		НастройкиТранспортаОбменаСтарогоУзла.Отбор.УзелИнформационнойБазы.Установить(СтарыйУзел);
		НастройкиТранспортаОбменаСтарогоУзла.Прочитать();
		
		Если НастройкиТранспортаОбменаСтарогоУзла.Количество()>0 Тогда
			НастройкиТранспортаОбменаНовогоУзла.Загрузить(НастройкиТранспортаОбменаСтарогоУзла.Выгрузить());
			Для Каждого НастройкаУзла Из НастройкиТранспортаОбменаНовогоУзла Цикл
				НастройкаУзла.Корреспондент = НовыйУзел;
			КонецЦикла;
			
			НастройкиТранспортаОбменаСтарогоУзла.Очистить();
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НастройкиТранспортаОбменаСтарогоУзла);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НастройкиТранспортаОбменаНовогоУзла);
		КонецЕсли;
	КонецЕсли;
	
	НастройкиУзловИнформационныхБазНовогоУзла = РегистрыСведений.ОбщиеНастройкиУзловИнформационныхБаз.СоздатьНаборЗаписей();
	НастройкиУзловИнформационныхБазНовогоУзла.Отбор.УзелИнформационнойБазы.Установить(НовыйУзел);
	НастройкиУзловИнформационныхБазНовогоУзла.Прочитать();
	Если НастройкиУзловИнформационныхБазНовогоУзла.Количество()=0 Тогда
		НастройкиУзловИнформационныхБазСтарогоУзла = РегистрыСведений.ОбщиеНастройкиУзловИнформационныхБаз.СоздатьНаборЗаписей();
		НастройкиУзловИнформационныхБазСтарогоУзла.Отбор.УзелИнформационнойБазы.Установить(СтарыйУзел);
		НастройкиУзловИнформационныхБазСтарогоУзла.Прочитать();
		
		Если НастройкиУзловИнформационныхБазСтарогоУзла.Количество()>0 Тогда
			НастройкиУзловИнформационныхБазНовогоУзла.Загрузить(НастройкиУзловИнформационныхБазСтарогоУзла.Выгрузить());
			Для Каждого НастройкаУзла Из НастройкиУзловИнформационныхБазНовогоУзла Цикл
				НастройкаУзла.УзелИнформационнойБазы = НовыйУзел;
			КонецЦикла;
			
			НастройкиУзловИнформационныхБазСтарогоУзла.Очистить();
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НастройкиУзловИнформационныхБазСтарогоУзла);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НастройкиУзловИнформационныхБазНовогоУзла);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиСоответствияОбъектовИнформационныхБаз(СтарыйУзел, НовыйУзел)
	
	СоответствияНовогоУзла = РегистрыСведений.СоответствияОбъектовИнформационныхБаз.СоздатьНаборЗаписей();
	СоответствияНовогоУзла.Отбор.УзелИнформационнойБазы.Установить(НовыйУзел);
	СоответствияНовогоУзла.Прочитать();
	Если СоответствияНовогоУзла.Количество()=0 Тогда
		СоответствияСтарогоУзла = РегистрыСведений.УдалитьСоответствияОбъектовИнформационныхБаз.СоздатьНаборЗаписей();
		СоответствияСтарогоУзла.Отбор.УзелИнформационнойБазы.Установить(СтарыйУзел);
		СоответствияСтарогоУзла.Прочитать();
		Если СоответствияСтарогоУзла.Количество()>0 Тогда
			СоответствияНовогоУзла.Загрузить(СоответствияСтарогоУзла.Выгрузить());
			СоответствияНовогоУзла.ОбменДанными.Загрузка = Истина;
			Для Каждого НастройкаУзла Из СоответствияНовогоУзла Цикл
				НастройкаУзла.УзелИнформационнойБазы = НовыйУзел;
				Если ЗначениеЗаполнено(НастройкаУзла.УникальныйИдентификаторИсточника) Тогда
					Если НЕ ЗначениеЗаполнено(НастройкаУзла.УникальныйИдентификаторПриемника) Тогда
						НастройкаУзла.ОбъектВыгруженПоСсылке = Истина;
					ИначеЕсли СОКРЛП(НастройкаУзла.УникальныйИдентификаторИсточникаСтрокой) = ""
							И НЕ НастройкаУзла.ОбъектВыгруженПоСсылке Тогда
						НастройкаУзла.УникальныйИдентификаторИсточникаСтрокой = Строка(НастройкаУзла.УникальныйИдентификаторИсточника.УникальныйИдентификатор());
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			СоответствияСтарогоУзла.Очистить();
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(СоответствияСтарогоУзла);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(СоответствияНовогоУзла);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиСостоянияОбменовИнформационныхБаз(СтарыйУзел, НовыйУзел)
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("УзелИнформационнойБазы", СтарыйУзел);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РезультатыОбменаДанными.ПроблемныйОбъект,
	|	РезультатыОбменаДанными.ТипПроблемы
	|ИЗ
	|	РегистрСведений.РезультатыОбменаДанными КАК РезультатыОбменаДанными
	|ГДЕ
	|	РезультатыОбменаДанными.УзелИнформационнойБазы = &УзелИнформационнойБазы";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.РезультатыОбменаДанными.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Прочитать();
		МенеджерЗаписи.УзелИнформационнойБазы = НовыйУзел;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
	СостоянияОбменовДаннымиНовогоУзла = РегистрыСведений.СостоянияОбменовДанными.СоздатьНаборЗаписей();
	СостоянияОбменовДаннымиНовогоУзла.Отбор.УзелИнформационнойБазы.Установить(НовыйУзел);
	СостоянияОбменовДаннымиНовогоУзла.Прочитать();
	Если СостоянияОбменовДаннымиНовогоУзла.Количество()=0 Тогда
		СостоянияОбменовДаннымиСтарогоУзла = РегистрыСведений.СостоянияОбменовДанными.СоздатьНаборЗаписей();
		СостоянияОбменовДаннымиСтарогоУзла.Отбор.УзелИнформационнойБазы.Установить(СтарыйУзел);
		СостоянияОбменовДаннымиСтарогоУзла.Прочитать();
		Если СостоянияОбменовДаннымиСтарогоУзла.Количество()>0 Тогда
			СостоянияОбменовДаннымиНовогоУзла.Загрузить(СостоянияОбменовДаннымиСтарогоУзла.Выгрузить());
			СостоянияОбменовДаннымиНовогоУзла.ОбменДанными.Загрузка = Истина;
			Для Каждого НастройкаУзла Из СостоянияОбменовДаннымиНовогоУзла Цикл
				НастройкаУзла.УзелИнформационнойБазы = НовыйУзел;
			КонецЦикла;
			
			СостоянияОбменовДаннымиСтарогоУзла.Очистить();
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(СостоянияОбменовДаннымиСтарогоУзла);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(СостоянияОбменовДаннымиНовогоУзла);
		КонецЕсли;
	КонецЕсли;
	
	СостоянияОбменовДаннымиНовогоУзла = РегистрыСведений.СостоянияУспешныхОбменовДанными.СоздатьНаборЗаписей();
	СостоянияОбменовДаннымиНовогоУзла.Отбор.УзелИнформационнойБазы.Установить(НовыйУзел);
	СостоянияОбменовДаннымиНовогоУзла.Прочитать();
	Если СостоянияОбменовДаннымиНовогоУзла.Количество()=0 Тогда
		СостоянияОбменовДаннымиСтарогоУзла = РегистрыСведений.СостоянияУспешныхОбменовДанными.СоздатьНаборЗаписей();
		СостоянияОбменовДаннымиСтарогоУзла.Отбор.УзелИнформационнойБазы.Установить(СтарыйУзел);
		СостоянияОбменовДаннымиСтарогоУзла.Прочитать();
		Если СостоянияОбменовДаннымиСтарогоУзла.Количество()>0 Тогда
			СостоянияОбменовДаннымиНовогоУзла.Загрузить(СостоянияОбменовДаннымиСтарогоУзла.Выгрузить());
			СостоянияОбменовДаннымиНовогоУзла.ОбменДанными.Загрузка = Истина;
			Для Каждого НастройкаУзла Из СостоянияОбменовДаннымиНовогоУзла Цикл
				НастройкаУзла.УзелИнформационнойБазы = НовыйУзел;
			КонецЦикла;
			
			СостоянияОбменовДаннымиСтарогоУзла.Очистить();
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(СостоянияОбменовДаннымиСтарогоУзла);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(СостоянияОбменовДаннымиНовогоУзла);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиНастройкиВыполненияОбменаДанными(СтараяНастройкаОбмена, НовыйУзел, ВидТранспортаСообщенийОбмена)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтараяНастройкаОбмена", СтараяНастройкаОбмена);
	Запрос.УстановитьПараметр("НовыйУзел", 			   НовыйУзел);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиОбмена.Ссылка,
	|	НастройкиОбмена.НастройкаОбмена,
	|	НастройкиОбмена.ВыполняемоеДействие,
	|	НастройкиОбмена.Ссылка.Наименование,
	|	НастройкиОбмена.Ссылка.ИспользоватьРегламентныеЗадания,
	|	НастройкиОбмена.Ссылка.РегламентноеЗадание,
	|	НастройкиОбмена.Ссылка.КоличествоЭлементовВТранзакцииНаЗагрузкуДанных,
	|	НастройкиОбмена.Ссылка.КоличествоЭлементовВТранзакцииНаВыгрузкуДанных,
	|	НастройкиОбмена.Ссылка.Комментарий
	|ИЗ
	|	Справочник.УдалитьНастройкиВыполненияОбмена.НастройкиОбмена КАК НастройкиОбмена
	|ГДЕ
	|	НастройкиОбмена.НастройкаОбмена = &СтараяНастройкаОбмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СценарииОбменовДаннымиНастройкиОбмена.Ссылка,
	|	СценарииОбменовДаннымиНастройкиОбмена.УзелИнформационнойБазы
	|ИЗ
	|	Справочник.СценарииОбменовДанными.НастройкиОбмена КАК СценарииОбменовДаннымиНастройкиОбмена
	|ГДЕ
	|	СценарииОбменовДаннымиНастройкиОбмена.УзелИнформационнойБазы = &НовыйУзел";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	СтарыеНастройкиВыполненияОбмена  	= РезультатЗапроса[0].Выгрузить();
	НовыеНастройкиСинхронизацииДанных 	= РезультатЗапроса[1].Выгрузить();
	
	Если СтарыеНастройкиВыполненияОбмена.Количество() = 0 
		ИЛИ НовыеНастройкиСинхронизацииДанных.Количество() > 0 Тогда 
		Возврат;
	КонецЕсли;
	
	СтарыеНастройки = СтарыеНастройкиВыполненияОбмена[0];
	
	НовыйСценарийОбменаДанными = Справочники.СценарииОбменовДанными.СоздатьЭлемент();
	НовыйСценарийОбменаДанными.Наименование 					= СтарыеНастройки.Наименование;
	НовыйСценарийОбменаДанными.ИспользоватьРегламентноеЗадание 	= СтарыеНастройки.ИспользоватьРегламентныеЗадания;
	НовыйСценарийОбменаДанными.Комментарий 					  	= СтарыеНастройки.Комментарий;
	
	Для Каждого СтрокаСтарыхНастроекОбмена ИЗ СтарыеНастройкиВыполненияОбмена Цикл
		
		Если СтрокаСтарыхНастроекОбмена.ВыполняемоеДействие = Перечисления.УдалитьДействиеПриОбмене.ОтложенныеДвижения 
			ИЛИ НЕ ЗначениеЗаполнено(СтрокаСтарыхНастроекОбмена.ВыполняемоеДействие) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяНастройкаОбмена = НовыйСценарийОбменаДанными.НастройкиОбмена.Добавить();
		
		НоваяНастройкаОбмена.УзелИнформационнойБазы = НовыйУзел;  
		НоваяНастройкаОбмена.ВидТранспортаОбмена    = ВидТранспортаСообщенийОбмена;
		
		Если СтрокаСтарыхНастроекОбмена.ВыполняемоеДействие = Перечисления.УдалитьДействиеПриОбмене.ЗагрузкаДанных Тогда
			НоваяНастройкаОбмена.ВыполняемоеДействие 			= Перечисления.ДействияПриОбмене.ЗагрузкаДанных;
		ИначеЕсли СтрокаСтарыхНастроекОбмена.ВыполняемоеДействие = Перечисления.УдалитьДействиеПриОбмене.ВыгрузкаДанных Тогда
			НоваяНастройкаОбмена.ВыполняемоеДействие 			= Перечисления.ДействияПриОбмене.ВыгрузкаДанных;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НовыйСценарийОбменаДанными);
	
	НастройкаВыполненияОбмена = СтарыеНастройки.Ссылка.ПолучитьОбъект();
	НастройкаВыполненияОбмена.УстановитьПометкуУдаления(Истина);
	
КонецПроцедуры

Процедура ПеренестиУзлыПланаОбменаБСПВУниверсальныйФормат(ПланОбменаБП20)
	
	Если Метаданные.ПланыОбмена.Найти(ПланОбменаБП20) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПланОбменаБП20.Ссылка КАК УзелПланаОбменаБП20,
	|	ПланОбменаБП30.Ссылка КАК УзелПланаОбменаБП30
	|ИЗ
	|	ПланОбмена.[ПланОбменаБП20] КАК ПланОбменаБП20
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК ПланОбменаБП30
	|		ПО ПланОбменаБП20.Код = ПланОбменаБП30.Код
	|ГДЕ
	|	ПланОбменаБП20.ПометкаУдаления = ЛОЖЬ
	|	И ПланОбменаБП20.Код <> """"";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ПланОбменаБП20]", ПланОбменаБП20);
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Если НЕ ЗначениеЗаполнено(Выборка.УзелПланаОбменаБП30) Тогда
			НовыйУзел = СоздатьНовыйУзелПланаОбменаЧерезУниверсальныйФормат(Выборка.УзелПланаОбменаБП20, ПланОбменаБП20);
		Иначе
			НовыйУзел = Выборка.УзелПланаОбменаБП30;
		КонецЕсли;
		
		Если Выборка.УзелПланаОбменаБП20 = ПланыОбмена[ПланОбменаБП20].ЭтотУзел() Тогда
			ЗафиксироватьТранзакцию();
			Продолжить;
		КонецЕсли;
		
		ПеренестиРегистрацииИзмененийДляУзла(Выборка.УзелПланаОбменаБП20, НовыйУзел);
		
		ПеренестиНастройкиТранпортаСообщенийОбмена(Выборка.УзелПланаОбменаБП20, НовыйУзел);
		
		ПеренестиПубличныеИдентификаторыСсылок(Выборка.УзелПланаОбменаБП20, НовыйУзел);
		
		Объект = Выборка.УзелПланаОбменаБП20.ПолучитьОбъект();
		Объект.УстановитьПометкуУдаления(Истина);
		ПланыОбмена.УдалитьРегистрациюИзменений(Выборка.УзелПланаОбменаБП20);
		
		ЗафиксироватьТранзакцию();
		
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьНовыйУзелПланаОбменаЧерезУниверсальныйФормат(СтарыйУзел, ИмяСтарогоПланаОбмена)
	
	НовыйУзел = ПолучитьОбъектНовогоУзлаОбменаЧерезУниверсальныйФормат(СтарыйУзел, ИмяСтарогоПланаОбмена);
	НовыйУзел.ОбменДанными.Загрузка = Истина;
	НовыйУзел.Записать();
	
	Возврат НовыйУзел.Ссылка;
	
КонецФункции

Функция ПолучитьОбъектНовогоУзлаОбменаЧерезУниверсальныйФормат(СтарыйУзел, ИмяСтарогоПланаОбмена)
	
	Если СтарыйУзел = ПланыОбмена[ИмяСтарогоПланаОбмена].ЭтотУзел() Тогда
		НовыйУзел = ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.ЭтотУзел().ПолучитьОбъект();
		ИсключаяСвойства = "НомерОтправленного, НомерПринятого, ПометкаУдаления";
	Иначе
		НовыйУзел = ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.СоздатьУзел();
		НовыйУзел.Заполнить(Неопределено);
		
		ИсключаяСвойства = "ПометкаУдаления";
		
		ИдентификаторНастройки = ИдентификаторНастройкиДляПланаОбмена(ИмяСтарогоПланаОбмена);
		НовыйУзел.ВариантНастройки = ИдентификаторНастройки;
		НовыйУзел.Заполнить(Неопределено);
		Если НовыйУзел.ВерсияФорматаОбмена = "1.2" Тогда
			// Для обменов с типовыми конфигурациями версия формата 1.2 не используется.
			// Эта версия оставлена для совместимости.
			НовыйУзел.ВерсияФорматаОбмена = "1.3";
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НовыйУзел, СтарыйУзел, , ИсключаяСвойства);
	Для Каждого ТабличнаяЧасть ИЗ Метаданные.ПланыОбмена[ИмяСтарогоПланаОбмена].ТабличныеЧасти Цикл
		Если Метаданные.ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.ТабличныеЧасти.Найти(ТабличнаяЧасть.Имя) <> Неопределено Тогда
			Для Каждого СтрокаСтарогоУзла Из СтарыйУзел[ТабличнаяЧасть.Имя] Цикл
				СтрокаНовогоУзла = НовыйУзел[ТабличнаяЧасть.Имя].Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаНовогоУзла, СтрокаСтарогоУзла);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НовыйУзел;
	
КонецФункции

Функция ИдентификаторНастройкиДляПланаОбмена(ИмяСтарогоПланаОбмена)
	
	Если ИмяСтарогоПланаОбмена = "УдалитьОбменУправлениеТорговлейБухгалтерияПредприятияКОРП"
		ИЛИ ИмяСтарогоПланаОбмена = "УдалитьОбменУправлениеТорговлейБухгалтерияПредприятия" Тогда
		Возврат "ОбменУТ11";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Процедура ПеренестиПубличныеИдентификаторыСсылок(СтарыйУзел, НовыйУзел)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствияОбъектовИнформационныхБаз.УникальныйИдентификаторИсточника КАК Ссылка
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовИнформационныхБаз КАК СоответствияОбъектовИнформационныхБаз
	|ГДЕ
	|	СоответствияОбъектовИнформационныхБаз.УзелИнформационнойБазы = &НастройкаСинхронизации");
	
	Запрос.УстановитьПараметр("НастройкаСинхронизации", СтарыйУзел);
	
	НаборЗаписейДанныеОбъектов = РегистрыСведений.ДанныеОбъектовДляРегистрацииВОбменах.СоздатьНаборЗаписей();
	НаборЗаписейДанныеОбъектов.Отбор.УзелИнформационнойБазы.Установить(НовыйУзел);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяЗаписьДанныеОбъектов = НаборЗаписейДанныеОбъектов.Добавить();
		НоваяЗаписьДанныеОбъектов.Ссылка = Выборка.Ссылка;
		НоваяЗаписьДанныеОбъектов.УзелИнформационнойБазы = НовыйУзел;
	КонецЦикла;
	
	НаборЗаписейДанныеОбъектов.Записать();
	
	// ПубличныеИдентификаторыСинхронизируемыхОбъектов - заполняются только на стороне БП,
	// так как их надо заполнить только для одного из корреспондентов.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СоответствияОбъектовИнформационныхБаз.УникальныйИдентификаторИсточника КАК УИДИсточника,
	|	КОЛИЧЕСТВО(СоответствияОбъектовИнформационныхБаз.УникальныйИдентификаторПриемника) КАК КоличествоСсылок
	|ПОМЕСТИТЬ СоответствияОбъектовСОтбором
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовИнформационныхБаз КАК СоответствияОбъектовИнформационныхБаз
	|ГДЕ
	|	СоответствияОбъектовИнформационныхБаз.УзелИнформационнойБазы = &НастройкаСинхронизации
	|	И СоответствияОбъектовИнформационныхБаз.УникальныйИдентификаторПриемника <> (ВЫРАЗИТЬ("""" КАК СТРОКА(36)))
	|
	|СГРУППИРОВАТЬ ПО
	|	СоответствияОбъектовИнформационныхБаз.УникальныйИдентификаторИсточника
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствияОбъектовСОтбором.УИДИсточника КАК УникальныйИдентификаторИсточника,
	|	СоответствияОбъектовИБ.УникальныйИдентификаторПриемника КАК УникальныйИдентификаторПриемника,
	|	СоответствияОбъектовИБ.ТипПриемника КАК ТипПриемника
	|ИЗ
	|	СоответствияОбъектовСОтбором КАК СоответствияОбъектовСОтбором
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияОбъектовИнформационныхБаз КАК СоответствияОбъектовИБ
	|		ПО (СоответствияОбъектовИБ.УникальныйИдентификаторИсточника = СоответствияОбъектовСОтбором.УИДИсточника)
	|			И (СоответствияОбъектовИБ.УзелИнформационнойБазы = &НастройкаСинхронизации)
	|			И (СоответствияОбъектовИБ.УникальныйИдентификаторПриемника <> (ВЫРАЗИТЬ("""" КАК СТРОКА(36))))
	|ГДЕ
	|	СоответствияОбъектовСОтбором.КоличествоСсылок > 1
	|	И СоответствияОбъектовИБ.УникальныйИдентификаторПриемника <> (ВЫРАЗИТЬ("""" КАК СТРОКА(36)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствияОбъектовСОтбором.УИДИсточника КАК Ссылка,
	|	СоответствияОбъектовИБ.УникальныйИдентификаторПриемника КАК Идентификатор
	|ИЗ
	|	СоответствияОбъектовСОтбором КАК СоответствияОбъектовСОтбором
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияОбъектовИнформационныхБаз КАК СоответствияОбъектовИБ
	|		ПО (СоответствияОбъектовИБ.УникальныйИдентификаторИсточника = СоответствияОбъектовСОтбором.УИДИсточника)
	|			И (СоответствияОбъектовИБ.УзелИнформационнойБазы = &НастройкаСинхронизации)
	|			И (СоответствияОбъектовИБ.УникальныйИдентификаторПриемника <> (ВЫРАЗИТЬ("""" КАК СТРОКА(36))))
	|ГДЕ
	|	СоответствияОбъектовСОтбором.КоличествоСсылок = 1");
	
	Запрос.УстановитьПараметр("НастройкаСинхронизации", СтарыйУзел);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ИмяСобытияЖурналРегистрации = НСтр("ru = 'Обновление планов обмена.Заполнение публичных идентификаторов'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	// Отображение ссылок "один ко многим"
	Если Не РезультатЗапроса[1].Пустой() Тогда
		
		Сообщение = НСтр("ru = 'Обнаружены записи, в которых одной ссылке на источник соответствуют несколько приемников'") 
					+ Символы.ПС + НСтр("ru = 'Эти записи обработаны не будут.'");
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналРегистрации, УровеньЖурналаРегистрации.Ошибка, , , Сообщение);
		
		Выборка = РезультатЗапроса[1].Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НСтрока = НСтр("ru = 'Уникальный идентификатор источника: %1, Уникальный идентификатор приемника: %2, Тип приемника: %3'");
			Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтрока, 
				Выборка.УникальныйИдентификаторИсточника, 
				Выборка.УникальныйИдентификаторПриемника, 
				Выборка.ТипПриемника);
			
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурналРегистрации, УровеньЖурналаРегистрации.Ошибка, , , Сообщение);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Формирование записей в регистр Публичные идентификаторы
	СчетчикЗаписей = 0;
	Выборка = РезультатЗапроса[2].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		// Проверка равенства идентификаторов
		ИдентификаторИсточникаСтрокой = СокрЛП(Выборка.Ссылка.УникальныйИдентификатор());
		
		Если ИдентификаторИсточникаСтрокой = СокрЛП(Выборка.Идентификатор) Тогда
			Продолжить;
		КонецЕсли;
		
		СчетчикЗаписей = СчетчикЗаписей + 1;
		СтруктураЗаписи = Новый Структура("УзелИнформационнойБазы, Ссылка, Идентификатор", 
			НовыйУзел, Выборка.Ссылка, Выборка.Идентификатор);
			
		ОбменДаннымиСлужебный.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи,
			"ПубличныеИдентификаторыСинхронизируемыхОбъектов", Ложь);
		
	КонецЦикла;
	
	// Очистка регистра СоответствиеОбъектовИнформационныхБаз
	НаборЗаписейСоответствиеОбъектов = РегистрыСведений.СоответствияОбъектовИнформационныхБаз.СоздатьНаборЗаписей();
	НаборЗаписейСоответствиеОбъектов.Отбор.УзелИнформационнойБазы.Установить(СтарыйУзел);
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписейСоответствиеОбъектов);
	
КонецПроцедуры

#КонецОбласти