#Область СлужебныеПроцедурыИФункции

Функция РаспознанныйТекстСодержитПустоеЗначениеПоля(ИмяПоля, Знач РаспознанныйТекст) Экспорт
	
	Если ПустаяСтрока(РаспознанныйТекст) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Реальных примеров, когда Грузополучатель = "он же" или прочерк, не найдено, но в целях
	// унификации и дополнительной проверки сделано по аналогии с полем Грузоотправитель
	Если ИмяПоля <> "Грузоотправитель"
		И ИмяПоля <> "Грузополучатель"
		И ИмяПоля <> "СтранаПроисхождения"
		И ИмяПоля <> "НомерГТД"
		Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	РаспознанныйТекст = НРег(СокрЛП(РаспознанныйТекст));
	
	Если РаспознанныйТекст = "он же" И (ИмяПоля = "Грузоотправитель" ИЛИ ИмяПоля = "Грузополучатель") Тогда
		Возврат Истина;
	Иначе
		// Исключения для вариантов написания "- -"
		РаспознанныйТекст = СтрЗаменить(РаспознанныйТекст, "—", ""); // код 8212
		РаспознанныйТекст = СтрЗаменить(РаспознанныйТекст, "–", ""); // код 8211
		РаспознанныйТекст = СтрЗаменить(РаспознанныйТекст, "-", ""); // код 45
		РаспознанныйТекст = СтрЗаменить(РаспознанныйТекст, "=", ""); // код 61
		РаспознанныйТекст = СтрЗаменить(РаспознанныйТекст, "_", ""); // код 95
		
		Возврат ПустаяСтрока(РаспознанныйТекст);
	КонецЕсли;
	
КонецФункции

Функция ЭтоПримитивныйТип(Знач ПроверяемыйТип) Экспорт
	
	Возврат ПроверяемыйТип = Тип("Строка")
	Или ПроверяемыйТип = Тип("Дата") 
	Или ПроверяемыйТип = Тип("Число") 
	Или ПроверяемыйТип = Тип("Булево");
	
КонецФункции

Функция ВсеКоординатыНулевые(Координаты) Экспорт
	
	ТипКоординат = ТипЗнч(Координаты);
	Если ТипКоординат = Тип("ДанныеФормыЭлементКоллекции") Тогда
		Результат = (Координаты.КоординатаX0 = 0 И Координаты.КоординатаX1 = 0
			И Координаты.КоординатаY0 = 0 И Координаты.КоординатаY1 = 0);
	ИначеЕсли ТипКоординат = Тип("Массив") Тогда
		Результат = (Координаты[0] = 0 И Координаты[1] = 0 И Координаты[2] = 0 И Координаты[3] = 0);
	Иначе
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти