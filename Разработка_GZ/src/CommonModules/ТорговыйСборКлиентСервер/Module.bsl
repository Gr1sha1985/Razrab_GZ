
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает параметры видимости элементов форм Справочники.ТорговыеТочки.Формы.ФормаЭлемента и
// РегистрыСведений.ПараметрыТорговыхТочек.Формы.ФормаЗаписи, в соответствии с выбранным типом
// торговой точки.
//
// Параметры:
//  ТипТорговойТочки - ПеречислениеСсылка.ТипыТорговыхТочек - тип торговой точки.
//
// Возвращаемое значение:
//  Структура - конструктор структуры НовыеПараметрыВидимостиРеквизитов().
//    * Ключ - Строка - Имя элемента формы,
//    * Значение - Булево - Признак видимости элемента.
//
Функция ПараметрыВидимостиРеквизитовПоТипуТорговойТочки(ТипТорговойТочки) Экспорт
	
	Параметры = НовыеПараметрыВидимостиРеквизитов();
	
	Если ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.Магазин") Тогда
		Параметры.ГруппаПлощадьТорговогоЗала  = Истина;
		Параметры.НадписьПлощадьТорговогоЗала = Истина;
		Параметры.ГруппаКадастровыйНомер      = Истина;
		Параметры.НадписьКадастровыйНомер     = Истина;
		Параметры.ВидОбъектаНедвижимости      = Истина;
		Параметры.ГруппаНалоговыеЛьготы       = Истина;
		
	ИначеЕсли ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.Павильон") Тогда
		Параметры.ГруппаПлощадьТорговогоЗала       = Истина;
		Параметры.НадписьПлощадьТорговогоЗала      = Истина;
		Параметры.ГруппаКадастровыйНомер           = Истина;
		Параметры.НадписьКадастровыйНомерПомещения = Истина;
		Параметры.ГруппаНалоговыеЛьготы            = Истина;
		
	ИначеЕсли ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.Киоск")
		ИЛИ ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.ТорговаяПалатка") Тогда
		Параметры.НомерРазрешения       = Истина;
		Параметры.ГруппаНалоговыеЛьготы = Истина;
		
	ИначеЕсли ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.ТорговыйАвтомат") Тогда
		Параметры.НомерРазрешения = Истина;
		
	ИначеЕсли ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.АвтолавкаАвтоприцеп")
		ИЛИ ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.ТорговаяТележка")
		ИЛИ ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.ТорговыйЛоток") Тогда
		Параметры.НомерРазрешения       = Истина;
		Параметры.ГруппаНалоговыеЛьготы = Истина;
		
	ИначеЕсли ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.ПрочееСТорговымЗалом") Тогда
		Параметры.ГруппаПлощадьТорговогоЗала  = Истина;
		Параметры.НадписьПлощадьТорговогоЗала = Истина;
		Параметры.ГруппаКадастровыйНомер      = Истина;
		Параметры.НадписьКадастровыйНомер     = Истина;
		Параметры.ВидОбъектаНедвижимости      = Истина;
		Параметры.ГруппаНалоговыеЛьготы       = Истина;
		
	ИначеЕсли ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.ПрочееБезТорговогоЗала") Тогда
		Параметры.НомерРазрешения                        = Истина;
		Параметры.ГруппаПостановкаНаУчетВНалоговомОргане = Истина;
		Параметры.ГруппаНалоговыеЛьготы                  = Истина;
		
	ИначеЕсли ТипТорговойТочки = ПредопределенноеЗначение("Перечисление.ТипыТорговыхТочек.РозничныйРынок") Тогда
		Параметры.ГруппаПлощадьТорговогоЗала               = Истина;
		Параметры.НадписьПлощадьРынка                      = Истина;
		Параметры.ГруппаКадастровыйНомер                   = Истина;
		Параметры.НадписьКадастровыйНомерЗемельногоУчастка = Истина;
		
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыеПараметрыВидимостиРеквизитов()
	
	Параметры = Новый Структура;
	Параметры.Вставить("ГруппаПлощадьТорговогоЗала"  , Ложь);
	Параметры.Вставить("НадписьПлощадьТорговогоЗала" , Ложь);
	Параметры.Вставить("НадписьПлощадьРынка"         , Ложь);
	
	Параметры.Вставить("ГруппаКадастровыйНомер"                   , Ложь);
	Параметры.Вставить("НадписьКадастровыйНомер"                  , Ложь);
	Параметры.Вставить("НадписьКадастровыйНомерПомещения"         , Ложь);
	Параметры.Вставить("НадписьКадастровыйНомерЗемельногоУчастка" , Ложь);
	Параметры.Вставить("ВидОбъектаНедвижимости"                   , Ложь);
	
	Параметры.Вставить("НомерРазрешения"                        , Ложь);
	Параметры.Вставить("ГруппаПостановкаНаУчетВНалоговомОргане" , Истина);
	Параметры.Вставить("ГруппаНалоговыеЛьготы"                  , Ложь);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти