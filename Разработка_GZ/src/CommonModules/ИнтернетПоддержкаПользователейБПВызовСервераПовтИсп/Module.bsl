
#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработкаНовостей

// Функция переопределяет список лент новостей, по которым надо получать контекстные новости
// По-умолчанию (значение Неопределено) контекстные новости "собираются" по всем лентам новостей
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   СписокЗначений, Массив, Неопределено - список (массив) лент новостей, по которым надо получать контекстные новости,
//         или Неопределено (по-умолчанию), если получать надо по всем лентам новостей
//
Функция ПолучитьСписокЛентНовостейДляКонтекстныхНовостей() Экспорт
	
	Результат = Новый СписокЗначений;
	Если СервисЭлектронныхТрудовыхКнижек.ИспользуетсяСервисЭлектронныхТрудовыхКнижек() Тогда
		// В этом режиме доступна единственная лента
		Результат.Добавить(ОбработкаНовостейПовтИсп.ПолучитьЛентуНовостейПоКоду("WhatIsNew"));
		Результат.Добавить(ОбработкаНовостейПовтИсп.ПолучитьЛентуНовостейПоКоду("urgent"));
	Иначе
		
		// Для БП принято считать, что отключая ленту новостей в списке новостей,
		//  пользователь отключает эту ленту также и для контекстных новостей
		МассивДоступныхЛентНовостей = ХранилищаНастроек.НастройкиНовостей.Загрузить("ДоступныеЛентыНовостей");
		Если ТипЗнч(МассивДоступныхЛентНовостей) = Тип("Массив") Тогда
			
			Результат.ЗагрузитьЗначения(МассивДоступныхЛентНовостей);
			
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращает, можно ли показывать новости в текущем режиме командного интерфейса.
// 
Функция РежимИнтерфейсаПользователяРазрешаетРаботуСНовостями() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных()
		И (ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком()) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецОбласти