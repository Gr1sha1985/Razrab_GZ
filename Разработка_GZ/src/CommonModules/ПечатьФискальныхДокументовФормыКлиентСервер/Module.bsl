#Область ПрограммныйИнтерфейс

Процедура ПоказатьПредупреждениеПечатьЧека(Форма, КомандаПечатиЧека = "НапечататьЧек") Экспорт
	Если НЕ Форма.ЕстьФискальныеОперацииПоДокументу Тогда
		Возврат;
	КонецЕсли; 
	
	Элементы = Форма.Элементы;
	
	КомандаПечатиЧека = Элементы.Найти(КомандаПечатиЧека);
	Если КомандаПечатиЧека <> Неопределено Тогда
		
		// Баннер может быть активирован ранее с другим текстом
		ГруппаПредупреждениеОПечатиЧека = Элементы.Найти("ПредупреждениеОПечатиЧека");
		Если ГруппаПредупреждениеОПечатиЧека <> Неопределено 
			И НЕ ГруппаПредупреждениеОПечатиЧека.Видимость Тогда
			
			// Гасим команду в командной панели, так как теперь она отражается в баннере
			КомандаПечатиЧека.Видимость = Ложь;
			
			ШаблонСтроки = НСтр("ru = 'Документ был изменен с момента пробития <a href=''ПоказатьКассовыйЧек''>кассового чека</a>'");
			
			#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
			 	ТекстПредупреждения = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки);
			#Иначе
			 	ТекстПредупреждения = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ШаблонСтроки);
			#КонецЕсли
			
			ГруппаПредупреждениеОПечатиЧека.Видимость = Истина;
			Элементы.ТекстПредупрежденияОПечатиЧека.Заголовок = ТекстПредупреждения;
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

Процедура ПодготовитьФормуНаСервере(Форма, ИмяКоманды = "НапечататьЧек") Экспорт
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ЕстьФискальныеОперацииПоДокументу") Тогда
		МассивРеквизитов = Новый Массив;
		МассивРеквизитов.Добавить(Новый РеквизитФормы("ЕстьФискальныеОперацииПоДокументу", Новый ОписаниеТипов("Булево")));
		Форма.ИзменитьРеквизиты(МассивРеквизитов);
	КонецЕсли; 

	Форма.ЕстьФискальныеОперацииПоДокументу = ЗначениеЗаполнено(Объект.Ссылка) 
		И МенеджерОборудованияВызовСервера.ДанныеФискальнойОперации(Объект.Ссылка) <> Неопределено;
	
	// Если баннер уже активирован (например из формы списка фискаальных операций), то не показываем его повторно.
	ГруппаПредупреждениеОПечатиЧека = Элементы.Найти("ПредупреждениеОПечатиЧека");
	Если ГруппаПредупреждениеОПечатиЧека <> Неопределено Тогда
		БаннерПечатьЧекаАктивен     = ГруппаПредупреждениеОПечатиЧека.Видимость;
		Если НЕ БаннерПечатьЧекаАктивен 
			И Форма.Параметры.Свойство("ПоказатьБаннерЧекКоррекции") Тогда
			
			БаннерПечатьЧекаАктивен = Истина;
			ГруппаПредупреждениеОПечатиЧека.Видимость = Истина;
			Элементы.ТекстПредупрежденияОПечатиЧека.Заголовок = НСтр("ru = 'Измените документ и напечатайте чек'");
		КонецЕсли; 
	Иначе
		БаннерПечатьЧекаАктивен     = Ложь;
	КонецЕсли; 
	
	// Если показываем баннер, то не показываем кнопку печати чека отдельно (так как она есть в составе баннера).
	ЭлементПечатьЧека           = Элементы.Найти(ИмяКоманды);
	Если ЭлементПечатьЧека <> Неопределено Тогда
		ЭлементПечатьЧека.Видимость = НЕ БаннерПечатьЧекаАктивен;
		
		Если ЭлементПечатьЧека.Видимость Тогда
			ЭлементПечатьЧека.Заголовок = ?(Форма.ЕстьФискальныеОперацииПоДокументу, НСтр("ru = 'Чек коррекции'"), НСтр("ru = 'Чек'"));
		КонецЕсли; 
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 
