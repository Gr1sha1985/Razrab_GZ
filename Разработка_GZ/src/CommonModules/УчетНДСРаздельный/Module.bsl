////////////////////////////////////////////////////////////////////////////////
// Подсистема "Раздельный учет НДС".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ФормированиеДвиженийДокументов

#Область АвансовыйОтчет

Процедура СформироватьДвиженияПоступлениеТоваровУслугОтПодотчетногоЛица(ТаблицаТовары, ТаблицаУслуги, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьРеквизитыПоступлениеТоваровУслугОтПодотчетногоЛица(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ПодготовитьПараметрыПоступлениеТоваровУслугОтПодотчетногоЛица(Параметры, ТаблицаТовары, ТаблицаУслуги);
	
	УчетНДС.ЗаполнитьВидыЦенностейПоступлениеОтПоставщика(Параметры.Товары, Реквизиты, Неопределено, "СчетУчета");
	УчетНДС.ЗаполнитьВидыЦенностейПоступлениеОтПоставщика(Параметры.Услуги, Реквизиты, Неопределено, "СчетЗатрат");

	ДанныеДвижений = ПодготовитьДанныеДвиженийПоступлениеТоваровУслугОтПодотчетногоЛица(
		Параметры.Товары, Параметры.Услуги, Реквизиты);
	
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
		
КонецПроцедуры

Функция ПодготовитьРеквизитыПоступлениеТоваровУслугОтПодотчетногоЛица(ТаблицаРеквизиты)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                    // <ДокументСсылка.АвансовыйОтчет>
	+ "Период,"                         // <Дата>
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "Подразделение,"                  // <Ссылка на справочник подразделений>
	+ "Склад,"                          // <СправочникСсылка.Склады>
	+ "УчетАгентскогоНДС,"              // <Булево>
	+ "ЭлектронныеУслуги,"              // <Булево>
	+ "ФизЛицо,"                        // <СправочникСсылка.ФизическиеЛица>
	+ "ВалютаДокумента"                 // <СправочникСсылка.Валюты>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;	
		
КонецФункции

Процедура ПодготовитьПараметрыПоступлениеТоваровУслугОтПодотчетногоЛица(Параметры, ТаблицаТовары, ТаблицаУслуги)

	// Подготовка таблицы Товары

	СписокОбязательныхКолонок = ""
	+ "ПредъявленСчетФактура,"   // <Булево> - признак, что на момент поступления товаров счет-фактура предъявлен поставщиком
	+ "СчетФактура,"             // <ДокументСсылка.СчетФактураПолученный>
	+ "Контрагент,"              // <СправочникСсылка.Контрагенты>
	+ "Номенклатура,"            // <СправочникСсылка.Номенклатура>
	+ "Содержание,"              // <Строка,150> - содержание для проводок
	+ "СуммаРуб,"                // <Число,15,2> - сумма в рублях
	+ "СуммаБезНДСРуб,"          // <Число,15,2> - сумма без НДС в рублях
	+ "СуммаНДСРуб,"             // <Число,15,2> - сумма НДС в рублях
	+ "СчетУчета,"               // <ПланСчетовСсылка.Хозрасчетный>
	+ "СчетУчетаНДС,"            // <ПланСчетовСсылка.Хозрасчетный>
	+ "СпособУчетаНДС,"          // <ПеречислениеСсылка.СпособыУчетаНДС>
	+ "СуммаВзаиморасчетов,"     // <Число,15,2> - сумма в валюте расчетов с поставщиком
	+ "СуммаНДСВзаиморасчетов,"  // <Число,15,2> - сумма НДС в валюте расчетов с поставщиком
	+ "СтавкаНДС,"               // <ПеречислениеСсылка.СтавкиНДС>
	+ "Количество"               // <Число,15,3>
	;
	Параметры.Вставить("Товары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
	Реквизиты = Параметры.Реквизиты[0];
	// Документом партии при поступлении от подотчетного лица выступает авансовый отчет
	Параметры.Товары.Колонки.Добавить("Партия", Документы.ТипВсеСсылки());
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Регистратор, "Партия");
	// Склад берем Из шапки авансового отчета
	Параметры.Товары.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Склад, "Склад");
	Параметры.Товары.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Подразделение, "Подразделение");
	
	// Подготовка таблицы Услуги

	СписокОбязательныхКолонок = ""
	+ "ПредъявленСчетФактура,"   // <Булево> - признак, что на момент поступления товаров счет-фактура предъявлен поставщиком
	+ "СчетФактура,"             // <ДокументСсылка.СчетФактураПолученный>
	+ "Контрагент,"              // <СправочникСсылка.Контрагенты>
	+ "Содержание,"              // <Строка,150> - содержание для проводок
	+ "СуммаРуб,"                // <Число,15,2> - сумма в рублях
	+ "СуммаБезНДСРуб,"          // <Число,15,2> - сумма без НДС в рублях
	+ "СуммаНДСРуб,"             // <Число,15,2> - сумма НДС в рублях
	+ "СчетЗатрат,"              // <ПланСчетовСсылка.Хозрасчетный>
	+ "Субконто1,"               // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Субконто2,"               // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Субконто3,"               // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "СтатьяЗатрат,"            // <СправочникСсылка.СтатьиЗатрат>
	+ "ПодразделениеЗатрат,"     // <Ссылка на справочник подразделений>
	+ "СчетУчетаНДС,"            // <ПланСчетовСсылка.Хозрасчетный>
	+ "СпособУчетаНДС,"          // <ПеречислениеСсылка.СпособыУчетаНДС>	
	+ "СуммаВзаиморасчетов,"     // <Число,15,2> - сумма в валюте расчетов с поставщиком
	+ "СуммаНДСВзаиморасчетов,"  // <Число,15,2> - сумма НДС в валюте расчетов с поставщиком
	+ "СтавкаНДС"                // <ПеречислениеСсылка.СтавкиНДС>
	;
	Параметры.Вставить("Услуги", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаУслуги, СписокОбязательныхКолонок));
		
	Параметры.Услуги.Колонки.Добавить("Партия", Документы.ТипВсеСсылки());
	Параметры.Услуги.ЗаполнитьЗначения(Реквизиты.Регистратор, "Партия");
    Параметры.Услуги.Колонки.ПодразделениеЗатрат.Имя = "Подразделение";
	
КонецПроцедуры

Функция ПодготовитьДанныеДвиженийПоступлениеТоваровУслугОтПодотчетногоЛица(Товары, Услуги, Реквизиты)

	ПартионныйУчет = УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период) = Перечисления.СпособыОценки.ФИФО;
	
	ДанныеДвижений = Товары.Скопировать(,
		"СчетУчета,Подразделение,Номенклатура,Склад,Партия,Контрагент,ПредъявленСчетФактура,
		|СчетФактура,ВидЦенности,СчетУчетаНДС,СтавкаНДС,СпособУчетаНДС,
		|Количество,СуммаБезНДСРуб,СуммаНДСРуб");
	ДанныеДвижений.Колонки.СчетУчета.Имя = "СчетЗатрат";

	ДанныеДвижений.Колонки.Добавить("Субконто1");
	ДанныеДвижений.Колонки.Добавить("Субконто2");
	ДанныеДвижений.Колонки.Добавить("Субконто3");
	
	Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл

		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура
			 ИЛИ СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства Тогда
				СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(СтрокаТаблицы.СчетЗатрат) Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Склад;
				КонецЕсли;
	        ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
				Если ПартионныйУчет Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Партия;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если НЕ СвойстваСчета.УчетПоПодразделениям Тогда
			СтрокаТаблицы.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
		КонецЕсли;
		
	КонецЦикла;

	ДанныеДвижений.Колонки.Удалить("Номенклатура");
	ДанныеДвижений.Колонки.Удалить("Склад");
	
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(Услуги, ДанныеДвижений);
	
	ДанныеДвижений.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Регистратор, "Регистратор");
	
	ДанныеДвижений.Колонки.Добавить("Период",      Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Период, "Период,ДатаСобытия");

	ДанныеДвижений.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");

	ДанныеДвижений.Колонки.Контрагент.Имя = "Поставщик";
	ДанныеДвижений.Колонки.Добавить("ДоговорКонтрагента", // колонка договор всегда пустая
		Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ДанныеДвижений.Колонки.Добавить("ЕстьНДСПредъявленный", Новый ОписаниеТипов("Булево"));		

	ДанныеДвижений.Колонки.СпособУчетаНДС.Имя = "НовыйСпособУчетаНДС";
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДСИзменился",
		Новый ОписаниеТипов("Булево"));
	ДанныеДвижений.ЗаполнитьЗначения(Истина, "СпособУчетаНДСИзменился");
		
	ДанныеДвижений.Колонки.СчетФактура.Имя = "СчетФактураДокумент";	
	ДанныеДвижений.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
		
	Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл
		Если СтрокаТаблицы.ПредъявленСчетФактура Тогда
			СтрокаТаблицы.СчетФактура = СтрокаТаблицы.СчетФактураДокумент;
			СтрокаТаблицы.ЕстьНДСПредъявленный = Истина;
		Иначе	
			СтрокаТаблицы.СчетФактура = Реквизиты.Регистратор;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеДвижений.Колонки.СуммаНДСРуб.Имя = "НДС";
	ДанныеДвижений.Колонки.СуммаБезНДСРуб.Имя = "СуммаБезНДС";
	
	ДанныеДвижений.Колонки.Добавить("ИсправленныйСчетФактура",
		Новый ОписаниеТипов("ДокументСсылка.КорректировкаПоступления"));
		
	Возврат ДанныеДвижений;

КонецФункции

Процедура СформироватьДвиженияИспользованиеБилетов(ТаблицаБилеты, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьРеквизитыПоступлениеТоваровУслугОтПодотчетногоЛица(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ПодготовитьПараметрыИспользованиеБилетов(Параметры, ТаблицаБилеты);
	
	ДанныеДвижений = ПодготовитьДанныеДвиженийИспользованиеБилетов(
		Параметры.Билеты, Реквизиты);
	
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
		
КонецПроцедуры

Процедура ПодготовитьПараметрыИспользованиеБилетов(Параметры, ТаблицаБилеты)

	// Подготовка таблицы Билеты

	СписокОбязательныхКолонок = ""
	+ "СчетФактура,"             // <ДокументСсылка.СчетФактураПолученный>
	+ "Поставщик,"               // <СправочникСсылка.Контрагенты>
	+ "СуммаБезНДСРуб,"          // <Число,15,2> - сумма без НДС в рублях
	+ "СуммаНДСРуб,"             // <Число,15,2> - сумма НДС в рублях
	+ "СчетЗатрат,"     	     // <ПланСчетовСсылка.Хозрасчетный>
	+ "Субконто1,"     	         // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Субконто2,"     	         // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Субконто3,"     	         // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "СпособУчетаНДС,"     	 // <ПеречислениеСсылка.СпособыУчетаНДС>
	+ "СтавкаНДС"                // <ПеречислениеСсылка.СтавкиНДС>
	;
	
	Параметры.Вставить("Билеты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаБилеты, СписокОбязательныхКолонок));
	
КонецПроцедуры

Функция ПодготовитьДанныеДвиженийИспользованиеБилетов(Билеты, Реквизиты)

	ПартионныйУчет = УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период) = Перечисления.СпособыОценки.ФИФО;
	
	ДанныеДвижений = Билеты.Скопировать();
	
	ДанныеДвижений.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");

	ДанныеДвижений.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Подразделение, "Подразделение");
	
	// Документом партии для билетов выступает авансовый отчет
	ДанныеДвижений.Колонки.Добавить("Партия", Документы.ТипВсеСсылки());
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Регистратор, "Партия");
	
	// При использовании билетов, купленных организацией для сотрудника, сам билет заменяет счет-фактура 
	ДанныеДвижений.Колонки.Добавить("ПредъявленСчетФактура", Новый ОписаниеТипов("Булево"));
	ДанныеДвижений.ЗаполнитьЗначения(Истина, "ПредъявленСчетФактура"); 
	
	ДанныеДвижений.Колонки.Добавить("СчетУчетаНДС", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ДанныеДвижений.ЗаполнитьЗначения(ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымУслугам, "СчетУчетаНДС"); // 19.04 
	
	ДанныеДвижений.Колонки.Добавить("ВидЦенности", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЦенностей"));
	ДанныеДвижений.ЗаполнитьЗначения(Перечисления.ВидыЦенностей.КомандировочныеРасходы, "ВидЦенности");
		
	ДанныеДвижений.Колонки.СпособУчетаНДС.Имя = "НовыйСпособУчетаНДС";	
	
	ДанныеДвижений.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(15,3));
	
	Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл

		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
		
		Если ПартионныйУчет Тогда
			Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
				Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Партия;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НЕ СвойстваСчета.УчетПоПодразделениям Тогда
			СтрокаТаблицы.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
		КонецЕсли;
		
	КонецЦикла;
		
	ДанныеДвижений.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Регистратор, "Регистратор");
	
	ДанныеДвижений.Колонки.Добавить("Период",      Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Период, "Период,ДатаСобытия");
	
	ДанныеДвижений.Колонки.Добавить("ДоговорКонтрагента", // колонка договор всегда пустая
		Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ДанныеДвижений.Колонки.Добавить("ЕстьНДСПредъявленный", Новый ОписаниеТипов("Булево"));
	ДанныеДвижений.ЗаполнитьЗначения(Истина, "ЕстьНДСПредъявленный");

	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДСИзменился",
		Новый ОписаниеТипов("Булево"));
	ДанныеДвижений.ЗаполнитьЗначения(Истина, "СпособУчетаНДСИзменился");
		
	ДанныеДвижений.Колонки.СчетФактура.Имя = "СчетФактураДокумент";	
	ДанныеДвижений.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
		
	Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл
		СтрокаТаблицы.СчетФактура = СтрокаТаблицы.СчетФактураДокумент;
		СтрокаТаблицы.ЕстьНДСПредъявленный = Истина;
	КонецЦикла;
	
	ДанныеДвижений.Колонки.СуммаНДСРуб.Имя = "НДС";
	ДанныеДвижений.Колонки.СуммаБезНДСРуб.Имя = "СуммаБезНДС";

	Возврат ДанныеДвижений;

КонецФункции

#КонецОбласти

#Область ВозвратТоваровОтПокупателя

Функция ПодготовитьТаблицуСобственныеТоварыУслугиНДС(СобственныеТоварыУслугиНДС, ТоварыНДС) Экспорт
	
	Параметры = ПодготовитьПараметрыТаблицыСобственныеТоварыУслугиНДС(СобственныеТоварыУслугиНДС, ТоварыНДС);
	Параметры.ТоварыНДС.Индексы.Добавить("ИмяСписка,НомерСтроки");
	
	Для Каждого СтрокаТаблицы Из Параметры.СобственныеТоварыУслугиНДС Цикл
		
		СтруктураОтбора = Новый Структура("ИмяСписка, НомерСтроки");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТаблицы);
		НайденныеСтроки = Параметры.ТоварыНДС.НайтиСтроки(СтруктураОтбора);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаТаблицы.СпособУчетаНДС = НайденныеСтроки[0].СпособУчетаНДС;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Параметры.СобственныеТоварыУслугиНДС;
	
КонецФункции

Функция ПодготовитьТаблицуСобственныеТоварыУслугиНДСВозвратКомиссионеру(СобственныеТоварыУслугиНДС, ТоварыНДС) Экспорт
	
	Параметры = ПодготовитьПараметрыТаблицыСобственныеТоварыУслугиНДСВозвратКомиссионеру(СобственныеТоварыУслугиНДС, ТоварыНДС);
	
	Параметры.СобственныеТоварыУслугиНДС.Колонки.Добавить("СпособУчетаНДС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
		
	Параметры.СобственныеТоварыУслугиНДС.Колонки.Добавить("СчетФактура",
		Документы.ТипВсеСсылки());

	Параметры.СобственныеТоварыУслугиНДС.Колонки.Добавить("КодВидаОперации",
		Новый ОписаниеТипов("Строка"));
		
	Параметры.ТоварыНДС.Индексы.Добавить("ИмяСписка,НомерСтроки");
	
	Для Каждого СтрокаТаблицы Из Параметры.СобственныеТоварыУслугиНДС Цикл
		
		СтруктураОтбора = Новый Структура("ИмяСписка, НомерСтроки");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТаблицы);
		НайденныеСтроки = Параметры.ТоварыНДС.НайтиСтроки(СтруктураОтбора);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, НайденныеСтроки[0],
				"СпособУчетаНДС, СчетФактура, Покупатель, КодВидаОперации");
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Параметры.СобственныеТоварыУслугиНДС;
	
КонецФункции

Функция ПодготовитьПараметрыТаблицыСобственныеТоварыУслугиНДСВозвратКомиссионеру(ТаблицаСобственныеТоварыУслугиНДС, ТаблицаТоварыНДС)

	Параметры = Новый Структура;

	// Подготовка таблицы СобственныеТоварыУслугиНДС

	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "НомерСтроки,"
	+ "Номенклатура,"
	+ "Сделка,"
	+ "Количество,"
	+ "СуммаВзаиморасчетов,"
	+ "СуммаРуб,"
	+ "СуммаНДСРуб,"
	+ "СуммаБУ,"
	+ "СуммаНУ,"
	+ "СчетУчета,"
	+ "СчетДоходов,"
	+ "Субконто,"
	+ "СтавкаНДС,"
	+ "СчетУчетаНДСПоРеализации,"
	+ "КорСчет,"
	+ "КорСубконто1,"
	+ "КорСубконто2,"
	+ "КорСубконто3,"
	+ "Контрагент,"
	+ "Покупатель,"
	+ "ВалютаВзаиморасчетов,"
	+ "Подразделение,"
	+ "ЭтоКомиссия,"
	+ "ЭтоУслуга,"
	+ "Комитент,"
	+ "ДоговорКомиссии,"
	+ "ДокументРасчетовСКомитентом,"
	+ "ДокументРасчетов,"
	+ "ДатаРеализации,"
	+ "СчетРасчетовСКомитентом,"
	+ "СчетАвансовСКомитентом,"
	+ "ВалютаРасчетовСКомитентом,"
	+ "СуммаРасчетовСКомитентом,"
	+ "СуммаПоступленияОтКомитента,"
	+ "СуммаБезНДСРуб"
	;
	Параметры.Вставить("СобственныеТоварыУслугиНДС", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСобственныеТоварыУслугиНДС, СписокОбязательныхКолонок));

	// Подготовка таблицы ТоварыНДС 
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка," 
	+ "НомерСтроки," 
	+ "СпособУчетаНДС," 
	+ "СчетФактура," 
	+ "Покупатель," 
	+ "КодВидаОперации" 
	;
	Параметры.Вставить("ТоварыНДС", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТоварыНДС, СписокОбязательныхКолонок));
		
	Возврат Параметры;

КонецФункции

Функция ПодготовитьПараметрыТаблицыСобственныеТоварыУслугиНДС(ТаблицаСобственныеТоварыУслугиНДС, ТаблицаТоварыНДС)

	Параметры = Новый Структура;

	// Подготовка таблицы СобственныеТоварыУслугиНДС

	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "НомерСтроки,"
	+ "Номенклатура,"
	+ "Количество,"
	+ "СуммаВзаиморасчетов,"
	+ "СуммаРуб,"
	+ "СуммаНДСРуб,"
	+ "СуммаБУ,"
	+ "СуммаНУ,"
	+ "СчетУчета,"
	+ "СчетДоходов,"
	+ "Субконто,"
	+ "СтавкаНДС,"
	+ "СчетУчетаНДСПоРеализации,"
	+ "КорСчет,"
	+ "КорСубконто1,"
	+ "КорСубконто2,"
	+ "КорСубконто3,"
	+ "Контрагент,"
	+ "Покупатель,"
	+ "ВалютаВзаиморасчетов,"
	+ "Подразделение,"
	+ "ЭтоКомиссия,"
	+ "ЭтоУслуга,"
	+ "Комитент,"
	+ "ДоговорКомиссии,"
	+ "ДокументРасчетовСКомитентом,"
	+ "ДокументРасчетов,"
	+ "ДатаРеализации,"
	+ "СчетРасчетовСКомитентом,"
	+ "СчетАвансовСКомитентом,"
	+ "ВалютаРасчетовСКомитентом,"
	+ "СуммаРасчетовСКомитентом,"
	+ "СуммаПоступленияОтКомитента,"
	+ "СуммаБезНДСРуб"
	;
	Параметры.Вставить("СобственныеТоварыУслугиНДС", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСобственныеТоварыУслугиНДС, СписокОбязательныхКолонок));

	Параметры.СобственныеТоварыУслугиНДС.Колонки.Добавить("СпособУчетаНДС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
		
	// Подготовка таблицы Товары

	СписокОбязательныхКолонок = ""
	+ "ИмяСписка," 
	+ "НомерСтроки," 
	+ "СпособУчетаНДС" 
	;
	Параметры.Вставить("ТоварыНДС", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТоварыНДС, СписокОбязательныхКолонок));
		
	Возврат Параметры;

КонецФункции

Процедура СформироватьДвиженияВозвратТоваровОтПокупателя(ТаблицаВозвращаемыеТовары, Реквизиты, Движения, Отказ) Экспорт

	Если ЗначениеЗаполнено(Реквизиты.ДокументОтгрузки) Тогда
		СформироватьДвиженияВозвратТоваровПоДокументуОтгрузки(ТаблицаВозвращаемыеТовары, Реквизиты, Движения, Отказ);
	Иначе
		ДанныеДвижений = ПодготовитьДанныеДвиженийВозвратТоваровОтПокупателя(ТаблицаВозвращаемыеТовары, Реквизиты);
		СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьТаблицуПартийПоРегиструНДСРаздельныйУчет(Реквизиты)

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Регистратор",		Реквизиты.Регистратор);
	Запрос.УстановитьПараметр("ДокументОтгрузки",	Реквизиты.ДокументОтгрузки);
	Запрос.УстановитьПараметр("Организация",		Реквизиты.Организация);
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество,
	|	ТаблицаТовары.Ссылка.Организация КАК Организация
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.СчетУчета,
	|	ТаблицаТовары.Ссылка.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СчетУчета,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСРаздельныйУчет.Организация КАК Организация,
	|	НДСРаздельныйУчет.Партия,
	|	СУММА(НДСРаздельныйУчет.Количество) КАК Количество,
	|	СУММА(НДСРаздельныйУчет.НДС) КАК НДС,
	|	СУММА(НДСРаздельныйУчет.СуммаБезНДС) КАК СуммаБезНДС,
	|	НДСРаздельныйУчет.АналитикаУчетаЗатрат,
	|	НДСРаздельныйУчет.АналитикаУчетаНДС,
	|	НДСРаздельныйУчет.СпособУчетаНДС,
	|	Аналитика.СчетЗатрат КАК СчетУчета,
	|	Аналитика.Субконто1 КАК Номенклатура,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПартии,
	|	ЕСТЬNULL(ДанныеПервичныхДокументовСчФ.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСчетаФактуры
	|ПОМЕСТИТЬ ДвиженияПоНДСРаздельныйУчет
	|ИЗ
	|	РегистрНакопления.НДСРаздельныйУчет КАК НДСРаздельныйУчет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаЗатрат КАК Аналитика
	|		ПО НДСРаздельныйУчет.АналитикаУчетаЗатрат = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = НДСРаздельныйУчет.Партия)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНДС КАК АналитикаНДС
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументовСчФ
	|			ПО АналитикаНДС.СчетФактура = ДанныеПервичныхДокументовСчФ.Документ
	|		ПО НДСРаздельныйУчет.АналитикаУчетаНДС = АналитикаНДС.КлючАналитики
	|ГДЕ
	|	НДСРаздельныйУчет.Регистратор = &ДокументОтгрузки
	|	И НДСРаздельныйУчет.Организация = &Организация
	|	И НДСРаздельныйУчет.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСРаздельныйУчет.Организация,
	|	НДСРаздельныйУчет.Партия,
	|	НДСРаздельныйУчет.АналитикаУчетаЗатрат,
	|	НДСРаздельныйУчет.АналитикаУчетаНДС,
	|	НДСРаздельныйУчет.СпособУчетаНДС,
	|	Аналитика.СчетЗатрат,
	|	Аналитика.Субконто1,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)),
	|	ЕСТЬNULL(ДанныеПервичныхДокументовСчФ.Дата, ДАТАВРЕМЯ(1, 1, 1))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СчетУчета,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПоПриобретеннымЦенностям.Организация,
	|	НДСПоПриобретеннымЦенностям.СчетУчета,
	|	НДСПоПриобретеннымЦенностям.Номенклатура,
	|	НДСПоПриобретеннымЦенностям.Склад,
	|	НДСПоПриобретеннымЦенностям.Партия,
	|	НДСПоПриобретеннымЦенностям.СчетФактура,
	|	НДСПоПриобретеннымЦенностям.НДСВключенВСтоимость,
	|	НДСПоПриобретеннымЦенностям.ВидЦенности,
	|	НДСПоПриобретеннымЦенностям.СчетУчетаНДС,
	|	НДСПоПриобретеннымЦенностям.СтавкаНДС,
	|	СУММА(НДСПоПриобретеннымЦенностям.Количество) КАК Количество,
	|	СУММА(НДСПоПриобретеннымЦенностям.Стоимость) КАК Стоимость,
	|	СУММА(НДСПоПриобретеннымЦенностям.НДС) КАК НДС
	|ПОМЕСТИТЬ НДСПоПриобретеннымЦенностям
	|ИЗ
	|	РегистрНакопления.НДСПоПриобретеннымЦенностям КАК НДСПоПриобретеннымЦенностям
	|ГДЕ
	|	НДСПоПриобретеннымЦенностям.Регистратор = &ДокументОтгрузки
	|	И НДСПоПриобретеннымЦенностям.Организация = &Организация
	|	И НДСПоПриобретеннымЦенностям.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПоПриобретеннымЦенностям.СчетУчета,
	|	НДСПоПриобретеннымЦенностям.Организация,
	|	НДСПоПриобретеннымЦенностям.Номенклатура,
	|	НДСПоПриобретеннымЦенностям.Склад,
	|	НДСПоПриобретеннымЦенностям.ВидЦенности,
	|	НДСПоПриобретеннымЦенностям.НДСВключенВСтоимость,
	|	НДСПоПриобретеннымЦенностям.СчетФактура,
	|	НДСПоПриобретеннымЦенностям.Партия,
	|	НДСПоПриобретеннымЦенностям.СчетУчетаНДС,
	|	НДСПоПриобретеннымЦенностям.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСПоПриобретеннымЦенностям.Номенклатура,
	|	НДСПоПриобретеннымЦенностям.СчетУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""НДСРаздельныйУчет"" КАК Регистр,
	|	НДСРаздельныйУчет.Организация,
	|	НДСРаздельныйУчет.Партия КАК Партия,
	|	НДСРаздельныйУчет.АналитикаУчетаЗатрат,
	|	НДСРаздельныйУчет.АналитикаУчетаНДС,
	|	НДСРаздельныйУчет.СпособУчетаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСРаздельныйУчет.Количество, 0) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьКоличество,
	|	ЕСТЬNULL(НДСРаздельныйУчет.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(НДСРаздельныйУчет.НДС, 0) КАК НДС,
	|	ЕСТЬNULL(НДСРаздельныйУчет.СуммаБезНДС, 0) КАК СуммаБезНДС,
	|	ТаблицаДокумента.Количество КАК КоличествоСписать,
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.Номенклатура,
	|	НДСРаздельныйУчет.ДатаПартии,
	|	НДСРаздельныйУчет.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК СчетФактура,
	|	НЕОПРЕДЕЛЕНО КАК ВидЦенности,
	|	НЕОПРЕДЕЛЕНО КАК СчетУчетаНДС,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС
	|ИЗ
	|	ТаблицаДокумента КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияПоНДСРаздельныйУчет КАК НДСРаздельныйУчет
	|		ПО ТаблицаДокумента.Номенклатура = НДСРаздельныйУчет.Номенклатура
	|			И ТаблицаДокумента.СчетУчета = НДСРаздельныйУчет.СчетУчета
	|			И ТаблицаДокумента.Организация = НДСРаздельныйУчет.Организация
	|ГДЕ
	|	НЕ НДСРаздельныйУчет.Организация ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""НДСПоПриобретеннымЦенностям"",
	|	НДСПоПриобретеннымЦенностям.Организация,
	|	НДСПоПриобретеннымЦенностям.Партия,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаЗатрат.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНДС.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА НДСПоПриобретеннымЦенностям.НДСВключенВСтоимость
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.УчитываетсяВCтоимости)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.ПринимаетсяКВычету)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСПоПриобретеннымЦенностям.Количество, 0) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЕСТЬNULL(НДСПоПриобретеннымЦенностям.Количество, 0),
	|	ЕСТЬNULL(НДСПоПриобретеннымЦенностям.НДС, 0),
	|	ЕСТЬNULL(НДСПоПриобретеннымЦенностям.Стоимость, 0) - ЕСТЬNULL(НДСПоПриобретеннымЦенностям.НДС, 0),
	|	ТаблицаДокумента.Количество,
	|	НДСПоПриобретеннымЦенностям.СчетУчета,
	|	НДСПоПриобретеннымЦенностям.Номенклатура,
	|	ЕСТЬNULL(ДанныеПервичныхДокументовПартия.Дата, НДСПоПриобретеннымЦенностям.Партия.Дата),
	|	ЕСТЬNULL(ДанныеПервичныхДокументовСчетФактура.Дата, НДСПоПриобретеннымЦенностям.СчетФактура.Дата),
	|	НДСПоПриобретеннымЦенностям.Склад,
	|	НДСПоПриобретеннымЦенностям.СчетФактура,
	|	НДСПоПриобретеннымЦенностям.ВидЦенности,
	|	НДСПоПриобретеннымЦенностям.СчетУчетаНДС,
	|	НДСПоПриобретеннымЦенностям.СтавкаНДС
	|ИЗ
	|	ТаблицаДокумента КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСПоПриобретеннымЦенностям КАК НДСПоПриобретеннымЦенностям
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументовПартия
	|			ПО НДСПоПриобретеннымЦенностям.Партия = ДанныеПервичныхДокументовПартия.Документ
	|				И НДСПоПриобретеннымЦенностям.Организация = ДанныеПервичныхДокументовПартия.Организация
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументовСчетФактура
	|			ПО НДСПоПриобретеннымЦенностям.СчетФактура = ДанныеПервичныхДокументовСчетФактура.Документ
	|				И НДСПоПриобретеннымЦенностям.Организация = ДанныеПервичныхДокументовСчетФактура.Организация
	|		ПО ТаблицаДокумента.Номенклатура = НДСПоПриобретеннымЦенностям.Номенклатура
	|			И ТаблицаДокумента.СчетУчета = НДСПоПриобретеннымЦенностям.СчетУчета
	|ГДЕ
	|	НЕ НДСПоПриобретеннымЦенностям.Организация ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	НДСРаздельныйУчет.ДатаПартии УБЫВ,
	|	ДатаСчетаФактуры УБЫВ";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура СформироватьДвиженияВозвратТоваровПоДокументуОтгрузки(ТаблицаВозвращаемыеТовары, Реквизиты, Движения, Отказ)

	ТаблицаПартий = ПолучитьТаблицуПартийПоРегиструНДСРаздельныйУчет(Реквизиты);
	
	Если ТаблицаПартий.Количество() = 0 Тогда
		
		//Данные о движениях по партиям НДС не обнаружены, возможно Раздельный учет на дату реализации не велся,
		//либо при реализации был отключен контроль остатков
		ДанныеДвижений = ПодготовитьДанныеДвиженийВозвратТоваровОтПокупателя(ТаблицаВозвращаемыеТовары, Реквизиты);
		СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
		
		Возврат;
	КонецЕсли;
	
	СформироватьДвиженияПартииРаздельныйУчет(ТаблицаПартий, ТаблицаВозвращаемыеТовары, Реквизиты, Движения);
	
	
КонецПроцедуры

Функция ПодготовитьДанныеДвиженийВозвратТоваровОтПокупателя(ТаблицаВозвращаемыеТовары, Реквизиты)
	
	ПартионныйУчет = УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период) = Перечисления.СпособыОценки.ФИФО;
	
	ДанныеДвижений = ТаблицаВозвращаемыеТовары.Скопировать(,
		"СчетУчета,Подразделение,Номенклатура,Склад,Партия,
		|СчетФактура,ВидЦенности,СчетУчетаНДС,СтавкаНДС,СпособУчетаНДС,
		|Количество,СуммаБезНДСРуб,СуммаНДСРуб");
		
	ДанныеДвижений.Колонки.СчетУчета.Имя = "СчетЗатрат";

	ДанныеДвижений.Колонки.Добавить("Субконто1");
	ДанныеДвижений.Колонки.Добавить("Субконто2");
	ДанныеДвижений.Колонки.Добавить("Субконто3");
	
	Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл

		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура
			 ИЛИ СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства Тогда
				СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(СтрокаТаблицы.СчетЗатрат) Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Склад;
				КонецЕсли;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
				Если ПартионныйУчет Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Партия;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если НЕ СвойстваСчета.УчетПоПодразделениям Тогда
			СтрокаТаблицы.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
		КонецЕсли;
		
	КонецЦикла;

	ДанныеДвижений.Колонки.Удалить("Номенклатура");
	ДанныеДвижений.Колонки.Удалить("Склад");
	
	ДанныеДвижений.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Регистратор, "Регистратор,Партия");
	
	ДанныеДвижений.Колонки.Добавить("Период",      Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Период, "Период,ДатаСобытия");

	ДанныеДвижений.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");

	ДанныеДвижений.Колонки.Добавить("Поставщик", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Контрагент, "Поставщик");
	ДанныеДвижений.Колонки.Добавить("ДоговорКонтрагента", // колонка договор всегда пустая
		Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));

	ДанныеДвижений.Колонки.СпособУчетаНДС.Имя = "НовыйСпособУчетаНДС";
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДСИзменился",
		Новый ОписаниеТипов("Булево"));
	ДанныеДвижений.ЗаполнитьЗначения(Истина, "СпособУчетаНДСИзменился");
		
	ДанныеДвижений.Колонки.СчетФактура.Имя = "СчетФактураДокумент";	
	ДанныеДвижений.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
	
	ДанныеДвижений.Колонки.Добавить("ЕстьНДСПредъявленный", Новый ОписаниеТипов("Булево"));
	Если Реквизиты.Период < '20190101' Тогда 
		ДанныеДвижений.ЗаполнитьЗначения(Истина, "ЕстьНДСПредъявленный");
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл
		Если Реквизиты.ПокупателемВыставляетсяСчетФактураНаВозврат Тогда
			СтрокаТаблицы.СчетФактура	= Реквизиты.Регистратор;
		Иначе
			СтрокаТаблицы.СчетФактура = Реквизиты.ДокументОтгрузки;
		КонецЕсли;	
	КонецЦикла;
	
	ДанныеДвижений.Колонки.СуммаНДСРуб.Имя = "НДС";
	ДанныеДвижений.Колонки.СуммаБезНДСРуб.Имя = "СуммаБезНДС";
	
	ДанныеДвижений.Колонки.Добавить("ИсправленныйСчетФактура",
		Новый ОписаниеТипов("ДокументСсылка.КорректировкаПоступления"));

	Возврат ДанныеДвижений;
	
КонецФункции

Процедура СформироватьДвиженияВозвратТоваровКомиссионеру(ТаблицаВозвращаемыеТовары, Реквизиты, Движения, Отказ) Экспорт
	
	ДанныеДвижений = ПодготовитьДанныеДвиженийВозвратТоваровКомиссионеру(ТаблицаВозвращаемыеТовары, Реквизиты);
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьДанныеДвиженийВозвратТоваровКомиссионеру(ТаблицаВозвращаемыеТовары, Реквизиты)
	
	ПартионныйУчет = (УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период) = Перечисления.СпособыОценки.ФИФО);
	
	ДанныеДвижений = ТаблицаВозвращаемыеТовары.СкопироватьКолонки(
		"Подразделение,Номенклатура,Склад,Партия,
		|СчетФактура,ВидЦенности,СчетУчетаНДС,СтавкаНДС,СпособУчетаНДС, Количество");

	
	ДанныеДвижений.Колонки.Добавить("Регистратор", 	Документы.ТипВсеСсылки());
	ДанныеДвижений.Колонки.Добавить("Период",      	Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.Колонки.Добавить("ДатаСобытия", 	Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.Колонки.Добавить("Организация", 	Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ДанныеДвижений.Колонки.Добавить("Поставщик", 	Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ДанныеДвижений.Колонки.Добавить("ДоговорКонтрагента", // колонка договор всегда пустая
		Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ДанныеДвижений.Колонки.Добавить("ЕстьНДСПредъявленный", Новый ОписаниеТипов("Булево"));	
	ДанныеДвижений.Колонки.Добавить("НовыйСпособУчетаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДСИзменился",
		Новый ОписаниеТипов("Булево"));
	ДанныеДвижений.Колонки.Добавить("СчетФактураДокумент", Документы.ТипВсеСсылки());
	ДанныеДвижений.Колонки.Добавить("СчетЗатрат", 	Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ДанныеДвижений.Колонки.Добавить("Субконто1");
	ДанныеДвижений.Колонки.Добавить("Субконто2");
	ДанныеДвижений.Колонки.Добавить("Субконто3");
	ДанныеДвижений.Колонки.Добавить("НДС", 			ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ДанныеДвижений.Колонки.Добавить("СуммаБезНДС", 	ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	
	Для каждого СтрокаТаблицы Из ТаблицаВозвращаемыеТовары Цикл
		НоваяСтрока = ДанныеДвижений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы,,"СпособУчетаНДС, СчетФактура");
		
		НоваяСтрока.СчетЗатрат 				= СтрокаТаблицы.СчетУчета;
		НоваяСтрока.Регистратор 			= Реквизиты.Регистратор;
		НоваяСтрока.Партия 					= Реквизиты.Регистратор;
		НоваяСтрока.Период 					= Реквизиты.Период;
		НоваяСтрока.ДатаСобытия 			= Реквизиты.Период;
		НоваяСтрока.Организация 			= Реквизиты.Организация;
		НоваяСтрока.Поставщик 				= Реквизиты.Контрагент;
		НоваяСтрока.ЕстьНДСПредъявленный    = Истина;
		НоваяСтрока.СпособУчетаНДСИзменился = Истина;
		НоваяСтрока.НовыйСпособУчетаНДС 	= СтрокаТаблицы.СпособУчетаНДС;
		НоваяСтрока.СчетФактураДокумент 	= СтрокаТаблицы.СчетФактура;
		НоваяСтрока.НДС 					= СтрокаТаблицы.СуммаНДСРуб;
		НоваяСтрока.СуммаБезНДС 			= СтрокаТаблицы.СуммаБезНДСРуб;
		НоваяСтрока.СчетФактура				= ?(СтрокаТаблицы.ПокупателемВыставляетсяСчетФактураНаВозврат, 
													Реквизиты.Регистратор, 
													СтрокаТаблицы.Сделка);
													
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетУчета);
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура
				ИЛИ СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства Тогда 
				НоваяСтрока["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(СтрокаТаблицы.СчетУчета) Тогда
					НоваяСтрока["Субконто" + Ном] = СтрокаТаблицы.Склад;
				КонецЕсли;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты Тогда
				НоваяСтрока["Субконто" + Ном] = Реквизиты.Контрагент;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда 
				Если ПартионныйУчет Тогда
					НоваяСтрока["Субконто" + Ном] = СтрокаТаблицы.Партия;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если НЕ СвойстваСчета.УчетПоПодразделениям Тогда
			СтрокаТаблицы.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеДвижений;
	
КонецФункции

// Отбирает на основании таблицы списания товаров в БУ подходящие партии из таблицы по РН Раздельный учет НДС
// и формирует движения по регистру накопления "Раздельный учет НДС".
//
// Параметры:
//   ТаблицаПартий             - ТаблицаЗначений - остатки в РН РаздельныйУчетНДС.
//   ТаблицаВозвращаемыеТовары - ТаблицаЗначений - фактически возвращаемые (сторно реализации) партии в БУ.
//   Реквизиты                 - СтрокаТаблицыЗначений - реквизиты документа, которым оформлен возврат.
//   Движения                  - КоллекцияДвижений - коллекция движений документа, которым оформлен возврат.
Процедура СформироватьДвиженияПартииРаздельныйУчет(ТаблицаПартий, ТаблицаВозвращаемыеТовары, Реквизиты, Движения)
	
	ТаблицаВозвращаемыеТовары.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаВозвращаемыеТовары.Колонки.Добавить("СчетЗатрат", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаВозвращаемыеТовары.Колонки.Добавить("Субконто1");
	ТаблицаВозвращаемыеТовары.Колонки.Добавить("Субконто2");
	ТаблицаВозвращаемыеТовары.Колонки.Добавить("Субконто3");
	ТаблицаВозвращаемыеТовары.Колонки.Добавить("АналитикаУчетаЗатрат",
		Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаЗатрат"));
	ТаблицаВозвращаемыеТовары.Колонки.Добавить("АналитикаУчетаНДС",
		Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНДС"));
		
	ПартионныйУчет = УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период) = Перечисления.СпособыОценки.ФИФО;
		
	Для Каждого СтрокаТаблицы Из ТаблицаВозвращаемыеТовары Цикл
		
		СтрокаТаблицы.СчетЗатрат  = СтрокаТаблицы.СчетУчета;
		СтрокаТаблицы.Организация = Реквизиты.Организация;
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура
				ИЛИ СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства Тогда
				СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(СтрокаТаблицы.СчетЗатрат) Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Склад;
				КонецЕсли;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
				Если ПартионныйУчет Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Партия;
				КонецЕсли;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты Тогда
				СтрокаТаблицы["Субконто" + Ном] = Реквизиты.Контрагент;
			КонецЕсли;
		КонецЦикла;
		
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			СтрокаТаблицы.Подразделение = Реквизиты.Подразделение;
		КонецЕсли;
		
		СтрокаТаблицы.АналитикаУчетаЗатрат = Справочники.КлючиАналитикиУчетаЗатрат.КлючиАналитикиУчетаЗатратДокумента(СтрокаТаблицы);
		
	КонецЦикла;
	
	ТаблицаПартий.Индексы.Добавить("ЕстьКоличество");
	
	ТаблицаПартийТовары     = ТаблицаПартий.Скопировать(Новый Структура("ЕстьКоличество", Истина));
	ТаблицаПартийДопРасходы = ТаблицаПартий.Скопировать(Новый Структура("ЕстьКоличество", Ложь));
	
	Для Каждого СтрокаТаблицы Из ТаблицаВозвращаемыеТовары Цикл
		
		КоличествоОсталосьПогасить = СтрокаТаблицы.Количество;
		
		СтруктураПоиска = Новый Структура("АналитикаУчетаЗатрат", СтрокаТаблицы.АналитикаУчетаЗатрат);
		ПартииСписания = ТаблицаПартийТовары.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого ПартияСписания Из ПартииСписания Цикл
			
			Если КоличествоОсталосьПогасить <= 0 Тогда
				Прервать;
			ИначеЕсли ПартияСписания.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Количество = Мин(ПартияСписания.Количество, КоличествоОсталосьПогасить);
			КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - Количество;
			
			Если ПартияСписания.Количество = Количество Тогда
				СуммаБезНДС = ПартияСписания.СуммаБезНДС;
				НДС        = ПартияСписания.НДС;
			Иначе
				СуммаБезНДС = (ПартияСписания.СуммаБезНДС/ПартияСписания.Количество) * Количество;
				НДС         = (ПартияСписания.НДС/ПартияСписания.Количество) * Количество;
			КонецЕсли;
			
			Движение = Движения.НДСРаздельныйУчет.ДобавитьРасход();
			
			ЗаполнитьЗначенияСвойств(Движение, Реквизиты);
			ЗаполнитьЗначенияСвойств(Движение, ПартияСписания);
			
			Движение.Количество  = - Количество;
			Движение.СуммаБезНДС = - СуммаБезНДС;
			Движение.НДС         = - НДС;
			
			КоэффициентСписанияДопРасходов = Количество / ПартияСписания.Количество;
			
			ПартииДопРасходов = ТаблицаПартийДопРасходы.НайтиСтроки(СтруктураПоиска);

			Для Каждого ПартияДопРасходов Из ПартииДопРасходов Цикл
				
				СписаннаяСуммаБезНДС = Окр(ПартияДопРасходов.СуммаБезНДС * КоэффициентСписанияДопРасходов, 2);
				СписанныйНДС         = Окр(ПартияДопРасходов.НДС * КоэффициентСписанияДопРасходов, 2);

				Движение = Движения.НДСРаздельныйУчет.ДобавитьРасход();
				
				ЗаполнитьЗначенияСвойств(Движение, Реквизиты);
				ЗаполнитьЗначенияСвойств(Движение, ПартияДопРасходов);
				
				Движение.Количество  = 0;
				Движение.СуммаБезНДС = - СписаннаяСуммаБезНДС;
				Движение.НДС         = - СписанныйНДС;
			
			КонецЦикла;
				
		КонецЦикла;
		
	КонецЦикла;
	
	Движения.НДСРаздельныйУчет.Записывать = Истина;
	
КонецПроцедуры

Процедура СформироватьДвиженияВозвратТоваровВРознице(ТаблицаВозвращаемыеТовары, ТаблицаПартииВозвратовРаздельныйУчет, Реквизиты, Движения, Отказ) Экспорт
	
	Если ТаблицаПартииВозвратовРаздельныйУчет.Количество() <> 0 Тогда
		СформироватьДвиженияПартииРаздельныйУчет(
			ТаблицаПартииВозвратовРаздельныйУчет, 
			ТаблицаВозвращаемыеТовары, 
			Реквизиты, 
			Движения);
	КонецЕсли; 
	
	ДанныеДвижений = ПодготовитьДанныеДвиженийВозвратТоваровВРознице(ТаблицаВозвращаемыеТовары, Реквизиты);
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьДанныеДвиженийВозвратТоваровВРознице(ТаблицаВозвращаемыеТовары, Реквизиты)
	
	ПартионныйУчет = (УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период) = Перечисления.СпособыОценки.ФИФО);
	
	ДанныеДвижений = ТаблицаВозвращаемыеТовары.СкопироватьКолонки(
		"Подразделение,Номенклатура,Склад,Партия,
		|СчетФактура,ВидЦенности,СчетУчетаНДС,СтавкаНДС,СпособУчетаНДС, Количество");
	
	ДанныеДвижений.Колонки.Добавить("Регистратор",             Документы.ТипВсеСсылки());
	ДанныеДвижений.Колонки.Добавить("Период",                  Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.Колонки.Добавить("ДатаСобытия",             Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.Колонки.Добавить("Организация",             Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ДанныеДвижений.Колонки.Добавить("ЕстьНДСПредъявленный", Новый ОписаниеТипов("Булево"));	
	ДанныеДвижений.Колонки.Добавить("НовыйСпособУчетаНДС",     Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДСИзменился", Новый ОписаниеТипов("Булево"));
	ДанныеДвижений.Колонки.Добавить("СчетФактураДокумент",     Документы.ТипВсеСсылки());
	ДанныеДвижений.Колонки.Добавить("СчетЗатрат",              Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ДанныеДвижений.Колонки.Добавить("Субконто1");
	ДанныеДвижений.Колонки.Добавить("Субконто2");
	ДанныеДвижений.Колонки.Добавить("Субконто3");
	ДанныеДвижений.Колонки.Добавить("НДС",                     ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ДанныеДвижений.Колонки.Добавить("СуммаБезНДС",             ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	
	Для каждого СтрокаТаблицы Из ТаблицаВозвращаемыеТовары Цикл
		// Для заполненной сделки делаем возврат на партию списания в СформироватьДвиженияПартииРаздельныйУчет
		Если ЗначениеЗаполнено(СтрокаТаблицы.Сделка) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ДанныеДвижений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы,,"СпособУчетаНДС, СчетФактура");
		
		НоваяСтрока.СчетЗатрат 				= СтрокаТаблицы.СчетУчета;
		НоваяСтрока.Регистратор 			= Реквизиты.Регистратор;
		НоваяСтрока.Партия 					= Реквизиты.Регистратор;
		НоваяСтрока.Период 					= Реквизиты.Период;
		НоваяСтрока.ДатаСобытия 			= Реквизиты.Период;
		НоваяСтрока.Организация 			= Реквизиты.Организация;
		НоваяСтрока.ЕстьНДСПредъявленный = Ложь;
		НоваяСтрока.СпособУчетаНДСИзменился = Истина;
		НоваяСтрока.НовыйСпособУчетаНДС 	= СтрокаТаблицы.СпособУчетаНДС;
		НоваяСтрока.СчетФактураДокумент 	= СтрокаТаблицы.СчетФактура;
		НоваяСтрока.НДС 					= СтрокаТаблицы.СуммаНДСРуб;
		НоваяСтрока.СуммаБезНДС 			= СтрокаТаблицы.СуммаБезНДСРуб;
		НоваяСтрока.СчетФактура				= СтрокаТаблицы.Сделка;
													
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетУчета);
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура
				ИЛИ СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства Тогда 
				НоваяСтрока["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(СтрокаТаблицы.СчетУчета) Тогда
					НоваяСтрока["Субконто" + Ном] = СтрокаТаблицы.Склад;
				КонецЕсли;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты Тогда
				НоваяСтрока["Субконто" + Ном] = Реквизиты.Контрагент;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда 
				Если ПартионныйУчет Тогда
					НоваяСтрока["Субконто" + Ном] = СтрокаТаблицы.Партия;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если НЕ СвойстваСчета.УчетПоПодразделениям Тогда
			СтрокаТаблицы.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеДвижений;
КонецФункции

#КонецОбласти

#Область ГТДИмпорт

Процедура СформироватьДвиженияУплатаНДСнаТаможне(ТаблицаТовары, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаТовары) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьРеквизитыУплатаНДСнаТаможне(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ПодготовитьПараметрыУплатаНДСнаТаможне(Параметры, ТаблицаТовары);
	
	// Выделяем таможенные платежи по ОС
	УчетНДС.ЗаполнитьВидыЦенностей(Параметры.Товары, Неопределено, "СчетУчета");
	Для каждого СтрокаТаблицы Из Параметры.Товары Цикл
		Если СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.ОС Тогда
			СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.ТаможенныеПлатежиОС;
		Иначе
			СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.ТаможенныеПлатежи;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеДвижений = ПодготовитьДанныеДвиженийПоступлениеТоваровУслугОтПоставщика(Параметры.Товары, Неопределено, Реквизиты);
	
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьРеквизитыУплатаНДСнаТаможне(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                    // <ДокументСсылка>
	+ "Период,"                         // <Дата>
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "Подразделение,"                  // <Ссылка на справочник подразделений>
	+ "Контрагент,"                     // <СправочникСсылка.Контрагенты>
	+ "ДоговорКонтрагента,"             // <СправочникСсылка.ДоговорыКонтрагентов>
	+ "СчетУчетаРасчетовСКонтрагентом," // <ПланСчетовСсылка.Хозрасчетный>
	+ "НДСПредъявленКВычету,"           // <Булево> - признак, что НДС по ГТД сразу предъявляется к вычету
	+ "ВалютаВзаиморасчетов,"           // <СправочникСсылка.Валюты>
	+ "Содержание"                      // <Строка, 150> - содержание для проводок
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	// Ограничение: НДС, уплаченный на таможне, в стоимость сейчас включить нельзя (всегда НДСВключенВСтоимость=Ложь)
	Параметры.Реквизиты.Колонки.Добавить("НДСВключенВСтоимость", Новый ОписаниеТипов("Булево"));
	// При уплате НДС на таможне всегда УчетАгентскогоНДС=Ложь
	Параметры.Реквизиты.Колонки.Добавить("УчетАгентскогоНДС", Новый ОписаниеТипов("Булево"));

	Возврат Параметры;

КонецФункции

Процедура ПодготовитьПараметрыУплатаНДСнаТаможне(Параметры, ТаблицаТовары)

	// Подготовка таблицы Товары

	СписокОбязательныхКолонок = ""
	+ "ДокументПартии,"          // <ДокументСсылка> - документ поступления товаров
	+ "Номенклатура,"            // <СправочникСсылка.Номенклатура> - поступивший ранее товар
	+ "Склад,"                   // <СправочникСсылка.Склады> - склад, на который поступил ранее товар
	+ "Содержание,"              // <Строка,150> - содержание для проводок
	+ "Подразделение,"           // Подразделение для проводок
	+ "СчетУчета,"               // <ПланСчетовСсылка.Хозрасчетный>
	+ "СчетУчетаНДС,"            // <ПланСчетовСсылка.Хозрасчетный>
	+ "СуммаБезНДСРуб,"             // <Число,15,2> - Таможенная стоимость в рублях
	+ "СуммаНДСРуб,"             // <Число,15,2> - сумма НДС в рублях
	+ "СуммаНДСВзаиморасчетов,"  // <Число,15,2> - сумма НДС в валюте расчетов с таможней
	+ "СтавкаНДС,"               // <ПеречислениеСсылка.СтавкиНДС>
	+ "СпособУчетаНДС"           // <ПеречислениеСсылка.СпособыУчетаНДС>
	;
	Параметры.Вставить("Товары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
	Реквизиты = Параметры.Реквизиты[0];
	// Документом партии служит документ поступления товаров
	Параметры.Товары.Колонки.ДокументПартии.Имя = "Партия";
	// Документом-основанием счета-фактуры выступает сам документ ГТД по импорту
	Параметры.Товары.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Регистратор, "СчетФактура");
	// Для расходов по уплате НДС на таможне, как и для всех остальных доп.расходов,
	// количество в регистре НДСпоПриобретеннымЦенностям всегда равно нулю
	Параметры.Товары.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	// При уплате НДС на таможне в регистре НДСпоПриобретеннымЦенностям отражается только сам уплаченный НДС,
	// поэтому СуммаРуб, СуммаБезНДСРуб и СуммаВзаиморасчетов равны 0
	Параметры.Товары.Колонки.Добавить("СуммаРуб", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Параметры.Товары.Колонки.Добавить("СуммаВзаиморасчетов", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Параметры.Товары.Колонки.Добавить("СтатьяЗатрат", Новый ОписаниеТипов("СправочникСсылка.СтатьиЗатрат"));
	Параметры.Товары.Колонки.Добавить("СпособСтроительства", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыСтроительства"));
	
	Параметры.Товары.Колонки.СуммаНДСРуб.Имя = "НДС";
	Параметры.Товары.Колонки.СуммаБезНДСРуб.Имя = "СуммаБезНДС";
	
КонецПроцедуры

#КонецОбласти

#Область КомплектацияНоменклатуры

Процедура СформироватьДвиженияКомплектацияТоваров(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаТовары)
	 Или Не ЗначениеЗаполнено(ТаблицаСписанныеТоварыБухУчет)
	 Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
	    Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период)
	 Или Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;

	Параметры = ПодготовитьПараметрыКомплектацияТоваров(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];

	СписанныеПартииНДС = ПолучитьТаблицуСписанныеПартииНДС(
		Параметры.Товары, Параметры.СписанныеТоварыБухУчет, Реквизиты, Отказ);
	СформироватьДвиженияВыбытиеТоваров(
		СписанныеПартииНДС, Параметры.СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
		
	ТаблицаДляИзмененияСпособаУчетаНДС = ПодготовитьТаблицуДляИзмененияСпособаУчетаНДСКомплектацияТоваров(
		СписанныеПартииНДС, Реквизиты);
	СформироватьДвиженияПоступлениеЦенностей(ТаблицаДляИзмененияСпособаУчетаНДС, Реквизиты, Движения, Отказ);

КонецПроцедуры

Функция ПодготовитьПараметрыКомплектацияТоваров(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                   // <Дата>
	+ "Регистратор,"              // <ДокументСсылка>
	+ "Организация,"              // <СправочникСсылка.Организации>
	+ "Склад,"                    // <СправочникСсылка.Склады>
	+ "Подразделение,"            // <Ссылка на справочник подразделений>
	+ "НДСвСтоимостиТоваров,"	  // <ПеречислениеСсылка.ДействиеНДСВСтоимостиТоваров> - действие по включению/исключению НДС Из стоимости
	+ "СчетСписанияНДС,"          // <ПланСчетовСсылка.Хозрасчетный> - счет затрат, куда относится ранее принятый к вычету НДС при включении НДС в стоимость
	+ "СубконтоСписанияНДС1,"     // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "СубконтоСписанияНДС2,"     // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "СубконтоСписанияНДС3,"     // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Комплект,"                 // <СправочникСсылка.Номенклатура>
	+ "КоличествоКомплектов,"     // <Число,15,3>
	+ "СчетУчетаКомплектов"       // <ПланСчетовСсылка.Хозрасчетный>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	// Для списания на расходы ранее принятого к вычету НДС счет и аналитика списания товара не используются
	Параметры.Реквизиты.Колонки.Добавить("СписыватьНДСнаКорСчетИАналитикуТовара", Новый ОписаниеТипов("Булево")); // Ложь

	// Подготовка таблицы товаров документа:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "СинонимСписка,"
	+ "НомерСтрокиДокумента,"
	+ "Номенклатура,"
	+ "Количество,"
	+ "СчетУчета,"
	+ "НовыйСпособУчетаНДС"
	;
	Параметры.Вставить("Товары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));

	// Подготовка таблицы списанных товаров по данным бух.учета:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "СинонимСписка,"
	+ "НомерСтроки,"
	+ "СчетУчета,"
	+ "Номенклатура,"
	+ "Склад,"
	+ "Партия,"
	+ "Количество,"
	+ "КорСчетСписания,"
	+ "ВидКорСубконто1,"
	+ "ВидКорСубконто2,"
	+ "ВидКорСубконто3,"
	+ "КорСубконто1,"
	+ "КорСубконто2,"
	+ "КорСубконто3,"
	+ "КорПодразделение,"
	+ "Подразделение"
	;
	Параметры.Вставить("СписанныеТоварыБухУчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСписанныеТоварыБухУчет, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Функция ПодготовитьТаблицуДляИзмененияСпособаУчетаНДСКомплектацияТоваров(СписанныеПартииНДС, Реквизиты)
	
	ТаблицаЦенностей = СписанныеПартииНДС.СкопироватьКолонки("
		|ВидЦенности,ДатаСобытия,ИсправленныйСчетФактура,Количество,
		|НДС,Организация,Партия,Период,Поставщик,Регистратор,СпособУчетаНДС,СтавкаНДС,ЕстьНДСПредъявленный,
		|СуммаБезНДС,СчетУчетаНДС,СчетФактура,НовыйСпособУчетаНДС,СпособУчетаНДСИзменился,
		|СтавкаНДСДокумента,СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3");
	
	ТаблицаЦенностей.Колонки.Добавить("ДоговорКонтрагента",
		Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
		
	Если СписанныеПартииНДС.Количество() = 0 Тогда
		Возврат ТаблицаЦенностей;
	КонецЕсли;
	
	НоваяСтрока = ТаблицаЦенностей.Добавить();
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Реквизиты, "Период,Регистратор,Организация");
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СписанныеПартииНДС[0], "СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3");
	НоваяСтрока.Партия = Реквизиты.Регистратор;
	НоваяСтрока.СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ; // 19.03
	НоваяСтрока.ВидЦенности = УчетНДС.ОпределитьВидЦенности(Реквизиты.СчетУчетаКомплектов);
    НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
	НоваяСтрока.СпособУчетаНДС = СписанныеПартииНДС[0].НовыйСпособУчетаНДС;
	НоваяСтрока.Количество = Реквизиты.КоличествоКомплектов;
	
	Для Каждого СтрокаТаблицы Из СписанныеПартииНДС Цикл
		
		СуммаБезНДС = СтрокаТаблицы.СуммаБезНДС - СтрокаТаблицы.СуммаБезНДСПринятоКВычету; 
		НДС = СтрокаТаблицы.НДС - СтрокаТаблицы.НДСПринятоКВычету;
		
		Если СуммаБезНДС > 0 ИЛИ НДС > 0 Тогда
		
			НоваяСтрока = ТаблицаЦенностей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"Количество");
		
			НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
			НоваяСтрока.НДС = НДС;
			
		КонецЕсли;
		
		Если СтрокаТаблицы.СуммаБезНДСПринятоКВычету > 0
		 ИЛИ СтрокаТаблицы.НДСПринятоКВычету > 0 Тогда
		 
			НоваяСтрока = ТаблицаЦенностей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"Количество");
			
			Если СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости Тогда
				НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ВосстановленУчитываетсяВCтоимости;
			ИначеЕсли СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 Тогда
				НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ВосстановленДляОперацийПо0;
			КонецЕсли;
			
			НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСПринятоКВычету;
			НоваяСтрока.НДС = СтрокаТаблицы.НДСПринятоКВычету;
			
		КонецЕсли;
		
	КонецЦикла;
	
    Возврат ТаблицаЦенностей;
	
КонецФункции

// Процедура формирует движение по раздельному учету по реализации отгруженных товаров
// Параметры:
//    ТаблицаТовары - ТаблицаЗначений - таблица Товары
//    ТаблицаСписанныеТоварыБухУчет - ТаблицаЗначений - таблица списанных товаров по данным бух.учета
//    ТаблицаРеквизиты - ТаблицаЗначений - таблицы шапки документа
//    Движения - КоллекцияДвижений- движения документа
//    Отказ - булево - признак отказа от проведения
//
Процедура СформироватьДвиженияРеализацияОтгруженныхТоваров(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не (ЗначениеЗаполнено(ТаблицаТовары)
			Или ЗначениеЗаполнено(ТаблицаСписанныеТоварыБухУчет)
			Или Не ЗначениеЗаполнено(ТаблицаРеквизиты)) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если НЕ РаздельныйУчетНДСНаСчете19 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыРеализацияОтгруженныхТоваров(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	Если Реквизиты.НДСНачисленПриОтгрузке Тогда
			
		СписанныеПартииНДС = УчетНДСПереопределяемый.ПолучитьТаблицуСписанныеПартииНДС(
			Параметры.Товары, Параметры.СписанныеТоварыБухУчет, Реквизиты, Отказ);
			
		СформироватьДвиженияВыбытиеТоваров(
			СписанныеПартииНДС, 
			Параметры.СписанныеТоварыБухУчет,
			Реквизиты, Движения, Отказ);
			
	КонецЕсли;

КонецПроцедуры

// Функция подготавливает таблицы 
// Параметры:
//   ТаблицаТовары - ТаблицаЗначений - таблица товары
//   ТаблицаСписанныеТоварыБухУчет - ТаблицаЗначений - таблица списанных товаров по данным бух.учета
//   ТаблицаРеквизиты - ТаблицаЗначений - таблица шапки документа
//
Функция ПодготовитьПараметрыРеализацияОтгруженныхТоваров(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы шапки документа
	СписокОбязательныхКолонок = ""
	+ "Период,"                         // <Дата>
	+ "Регистратор,"                    // <ДокументСсылка>
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "Контрагент,"                     // <СправочникСсылка.Контрагенты>
	+ "КодОперацииПоСделке,"            // <СправочникСсылка.КодыОперацийРаздела7ДекларацииПоНДС> - 
	                                    // код операции для раздела 7 Декларации по НДС, указанный в договоре контрагента
	+ "ДокументОтгрузки,"               // <ДокументСсылка.РеализацияТоваровУслуг>
	+ "НДСИсчисляетсяНалоговымАгентом," // <Булево> - признак покупателя-налогового агента.
	+ "НДСНачисленПриОтгрузке"          // <Булево>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));

	// Подготовка таблицы Товары

	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"            // <Строка> - имя табличной части документа
	+ "СинонимСписка,"        // <Строка> - синоним табличной части документа
	+ "НомерСтрокиДокумента," // <Число,10,0> - номер строки документа
	+ "Номенклатура,"         // <СправочникСсылка.Номенклатура>
	+ "СтавкаНДС,"            // <ПеречислениеСсылка.СтавкиНДС>
	+ "СчетУчета,"            // <ПланСчетовСсылка.Хозрасчетный> - счет учета товара
	+ "Количество"            // <Число,15,3>
	;
	Параметры.Вставить("Товары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
	// ГТД были списаны при отгрузке, поэтому добавляем пустые колонки
	Параметры.Товары.Колонки.Добавить("НомерГТД",            Новый ОписаниеТипов("СправочникСсылка.НомераГТД"));
	Параметры.Товары.Колонки.Добавить("СтранаПроисхождения", Новый ОписаниеТипов("СправочникСсылка.СтраныМира"));

	// Подготовка таблицы списанных товаров по данным бух.учета:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "СинонимСписка,"
	+ "НомерСтроки,"
	+ "СчетУчета,"
	+ "Номенклатура,"
	+ "Склад,"
	+ "Партия,"
	+ "Количество,"
	+ "КорСчетСписания,"
	+ "ВидКорСубконто1,"
	+ "ВидКорСубконто2,"
	+ "ВидКорСубконто3,"
	+ "КорСубконто1,"
	+ "КорСубконто2,"
	+ "КорСубконто3,"
	+ "КорПодразделение,"
	+ "Подразделение"
	;
	Параметры.Вставить("СписанныеТоварыБухУчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСписанныеТоварыБухУчет, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура СформироватьДвиженияРазукомплектацияТоваров(ТаблицаКомплектующие, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаСписанныеТоварыБухУчет)
	 Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период)
	 Или Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыРазукомплектацияТоваров(ТаблицаКомплектующие, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	Параметры.Комплектующие.Индексы.Добавить("СчетУчета,Номенклатура");
	Отбор = Новый Структура("Номенклатура,СчетУчета");
	ТаблицаКомплекта = Параметры.Комплектующие.СкопироватьКолонки();
	Для Каждого СтрокаТаблицы Из Параметры.СписанныеТоварыБухУчет Цикл
		НоваяСтрока = ТаблицаКомплекта.Добавить(); 
		НоваяСтрока.СчетУчета = СтрокаТаблицы.КорСчетСписания;
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(НоваяСтрока.СчетУчета);
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура Тогда
			 	НоваяСтрока.Номенклатура = СтрокаТаблицы["КорСубконто" + Ном];
			КонецЕсли;
		КонецЦикла;
		ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
		Комплектующие = Параметры.Комплектующие.НайтиСтроки(Отбор);
		НоваяСтрока.НовыйСпособУчетаНДС = Комплектующие[0].НовыйСпособУчетаНДС;
		НоваяСтрока.Количество = СтрокаТаблицы.Количество;
	КонецЦикла;
	
	ТаблицаКомплекта.ЗаполнитьЗначения(Реквизиты.Комплект, "Номенклатура");
	ТаблицаКомплекта.ЗаполнитьЗначения(Реквизиты.СчетУчетаКомплектов, "СчетУчета");
	ТаблицаКомплекта.ЗаполнитьЗначения(1, "НомерСтрокиДокумента");
	ТаблицаКомплекта.ЗаполнитьЗначения("Комплектующие", "ИмяСписка");
	
	ТаблицаКомплекта.Свернуть("
		|ИмяСписка,НомерСтрокиДокумента,Номенклатура,СчетУчета,НовыйСпособУчетаНДС",
		"Количество");

	СписанныеПартииНДС = ПолучитьТаблицуСписанныеПартииНДС(
		ТаблицаКомплекта, Параметры.КомплектующиеБухУчет, Реквизиты, Отказ);
		
	СформироватьДвиженияВыбытиеТоваров(
		СписанныеПартииНДС, Параметры.СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
		
	ТаблицаДляИзмененияСпособаУчетаНДС = ПодготовитьТаблицуДляИзмененияСпособаУчетаНДСРазукомплектацияТоваров(
		Параметры.Комплектующие, СписанныеПартииНДС, Реквизиты);
	СформироватьДвиженияПоступлениеЦенностей(ТаблицаДляИзмененияСпособаУчетаНДС, Реквизиты, Движения, Отказ);

КонецПроцедуры

Функция ПодготовитьПараметрыРазукомплектацияТоваров(ТаблицаКомплектующие, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                   // <Дата>
	+ "Регистратор,"              // <ДокументСсылка>
	+ "Организация,"              // <СправочникСсылка.Организации>
	+ "Подразделение,"            // <Ссылка на справочник подразделений>
	+ "Склад,"                    // <СправочникСсылка.Склады>
	+ "НДСвСтоимостиТоваров,"	  // <ПеречислениеСсылка.ДействиеНДСВСтоимостиТоваров> - действие по включению/исключению НДС Из стоимости
	+ "СчетСписанияНДС,"          // <ПланСчетовСсылка.Хозрасчетный> - счет затрат, куда относится ранее принятый к вычету НДС при включении НДС в стоимость
	+ "СубконтоСписанияНДС1,"     // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "СубконтоСписанияНДС2,"     // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "СубконтоСписанияНДС3,"     // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Комплект,"                 // <СправочникСсылка.Номенклатура>
	+ "КоличествоКомплектов,"     // <Число,15,3>
	+ "СчетУчетаКомплектов"       // <ПланСчетовСсылка.Хозрасчетный>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	// Для списания на расходы ранее принятого к вычету НДС счет и аналитика списания товара не используются
	Параметры.Реквизиты.Колонки.Добавить("СписыватьНДСнаКорСчетИАналитикуТовара", Новый ОписаниеТипов("Булево")); // Ложь

	// Подготовка таблицы комплектующих:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "СинонимСписка,"
	+ "НомерСтрокиДокумента,"
	+ "Номенклатура,"
	+ "Количество,"
	+ "ДоляСтоимости,"
	+ "СчетУчета,"
	+ "НовыйСпособУчетаНДС"
	;
	Параметры.Вставить("Комплектующие", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаКомплектующие, СписокОбязательныхКолонок));

	// Подготовка таблицы списанных товаров по данным бух.учета:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "СинонимСписка,"
	+ "НомерСтроки,"
	+ "СчетУчета,"
	+ "Номенклатура,"
	+ "Склад,"
	+ "Партия,"
	+ "Количество,"
	+ "КорСчетСписания,"
	+ "ВидКорСубконто1,"
	+ "ВидКорСубконто2,"
	+ "ВидКорСубконто3,"
	+ "КорСубконто1,"
	+ "КорСубконто2,"
	+ "КорСубконто3,"
	+ "КорПодразделение,"
	+ "Подразделение"
	;
	Параметры.Вставить("СписанныеТоварыБухУчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСписанныеТоварыБухУчет, СписокОбязательныхКолонок));
			
	КомплектующиеБухУчет = Параметры.СписанныеТоварыБухУчет;
	КомплектующиеБухУчет.ЗаполнитьЗначения(1, "НомерСтроки");
	
	Параметры.Вставить("КомплектующиеБухУчет", КомплектующиеБухУчет);

	Возврат Параметры;

КонецФункции

Функция ПодготовитьТаблицуДляИзмененияСпособаУчетаНДСРазукомплектацияТоваров(Комплектующие, СписанныеПартииНДС, Реквизиты)
	
	ТаблицаЦенностей = СписанныеПартииНДС.СкопироватьКолонки("
		|ВидЦенности,ДатаСобытия,ИсправленныйСчетФактура,Количество,
		|НДС,Организация,Партия,Период,Поставщик,Регистратор,СпособУчетаНДС,СтавкаНДС,ЕстьНДСПредъявленный,
		|СуммаБезНДС,СчетУчетаНДС,СчетФактура,НовыйСпособУчетаНДС,СпособУчетаНДСИзменился,
		|СтавкаНДСДокумента,СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,
		|БлокироватьДоПодтвержденияСтавки0");
	
	ТаблицаЦенностей.Колонки.Добавить("ДоговорКонтрагента",
		Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
		
	Если СписанныеПартииНДС.Количество() = 0 Тогда
		Возврат ТаблицаЦенностей;
	КонецЕсли;
	
	// Добавим в таблицу комплектующие (только количество) со ставкой "Без НДС" и без счета-фактуры
	
	ТаблицаЦенностей.Колонки.Добавить("ДоляСтоимости",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,0)));
	
	СпособОценкиМПЗПоСредней = (УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период)
		= Перечисления.СпособыОценки.ПоСредней);
	
	Для каждого СтрокаТаблицы Из Комплектующие Цикл
		
		НоваяСтрока = ТаблицаЦенностей.Добавить();
		
		НоваяСтрока.СчетЗатрат = СтрокаТаблицы.СчетУчета; 
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(НоваяСтрока.СчетЗатрат);
		
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			НоваяСтрока.Подразделение = Реквизиты.Подразделение;
		КонецЕсли;
		
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура Тогда
			 	НоваяСтрока["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(НоваяСтрока.СчетЗатрат) Тогда
					НоваяСтрока["Субконто" + Ном] = Реквизиты.Склад;
				КонецЕсли;
	        ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
				Если НЕ СпособОценкиМПЗПоСредней Тогда
					НоваяСтрока["Субконто" + Ном] = Реквизиты.Регистратор;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, "Количество,ДоляСтоимости,НовыйСпособУчетаНДС");
		НоваяСтрока.СпособУчетаНДС = СтрокаТаблицы.НовыйСпособУчетаНДС;
		
	КонецЦикла;
	
	ТаблицаЦенностей.ЗаполнитьЗначения(Реквизиты.Период,      "Период,ДатаСобытия");
	ТаблицаЦенностей.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");
	ТаблицаЦенностей.ЗаполнитьЗначения(Реквизиты.Регистратор, "Партия,Регистратор");
	ТаблицаЦенностей.ЗаполнитьЗначения(ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ, "СчетУчетаНДС"); // 19.03
	ТаблицаЦенностей.ЗаполнитьЗначения(Перечисления.СтавкиНДС.БезНДС, "СтавкаНДС");
	УчетНДС.ЗаполнитьВидыЦенностей(ТаблицаЦенностей, Неопределено, "СчетЗатрат");

	СписанныеПартииНДС.Свернуть("
	|Партия,Поставщик,ИсправленныйСчетФактура,
	|ВидЦенности,СчетФактура,СчетУчетаНДС,СтавкаНДС,
	|СпособУчетаНДС,НовыйСпособУчетаНДС,ЕстьНДСПредъявленный",
	"СуммаБезНДС,НДС,СуммаБезНДСПринятоКВычету,НДСПринятоКВычету");
	
	// Распределяем стоимость и НДС каждой списанной партии комплекта между всеми комплектующими
	// пропорционально указанной в документе разукомплектации доле стоимости комплектующей

	// Количество не заполняется, движения отражаются аналогично доп.расходам
	ТаблицаЦенностейСуммы = ТаблицаЦенностей.Скопировать();
	ТаблицаЦенностейСуммы.ЗаполнитьЗначения(0, "Количество");

	Для каждого СтрокаТаблицы Из СписанныеПартииНДС Цикл

		СуммаБезНДС = СтрокаТаблицы.СуммаБезНДС - СтрокаТаблицы.СуммаБезНДСПринятоКВычету; 
		НДС = СтрокаТаблицы.НДС - СтрокаТаблицы.НДСПринятоКВычету;
		
		Если СуммаБезНДС > 0 ИЛИ НДС > 0 Тогда
		
			ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(
				СуммаБезНДС, ТаблицаЦенностейСуммы, "СуммаБезНДС", "ДоляСтоимости");
			ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(
				НДС, ТаблицаЦенностейСуммы, "НДС", "ДоляСтоимости");
				
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.ВидЦенности, "ВидЦенности");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.СчетФактура, "СчетФактура");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.СчетУчетаНДС, "СчетУчетаНДС");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.СтавкаНДС, "СтавкаНДС");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.Поставщик, "Поставщик");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.Партия, "Партия");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.ИсправленныйСчетФактура, "ИсправленныйСчетФактура");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.СпособУчетаНДС, "СпособУчетаНДС");			
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.НовыйСпособУчетаНДС, "НовыйСпособУчетаНДС");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.ЕстьНДСПредъявленный, "ЕстьНДСПредъявленный");
			
			Для каждого СтрокаТаблицыСумм Из ТаблицаЦенностейСуммы Цикл
				НоваяСтрока = ТаблицаЦенностей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыСумм);
			КонецЦикла;
			
		КонецЕсли;
		
		Если СтрокаТаблицы.СуммаБезНДСПринятоКВычету > 0
		 ИЛИ СтрокаТаблицы.НДСПринятоКВычету > 0 Тогда
		 
			ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(
				СтрокаТаблицы.СуммаБезНДСПринятоКВычету, ТаблицаЦенностейСуммы, "СуммаБезНДС", "ДоляСтоимости");
			ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(
				СтрокаТаблицы.НДСПринятоКВычету, ТаблицаЦенностейСуммы, "НДС", "ДоляСтоимости");
		 
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.ВидЦенности, "ВидЦенности");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.СчетФактура, "СчетФактура");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.СчетУчетаНДС, "СчетУчетаНДС");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.СтавкаНДС, "СтавкаНДС");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.Поставщик, "Поставщик");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.Партия, "Партия");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.ИсправленныйСчетФактура, "ИсправленныйСчетФактура");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.СпособУчетаНДС, "СпособУчетаНДС");
			ТаблицаЦенностейСуммы.ЗаполнитьЗначения(СтрокаТаблицы.ЕстьНДСПредъявленный, "ЕстьНДСПредъявленный");
			
			Если СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости Тогда
				ТаблицаЦенностейСуммы.ЗаполнитьЗначения(
					Перечисления.СпособыУчетаНДС.ВосстановленУчитываетсяВCтоимости, "НовыйСпособУчетаНДС");
			ИначеЕсли СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 Тогда
				ТаблицаЦенностейСуммы.ЗаполнитьЗначения(
					Перечисления.СпособыУчетаНДС.ВосстановленДляОперацийПо0, "НовыйСпособУчетаНДС");
			КонецЕсли;
			
			Для каждого СтрокаТаблицыСумм Из ТаблицаЦенностейСуммы Цикл
				НоваяСтрока = ТаблицаЦенностей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыСумм);
			КонецЦикла;
			
		КонецЕсли;

	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицаЦенностей Цикл
		ЗаполнитьСпособУчетаНДСИзменился(СтрокаТаблицы);	
	КонецЦикла;

	Возврат ТаблицаЦенностей;
	
КонецФункции

#КонецОбласти

#Область КорректировкаПоступления

Процедура ОпределитьСуммыДляРаспределенияНДСКорректировкаПоступления(ТаблицаПоЦенностям, Реквизиты, Отказ) Экспорт
	
	ТаблицаПоЦенностям.Колонки.Добавить("НДСПринимаетсяКВычету",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("НДСУчитываетсяВCтоимости",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("НДСДляОперацийПо0",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("НДСВалПринимаетсяКВычету",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("НДСВалУчитываетсяВCтоимости",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("НДСВалДляОперацийПо0",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("СубконтоСФПолученные",
		Документы.ТипВсеСсылки());
	ТаблицаПоЦенностям.Колонки.Добавить("НовыйСпособУчетаНДС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
		
	ТаблицаПоЦенностям.ЗагрузитьКолонку(ТаблицаПоЦенностям.ВыгрузитьКолонку("СпособУчетаНДС"), "НовыйСпособУчетаНДС");
		
	ПериодПоступления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.ДокументПоступленияСсылка, "Дата");
		
	Если НачалоКвартала(ПериодПоступления) = НачалоКвартала(Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
		
	ТаблицаПоЦенностям.Индексы.Добавить("СпособУчетаНДС");
	
	Отбор = Новый Структура("СпособУчетаНДС", Перечисления.СпособыУчетаНДС.Распределяется);
	СтрокиКРаспределению = ТаблицаПоЦенностям.НайтиСтроки(Отбор);
	
	Если СтрокиКРаспределению.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.БазаРаспределенияНДС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = НачалоКвартала(ПериодПоступления);
	МенеджерЗаписи.Организация = Реквизиты.Организация;
	МенеджерЗаписи.Прочитать();
		
	Если МенеджерЗаписи.Выбран() Тогда
		
		Пропорция = МенеджерЗаписи;
		БазаРаспределения = Пропорция.ВыручкаНДС + Пропорция.ВыручкаБезНДС + Пропорция.ВыручкаЕНВД + Пропорция.ВыручкаНДС0;
		
		Для Каждого СтрокаТаблицы Из СтрокиКРаспределению Цикл
			
			СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределен;
			
			УчтеноНДСВал  = 0;
			УчтеноНДС     = 0;
			УчтеноВыручки = 0;
			
			НДС    = СтрокаТаблицы.СуммаНДСРуб - СтрокаТаблицы.СуммаНДСРубДоИзменения;
			НДСВал = СтрокаТаблицы.СуммаНДСВзаиморасчетов - СтрокаТаблицы.СуммаНДСВзаиморасчетовДоИзменения;
			
			Если Пропорция.ВыручкаНДС <> 0 Тогда

				СтрокаТаблицы.НДСВалПринимаетсяКВычету = Окр(НДСВал
					* (Пропорция.ВыручкаНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДСВал;
				СтрокаТаблицы.НДСПринимаетсяКВычету = Окр(НДС
					* (Пропорция.ВыручкаНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

				УчтеноНДСВал    = УчтеноНДСВал + СтрокаТаблицы.НДСВалПринимаетсяКВычету;
				УчтеноНДС       = УчтеноНДС + СтрокаТаблицы.НДСПринимаетсяКВычету;
				УчтеноВыручки   = УчтеноВыручки + Пропорция.ВыручкаНДС;

			КонецЕсли;

			Если Пропорция.ВыручкаБезНДС + Пропорция.ВыручкаЕНВД <> 0 Тогда

				СтрокаТаблицы.НДСВалУчитываетсяВCтоимости = Окр(НДСВал
					* (Пропорция.ВыручкаБезНДС + Пропорция.ВыручкаЕНВД  + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДСВал;
				СтрокаТаблицы.НДСУчитываетсяВCтоимости = Окр(НДС
					* (Пропорция.ВыручкаБезНДС + Пропорция.ВыручкаЕНВД + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

				УчтеноСуммы   = УчтеноНДСВал + СтрокаТаблицы.НДСВалУчитываетсяВCтоимости;
				УчтеноНДС     = УчтеноНДС + СтрокаТаблицы.НДСУчитываетсяВCтоимости;
				УчтеноВыручки = УчтеноВыручки + Пропорция.ВыручкаБезНДС + Пропорция.ВыручкаЕНВД;

			КонецЕсли;

			Если Пропорция.ВыручкаНДС0 <> 0 Тогда

				СтрокаТаблицы.НДСВалДляОперацийПо0 = Окр(НДСВал
					* (Пропорция.ВыручкаНДС0 + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДСВал;
				СтрокаТаблицы.НДСДляОперацийПо0 = Окр(НДС
					* (Пропорция.ВыручкаНДС0 + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

				УчтеноНДСВал  = УчтеноНДСВал + СтрокаТаблицы.НДСВалДляОперацийПо0;
				УчтеноНДС     = УчтеноНДС + СтрокаТаблицы.НДСДляОперацийПо0;
				УчтеноВыручки = УчтеноВыручки + Пропорция.ВыручкаНДС0;

			КонецЕсли;
		
		 КонецЦикла;
		
	Иначе
		
		Если НЕ Отказ Тогда
		
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не обнаружена база распределения НДС за %1'"),
				Формат(ПериодПоступления, "ДФ='к ''Квартал'' гггг  ''г.'''"));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияНДСПредъявленныйРеализация0КорректировкаПоступления(ДанныеДвижений, Реквизиты, Движения, Отказ) Экспорт
	
	Если ДанныеДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДанныеДвижений.Индексы.Добавить("Сторно");
	ДанныеДвиженийПриход = ДанныеДвижений.Скопировать(Новый Структура("Сторно", Ложь));
	
	ДанныеДвиженийСторно = ДанныеДвижений.НайтиСтроки(Новый Структура("Сторно", Истина));
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор));
	Запрос.УстановитьПараметр("СчетФактура", Реквизиты.ДокументОснование);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Остатки.Организация КАК Организация,
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.Состояние КАК Состояние,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки КАК ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Остатки.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСПредъявленныйРеализация0Остатки.НДСОстаток КАК НДС,
	|	НДСПредъявленныйРеализация0Остатки.Поставщик КАК Поставщик,
	|	НДСПредъявленныйРеализация0Остатки.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(&МоментВремени, СчетФактура = &СчетФактура) КАК НДСПредъявленныйРеализация0Остатки
	|ГДЕ
	|	НДСПредъявленныйРеализация0Остатки.НДСОстаток + НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток > 0";
	ОстаткиПоСФ = Запрос.Выполнить().Выгрузить();
	
	Отбор = Новый Структура("ВидЦенности,СтавкаНДС,СчетУчетаНДС");
	
	Для каждого СтрокаТаблицы Из ДанныеДвиженийСторно Цикл
		
		СуммаБезНДС = -СтрокаТаблицы.СуммаБезНДС;
		НДС = -СтрокаТаблицы.НДС;
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		НайденныеСтроки = ОстаткиПоСФ.НайтиСтроки(Отбор);
		
		Для каждого СтрокаОстатков Из НайденныеСтроки Цикл
			
			СуммаБезНДСКУменьшению = Мин(СуммаБезНДС, СтрокаОстатков.СуммаБезНДС);
			НДСКУменьшению = Мин(НДС, СтрокаОстатков.НДС);
			
			Если СуммаБезНДСКУменьшению > 0 И НДСКУменьшению > 0 Тогда
				
				НоваяСтрока = ДанныеДвиженийПриход.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы,,"Состояние,ДокументОтгрузки,СуммаБезНДС,НДС");
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОстатков, "Поставщик,ИсправленныйСчетФактура,Состояние,ДокументОтгрузки");
				
				НоваяСтрока.СуммаБезНДС = -СуммаБезНДСКУменьшению;
				НоваяСтрока.НДС = -НДСКУменьшению;
				
				СуммаБезНДС = СуммаБезНДС - СуммаБезНДСКУменьшению;
				НДС = НДС - НДСКУменьшению;
				
			Иначе
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
						
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ДанныеДвиженийПриход Цикл

		Запись = Движения.НДСПредъявленныйРеализация0.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
	
	КонецЦикла;

	Движения.НДСПредъявленныйРеализация0.Записывать = Истина;
	
КонецПроцедуры

Процедура СформироватьДвиженияНДСКорректировкиПоступлениеТоваровУслуг(РеквизитыДокумента, СтруктураТаблицДокумента, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(РеквизитыДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = РеквизитыДокумента[0];
	Если Не Реквизиты.РаздельныйУчетНДСНаСчете19 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПолучитьРеквизитыКорректировкиПоступленияПоНДС(РеквизитыДокумента);
	Реквизиты = Параметры.Реквизиты[0];
	
	// Корректировочный счет-фактура
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
		
		// Уменьшение стоимости
		ОтразитьКорректировкуПоступленияВКнигеПродаж(
			Реквизиты,
			СтруктураТаблицДокумента,
			Движения,
			Отказ);
		
		// Увеличение стоимости
		ОтразитьКорректировкуПоступленияВКнигеПокупок(
			Реквизиты,
			СтруктураТаблицДокумента.ТаблицаВычет,
			СтруктураТаблицДокумента.КнигаПокупок,
			Движения,
			Отказ);
		
		// Исправительный счет-фактура либо исправление собственной ошибки
	Иначе
		
		Если Реквизиты.СокращеннаяАналитика Тогда
			// Сторно движений исправляемого счета-фактуры
			УчетНДС.СформироватьСторнирующиеДвиженияКорректировкаПоступления(
				Реквизиты,
				СтруктураТаблицДокумента,
				Движения,
				Отказ);
			
			// Новые движения по исправленному счету-фактуре
			СформироватьИсправленныеДвиженияКорректировкаПоступления(
				Реквизиты,
				СтруктураТаблицДокумента,
				Движения,
				Отказ);
		Иначе
			
			// Сторно движений исправляемого счета-фактуры
			УчетНДС.СформироватьСторнирующиеДвиженияКорректировкаПоступления(
				Реквизиты,
				СтруктураТаблицДокумента,
				Движения,
				Отказ);
			
			УчетНДС.СформироватьДвиженияНДСПредъявленныйРеализация0Расход(
				СтруктураТаблицДокумента.НДСПредъявленныйРеализация0,
				Реквизиты,
				Движения,
				Отказ);
			
			СформироватьДвиженияНДСРаздельныйУчетВыбытиеЦенностей(
				СтруктураТаблицДокумента.НДСРаздельныйУчет,
				Движения,
				Отказ);
			
			// Возвращаем состорнированные записи с исправленным счет-фактурой
			СформироватьВозвратОтсторнированныхДвиженийКорректировкаПоступления(
				Реквизиты,
				СтруктураТаблицДокумента,
				Движения,
				Отказ);
			
			// Новые движения по исправленному счету-фактуре на дельту
			СформироватьИсправленныеДвиженияКорректировкаПоступления(
				Реквизиты,
				СтруктураТаблицДокумента,
				Движения,
				Отказ);
			
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьРеквизитыКорректировкиПоступленияПоНДС(РеквизитыДокумента)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы шапки документа
	СписокОбязательныхКолонок = ""
	+ "ВидОперации,"
	+ "Регистратор,"
	+ "Период,"
	+ "Организация,"
	+ "ИсправляемыйСчетФактура,"
	+ "СчетФактура,"
	+ "КорректироватьБУиНУ,"
	+ "ДокументПоступленияСсылка,"
	+ "ВосстановитьНДС,"
	+ "Склад,"
	+ "РаздельныйУчетНДС,"
	+ "РаздельныйУчетНДСНаСчете19,"
	+ "КурсДокумента,"
	+ "КратностьДокумента,"
	+ "ДокументОснование,"
	+ "НДСВключенВСтоимость,"
	+ "ДоговорКонтрагента,"
	+ "Контрагент,"
	+ "Подразделение,"
	+ "ВестиПартионныйУчет,"
	+ "СокращеннаяАналитика,"
	+ "ДокументПоступленияДата";
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(РеквизитыДокумента, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Процедура СформироватьИсправленныеДвиженияКорректировкаПоступления(Реквизиты, СтруктураТаблицДокумента, Движения, Отказ)
	
	Если ТипЗнч(Реквизиты.ИсправляемыйСчетФактура) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		// Исправление корректировочного счета-фактуры, уменьшение стоимости.
		ОтразитьКорректировкуПоступленияВКнигеПродаж(
			Реквизиты,
			СтруктураТаблицДокумента,
			Движения,
			Отказ);
	КонецЕсли;
	
	Если Реквизиты.СокращеннаяАналитика Тогда 
		
		ОтразитьКорректировкуПоступленияВКнигеПокупок(
		Реквизиты,
		СтруктураТаблицДокумента.ТаблицаВычет,
		СтруктураТаблицДокумента.КнигаПокупок,
		Движения,
		Отказ);
		
	Иначе
		
		ОтразитьКорректировкуПоступленияВКнигеПокупокСамодостаточнаяАналитика(
		Реквизиты,
		СтруктураТаблицДокумента,
		Движения,
		Отказ);
		
	КонецЕсли;

КонецПроцедуры

Процедура ОтразитьКорректировкуПоступленияВКнигеПродаж(Реквизиты, СтруктураТаблицДокумента, Движения, Отказ)

	Если Не ЗначениеЗаполнено(СтруктураТаблицДокумента.ТаблицаВосстановление) Тогда
		Возврат;
	КонецЕсли;
		
	ТаблицаУменьшениеВычета = СтруктураТаблицДокумента.ТаблицаВосстановление;
	
	ПодготовитьТаблицуПоЦенностям(ТаблицаУменьшениеВычета, Реквизиты, Отказ);
	
	ТаблицаУменьшениеВычета.Колонки.Добавить("СуммаБезНДСПринятоКВычету",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаУменьшениеВычета.Колонки.Добавить("НДСПринятоКВычету",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор));
	Запрос.УстановитьПараметр("СчетФактура", ?(НачалоКвартала(Реквизиты.Период) = НачалоКвартала(Реквизиты.ДокументПоступленияДата),
		Реквизиты.ДокументПоступленияСсылка, Реквизиты.ДокументОснование));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДСПредъявленныйОстатки.Организация,
	|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйОстатки.ВидЦенности,
	|	НДСПредъявленныйОстатки.СтавкаНДС,
	|	НДСПредъявленныйОстатки.СчетУчетаНДС,
	|	НДСПредъявленныйОстатки.Поставщик,
	|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСПредъявленныйОстатки.НДСОстаток КАК НДС,
	|	НДСПредъявленныйОстатки.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(&МоментВремени, СчетФактура = &СчетФактура) КАК НДСПредъявленныйОстатки
	|ГДЕ
	|	НДСПредъявленныйОстатки.НДСОстаток + НДСПредъявленныйОстатки.СуммаБезНДСОстаток > 0";
	
	ОстаткиПоСФ = Запрос.Выполнить().Выгрузить();
	ОстаткиПоСФ.Индексы.Добавить("ВидЦенности,СтавкаНДС,СчетУчетаНДС");
	
	Отбор = Новый Структура("ВидЦенности,СтавкаНДС,СчетУчетаНДС");

	Для каждого СтрокаТаблицы Из ТаблицаУменьшениеВычета Цикл
		
		Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету Тогда
			СуммаБезНДСПринятоКВычету = СтрокаТаблицы.СуммаБезНДС;
			НДСПринятоКВычету         = СтрокаТаблицы.НДС;
		ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется Тогда
			Если СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету <> 0
			 ИЛИ СтрокаТаблицы.НДСПринимаетсяКВычету <> 0 Тогда
				СуммаБезНДСПринятоКВычету = СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету;
				НДСПринятоКВычету         = СтрокаТаблицы.НДСПринимаетсяКВычету;
			Иначе
				Продолжить;
			КонецЕсли;
		Иначе
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		НайденныеСтроки = ОстаткиПоСФ.НайтиСтроки(Отбор);
		
		Для каждого СтрокаОстатков Из НайденныеСтроки Цикл
			
			СуммаБезНДСПринятоКВычету = СуммаБезНДСПринятоКВычету - СтрокаОстатков.СуммаБезНДС;
			НДСПринятоКВычету = НДСПринятоКВычету - СтрокаОстатков.НДС;
			
			СтрокаОстатков.СуммаБезНДС = ?(СуммаБезНДСПринятоКВычету < 0, -СуммаБезНДСПринятоКВычету, 0);
			СтрокаОстатков.НДС = ?(НДСПринятоКВычету < 0, -НДСПринятоКВычету, 0);
			
		КонецЦикла;
			
		СтрокаТаблицы.СуммаБезНДСПринятоКВычету = Макс(СуммаБезНДСПринятоКВычету, 0);
		СтрокаТаблицы.НДСПринятоКВычету = Макс(НДСПринятоКВычету, 0);

	КонецЦикла;
	
	ТаблицаНДСПредъявленный = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленный");
	ТаблицаНДСЗаписиКнигиПродаж = СтруктураТаблицДокумента.ТаблицаВосстановление.СкопироватьКолонки();
	
	Для каждого СтрокаТаблицы Из ТаблицаУменьшениеВычета Цикл
		
		Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости
		 ИЛИ СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТаблицы.НДСПринятоКВычету > 0
		 ИЛИ СтрокаТаблицы.СуммаБезНДСПринятоКВычету > 0 Тогда 
		
			НоваяСтрока = ТаблицаНДСЗаписиКнигиПродаж.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.СчетФактура = Реквизиты.СчетФактура;
			НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСПринятоКВычету;
			НоваяСтрока.НДС = СтрокаТаблицы.НДСПринятоКВычету;
			
		КонецЕсли;
		
		Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету Тогда
			НДС = СтрокаТаблицы.НДС - СтрокаТаблицы.НДСПринятоКВычету;
			СуммаБезНДС = СтрокаТаблицы.СуммаБезНДС - СтрокаТаблицы.СуммаБезНДСПринятоКВычету;
		Иначе
			НДС = СтрокаТаблицы.НДСПринимаетсяКВычету - СтрокаТаблицы.НДСПринятоКВычету;
			СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету - СтрокаТаблицы.СуммаБезНДСПринятоКВычету;
		КонецЕсли;
		
		Если НДС > 0 
		 ИЛИ СуммаБезНДС > 0 Тогда
		
			// Сторнирующие движения по исправляемому документу
			НоваяСтрока = ТаблицаНДСПредъявленный.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"Событие,СуммаБезНДС,НДС");
			НоваяСтрока.СчетФактура = ?(НачалоКвартала(Реквизиты.Период) = НачалоКвартала(Реквизиты.ДокументПоступленияДата),
				Реквизиты.ДокументПоступленияСсылка, Реквизиты.ДокументОснование);
			НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком;
			НоваяСтрока.Поставщик = СтрокаТаблицы.Покупатель;
			НоваяСтрока.СуммаБезНДС = -СуммаБезНДС;
			НоваяСтрока.НДС = -НДС;
			
		КонецЕсли;
		
	КонецЦикла;
	
	УчетНДС.СформироватьДвиженияНДСПредъявленный(
		ТаблицаНДСПредъявленный, Движения, Отказ);
	СформироватьДвиженияНДСЗаписиКнигиПродажКорректировкаПоступления(
		Реквизиты, ТаблицаНДСЗаписиКнигиПродаж, Ложь, Движения, Отказ);

КонецПроцедуры

Процедура ОтразитьКорректировкуПоступленияВКнигеПокупок(
	Реквизиты, ВычетНДСНаОснованииДокумента, ТаблицаСторноВычета, Движения, Отказ)

	ОтслеживатьРасхождения = ВычетНДСНаОснованииДокумента<> Неопределено
		И ВычетНДСНаОснованииДокумента.Колонки.Найти("СуммаНДСПерепоставкиРуб") <> Неопределено;
		
	Если НулевыеСуммыВТаблице(ВычетНДСНаОснованииДокумента, ОтслеживатьРасхождения) Тогда 
		Возврат;
	КонецЕсли;
	
	// Приход всей суммы предъявленного НДС по регистру "НДС предъявленный"
	УчетНДС.СформироватьДвиженияНДСПредъявленный(
		ВычетНДСНаОснованииДокумента,
		Движения,
		Отказ);
	
	Если ВычетНДСНаОснованииДокумента.Итог("СуммаНДСПерепоставкиРуб")<> 0
		И Реквизиты.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
		СформироватьПроводкиПерепоставка(Реквизиты, ВычетНДСНаОснованииДокумента, Движения, Отказ)
	КонецЕсли;
	
	// Определение способов учета НДС
	СтруктураТаблиц = ПодготовитьТаблицыПоСпособамУчета(
		Реквизиты,
		ВычетНДСНаОснованииДокумента,
		ТаблицаСторноВычета,
		Отказ);
	
	// Блокировка НДС, не подлежащего вычету
	УчетНДС.СформироватьДвиженияНДСПредъявленный(
		СтруктураТаблиц.НеПринимаемыйКВычетуНДС, 
		Движения,
		Отказ,
		Истина);
	
	// Приход распределенного ранее НДС
	УчетНДС.СформироватьДвиженияНДСПредъявленный(
		СтруктураТаблиц.РаспределеныйНДС, 
		Движения,
		Отказ);
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеСобственнойОшибки 
		И СтруктураТаблиц.ПринимаемыйКВычетуНДС.Количество() > 0 Тогда
		// Запись дополнительного листа при исправлении собственной ошибки отразим сразу
		
		УчетНДС.СформироватьДвиженияНДСПредъявленный(
			СтруктураТаблиц.ПредъявленныйНДСПринимаемыйКВычету,
			Движения,
			Отказ,
			Истина);
		
		СформироватьДвиженияЗаписьКнигиПокупок(
			Реквизиты,
			СтруктураТаблиц.ПринимаемыйКВычетуНДС,
			Движения,
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьПроводкиПерепоставка(Реквизиты, ВычетНДСНаОснованииДокумента, Движения, Отказ)
	
	Если Не ЗначениеЗаполнено(ВычетНДСНаОснованииДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ВычетНДСНаОснованииДокумента Цикл
		
		Если СтрокаТаблицы.СуммаНДСПерепоставкиРуб = 0 
			ИЛИ СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости Тогда 
			Продолжить;
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		
		Проводка.СчетДт = СтрокаТаблицы.СчетУчетаНДС;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СФПолученные", СтрокаТаблицы.Регистратор);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Контрагенты",  Реквизиты.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СпособыУчетаНДС", СтрокаТаблицы.СпособУчетаНДС);
		
		Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СФПолученные",    Реквизиты.ДокументПоступленияСсылка);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты",     Реквизиты.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СпособыУчетаНДС", СтрокаТаблицы.СпособУчетаНДС);
		
		Проводка.Сумма = СтрокаТаблицы.СуммаНДСПерепоставкиРуб;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьТаблицыПоСпособамУчета(Реквизиты, ВычетНДСНаОснованииДокумента, ТаблицаСторноВычета, Отказ)
	
	ПодготовитьТаблицуПоЦенностям(ВычетНДСНаОснованииДокумента, Реквизиты, Отказ);
	
	ПредъявленныйНДСПринимаемыйКВычету = ВычетНДСНаОснованииДокумента.СкопироватьКолонки();
	НеПринимаемыйКВычетуНДС = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленный");
	РаспределеныйНДС = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленный");
	
	ОтслеживатьРасхождения = ВычетНДСНаОснованииДокумента.Колонки.Найти("СуммаНДСПерепоставкиРуб") <> Неопределено;
	
	Для Каждого СтрокаТаблицы Из ВычетНДСНаОснованииДокумента Цикл
		
		Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости Тогда
			Событие = Перечисления.СобытияПоНДСПокупки.НДСВключенВСтоимость;
		ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 Тогда
			Событие = Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0;
		ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется Тогда
			Событие = Перечисления.СобытияПоНДСПокупки.НДСПодлежитРаспределению;
		ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету Тогда
			НоваяСтрока = ПредъявленныйНДСПринимаемыйКВычету.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"Событие");
			НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком;
			Продолжить;
		Иначе
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = НеПринимаемыйКВычетуНДС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"Событие");
		Если ОтслеживатьРасхождения
			И (СтрокаТаблицы.СуммаНДСПерепоставкиРуб <> 0 ИЛИ СтрокаТаблицы.СуммаПерепоставкиРуб <> 0) Тогда
			НоваяСтрока.НДС         = СтрокаТаблицы.СуммаНДСПерепоставкиРуб;
			НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаПерепоставкиРуб - СтрокаТаблицы.СуммаНДСПерепоставкиРуб;
		КонецЕсли;
		
		НоваяСтрока.Событие = Событие;
		
		Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется Тогда
			
			Если СтрокаТаблицы.НДСПринимаетсяКВычету > 0
				ИЛИ СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету > 0 Тогда
				НоваяСтрока = РаспределеныйНДС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"СуммаБезНДС,НДС,Событие");
				НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету;
				НоваяСтрока.НДС = СтрокаТаблицы.НДСПринимаетсяКВычету;
				НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.НДСРаспределен;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Если по исправляемому счету-фактуре был отражен и сторнирован вычет НДС
	// сформируем таблицу для отражения вычета НДС по исправленному счету-фактуре
	// Вычет отразим в доп листе или основном разделе той книги покупок, 
	// в которой ранее осуществили сторнирующую запись доп листа

	Если ТаблицаСторноВычета.Количество() > 0 Тогда
		
		ПринимаемыйКВычетуНДС = ПредъявленныйНДСПринимаемыйКВычету.Скопировать();
		
		СтрокаСторнирующейЗаписи = ТаблицаСторноВычета[0];
		ПринимаемыйКВычетуНДС.ЗаполнитьЗначения(
			СтрокаСторнирующейЗаписи.ЗаписьДополнительногоЛиста, "ЗаписьДополнительногоЛиста");
		ПринимаемыйКВычетуНДС.ЗаполнитьЗначения(
			СтрокаСторнирующейЗаписи.КорректируемыйПериод, "КорректируемыйПериод");
		ПринимаемыйКВычетуНДС.ЗаполнитьЗначения(НСтр("ru='НДС'"), "Содержание");
		ПринимаемыйКВычетуНДС.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету, "Событие");
		ПринимаемыйКВычетуНДС.ЗаполнитьЗначения(СтрокаСторнирующейЗаписи.ДатаСобытия, "ДатаСобытия");
		
	Иначе
		ПринимаемыйКВычетуНДС = ВычетНДСНаОснованииДокумента.СкопироватьКолонки();
		ПредъявленныйНДСПринимаемыйКВычету = ВычетНДСНаОснованииДокумента.СкопироватьКолонки();
	КонецЕсли;
	
	Возврат Новый Структура(
		"РаспределеныйНДС, ПредъявленныйНДСПринимаемыйКВычету, ПринимаемыйКВычетуНДС, НеПринимаемыйКВычетуНДС", 
		 РаспределеныйНДС, ПредъявленныйНДСПринимаемыйКВычету, ПринимаемыйКВычетуНДС, НеПринимаемыйКВычетуНДС);

КонецФункции

Процедура ПодготовитьТаблицуПоЦенностям(ТаблицаПоЦенностям, Реквизиты, Отказ)
	
	ТаблицаПоЦенностям.Колонки.Добавить("НДСПринимаетсяКВычету",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("НДСУчитываетсяВCтоимости",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("НДСДляОперацийПо0",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("СуммаБезНДСПринимаетсяКВычету",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("СуммаБезНДСУчитываетсяВCтоимости",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаПоЦенностям.Колонки.Добавить("СуммаБезНДСДляОперацийПо0",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	ПериодПоступления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.ДокументПоступленияСсылка, "Дата");
	
	Если НачалоКвартала(ПериодПоступления) < НачалоКвартала(Реквизиты.Период) Тогда
		
		ТаблицаПоЦенностям.Индексы.Добавить("СпособУчетаНДС");
		ОтборРаспределяется = Новый Структура("СпособУчетаНДС", Перечисления.СпособыУчетаНДС.Распределяется);
		СтрокиКРаспределению = ТаблицаПоЦенностям.НайтиСтроки(ОтборРаспределяется);
		
		Если СтрокиКРаспределению.Количество() > 0 Тогда

			МенеджерЗаписи = РегистрыСведений.БазаРаспределенияНДС.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период = НачалоКвартала(ПериодПоступления);
			МенеджерЗаписи.Организация = Реквизиты.Организация;
			МенеджерЗаписи.Прочитать();
				
			Если МенеджерЗаписи.Выбран() Тогда
				
				БазаРаспределения = МенеджерЗаписи.ВыручкаНДС + МенеджерЗаписи.ВыручкаБезНДС 
									+ МенеджерЗаписи.ВыручкаЕНВД + МенеджерЗаписи.ВыручкаНДС0;
				
				Для Каждого СтрокаТаблицы Из СтрокиКРаспределению Цикл
					
					УчтеноСуммаБезНДС = 0;
					УчтеноНДС     	  = 0;
					УчтеноВыручки	  = 0;
					
					Если МенеджерЗаписи.ВыручкаНДС <> 0 Тогда

						СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету = Окр(СтрокаТаблицы.СуммаБезНДС
							* (МенеджерЗаписи.ВыручкаНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноСуммаБезНДС;
						СтрокаТаблицы.НДСПринимаетсяКВычету = Окр(СтрокаТаблицы.НДС
							* (МенеджерЗаписи.ВыручкаНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

						УчтеноСуммаБезНДС = УчтеноСуммаБезНДС + СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету;
						УчтеноНДС         = УчтеноНДС + СтрокаТаблицы.НДСПринимаетсяКВычету;
						УчтеноВыручки     = УчтеноВыручки + МенеджерЗаписи.ВыручкаНДС;
						
					КонецЕсли;

					Если МенеджерЗаписи.ВыручкаБезНДС + МенеджерЗаписи.ВыручкаЕНВД <> 0 Тогда

						СтрокаТаблицы.СуммаБезНДСУчитываетсяВCтоимости = Окр(СтрокаТаблицы.СуммаБезНДС
							* (МенеджерЗаписи.ВыручкаБезНДС + МенеджерЗаписи.ВыручкаЕНВД  + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноСуммаБезНДС;
						СтрокаТаблицы.НДСУчитываетсяВCтоимости = Окр(СтрокаТаблицы.НДС
							* (МенеджерЗаписи.ВыручкаБезНДС + МенеджерЗаписи.ВыручкаЕНВД + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

						УчтеноСуммаБезНДС = УчтеноСуммаБезНДС + СтрокаТаблицы.СуммаБезНДСУчитываетсяВCтоимости;
						УчтеноНДС         = УчтеноНДС + СтрокаТаблицы.НДСУчитываетсяВCтоимости;
						УчтеноВыручки     = УчтеноВыручки + МенеджерЗаписи.ВыручкаБезНДС + МенеджерЗаписи.ВыручкаЕНВД;

					КонецЕсли;

					Если МенеджерЗаписи.ВыручкаНДС0 <> 0 Тогда

						СтрокаТаблицы.СуммаБезНДСДляОперацийПо0 = Окр(СтрокаТаблицы.СуммаБезНДС
							* (МенеджерЗаписи.ВыручкаНДС0 + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноСуммаБезНДС;
						СтрокаТаблицы.НДСДляОперацийПо0 = Окр(СтрокаТаблицы.НДС
							* (МенеджерЗаписи.ВыручкаНДС0 + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

						УчтеноСуммаБезНДС = УчтеноСуммаБезНДС + СтрокаТаблицы.СуммаБезНДСДляОперацийПо0;
						УчтеноНДС         = УчтеноНДС + СтрокаТаблицы.НДСДляОперацийПо0;
						УчтеноВыручки     = УчтеноВыручки + МенеджерЗаписи.ВыручкаНДС0;

					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				
				Если НЕ Отказ Тогда
				
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Не обнаружена база распределения НДС за %1'"),
						Формат(ПериодПоступления, "ДФ='к ''Квартал'' гггг  ''г.'''"));
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
		
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьВозвратОтсторнированныхДвиженийКорректировкаПоступления(Реквизиты, СтруктураТаблицДокумента,
																	Движения, Отказ)
																	
	// Вернем остатки по регистру НДСПредъявленный с заполненным исправленным счет-фактурой
	ТаблицаНДСПредъявленный = СтруктураТаблицДокумента.НДСПредъявленный.СкопироватьКолонки();
	ТаблицаНДСПредъявленный.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
	ТаблицаНДСПредъявленный.Колонки.Добавить("Событие", Новый ОписаниеТипов("ПеречислениеСсылка.СобытияПоНДСПокупки"));
	Для каждого Строка Из СтруктураТаблицДокумента.НДСПредъявленный Цикл
		Если Строка.НДС <> 0 Тогда
			НоваяСтрока = ТаблицаНДСПредъявленный.Добавить();
			ЗаполнитьЗначенияСвойств (НоваяСтрока, Строка);
			НоваяСтрока.ИсправленныйСчетФактура = Реквизиты.Регистратор;
			НоваяСтрока.ДатаСобытия = Реквизиты.Период;
			НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету;
		КонецЕсли;
	КонецЦикла; 
	
	СформироватьДвиженияНДСПредъявленный(
	ТаблицаНДСПредъявленный,
	Движения,
	Отказ);
	
	// Вернем остатки по регистру НДСПредъявленныйРеализация0 с заполненным исправленным счет-фактурой
	ТаблицаНДСПредъявленныйРеализация0 = СтруктураТаблицДокумента.НДСПредъявленныйРеализация0;
	ТаблицаНДСПредъявленныйРеализация0.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
	ТаблицаНДСПредъявленныйРеализация0.Колонки.Добавить("Событие", 
		Новый ОписаниеТипов("ПеречислениеСсылка.СобытияПоНДСПокупки"));
		
	Для каждого Стр Из ТаблицаНДСПредъявленныйРеализация0 Цикл
		Стр.ДатаСобытия = Реквизиты.Период;
		Стр.ИсправленныйСчетФактура = Реквизиты.Регистратор;
		Если Стр.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 Тогда
			Стр.Событие = Перечисления.СобытияПоНДСПокупки.ПодтвержденаСтавка0;
		ИначеЕсли Стр.Состояние = Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение Тогда
			Стр.Событие = Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0;
		КонецЕсли;
	КонецЦикла;
	
	СформироватьДвиженияНДСПредъявленныйРеализация0ПоступлениеЦенностей(
	ТаблицаНДСПредъявленныйРеализация0,
	Движения,
	Отказ);
	
	// Вернем остатки по регистру НДСРаздельныйУчет с заполненным исправленным счет-фактурой
	ДвиженияНДСРаздельныйУчет = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСРаздельныйУчет");
	Для каждого Стр Из СтруктураТаблицДокумента.НДСРаздельныйУчет Цикл
		
		АналитикаУчетаНДС = Новый Структура("Организация,СчетФактура,ВидЦенности,СчетУчетаНДС,
		|СтавкаНДС,Поставщик,ИсправленныйСчетФактура",
		Стр.Организация,
		Стр.СчетФактура,
		Стр.ВидЦенности,
		Стр.СчетУчетаНДС,
		Стр.СтавкаНДС,
		Реквизиты.Контрагент,
		Реквизиты.Регистратор);
		НоваяСтрока = ДвиженияНДСРаздельныйУчет.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		НоваяСтрока.АналитикаУчетаНДС =
			Справочники.КлючиАналитикиУчетаНДС.КлючиАналитикиУчетаНДСДокумента(АналитикаУчетаНДС);
		
	КонецЦикла;

	СформироватьДвиженияНДСРаздельныйУчетПоступлениеЦенностей(
	ДвиженияНДСРаздельныйУчет,
	Движения,
	Отказ);
	
КонецПроцедуры

Процедура ОтразитьКорректировкуПоступленияВКнигеПокупокСамодостаточнаяАналитика(
	Реквизиты, СтруктураТаблиц, Движения, Отказ)

	ВычетНДСНаОснованииДокумента = СтруктураТаблиц.ВычетНДСНаОснованииДокумента;
	ТаблицаСторноВычета          = СтруктураТаблиц.КнигаПокупок;
	// Движения по регистру "НДС предъявленный" приход предъявленного НДС
	Если ВычетНДСНаОснованииДокумента <> Неопределено
		И ВычетНДСНаОснованииДокумента.Количество() > 0 Тогда
		
		НДСПредъявленныйРеализация0 = ПолучитьОстаткиДляРаспределенияНДСПредъявленныйРеализация0(Реквизиты);
		
		// Определение способов учета НДС
		ТаблицыРаспределения = ПодготовитьТаблицыПоСпособамУчетаСамодостаточныеЗаписи(
			Реквизиты,
			СтруктураТаблиц,
			НДСПредъявленныйРеализация0,
			Отказ);
		
		
		СформироватьДвиженияНДСРаздельныйУчетПоступлениеЦенностей(
			ТаблицыРаспределения.ДвиженияНДСРаздельныйУчет,
			Движения,
			Отказ);
		
		ТаблицаНДСПредъявленныйРеализация0 = ПодготовитьТаблицуДвиженийНДСПредъявленныйРеализация0(Реквизиты,
		ТаблицыРаспределения.НДСДляОперацийПо0,
		НДСПредъявленныйРеализация0);
		
		СформироватьДвиженияНДСПредъявленныйРеализация0ПоступлениеЦенностей(
			ТаблицаНДСПредъявленныйРеализация0,
			Движения,
			Отказ);
		
		СформироватьДвиженияНДСПредъявленный(
			ТаблицыРаспределения.ПринимаемыйКВычетуНДС,
			Движения,
			Отказ);
		
	ИначеЕсли ТаблицаСторноВычета.Количество() > 0 Тогда
		
		ПредъявленныйНДСПринимаемыйКВычету = ТаблицаСторноВычета.Скопировать();
		
		Для каждого СтрокаТаблицы Из ПредъявленныйНДСПринимаемыйКВычету Цикл
			СтрокаТаблицы.СуммаБезНДС             = - СтрокаТаблицы.СуммаБезНДС;
			СтрокаТаблицы.НДС                     = - СтрокаТаблицы.НДС;
			СтрокаТаблицы.Содержание              = НСтр("ru='НДС'");
			СтрокаТаблицы.ИсправленныйСчетФактура = Реквизиты.Регистратор;
		КонецЦикла;
		
		СформироватьДвиженияЗаписьКнигиПокупок(
			Реквизиты,
			ПредъявленныйНДСПринимаемыйКВычету,
			Движения,
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьОстаткиДляРаспределенияНДСПредъявленныйРеализация0(Реквизиты)
	
	Запрос = Новый Запрос();
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Остатки.Организация КАК Организация,
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.Состояние КАК Состояние,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки КАК ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Остатки.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Остатки.Поставщик КАК Поставщик,
	|	&Ссылка КАК ИсправленныйСчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСПредъявленныйРеализация0Остатки.НДСОстаток КАК НДС,
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|			&МоментДокумента,
	|			Организация = &Организация
	|				И СчетФактура = &ИсправляемыйСчетФактура
	|				И НЕ СтавкаНДС В (&ИсключенныеСтавкиНДС)) КАК НДСПредъявленныйРеализация0Остатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Обороты.Организация,
	|	НДСПредъявленныйРеализация0Обороты.СчетФактура,
	|	НДСПредъявленныйРеализация0Обороты.Состояние,
	|	НДСПредъявленныйРеализация0Обороты.ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Обороты.ВидЦенности,
	|	НДСПредъявленныйРеализация0Обороты.СтавкаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Обороты.Поставщик,
	|	&Ссылка,
	|	НДСПредъявленныйРеализация0Обороты.СуммаБезНДСРасход,
	|	НДСПредъявленныйРеализация0Обороты.НДСРасход,
	|	&Ссылка,
	|	&Период
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Обороты(
	|			,
	|			&МоментДокумента,
	|			,
	|			Организация = &Организация
	|				И СчетФактура = &ИсправляемыйСчетФактура
	|				И Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0)
	|				И НЕ СтавкаНДС В (&ИсключенныеСтавкиНДС)) КАК НДСПредъявленныйРеализация0Обороты";

	Запрос.Текст = ТекстЗапроса;
	ИсключенныеСтавкиНДС = Новый Массив;
	ИсключенныеСтавкиНДС.Добавить(Перечисления.СтавкиНДС.БезНДС);
	ИсключенныеСтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС0);
	Запрос.УстановитьПараметр("МоментДокумента",
		Новый Граница(Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация",				Реквизиты.Организация);
	Запрос.УстановитьПараметр("Период",						Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",						Реквизиты.Регистратор);
	Запрос.УстановитьПараметр("ИсправляемыйСчетФактура",	Реквизиты.ИсправляемыйСчетФактура);
	Запрос.УстановитьПараметр("ИсключенныеСтавкиНДС", 		ИсключенныеСтавкиНДС);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() > 0 Тогда
		Возврат Результат;
	Иначе
		Возврат ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленныйРеализация0");
	КонецЕсли;
	
КонецФункции

Функция ПодготовитьТаблицуДвиженийНДСПредъявленныйРеализация0(Реквизиты, НДСДляОпераций0, НДСПредъявленныйРеализация0)
	
	ТаблицаНДСПредъявленныйРеализация0 = 
	ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленныйРеализация0");
	
	Если НДСДляОпераций0.Количество() = 0 Тогда
		Возврат ТаблицаНДСПредъявленныйРеализация0;
	КонецЕсли;
	
	НДСПредъявленныйРеализация0ПоСчетам = НДСПредъявленныйРеализация0.Скопировать(,"СчетУчетаНДС, НДС, СуммаБезНДС");
	НДСПредъявленныйРеализация0ПоСчетам.Свернуть("СчетУчетаНДС", "НДС, СуммаБезНДС");
	НДСДляОпераций0.Свернуть("СтавкаНДС, СчетУчетаНДС", "НДС, СуммаБезНДС");
	Для каждого СтрокаПоСтавкеНДС Из НДСДляОпераций0 Цикл
		НДСКРаспределению         = СтрокаПоСтавкеНДС.НДС;
		СуммаБезНДСКРаспределению = СтрокаПоСтавкеНДС.СуммаБезНДС;
		СтавкаНДС = СтрокаПоСтавкеНДС.СтавкаНДС;
		
		Сч = 1;
		СчНДС = 0;
		СчСуммаБезНДС = 0;
		Отбор = Новый Структура("СчетУчетаНДС", СтрокаПоСтавкеНДС.СчетУчетаНДС);
		СтрокиДляРаспределенияПоСчету = НДСПредъявленныйРеализация0.НайтиСтроки(Отбор);
		СтрокиДляПолученияИтогов = НДСПредъявленныйРеализация0ПоСчетам.НайтиСтроки(Отбор);
		Если СтрокиДляПолученияИтогов.Количество() > 0 Тогда
			НДСОстаток = СтрокиДляПолученияИтогов[0].НДС;
			СуммаБезНДСОстаток = СтрокиДляПолученияИтогов[0].СуммаБезНДС;
			ОбщееКоличество = СтрокиДляРаспределенияПоСчету.Количество();
		Иначе
			НДСОстаток = 1;
			СуммаБезНДСОстаток = 1;
			ОбщееКоличество = 0;
		КонецЕсли;
		
		Для Каждого Стр Из СтрокиДляРаспределенияПоСчету Цикл
			НовСтрока = ТаблицаНДСПредъявленныйРеализация0.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, Стр);
			НовСтрока.ДатаСобытия = Реквизиты.Период;
			НовСтрока.ИсправленныйСчетФактура = Реквизиты.Регистратор;
			НовСтрока.СтавкаНДС = СтавкаНДС;
			Если НовСтрока.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 Тогда
				НовСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПодтвержденаСтавка0;
			ИначеЕсли Стр.Состояние = Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение Тогда
				НовСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0;
			КонецЕсли;
			Если Сч = ОбщееКоличество Тогда
				НовСтрока.НДС = НДСКРаспределению - СчНДС;
				НовСтрока.СуммаБезНДС = СуммаБезНДСКРаспределению - СчСуммаБезНДС;
			Иначе
				НовСтрока.НДС = Окр(НДСКРаспределению / НДСОстаток * Стр.НДС,2);
				НовСтрока.СуммаБезНДС = Окр(СуммаБезНДСКРаспределению / СуммаБезНДСОстаток * Стр.СуммаБезНДС,2);
				СчНДС = СчНДС + НовСтрока.НДС;
				СчСуммаБезНДС = СчСуммаБезНДС + НовСтрока.СуммаБезНДС;
				Сч = Сч + 1;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаНДСПредъявленныйРеализация0.Свернуть("ВидЦенности, ДатаСобытия, ДокументОтгрузки, ИсправленныйСчетФактура,
	|Организация, Период, Поставщик, Событие, Состояние, СтавкаНДС, СчетУчетаНДС, СчетФактура", 
	"НДС, СуммаБезНДС");
	
	Возврат ТаблицаНДСПредъявленныйРеализация0;
	
КонецФункции

Функция ПодготовитьТаблицыПоСпособамУчетаСамодостаточныеЗаписи(Реквизиты, СтруктураТаблиц,
										ТаблицаНДСПредъявленныйРеализация0, Отказ)
	
	ВычетНДСНаОснованииДокумента       = СтруктураТаблиц.ВычетНДСНаОснованииДокумента;
	ТаблицаСторноВычета                = СтруктураТаблиц.КнигаПокупок;
	ТаблицаНДСРаздельныйУчет           = СтруктураТаблиц.НДСРаздельныйУчет;
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИзмененаСтавкаНДС", Истина);
	ТаблицаПоИзмененымСтавкамНДС = ВычетНДСНаОснованииДокумента.Скопировать(СтруктураОтбора);
	Для Каждого Стр Из ТаблицаПоИзмененымСтавкамНДС Цикл
		Стр.НДС			 = Стр.НДСДоИзменения;
		Стр.СуммаБезНДС  = Стр.СуммаБезНДСРубДоИзменения;
	КонецЦикла;
	
	ПодготовитьТаблицуПоЦенностям(ВычетНДСНаОснованииДокумента, Реквизиты, Отказ);
	ПодготовитьТаблицуПоЦенностям(ТаблицаПоИзмененымСтавкамНДС, Реквизиты, Отказ);

	
	НДС0 = ТаблицаНДСПредъявленныйРеализация0.Итог("НДС");
	
	ПринимаемыйКВычетуНДС = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленный");
	НДСДляОперацийПо0 = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленныйРеализация0");
	ДвиженияНДСРаздельныйУчет = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСРаздельныйУчет");
	
	СтавкаБезНДС	= Перечисления.СтавкиНДС.БезНДС;
	СтавкаНДС0 		= Перечисления.СтавкиНДС.НДС0;
	ПустаяСтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
	СчетаПартионногоУчетаЦенностей = СчетаПартионногоУчетаЦенностейДляЦелейНДС();
	ВычетНДСНаОснованииДокумента.Колонки.Добавить("ДляЦелейПартионногоУчета", Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаТаблицы Из ВычетНДСНаОснованииДокумента Цикл
		
		СтрокаТаблицы.ДляЦелейПартионногоУчета = СчетаПартионногоУчетаЦенностей.Найти(СтрокаТаблицы.СчетУчета) <> Неопределено;

		Если СтрокаТаблицы.СтавкаНДС = СтавкаБезНДС Или СтрокаТаблицы.СтавкаНДС = СтавкаНДС0 Тогда
			СтавкаПослеИзмененияБезНДС = Истина;
		Иначе 
			СтавкаПослеИзмененияБезНДС = Ложь;
		КонецЕсли;
		Если СтрокаТаблицы.СтавкаНДСДоИзменения = СтавкаБезНДС
			Или СтрокаТаблицы.СтавкаНДСДоИзменения = СтавкаНДС0
			Или СтрокаТаблицы.СтавкаНДСДоИзменения = ПустаяСтавкаНДС Тогда
			СтавкаДоИзмененияБезНДС = Истина;
		Иначе
			СтавкаДоИзмененияБезНДС = Ложь;
		КонецЕсли;
		
		Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости Тогда
			Событие = Перечисления.СобытияПоНДСПокупки.НДСВключенВСтоимость;
		ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 Тогда
			Событие = Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0;
		ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется Тогда
			Событие = Перечисления.СобытияПоНДСПокупки.НДСПодлежитРаспределению;
		ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету Тогда
			Если СтрокаТаблицы.ИзмененаСтавкаНДС И Не СтавкаДоИзмененияБезНДС  Тогда
				// Отсторнируем сумму по старой ставке НДС
				НоваяСтрока = ПринимаемыйКВычетуНДС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"Событие");
				НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком;
				НоваяСтрока.ДатаСобытия = Реквизиты.Период;
				НоваяСтрока.СуммаБезНДС = - СтрокаТаблицы.СуммаБезНДСРубДоИзменения;
				НоваяСтрока.НДС = - СтрокаТаблицы.НДСДоИзменения;
				НоваяСтрока.СтавкаНДС = СтрокаТаблицы.СтавкаНДСДоИзменения;
				Если Не СтавкаПослеИзмененияБезНДС Тогда
					// Перенесем сумму НДС на новую ставку НДС 
					НоваяСтрока = ПринимаемыйКВычетуНДС.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"Событие");
					НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком;
					НоваяСтрока.ДатаСобытия = Реквизиты.Период;
					НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСРубДоИзменения;
					НоваяСтрока.НДС = СтрокаТаблицы.НДСДоИзменения;
				КонецЕсли;
			КонецЕсли;
			// Строка по изменениям в НДС
			Если (СтрокаТаблицы.НДС <> 0 Или СтрокаТаблицы.СуммаБезНДС <> 0)
				И Не СтавкаПослеИзмененияБезНДС Тогда
				НоваяСтрока = ПринимаемыйКВычетуНДС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"Событие");
				НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком;
				НоваяСтрока.ДатаСобытия = Реквизиты.Период;
				Если СтрокаТаблицы.ИзмененаСтавкаНДС И СтавкаПослеИзмененияБезНДС Тогда
					НоваяСтрока.СуммаБезНДС = - СтрокаТаблицы.СуммаБезНДСРубДоИзменения;
					НоваяСтрока.СтавкаНДС = СтрокаТаблицы.СтавкаНДСДоИзменения;
				ИначеЕсли СтавкаДоИзмененияБезНДС Тогда
					НоваяСтрока.СуммаБезНДС = НоваяСтрока.СуммаБезНДС + СтрокаТаблицы.СуммаБезНДСРубДоИзменения;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется Тогда
			Если СтрокаТаблицы.ИзмененаСтавкаНДС Тогда
				СтруктураПоиска = Новый Структура;
				СтруктураПоиска.Вставить("Номенклатура", СтрокаТаблицы.Номенклатура);
				СтруктураПоиска.Вставить("СпособУчетаНДС", СтрокаТаблицы.СпособУчетаНДС);
				СтруктураПоиска.Вставить("СтавкаНДС", СтрокаТаблицы.СтавкаНДС);
				СтруктураПоиска.Вставить("Количество", СтрокаТаблицы.Количество);
				СтруктураПоиска.Вставить("НДСДоИзменения", СтрокаТаблицы.НДСДоИзменения);
				СтруктураПоиска.Вставить("СуммаБезНДСРубДоИзменения", СтрокаТаблицы.СуммаБезНДСРубДоИзменения);
				РаспределениеДоИзмененияСтавкиНДС = ТаблицаПоИзмененымСтавкамНДС.НайтиСтроки(СтруктураПоиска);
				Если РаспределениеДоИзмененияСтавкиНДС.Количество() > 0 Тогда
					СтрокаРаспределенияДоИзмененияСтавкиНДС = РаспределениеДоИзмененияСтавкиНДС[0];
					Если (СтрокаРаспределенияДоИзмененияСтавкиНДС.НДСПринимаетсяКВычету <> 0
						ИЛИ СтрокаРаспределенияДоИзмененияСтавкиНДС.СуммаБезНДСПринимаетсяКВычету <> 0)
						И Не СтавкаДоИзмененияБезНДС Тогда
						// Сторнируем НДС уже распределенный по ставке НДС до изменения
						НоваяСтрока = ПринимаемыйКВычетуНДС.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"СуммаБезНДС,НДС,Событие,ДатаСобытия");
						НоваяСтрока.СуммаБезНДС = - СтрокаРаспределенияДоИзмененияСтавкиНДС.СуммаБезНДСПринимаетсяКВычету;
						НоваяСтрока.СтавкаНДС = СтрокаТаблицы.СтавкаНДСДоИзменения;
						НоваяСтрока.НДС = -СтрокаРаспределенияДоИзмененияСтавкиНДС.НДСПринимаетсяКВычету;
						НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком;
						НоваяСтрока.ДатаСобытия = Реквизиты.Период;
						// Возвращаем распределенный НДС по новой ставке 
						Если Не СтавкаПослеИзмененияБезНДС Тогда
							НоваяСтрока = ПринимаемыйКВычетуНДС.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"СуммаБезНДС,НДС,Событие,ДатаСобытия");
							НоваяСтрока.СуммаБезНДС =  СтрокаРаспределенияДоИзмененияСтавкиНДС.СуммаБезНДСПринимаетсяКВычету;
							НоваяСтрока.НДС = СтрокаРаспределенияДоИзмененияСтавкиНДС.НДСПринимаетсяКВычету;
							НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком;
							НоваяСтрока.ДатаСобытия = Реквизиты.Период;
						КонецЕсли;
					КонецЕсли;
					Если (СтрокаРаспределенияДоИзмененияСтавкиНДС.НДСДляОперацийПо0 <> 0 
						ИЛИ СтрокаРаспределенияДоИзмененияСтавкиНДС.СуммаБезНДСДляОперацийПо0 <> 0)
						И Не СтавкаДоИзмененияБезНДС Тогда 
						// Сторнируем НДС уже распределенный по ставке НДС до изменения
						НоваяСтрока = НДСДляОперацийПо0.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
						НоваяСтрока.СуммаБезНДС = - СтрокаРаспределенияДоИзмененияСтавкиНДС.СуммаБезНДСДляОперацийПо0;
						НоваяСтрока.НДС = - СтрокаРаспределенияДоИзмененияСтавкиНДС.НДСДляОперацийПо0;
						НоваяСтрока.СтавкаНДС = СтрокаТаблицы.СтавкаНДСДоИзменения;
						// Возвращаем распределенный НДС по новой ставке 
						Если Не СтавкаПослеИзмененияБезНДС Тогда
							НоваяСтрока = НДСДляОперацийПо0.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
							НоваяСтрока.СуммаБезНДС = СтрокаРаспределенияДоИзмененияСтавкиНДС.СуммаБезНДСДляОперацийПо0;
							НоваяСтрока.НДС = СтрокаРаспределенияДоИзмененияСтавкиНДС.НДСДляОперацийПо0;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если (СтрокаТаблицы.НДСПринимаетсяКВычету <> 0
				ИЛИ СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету <> 0) 
				И Не СтавкаПослеИзмененияБезНДС Тогда
				НоваяСтрока = ПринимаемыйКВычетуНДС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"СуммаБезНДС,НДС,Событие,ДатаСобытия");
				НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету;
				НоваяСтрока.НДС = СтрокаТаблицы.НДСПринимаетсяКВычету;
				НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком;
				НоваяСтрока.ДатаСобытия = Реквизиты.Период;
			КонецЕсли;
			Если (СтрокаТаблицы.НДСДляОперацийПо0 <> 0 
				ИЛИ СтрокаТаблицы.СуммаБезНДСДляОперацийПо0 <> 0) 
				И Не СтавкаПослеИзмененияБезНДС Тогда
				НоваяСтрока = НДСДляОперацийПо0.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСДляОперацийПо0;
				НоваяСтрока.НДС = СтрокаТаблицы.НДСДляОперацийПо0;
			КонецЕсли;
			
			Если СтрокаТаблицы.Услуга И СтрокаТаблицы.ИзмененаСтавкаНДС 
				И СтавкаДоИзмененияБезНДС И Не СтавкаПослеИзмененияБезНДС Тогда
				ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
					СтрокаТаблицы.НДС, СтрокаТаблицы.СуммаБезНДСРубДоИзменения + СтрокаТаблицы.СуммаБезНДС, 0);
			КонецЕсли;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура;
		Если СтрокаТаблицы.Услуга Тогда
			СтруктураПоиска.Вставить("СчетЗатрат",   СтрокаТаблицы.СчетУчета);
		Иначе
			СтруктураПоиска.Вставить("Номенклатура", СтрокаТаблицы.Номенклатура);
		КонецЕсли;
		СтруктураПоиска.Вставить("ВидЦенности",   СтрокаТаблицы.ВидЦенности);
		СтруктураПоиска.Вставить("СчетУчетаНДС",  СтрокаТаблицы.СчетУчетаНДС);
		СтруктураПоиска.Вставить("СтавкаНДС",     ?( СтрокаТаблицы.ИзмененаСтавкаНДС,
			СтрокаТаблицы.СтавкаНДСДоИзменения, СтрокаТаблицы.СтавкаНДС));
		СтруктураПоиска.Вставить("СпособУчетаНДС", СтрокаТаблицы.СпособУчетаНДС);
		ОстаткиПоСтроке = ТаблицаНДСРаздельныйУчет.НайтиСтроки(СтруктураПоиска);
		Если ОстаткиПоСтроке.Количество() = 0
			И СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется 
			И НЕ СтрокаТаблицы.Услуга Тогда
			СтруктураПоиска.Вставить("СпособУчетаНДС", Перечисления.СпособыУчетаНДС.Распределен);
			ОстаткиПоСтроке = ТаблицаНДСРаздельныйУчет.НайтиСтроки(СтруктураПоиска);
		КонецЕсли;
	
		КоличествоОстаток	= ТаблицаНДСРаздельныйУчет.Скопировать(ОстаткиПоСтроке).Итог("Количество");
		СуммаОстаток		= ТаблицаНДСРаздельныйУчет.Скопировать(ОстаткиПоСтроке).Итог("СуммаБезНДС");
		НДСОстаток			= ТаблицаНДСРаздельныйУчет.Скопировать(ОстаткиПоСтроке).Итог("НДС");
		
		Если СтрокаТаблицы.ИзмененаСтавкаНДС И (КоличествоОстаток <> 0 
			Или НДСОстаток <>0 Или СуммаОстаток <> 0) Тогда
			// Сторно суммы по ставке до изменения
			ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
			-НДСОстаток, -СуммаОстаток, -КоличествоОстаток, Истина);
			
			// Перенос отсторнированной суммы на новую ставку 
			Если Не (СтрокаТаблицы.Услуга И СтавкаПослеИзмененияБезНДС) Тогда
				ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
				НДСОстаток, СуммаОстаток, КоличествоОстаток);
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 
			И НДС0 <> 0 Тогда
			
			Если СтрокаТаблицы.ИзмененаСтавкаНДС И Не СтавкаДоИзмененияБезНДС Тогда 
				// Состорнировали сумма по ставке НДС до изменения
				НоваяСтрока = НДСДляОперацийПо0.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.НДС = - (СтрокаТаблицы.НДСДоИзменения - НДСОстаток);
				НоваяСтрока.СтавкаНДС = СтрокаТаблицы.СтавкаНДСДоИзменения;
				НоваяСтрока.СуммаБезНДС = -(СтрокаТаблицы.СуммаБезНДСРубДоИзменения - СуммаОстаток);
				
				Если Не СтавкаПослеИзмененияБезНДС Тогда
					// Теперь состорнированная сумма НДС переносится на новую ставку НДС
					НоваяСтрока = НДСДляОперацийПо0.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					НоваяСтрока.НДС = СтрокаТаблицы.НДСДоИзменения - НДСОстаток;
					НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСРубДоИзменения - СуммаОстаток;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаТаблицы.Услуга Тогда
			Если СуммаОстаток <> 0 И Не СтавкаПослеИзмененияБезНДС Тогда
				ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
				СтрокаТаблицы.НДС, СтрокаТаблицы.СуммаБезНДС, СтрокаТаблицы.Количество);
			КонецЕсли;
			Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0
				И Не СтавкаПослеИзмененияБезНДС Тогда
				НоваяСтрока = НДСДляОперацийПо0.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				Если СтавкаДоИзмененияБезНДС Тогда
					НоваяСтрока.СуммаБезНДС = НоваяСтрока.СуммаБезНДС + СтрокаТаблицы.СуммаБезНДСРубДоИзменения;
				КонецЕсли;
			КонецЕсли;
		Иначе
			
			Если СтрокаТаблицы.Количество = 0 Тогда
				
				Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 Тогда
					
					// Если есть остатки в регистре НДСПредъявленныйРеализация0
					// значит измененную сумму надо распределить между НДСПредъявленныйРеализация0 и НДСРаздельныйУчет
					// Это распределение зависит от количества на остатке по регистру НДСРаздельныйУчет
					Если НДС0 <> 0 Тогда 
						Если ТаблицаНДСРаздельныйУчет.Количество() = 0 Тогда
							
							Если СтрокаТаблицы.ИзмененаСтавкаНДС И Не СтавкаПослеИзмененияБезНДС Тогда
								ЗначениеСтавкиНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС);
								НоваяСтрока = НДСДляОперацийПо0.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
								НоваяСтрока.СуммаБезНДС = ? (СтавкаДоИзмененияБезНДС,СтрокаТаблицы.СуммаБезНДСРубДоИзменения - СуммаОстаток, 0);
								НоваяСтрока.НДС = 0;
							КонецЕсли;
							
							НоваяСтрока = НДСДляОперацийПо0.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
							НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДС;
							НоваяСтрока.НДС = СтрокаТаблицы.НДС;
							
							Продолжить;
						КонецЕсли;
						// Поищем, есть ли остатки на НДСРаздельныйУчет
						Если КоличествоОстаток < СтрокаТаблицы.КоличествоДоИзменения Тогда
							НДС = Окр(СтрокаТаблицы.НДС / СтрокаТаблицы.КоличествоДоИзменения * КоличествоОстаток, 2);
							СуммаБезНДС = Окр(СтрокаТаблицы.СуммаБезНДС / СтрокаТаблицы.КоличествоДоИзменения * КоличествоОстаток, 2);
							ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
							НДС,СуммаБезНДС, 0);
							
							Если СтрокаТаблицы.ИзмененаСтавкаНДС И Не СтавкаПослеИзмененияБезНДС Тогда
								ЗначениеСтавкиНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС);
								НоваяСтрока = НДСДляОперацийПо0.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
								НоваяСтрока.СуммаБезНДС = ? (СтавкаДоИзмененияБезНДС,СтрокаТаблицы.СуммаБезНДСРубДоИзменения - СуммаОстаток, 0);
								НоваяСтрока.НДС = 0;
							КонецЕсли;
							
							// Отражение изменений по строке 
							НоваяСтрока = НДСДляОперацийПо0.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
							НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДС - СуммаБезНДС;
							НоваяСтрока.НДС = СтрокаТаблицы.НДС - НДС;
							
						ИначеЕсли КоличествоОстаток <> 0 Тогда
							ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
							СтрокаТаблицы.НДС, СтрокаТаблицы.СуммаБезНДС, 0);
						КонецЕсли;
					Иначе
						Если КоличествоОстаток <> 0 Тогда
							НДС = Окр(СтрокаТаблицы.НДС / СтрокаТаблицы.КоличествоДоИзменения * КоличествоОстаток, 2);
							СуммаБезНДС = Окр(СтрокаТаблицы.СуммаБезНДС / СтрокаТаблицы.КоличествоДоИзменения * КоличествоОстаток, 2);
							ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
							НДС, СуммаБезНДС, 0);
						КонецЕсли;
					КонецЕсли;
				Иначе
					Если КоличествоОстаток <> 0 Тогда
						НДС = Окр(СтрокаТаблицы.НДС / СтрокаТаблицы.КоличествоДоИзменения * КоличествоОстаток, 2);
						СуммаБезНДС = Окр(СтрокаТаблицы.СуммаБезНДС / СтрокаТаблицы.КоличествоДоИзменения * КоличествоОстаток, 2);
						ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
						НДС, СуммаБезНДС, 0);
					КонецЕсли;
				КонецЕсли;
			Иначе
				// Отражение изменения количества 
				ЗначениеСтавкиНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС);
				СуммаБезНДС = Окр(СтрокаТаблицы.Цена * СтрокаТаблицы.Количество, 2);
				НДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(СуммаБезНДС, Реквизиты.НДСВключенВСтоимость, ЗначениеСтавкиНДС);
				
				ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
				НДС, СуммаБезНДС, СтрокаТаблицы.Количество);
				
				Если СтрокаТаблицы.ИзмененаСтавкаНДС Тогда
					Если СтавкаПослеИзмененияБезНДС Тогда 
						НДС = НДСОстаток;
					Иначе
						ЗначениеСтавкиНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС);
						
						СуммаПоНовойСтавкеОстатка = УчетНДСКлиентСервер.РассчитатьСуммуНДС(СуммаОстаток, Реквизиты.НДСВключенВСтоимость,
						ЗначениеСтавкиНДС);
						НДС = НДСОстаток - СуммаПоНовойСтавкеОстатка;
					КонецЕсли;
					ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
					-НДС, 0, 0); 
				КонецЕсли;
				// Нужно посчитать разницу в НДС между различными ставками
				Если СтрокаТаблицы.ИзмененаСтавкаНДС И Не СтавкаПослеИзмененияБезНДС Тогда
					НоваяСтрока = НДСДляОперацийПо0.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					НоваяСтрока.СуммаБезНДС = ? (СтавкаДоИзмененияБезНДС,СтрокаТаблицы.СуммаБезНДСРубДоИзменения - СуммаОстаток, 0);
					НоваяСтрока.НДС = (УчетНДСКлиентСервер.РассчитатьСуммуНДС(СтрокаТаблицы.СуммаБезНДСРубДоИзменения - СуммаОстаток,
					Реквизиты.НДСВключенВСтоимость, ЗначениеСтавкиНДС))
					- (СтрокаТаблицы.НДСДоИзменения - НДСОстаток);
					
				КонецЕсли;
				// Если изменилось не только количество, но и цена 
				СуммаБезНДС = СтрокаТаблицы.ИзменениеЦены * КоличествоОстаток;
				НДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(СуммаБезНДС, Реквизиты.НДСВключенВСтоимость, ЗначениеСтавкиНДС);
				Если СуммаБезНДС <> 0 Или НДС <> 0 Тогда 
					ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты,
					НДС, СуммаБезНДС, 0);
				КонецЕсли;
				
				
				Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 
					И НДС0 <> 0 И СтрокаТаблицы.ИзменениеЦены <> 0 Тогда
					НоваяСтрока = НДСДляОперацийПо0.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.ИзменениеЦены * 
					(СтрокаТаблицы.КоличествоДоИзменения - КоличествоОстаток);
					НоваяСтрока.НДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(НоваяСтрока.СуммаБезНДС,
					Реквизиты.НДСВключенВСтоимость, ЗначениеСтавкиНДС);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаСторнирующейЗаписи Из ТаблицаСторноВычета Цикл
		Если СтрокаСторнирующейЗаписи.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету Тогда
			НоваяСтрока = ПринимаемыйКВычетуНДС.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСторнирующейЗаписи, ,"Событие");
			НоваяСтрока.СуммаБезНДС = - НоваяСтрока.СуммаБезНДС;
			НоваяСтрока.НДС = - НоваяСтрока.НДС;
			НоваяСтрока.ИсправленныйСчетФактура = Реквизиты.Регистратор;
			НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредъявленНДСПоставщиком;
			НоваяСтрока.ДатаСобытия = Реквизиты.Период;
		Иначе
			НоваяСтрока = НДСДляОперацийПо0.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСторнирующейЗаписи);
			НоваяСтрока.СуммаБезНДС = - СтрокаСторнирующейЗаписи.СуммаБезНДС;
			НоваяСтрока.НДС = - СтрокаСторнирующейЗаписи.НДС;
		КонецЕсли;
	КонецЦикла;
	
	ПринимаемыйКВычетуНДС.Свернуть("Период, Организация, СчетФактура, ВидЦенности, 
	|СтавкаНДС, СчетУчетаНДС, Поставщик, ДатаОплаты, ДоговорКонтрагента,
	|ИсправленныйСчетФактура, ДатаСобытия, Событие", "НДС, СуммаБезНДС");
	ДвиженияНДСРаздельныйУчет.Свернуть("Период, Организация, АналитикаУчетаЗатрат,
	|АналитикаУчетаНДС, Партия, СпособУчетаНДС", "Количество, НДС, СуммаБезНДС");
		
	Возврат Новый Структура(
	"ПринимаемыйКВычетуНДС, НДСДляОперацийПо0, ДвиженияНДСРаздельныйУчет",
	ПринимаемыйКВычетуНДС, НДСДляОперацийПо0, ДвиженияНДСРаздельныйУчет);

КонецФункции
	
Процедура ДобавитьЗаписьВНДСРаздельныйУчет(ДвиженияНДСРаздельныйУчет, СтрокаТаблицы, Реквизиты, НДС, СуммаБезНДС, Количество, СтавкаНДСДоИзменения = Ложь)
	
	Если СтрокаТаблицы.Услуга = Ложь И СтрокаТаблицы.ДляЦелейПартионногоУчета = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	АналитикаУчетаЗатрат = Новый Структура("Организация,СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3");
	АналитикаУчетаЗатрат.Организация = Реквизиты.Организация;
	АналитикаУчетаЗатрат.СчетЗатрат = СтрокаТаблицы.СчетУчета;
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетУчета);
	Если СтрокаТаблицы.Услуга Тогда 
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			АналитикаУчетаЗатрат.Подразделение = СтрокаТаблицы.ПодразделениеЗатрат;
		КонецЕсли;
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СтрокаТаблицы.ДляЦелейПартионногоУчета И СвойстваСчета["ВидСубконто" + Ном + "ТолькоОбороты"] Тогда
				АналитикаУчетаЗатрат["Субконто" + Ном] = Неопределено;
			Иначе
				АналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы["Субконто" + Ном];
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			АналитикаУчетаЗатрат.Подразделение = Реквизиты.Подразделение;
		КонецЕсли;
		
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура Тогда
				АналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(АналитикаУчетаЗатрат.СчетЗатрат) Тогда
					АналитикаУчетаЗатрат["Субконто" + Ном] = Реквизиты.Склад;
				КонецЕсли;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
				Если Реквизиты.ВестиПартионныйУчет 
					ИЛИ БухгалтерскийУчетПовтИсп.СчетУчетаКомиссионногоТовара(СтрокаТаблицы.СчетУчета) Тогда
					АналитикаУчетаЗатрат["Субконто" + Ном] = Реквизиты.ДокументПоступленияСсылка;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	АналитикаУчетаНДС = Новый Структура("Организация,СчетФактура,ВидЦенности,СчетУчетаНДС,
	|СтавкаНДС,Поставщик,ИсправленныйСчетФактура",
	Реквизиты.Организация,
	Реквизиты.ИсправляемыйСчетФактура,
	СтрокаТаблицы.ВидЦенности,
	СтрокаТаблицы.СчетУчетаНДС,
	?(СтавкаНДСДоИзменения, СтрокаТаблицы.СтавкаНДСДоИзменения, СтрокаТаблицы.СтавкаНДС),
	СтрокаТаблицы.Поставщик,
	Реквизиты.Регистратор);
	
	НоваяСтрока = ДвиженияНДСРаздельныйУчет.Добавить();
	
	НоваяСтрока.Период       			= Реквизиты.Период;
	НоваяСтрока.Организация				= Реквизиты.Организация;
	НоваяСтрока.АналитикаУчетаЗатрат	= 
	Справочники.КлючиАналитикиУчетаЗатрат.КлючиАналитикиУчетаЗатратДокумента(АналитикаУчетаЗатрат);
	НоваяСтрока.АналитикаУчетаНДС		=
	Справочники.КлючиАналитикиУчетаНДС.КлючиАналитикиУчетаНДСДокумента(АналитикаУчетаНДС);
	Если СтавкаНДСДоИзменения Тогда 
		НоваяСтрока.Партия              = Реквизиты.ДокументПоступленияСсылка;
	ИначеЕсли СтрокаТаблицы.ИзмененаСтавкаНДС Тогда 
		НоваяСтрока.Партия              = Реквизиты.Регистратор;
	Иначе
		НоваяСтрока.Партия              =
		?(СуммаБезНДС < 0 Или Количество < 0, Реквизиты.ДокументПоступленияСсылка, Реквизиты.Регистратор);
	КонецЕсли;
	Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется
		И НачалоКвартала(Реквизиты.ДокументПоступленияДата) <> НачалоКвартала(Реквизиты.Период) Тогда
		НоваяСтрока.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределен;
	Иначе
		НоваяСтрока.СпособУчетаНДС          = СтрокаТаблицы.СпособУчетаНДС;
	КонецЕсли;
	НоваяСтрока.Количество              = Количество;
	НоваяСтрока.СуммаБезНДС             = СуммаБезНДС;
	НоваяСтрока.НДС                     = НДС;

КонецПроцедуры

#КонецОбласти

#Область КорректировкаРеализации

Процедура СформироватьДвиженияКорректировкиРеализацияТоваровУслуг(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаВозвращенныеТовары, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не (ЗначениеЗаполнено(ТаблицаТовары)
			Или ЗначениеЗаполнено(ТаблицаСписанныеТоварыБухУчет)
			Или ЗначениеЗаполнено(ТаблицаВозвращенныеТовары))
	 Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыКорректировкиРеализацияТоваровУслуг(
			ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаВозвращенныеТовары, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	СписанныеПартииНДС = ПолучитьТаблицуСписанныеПартииНДС(Параметры.Товары, Параметры.СписанныеТоварыБухУчет, Реквизиты, Отказ);
	СформироватьДвиженияВыбытиеТоваров(СписанныеПартииНДС, Параметры.СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
	
	Для Каждого СтрокаТаблицы Из Параметры.ВозвращенныеТовары Цикл
		СтрокаТаблицы.Количество = -СтрокаТаблицы.Количество;
	КонецЦикла;
	
	СформироватьДвиженияНДСРаздельныйУчетОприходованиеТоваров(Параметры.ВозвращенныеТовары, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьПараметрыКорректировкиРеализацияТоваровУслуг(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаВозвращенныеТовары, ТаблицаРеквизиты)
	
	Параметры = Новый Структура;

	// Подготовка таблицы шапки документа
	СписокОбязательныхКолонок = ""
	+ "Период,"					// <Дата>
	+ "Регистратор,"			// <ДокументСсылка>
	+ "Организация,"			// <СправочникСсылка.Организации>
	+ "Склад,"
	+ "Подразделение"
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));

	// Подготовка таблицы Товары
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"            // <Строка> - имя табличной части документа
	+ "СинонимСписка,"        // <Строка> - синоним табличной части документа
	+ "НомерСтрокиДокумента," // <Число,10,0> - номер строки документа
	+ "Номенклатура,"         // <СправочникСсылка.Номенклатура>
	+ "СчетУчета,"            // <ПланСчетовСсылка.Хозрасчетный> - счет учета товара
	+ "СтавкаНДС,"            // <ПеречислениеСсылка.СтавкиНДС> - ставка НДС, указанная в документе
	+ "НомерГТД,"             // <СправочникСсылка.НомераГТД>
	+ "СтранаПроисхождения,"  // <СправочникСсылка.СтраныМира>
	+ "Количество"	          // <Число,15,3>
	;
	Параметры.Вставить("Товары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
	// В таблице списанных партий СтавкаНДС используется для ставки, указанной в документе поступления товара.
	// Она может отличаться от ставки НДС документа реализации, поэтому ставку реализации переименовываем
	Если Параметры.Товары.Количество() > 0 Тогда
		Параметры.Товары.Колонки.СтавкаНДС.Имя = "СтавкаНДСДокумента";
	КонецЕсли;
	
	// Подготовка таблицы списанных товаров по данным бух.учета:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "СинонимСписка,"
	+ "НомерСтроки,"
	+ "СчетУчета,"
	+ "Номенклатура,"
	+ "Склад,"
	+ "Партия,"
	+ "Количество,"
	+ "КорСчетСписания,"
	+ "ВидКорСубконто1,"
	+ "ВидКорСубконто2,"
	+ "ВидКорСубконто3,"
	+ "КорСубконто1,"
	+ "КорСубконто2,"
	+ "КорСубконто3,"
	+ "КорПодразделение,"
	+ "Подразделение"
	;
	Параметры.Вставить("СписанныеТоварыБухУчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСписанныеТоварыБухУчет, СписокОбязательныхКолонок));

	Параметры.СписанныеТоварыБухУчет.Индексы.Добавить("СчетУчета");
	Отбор = Новый Структура("СчетУчета", ПланыСчетов.Хозрасчетный.КорректировкаТоваровПрошлогоПериода);
	СтрокиКУдалению = Параметры.СписанныеТоварыБухУчет.НайтиСтроки(Отбор);
	
	Если СтрокиКУдалению.Количество() > 0 Тогда
	    Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			Параметры.СписанныеТоварыБухУчет.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЕсли;
		
		
	// Подготовка таблицы возвращенных товаров по данным бух.учета:
	СписокОбязательныхКолонок = ""
	+ "СчетУчета,"
	+ "Номенклатура,"
	+ "Партия,"
	+ "Количество"
	;
	Параметры.Вставить("ВозвращенныеТовары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаВозвращенныеТовары, СписокОбязательныхКолонок));
			
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область МодернизацияОС

Процедура СформироватьДвиженияМодернизацияОС(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаТовары)
	 Или Не ЗначениеЗаполнено(ТаблицаСписанныеТоварыБухУчет) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период)
	 Или Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыМодернизацияОС(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];

	ЗаполнитьКорСчетСписанияМодернизацияОС(Параметры.СписанныеТоварыБухУчет, Реквизиты);	
	
	СписанныеПартииНДС = ПолучитьТаблицуСписанныеПартииНДС(
		Параметры.Товары, Параметры.СписанныеТоварыБухУчет, Реквизиты, Отказ);
	СформироватьДвиженияВыбытиеТоваров(
		СписанныеПартииНДС, Параметры.СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);

КонецПроцедуры

Функция ПодготовитьПараметрыМодернизацияОС(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                   				// <Дата>
	+ "Регистратор,"              				// <ДокументСсылка>
	+ "Организация,"              				// <СправочникСсылка.Организации>
	+ "Подразделение,"            				// <Ссылка на справочник подразделений>
	+ "СчетУчетаЗатратПоАмортизационнойПремии,"	// <ПланСчетовСсылка.Хозрасчетный>
	+ "ПодразделениеПоАмортизационнойПремии,"	// <Ссылка на справочник подразделений>
	+ "СубконтоПоАмортизационнойПремии1,"		// <Характеристика.ВидыСубконтоХозрасчетные>
	+ "СубконтоПоАмортизационнойПремии2,"		// <Характеристика.ВидыСубконтоХозрасчетные>
	+ "СубконтоПоАмортизационнойПремии3"		// <Характеристика.ВидыСубконтоХозрасчетные>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	Параметры.Реквизиты.Колонки.Добавить("НДСвСтоимостиТоваров", Новый ОписаниеТипов("ПеречислениеСсылка.ДействиеНДСВСтоимостиТоваров"));
	Параметры.Реквизиты.ЗаполнитьЗначения(Перечисления.ДействиеНДСВСтоимостиТоваров.НеИзменять, "НДСвСтоимостиТоваров");

	// Подготовка таблицы товаров документа:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "НомерСтрокиДокумента,"
	+ "Номенклатура,"
	+ "Количество,"
	+ "СчетУчета,"
	+ "НовыйСпособУчетаНДС,"
	+ "ПроцентАмортизационнойПремии"
	;
	Параметры.Вставить("Товары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
		
	Реквизиты = Параметры.Реквизиты[0];	
	Параметры.Товары.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Подразделение, "Подразделение");
		
	// Подготовка таблицы списанных товаров по данным бух.учета:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "СинонимСписка,"
	+ "НомерСтроки,"
	+ "СчетУчета,"
	+ "Номенклатура,"
	+ "Склад,"
	+ "Партия,"
	+ "Количество,"
	+ "ВидКорСубконто1,"
	+ "ВидКорСубконто2,"
	+ "ВидКорСубконто3,"
	+ "КорСубконто1,"
	+ "КорСубконто2,"
	+ "КорСубконто3,"
	+ "КорПодразделение,"
	+ "Подразделение"
	;
	Параметры.Вставить("СписанныеТоварыБухУчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСписанныеТоварыБухУчет, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура ЗаполнитьКорСчетСписанияМодернизацияОС(СписанныеТоварыБухУчет, Реквизиты)
	
	ОбъектыОС = СписанныеТоварыБухУчет.ВыгрузитьКолонку("КорСубконто1");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("ОбъектыОС", ОбъектыОС);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетаУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СчетаУчетаОС.СчетУчета КАК КорСчетСписания
	|ИЗ
	|	РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И ОсновноеСредство В (&ОбъектыОС)) КАК СчетаУчетаОС";
	
	СчетаУчетаОбъектовОС = Запрос.Выполнить().Выгрузить();
	СчетаУчетаОбъектовОС.Индексы.Добавить("ОсновноеСредство");
	
	СписанныеТоварыБухУчет.Колонки.Добавить("КорСчетСписания", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	Для Каждого СтрокаТаблицы Из СписанныеТоварыБухУчет Цикл
		
		НайденнаяСтрока = СчетаУчетаОбъектовОС.Найти(СтрокаТаблицы.КорСубконто1, "ОсновноеСредство");
		Если НайденнаяСтрока <> Неопределено Тогда
			СтрокаТаблицы.КорСчетСписания = НайденнаяСтрока.КорСчетСписания;		
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОтчетКомиссионераОПродажах

Процедура СформироватьДвиженияПоступлениеПосредническихУслуг(ТаблицаУслуги, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаУслуги)
	 Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьРеквизитыПоступлениеПосредническихУслуг(ТаблицаУслуги, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];

	ПодготовитьПараметрыПоступлениеПосредническихУслуг(Параметры, ТаблицаУслуги);
	
	УчетНДС.ЗаполнитьВидыЦенностей(Параметры.Услуги, Перечисления.ВидыЦенностей.ПосредническиеУслуги, "СчетЗатрат");

	ДанныеДвижений = ПодготовитьДанныеДвиженийПоступлениеТоваровУслугОтПоставщика(
		Неопределено, Параметры.Услуги, Реквизиты);
		
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);	
		
КонецПроцедуры

Функция ПодготовитьРеквизитыПоступлениеПосредническихУслуг(ТаблицаУслуги, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                    // <ДокументСсылка>
	+ "Период,"                         // <Дата>
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "Подразделение,"                  // Подразделение для проводок
	+ "НДСВключенВСтоимость,"           // <Булево>
	+ "Контрагент,"                     // <СправочникСсылка.Контрагенты>
	+ "ДоговорКонтрагента,"             // <СправочникСсылка.ДоговорыКонтрагентов>
	+ "СчетУчетаРасчетовСКонтрагентом," // <ПланСчетовСсылка.Хозрасчетный>
	+ "ВалютаВзаиморасчетов"            // <СправочникСсылка.Валюты>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	Параметры.Реквизиты.Колонки.Добавить("УчетАгентскогоНДС", Новый ОписаниеТипов("Булево"));

	Возврат Параметры;

КонецФункции

Процедура ПодготовитьПараметрыПоступлениеПосредническихУслуг(Параметры, ТаблицаУслуги)

	// Подготовка таблицы Услуги

	СписокОбязательныхКолонок = ""
	+ "Содержание,"              // <Строка,150> - содержание для проводок
	+ "СуммаРуб,"                // <Число,15,2> - сумма в рублях
	+ "СуммаБезНДСРуб,"          // <Число,15,2> - сумма без НДС в рублях
	+ "СуммаНДСРуб,"             // <Число,15,2> - сумма НДС в рублях
	+ "СчетЗатрат,"              // <ПланСчетовСсылка.Хозрасчетный>
	+ "Субконто1,"               // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Субконто2,"               // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Субконто3,"               // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "СтатьяЗатрат,"            // <СправочникСсылка.СтатьиЗатрат>
	+ "ПодразделениеЗатрат,"     // <Ссылка на справочник подразделений>
	+ "СчетУчетаНДС,"            // <ПланСчетовСсылка.Хозрасчетный>
	+ "СуммаВзаиморасчетов,"     // <Число,15,2> - сумма в валюте расчетов с поставщиком
	+ "СуммаНДСВзаиморасчетов,"  // <Число,15,2> - сумма НДС в валюте расчетов с поставщиком
	+ "СтавкаНДС,"               // <ПеречислениеСсылка.СтавкиНДС>
	+ "СпособУчетаНДС"           // <ПеречислениеСсылка.СпособыУчетаНДС>
	;
	Параметры.Вставить("Услуги", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаУслуги, СписокОбязательныхКолонок));
		
	// Документом-основанием счета-фактуры при поступлении от поставщика выступает документ поступления
	Реквизиты = Параметры.Реквизиты[0];
	Параметры.Услуги.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
	Параметры.Услуги.Колонки.Добавить("Партия",      Документы.ТипВсеСсылки());
	Параметры.Услуги.ЗаполнитьЗначения(Реквизиты.Регистратор, "Партия,СчетФактура");
	
	Параметры.Услуги.Колонки.ПодразделениеЗатрат.Имя = "Подразделение";
	Параметры.Услуги.Колонки.СуммаБезНДСРуб.Имя = "СуммаБезНДС";
	Параметры.Услуги.Колонки.СуммаНДСРуб.Имя = "НДС";

КонецПроцедуры

#КонецОбласти

#Область ПоступлениеВЛизинг

Процедура СформироватьДвиженияПоступлениеПредметовЛизингаОтПоставщика(ТаблицаПредметыЛизинга, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьРеквизитыПоступлениеПредметовЛизингаОтПоставщика(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ПодготовитьПараметрыПоступлениеПредметовЛизингаОтПоставщика(Параметры, ТаблицаПредметыЛизинга);
	
	УчетНДС.ЗаполнитьВидыЦенностейПоступлениеОтПоставщика(Параметры.ПредметыЛизинга, Реквизиты, Неопределено, "СчетУчета");

	ДанныеДвижений = ПодготовитьДанныеДвиженийПоступлениеТоваровУслугОтПоставщика(
		Параметры.ПредметыЛизинга, Неопределено, Реквизиты);
		
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьРеквизитыПоступлениеПредметовЛизингаОтПоставщика(ТаблицаРеквизиты)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Реквизиты
	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                    // <ДокументСсылка>
	+ "Период,"                         // <Дата>
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "Подразделение,"                  // <Ссылка на справочник подразделений>
	+ "Склад,"                          // <СправочникСсылка.Склады>
	+ "УчетАгентскогоНДС,"              // <Булево>
	+ "ЭлектронныеУслуги,"              // <Булево>
	+ "ВидАгентскогоДоговора,"          // <ПеречислениеСсылка.ВидыАгентскихДоговоров> - заполняется, если УчетАгентскогоНДС = Истина
	+ "Контрагент,"                     // <СправочникСсылка.Контрагенты>
	+ "ДоговорКонтрагента,"             // <СправочникСсылка.ДоговорыКонтрагентов>
	+ "СчетУчетаРасчетовСКонтрагентом," // <ПланСчетовСсылка.Хозрасчетный>
	+ "ВалютаВзаиморасчетов"            // <СправочникСсылка.Валюты>
	;
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Параметры.Реквизиты.Колонки.Добавить("ТипСклада", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыСкладов"));
	
	Возврат Параметры;
	
КонецФункции

Процедура ПодготовитьПараметрыПоступлениеПредметовЛизингаОтПоставщика(Параметры, ТаблицаПредметыЛизинга)
	
	СписокОбязательныхКолонок = ""
	+ "Номенклатура,"            // <СправочникСсылка.Номенклатура>
	+ "СуммаБезНДСРуб,"          // <Число,15,2> - сумма без НДС в рублях
	+ "СуммаНДСРуб,"             // <Число,15,2> - сумма НДС в рублях
	+ "СчетУчета,"               // <ПланСчетовСсылка.Хозрасчетный>
	+ "СчетУчетаНДС,"            // <ПланСчетовСсылка.Хозрасчетный>
	+ "СпособУчетаНДС,"          // <ПеречислениеСсылка.СпособыУчетаНДС>
	+ "СтавкаНДС,"               // <ПеречислениеСсылка.СтавкиНДС>
	+ "Количество"               // <Число,15,3>
	;
	
	Параметры.Вставить("ПредметыЛизинга", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаПредметыЛизинга, СписокОбязательныхКолонок));
		
	Реквизиты = Параметры.Реквизиты[0];
	// Документом партии и документом-основанием счета-фактуры при поступлении от поставщика выступает документ поступления
	Параметры.ПредметыЛизинга.Колонки.Добавить("Партия",      Документы.ТипВсеСсылки());
	Параметры.ПредметыЛизинга.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
	Параметры.ПредметыЛизинга.ЗаполнитьЗначения(Реквизиты.Регистратор, "Партия,СчетФактура");
	// Склад берем Из шапки документа поступления
	Параметры.ПредметыЛизинга.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	Параметры.ПредметыЛизинга.ЗаполнитьЗначения(Реквизиты.Склад, "Склад");
	// Подразделение берем из шапки документа поступления
	Параметры.ПредметыЛизинга.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Параметры.ПредметыЛизинга.ЗаполнитьЗначения(Реквизиты.Подразделение, "Подразделение");
	
	Параметры.ПредметыЛизинга.Колонки.СуммаБезНДСРуб.Имя = "СуммаБезНДС";
	Параметры.ПредметыЛизинга.Колонки.СуммаНДСРуб.Имя = "НДС";
	
	Параметры.ПредметыЛизинга.Колонки.Добавить("СтатьяЗатрат", Новый ОписаниеТипов("СправочникСсылка.СтатьиЗатрат"));
	
	Параметры.ПредметыЛизинга.Колонки.Добавить("СпособСтроительства", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыСтроительства"));
	Параметры.ПредметыЛизинга.ЗаполнитьЗначения(Перечисления.СпособыСтроительства.Подрядный, "СпособСтроительства");
	
КонецПроцедуры

#КонецОбласти

#Область ПоступлениеДопРасходов

Процедура СформироватьДвиженияПоступлениеДопРасходовОтПоставщика(ТаблицаТовары, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаТовары) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;

	Параметры = ПодготовитьРеквизитыПоступлениеДопРасходовОтПоставщика(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];

	ПодготовитьПараметрыПоступлениеДопРасходовОтПоставщика(Параметры, ТаблицаТовары);

	// Вид ценности по умолчанию для доп.расходов - прочие работы и услуги
	УчетНДС.ЗаполнитьВидыЦенностейПоступлениеОтПоставщика(Параметры.Товары, Реквизиты, Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги, "СчетУчета");
	
	ДанныеДвижений = ПодготовитьДанныеДвиженийПоступлениеТоваровУслугОтПоставщика(Параметры.Товары, Неопределено, Реквизиты);
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьРеквизитыПоступлениеДопРасходовОтПоставщика(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                    // <ДокументСсылка>
	+ "Период,"                         // <Дата>
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "НДСВключенВСтоимость,"           // <Булево>
	+ "УчетАгентскогоНДС,"              // <Булево>
	+ "ЭлектронныеУслуги,"              // <Булево>
	+ "ВидАгентскогоДоговора,"          // <ПеречислениеСсылка.ВидыАгентскихДоговоров> - заполняется, если УчетАгентскогоНДС = Истина
	+ "Контрагент,"                     // <СправочникСсылка.Контрагенты>
	+ "ДоговорКонтрагента,"             // <СправочникСсылка.ДоговорыКонтрагентов>
	+ "СчетУчетаРасчетовСКонтрагентом," // <ПланСчетовСсылка.Хозрасчетный>
	+ "ВалютаВзаиморасчетов"            // <СправочникСсылка.Валюты>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура ПодготовитьПараметрыПоступлениеДопРасходовОтПоставщика(Параметры, ТаблицаТовары)

	// Подготовка таблицы Товары

	СписокОбязательныхКолонок = ""
	+ "ДокументПартии,"          // <ДокументСсылка> - документ поступления товаров
	+ "Номенклатура,"            // <СправочникСсылка.Номенклатура> - поступивший ранее товар
	+ "Склад,"                   // <СправочникСсылка.Склады> - склад, на который поступил ранее товар
	+ "Содержание,"              // <Строка,150> - содержание для проводок
	+ "Подразделение,"           // Подразделение для проводок
	+ "СчетУчета,"               // <ПланСчетовСсылка.Хозрасчетный>
	+ "СчетУчетаНДС,"            // <ПланСчетовСсылка.Хозрасчетный>
	+ "СпособУчетаНДС,"          // <ПеречислениеСсылка.СпособыУчетаНДС>
	+ "СуммаРуб,"                // <Число,15,2> - сумма в рублях
	+ "СуммаБезНДСРуб,"          // <Число,15,2> - сумма без НДС в рублях
	+ "СуммаНДСРуб,"             // <Число,15,2> - сумма НДС в рублях
	+ "СуммаВзаиморасчетов,"     // <Число,15,2> - сумма в валюте расчетов с поставщиком
	+ "СуммаНДСВзаиморасчетов,"  // <Число,15,2> - сумма НДС в валюте расчетов с поставщиком
	+ "СтавкаНДС"                // <ПеречислениеСсылка.СтавкиНДС>
	;
	Параметры.Вставить("Товары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
	Реквизиты = Параметры.Реквизиты[0];
	// Документом партии при поступлении доп.расходов служит документ поступления товаров
	Параметры.Товары.Колонки.ДокументПартии.Имя = "Партия";
	// Документом-основанием счета-фактуры при поступлении доп.расходов выступает документ поступления
	Параметры.Товары.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
	Параметры.Товары.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Регистратор, "СчетФактура,Регистратор");
	// Для доп.расходов количество в регистре НДСпоПриобретеннымЦенностям всегда равно нулю
	Параметры.Товары.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));

	Параметры.Товары.Колонки.Добавить("СтатьяЗатрат", Новый ОписаниеТипов("СправочникСсылка.СтатьиЗатрат"));
	Параметры.Товары.Колонки.Добавить("СпособСтроительства", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыСтроительства"));
	
	Параметры.Товары.Колонки.СуммаБезНДСРуб.Имя = "СуммаБезНДС";
	Параметры.Товары.Колонки.СуммаНДСРуб.Имя = "НДС";
	
КонецПроцедуры

#КонецОбласти

#Область ПоступлениеИзПереработки

Процедура СформироватьДвиженияПоступлениеИзПереработки(ТаблицаТовары, ТаблицаУслуги, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;

	Параметры = ПодготовитьРеквизитыПоступлениеИзПереработки(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ПодготовитьПараметрыПоступлениеТоваровУслугОтПоставщика(Параметры, ТаблицаТовары, ТаблицаУслуги);
    	
	УчетНДС.ЗаполнитьВидыЦенностейПоступлениеОтПоставщика(Параметры.Товары, Реквизиты, Неопределено, "СчетУчета");
	УчетНДС.ЗаполнитьВидыЦенностейПоступлениеОтПоставщика(Параметры.Услуги, Реквизиты, Неопределено, "СчетЗатрат");

	ДанныеДвижений = ПодготовитьДанныеДвиженийПоступлениеТоваровУслугОтПоставщика(
		Параметры.Товары, Параметры.Услуги, Реквизиты);
		
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьРеквизитыПоступлениеИзПереработки(ТаблицаРеквизиты)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                    // <ДокументСсылка>
	+ "Период,"                         // <Дата>
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "Подразделение,"                  // <Ссылка на справочник подразделений>
	+ "Склад,"                          // <СправочникСсылка.Склады>
	+ "ТипСклада,"                      // <ПеречислениеСсылка.ТипыСкладов>
	+ "УчетАгентскогоНДС,"              // <Булево>
	+ "ЭлектронныеУслуги,"              // <Булево>
	+ "ВидАгентскогоДоговора,"          // <ПеречислениеСсылка.ВидыАгентскихДоговоров> - заполняется, если УчетАгентскогоНДС = Истина
	+ "Контрагент,"                     // <СправочникСсылка.Контрагенты>
	+ "ДоговорКонтрагента,"             // <СправочникСсылка.ДоговорыКонтрагентов>
	+ "СчетУчетаРасчетовСКонтрагентом," // <ПланСчетовСсылка.Хозрасчетный>
	+ "ВалютаВзаиморасчетов,"           // <СправочникСсылка.Валюты>
	+ "ТипСкладаПолучателя"            	// <ПеречислениеСсылка.ТипыСкладов>
	;
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;	
		
КонецФункции

#КонецОбласти

#Область ПоступлениеНМА

Процедура СформироватьДвиженияПоступлениеНематериальныхАктивовОтПоставщика(ТаблицаНМА, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаНМА) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьРеквизитыПоступлениеНематериальныхАктивовОтПоставщика(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];

	ПодготовитьПараметрыПоступлениеНематериальныхАктивовОтПоставщика(Параметры, ТаблицаНМА);

	УчетНДС.ЗаполнитьВидыЦенностейПоступлениеОтПоставщика(Параметры.НематериальныеАктивы, Реквизиты, Неопределено, "СчетУчета");
	
	ДанныеДвижений = ПодготовитьДанныеДвиженийПоступлениеНематериальныхАктивов(
		Параметры.НематериальныеАктивы, Реквизиты);
		
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьРеквизитыПоступлениеНематериальныхАктивовОтПоставщика(ТаблицаРеквизиты) 
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Реквизиты
	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                    // <ДокументСсылка>
	+ "Период,"                         // <Дата>
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "Подразделение,"                  // <Ссылка на справочник подразделений>
	+ "НДСВключенВСтоимость,"           // <Булево>
	+ "УчетАгентскогоНДС,"              // <Булево>
	+ "ЭлектронныеУслуги,"              // <Булево>
	+ "ВидАгентскогоДоговора,"          // <ПеречислениеСсылка.ВидыАгентскихДоговоров> - заполняется, если УчетАгентскогоНДС = Истина
	+ "Контрагент,"                     // <СправочникСсылка.Контрагенты>
	+ "ДоговорКонтрагента,"             // <СправочникСсылка.ДоговорыКонтрагентов>
	+ "СчетУчетаРасчетовСКонтрагентом," // <ПланСчетовСсылка.Хозрасчетный>
	+ "ВалютаВзаиморасчетов"            // <СправочникСсылка.Валюты>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;
	
КонецФункции

Процедура ПодготовитьПараметрыПоступлениеНематериальныхАктивовОтПоставщика(Параметры, ТаблицаНМА) 
	
	// Подготовка таблицы Нематериальные активы

	СписокОбязательныхКолонок = ""
	+ "НематериальныйАктив,"     // <СправочникСсылка.НематериальныеАктивы>
	+ "Содержание,"              // <Строка,150> - содержание для проводок
	+ "СуммаРуб,"                // <Число,15,2> - сумма в рублях
	+ "СуммаБезНДСРуб,"          // <Число,15,2> - сумма без НДС в рублях
	+ "СуммаНДСРуб,"             // <Число,15,2> - сумма НДС в рублях
	+ "СчетУчета,"               // <ПланСчетовСсылка.Хозрасчетный>
	+ "СчетУчетаНДС,"            // <ПланСчетовСсылка.Хозрасчетный>
	+ "СпособУчетаНДС,"          // <ПеречислениеСсылка.СпособыУчетаНДС>
	+ "СуммаВзаиморасчетов,"     // <Число,15,2> - сумма в валюте расчетов с поставщиком
	+ "СуммаНДСВзаиморасчетов,"  // <Число,15,2> - сумма НДС в валюте расчетов с поставщиком
	+ "СтавкаНДС"               // <ПеречислениеСсылка.СтавкиНДС>
	;
	Параметры.Вставить("НематериальныеАктивы", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаНМА, СписокОбязательныхКолонок));
		
	Реквизиты = Параметры.Реквизиты[0];
	// Документом-основанием счета-фактуры при поступлении от поставщика выступает документ поступления
	Параметры.НематериальныеАктивы.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
	Параметры.НематериальныеАктивы.Колонки.Добавить("Партия", Документы.ТипВсеСсылки());
	Параметры.НематериальныеАктивы.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	Параметры.НематериальныеАктивы.ЗаполнитьЗначения(Реквизиты.Регистратор, "СчетФактура,Партия,Регистратор");
	
КонецПроцедуры

Функция ПодготовитьДанныеДвиженийПоступлениеНематериальныхАктивов(НематериальныеАктивы, Реквизиты)

	ДанныеДвижений = НематериальныеАктивы.Скопировать(,
		"СчетУчета,НематериальныйАктив,СчетФактура,ВидЦенности,СтавкаНДС,СчетУчетаНДС,СпособУчетаНДС,
		|Регистратор,Партия,СуммаБезНДСРуб,СуммаНДСРуб");
	ДанныеДвижений.Свернуть(
		"СчетУчета,НематериальныйАктив,СчетФактура,ВидЦенности,СтавкаНДС,СчетУчетаНДС,СпособУчетаНДС,
		|Регистратор,Партия",
		"СуммаБезНДСРуб,СуммаНДСРуб");

	ДанныеДвижений.Колонки.Добавить("Период",      Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Период, "Период,ДатаСобытия");

	ДанныеДвижений.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");

	ДанныеДвижений.Колонки.Добавить("Поставщик", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Контрагент, "Поставщик");

	ДанныеДвижений.Колонки.Добавить("ДоговорКонтрагента", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	Если Реквизиты.УчетАгентскогоНДС Тогда 	// Договор должен указываться только для налогового агента
		ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.ДоговорКонтрагента, "ДоговорКонтрагента");
	КонецЕсли;
	
	ДанныеДвижений.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Подразделение, "Подразделение");
	
	ДанныеДвижений.Колонки.СпособУчетаНДС.Имя = "НовыйСпособУчетаНДС";
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДСИзменился", Новый ОписаниеТипов("Булево"));
	ДанныеДвижений.ЗаполнитьЗначения(Истина, "СпособУчетаНДСИзменился");
	
	ДанныеДвижений.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	
	ДанныеДвижений.Колонки.СуммаБезНДСРуб.Имя = "СуммаБезНДС";
	ДанныеДвижений.Колонки.СуммаНДСРуб.Имя = "НДС";
	
	ДанныеДвижений.Колонки.СчетУчета.Имя = "СчетЗатрат";
	
	ДанныеДвижений.Колонки.НематериальныйАктив.Имя = "Субконто1";
	ДанныеДвижений.Колонки.Добавить("Субконто2");
	ДанныеДвижений.Колонки.Добавить("Субконто3");
	
	ДанныеДвижений.Колонки.Добавить("ЕстьНДСПредъявленный", Новый ОписаниеТипов("Булево"));
	Если ДанныеДвижений.Итог("НДС") = 0 Тогда
		ДанныеДвижений.ЗаполнитьЗначения(Ложь, "ЕстьНДСПредъявленный");
	Иначе
		ДанныеДвижений.ЗаполнитьЗначения(Истина, "ЕстьНДСПредъявленный");
	КонецЕсли;
	
	ДанныеДвижений.Колонки.Добавить("ИсправленныйСчетФактура",
		Новый ОписаниеТипов("ДокументСсылка.КорректировкаПоступления"));
	
	Возврат ДанныеДвижений;
	
КонецФункции

#КонецОбласти

#Область ПоступлениеТоваровУслуг

Процедура СформироватьДвиженияПоступлениеТоваровУслугОтПоставщика(ТаблицаТовары, ТаблицаУслуги, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьРеквизитыПоступлениеТоваровУслугОтПоставщика(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьПараметрыПоступлениеТоваровУслугОтПоставщика(Параметры, ТаблицаТовары, ТаблицаУслуги);
	
	УчетНДС.ЗаполнитьВидыЦенностейПоступлениеОтПоставщика(Параметры.Товары, Реквизиты, Неопределено, "СчетУчета");
	УчетНДС.ЗаполнитьВидыЦенностейПоступлениеОтПоставщика(Параметры.Услуги, Реквизиты, Неопределено, "СчетЗатрат");

	ДанныеДвижений = ПодготовитьДанныеДвиженийПоступлениеТоваровУслугОтПоставщика(
		Параметры.Товары, Параметры.Услуги, Реквизиты);
		
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьРеквизитыПоступлениеТоваровУслугОтПоставщика(ТаблицаРеквизиты)

	Параметры = Новый Структура;
	
	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                    // <ДокументСсылка>
	+ "Период,"                         // <Дата>
	+ "ВидОперации,"                    // <ПеречислениеСсылка.ВидыОперацийПоступлениеТоваровУслуг; Неопределено>
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "Подразделение,"                  // <Ссылка на справочник подразделений>
	+ "Склад,"                          // <СправочникСсылка.Склады>
	+ "ТипСклада,"                      // <ПеречислениеСсылка.ТипыСкладов>
	+ "УчетАгентскогоНДС,"              // <Булево>
	+ "ЭлектронныеУслуги,"              // <Булево>
	+ "ВидАгентскогоДоговора,"          // <ПеречислениеСсылка.ВидыАгентскихДоговоров> - заполняется, если УчетАгентскогоНДС = Истина
	+ "Контрагент,"                     // <СправочникСсылка.Контрагенты>
	+ "ДоговорКонтрагента,"             // <СправочникСсылка.ДоговорыКонтрагентов>
	+ "СчетУчетаРасчетовСКонтрагентом," // <ПланСчетовСсылка.Хозрасчетный>
	+ "ВалютаВзаиморасчетов"            // <СправочникСсылка.Валюты>
	;
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;	
		
КонецФункции		

Процедура ПодготовитьПараметрыПоступлениеТоваровУслугОтПоставщика(Параметры, ТаблицаТовары, ТаблицаУслуги)

	// Подготовка таблицы Товары

	СписокОбязательныхКолонок = ""
	+ "Номенклатура,"            // <СправочникСсылка.Номенклатура>
	+ "СуммаБезНДСРуб,"          // <Число,15,2> - сумма без НДС в рублях
	+ "СуммаНДСРуб,"             // <Число,15,2> - сумма НДС в рублях
	+ "СчетУчета,"               // <ПланСчетовСсылка.Хозрасчетный>
	+ "СчетУчетаНДС,"            // <ПланСчетовСсылка.Хозрасчетный>
	+ "СпособУчетаНДС,"          // <ПеречислениеСсылка.СпособыУчетаНДС>
	+ "СтавкаНДС,"               // <ПеречислениеСсылка.СтавкиНДС>
	+ "Количество"               // <Число,15,3>
	;
	
	ЕстьСтатьяЗатрат = Ложь;
	ЕстьСтатьяЗатрат = ТаблицаТовары <> Неопределено И ТаблицаТовары.Колонки.Найти("СтатьяЗатрат") <> Неопределено;
	Если ЕстьСтатьяЗатрат Тогда
		СписокОбязательныхКолонок = СписокОбязательныхКолонок + ",СтатьяЗатрат";
	КонецЕсли;
	
	ЕстьТранспортноеСредство = ТаблицаТовары <> Неопределено
		И ТаблицаТовары.Колонки.Найти("ТранспортноеСредство") <> Неопределено;
	Если ЕстьТранспортноеСредство Тогда
		СписокОбязательныхКолонок = СписокОбязательныхКолонок + ",ТранспортноеСредство";
	КонецЕсли;
	
	ЕстьРасхождения = ТаблицаТовары <> Неопределено И ТаблицаТовары.Колонки.Найти("СуммаПодтвержденнаяРуб") <> Неопределено
		И ТаблицаТовары.Колонки.Найти("СуммаНДСПодтвержденнаяРуб") <> Неопределено;
	Если ЕстьРасхождения Тогда 
		СписокОбязательныхКолонок = СписокОбязательныхКолонок + ",СуммаПодтвержденнаяРуб, СуммаНДСПодтвержденнаяРуб";
	КонецЕсли;
		
	Параметры.Вставить("Товары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
	Реквизиты = Параметры.Реквизиты[0];
	// Документом партии и документом-основанием счета-фактуры при поступлении от поставщика выступает документ поступления
	Параметры.Товары.Колонки.Добавить("Партия",      Документы.ТипВсеСсылки());
	Параметры.Товары.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Регистратор, "Партия,СчетФактура");
	// Склад берем Из шапки документа поступления
	Параметры.Товары.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Склад, "Склад");
	// Подразделение берем из шапки документа поступления
	Параметры.Товары.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Подразделение, "Подразделение");
	
	Параметры.Товары.Колонки.СуммаБезНДСРуб.Имя = "СуммаБезНДС";
	Параметры.Товары.Колонки.СуммаНДСРуб.Имя = "НДС";
	
	Если НЕ ЕстьСтатьяЗатрат Тогда
		Параметры.Товары.Колонки.Добавить("СтатьяЗатрат", Новый ОписаниеТипов("СправочникСсылка.СтатьиЗатрат"));
	КонецЕсли;
	
	Параметры.Товары.Колонки.Добавить("СпособСтроительства", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыСтроительства"));
	Параметры.Товары.ЗаполнитьЗначения(Перечисления.СпособыСтроительства.Подрядный, "СпособСтроительства");
	
	// Подготовка таблицы Услуги

	СписокОбязательныхКолонок = ""
	+ "СуммаБезНДСРуб,"          // <Число,15,2> - сумма без НДС в рублях
	+ "СуммаНДСРуб,"             // <Число,15,2> - сумма НДС в рублях
	+ "СчетЗатрат,"              // <ПланСчетовСсылка.Хозрасчетный>
	+ "Субконто1,"               // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Субконто2,"               // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Субконто3,"               // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "ПодразделениеЗатрат,"     // <Ссылка на справочник подразделений>
	+ "СчетУчетаНДС,"            // <ПланСчетовСсылка.Хозрасчетный>
	+ "СпособУчетаНДС,"          // <ПеречислениеСсылка.СпособыУчетаНДС>
	+ "СтавкаНДС"               // <ПеречислениеСсылка.СтавкиНДС>
	;
	
	ЕстьРасхождения = ТаблицаУслуги <> Неопределено И ТаблицаУслуги.Колонки.Найти("СуммаПодтвержденнаяРуб") <> Неопределено
		И ТаблицаУслуги.Колонки.Найти("СуммаНДСПодтвержденнаяРуб") <> Неопределено;
	Если ЕстьРасхождения Тогда 
		СписокОбязательныхКолонок = СписокОбязательныхКолонок + ",СуммаПодтвержденнаяРуб, СуммаНДСПодтвержденнаяРуб";
	КонецЕсли;
	
	Параметры.Вставить("Услуги", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаУслуги, СписокОбязательныхКолонок));
	// Документом-основанием счета-фактуры при поступлении от поставщика выступает документ поступления
	Параметры.Услуги.Колонки.Добавить("Партия",      Документы.ТипВсеСсылки());
	Параметры.Услуги.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
	Параметры.Услуги.ЗаполнитьЗначения(Реквизиты.Регистратор, "Партия,СчетФактура");
	
	Параметры.Услуги.Колонки.ПодразделениеЗатрат.Имя = "Подразделение";
	Параметры.Услуги.Колонки.СуммаБезНДСРуб.Имя = "СуммаБезНДС";
	Параметры.Услуги.Колонки.СуммаНДСРуб.Имя = "НДС";

КонецПроцедуры

Функция ПодготовитьДанныеДвиженийПоступлениеТоваровУслугОтПоставщика(Товары, Услуги, Реквизиты)

	Если Товары <> Неопределено Тогда
		
		ПартионныйУчет = УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период) = Перечисления.СпособыОценки.ФИФО;
		
		СписокКолонок = "СчетУчета,Подразделение,Номенклатура,Склад,Партия,СтатьяЗатрат,СпособСтроительства,
			|СчетФактура,ВидЦенности,СчетУчетаНДС,СтавкаНДС,СпособУчетаНДС,
			|Количество,СуммаБезНДС,НДС";
		
		ДобавитьНеобязательнуюКолонкуВСписок(Товары, СписокКолонок, "СуммаПодтвержденнаяРуб");
		ДобавитьНеобязательнуюКолонкуВСписок(Товары, СписокКолонок, "СуммаНДСПодтвержденнаяРуб");
		ДобавитьНеобязательнуюКолонкуВСписок(Товары, СписокКолонок, "ТранспортноеСредство");
		
		Если Товары.Колонки.Найти("СчетУчетаЗабалансовый") = Неопределено Тогда
			ДанныеДвижений = Товары.Скопировать(,СписокКолонок);
		Иначе 
			ДанныеДвижений = Товары.Скопировать(Новый Структура("СчетУчетаЗабалансовый", Ложь), СписокКолонок);
		КонецЕсли;
		ДанныеДвижений.Колонки.СчетУчета.Имя = "СчетЗатрат";
	
		ДанныеДвижений.Колонки.Добавить("Субконто1");
		ДанныеДвижений.Колонки.Добавить("Субконто2");
		ДанныеДвижений.Колонки.Добавить("Субконто3");
		
		ВидСубконтоТранспортныеСредства = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ТранспортныеСредства;
		ЕстьТранспортноеСредство = ДанныеДвижений.Колонки.Найти("ТранспортноеСредство") <> Неопределено;
		
		Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл

			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
			Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
				Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура
				 ИЛИ СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства
				 ИЛИ СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
				ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
					Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(СтрокаТаблицы.СчетЗатрат) Тогда
						СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Склад;
					КонецЕсли;
		        ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
					Если ПартионныйУчет Тогда
						СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.Партия;
					КонецЕсли;
		        ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.СтатьяЗатрат;
		        ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СпособыСтроительства Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.СпособСтроительства;
		        ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС Тогда
					СтрокаТаблицы["Субконто" + Ном] = СтрокаТаблицы.СтавкаНДС;
				ИначеЕсли СвойстваСчета[СтрШаблон("ВидСубконто%1", Ном)] = ВидСубконтоТранспортныеСредства
					И ЕстьТранспортноеСредство Тогда
					СтрокаТаблицы[СтрШаблон("Субконто%1", Ном)] = СтрокаТаблицы.ТранспортноеСредство;
				КонецЕсли;
			КонецЦикла;
			Если НЕ СвойстваСчета.УчетПоПодразделениям Тогда
				СтрокаТаблицы.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			
		КонецЦикла;

		ДанныеДвижений.Колонки.Удалить("Номенклатура");
		ДанныеДвижений.Колонки.Удалить("Склад");
		ДанныеДвижений.Колонки.Удалить("СтатьяЗатрат");
		ДанныеДвижений.Колонки.Удалить("СпособСтроительства");
		Если ЕстьТранспортноеСредство Тогда
			ДанныеДвижений.Колонки.Удалить("ТранспортноеСредство");
		КонецЕсли;
		
		Если Услуги <> Неопределено Тогда
			Если ДанныеДвижений.Колонки.Найти("СуммаПодтвержденнаяРуб") = Неопределено
				И ДанныеДвижений.Колонки.Найти("СуммаНДСПодтвержденнаяРуб") = Неопределено
				И Услуги.Колонки.Найти("СуммаПодтвержденнаяРуб") <> Неопределено
				И Услуги.Колонки.Найти("СуммаНДСПодтвержденнаяРуб") <> Неопределено Тогда 
				ДанныеДвижений.Колонки.Добавить("СуммаПодтвержденнаяРуб", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
				ДанныеДвижений.Колонки.Добавить("СуммаНДСПодтвержденнаяРуб", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
			КонецЕсли;
			ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(Услуги, ДанныеДвижений);
		КонецЕсли;
	Иначе
		СписокКолонок = "СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,Партия,
			|СчетФактура,ВидЦенности,СчетУчетаНДС,СтавкаНДС,СпособУчетаНДС,
			|СуммаБезНДС,НДС";
		ДобавитьНеобязательнуюКолонкуВСписок(Услуги, СписокКолонок, "СуммаПодтвержденнаяРуб");
		ДобавитьНеобязательнуюКолонкуВСписок(Услуги, СписокКолонок, "СуммаНДСПодтвержденнаяРуб");
		ДанныеДвижений = Услуги.Скопировать(,СписокКолонок);
		ДанныеДвижений.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	КонецЕсли;
	
	ДанныеДвижений.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Регистратор, "Регистратор");
	
	ДанныеДвижений.Колонки.Добавить("Период",      Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Период, "Период,ДатаСобытия");

	ДанныеДвижений.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");

	ДанныеДвижений.Колонки.Добавить("Поставщик", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.Контрагент, "Поставщик");

	ДанныеДвижений.Колонки.СпособУчетаНДС.Имя = "НовыйСпособУчетаНДС";
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	
	ДанныеДвижений.Колонки.Добавить("СпособУчетаНДСИзменился",
		Новый ОписаниеТипов("Булево"));
	ДанныеДвижений.ЗаполнитьЗначения(Истина, "СпособУчетаНДСИзменился");
	
	ДанныеДвижений.Колонки.Добавить("ДоговорКонтрагента", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	Если Реквизиты.УчетАгентскогоНДС Тогда 	// Договор должен указываться только для налогового агента
		ДанныеДвижений.ЗаполнитьЗначения(Реквизиты.ДоговорКонтрагента, "ДоговорКонтрагента");
	КонецЕсли;
	
	ДанныеДвижений.Колонки.Добавить("ЕстьНДСПредъявленный", Новый ОписаниеТипов("Булево"));
	Если ДанныеДвижений.Итог("НДС") = 0 Тогда
		ДанныеДвижений.ЗаполнитьЗначения(Ложь, "ЕстьНДСПредъявленный");
	Иначе
		ДанныеДвижений.ЗаполнитьЗначения(Истина, "ЕстьНДСПредъявленный");
	КонецЕсли;
	
	ДанныеДвижений.Колонки.Добавить("ИсправленныйСчетФактура",
		Новый ОписаниеТипов("ДокументСсылка.КорректировкаПоступления"));
	
	Возврат ДанныеДвижений;

КонецФункции

Процедура ДобавитьНеобязательнуюКолонкуВСписок(Таблица, СписокОбязательныхКолонок, ИмяКолонки)
	
	ЕстьКолонка = Ложь;
	ЕстьКолонка = Таблица <> Неопределено И Таблица.Колонки.Найти(ИмяКолонки) <> Неопределено;
	Если ЕстьКолонка
		И СтрНайти(СписокОбязательныхКолонок, ИмяКолонки) = 0 Тогда
		СписокОбязательныхКолонок = СписокОбязательныхКолонок + "," + ИмяКолонки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПринятиеКУчетуНМА

Процедура СформироватьДвиженияПринятиеНаУчетНМА(ТаблицаНМА, ТаблицаСписанныеНМАБухУчет, ТаблицаЗатратПоВключениюНДСВСоставРасходов,
		ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период)
	 Или Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыПринятиеНаУчетНМА(ТаблицаНМА, ТаблицаСписанныеНМАБухУчет, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];

	СписанныеПартииНДС = ПолучитьТаблицуСписанныеПартииНДС(
		Параметры.НМА, Параметры.СписанныеНМАБухУчет, Реквизиты, Отказ);
	СформироватьДвиженияВыбытиеТоваров(
		СписанныеПартииНДС, Параметры.СписанныеНМАБухУчет, Реквизиты, Движения, Отказ);
	ВключитьНДСВРасходыНУ(СписанныеПартииНДС, ТаблицаЗатратПоВключениюНДСВСоставРасходов,
		Реквизиты, Движения, Отказ)
КонецПроцедуры

Функция ПодготовитьПараметрыПринятиеНаУчетНМА(ТаблицаНМА, ТаблицаСписанныеНМАБухУчет, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                   // <Дата>
	+ "Регистратор,"              // <ДокументСсылка>
	+ "Организация"              // <СправочникСсылка.Организации>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));

	// Подготовка таблицы НМА документа:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "НомерСтрокиДокумента,"
	+ "Номенклатура,"
	+ "Количество,"
	+ "СчетУчета,"
	+ "НовыйСпособУчетаНДС,"
	+ "Подразделение"
	;
	Параметры.Вставить("НМА", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаНМА, СписокОбязательныхКолонок));
		
	// Подготовка таблицы списанных товаров по данным бух.учета:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "СинонимСписка,"
	+ "НомерСтроки,"
	+ "СчетУчета,"
	+ "Номенклатура,"
	+ "Склад,"
	+ "Партия,"
	+ "Количество,"
	+ "КорСчетСписания,"
	+ "ВидКорСубконто1,"
	+ "ВидКорСубконто2,"
	+ "ВидКорСубконто3,"
	+ "КорСубконто1,"
	+ "КорСубконто2,"
	+ "КорСубконто3,"
	+ "КорПодразделение,"
	+ "Подразделение"
	;
	Параметры.Вставить("СписанныеНМАБухУчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСписанныеНМАБухУчет, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

#КонецОбласти

#Область ПринятиеКУчетуОС

Процедура СформироватьДвиженияПринятиеНаУчетОС(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаТовары)
	 Или Не ЗначениеЗаполнено(ТаблицаСписанныеТоварыБухУчет) Тогда
		Возврат;
	КонецЕсли;

	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период)
	 Или Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыПринятиеНаУчетОС(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];

	СписанныеПартииНДС = ПолучитьТаблицуСписанныеПартииНДС(
		Параметры.Товары, Параметры.СписанныеТоварыБухУчет, Реквизиты, Отказ);
	СформироватьДвиженияВыбытиеТоваров(
		СписанныеПартииНДС, Параметры.СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);

КонецПроцедуры

Функция ПодготовитьПараметрыПринятиеНаУчетОС(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                   // <Дата>
	+ "Регистратор,"              // <ДокументСсылка>
	+ "Организация,"              // <СправочникСсылка.Организации>
	+ "Подразделение,"             // <Ссылка на справочник подразделений>
	+ "ПроцентАмортизационнойПремии"
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	Параметры.Реквизиты.Колонки.Добавить("НДСвСтоимостиТоваров", Новый ОписаниеТипов("ПеречислениеСсылка.ДействиеНДСВСтоимостиТоваров"));
	Параметры.Реквизиты.ЗаполнитьЗначения(Перечисления.ДействиеНДСВСтоимостиТоваров.НеИзменять, "НДСвСтоимостиТоваров");

	// Подготовка таблицы товаров документа:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "НомерСтрокиДокумента,"
	+ "Номенклатура,"
	+ "Количество,"
	+ "СчетУчета,"
	+ "НовыйСпособУчетаНДС"
	;
	Параметры.Вставить("Товары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
		
	Реквизиты = Параметры.Реквизиты[0];	
	Параметры.Товары.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Подразделение, "Подразделение");
		
	// Подготовка таблицы списанных товаров по данным бух.учета:
	СписокОбязательныхКолонок = ""
	+ "ИмяСписка,"
	+ "СинонимСписка,"
	+ "НомерСтроки,"
	+ "СчетУчета,"
	+ "Номенклатура,"
	+ "Склад,"
	+ "Партия,"
	+ "Количество,"
	+ "КорСчетСписания,"
	+ "ВидКорСубконто1,"
	+ "ВидКорСубконто2,"
	+ "ВидКорСубконто3,"
	+ "КорСубконто1,"
	+ "КорСубконто2,"
	+ "КорСубконто3,"
	+ "КорПодразделение,"
	+ "Подразделение"
	;
	Параметры.Вставить("СписанныеТоварыБухУчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСписанныеТоварыБухУчет, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

#КонецОбласти

#Область РаспределениеНДС

Процедура СформироватьДвиженияРаспределениеНДС(ТаблицаРеквизиты, ТаблицаРаспределяемыйНДС, ТаблицаВключенныйВСтоимостьНДС, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не Реквизиты.РаздельныйУчетНДСНаСчете19 Тогда
		Возврат;
	КонецЕсли;

	Если Не УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыРаспределениеНДС(ТаблицаРеквизиты, ТаблицаРаспределяемыйНДС, ТаблицаВключенныйВСтоимостьНДС);
	Реквизиты = Параметры.Реквизиты[0];
	
	ПартииНДС = Параметры.РаспределяемыйНДС;
	РаспределяемыйНДСДляСтавки0 = Параметры.РаспределяемыйНДС.Скопировать();
	
	//Бух. проводки распределение НДС на 19 счете
	ДанныеПроводок = ПодготовитьДанныеПроводокРаспределениеНДСНаСчете19(ПартииНДС);
	СформироватьПроводкиПоКорректировкеСпособаУчетаНДС(ДанныеПроводок, Реквизиты, Движения, Отказ);
	
	//Бух. проводки включение НДС в стоимость
	ДанныеПроводок = ПодготовитьДанныеПроводокВключениеВСтоимостьРаспределениеНДС(ПартииНДС, Реквизиты);
	СформироватьПроводкиВключениеНДСВСтоимость(ДанныеПроводок.ПрочиеЦенности, Движения, Отказ);
	СформироватьПроводкиВключениеНДСВСтоимостьОСиНМАРаспределениеНДС(ДанныеПроводок.ОСиНМА, Движения, Отказ);
	
	//Корректировка записей регистров по ОС
	ТаблицыДвиженийОС = ПодготовитьТаблицыДвиженийПоИзменениюСтоимостиОС(ДанныеПроводок.ОСиНМА, Реквизиты);
	СформироватьДвиженияПоИзменениюСтоимостиОС(ТаблицыДвиженийОС, Реквизиты, Движения, Отказ);
	
	//Корректировка записей регистров по НМА
	ТаблицыДвиженийНМА = ПодготовитьТаблицыДвиженийПоИзменениюСтоимостиНМА(ДанныеПроводок.ОСиНМА, Реквизиты);
	СформироватьДвиженияПоИзменениюСтоимостиНМА(ТаблицыДвиженийНМА, Реквизиты, Движения, Отказ);

	// Включение НДС в расходы НУ по ОС и НМА
	СформироватьПроводкиВключениеНДСВРасходыНУ(
		ДанныеПроводок.ОСиНМА, ТаблицыДвиженийОС, ТаблицыДвиженийНМА, Реквизиты, Движения, Отказ);
		
	// Бух. проводки по спецодежде и спецоснастке в эксплуатации
	СформироватьПроводкиСпецодеждаСпецоснасткаВЭксплуатации(ДанныеПроводок.ПрочиеЦенности, Реквизиты, Движения, Отказ);
	
	//Движения по НДСПредъявленный
	ДанныеДвижений = ПодготовитьДанныеДвиженийНДСПредъявленныйРаспределениеНДС(ПартииНДС, Реквизиты);
	СформироватьДвиженияНДСПредъявленный(ДанныеДвижений, Движения, Отказ);
	
	//Движения НДСРаздельныйУчет
	ДанныеДвижений = ПодготовитьДанныеДвиженийНДСРаздельныйУчетВыбытиеЦенностей(Параметры.РаспределяемыйНДС);
	СформироватьДвиженияНДСРаздельныйУчетВыбытиеЦенностей(ДанныеДвижений, Движения, Отказ);
	
	ДанныеДвижений = ПодготовитьДанныеДвиженийНДСРаздельныйУчетПоступлениеЦенностей(Параметры.РаспределяемыйНДС, Реквизиты);
	СформироватьДвиженияНДСРаздельныйУчетПоступлениеЦенностей(ДанныеДвижений, Движения, Отказ);
	
	//Распределение по документам реализации со ставкой 0%
	Если Реквизиты.ВыручкаНДС0 > 0 Тогда
		
		СчетаФактурыПоДаннымРаспределенияНДС = ПодготовитьДанныеДвиженийНДСПредъявленныйРеализация0ВыбытиеТоваров(
			РаспределяемыйНДСДляСтавки0);
		
		Если НЕ Реквизиты.ОСиНМА Тогда
			ОчиститьДвиженияНДСПредъявленныйРеализация0(Движения, Отказ);
			
			СчетаФактурыПоДаннымНДСПредъявленныйРеализация0 = ПодготовитьСчетаФактурыПоДаннымНДСПредъявленныйРеализация0(Реквизиты);
			СформироватьДвиженияНДСПредъявленныйРеализация0ПоступлениеЦенностей(
				СчетаФактурыПоДаннымНДСПредъявленныйРеализация0, Движения, Отказ, Истина);	
			
			ДанныеДвижений = ПодготовитьДанныеДвиженийНДСПредъявленныйРеализация0РаспределениеНДС(
				Реквизиты, СчетаФактурыПоДаннымРаспределенияНДС, СчетаФактурыПоДаннымНДСПредъявленныйРеализация0);
			СформироватьДвиженияНДСПредъявленныйРеализация0ПоступлениеЦенностей(
				ДанныеДвижений, Движения, Отказ);
		Иначе
			СформироватьДвиженияНДСПредъявленныйРеализация0ПоступлениеЦенностей(
				СчетаФактурыПоДаннымРаспределенияНДС, Движения, Отказ);
		КонецЕсли;

	КонецЕсли;
	
	Если НЕ Реквизиты.ОСиНМА Тогда
		СформирватьДвиженияБазаРаспределенияНДС(Реквизиты);
	КонецЕсли;
	
	// Движения НДСНеоблагаемыеОперации
	СформироватьДвиженияНДСНеоблагаемыеОперации(Параметры.ВключенныйВСтоимостьНДС, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьПараметрыРаспределениеНДС(ТаблицаРеквизиты, ТаблицаРаспределяемыйНДС, ТаблицаВключенныйВСтоимостьНДС)

	Параметры = Новый Структура;

	// Реквизиты шапки документа:
	ОбязательныеКолонки = ""

	+ "Регистратор,"
	+ "Номер,"
	+ "Период,"
	+ "НачалоПериода,"
	+ "КонецПериода,"
	+ "Организация,"
	+ "РаздельныйУчетНДСНаСчете19,"
	+ "ВыручкаЕНВД,"
	+ "ВыручкаБезНДС,"
	+ "ВыручкаНДС0,"
	+ "ВыручкаНДС,"
	+ "СобытиеОС"
	;

	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, ОбязательныеКолонки));

	Реквизиты = Параметры.Реквизиты[0];
	
	Параметры.Реквизиты.Колонки.Добавить("ОСиНМА", Новый ОписаниеТипов("Булево"));
	Параметры.Реквизиты.ЗаполнитьЗначения(
		КонецМесяца(Реквизиты.Период) <> КонецКвартала(Реквизиты.Период), "ОСиНМА");
		
	ОбязательныеКолонки = ""

	+ "СчетФактура,"
	+ "Поставщик,"
	+ "СуммаБезНДС,"
	+ "НДС,"
	+ "СуммаБезНДСПринимаетсяКВычету,"
	+ "НДСПринимаетсяКВычету,"
	+ "СуммаБезНДСУчитываетсяВCтоимости,"
	+ "НДСУчитываетсяВCтоимости,"
	+ "СуммаБезНДСДляОперацийПо0,"
	+ "НДСДляОперацийПо0,"
	+ "ВидЦенности,"
	+ "СчетУчетаНДС,"
	+ "СтавкаНДС,"
	+ "Партия,"
	+ "СчетЗатрат,"
	+ "Подразделение,"
	+ "Субконто1,"
	+ "Субконто2,"
	+ "Субконто3,"
	+ "ИсходноеСубконто1,"
	+ "ИсходноеСубконто2,"
	+ "ИсходноеСубконто3,"
	+ "Количество,"
	+ "СпособУчетаНДС,"
	+ "НовыйСпособУчетаНДС,"
	+ "Модернизация,"
	+ "ИсправленныйСчетФактура"
	;
	
	Параметры.Вставить("РаспределяемыйНДС", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРаспределяемыйНДС, ОбязательныеКолонки));

	Параметры.РаспределяемыйНДС.Свернуть(
		"СчетФактура,Поставщик,ВидЦенности,СчетУчетаНДС,СтавкаНДС,Партия,
		|СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,
		|ИсходноеСубконто1,ИсходноеСубконто2,ИсходноеСубконто3,СпособУчетаНДС,
		|НовыйСпособУчетаНДС,Модернизация, ИсправленныйСчетФактура",
		"СуммаБезНДС,НДС,СуммаБезНДСПринимаетсяКВычету,НДСПринимаетсяКВычету,
		|СуммаБезНДСУчитываетсяВCтоимости,НДСУчитываетсяВCтоимости,
		|СуммаБезНДСДляОперацийПо0,НДСДляОперацийПо0,Количество");	
		
	Параметры.РаспределяемыйНДС.Колонки.Добавить("ДатаСобытия",
		Новый ОписаниеТипов("Дата"));
	Параметры.РаспределяемыйНДС.Колонки.Добавить("Период",
		Новый ОписаниеТипов("Дата"));
	Параметры.РаспределяемыйНДС.Колонки.Добавить("Организация",
		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Параметры.РаспределяемыйНДС.Колонки.Добавить("СпособУчетаНДСИзменился",
		Новый ОписаниеТипов("Булево"));
	Параметры.РаспределяемыйНДС.Колонки.Добавить("Регистратор",
		Документы.ТипВсеСсылки());
	
	Параметры.РаспределяемыйНДС.ЗаполнитьЗначения(Реквизиты.Период, "Период,ДатаСобытия");
	Параметры.РаспределяемыйНДС.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");
	Параметры.РаспределяемыйНДС.ЗаполнитьЗначения(Истина, "СпособУчетаНДСИзменился");
	Параметры.РаспределяемыйНДС.ЗаполнитьЗначения(Реквизиты.Регистратор, "Регистратор");
	
	ОбязательныеКолонки = ""
	+ "Организация,"
	+ "Период,"
	+ "СуммаНДСНеПодлежащаяВычету"
	;
	
	Параметры.Вставить("ВключенныйВСтоимостьНДС", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаВключенныйВСтоимостьНДС, ОбязательныеКолонки));
	
	Возврат Параметры;

КонецФункции

Функция ПодготовитьДанныеДвиженийНДСРаздельныйУчетВыбытиеЦенностей(ТаблицаРаспределяемыйНДС)
	
	ДанныеДвиженийНДСРаздельныйУчет = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСРаздельныйУчет");
	
	Если ТаблицаРаспределяемыйНДС.Количество() > 0 Тогда
		СокращеннаяАналитика = 
		УчетНДСРаздельный.СокращеннаяАналитика(ТаблицаРаспределяемыйНДС[0].Период);
	Иначе
		СокращеннаяАналитика = Истина;
	КонецЕсли;
	Если СокращеннаяАналитика Тогда
		ТаблицаРаспределяемыйНДС.ЗаполнитьЗначения(Справочники.Контрагенты.ПустаяСсылка(), "Поставщик");
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ТаблицаРаспределяемыйНДС Цикл
		
		НоваяСтрока = ДанныеДвиженийНДСРаздельныйУчет.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		АналитикаУчетаЗатрат = Новый Структура("Организация,СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3");
		ЗаполнитьЗначенияСвойств(АналитикаУчетаЗатрат, СтрокаТаблицы, "Организация,СчетЗатрат,Подразделение");
		АналитикаУчетаЗатрат.Субконто1 = СтрокаТаблицы.ИсходноеСубконто1;
		АналитикаУчетаЗатрат.Субконто2 = СтрокаТаблицы.ИсходноеСубконто2;
		АналитикаУчетаЗатрат.Субконто3 = СтрокаТаблицы.ИсходноеСубконто3;
		
		НоваяСтрока.АналитикаУчетаЗатрат = Справочники.КлючиАналитикиУчетаЗатрат.КлючиАналитикиУчетаЗатратДокумента(АналитикаУчетаЗатрат);
		НоваяСтрока.АналитикаУчетаНДС = Справочники.КлючиАналитикиУчетаНДС.КлючиАналитикиУчетаНДСДокумента(СтрокаТаблицы);
		
	КонецЦикла;
	
	ДанныеДвиженийНДСРаздельныйУчет.Свернуть(
		"Период,Организация,АналитикаУчетаЗатрат,АналитикаУчетаНДС,Партия,СпособУчетаНДС",
		"Количество,СуммаБезНДС,НДС");
	
	Возврат ДанныеДвиженийНДСРаздельныйУчет;
	
КонецФункции

Функция ПодготовитьДанныеПроводокРаспределениеНДСНаСчете19(ПартииНДС)
	
	ДанныеПроводок = ПартииНДС.СкопироватьКолонки("СчетУчетаНДС,Поставщик,СчетФактура,НовыйСпособУчетаНДС,НДС");
		
	Для Каждого СтрокаТаблицы Из ПартииНДС Цикл
		
		Если НЕ СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету
			  И СтрокаТаблицы.НДСПринимаетсяКВычету <> 0 Тогда
			
			НоваяСтрока = ДанныеПроводок.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НДС = СтрокаТаблицы.НДСПринимаетсяКВычету;
			НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету;
			
		КонецЕсли;
			
		Если НЕ СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости
			  И СтрокаТаблицы.НДСУчитываетсяВCтоимости <> 0 Тогда
			
			НоваяСтрока = ДанныеПроводок.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НДС = СтрокаТаблицы.НДСУчитываетсяВCтоимости;
			НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости;
			
		КонецЕсли;
		
		Если НЕ СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0
			  И СтрокаТаблицы.НДСДляОперацийПо0 <> 0 Тогда
			
			НоваяСтрока = ДанныеПроводок.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НДС = СтрокаТаблицы.НДСДляОперацийПо0;
			НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеПроводок.Колонки.Добавить("Содержание",
		Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)));
	ДанныеПроводок.ЗаполнитьЗначения(НСтр("ru = 'Распределение НДС'"), "Содержание");	
	
	ДанныеПроводок.Колонки.Добавить("СпособУчетаНДС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	ДанныеПроводок.ЗаполнитьЗначения(Перечисления.СпособыУчетаНДС.Распределяется, "СпособУчетаНДС");	
	
	ДанныеПроводок.Свернуть("
		|СчетУчетаНДС,Поставщик,СчетФактура,СпособУчетаНДС,
		|Содержание,НовыйСпособУчетаНДС",
		"НДС");
	
	Возврат ДанныеПроводок;
	
КонецФункции

Функция ПодготовитьДанныеПроводокВключениеВСтоимостьРаспределениеНДС(ПартииНДС, Реквизиты)
	
	ДанныеПроводокОСиНМА = ПартииНДС.СкопироватьКолонки("
		|Организация,Период,
		|СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,
		|СчетУчетаНДС,СчетФактура,Поставщик,
		|НДС,Модернизация");
		
	ДанныеПроводокОСиНМА.Колонки.Добавить("Содержание",
		Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)));
	ДанныеПроводокОСиНМА.Колонки.Добавить("НовыйСпособУчетаНДС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
		
	ДанныеПроводокПрочиеЦенности = ДанныеПроводокОСиНМА.СкопироватьКолонки();
	
	ДанныеПроводокОСиНМА.Колонки.Добавить("ЭтоОС",	Новый ОписаниеТипов("Булево"));
	ДанныеПроводокОСиНМА.Колонки.Добавить("ЭтоНМА", Новый ОписаниеТипов("Булево"));
	
	СчетаУчетаОС = СчетаУчетаОС();
	СчетаУчетаНМА = СчетаУчетаНМА();
		
	Для Каждого СтрокаТаблицы Из ПартииНДС Цикл
		
		Если СтрокаТаблицы.НДСУчитываетсяВCтоимости = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СчетаУчетаОС.Найти(СтрокаТаблицы.СчетЗатрат) <> Неопределено Тогда
			НоваяСтрока = ДанныеПроводокОСиНМА.Добавить();
	        НоваяСтрока.ЭтоОС = Истина;
		ИначеЕсли СчетаУчетаНМА.Найти(СтрокаТаблицы.СчетЗатрат) <> Неопределено Тогда
			НоваяСтрока = ДанныеПроводокОСиНМА.Добавить();
	        НоваяСтрока.ЭтоНМА = Истина;
		Иначе
			НоваяСтрока = ДанныеПроводокПрочиеЦенности.Добавить();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.НДС = СтрокаТаблицы.НДСУчитываетсяВCтоимости;
		НоваяСтрока.Содержание = НСтр("ru = 'НДС включен в стоимость ценностей'");
		НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости;
		
	КонецЦикла;
	
	Если ДанныеПроводокПрочиеЦенности.Количество() > 0 Тогда
		
		ЗаменитьАналитикуСписанияНДСПоАренднымОбязательствам(ДанныеПроводокПрочиеЦенности, Реквизиты);
		
	КонецЕсли;
	
	Если ДанныеПроводокОСиНМА.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДанныеПроводокОСиНМА", ДанныеПроводокОСиНМА);
		Запрос.УстановитьПараметр("НачалоПериода", Реквизиты.НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", Реквизиты.КонецПериода);
		Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПроводокОСиНМА.СчетЗатрат КАК СчетДт,
		|	ДанныеПроводокОСиНМА.Субконто1 КАК СубконтоДт1
		|ПОМЕСТИТЬ ДанныеПроводокОСиНМА
		|ИЗ
		|	&ДанныеПроводокОСиНМА КАК ДанныеПроводокОСиНМА
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СчетДт,
		|	СубконтоДт1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОборотыДтКт.СчетДт,
		|	ХозрасчетныйОборотыДтКт.СубконтоДт1,
		|	ХозрасчетныйОборотыДтКт.СчетКт,
		|	ХозрасчетныйОборотыДтКт.СубконтоКт1,
		|	ХозрасчетныйОборотыДтКт.СубконтоКт2,
		|	ХозрасчетныйОборотыДтКт.СубконтоКт3
		|ПОМЕСТИТЬ ОборотыДтКт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			СчетДт В
		|				(ВЫБРАТЬ
		|					ДанныеПроводокОСиНМА.СчетДт
		|				ИЗ
		|					ДанныеПроводокОСиНМА),
		|			,
		|			,
		|			,
		|			Организация = &Организация
		|				И СубконтоДт1 В
		|					(ВЫБРАТЬ
		|						ДанныеПроводокОСиНМА.СубконтоДт1
		|					ИЗ
		|						ДанныеПроводокОСиНМА)) КАК ХозрасчетныйОборотыДтКт
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ХозрасчетныйОборотыДтКт.СчетДт,
		|	ХозрасчетныйОборотыДтКт.СубконтоДт1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПроводокОСиНМА.СчетДт КАК СчетЗатрат,
		|	ДанныеПроводокОСиНМА.СубконтоДт1 КАК Субконто1,
		|	ОборотыДтКт.СчетКт КАК КорСчетЗатрат,
		|	ОборотыДтКт.СубконтоКт1 КАК КорСубконто1,
		|	ОборотыДтКт.СубконтоКт2 КАК КорСубконто2,
		|	ОборотыДтКт.СубконтоКт3 КАК КорСубконто3
		|ИЗ
		|	ДанныеПроводокОСиНМА КАК ДанныеПроводокОСиНМА
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОборотыДтКт КАК ОборотыДтКт
		|		ПО ДанныеПроводокОСиНМА.СчетДт = ОборотыДтКт.СчетДт
		|			И ДанныеПроводокОСиНМА.СубконтоДт1 = ОборотыДтКт.СубконтоДт1";

		
		ТаблицаКорАналитикиУчета = Запрос.Выполнить().Выгрузить();
		ТаблицаКорАналитикиУчета.Индексы.Добавить("СчетЗатрат,Субконто1");
		
		ДанныеПроводокОСиНМА.Колонки.Добавить("КорСчетЗатрат");
		ДанныеПроводокОСиНМА.Колонки.Добавить("КорСубконто1");
		ДанныеПроводокОСиНМА.Колонки.Добавить("КорСубконто2");
		ДанныеПроводокОСиНМА.Колонки.Добавить("КорСубконто3");
		ДанныеПроводокОСиНМА.Колонки.Добавить("ЕстьКорАналитикаУчета", Новый ОписаниеТипов("Булево"));
		
		Отбор = Новый Структура("СчетЗатрат,Субконто1");
		
		Для Каждого СтрокаТаблицы Из ДанныеПроводокОСиНМА Цикл
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
			КорАналитикаУчета = ТаблицаКорАналитикиУчета.НайтиСтроки(Отбор);
			Если КорАналитикаУчета.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, КорАналитикаУчета[0], ,"СчетЗатрат,Субконто1");
				СтрокаТаблицы.ЕстьКорАналитикаУчета = Истина;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ДанныеПроводок = Новый Структура("ОСиНМА,ПрочиеЦенности", ДанныеПроводокОСиНМА, ДанныеПроводокПрочиеЦенности);
	
	Возврат ДанныеПроводок;
	
КонецФункции

Функция ПодготовитьДанныеДвиженийНДСПредъявленныйРаспределениеНДС(ПартииНДС, Реквизиты)
	
	ДанныеДвижений = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленный");
	
	Для Каждого СтрокаТаблицы Из ПартииНДС Цикл
		
		Если СтрокаТаблицы.НДСПринимаетсяКВычету = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ДанныеДвижений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету;
		НоваяСтрока.НДС = СтрокаТаблицы.НДСПринимаетсяКВычету;
		
	КонецЦикла;
	
	// Получение договоров по счету-фактуре
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("СчетаФактуры", 
		ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(
		ДанныеДвижений.ВыгрузитьКолонку("СчетФактура")));
	Запрос.УстановитьПараметр("ПустойДоговор", Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСПредъявленный.СчетФактура,
	|	НДСПредъявленный.ДоговорКонтрагента
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|ГДЕ
	|	НДСПредъявленный.Организация = &Организация
	|	И НДСПредъявленный.СчетФактура В(&СчетаФактуры)
	|	И НДСПредъявленный.ДоговорКонтрагента <> &ПустойДоговор";
	
	ДоговорыПоСчетуФактуре = Запрос.Выполнить().Выгрузить();
	ДоговорыПоСчетуФактуре.Индексы.Добавить("СчетФактура");
	
	Для Каждого Движение Из ДанныеДвижений Цикл
		
		НайденнаяСтрока = ДоговорыПоСчетуФактуре.Найти(Движение.СчетФактура,"СчетФактура");
		Если НайденнаяСтрока <> Неопределено Тогда
			Движение.ДоговорКонтрагента = НайденнаяСтрока.ДоговорКонтрагента;
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДвижений.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПокупки.НДСРаспределен, "Событие");
	
	ДанныеДвижений.Свернуть("Период,Организация,СчетФактура,ВидЦенности,
							|СтавкаНДС,СчетУчетаНДС,Поставщик,ДатаОплаты,Событие,
							|ДоговорКонтрагента,ИсправленныйСчетФактура,ДатаСобытия",
							"СуммаБезНДС,НДС");
							
	Возврат ДанныеДвижений;
	
КонецФункции

Функция ПодготовитьСчетаФактурыПоДаннымНДСПредъявленныйРеализация0(Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Реквизиты.Организация);
	Запрос.УстановитьПараметр("ГраницаОстатков",  Новый Граница(Реквизиты.КонецПериода, ВидГраницы.Включая));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Остатки.Организация КАК Организация,
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.Состояние КАК Состояние,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки КАК ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Остатки.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСПредъявленныйРеализация0Остатки.НДСОстаток КАК НДС,
	|	НДСПредъявленныйРеализация0Остатки.Поставщик КАК Поставщик,
	|	НДСПредъявленныйРеализация0Остатки.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|			&ГраницаОстатков,
	|			Организация = &Организация
	|				И ДокументОтгрузки = НЕОПРЕДЕЛЕНО) КАК НДСПредъявленныйРеализация0Остатки
	|ГДЕ
	|	НДСПредъявленныйРеализация0Остатки.СуммаБезНДСОстаток + НДСПредъявленныйРеализация0Остатки.НДСОстаток > 0";
	
	Результат = Запрос.Выполнить();
	
	СчетаФактурыПоДаннымНДСПредъявленныйРеализация0 = Результат.Выгрузить();
	СчетаФактурыПоДаннымНДСПредъявленныйРеализация0.Колонки.Добавить("Период",
		Новый ОписаниеТипов("Дата"));
	СчетаФактурыПоДаннымНДСПредъявленныйРеализация0.Колонки.Добавить("ДатаСобытия",
		Новый ОписаниеТипов("Дата"));
	СчетаФактурыПоДаннымНДСПредъявленныйРеализация0.Колонки.Добавить("Событие",
		Новый ОписаниеТипов("ПеречислениеСсылка.СобытияПоНДСПокупки"));
	
	СчетаФактурыПоДаннымНДСПредъявленныйРеализация0.ЗаполнитьЗначения(
		Реквизиты.Период, "Период,ДатаСобытия");
	СчетаФактурыПоДаннымНДСПредъявленныйРеализация0.ЗаполнитьЗначения(
		Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0, "Событие");
	
	Возврат СчетаФактурыПоДаннымНДСПредъявленныйРеализация0;
	
КонецФункции

Функция ПодготовитьДанныеДвиженийНДСПредъявленныйРеализация0РаспределениеНДС(
	Реквизиты, СчетаФактурыПоДаннымРаспределенияНДС, СчетаФактурыПоДаннымНДСПредъявленныйРеализация0)
	
	СчетаФактурыДляРаспределения = СчетаФактурыПоДаннымРаспределенияНДС.Скопировать();
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(
		СчетаФактурыПоДаннымНДСПредъявленныйРеализация0, СчетаФактурыДляРаспределения);
	
	ДанныеДвижений = СчетаФактурыДляРаспределения.СкопироватьКолонки();
	
	Если СчетаФактурыДляРаспределения.Количество() = 0 Тогда
		Возврат ДанныеДвижений;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация",   Реквизиты.Организация);
	Запрос.УстановитьПараметр("ВидЦенности",   Перечисления.ВидыЦенностей.ТоварыНесырьевые);
	Запрос.УстановитьПараметр("НачалоПериода", Реквизиты.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  Реквизиты.КонецПериода);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДСРеализация0Обороты.СчетФактура КАК ДокументОтгрузки,
	|	СУММА(НДСРеализация0Обороты.СуммаБезНДСПриход + НДСРеализация0Обороты.НДСПриход) КАК СуммаПриход
	|ПОМЕСТИТЬ ТаблицаДокументыОтгрузки
	|ИЗ
	|	РегистрНакопления.НДСРеализация0.Обороты(
	|			НАЧАЛОПЕРИОДА(&НачалоПериода, ДЕНЬ),
	|			КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
	|			Период,
	|			Организация = &Организация
	|				И ВидЦенности <> &ВидЦенности) КАК НДСРеализация0Обороты
	|ГДЕ
	|	НДСРеализация0Обороты.СуммаБезНДСПриход + НДСРеализация0Обороты.НДСПриход > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСРеализация0Обороты.СчетФактура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументыОтгрузки.ДокументОтгрузки,
	|	ТаблицаДокументыОтгрузки.СуммаПриход
	|ИЗ
	|	ТаблицаДокументыОтгрузки КАК ТаблицаДокументыОтгрузки";

	// Определим реализацию по ставке 0% за налоговый период
	БазоваяТаблицаРаспределения = Запрос.Выполнить().Выгрузить();

	Если БазоваяТаблицаРаспределения.Количество() > 0 Тогда

		Запрос.УстановитьПараметр("НаДату", Новый Граница(КонецДня(Реквизиты.Период), ВидГраницы.Включая));
		Запрос.УстановитьПараметр("МоментВремени", 
			Новый Граница(Новый МоментВремени(КонецДня(Реквизиты.Период), Реквизиты.Регистратор), ВидГраницы.Исключая));
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокументыОтгрузки.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ТаблицаДокументыОтгрузки.СуммаПриход КАК СуммаПриход
		|ПОМЕСТИТЬ СписокДокументовОтгрузки
		|ИЗ
		|	ТаблицаДокументыОтгрузки КАК ТаблицаДокументыОтгрузки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОтгрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НДСРеализация0Остатки.СчетФактура КАК ДокументОтгрузки,
		|	НДСРеализация0Остатки.Состояние,
		|	СУММА(НДСРеализация0Остатки.СуммаБезНДСОстаток + НДСРеализация0Остатки.НДСОстаток) КАК СуммаПриход
		|ПОМЕСТИТЬ РеализацииОжидающиеПодтверждения
		|ИЗ
		|	РегистрНакопления.НДСРеализация0.Остатки(
		|			&НаДату,
		|			Организация = &Организация
		|				И СчетФактура В
		|					(ВЫБРАТЬ
		|						СписокДокументовОтгрузки.ДокументОтгрузки
		|					ИЗ
		|						СписокДокументовОтгрузки)) КАК НДСРеализация0Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	НДСРеализация0Остатки.СчетФактура,
		|	НДСРеализация0Остатки.Состояние
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОтгрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НДСПредъявленныйРеализация0.Состояние КАК Состояние,
		|	НДСПредъявленныйРеализация0.ДокументОтгрузки КАК ДокументОтгрузки,
		|	СписокДокументовОтгрузки.СуммаПриход КАК СуммаПриход
		|ИЗ
		|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
		|			&МоментВремени,
		|			Организация = &Организация
		|				И НЕ ДокументОтгрузки В
		|						(ВЫБРАТЬ
		|							РеализацииОжидающиеПодтверждения.ДокументОтгрузки
		|						ИЗ
		|							РеализацииОжидающиеПодтверждения)) КАК НДСПредъявленныйРеализация0
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокДокументовОтгрузки КАК СписокДокументовОтгрузки
		|		ПО (СписокДокументовОтгрузки.ДокументОтгрузки = НДСПредъявленныйРеализация0.ДокументОтгрузки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацииОжидающиеПодтверждения.Состояние,
		|	РеализацииОжидающиеПодтверждения.ДокументОтгрузки,
		|	РеализацииОжидающиеПодтверждения.СуммаПриход
		|ИЗ
		|	РеализацииОжидающиеПодтверждения КАК РеализацииОжидающиеПодтверждения";

		ТаблицаТекущихСостояний = Запрос.Выполнить().Выгрузить();
		ТаблицаРаспределения = УчетНДС.СформироватьКорректирующиеЗаписи(БазоваяТаблицаРаспределения, ТаблицаТекущихСостояний,
			Новый Структура("СуммаПриход"), , , , Истина);
		ТаблицаРаспределения.Индексы.Добавить("Состояние");
		СтрокиБезСостояния =  ТаблицаРаспределения.НайтиСтроки(Новый Структура("Состояние", Неопределено));
		
		Для каждого Строка Из СтрокиБезСостояния Цикл
			Строка.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0;
		КонецЦикла;

	Иначе
		ТаблицаРаспределения = БазоваяТаблицаРаспределения.Скопировать();
		ТаблицаРаспределения.Колонки.Добавить("Состояние", Новый описаниеТипов("ПеречислениеСсылка.НДССостоянияРеализация0"));
		ТаблицаРаспределения.ЗаполнитьЗначения(Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение, "Состояние");
	КонецЕсли;
	
	УпорядочитьТаблицуПоДатеДокумента(ТаблицаРаспределения, "ДокументОтгрузки");
	УпорядочитьТаблицуПоДатеДокумента(СчетаФактурыДляРаспределения, "СчетФактура");
	
	МассивКоэффициентов = ТаблицаРаспределения.ВыгрузитьКолонку("СуммаПриход");

	ТаблицаРаспределения.Колонки.Добавить("НДС", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	МассивСумм = ОбщегоНазначенияБПКлиентСервер.РаспределитьПропорционально(
		СчетаФактурыДляРаспределения.Итог("НДС"), МассивКоэффициентов);
	Если МассивСумм <> Неопределено Тогда
		ТаблицаРаспределения.ЗагрузитьКолонку(МассивСумм, "НДС");
	КонецЕсли;
	
	Для Каждого ДокументОтгрузки Из ТаблицаРаспределения Цикл

		НДСОсталосьПогасить = ДокументОтгрузки.НДС;
		
		Для Каждого СчетФактура Из СчетаФактурыДляРаспределения Цикл

			Если НДСОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			Если СчетФактура.НДС <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НДС = Мин(НДСОсталосьПогасить, СчетФактура.НДС);
			СуммаБезНДС = Окр(СчетФактура.СуммаБезНДС * НДС / СчетФактура.НДС, 2);
			
			НоваяСтрока = ДанныеДвижений.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СчетФактура,, "СуммаБезНДС,НДС");
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДокументОтгрузки, "ДокументОтгрузки,Состояние");
			
			НоваяСтрока.НДС = НДС;
			НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
			
			СчетФактура.НДС = СчетФактура.НДС - НДС;
			СчетФактура.СуммаБезНДС = СчетФактура.СуммаБезНДС - СуммаБезНДС;
			
			НДСОсталосьПогасить = НДСОсталосьПогасить - НДС;
			
		КонецЦикла;

	КонецЦикла;

	Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл

		Если СтрокаТаблицы.Состояние = Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение Тогда
			СтрокаТаблицы.Событие = Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0;
		ИначеЕсли СтрокаТаблицы.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 Тогда
			СтрокаТаблицы.Событие = Перечисления.СобытияПоНДСПокупки.ПодтвержденаСтавка0;
		ИначеЕсли СтрокаТаблицы.Состояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0 Тогда
			СтрокаТаблицы.Событие = Перечисления.СобытияПоНДСПокупки.НеПодтвержденаСтавка0;
		КонецЕсли;

	КонецЦикла;
	
	Возврат ДанныеДвижений;
	
КонецФункции

Процедура УпорядочитьТаблицуПоДатеДокумента(Таблица, КолонкаДокумента)
	
	Если Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СписокДокументов = ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(
		Таблица.ВыгрузитьКолонку(КолонкаДокумента), Истина);
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДокументов", СписокДокументов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеПервичныхДокументов.Документ,
	|	ДанныеПервичныхДокументов.ДатаРегистратора
	|ИЗ
	|	РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|ГДЕ
	|	ДанныеПервичныхДокументов.Документ В(&СписокДокументов)";
	
	ТаблицаДат = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаДат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Таблица.Колонки.Добавить("ДатаДокумента", Новый ОписаниеТипов("Дата"));
	
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		
		НайденнаяСтрока = ТаблицаДат.Найти(СтрокаТаблицы[КолонкаДокумента], "Документ");
		Если НайденнаяСтрока <> Неопределено Тогда
			СтрокаТаблицы.ДатаДокумента = НайденнаяСтрока.ДатаРегистратора;
		КонецЕсли;
		
	КонецЦикла;
	
	Таблица.Сортировать("ДатаДокумента");
	Таблица.Колонки.Удалить("ДатаДокумента");
	
КонецПроцедуры

Функция ПодготовитьТаблицыДвиженийПоИзменениюСтоимостиОС(ДанныеПроводокОСиНМА, Реквизиты)
	
	ПустаяТаблица = Новый ТаблицаЗначений;
	ТаблицыДвижений = Новый Структура;
	
	ТаблицыДвижений.Вставить("ПервоначальныеСведенияОСБухгалтерскийУчет", ПустаяТаблица);
	ТаблицыДвижений.Вставить("ПервоначальныеСведенияОСНалоговыйУчет", ПустаяТаблица);
	ТаблицыДвижений.Вставить("ПараметрыАмортизацииОСБухгалтерскийУчет", ПустаяТаблица);
	ТаблицыДвижений.Вставить("ТаблицаАмортизационнойПремии", ПустаяТаблица);
	ТаблицыДвижений.Вставить("СобытияОСОрганизаций", ПустаяТаблица);

	ДанныеПроводок = ДанныеПроводокОСиНМА.Скопировать(Новый Структура("ЭтоОС",Истина));
	
	ТаблицаОС = ДанныеПроводок.СкопироватьКолонки("Субконто1,НДС,Модернизация");
	
	Для Каждого СтрокаПроводок Из ДанныеПроводок Цикл
		НоваяСтрока = ТаблицаОС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПроводок);
	КонецЦикла;
	
	ТаблицаОС.Колонки.Субконто1.Имя = "ОсновноеСредство";
	ТаблицаОС.Свернуть("ОсновноеСредство,Модернизация", "НДС");
	
 	Если ТаблицаОС.Количество() = 0 Тогда
		Возврат ТаблицыДвижений;
	КонецЕсли;
	
	СубконтоКВ = Новый Массив();
	СубконтоКВ.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
	СубконтоКВ.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыАмортизационнойПремии);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Реквизиты.Организация);
	Запрос.УстановитьПараметр("МоментДокумента",
									  Новый Граница(Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("НачалоПериода", Реквизиты.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", Реквизиты.КонецПериода);
	Запрос.УстановитьПараметр("СубконтоКВ", СубконтоКВ);
	Запрос.УстановитьПараметр("ТаблицаОС",  ТаблицаОС);
									  
	Запрос.Текст = 								  
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.Модернизация КАК Модернизация,
	|	ТаблицаОС.НДС КАК Сумма
	|ПОМЕСТИТЬ ВТТаблицаОС
	|ИЗ
	|	&ТаблицаОС КАК ТаблицаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|	СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета КАК СчетУчета
	|ПОМЕСТИТЬ СчетаУчетаОС
	|ИЗ
	|	РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
	|			&МоментДокумента,
	|			Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						ВТТаблицаОС.ОсновноеСредство
	|					ИЗ
	|						ВТТаблицаОС)) КАК СчетаБухгалтерскогоУчетаОССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.Организация КАК Организация,
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ПараметрВыработки КАК ПараметрВыработки,
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости
	|ПОМЕСТИТЬ ПервоначальныеСведенияОСБухгалтерскийУчет
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|			&МоментДокумента,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					ВТТаблицаОС.ОсновноеСредство
	|				ИЗ
	|					ВТТаблицаОС
	|				ГДЕ
	|					НЕ ВТТаблицаОС.Модернизация)) КАК ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.Организация КАК Организация,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.ПорядокВключенияСтоимостиВСоставРасходов КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.СпособОтраженияРасходовПриВключенииВСтоимость КАК СпособОтраженияРасходовПриВключенииВСтоимость,
	|	ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних.СпособОтраженияРасходовАналогичноАмортизации КАК СпособОтраженияРасходовАналогичноАмортизации
	|ПОМЕСТИТЬ ПервоначальныеСведенияОСНалоговыйУчет
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОСНалоговыйУчет.СрезПоследних(
	|			&МоментДокумента,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					ВТТаблицаОС.ОсновноеСредство
	|				ИЗ
	|					ВТТаблицаОС
	|				ГДЕ
	|					НЕ ВТТаблицаОС.Модернизация)) КАК ПервоначальныеСведенияОСНалоговыйУчетСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.Организация КАК Организация,
	|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРабот КАК ОбъемПродукцииРабот,
	|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.КоэффициентАмортизации КАК КоэффициентАмортизации,
	|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.КоэффициентУскорения КАК КоэффициентУскорения
	|ПОМЕСТИТЬ ПараметрыАмортизацииОСБухгалтерскийУчет
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	|			&МоментДокумента,
	|			Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						ВТТаблицаОС.ОсновноеСредство
	|					ИЗ
	|						ВТТаблицаОС)) КАК ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Счет КАК Счет,
	|	ХозрасчетныйОбороты.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОбороты.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОбороты.Организация КАК Организация,
	|	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА МодернизацияОСОС.СуммаМодернизацииНУ = 0
	|			ТОГДА 0
	|		КОГДА МодернизацияОСОС.СуммаКапитальныхВложенийВключаемыхВРасходыНУ >= МодернизацияОСОС.СуммаМодернизацииНУ
	|			ТОГДА 100
	|		ИНАЧЕ ВЫРАЗИТЬ(МодернизацияОСОС.СуммаКапитальныхВложенийВключаемыхВРасходыНУ / МодернизацияОСОС.СуммаМодернизацииНУ * 100 КАК ЧИСЛО(5, 2))
	|	КОНЕЦ / 100 КАК Коэффициент,
	|	ИСТИНА КАК МодернизацияОС,
	|	МодернизацияОСОС.Ссылка.СчетУчетаЗатратПоАмортизационнойПремии КАК СчетУчетаЗатратПоАмортизационнойПремии,
	|	МодернизацияОСОС.Ссылка.ПодразделениеОрганизацииПоАмортизационнойПремии КАК ПодразделениеОрганизацииПоАмортизационнойПремии,
	|	МодернизацияОСОС.Ссылка.СубконтоПоАмортизационнойПремии1 КАК СубконтоПоАмортизационнойПремии1,
	|	МодернизацияОСОС.Ссылка.СубконтоПоАмортизационнойПремии2 КАК СубконтоПоАмортизационнойПремии2,
	|	МодернизацияОСОС.Ссылка.СубконтоПоАмортизационнойПремии3 КАК СубконтоПоАмортизационнойПремии3,
	|	СчетаУчетаОС.СчетУчета КАК СчетУчета,
	|	МодернизацияОСОС.Ссылка.МестонахождениеОС КАК ПодразделениеУчета
	|ПОМЕСТИТЬ АмортизационнаяПремия
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыНаКапитальныеВложения),
	|			&СубконтоКВ,
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ВТТаблицаОС.ОсновноеСредство
	|					ИЗ
	|						ВТТаблицаОС
	|					ГДЕ
	|						ВТТаблицаОС.Модернизация)
	|				И Субконто2 ССЫЛКА Документ.МодернизацияОС,
	|			,
	|			) КАК ХозрасчетныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МодернизацияОС.ОС КАК МодернизацияОСОС
	|		ПО ХозрасчетныйОбороты.Субконто1 = МодернизацияОСОС.ОсновноеСредство
	|			И ХозрасчетныйОбороты.Субконто2 = МодернизацияОСОС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаУчетаОС КАК СчетаУчетаОС
	|		ПО ХозрасчетныйОбороты.Субконто1 = СчетаУчетаОС.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Счет,
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.Субконто2,
	|	ХозрасчетныйОбороты.Организация,
	|	ХозрасчетныйОбороты.Подразделение,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Документ.ПринятиеКУчетуОС).ПорядокВключенияСтоимостиВСоставРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации)
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Документ.ПринятиеКУчетуОС).ПроцентКапитальныхВложенийВключаемыхВРасходыНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ / 100,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыНаКапитальныеВложения),
	|			&СубконтоКВ,
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ВТТаблицаОС.ОсновноеСредство
	|					ИЗ
	|						ВТТаблицаОС
	|					ГДЕ
	|						НЕ ВТТаблицаОС.Модернизация)
	|				И Субконто2 ССЫЛКА Документ.ПринятиеКУчетуОС,
	|			,
	|			) КАК ХозрасчетныйОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство КАК ОсновноеСредство,
	|	ПервоначальныеСведенияОСБухгалтерскийУчет.Организация КАК Организация,
	|	ПервоначальныеСведенияОСБухгалтерскийУчет.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПервоначальныеСведенияОСБухгалтерскийУчет.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияОСБухгалтерскийУчет.ПервоначальнаяСтоимость + ВТТаблицаОС.Сумма КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияОСБухгалтерскийУчет.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияОСБухгалтерскийУчет.ПараметрВыработки КАК ПараметрВыработки,
	|	ПервоначальныеСведенияОСБухгалтерскийУчет.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости
	|ИЗ
	|	ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОСБухгалтерскийУчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаОС КАК ВТТаблицаОС
	|		ПО ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство = ВТТаблицаОС.ОсновноеСредство
	|			И (НЕ ВТТаблицаОС.Модернизация)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияОСНалоговыйУчет.ОсновноеСредство КАК ОсновноеСредство,
	|	ПервоначальныеСведенияОСНалоговыйУчет.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияОСНалоговыйУчет.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияОСНалоговыйУчет.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	ПервоначальныеСведенияОСНалоговыйУчет.Организация КАК Организация,
	|	ПервоначальныеСведенияОСНалоговыйУчет.ПервоначальнаяСтоимостьНУ + ВТТаблицаОС.Сумма КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияОСНалоговыйУчет.ПорядокВключенияСтоимостиВСоставРасходов КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	ПервоначальныеСведенияОСНалоговыйУчет.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияОСНалоговыйУчет.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияОСНалоговыйУчет.СпособОтраженияРасходовПриВключенииВСтоимость КАК СпособОтраженияРасходовПриВключенииВСтоимость,
	|	ПервоначальныеСведенияОСНалоговыйУчет.СпособОтраженияРасходовАналогичноАмортизации КАК СпособОтраженияРасходовАналогичноАмортизации
	|ИЗ
	|	ПервоначальныеСведенияОСНалоговыйУчет КАК ПервоначальныеСведенияОСНалоговыйУчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаОС КАК ВТТаблицаОС
	|		ПО ПервоначальныеСведенияОСНалоговыйУчет.ОсновноеСредство = ВТТаблицаОС.ОсновноеСредство
	|			И (НЕ ВТТаблицаОС.Модернизация)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.ОсновноеСредство КАК ОсновноеСредство,
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.Организация КАК Организация,
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.ОбъемПродукцииРабот КАК ОбъемПродукцииРабот,
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.СтоимостьДляВычисленияАмортизации + ЕСТЬNULL(ВТТаблицаОС.Сумма, 0) + ЕСТЬNULL(ВТТаблицаОСМодернизация.Сумма, 0) КАК СтоимостьДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.КоэффициентАмортизации КАК КоэффициентАмортизации,
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.КоэффициентУскорения КАК КоэффициентУскорения
	|ИЗ
	|	ПараметрыАмортизацииОСБухгалтерскийУчет КАК ПараметрыАмортизацииОСБухгалтерскийУчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаОС КАК ВТТаблицаОС
	|		ПО ПараметрыАмортизацииОСБухгалтерскийУчет.ОсновноеСредство = ВТТаблицаОС.ОсновноеСредство
	|			И (НЕ ВТТаблицаОС.Модернизация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаОС КАК ВТТаблицаОСМодернизация
	|		ПО ПараметрыАмортизацииОСБухгалтерскийУчет.ОсновноеСредство = ВТТаблицаОСМодернизация.ОсновноеСредство
	|			И (ВТТаблицаОСМодернизация.Модернизация)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АмортизационнаяПремия.Счет КАК Счет,
	|	АмортизационнаяПремия.Субконто1 КАК Субконто1,
	|	АмортизационнаяПремия.Субконто2 КАК Субконто2,
	|	АмортизационнаяПремия.Организация КАК Организация,
	|	АмортизационнаяПремия.Подразделение КАК Подразделение,
	|	АмортизационнаяПремия.Коэффициент * ВТТаблицаОС.Сумма КАК СуммаНУ,
	|	АмортизационнаяПремия.МодернизацияОС КАК МодернизацияОС,
	|	АмортизационнаяПремия.СчетУчета КАК СчетУчета,
	|	АмортизационнаяПремия.ПодразделениеУчета КАК ПодразделениеУчета,
	|	АмортизационнаяПремия.СчетУчетаЗатратПоАмортизационнойПремии КАК СчетУчетаЗатратПоАмортизационнойПремии,
	|	АмортизационнаяПремия.ПодразделениеОрганизацииПоАмортизационнойПремии КАК ПодразделениеОрганизацииПоАмортизационнойПремии,
	|	АмортизационнаяПремия.СубконтоПоАмортизационнойПремии1 КАК СубконтоПоАмортизационнойПремии1,
	|	АмортизационнаяПремия.СубконтоПоАмортизационнойПремии2 КАК СубконтоПоАмортизационнойПремии2,
	|	АмортизационнаяПремия.СубконтоПоАмортизационнойПремии3 КАК СубконтоПоАмортизационнойПремии3
	|ИЗ
	|	АмортизационнаяПремия КАК АмортизационнаяПремия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаОС КАК ВТТаблицаОС
	|		ПО АмортизационнаяПремия.Субконто1 = ВТТаблицаОС.ОсновноеСредство
	|			И АмортизационнаяПремия.МодернизацияОС = ВТТаблицаОС.Модернизация";
								  
	Результат = Запрос.ВыполнитьПакет();
	
	Если Результат.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицыДвижений.ПервоначальныеСведенияОСБухгалтерскийУчет = Результат[6].Выгрузить();
	ТаблицыДвижений.ПервоначальныеСведенияОСНалоговыйУчет = Результат[7].Выгрузить();
	ТаблицыДвижений.ПараметрыАмортизацииОСБухгалтерскийУчет = Результат[8].Выгрузить();
	ТаблицыДвижений.ТаблицаАмортизационнойПремии = Результат[9].Выгрузить();
	
	ТаблицаОС.Свернуть("ОсновноеСредство", "НДС");
	
	СобытияОСОрганизаций = РегистрыСведений.СобытияОСОрганизаций.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	СобытияОСОрганизаций.Колонки.Удалить("Регистратор");
	СобытияОСОрганизаций.Колонки.Удалить("Активность");
	СобытияОСОрганизаций.Колонки.Удалить("НомерСтроки");
	
	НазваниеДокумента = Реквизиты.Регистратор.Метаданные().Представление();

	Для Каждого СтрокаТаблицы Из ТаблицаОС Цикл
		
		НоваяСтрока = СобытияОСОрганизаций.Добавить();
		НоваяСтрока.Период            = Реквизиты.Период;
		НоваяСтрока.ОсновноеСредство  = СтрокаТаблицы.ОсновноеСредство;
		НоваяСтрока.Организация       = Реквизиты.Организация;
		НоваяСтрока.Событие           = Реквизиты.СобытиеОС;
		НоваяСтрока.НазваниеДокумента = НазваниеДокумента;
		НоваяСтрока.НомерДокумента    = Реквизиты.Номер;
		НоваяСтрока.СуммаЗатратБУ     = СтрокаТаблицы.НДС;
		НоваяСтрока.СуммаЗатратНУ     = СтрокаТаблицы.НДС;
		
	КонецЦикла;
	
	ТаблицыДвижений.СобытияОСОрганизаций = СобытияОСОрганизаций;
	
	Возврат ТаблицыДвижений;
	
КонецФункции

Процедура СформироватьДвиженияПоИзменениюСтоимостиОС(ТаблицыДвижений, Реквизиты, Движения, Отказ)
	
	Если ТаблицыДвижений.ПервоначальныеСведенияОСБухгалтерскийУчет.Количество() > 0 Тогда
		Для Каждого СтрокаТаблицы Из ТаблицыДвижений.ПервоначальныеСведенияОСБухгалтерскийУчет Цикл
			Движение = Движения.ПервоначальныеСведенияОСБухгалтерскийУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
			ЗаполнитьЗначенияСвойств(Движение, Реквизиты, "Период");
		КонецЦикла;
		Движения.ПервоначальныеСведенияОСБухгалтерскийУчет.Записывать = Истина;	
	КонецЕсли;
	
	Если ТаблицыДвижений.ПервоначальныеСведенияОСНалоговыйУчет.Количество() > 0 Тогда
		Для Каждого СтрокаТаблицы Из ТаблицыДвижений.ПервоначальныеСведенияОСНалоговыйУчет Цикл
			Движение = Движения.ПервоначальныеСведенияОСНалоговыйУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
			ЗаполнитьЗначенияСвойств(Движение, Реквизиты, "Период");
		КонецЦикла;
		Движения.ПервоначальныеСведенияОСНалоговыйУчет.Записывать = Истина;	
	КонецЕсли;
	
	Если ТаблицыДвижений.ПараметрыАмортизацииОСБухгалтерскийУчет.Количество() > 0 Тогда
		Для Каждого СтрокаТаблицы Из ТаблицыДвижений.ПараметрыАмортизацииОСБухгалтерскийУчет Цикл
			Движение = Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
			ЗаполнитьЗначенияСвойств(Движение, Реквизиты, "Период");
		КонецЦикла;
		Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Записывать = Истина;	
	КонецЕсли;
	
	Если ТаблицыДвижений.ТаблицаАмортизационнойПремии.Количество() > 0 Тогда
		Для Каждого СтрокаТаблицы Из ТаблицыДвижений.ТаблицаАмортизационнойПремии Цикл
			
			Проводка = Движения.Хозрасчетный.Добавить();

			Проводка.Период      = Реквизиты.Период;
			Проводка.Организация = Реквизиты.Организация;

			Проводка.Содержание = НСтр("ru = 'Амортизационная премия'");

			Проводка.СчетДт = СтрокаТаблицы.Счет;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства",
				СтрокаТаблицы.Субконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ДокументыАмортизационнойПремии",
				СтрокаТаблицы.Субконто2);

			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);

			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;

			Проводка.СуммаНУДт = СтрокаТаблицы.СуммаНУ;
			
			Если СтрокаТаблицы.МодернизацияОС Тогда
				
				//Спишем амортизационную премию
				Проводка = Движения.Хозрасчетный.Добавить();

				Проводка.Период      = Реквизиты.Период;
				Проводка.Организация = Реквизиты.Организация;

				Проводка.Содержание = НСтр("ru = 'Амортизационная премия'");

				Проводка.СчетКт = СтрокаТаблицы.Счет;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства",
					СтрокаТаблицы.Субконто1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ДокументыАмортизационнойПремии",
					СтрокаТаблицы.Субконто2);

				СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);

				Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
					Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
				КонецЕсли;

				Проводка.СуммаНУКт = СтрокаТаблицы.СуммаНУ;
				
				//Включим амортизационную премию в состав расходов
				Проводка = Движения.Хозрасчетный.Добавить();

				Проводка.Период      = Реквизиты.Период;
				Проводка.Организация = Реквизиты.Организация;

				Проводка.Содержание = НСтр("ru = 'Амортизационная премия'");

				Проводка.СчетДт = СтрокаТаблицы.СчетУчетаЗатратПоАмортизационнойПремии;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1,
					СтрокаТаблицы.СубконтоПоАмортизационнойПремии1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2,
					СтрокаТаблицы.СубконтоПоАмортизационнойПремии2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3,
					СтрокаТаблицы.СубконтоПоАмортизационнойПремии3);

				СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);

				Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
					Проводка.ПодразделениеДт = СтрокаТаблицы.ПодразделениеОрганизацииПоАмортизационнойПремии;
				КонецЕсли;

				Проводка.СуммаНУДт = СтрокаТаблицы.СуммаНУ;
				Проводка.СуммаВРДт = -СтрокаТаблицы.СуммаНУ;
				
				Проводка.СчетКт = СтрокаТаблицы.СчетУчета;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства",
					СтрокаТаблицы.Субконто1);
					
				СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
				Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
					Проводка.ПодразделениеКт = СтрокаТаблицы.ПодразделениеУчета;
				КонецЕсли;
				
				Проводка.СуммаНУКт = СтрокаТаблицы.СуммаНУ;
				Проводка.СуммаВРКт = -СтрокаТаблицы.СуммаНУ;
				
			КонецЕсли;
			
		КонецЦикла;
		Движения.Хозрасчетный.Записывать = Истина;	
	КонецЕсли;

	Если ТаблицыДвижений.СобытияОСОрганизаций.Количество() > 0 Тогда
		Для Каждого СтрокаТаблицы Из ТаблицыДвижений.СобытияОСОрганизаций Цикл
			Движение = Движения.СобытияОСОрганизаций.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
		КонецЦикла;
		Движения.СобытияОСОрганизаций.Записывать = Истина;	
	КонецЕсли;
	
КонецПроцедуры

Функция ПодготовитьТаблицыДвиженийПоИзменениюСтоимостиНМА(ДанныеПроводокОСиНМА, Реквизиты)
	
	ПустаяТаблица = Новый ТаблицаЗначений;
	ТаблицыДвижений = Новый Структура;
	
	ТаблицыДвижений.Вставить("ПервоначальныеСведенияНМАБухгалтерскийУчет", ПустаяТаблица);
	ТаблицыДвижений.Вставить("ПервоначальныеСведенияНМАНалоговыйУчет", ПустаяТаблица);

	ДанныеПроводок = ДанныеПроводокОСиНМА.Скопировать(Новый Структура("ЭтоНМА",Истина));
	
	ТаблицаНМА = ДанныеПроводок.СкопироватьКолонки("Субконто1,НДС");
	
	Для Каждого СтрокаПроводок Из ДанныеПроводок Цикл
		НоваяСтрока = ТаблицаНМА.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПроводок);
	КонецЦикла;
	
	ТаблицаНМА.Колонки.Субконто1.Имя = "НематериальныйАктив";
	ТаблицаНМА.Свернуть("НематериальныйАктив", "НДС");
	
	Если ТаблицаНМА.Количество() = 0 Тогда
		Возврат ТаблицыДвижений;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Реквизиты.Организация);
	Запрос.УстановитьПараметр("МоментДокумента",
									  Новый Граница(Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ТаблицаНМА",  ТаблицаНМА);
									  
	Запрос.Текст = 								  
	"ВЫБРАТЬ
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаНМА.НДС КАК Сумма
	|ПОМЕСТИТЬ ВТТаблицаНМА
	|ИЗ
	|	&ТаблицаНМА КАК ТаблицаНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Организация КАК Организация,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Коэффициент КАК Коэффициент
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАБухгалтерскийУчет
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|			&МоментДокумента,
	|			НематериальныйАктив В
	|				(ВЫБРАТЬ
	|					ВТТаблицаНМА.НематериальныйАктив
	|				ИЗ
	|					ВТТаблицаНМА)) КАК ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ДатаПриобретения КАК ДатаПриобретения,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.Организация КАК Организация,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ПорядокВключенияСтоимостиВСоставРасходов КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.СпособОтраженияРасходовПриВключенииВСтоимость КАК СпособОтраженияРасходовПриВключенииВСтоимость,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.СпособОтраженияРасходовАналогичноАмортизации КАК СпособОтраженияРасходовАналогичноАмортизации
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАНалоговыйУчет
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СрезПоследних(
	|			&МоментДокумента,
	|			НематериальныйАктив В
	|				(ВЫБРАТЬ
	|					ВТТаблицаНМА.НематериальныйАктив
	|				ИЗ
	|					ВТТаблицаНМА)) КАК ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.Организация КАК Организация,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.ПервоначальнаяСтоимость + ВТТаблицаНМА.Сумма КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.Коэффициент КАК Коэффициент
	|ИЗ
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет КАК ПервоначальныеСведенияНМАБухгалтерскийУчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаНМА КАК ВТТаблицаНМА
	|		ПО ПервоначальныеСведенияНМАБухгалтерскийУчет.НематериальныйАктив = ВТТаблицаНМА.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияНМАНалоговыйУчет.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.ДатаПриобретения КАК ДатаПриобретения,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.Организация КАК Организация,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.ПервоначальнаяСтоимостьНУ + ВТТаблицаНМА.Сумма КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.ПорядокВключенияСтоимостиВСоставРасходов КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.СпособОтраженияРасходовПриВключенииВСтоимость КАК СпособОтраженияРасходовПриВключенииВСтоимость,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.СпособОтраженияРасходовАналогичноАмортизации КАК СпособОтраженияРасходовАналогичноАмортизации
	|ИЗ
	|	ПервоначальныеСведенияНМАНалоговыйУчет КАК ПервоначальныеСведенияНМАНалоговыйУчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаНМА КАК ВТТаблицаНМА
	|		ПО ПервоначальныеСведенияНМАНалоговыйУчет.НематериальныйАктив = ВТТаблицаНМА.НематериальныйАктив";
								  
	Результат = Запрос.ВыполнитьПакет();
	
	Если Результат.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицыДвижений.ПервоначальныеСведенияНМАБухгалтерскийУчет = Результат[3].Выгрузить();
	ТаблицыДвижений.ПервоначальныеСведенияНМАНалоговыйУчет = Результат[4].Выгрузить();
	
	Возврат ТаблицыДвижений;
	
КонецФункции

Процедура СформироватьДвиженияПоИзменениюСтоимостиНМА(ТаблицыДвижений, Реквизиты, Движения, Отказ)
	
	Если ТаблицыДвижений.ПервоначальныеСведенияНМАБухгалтерскийУчет.Количество() > 0 Тогда
		Для Каждого СтрокаТаблицы Из ТаблицыДвижений.ПервоначальныеСведенияНМАБухгалтерскийУчет Цикл
			Движение = Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
			ЗаполнитьЗначенияСвойств(Движение, Реквизиты, "Период");
		КонецЦикла;
		Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Записывать = Истина;	
	КонецЕсли;
	
	Если ТаблицыДвижений.ПервоначальныеСведенияНМАНалоговыйУчет.Количество() > 0 Тогда
		Для Каждого СтрокаТаблицы Из ТаблицыДвижений.ПервоначальныеСведенияНМАНалоговыйУчет Цикл
			Движение = Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаТаблицы);
			ЗаполнитьЗначенияСвойств(Движение, Реквизиты, "Период");
		КонецЦикла;
		Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Записывать = Истина;	
	КонецЕсли;
	
КонецПроцедуры

Процедура СформирватьДвиженияБазаРаспределенияНДС(Реквизиты)
	
	МенеджерЗаписи = РегистрыСведений.БазаРаспределенияНДС.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Реквизиты);
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура СформироватьПроводкиВключениеНДСВРасходыНУ(
	ДанныеПроводокОСиНМА, ТаблицыДвиженийОС, ТаблицыДвиженийНМА, Реквизиты, Движения, Отказ)
	
	ДанныеПроводок = ДанныеПроводокОСиНМА.Скопировать(
		Новый Структура("Модернизация", Ложь), "СчетЗатрат, Субконто1, Подразделение, НДС");
	
	Если ДанныеПроводок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("ПорядокВключенияСтоимостиВСоставРасходов", 
		Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету);
	
	Если ТаблицыДвиженийОС.ПервоначальныеСведенияОСНалоговыйУчет.Количество() > 0 Тогда
		ВыборкаСведенийОС = ТаблицыДвиженийОС.ПервоначальныеСведенияОСНалоговыйУчет.НайтиСтроки(Отбор);
	Иначе
		ВыборкаСведенийОС = Новый Массив;
	КонецЕсли;
		
	Если ТаблицыДвиженийНМА.ПервоначальныеСведенияНМАНалоговыйУчет.Количество() > 0 Тогда
		ВыборкаСведенийНМА = ТаблицыДвиженийНМА.ПервоначальныеСведенияНМАНалоговыйУчет.НайтиСтроки(Отбор);
	Иначе
		ВыборкаСведенийНМА = Новый Массив;
	КонецЕсли;
	
	Если ВыборкаСведенийОС.Количество() + ВыборкаСведенийНМА.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТипыОбъектыУчета = Новый Массив;
	ТипыОбъектыУчета.Добавить(Тип("СправочникСсылка.ОсновныеСредства"));
	ТипыОбъектыУчета.Добавить(Тип("СправочникСсылка.НематериальныеАктивы"));
	
	ТаблицаДляСписания = Новый ТаблицаЗначений;
	ТаблицаДляСписания.Колонки.Добавить("ОбъектУчета",
		Новый ОписаниеТипов(Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"), ТипыОбъектыУчета));
	ТаблицаДляСписания.Колонки.Добавить("НаправлениеАмортизации",
		Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияРасходовПоАмортизации"));
	ТаблицаДляСписания.Колонки.Добавить("СчетНачисленияАмортизации",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаДЛяСписания.Колонки.Добавить("Подразделение",
		Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаДляСписания.Колонки.Добавить("СуммаАмортизацииБУ",
		ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаДляСписания.Колонки.Добавить("СуммаАмортизацииПР",
		ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаДляСписания.Колонки.Добавить("СуммаАмортизацииВР",
		ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаДляСписания.Колонки.Добавить("СуммаАмортизацииНУ",
		ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаДляСписания.Колонки.Добавить("НомерСтроки",
		ОбщегоНазначения.ОписаниеТипаЧисло(5, 0));
	
	ЗаменятьСтатьюЗатрат = Новый Соответствие;
		
	Для Каждого СведенияОС Из ВыборкаСведенийОС Цикл
		
		Если НЕ ЗначениеЗаполнено(СведенияОС.СпособОтраженияРасходовПриВключенииВСтоимость) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаменятьСтатьюЗатрат.Вставить(СведенияОС.ОсновноеСредство, СведенияОС.СпособОтраженияРасходовАналогичноАмортизации);
		
		МассивДопСведений = ДанныеПроводок.НайтиСтроки(Новый Структура("Субконто1", СведенияОС.ОсновноеСредство));
		Для Каждого ДопСведения Из МассивДопСведений Цикл
			Если ДопСведения.НДС = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаСписания = ТаблицаДляСписания.Добавить();
			СтрокаСписания.ОбъектУчета               = СведенияОС.ОсновноеСредство;
			СтрокаСписания.НаправлениеАмортизации    = СведенияОС.СпособОтраженияРасходовПриВключенииВСтоимость;
			СтрокаСписания.СчетНачисленияАмортизации = ДопСведения.СчетЗатрат;
			СтрокаСписания.Подразделение             = ДопСведения.Подразделение;
			СтрокаСписания.СуммаАмортизацииНУ        = ДопСведения.НДС;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого СведенияНМА Из ВыборкаСведенийНМА Цикл
		
		Если НЕ ЗначениеЗаполнено(СведенияНМА.СпособОтраженияРасходовПриВключенииВСтоимость) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаменятьСтатьюЗатрат.Вставить(СведенияНМА.НематериальныйАктив, СведенияНМА.СпособОтраженияРасходовАналогичноАмортизации);
		
		МассивДопСведений = ДанныеПроводок.НайтиСтроки(Новый Структура("Субконто1", СведенияНМА.НематериальныйАктив));
		Для Каждого ДопСведения Из МассивДопСведений Цикл
			Если ДопСведения.НДС = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаСписания = ТаблицаДляСписания.Добавить();
			СтрокаСписания.ОбъектУчета               = СведенияНМА.НематериальныйАктив;
			СтрокаСписания.НаправлениеАмортизации    = СведенияНМА.СпособОтраженияРасходовПриВключенииВСтоимость;
			СтрокаСписания.СчетНачисленияАмортизации = ДопСведения.СчетЗатрат;
			СтрокаСписания.Подразделение             = ДопСведения.Подразделение;
			СтрокаСписания.СуммаАмортизацииНУ        = ДопСведения.НДС;
		КонецЦикла;
		
	КонецЦикла;
	
	Если ТаблицаДляСписания.Количество() > 0 Тогда
		
		ТаблицаРеквизитов = Новый ТаблицаЗначений;
		ТаблицаРеквизитов.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
		ТаблицаРеквизитов.Колонки.Добавить("ИмяСписка", ОбщегоНазначения.ОписаниеТипаСтрока(1));
		ТаблицаРеквизитов.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
		ТаблицаРеквизитов.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
		ЗаполнитьЗначенияСвойств(ТаблицаРеквизитов.Добавить(), Реквизиты);
		
		ТаблицаЗатратПоАмортизации = 
			УправлениеВнеоборотнымиАктивамиПереопределяемый.ПодготовитьТаблицуРаспределениеАмортизацииПоНаправлениямРегл(
				ТаблицаДляСписания, ТаблицаРеквизитов, Отказ);
			
		СтатьяЗатрат = Справочники.СтатьиЗатрат.ПредопределенныйЭлемент("НеамортизируемоеИмущество");
			
		Для каждого СтрокаОС Из ТаблицаЗатратПоАмортизации Цикл
			СтрокаОС.СуммаВР = СтрокаОС.СуммаВР - СтрокаОС.СуммаНУ;
			
			Если НЕ ЗаменятьСтатьюЗатрат[СтрокаОС.ОбъектУчета] Тогда
				Продолжить;
			КонецЕсли;
			
			Для Сч = 1 По 3 Цикл
				Если ТипЗнч(СтрокаОС["Субконто" + Сч]) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
					СтрокаОС["Субконто" + Сч] = СтатьяЗатрат;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
			
		ТаблицаРеквизитов.Колонки.Добавить("Содержание");
		ТаблицаРеквизитов.ЗаполнитьЗначения(НСтр("ru = 'Включение стоимости в состав расходов (НУ)'"), "Содержание");
			
		УправлениеВнеоборотнымиАктивами.СформироватьДвиженияНачислениеАмортизации(
			ТаблицаЗатратПоАмортизации, ТаблицаРеквизитов, Движения, Отказ);
			
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьПроводкиСпецодеждаСпецоснасткаВЭксплуатации(ДанныеПроводок, Реквизиты, Движения, Отказ)
	
	ВидыСпецматериалов = Новый Массив;
	ВидыСпецматериалов.Добавить("Спецодежда");
	ВидыСпецматериалов.Добавить("Спецоснастка");
	
	Для Каждого ВидСпецматериала Из ВидыСпецматериалов Цикл
		
		СчетУчетаСпецматериала = ПланыСчетов.Хозрасчетный[ВидСпецматериала + "ВЭксплуатации"];
		
		Отбор = Новый Структура("СчетЗатрат", СчетУчетаСпецматериала);
		Спецматериалы = ДанныеПроводок.Скопировать(Отбор, "СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,НДС");
		Если Спецматериалы.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетУчетаСпецматериала);
		ВидыСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные;
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ВидыСубконто.Номенклатура Тогда
				Спецматериалы.Колонки["Субконто" + Ном].Имя = "Номенклатура";
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ВидыСубконто.ПартииМатериаловВЭксплуатации Тогда
				Спецматериалы.Колонки["Субконто" + Ном].Имя = "ПартияМатериаловВЭксплуатации";
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ВидыСубконто.РаботникиОрганизаций Тогда
				Спецматериалы.Колонки["Субконто" + Ном].Имя = "ФизЛицо";
			КонецЕсли;
		КонецЦикла;
		
		Если Спецматериалы.Колонки.Найти("Номенклатура") = Неопределено Тогда
			Спецматериалы.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		КонецЕсли;
		Если Спецматериалы.Колонки.Найти("ПартияМатериаловВЭксплуатации") = Неопределено Тогда
			Спецматериалы.Колонки.Добавить("ПартияМатериаловВЭксплуатации", Документы.ТипВсеСсылки());
		КонецЕсли;
		Если Спецматериалы.Колонки.Найти("ФизЛицо") = Неопределено Тогда
			Спецматериалы.Колонки.Добавить("ФизЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
		КонецЕсли;
		
		СпособПогашенияСтоимостиНУ    = УчетнаяПолитика.СпособПогашенияСтоимостиСпецодеждыНУ(Реквизиты.Организация, Реквизиты.Период);
		ДоступнаАмортизацияСпецодежды = Реквизиты.Период < УчетМатериаловВЭксплуатации.ДатаОтменыСпецодежды();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Спецматериалы", Спецматериалы);
		Запрос.УстановитьПараметр("СчетМЦ", ПланыСчетов.Хозрасчетный[ВидСпецматериала + "ВЭксплуатацииВспомогательный"]);
		Запрос.УстановитьПараметр("СпособПогашенияСтоимостиНУ", СпособПогашенияСтоимостиНУ);

		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Спецматериалы.СчетЗатрат КАК СчетПередачи,
		|	Спецматериалы.Номенклатура КАК Номенклатура,
		|	Спецматериалы.ПартияМатериаловВЭксплуатации КАК ПартияМатериаловВЭксплуатации,
		|	Спецматериалы.ФизЛицо КАК ФизЛицо,
		|	Спецматериалы.Подразделение КАК Подразделение,
		|	Спецматериалы.НДС КАК Сумма
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	&Спецматериалы КАК Спецматериалы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ПартияМатериаловВЭксплуатации,
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.СчетПередачи КАК СчетПередачи,
		|	&СчетМЦ КАК СчетМЦ,
		|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ТаблицаНоменклатуры.ПартияМатериаловВЭксплуатации КАК ПартияМатериаловВЭксплуатации,
		|	ТаблицаНоменклатуры.ФизЛицо КАК ФизЛицо,
		|	ТаблицаНоменклатуры.Подразделение КАК Подразделение,
		|	ПередачаМатериаловВЭксплуатацию.НазначениеИспользования.СпособПогашенияСтоимости КАК СпособПогашенияСтоимости,
		|	ВЫБОР
		|		КОГДА &СпособПогашенияСтоимостиНУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиНУ.ПриПередачеВЭксплуатацию)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию)
		|		ИНАЧЕ ПередачаМатериаловВЭксплуатацию.НазначениеИспользования.СпособПогашенияСтоимости
		|	КОНЕЦ КАК СпособПогашенияСтоимостиНУ,
		|	ПередачаМатериаловВЭксплуатацию.НазначениеИспользования.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
		|	ТаблицаНоменклатуры.Сумма КАК СуммаМЦ,
		|	ТаблицаНоменклатуры.Сумма КАК СуммаМЦНУ
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаМатериаловВЭксплуатацию.Спецодежда КАК ПередачаМатериаловВЭксплуатацию
		|		ПО ТаблицаНоменклатуры.Номенклатура = ПередачаМатериаловВЭксплуатацию.Номенклатура
		|			И ТаблицаНоменклатуры.ПартияМатериаловВЭксплуатации = ПередачаМатериаловВЭксплуатацию.Ссылка
		|			И (&ДопУсловие)
		|ГДЕ
		|	ТаблицаНоменклатуры.ПартияМатериаловВЭксплуатации ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию
		|	И НЕ ПередачаМатериаловВЭксплуатацию.НазначениеИспользования ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.СчетПередачи,
		|	&СчетМЦ,
		|	ТаблицаНоменклатуры.Номенклатура,
		|	ТаблицаНоменклатуры.ПартияМатериаловВЭксплуатации,
		|	ТаблицаНоменклатуры.ФизЛицо,
		|	ТаблицаНоменклатуры.Подразделение,
		|	ДокПартия.НазначениеИспользования.СпособПогашенияСтоимости,
		|	ВЫБОР
		|		КОГДА &СпособПогашенияСтоимостиНУ = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимостиНУ.ПриПередачеВЭксплуатацию)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию)
		|		ИНАЧЕ ДокПартия.НазначениеИспользования.СпособПогашенияСтоимости
		|	КОНЕЦ,
		|	ДокПартия.НазначениеИспользования.СпособОтраженияРасходов,
		|	ТаблицаНоменклатуры.Сумма,
		|	ТаблицаНоменклатуры.Сумма
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПартияМатериаловВЭксплуатации КАК ДокПартия
		|		ПО ТаблицаНоменклатуры.Номенклатура = ДокПартия.Номенклатура
		|			И ТаблицаНоменклатуры.ПартияМатериаловВЭксплуатации = ДокПартия.Ссылка
		|ГДЕ
		|	ТаблицаНоменклатуры.ПартияМатериаловВЭксплуатации ССЫЛКА Документ.ПартияМатериаловВЭксплуатации
		|	И НЕ ДокПартия.НазначениеИспользования ЕСТЬ NULL";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Спецодежда", ВидСпецматериала);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловие",
			?(ВидСпецматериала = "Спецодежда", "ТаблицаНоменклатуры.ФизЛицо = ПередачаМатериаловВЭксплуатацию.ФизЛицо", "ИСТИНА"));
		
		ТаблицаПереданныеМатериалы = Запрос.Выполнить().Выгрузить();
		
		ТаблицаПереданныеМатериалы.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
		ТаблицаПереданныеМатериалы.Колонки.Добавить("СуммаНУ", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
		ТаблицаПереданныеМатериалы.Колонки.Добавить("СуммаПР", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
		ТаблицаПереданныеМатериалы.Колонки.Добавить("СуммаВР", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
		ТаблицаПереданныеМатериалы.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(15,3));
		ТаблицаПереданныеМатериалы.Колонки.Добавить("КоличествоМЦ", ОбщегоНазначения.ОписаниеТипаЧисло(15,3));
		
		ПриПередачеВЭксплуатацию = Перечисления.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию;
		Для Каждого СтрокаТаблицы Из ТаблицаПереданныеМатериалы Цикл
			Если (НЕ ДоступнаАмортизацияСпецодежды)
				ИЛИ СтрокаТаблицы.СпособПогашенияСтоимости = ПриПередачеВЭксплуатацию Тогда
				СтрокаТаблицы.Сумма = СтрокаТаблицы.СуммаМЦ;
			КонецЕсли;
			Если (НЕ ДоступнаАмортизацияСпецодежды)
				ИЛИ СтрокаТаблицы.СпособПогашенияСтоимостиНУ = ПриПередачеВЭксплуатацию Тогда
				СтрокаТаблицы.СуммаНУ = СтрокаТаблицы.СуммаМЦ;
			КонецЕсли;
			СтрокаТаблицы.СуммаВР = СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНУ;
		КонецЦикла;
		
		ТаблицаНачислениеПогашенияСтоимостиМатериалов = 
			УчетМатериаловВЭксплуатации.ПодготовитьТаблицуНачислениеПогашенияСтоимостиСпецодеждыСпецоснастки(
			ТаблицаПереданныеМатериалы, Реквизиты, Отказ);
		
		УчетМатериаловВЭксплуатации.СформироватьПроводкиПогашениеСтоимостиСпецодеждыСпецоснастки(
			ТаблицаНачислениеПогашенияСтоимостиМатериалов, Реквизиты, Движения, Отказ);
		
		УчетМатериаловВЭксплуатации.СформироватьПроводкиПоступлениеСпецодеждыСпецоснасткиВЭксплуатациюМЦ(
			ТаблицаНачислениеПогашенияСтоимостиМатериалов, Реквизиты, Движения, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ТаможенныйСоюз

Процедура СформироватьДвиженияВвозТоваровИзТаможенногоСоюза(ТаблицаТовары, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаТовары)
	 Или ТаблицаТовары.Итог("НДС") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьРеквизитыВвозТоваровИзТаможенногоСоюза(ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];

	ПодготовитьПараметрыВвозТоваровИзТаможенногоСоюза(Параметры, ТаблицаТовары);
	
	ДанныеДвижений = ПодготовитьДанныеДвиженийПоступлениеТоваровУслугОтПоставщика(Параметры.Товары, Неопределено, Реквизиты);
	СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьРеквизитыВвозТоваровИзТаможенногоСоюза(ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                    // <ДокументСсылка>
	+ "Период,"                         // <Дата>
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "НДСВключенВСтоимость,"           // <Булево>
	+ "УчетАгентскогоНДС,"              // <Булево>
	+ "Контрагент,"                     // <СправочникСсылка.Контрагенты>
	+ "ДоговорКонтрагента"              // <СправочникСсылка.ДоговорыКонтрагентов>
	;
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура ПодготовитьПараметрыВвозТоваровИзТаможенногоСоюза(Параметры, ТаблицаТовары)

	// Подготовка таблицы Товары

	СписокОбязательныхКолонок = ""
	+ "Партия,"              // <ДокументСсылка> - документ поступления товаров
	+ "ВидЦенности,"         // <ПеречислениеСсылка.ВидыЦеннотсей> 
	+ "Номенклатура,"        // <СправочникСсылка.Номенклатура> - поступивший ранее товар
	+ "Склад,"               // <СправочникСсылка.Склады> - склад, на который поступил ранее товар
	+ "Подразделение,"       // Подразделение для проводок
	+ "СчетУчета,"           // <ПланСчетовСсылка.Хозрасчетный>
	+ "СчетУчетаНДС,"        // <ПланСчетовСсылка.Хозрасчетный>
	+ "СпособУчетаНДС,"      // <ПеречислениеСсылка.СпособыУчетаНДС>
	+ "СуммаБезНДС,"         // <Число,15,2> - сумма без НДС в рублях
	+ "НДС,"                 // <Число,15,2> - сумма НДС в рублях
	+ "СтавкаНДС"            // <ПеречислениеСсылка.СтавкиНДС>
	;
	Параметры.Вставить("Товары", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаТовары, СписокОбязательныхКолонок));
	Реквизиты = Параметры.Реквизиты[0];
	Параметры.Товары.Колонки.Добавить("СчетФактура", Документы.ТипВсеСсылки());
	Параметры.Товары.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	Параметры.Товары.ЗаполнитьЗначения(Реквизиты.Регистратор, "СчетФактура,Регистратор");
	// Для доп. расходов в регистре "НДСРаздельныйУчет" количество всегда равно нулю
	Параметры.Товары.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	Параметры.Товары.Колонки.Добавить("СтатьяЗатрат", Новый ОписаниеТипов("СправочникСсылка.СтатьиЗатрат"));
	Параметры.Товары.Колонки.Добавить("СпособСтроительства", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыСтроительства"));
	
КонецПроцедуры

#КонецОбласти

#Область КурсовыеРазницыНалоговыйАгент

Процедура СформироватьДвиженияКурсовыеРазницыНалоговыйАгент(ТаблицаРеквизитов, ТаблицаВзаиморасчетов, Движения, Отказ) Экспорт

	Реквизиты = ТаблицаРеквизитов[0];
	Если Не УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыКурсовыеРазницыНалоговыйАгент(ТаблицаРеквизитов, ТаблицаВзаиморасчетов);
	Реквизиты = Параметры.Реквизиты[0];

	РеестрСчетовФактур = ПодготовитьТаблицуКурсовыеРазницыНалоговыйАгент(Реквизиты, Параметры.ТаблицаВзаиморасчетов);
	Если РеестрСчетовФактур.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	СтруктураТаблицДляДвижений = ПодготовитьТаблицыПоВидамРегистровНДС(РеестрСчетовФактур, Реквизиты);

	ДанныеПроводок = ПодготовитьДанныеПроводокКурсовыеРазницыНалоговыйАгент(РеестрСчетовФактур);
	
	// Проводки
	УчетНДС.СформироватьПроводкиПоступлениеТоваровУслугНалоговыйАгентНерезидент(ДанныеПроводок, Движения, Отказ);
	
	ДанныеПроводокВключениеВСтоимость = ПодготовитьДанныеПроводокВключениеВСтоимостьКурсовыеРазницыНалоговыйАгент(
		СтруктураТаблицДляДвижений.НДСРаздельныйУчет);
	СформироватьПроводкиВключениеНДСВСтоимость(ДанныеПроводокВключениеВСтоимость, Движения, Отказ);	
	
	// НДС предъявленный
	УчетНДС.СформироватьДвиженияНДСПредъявленныйПоступлениеТоваровУслуг(РеестрСчетовФактур, Движения, Отказ);

	ДвиженияНДСПредъявленный = ПодготовитьДанныеДвиженийНДСПредъявленныйКурсовыеРазницыНалоговыйАгент(РеестрСчетовФактур);
	УчетНДС.СформироватьДвиженияНДСПредъявленный(ДвиженияНДСПредъявленный, Движения, Отказ, Истина);
	
	// НДС реализация 0%
	УчетНДС.СформироватьДвиженияНДСПредъявленныйРеализация0(СтруктураТаблицДляДвижений.НДСПредъявленныйРеализация0, Реквизиты, Движения, Отказ);

	// НДС раздельный учет
	СформироватьДвиженияНДСРаздельныйУчетПоступлениеЦенностей(СтруктураТаблицДляДвижений.НДСРаздельныйУчет, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьПараметрыКурсовыеРазницыНалоговыйАгент(ТаблицаРеквизиты, ТаблицаВзаиморасчетов)

	Параметры = Новый Структура;

	// Подготовка таблицы Реквизиты
	СписокОбязательныхКолонок = ""
	+ "Период,"                   // <Дата>
	+ "Регистратор,"              // <ДокументСсылка>
	+ "Организация";              // <СправочникСсылка.Организации>

	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));

	// Подготовка таблицы взаиморасчетов:
	СписокОбязательныхКолонок = ""

	+ "ВалютаВзаиморасчетов,"
	+ "ДокументРасчетов,"
	+ "Контрагент,"
	+ "ДоговорКонтрагента,"
	+ "СуммаБУ,"
	+ "СуммаВзаиморасчетов";


	Параметры.Вставить("ТаблицаВзаиморасчетов", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаВзаиморасчетов, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Функция ПодготовитьТаблицуКурсовыеРазницыНалоговыйАгент(Реквизиты, ТаблицаВзаиморасчетов)

	// Отбираем взаиморасчеты по договору с исполнением обязанностей налогового агента
	ТаблицаВзаиморасчетовНалоговыйАгент = УчетНДС.ПолучитьТаблицуВзаиморасчетовНалоговыйАгент(Реквизиты, ТаблицаВзаиморасчетов);

	Если ТаблицаВзаиморасчетовНалоговыйАгент.Количество() = 0 Тогда
		Возврат ТаблицаВзаиморасчетовНалоговыйАгент;
	КонецЕсли;

	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

	РеестрСчетовФактур = ПолучитьДанныеПоКорректируемымСчетамФактурамНалоговыйАгент(Реквизиты, ТаблицаВзаиморасчетовНалоговыйАгент);

	Если РеестрСчетовФактур.Количество() = 0 Тогда
		Возврат РеестрСчетовФактур;
	КонецЕсли;

	РеестрСчетовФактур.Колонки.Добавить("Период", 	  ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	РеестрСчетовФактур.Колонки.Добавить("Содержание", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	РеестрСчетовФактур.ЗаполнитьЗначения(Реквизиты.Период, "Период");

	// Вычисляем суммовую разницу
	Для каждого СтрокаВзаиморасчетов Из ТаблицаВзаиморасчетовНалоговыйАгент Цикл
		
		СчетФактура = СтрокаВзаиморасчетов.СчетФактура;
		МетаданныеСчетаФактуры = СчетФактура.Метаданные();
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("КурсВзаиморасчетов", МетаданныеСчетаФактуры)
			И ОбщегоНазначения.ЕстьРеквизитОбъекта("КратностьВзаиморасчетов", МетаданныеСчетаФактуры) Тогда
			
			КурсПоДокументу = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактура, "КурсВзаиморасчетов, КратностьВзаиморасчетов");
			СуммаБУПоКурсуПоступления = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаВзаиморасчетов.СуммаВзаиморасчетов,
				СтрокаВзаиморасчетов.ВалютаВзаиморасчетов,
				ВалютаРегламентированногоУчета,
				КурсПоДокументу.КурсВзаиморасчетов, 1,
				КурсПоДокументу.КратностьВзаиморасчетов, 1);
				
		Иначе
			КурсНаДатуПоступления = РаботаСКурсамиВалют.ПолучитьКурсВалюты(
				СтрокаВзаиморасчетов.ВалютаВзаиморасчетов, СтрокаВзаиморасчетов.СчетФактура.Дата);
			СуммаБУПоКурсуПоступления = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаВзаиморасчетов.СуммаВзаиморасчетов,
				СтрокаВзаиморасчетов.ВалютаВзаиморасчетов,
				ВалютаРегламентированногоУчета,
				КурсНаДатуПоступления.Курс, 1,
				КурсНаДатуПоступления.Кратность, 1);
		КонецЕсли;
		
		СтрокаВзаиморасчетов.СуммоваяРазница = СтрокаВзаиморасчетов.СуммаБУ - СуммаБУПоКурсуПоступления;

		НайденныеСтроки = РеестрСчетовФактур.НайтиСтроки(Новый Структура("СчетФактура", СтрокаВзаиморасчетов.СчетФактура));
		Для каждого СтрокаРеестра Из НайденныеСтроки Цикл
			СтрокаРеестра.ДоговорКонтрагента = СтрокаВзаиморасчетов.ДоговорКонтрагента;
			СтрокаРеестра.Содержание = "Корректировка НДС по договору " + СтрокаВзаиморасчетов.ДоговорКонтрагента;
		КонецЦикла;

	КонецЦикла;

	ТаблицаВзаиморасчетовНалоговыйАгент.Свернуть("СчетФактура", "СуммоваяРазница");

	РеестрСчетовФактур.Колонки.Добавить("СуммоваяРазница", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	РеестрСчетовФактур.Колонки.Добавить("СР_БезНДС", 	   ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	РеестрСчетовФактур.Колонки.Добавить("СР_НДС", 		   ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
		
	РеестрСчетовФактур.ЗагрузитьКолонку(РеестрСчетовФактур.ВыгрузитьКолонку("Базис_БезНДС"), "СуммоваяРазница");

	Распределение = Новый Структура("СуммоваяРазница", "СуммоваяРазница");
	Отбор         = Новый Структура("СчетФактура", "СчетФактура");

	РеестрСчетовФактур = УчетНДС.СформироватьКорректирующиеЗаписи(ТаблицаВзаиморасчетовНалоговыйАгент, РеестрСчетовФактур, Распределение, Отбор);

	РеестрСчетовФактур.Колонки.Добавить("СР_СуммаБезНДСУчитываетсяВCтоимости",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	РеестрСчетовФактур.Колонки.Добавить("СР_НДСУчитываетсяВCтоимости",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	РеестрСчетовФактур.Колонки.Добавить("СР_СуммаБезНДСДляОперацийПо0",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	РеестрСчетовФактур.Колонки.Добавить("СР_НДСДляОперацийПо0",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	РеестрСчетовФактур.Колонки.Добавить("СР_СуммаБезНДСРаспределяется",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	РеестрСчетовФактур.Колонки.Добавить("СР_НДСРаспределяется",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
		
	СтрокиКУдалению = Новый Массив();

	// Разделим суммовую разницу на сумму без НДС и сумму с НДС
	Для каждого СтрокаРеестра Из РеестрСчетовФактур Цикл

		Если СтрокаРеестра.СуммоваяРазница = 0 ИЛИ СтрокаРеестра.Базис_СНДС = 0 ИЛИ СтрокаРеестра.Базис_НДС = 0 Тогда
			СтрокиКУдалению.Добавить(СтрокаРеестра);
			Продолжить;
		КонецЕсли;

		СтрокаРеестра.СР_БезНДС = СтрокаРеестра.СуммоваяРазница;
		СтрокаРеестра.СР_НДС 	= УчетНДСКлиентСервер.РассчитатьСуммуНДС(СтрокаРеестра.СР_БезНДС, Ложь, УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаРеестра.СтавкаНДС));

		Если СтрокаРеестра.Базис_БезНДС <> 0 Тогда 
			СтрокаРеестра.СР_СуммаБезНДСУчитываетсяВCтоимости = СтрокаРеестра.СуммаБезНДСУчитываетсяВCтоимости * СтрокаРеестра.СР_БезНДС / СтрокаРеестра.Базис_БезНДС;
			СтрокаРеестра.СР_СуммаБезНДСДляОперацийПо0 = СтрокаРеестра.СуммаБезНДСДляОперацийПо0 * СтрокаРеестра.СР_БезНДС / СтрокаРеестра.Базис_БезНДС;
			СтрокаРеестра.СР_СуммаБезНДСРаспределяется = СтрокаРеестра.СуммаБезНДСРаспределяется * СтрокаРеестра.СР_БезНДС / СтрокаРеестра.Базис_БезНДС;
		КонецЕсли;
			
		Если СтрокаРеестра.Базис_НДС <> 0 Тогда
			СтрокаРеестра.СР_НДСУчитываетсяВCтоимости = СтрокаРеестра.НДСУчитываетсяВCтоимости * СтрокаРеестра.СР_НДС / СтрокаРеестра.Базис_НДС;
			СтрокаРеестра.СР_НДСДляОперацийПо0 = СтрокаРеестра.НДСДляОперацийПо0 * СтрокаРеестра.СР_НДС / СтрокаРеестра.Базис_НДС;
			СтрокаРеестра.СР_НДСРаспределяется = СтрокаРеестра.НДСРаспределяется * СтрокаРеестра.СР_НДС / СтрокаРеестра.Базис_НДС;
		КонецЕсли;
		
	КонецЦикла;

	Если СтрокиКУдалению.Количество() > 0 Тогда
	    Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			РеестрСчетовФактур.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЕсли;

	РеестрСчетовФактур.Колонки.СР_НДС.Имя 	 = "НДС";
	РеестрСчетовФактур.Колонки.СР_БезНДС.Имя = "СуммаБезНДС";

	РеестрСчетовФактур.Колонки.Добавить("ДатаСобытия", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	РеестрСчетовФактур.ЗаполнитьЗначения(Реквизиты.Период, "ДатаСобытия");
	
	Возврат РеестрСчетовФактур;

КонецФункции

Функция ПолучитьДанныеПоКорректируемымСчетамФактурамНалоговыйАгент(Реквизиты, ТаблицаВзаиморасчетовНалоговыйАгент)

	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("Организация", 		   Реквизиты.Организация);
	Запрос.УстановитьПараметр("Период",				   Реквизиты.Период);
	Запрос.УстановитьПараметр("КонецПериода",          Новый Граница(Реквизиты.Период, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Предположение0",        Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);
	Запрос.УстановитьПараметр("ТаблицаСуммовыхРазниц", ТаблицаВзаиморасчетовНалоговыйАгент);

	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаСуммовыхРазниц.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ СписокСчетовФактур
	|ИЗ
	|	&ТаблицаСуммовыхРазниц КАК ТаблицаСуммовыхРазниц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленный.Организация,
	|	НДСПредъявленный.Поставщик,
	|	НДСПредъявленный.СчетФактура КАК СчетФактура,
	|	НДСПредъявленный.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленный.СчетУчетаНДС КАК СчетУчетаНДС,
	|	СУММА(НДСПредъявленный.СуммаБезНДС) КАК СуммаБезНДСПриход,
	|	СУММА(НДСПредъявленный.НДС) КАК НДСПриход
	|ПОМЕСТИТЬ НДСПредъявленныйПриход
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|ГДЕ
	|	НДСПредъявленный.Период < &Период
	|	И НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И НДСПредъявленный.Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.НДСРаспределен)
	|	И НДСПредъявленный.Организация = &Организация
	|	И НДСПредъявленный.СчетФактура В
	|			(ВЫБРАТЬ
	|				СписокСчетовФактур.СчетФактура
	|			ИЗ
	|				СписокСчетовФактур)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленный.Организация,
	|	НДСПредъявленный.Поставщик,
	|	НДСПредъявленный.СчетФактура,
	|	НДСПредъявленный.ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС,
	|	НДСПредъявленный.СчетУчетаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленный.СчетФактура КАК СчетФактура,
	|	НДСПредъявленный.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленный.СчетУчетаНДС КАК СчетУчетаНДС,
	|	СУММА(НДСПредъявленный.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(НДСПредъявленный.НДС) КАК НДС
	|ПОМЕСТИТЬ НДСПредъявленныйНДСВключенВСтоимость
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|ГДЕ
	|	НДСПредъявленный.Период < &Период
	|	И НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И НДСПредъявленный.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.НДСВключенВСтоимость)
	|	И НДСПредъявленный.Организация = &Организация
	|	И НДСПредъявленный.СчетФактура В
	|			(ВЫБРАТЬ
	|				СписокСчетовФактур.СчетФактура
	|			ИЗ
	|				СписокСчетовФактур)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленный.СчетФактура,
	|	НДСПредъявленный.ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС,
	|	НДСПредъявленный.СчетУчетаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленный.СчетФактура КАК СчетФактура,
	|	НДСПредъявленный.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленный.СчетУчетаНДС КАК СчетУчетаНДС,
	|	СУММА(НДСПредъявленный.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(НДСПредъявленный.НДС) КАК НДС
	|ПОМЕСТИТЬ НДСПредъявленныйПредполагаетсяСтавка0
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|ГДЕ
	|	НДСПредъявленный.Период < &Период
	|	И НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И НДСПредъявленный.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредполагаетсяСтавка0)
	|	И НДСПредъявленный.Организация = &Организация
	|	И НДСПредъявленный.СчетФактура В
	|			(ВЫБРАТЬ
	|				СписокСчетовФактур.СчетФактура
	|			ИЗ
	|				СписокСчетовФактур)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленный.СчетФактура,
	|	НДСПредъявленный.ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС,
	|	НДСПредъявленный.СчетУчетаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленный.СчетФактура КАК СчетФактура,
	|	НДСПредъявленный.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленный.СчетУчетаНДС КАК СчетУчетаНДС,
	|	СУММА(НДСПредъявленный.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(НДСПредъявленный.НДС) КАК НДС
	|ПОМЕСТИТЬ НДСПредъявленныйНДСПодлежитРаспределению
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|ГДЕ
	|	НДСПредъявленный.Период < &Период
	|	И НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И НДСПредъявленный.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.НДСПодлежитРаспределению)
	|	И НДСПредъявленный.Организация = &Организация
	|	И НДСПредъявленный.СчетФактура В
	|			(ВЫБРАТЬ
	|				СписокСчетовФактур.СчетФактура
	|			ИЗ
	|				СписокСчетовФактур)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленный.СчетФактура,
	|	НДСПредъявленный.ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС,
	|	НДСПредъявленный.СчетУчетаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйПриход.Организация,
	|	НДСПредъявленныйПриход.Поставщик,
	|	НДСПредъявленныйПриход.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйПриход.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйПриход.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйПриход.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйПриход.СуммаБезНДСПриход,
	|	НДСПредъявленныйПриход.НДСПриход,
	|	ЕСТЬNULL(НДСПредъявленныйНДСВключенВСтоимость.СуммаБезНДС, 0) КАК СуммаБезНДСУчитываетсяВCтоимости,
	|	ЕСТЬNULL(НДСПредъявленныйНДСВключенВСтоимость.НДС, 0) КАК НДСУчитываетсяВCтоимости,
	|	ЕСТЬNULL(НДСПредъявленныйНДСПодлежитРаспределению.СуммаБезНДС, 0) КАК СуммаБезНДСРаспределяется,
	|	ЕСТЬNULL(НДСПредъявленныйНДСПодлежитРаспределению.НДС, 0) КАК НДСРаспределяется,
	|	ЕСТЬNULL(НДСПредъявленныйПредполагаетсяСтавка0.СуммаБезНДС, 0) КАК СуммаБезНДСДляОперацийПо0,
	|	ЕСТЬNULL(НДСПредъявленныйПредполагаетсяСтавка0.НДС, 0) КАК НДСДляОперацийПо0
	|ПОМЕСТИТЬ ВТНДСПредъявленныйОбороты
	|ИЗ
	|	НДСПредъявленныйПриход КАК НДСПредъявленныйПриход
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСПредъявленныйНДСВключенВСтоимость КАК НДСПредъявленныйНДСВключенВСтоимость
	|		ПО НДСПредъявленныйПриход.СчетФактура = НДСПредъявленныйНДСВключенВСтоимость.СчетФактура
	|			И НДСПредъявленныйПриход.ВидЦенности = НДСПредъявленныйНДСВключенВСтоимость.ВидЦенности
	|			И НДСПредъявленныйПриход.СтавкаНДС = НДСПредъявленныйНДСВключенВСтоимость.СтавкаНДС
	|			И НДСПредъявленныйПриход.СчетУчетаНДС = НДСПредъявленныйНДСВключенВСтоимость.СчетУчетаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСПредъявленныйПредполагаетсяСтавка0 КАК НДСПредъявленныйПредполагаетсяСтавка0
	|		ПО НДСПредъявленныйПриход.СчетФактура = НДСПредъявленныйПредполагаетсяСтавка0.СчетФактура
	|			И НДСПредъявленныйПриход.ВидЦенности = НДСПредъявленныйПредполагаетсяСтавка0.ВидЦенности
	|			И НДСПредъявленныйПриход.СтавкаНДС = НДСПредъявленныйПредполагаетсяСтавка0.СтавкаНДС
	|			И НДСПредъявленныйПриход.СчетУчетаНДС = НДСПредъявленныйПредполагаетсяСтавка0.СчетУчетаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСПредъявленныйНДСПодлежитРаспределению КАК НДСПредъявленныйНДСПодлежитРаспределению
	|		ПО НДСПредъявленныйПриход.СчетФактура = НДСПредъявленныйНДСПодлежитРаспределению.СчетФактура
	|			И НДСПредъявленныйПриход.ВидЦенности = НДСПредъявленныйНДСПодлежитРаспределению.ВидЦенности
	|			И НДСПредъявленныйПриход.СтавкаНДС = НДСПредъявленныйНДСПодлежитРаспределению.СтавкаНДС
	|			И НДСПредъявленныйПриход.СчетУчетаНДС = НДСПредъявленныйНДСПодлежитРаспределению.СчетУчетаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Обороты.СчетФактура,
	|	НДСПредъявленныйРеализация0Обороты.ВидЦенности,
	|	НДСПредъявленныйРеализация0Обороты.СтавкаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход,
	|	НДСПредъявленныйРеализация0Обороты.НДСПриход
	|ПОМЕСТИТЬ ВТНДСПредъявленныйРеализация0Обороты
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Обороты(
	|			,
	|			&КонецПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						СписокСчетовФактур.СчетФактура
	|					ИЗ
	|						СписокСчетовФактур)
	|				И Состояние = &Предположение0) КАК НДСПредъявленныйРеализация0Обороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСПредъявленныйРеализация0Обороты.СчетФактура,
	|	НДСПредъявленныйРеализация0Обороты.ВидЦенности,
	|	НДСПредъявленныйРеализация0Обороты.СтавкаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналитикаУчетаНДС.Организация,
	|	АналитикаУчетаНДС.СчетФактура,
	|	АналитикаУчетаНДС.ВидЦенности,
	|	АналитикаУчетаНДС.СчетУчетаНДС,
	|	АналитикаУчетаНДС.СтавкаНДС,
	|	АналитикаУчетаНДС.КлючАналитики КАК КлючАналитики
	|ПОМЕСТИТЬ ВТАналитикиУчетаНДС
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНДС КАК АналитикаУчетаНДС
	|ГДЕ
	|	АналитикаУчетаНДС.СчетФактура В
	|			(ВЫБРАТЬ
	|				СписокСчетовФактур.СчетФактура
	|			ИЗ
	|				СписокСчетовФактур)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТАналитикиУчетаНДС.СчетФактура,
	|	ВТАналитикиУчетаНДС.ВидЦенности,
	|	ВТАналитикиУчетаНДС.СтавкаНДС,
	|	ВТАналитикиУчетаНДС.СчетУчетаНДС,
	|	НДСРаздельныйУчетОстатки.СуммаБезНДСОстаток,
	|	НДСРаздельныйУчетОстатки.НДСОстаток,
	|	НДСРаздельныйУчетОстатки.СпособУчетаНДС
	|ПОМЕСТИТЬ ВТНДСРаздельныйУчетОстатки
	|ИЗ
	|	РегистрНакопления.НДСРаздельныйУчет.Остатки(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И АналитикаУчетаНДС В
	|					(ВЫБРАТЬ
	|						ВТАналитикиУчетаНДС.КлючАналитики
	|					ИЗ
	|						ВТАналитикиУчетаНДС)) КАК НДСРаздельныйУчетОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиУчетаНДС КАК ВТАналитикиУчетаНДС
	|		ПО НДСРаздельныйУчетОстатки.АналитикаУчетаНДС = ВТАналитикиУчетаНДС.КлючАналитики
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВТАналитикиУчетаНДС.СчетФактура,
	|	ВТАналитикиУчетаНДС.ВидЦенности,
	|	ВТАналитикиУчетаНДС.СтавкаНДС,
	|	ВТАналитикиУчетаНДС.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйОбороты.Организация,
	|	НДСПредъявленныйОбороты.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйОбороты.ВидЦенности,
	|	НДСПредъявленныйОбороты.СтавкаНДС,
	|	НДСПредъявленныйОбороты.СчетУчетаНДС,
	|	НДСПредъявленныйОбороты.Поставщик,
	|	ЕСТЬNULL(НДСПредъявленныйОбороты.СуммаБезНДСПриход, 0) + ЕСТЬNULL(НДСПредъявленныйОбороты.НДСПриход, 0) КАК Базис_СНДС,
	|	ЕСТЬNULL(НДСПредъявленныйОбороты.СуммаБезНДСПриход, 0) КАК Базис_БезНДС,
	|	ЕСТЬNULL(НДСПредъявленныйОбороты.НДСПриход, 0) КАК Базис_НДС,
	|	ЕСТЬNULL(НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход, 0) КАК Реализация0_БезНДС,
	|	ЕСТЬNULL(НДСПредъявленныйРеализация0Обороты.НДСПриход, 0) КАК Реализация0_НДС,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Обороты.СчетФактура ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход = 0
	|						И НДСПредъявленныйРеализация0Обороты.НДСПриход = 0
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК ЕстьРеализация0,
	|	0 КАК РаздельныйУчет_БезНДС,
	|	0 КАК РаздельныйУчет_НДС,
	|	0 КАК ЕстьОстаткиПоРаздельномуУчету,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК ДоговорКонтрагента,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.ПринимаетсяКВычету) КАК СпособУчетаНДС,
	|	НДСПредъявленныйОбороты.СуммаБезНДСУчитываетсяВCтоимости,
	|	НДСПредъявленныйОбороты.НДСУчитываетсяВCтоимости,
	|	НДСПредъявленныйОбороты.СуммаБезНДСРаспределяется,
	|	НДСПредъявленныйОбороты.НДСРаспределяется,
	|	НДСПредъявленныйОбороты.СуммаБезНДСДляОперацийПо0,
	|	НДСПредъявленныйОбороты.НДСДляОперацийПо0
	|ПОМЕСТИТЬ ВТДанныеПоКорректируемымСчетамФактурам
	|ИЗ
	|	ВТНДСПредъявленныйОбороты КАК НДСПредъявленныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСПредъявленныйРеализация0Обороты КАК НДСПредъявленныйРеализация0Обороты
	|		ПО НДСПредъявленныйОбороты.СчетФактура = НДСПредъявленныйРеализация0Обороты.СчетФактура
	|			И НДСПредъявленныйОбороты.ВидЦенности = НДСПредъявленныйРеализация0Обороты.ВидЦенности
	|			И НДСПредъявленныйОбороты.СтавкаНДС = НДСПредъявленныйРеализация0Обороты.СтавкаНДС
	|			И НДСПредъявленныйОбороты.СчетУчетаНДС = НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСПредъявленныйОбороты.Организация,
	|	НДСПредъявленныйОбороты.СчетФактура,
	|	НДСПредъявленныйОбороты.ВидЦенности,
	|	НДСПредъявленныйОбороты.СтавкаНДС,
	|	НДСПредъявленныйОбороты.СчетУчетаНДС,
	|	НДСПредъявленныйОбороты.Поставщик,
	|	ЕСТЬNULL(НДСПредъявленныйОбороты.СуммаБезНДСПриход, 0) + ЕСТЬNULL(НДСПредъявленныйОбороты.НДСПриход, 0),
	|	ЕСТЬNULL(НДСПредъявленныйОбороты.СуммаБезНДСПриход, 0),
	|	ЕСТЬNULL(НДСПредъявленныйОбороты.НДСПриход, 0),
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(НДСРаздельныйУчетОстатки.СуммаБезНДСОстаток, 0),
	|	ЕСТЬNULL(НДСРаздельныйУчетОстатки.НДСОстаток, 0),
	|	ВЫБОР
	|		КОГДА НДСРаздельныйУчетОстатки.СчетФактура ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НДСРаздельныйУчетОстатки.СуммаБезНДСОстаток = 0
	|						И НДСРаздельныйУчетОстатки.НДСОстаток = 0
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.ПринимаетсяКВычету),
	|	НДСПредъявленныйОбороты.СуммаБезНДСУчитываетсяВCтоимости,
	|	НДСПредъявленныйОбороты.НДСУчитываетсяВCтоимости,
	|	НДСПредъявленныйОбороты.СуммаБезНДСРаспределяется,
	|	НДСПредъявленныйОбороты.НДСРаспределяется,
	|	НДСПредъявленныйОбороты.СуммаБезНДСДляОперацийПо0,
	|	НДСПредъявленныйОбороты.НДСДляОперацийПо0
	|ИЗ
	|	ВТНДСПредъявленныйОбороты КАК НДСПредъявленныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСРаздельныйУчетОстатки КАК НДСРаздельныйУчетОстатки
	|		ПО НДСПредъявленныйОбороты.СчетФактура = НДСРаздельныйУчетОстатки.СчетФактура
	|			И НДСПредъявленныйОбороты.ВидЦенности = НДСРаздельныйУчетОстатки.ВидЦенности
	|			И НДСПредъявленныйОбороты.СтавкаНДС = НДСРаздельныйУчетОстатки.СтавкаНДС
	|			И НДСПредъявленныйОбороты.СчетУчетаНДС = НДСРаздельныйУчетОстатки.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанныеПоКорректируемымСчетамФактурам.Организация,
	|	ВТДанныеПоКорректируемымСчетамФактурам.СчетФактура,
	|	ВТДанныеПоКорректируемымСчетамФактурам.ВидЦенности,
	|	ВТДанныеПоКорректируемымСчетамФактурам.СтавкаНДС,
	|	ВТДанныеПоКорректируемымСчетамФактурам.СчетУчетаНДС,
	|	ВТДанныеПоКорректируемымСчетамФактурам.Поставщик,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.Базис_СНДС) КАК Базис_СНДС,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.Базис_БезНДС) КАК Базис_БезНДС,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.Базис_НДС) КАК Базис_НДС,
	|	СУММА(ВТДанныеПоКорректируемымСчетамФактурам.Реализация0_БезНДС) КАК Реализация0_БезНДС,
	|	СУММА(ВТДанныеПоКорректируемымСчетамФактурам.Реализация0_НДС) КАК Реализация0_НДС,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.ЕстьРеализация0) КАК ЕстьРеализация0,
	|	СУММА(ВТДанныеПоКорректируемымСчетамФактурам.РаздельныйУчет_БезНДС) КАК РаздельныйУчет_БезНДС,
	|	СУММА(ВТДанныеПоКорректируемымСчетамФактурам.РаздельныйУчет_НДС) КАК РаздельныйУчет_НДС,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.ЕстьОстаткиПоРаздельномуУчету) КАК ЕстьОстаткиПоРаздельномуУчету,
	|	ВТДанныеПоКорректируемымСчетамФактурам.ДоговорКонтрагента,
	|	ВТДанныеПоКорректируемымСчетамФактурам.СпособУчетаНДС,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.СуммаБезНДСУчитываетсяВCтоимости) КАК СуммаБезНДСУчитываетсяВCтоимости,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.НДСУчитываетсяВCтоимости) КАК НДСУчитываетсяВCтоимости,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.СуммаБезНДСРаспределяется) КАК СуммаБезНДСРаспределяется,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.НДСРаспределяется) КАК НДСРаспределяется,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.СуммаБезНДСДляОперацийПо0) КАК СуммаБезНДСДляОперацийПо0,
	|	МАКСИМУМ(ВТДанныеПоКорректируемымСчетамФактурам.НДСДляОперацийПо0) КАК НДСДляОперацийПо0
	|ИЗ
	|	ВТДанныеПоКорректируемымСчетамФактурам КАК ВТДанныеПоКорректируемымСчетамФактурам
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеПоКорректируемымСчетамФактурам.Организация,
	|	ВТДанныеПоКорректируемымСчетамФактурам.СчетФактура,
	|	ВТДанныеПоКорректируемымСчетамФактурам.ВидЦенности,
	|	ВТДанныеПоКорректируемымСчетамФактурам.СтавкаНДС,
	|	ВТДанныеПоКорректируемымСчетамФактурам.СчетУчетаНДС,
	|	ВТДанныеПоКорректируемымСчетамФактурам.ДоговорКонтрагента,
	|	ВТДанныеПоКорректируемымСчетамФактурам.СпособУчетаНДС,
	|	ВТДанныеПоКорректируемымСчетамФактурам.Поставщик";

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ПодготовитьТаблицыПоВидамРегистровНДС(РеестрСчетовФактур, Реквизиты)

	Запрос = Новый Запрос;

	СостоянияПредъявленный = Новый СписокЗначений;
	СостоянияПредъявленный.Добавить(Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0);
	СостоянияПредъявленный.Добавить(Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0);

	Запрос.УстановитьПараметр("РеестрСчетовФактур", 	РеестрСчетовФактур);
	Запрос.УстановитьПараметр("Организация",        	Реквизиты.Организация);
	Запрос.УстановитьПараметр("СписокСчетовФактур",		ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(РеестрСчетовФактур.ВыгрузитьКолонку("СчетФактура"), Истина));
	Запрос.УстановитьПараметр("СписокВидыЦенностей",	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(РеестрСчетовФактур.ВыгрузитьКолонку("ВидЦенности"), Истина));
	Запрос.УстановитьПараметр("СписокСтавкиНДС",		ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(РеестрСчетовФактур.ВыгрузитьКолонку("СтавкаНДС"), Истина));
	Запрос.УстановитьПараметр("СписокСчетаУчетаНДС",	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(РеестрСчетовФактур.ВыгрузитьКолонку("СчетУчетаНДС"), Истина));
	Запрос.УстановитьПараметр("СостоянияПредъявленный", СостоянияПредъявленный);
	Запрос.УстановитьПараметр("СостоянияОжидание", 		Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);
	Запрос.УстановитьПараметр("НачалоПериода",      	НачалоКвартала(Реквизиты.Период));
	Запрос.УстановитьПараметр("КонецПериода",       	Новый Граница(Реквизиты.Период, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецПериодаДата",   	Реквизиты.Период);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеестрСчетовФактур.СчетФактура КАК СчетФактура,
	|	РеестрСчетовФактур.СчетУчетаНДС КАК СчетУчетаНДС,
	|	РеестрСчетовФактур.СтавкаНДС КАК СтавкаНДС,
	|	РеестрСчетовФактур.СуммоваяРазница КАК СуммоваяРазница,
	|	РеестрСчетовФактур.ВидЦенности КАК ВидЦенности,
	|	РеестрСчетовФактур.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РеестрСчетовФактур.Базис_БезНДС КАК БазисБезНДС,
	|	РеестрСчетовФактур.Базис_СНДС КАК БазисСНДС,
	|	РеестрСчетовФактур.Базис_НДС КАК БазисНДС,
	|	РеестрСчетовФактур.ЕстьРеализация0 КАК ЕстьРеализация0,
	|	РеестрСчетовФактур.ЕстьОстаткиПоРаздельномуУчету КАК ЕстьОстаткиПоРаздельномуУчету,
	|	РеестрСчетовФактур.Реализация0_БезНДС КАК Реализация0_БезНДС,
	|	РеестрСчетовФактур.Реализация0_НДС КАК Реализация0_НДС,
	|	РеестрСчетовФактур.РаздельныйУчет_БезНДС КАК РаздельныйУчет_БезНДС,
	|	РеестрСчетовФактур.РаздельныйУчет_НДС КАК РаздельныйУчет_НДС,
	|	РеестрСчетовФактур.СуммаБезНДС КАК СуммаБезНДС,
	|	РеестрСчетовФактур.НДС КАК НДС
	|ПОМЕСТИТЬ ВТРеестрСчетовФактур
	|ИЗ
	|	&РеестрСчетовФактур КАК РеестрСчетовФактур
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТРеестрСчетовФактур.Реализация0_БезНДС * ВТРеестрСчетовФактур.СуммаБезНДС / ВТРеестрСчетовФактур.БазисБезНДС) КАК СуммаБезНДС,
	|	СУММА(ВТРеестрСчетовФактур.Реализация0_НДС * ВТРеестрСчетовФактур.НДС / ВТРеестрСчетовФактур.БазисНДС) КАК НДС,
	|	ВТРеестрСчетовФактур.СчетФактура КАК СчетФактура,
	|	ВТРеестрСчетовФактур.ВидЦенности КАК ВидЦенности,
	|	ВТРеестрСчетовФактур.СтавкаНДС КАК СтавкаНДС,
	|	ВТРеестрСчетовФактур.СчетУчетаНДС КАК СчетУчетаНДС
	|ИЗ
	|	ВТРеестрСчетовФактур КАК ВТРеестрСчетовФактур
	|ГДЕ
	|	ВТРеестрСчетовФактур.ЕстьРеализация0 = 1
	|	И ВТРеестрСчетовФактур.Реализация0_БезНДС * ВТРеестрСчетовФактур.СуммаБезНДС / ВТРеестрСчетовФактур.БазисБезНДС <> 0
	|	И ВТРеестрСчетовФактур.Реализация0_НДС * ВТРеестрСчетовФактур.НДС / ВТРеестрСчетовФактур.БазисНДС <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРеестрСчетовФактур.СчетФактура,
	|	ВТРеестрСчетовФактур.ВидЦенности,
	|	ВТРеестрСчетовФактур.СтавкаНДС,
	|	ВТРеестрСчетовФактур.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТРеестрСчетовФактур.РаздельныйУчет_БезНДС * ВТРеестрСчетовФактур.СуммаБезНДС / ВТРеестрСчетовФактур.БазисБезНДС) КАК СуммаБезНДС,
	|	СУММА(ВТРеестрСчетовФактур.РаздельныйУчет_НДС * ВТРеестрСчетовФактур.НДС / ВТРеестрСчетовФактур.БазисНДС) КАК НДС,
	|	ВТРеестрСчетовФактур.СчетФактура КАК СчетФактура,
	|	ВТРеестрСчетовФактур.ВидЦенности КАК ВидЦенности,
	|	ВТРеестрСчетовФактур.СтавкаНДС КАК СтавкаНДС,
	|	ВТРеестрСчетовФактур.СчетУчетаНДС КАК СчетУчетаНДС
	|ИЗ
	|	ВТРеестрСчетовФактур КАК ВТРеестрСчетовФактур
	|ГДЕ
	|	ВТРеестрСчетовФактур.ЕстьОстаткиПоРаздельномуУчету = 1
	|	И ВТРеестрСчетовФактур.РаздельныйУчет_БезНДС * ВТРеестрСчетовФактур.СуммаБезНДС / ВТРеестрСчетовФактур.БазисБезНДС <> 0
	|	И ВТРеестрСчетовФактур.РаздельныйУчет_НДС * ВТРеестрСчетовФактур.НДС / ВТРеестрСчетовФактур.БазисНДС <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРеестрСчетовФактур.СчетФактура,
	|	ВТРеестрСчетовФактур.ВидЦенности,
	|	ВТРеестрСчетовФактур.СтавкаНДС,
	|	ВТРеестрСчетовФактур.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КонецПериодаДата КАК Период,
	|	&КонецПериодаДата КАК ДатаСобытия,
	|	НДСПредъявленныйРеализация0Обороты.Организация КАК Организация,
	|	НДСПредъявленныйРеализация0Обороты.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Обороты.Состояние КАК Состояние,
	|	НДСПредъявленныйРеализация0Обороты.ДокументОтгрузки КАК ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Обороты.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйРеализация0Обороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние В (&СостоянияПредъявленный)
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход)
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние = &СостоянияОжидание
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход - НДСПредъявленныйРеализация0Обороты.СуммаБезНДСРасход)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние В (&СостоянияПредъявленный)
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.НДСПриход)
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние = &СостоянияОжидание
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.НДСПриход - НДСПредъявленныйРеализация0Обороты.НДСРасход)
	|	КОНЕЦ КАК НДС,
	|	НДСПредъявленныйРеализация0Обороты.Поставщик КАК Поставщик,
	|	НДСПредъявленныйРеализация0Обороты.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Обороты(
	|			,
	|			&КонецПериода,
	|			,
	|			Организация = &Организация
	|				И СчетФактура В (&СписокСчетовФактур)
	|				И СтавкаНДС В (&СписокСтавкиНДС)
	|				И СчетУчетаНДС В (&СписокСчетаУчетаНДС)) КАК НДСПредъявленныйРеализация0Обороты
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленныйРеализация0Обороты.Организация,
	|	НДСПредъявленныйРеализация0Обороты.СчетФактура,
	|	НДСПредъявленныйРеализация0Обороты.Состояние,
	|	НДСПредъявленныйРеализация0Обороты.ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Обороты.ВидЦенности,
	|	НДСПредъявленныйРеализация0Обороты.СтавкаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Обороты.Поставщик,
	|	НДСПредъявленныйРеализация0Обороты.ИсправленныйСчетФактура
	|
	|ИМЕЮЩИЕ
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние В (&СостоянияПредъявленный)
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход)
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние = &СостоянияОжидание
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход - НДСПредъявленныйРеализация0Обороты.СуммаБезНДСРасход)
	|	КОНЕЦ <> 0 И
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние В (&СостоянияПредъявленный)
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.НДСПриход)
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние = &СостоянияОжидание
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.НДСПриход - НДСПредъявленныйРеализация0Обороты.НДСРасход)
	|	КОНЕЦ <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналитикаУчетаНДС.СчетФактура КАК СчетФактура,
	|	АналитикаУчетаНДС.ВидЦенности КАК ВидЦенности,
	|	АналитикаУчетаНДС.СчетУчетаНДС КАК СчетУчетаНДС,
	|	АналитикаУчетаНДС.СтавкаНДС КАК СтавкаНДС,
	|	АналитикаУчетаНДС.КлючАналитики КАК КлючАналитики
	|ПОМЕСТИТЬ ВТАналитикиУчетаНДС
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНДС КАК АналитикаУчетаНДС
	|ГДЕ
	|	АналитикаУчетаНДС.Организация = &Организация
	|	И АналитикаУчетаНДС.СчетФактура В(&СписокСчетовФактур)
	|	И АналитикаУчетаНДС.СтавкаНДС В(&СписокСтавкиНДС)
	|	И АналитикаУчетаНДС.СчетУчетаНДС В(&СписокСчетаУчетаНДС)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТАналитикиУчетаНДС.СчетФактура КАК СчетФактура,
	|	ВТАналитикиУчетаНДС.ВидЦенности КАК ВидЦенности,
	|	ВТАналитикиУчетаНДС.СтавкаНДС КАК СтавкаНДС,
	|	ВТАналитикиУчетаНДС.СчетУчетаНДС КАК СчетУчетаНДС,
	|	&КонецПериодаДата КАК Период,
	|	&КонецПериодаДата КАК ДатаСобытия,
	|	НДСРаздельныйУчетОстатки.Организация КАК Организация,
	|	НДСРаздельныйУчетОстатки.АналитикаУчетаЗатрат КАК АналитикаУчетаЗатрат,
	|	НДСРаздельныйУчетОстатки.АналитикаУчетаНДС КАК АналитикаУчетаНДС,
	|	НДСРаздельныйУчетОстатки.Партия КАК Партия,
	|	НДСРаздельныйУчетОстатки.СпособУчетаНДС КАК СпособУчетаНДС,
	|	НДСРаздельныйУчетОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСРаздельныйУчетОстатки.НДСОстаток КАК НДС,
	|	ВТАналитикаУчетаЗатрат.СчетЗатрат КАК СчетЗатрат,
	|	ВТАналитикаУчетаЗатрат.Подразделение КАК Подразделение,
	|	ВТАналитикаУчетаЗатрат.Субконто1 КАК Субконто1,
	|	ВТАналитикаУчетаЗатрат.Субконто2 КАК Субконто2,
	|	ВТАналитикаУчетаЗатрат.Субконто3 КАК Субконто3
	|ИЗ
	|	РегистрНакопления.НДСРаздельныйУчет.Остатки(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И АналитикаУчетаНДС В
	|					(ВЫБРАТЬ
	|						ВТАналитикиУчетаНДС.КлючАналитики
	|					ИЗ
	|						ВТАналитикиУчетаНДС КАК ВТАналитикиУчетаНДС)) КАК НДСРаздельныйУчетОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиУчетаНДС КАК ВТАналитикиУчетаНДС
	|		ПО НДСРаздельныйУчетОстатки.АналитикаУчетаНДС = ВТАналитикиУчетаНДС.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаЗатрат КАК ВТАналитикаУчетаЗатрат
	|		ПО НДСРаздельныйУчетОстатки.АналитикаУчетаЗатрат = ВТАналитикаУчетаЗатрат.КлючАналитики
	|ГДЕ
	|	НЕ(ЕСТЬNULL(НДСРаздельныйУчетОстатки.СуммаБезНДСОстаток, 0) = 0
	|				И ЕСТЬNULL(НДСРаздельныйУчетОстатки.НДСОстаток, 0) = 0)";

	Результат = Запрос.ВыполнитьПакет();

	СтруктураТаблиц = Новый Структура;

	СтруктураТаблиц.Вставить("Реализация0",         		Результат[1].Выгрузить());
	СтруктураТаблиц.Вставить("РаздельныйУчет",         		Результат[2].Выгрузить());
	СтруктураТаблиц.Вставить("НДСПредъявленныйРеализация0", Результат[3].Выгрузить());
	СтруктураТаблиц.Вставить("НДСРаздельныйУчет",			Результат[5].Выгрузить());

	СтруктураСкорректированныхТаблиц = Новый Структура();

	СтруктураСкорректированныхТаблиц.Вставить("НДСПредъявленныйРеализация0",
		УчетНДСПереопределяемый.РаспределитьПоБазису(СтруктураТаблиц.Реализация0, СтруктураТаблиц.НДСПредъявленныйРеализация0));
	СтруктураСкорректированныхТаблиц.Вставить("НДСРаздельныйУчет",
		УчетНДСПереопределяемый.РаспределитьПоБазису(СтруктураТаблиц.РаздельныйУчет, СтруктураТаблиц.НДСРаздельныйУчет));

	Возврат СтруктураСкорректированныхТаблиц;

КонецФункции

Функция ПодготовитьДанныеПроводокКурсовыеРазницыНалоговыйАгент(РеестрСчетовФактур)
	
	ДанныеПроводок = РеестрСчетовФактур.СкопироватьКолонки(
		"Период,Организация,Содержание,СчетУчетаНДС,СчетФактура,Поставщик,ДоговорКонтрагента,НДС");
	ДанныеПроводок.Колонки.Добавить("СпособУчетаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));	
		
	Для Каждого СтрокаТаблицы Из РеестрСчетовФактур Цикл
		
		НДС = СтрокаТаблицы.НДС - СтрокаТаблицы.СР_НДСУчитываетсяВCтоимости 
			- СтрокаТаблицы.СР_НДСДляОперацийПо0 - СтрокаТаблицы.СР_НДСРаспределяется;
		Если НДС <> 0 Тогда
			НоваяСтрока = ДанныеПроводок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НДС = НДС;
			НоваяСтрока.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету;
		КонецЕсли;
		
		НДС = СтрокаТаблицы.СР_НДСУчитываетсяВCтоимости;
		Если НДС <> 0 Тогда
			НоваяСтрока = ДанныеПроводок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НДС = НДС;
			НоваяСтрока.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости;
		КонецЕсли;
		
		НДС = СтрокаТаблицы.СР_НДСДляОперацийПо0;
		Если НДС <> 0 Тогда
			НоваяСтрока = ДанныеПроводок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НДС = НДС;
			НоваяСтрока.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0;
		КонецЕсли;
		
		НДС = СтрокаТаблицы.СР_НДСРаспределяется;
		Если НДС <> 0 Тогда
			НоваяСтрока = ДанныеПроводок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НДС = НДС;
			НоваяСтрока.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеПроводок;
	
КонецФункции

Функция ПодготовитьДанныеДвиженийНДСПредъявленныйКурсовыеРазницыНалоговыйАгент(РеестрСчетовФактур)
	
	ДанныеДвижений = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленный");
	
	Для Каждого СтрокаТаблицы Из РеестрСчетовФактур Цикл
		
		СуммаБезНДС = СтрокаТаблицы.СР_СуммаБезНДСУчитываетсяВCтоимости;
		НДС = СтрокаТаблицы.СР_НДСУчитываетсяВCтоимости;
		Если СуммаБезНДС <> 0 ИЛИ НДС <> 0 Тогда
			НоваяСтрока = ДанныеДвижений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
			НоваяСтрока.НДС = НДС;
			НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.НДСВключенВСтоимость;
		КонецЕсли;
		
		СуммаБезНДС = СтрокаТаблицы.СР_СуммаБезНДСДляОперацийПо0;
		НДС = СтрокаТаблицы.СР_НДСДляОперацийПо0;
		Если СуммаБезНДС <> 0 ИЛИ НДС <> 0 Тогда
			НоваяСтрока = ДанныеДвижений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
			НоваяСтрока.НДС = НДС;
			НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0;
		КонецЕсли;
		
		СуммаБезНДС = СтрокаТаблицы.СР_СуммаБезНДСРаспределяется;
		НДС = СтрокаТаблицы.СР_НДСРаспределяется;
		Если СуммаБезНДС <> 0 ИЛИ НДС <> 0 Тогда
			НоваяСтрока = ДанныеДвижений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
			НоваяСтрока.НДС = НДС;
			НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.НДСПодлежитРаспределению;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеДвижений;
	
КонецФункции

Функция ПодготовитьДанныеПроводокВключениеВСтоимостьКурсовыеРазницыНалоговыйАгент(ДвиженияНДСРаздельныйУчет)
	
	ДвиженияНДСРаздельныйУчет.Индексы.Добавить("СпособУчетаНДС");
	ДанныеПроводок = ДвиженияНДСРаздельныйУчет.Скопировать(
		Новый Структура("СпособУчетаНДС", Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости));
	ДанныеПроводок.Колонки.СпособУчетаНДС.Имя = "НовыйСпособУчетаНДС";
	
	ДанныеПроводок.Колонки.Добавить("СпособУчетаНДСИзменился", Новый ОписаниеТипов("Булево"));
	ДанныеПроводок.Колонки.Добавить("ЕстьНДСПредъявленный", Новый ОписаниеТипов("Булево"));
	ДанныеПроводок.Колонки.Добавить("Поставщик", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ДанныеПроводок.Колонки.Добавить("Покупатель", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ДанныеПроводок.Колонки.Добавить("ДоговорКонтрагента", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	
	ДанныеПроводок.ЗаполнитьЗначения(Истина, "СпособУчетаНДСИзменился,ЕстьНДСПредъявленный");
	
	ОпределитьПоставщиковПоСписаннымПартиямНДС(ДанныеПроводок);
		
	ДанныеПроводок.Свернуть("
		|Организация,Период,
		|СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,
		|СчетУчетаНДС,СчетФактура,Поставщик,НовыйСпособУчетаНДС",
		"НДС");
		
	ДанныеПроводок.Колонки.Добавить("Содержание",
		Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)));
	ДанныеПроводок.ЗаполнитьЗначения(НСтр("ru = 'НДС включен в стоимость ценностей'"), "Содержание");
	
	Возврат ДанныеПроводок;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПоступлениеЦенностей

Процедура СформироватьДвиженияПоступлениеЦенностей(ДанныеДвижений, Реквизиты, Движения, Отказ)
	
	ДанныеДвижений.Индексы.Добавить("СпособУчетаНДСИзменился,НовыйСпособУчетаНДС,ЕстьНДСПредъявленный");
	
	// Включим НДС в стоимость ценностей
	ТаблицаВключениеВСтоимость = ДанныеДвижений.Скопировать(
		Новый Структура("СпособУчетаНДСИзменился,НовыйСпособУчетаНДС,ЕстьНДСПредъявленный",
		Истина, Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости, Истина));
		
	ЕстьВключениеВСтоимость = ТаблицаВключениеВСтоимость.Количество() >	0;
	
	Если ЕстьВключениеВСтоимость Тогда
		Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуОС") 
		 ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуНМА")
		 ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.МодернизацияОС") Тогда
			СформироватьПроводкиВключениеНДСВСтоимостьОСиНМА(ТаблицаВключениеВСтоимость, Реквизиты, Движения, Отказ, Ложь);
		Иначе
			ДанныеПроводок = ПодготовитьДанныеПроводокПоступлениеЦенностей(ТаблицаВключениеВСтоимость, Реквизиты);
			СформироватьПроводкиВключениеНДСВСтоимость(ДанныеПроводок, Движения, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// Включим НДС в состав прочих затрат
	ТаблицаВключениеВЗатраты = ДанныеДвижений.Скопировать(
		Новый Структура("СпособУчетаНДСИзменился,НовыйСпособУчетаНДС,ЕстьНДСПредъявленный",
		Истина, Перечисления.СпособыУчетаНДС.ВосстановленУчитываетсяВCтоимости, Истина));
		
	ЕстьВключениеВЗатраты = ТаблицаВключениеВЗатраты.Количество() > 0;
	
	Если ЕстьВключениеВЗатраты Тогда
		ДанныеПроводок = ПодготовитьДанныеПроводокПоступлениеЦенностей(ТаблицаВключениеВЗатраты, Реквизиты);
		СформироватьПроводкиВключениеНДСВРасходы(ДанныеПроводок, Движения, Отказ);
	КонецЕсли;

	// Спишем НДС из регистра НДС предъявленный
	Если ТипЗнч(Реквизиты.Регистратор) <> Тип("ДокументСсылка.ПоступлениеВЛизинг") Тогда
		
		ТаблицаРасходНДСПредъявленный = ДанныеДвижений.Скопировать(
		Новый Структура("СпособУчетаНДСИзменился,ЕстьНДСПредъявленный", Истина, Истина));
		
		ЕстьРасходНДСПредъявленный = ТаблицаРасходНДСПредъявленный.Количество() > 0;
		
		Если ЕстьРасходНДСПредъявленный Тогда
			ДанныеДвиженийНДСПредъявленный = ПодготовитьДанныеДвиженийНДСПредъявленныйПоступлениеЦенностей(
			ТаблицаРасходНДСПредъявленный);
			УчетНДС.СформироватьДвиженияНДСПредъявленный(ДанныеДвиженийНДСПредъявленный, Движения, Отказ, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	// Отнесем НДС к операциям по 0%
	
	ТаблицаПриходНДСПредъявленныйРеализация0 = ДанныеДвижений.Скопировать(
		Новый Структура("НовыйСпособУчетаНДС,ЕстьНДСПредъявленный", Перечисления.СпособыУчетаНДС.ДляОперацийПо0, Истина));
	ТаблицаПриходНДСПредъявленныйРеализация0_2 = ДанныеДвижений.Скопировать(
		Новый Структура("НовыйСпособУчетаНДС,ЕстьНДСПредъявленный", Перечисления.СпособыУчетаНДС.ВосстановленДляОперацийПо0, Истина));	
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(
		ТаблицаПриходНДСПредъявленныйРеализация0_2, ТаблицаПриходНДСПредъявленныйРеализация0);
	
	ЕстьРеализация0 = ТаблицаПриходНДСПредъявленныйРеализация0.Количество() > 0;
	
	Если ЕстьРеализация0 Тогда
		ДанныеДвиженийНДСПредъявленныйРеализация0 = ПодготовитьДанныеДвиженийНДСПредъявленныйРеализация0ПоступлениеЦенностей(
			ТаблицаПриходНДСПредъявленныйРеализация0, Реквизиты);
		СформироватьДвиженияНДСПредъявленныйРеализация0ПоступлениеЦенностей(ДанныеДвиженийНДСПредъявленныйРеализация0, Движения, Отказ);
	КонецЕсли;	
		
	// Сформируем движения по НДСРаздельныйУчет
	ДанныеДвиженийНДСРаздельныйУчет = ПодготовитьДанныеДвиженийНДСРаздельныйУчетПоступлениеЦенностей(ДанныеДвижений, Реквизиты);
	СформироватьДвиженияНДСРаздельныйУчетПоступлениеЦенностей(ДанныеДвиженийНДСРаздельныйУчет, Движения, Отказ);
	
КонецПроцедуры

Функция ПодготовитьДанныеПроводокПоступлениеЦенностей(ДанныеПроводок, Реквизиты)
	
	ДанныеПроводок.Свернуть("
		|Организация,Период,
		|СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,
		|СчетУчетаНДС,СчетФактура,Поставщик,НовыйСпособУчетаНДС",
		"НДС");
		
	ДанныеПроводок.Колонки.Добавить("Содержание",
		Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)));
	ДанныеПроводок.ЗаполнитьЗначения(НСтр("ru = 'НДС включен в стоимость ценностей'"), "Содержание");

	ЗаменитьАналитикуСписанияНДСПоАренднымОбязательствам(ДанныеПроводок, Реквизиты);
	
	Возврат ДанныеПроводок;
	
КонецФункции

Функция ПодготовитьДанныеДвиженийНДСПредъявленныйПоступлениеЦенностей(ДанныеДвижений)
	
	ДанныеДвиженийНДСПредъявленный = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленный");
	
	// В случае если по документу есть расхождения, то фиксируем в регистре документально подтвержденные суммы.
	ЕстьРасхождения = ДанныеДвижений.Колонки.Найти("СуммаПодтвержденнаяРуб") <> Неопределено
		И ДанныеДвижений.Колонки.Найти("СуммаНДСПодтвержденнаяРуб") <> Неопределено;
	Для Каждого СтрокаДвижений Из ДанныеДвижений Цикл
		
		Если СтрокаДвижений.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету
			ИЛИ СтрокаДвижений.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Списывается Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ДанныеДвиженийНДСПредъявленный.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДвижений);
		
		Если ЕстьРасхождения
			И (СтрокаДвижений.СуммаПодтвержденнаяРуб <> 0 
			ИЛИ СтрокаДвижений.СуммаНДСПодтвержденнаяРуб <> 0) Тогда 
			НоваяСтрока.СуммаБезНДС = СтрокаДвижений.СуммаПодтвержденнаяРуб - СтрокаДвижений.СуммаНДСПодтвержденнаяРуб;
			НоваяСтрока.НДС         = СтрокаДвижений.СуммаНДСПодтвержденнаяРуб;
		КонецЕсли;
		
		Если СтрокаДвижений.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости
		 ИЛИ СтрокаДвижений.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ВосстановленУчитываетсяВCтоимости Тогда
			НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.НДСВключенВСтоимость;
		ИначеЕсли СтрокаДвижений.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0
		 ИЛИ СтрокаДвижений.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ВосстановленДляОперацийПо0 Тогда
			НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0;
		ИначеЕсли СтрокаДвижений.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется
			ИЛИ СтрокаДвижений.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределен Тогда
			НоваяСтрока.Событие = Перечисления.СобытияПоНДСПокупки.НДСПодлежитРаспределению;
		КонецЕсли;
		
	КонецЦикла;

	ДанныеДвиженийНДСПредъявленный.Свернуть("
		|Период,Организация,ДатаСобытия,Событие,
		|СчетФактура,ВидЦенности,СтавкаНДС,СчетУчетаНДС,
		|Поставщик,ДоговорКонтрагента,ИсправленныйСчетФактура",
		"СуммаБезНДС,НДС");
	
	Возврат ДанныеДвиженийНДСПредъявленный;
	
КонецФункции

Функция ПодготовитьДанныеДвиженийНДСПредъявленныйРеализация0ПоступлениеЦенностей(ДанныеДвижений, Реквизиты)
	
	НачислятьНДСПоОтгрузке = УчетнаяПолитика.НачислятьНДСПоОтгрузке(Реквизиты.Организация, Реквизиты.Период);
	СчетаТоварыОтгруженные = Новый Массив;
	Для каждого Счет Из БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные) Цикл //45
		СчетаТоварыОтгруженные.Добавить(Счет);
	КонецЦикла;

	СчетаПартионногоУчетаЦенностей = СчетаПартионногоУчетаЦенностейДляЦелейНДС();
	
	ЕстьКолонкаСтавкаНДСДокумента = ДанныеДвижений.Колонки.Найти("СтавкаНДСДокумента") <> Неопределено;
	
	Если ДанныеДвижений.Колонки.Найти("ДокументОтгрузки") = Неопределено Тогда
		ДанныеДвижений.Колонки.Добавить("ДокументОтгрузки", Документы.ТипВсеСсылки());
	КонецЕсли;
	
	Если ДанныеДвижений.Колонки.Найти("БлокироватьДоПодтвержденияСтавки0") = Неопределено Тогда
		ДанныеДвижений.Колонки.Добавить("БлокироватьДоПодтвержденияСтавки0", Новый ОписаниеТипов("Булево"));
		ДанныеДвижений.ЗаполнитьЗначения(Истина, "БлокироватьДоПодтвержденияСтавки0");
	КонецЕсли;
	
	ДанныеДвиженийНДСПредъявленныйРеализация0 = ДанныеДвижений.СкопироватьКолонки();
	
	Для Каждого СтрокаТаблицы Из ДанныеДвижений Цикл
		
		Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределен
		 ИЛИ СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ВосстановленУчитываетсяВCтоимости
		 ИЛИ СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Списывается 
		 ИЛИ НЕ СтрокаТаблицы.БлокироватьДоПодтвержденияСтавки0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СчетаПартионногоУчетаЦенностей.Найти(СтрокаТаблицы.СчетЗатрат) <> Неопределено Тогда
			Если СчетаТоварыОтгруженные.Найти(СтрокаТаблицы.СчетЗатрат) <> Неопределено Тогда
				Если Не НачислятьНДСПоОтгрузке Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока = ДанныеДвиженийНДСПредъявленныйРеализация0.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		Если ЕстьКолонкаСтавкаНДСДокумента 
			И СтрокаТаблицы.СтавкаНДСДокумента = Перечисления.СтавкиНДС.НДС0 
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументОтгрузки) Тогда
			НоваяСтрока.ДокументОтгрузки = СтрокаТаблицы.Регистратор;
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДвиженийНДСПредъявленныйРеализация0.Свернуть("
		|Регистратор,Период,Организация,СчетФактура,ДокументОтгрузки,
		|ВидЦенности,СтавкаНДС,СчетУчетаНДС,ДатаСобытия,
		|Поставщик,ИсправленныйСчетФактура",
		"СуммаБезНДС,НДС");
	
	ДанныеДвиженийНДСПредъявленныйРеализация0.Колонки.Добавить("Состояние",
		Новый ОписаниеТипов("ПеречислениеСсылка.НДССостоянияРеализация0"));
	ДанныеДвиженийНДСПредъявленныйРеализация0.ЗаполнитьЗначения(
		Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение, "Состояние");	
		
	ДанныеДвиженийНДСПредъявленныйРеализация0.Колонки.Добавить("Событие",
		Новый ОписаниеТипов("ПеречислениеСсылка.СобытияПоНДСПокупки"));
	ДанныеДвиженийНДСПредъявленныйРеализация0.ЗаполнитьЗначения(
		Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0, "Событие");	
		
	Возврат ДанныеДвиженийНДСПредъявленныйРеализация0;
	
КонецФункции

Функция ПодготовитьДанныеДвиженийНДСРаздельныйУчетПоступлениеЦенностей(ДанныеДвижений, Реквизиты)
	
	Если ДанныеДвижений.Колонки.Найти("АналитикаУчетаЗатрат") = Неопределено Тогда
		ДанныеДвижений.Колонки.Добавить("АналитикаУчетаЗатрат",
			Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаЗатрат"));
	КонецЕсли;	
		
	Если ДанныеДвижений.Колонки.Найти("АналитикаУчетаНДС") = Неопределено Тогда	
		ДанныеДвижений.Колонки.Добавить("АналитикаУчетаНДС",
			Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНДС"));
	КонецЕсли;
		
	Если ДанныеДвижений.Колонки.Найти("Поставщик") = Неопределено Тогда
		ДанныеДвижений.Колонки.Добавить("Поставщик",
			Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	КонецЕсли;
		
	Если ДанныеДвижений.Колонки.Найти("ИсправленныйСчетФактура") = Неопределено Тогда
		ДанныеДвижений.Колонки.Добавить("ИсправленныйСчетФактура",
			Новый ОписаниеТипов("ДокументСсылка.КорректировкаПоступления"));
	КонецЕсли;
	
	УчетПоПродажнойСтоимости = УчетнаяПолитика.СпособОценкиТоваровВРознице(Реквизиты.Организация, Реквизиты.Период)
		= Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;
	ПолучательНТТПоПродажнойСтоимости = Ложь;
	
	Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
	 ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПередачаТоваров")
	 ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПеремещениеТоваров") 
	 ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПоступлениеИзПереработки") Тогда 
		ПолучательНТТПоПродажнойСтоимости = УчетПоПродажнойСтоимости
			И Реквизиты.Владелец().Колонки.Найти("ТипСкладаПолучателя") <> Неопределено
			И Реквизиты.ТипСкладаПолучателя = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка;	 
 	Иначе
		ПолучательНТТПоПродажнойСтоимости = Реквизиты.Владелец().Колонки.Найти("ТипСклада") <> Неопределено
			И Реквизиты.ТипСклада = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка;
	КонецЕсли;
	
	СчетаПартионногоУчетаЦенностей = СчетаПартионногоУчетаЦенностейДляЦелейНДС();
	ДанныеДвиженийНДСРаздельныйУчет = ДанныеДвижений.СкопироватьКолонки();
	СокращеннаяАналитика = УчетНДСРаздельный.СокращеннаяАналитика(Реквизиты.Период);
	
	ЭтоПоступлениеТоплива = ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		И Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Топливо;
	
	Для Каждого СтрокаТаблицы Из ДанныеДвижений Цикл
		
		ДляЦелейПартионногоУчета = СчетаПартионногоУчетаЦенностей.Найти(СтрокаТаблицы.СчетЗатрат) <> Неопределено;
		ДляРаспределенияНДС = (СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется)
			ИЛИ (НЕ СтрокаТаблицы.СпособУчетаНДСИзменился И СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется);
		
		Если ЭтоПоступлениеТоплива И Не ДляРаспределенияНДС Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТаблицы.СуммаБезНДС = 0
			И СтрокаТаблицы.НДС = 0
			И СтрокаТаблицы.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
			
		Если ДляЦелейПартионногоУчета Тогда
			Если ПолучательНТТПоПродажнойСтоимости Тогда
				Продолжить;
			КонецЕсли;
		ИначеЕсли ДляРаспределенияНДС Тогда
			Если СтрокаТаблицы.НДС = 0 Тогда
				Продолжить;
			КонецЕсли;
		Иначе
			Продолжить;
		КонецЕсли;
			
		НоваяСтрока = ДанныеДвиженийНДСРаздельныйУчет.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
		Если Не СвойстваСчета.Количественный Тогда
			НоваяСтрока.Количество = 0;
		КонецЕсли;
		
		Если ДляЦелейПартионногоУчета Тогда
			Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
				Если СвойстваСчета["ВидСубконто" + Ном + "ТолькоОбороты"] Тогда
					НоваяСтрока["Субконто" + Ном] = Неопределено;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если СтрокаТаблицы.СпособУчетаНДСИзменился Тогда
			НоваяСтрока.СпособУчетаНДС = СтрокаТаблицы.НовыйСпособУчетаНДС;
		Иначе
			НоваяСтрока.СпособУчетаНДС = СтрокаТаблицы.СпособУчетаНДС;
		КонецЕсли;
		
		Если НоваяСтрока.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Списывается Тогда 
			НоваяСтрока.СуммаБезНДС = 0;
			НоваяСтрока.НДС = 0;
		КонецЕсли;
		
		НоваяСтрока.АналитикаУчетаЗатрат = Справочники.КлючиАналитикиУчетаЗатрат.КлючиАналитикиУчетаЗатратДокумента(НоваяСтрока);
		
		Если СокращеннаяАналитика Тогда
			НоваяСтрока.Поставщик = Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(НоваяСтрока.АналитикаУчетаНДС) Тогда
			НоваяСтрока.АналитикаУчетаНДС = Справочники.КлючиАналитикиУчетаНДС.КлючиАналитикиУчетаНДСДокумента(НоваяСтрока);
		КонецЕсли;
			
	КонецЦикла;
	
	ДанныеДвиженийНДСРаздельныйУчет.Свернуть("
		|Регистратор,Период,Организация,СпособУчетаНДС,
		|Партия,АналитикаУчетаЗатрат,АналитикаУчетаНДС",
		"Количество,СуммаБезНДС,НДС");
		
	Возврат ДанныеДвиженийНДСРаздельныйУчет;
	
КонецФункции

Процедура СформироватьДвиженияПоСтоимостиОСПриПринятииКУчету(ДанныеПроводок, Реквизиты, Движения, Отказ, ИсключениеИзСтоимости = Ложь)
	
	Если ДанныеПроводок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаОС = ДанныеПроводок.Скопировать(,"Субконто1,НДС");
	ТаблицаОС.Свернуть("Субконто1","НДС");
	ТаблицаОС.Колонки.Субконто1.Имя = "ОсновноеСредство";
	
	Для Каждого СтрокаТаблицы Из ТаблицаОС Цикл
		СтрокаТаблицы.НДС = ?(ИсключениеИзСтоимости, -1, 1) * СтрокаТаблицы.НДС;
	КонецЦикла;	
		
	ТаблицаОС.Индексы.Добавить("ОсновноеСредство");
	
	// Таблица первоначальных сведений ОС
	СтрокиДвижений = Движения.ПервоначальныеСведенияОСБухгалтерскийУчет.Выгрузить();
	Движения.ПервоначальныеСведенияОСБухгалтерскийУчет.Очистить();
	Для Каждого СтрокаДвижений Из СтрокиДвижений Цикл
		СтрокиТаблицыОС = ТаблицаОС.НайтиСтроки(Новый Структура("ОсновноеСредство", СтрокаДвижений.ОсновноеСредство));
		Для Каждого СтрокаСведений Из СтрокиТаблицыОС Цикл
			СтрокаДвижений.ПервоначальнаяСтоимость = СтрокаДвижений.ПервоначальнаяСтоимость + СтрокаСведений.НДС;
			Движение = Движения.ПервоначальныеСведенияОСБухгалтерскийУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижений);
		КонецЦикла;
	КонецЦикла;
	Движения.ПервоначальныеСведенияОСБухгалтерскийУчет.Записывать = Истина;
	
	// Таблица первоначальных сведений ОС (НУ)
	СтрокиДвижений = Движения.ПервоначальныеСведенияОСНалоговыйУчет.Выгрузить();
	Движения.ПервоначальныеСведенияОСНалоговыйУчет.Очистить();
	Для Каждого СтрокаДвижений Из СтрокиДвижений Цикл
		СтрокиТаблицыОС = ТаблицаОС.НайтиСтроки(Новый Структура("ОсновноеСредство", СтрокаДвижений.ОсновноеСредство));
		Для Каждого СтрокаСведений Из СтрокиТаблицыОС Цикл
			СтрокаДвижений.ПервоначальнаяСтоимостьНУ = СтрокаДвижений.ПервоначальнаяСтоимостьНУ + СтрокаСведений.НДС;
			Движение = Движения.ПервоначальныеСведенияОСНалоговыйУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижений);
		КонецЦикла;
	КонецЦикла;
	Движения.ПервоначальныеСведенияОСНалоговыйУчет.Записывать = Истина;
	
	// Таблица параметров амортизации (БУ)
 	СтрокиДвижений = Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Выгрузить();
	Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Очистить();
	Для Каждого СтрокаДвижений Из СтрокиДвижений Цикл
		СтрокиТаблицыОС = ТаблицаОС.НайтиСтроки(Новый Структура("ОсновноеСредство", СтрокаДвижений.ОсновноеСредство));
		Для Каждого СтрокаСведений Из СтрокиТаблицыОС Цикл
			СтрокаДвижений.СтоимостьДляВычисленияАмортизации = СтрокаДвижений.СтоимостьДляВычисленияАмортизации + СтрокаСведений.НДС;
			Движение = Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижений);
		КонецЦикла;
	КонецЦикла;
    Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Записывать = Истина;
	
	// Таблица амортизационной премии
	Если Реквизиты.ПроцентАмортизационнойПремии > 0 Тогда
		ТаблицаОС.Колонки.НДС.Имя = "СуммаНУ";
 		ТаблицаАмортизационнойПремии = УчетОС.ПодготовитьТаблицуАмортизационнаяПремия(ТаблицаОС, Реквизиты.Владелец(), Отказ);
		УчетОС.СформироватьДвиженияОтражениеАмортизационнойПремии(ТаблицаАмортизационнойПремии, Реквизиты.Владелец(), Движения, Отказ);
	КонецЕсли;
		
КонецПроцедуры

Процедура СформироватьДвиженияПоСтоимостиНМАПриПринятииКУчету(ДанныеПроводок, Реквизиты, Движения, Отказ, ИсключениеИзСтоимости = Ложь)
	
	Если ДанныеПроводок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНМА = ДанныеПроводок.Скопировать(,"Субконто1,НДС");
	ТаблицаНМА.Свернуть("Субконто1","НДС");
	ТаблицаНМА.Колонки.Субконто1.Имя = "НематериальныйАктив";
	
	Для Каждого СтрокаТаблицы Из ТаблицаНМА Цикл
		СтрокаТаблицы.НДС = ?(ИсключениеИзСтоимости, -1, 1) * СтрокаТаблицы.НДС;
	КонецЦикла;	
		
	ТаблицаНМА.Индексы.Добавить("НематериальныйАктив");
	
	// Таблица первоначальных сведений НМА
	СтрокиДвижений = Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Выгрузить();
	Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Очистить();
	Для Каждого СтрокаДвижений Из СтрокиДвижений Цикл
		СтрокиТаблицыНМА = ТаблицаНМА.НайтиСтроки(Новый Структура("НематериальныйАктив", СтрокаДвижений.НематериальныйАктив));
		Для Каждого СтрокаСведений Из СтрокиТаблицыНМА Цикл
			СтрокаДвижений.ПервоначальнаяСтоимость = СтрокаДвижений.ПервоначальнаяСтоимость + СтрокаСведений.НДС;
			Движение = Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижений);
		КонецЦикла;
	КонецЦикла;
	Движения.ПервоначальныеСведенияНМАБухгалтерскийУчет.Записывать = Истина;
	
	// Таблица первоначальных сведений НМА (НУ)
	СтрокиДвижений = Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Выгрузить();
	Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Очистить();
	Для Каждого СтрокаДвижений Из СтрокиДвижений Цикл
		СтрокиТаблицыНМА = ТаблицаНМА.НайтиСтроки(Новый Структура("НематериальныйАктив", СтрокаДвижений.НематериальныйАктив));
		Для Каждого СтрокаСведений Из СтрокиТаблицыНМА Цикл
			СтрокаДвижений.ПервоначальнаяСтоимостьНУ = СтрокаДвижений.ПервоначальнаяСтоимостьНУ + СтрокаСведений.НДС;
			Движение = Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижений);
		КонецЦикла;
	КонецЦикла;
	Движения.ПервоначальныеСведенияНМАНалоговыйУчет.Записывать = Истина;
			
КонецПроцедуры

Процедура СформироватьДвиженияПоСтоимостиОСПриМодернизации(ДанныеПроводок, Реквизиты, Движения, Отказ)
	
	Если ДанныеПроводок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаОС = ДанныеПроводок.Скопировать(,"Субконто1,НДС");
	ТаблицаОС.Свернуть("Субконто1","НДС");
	ТаблицаОС.Колонки.Субконто1.Имя = "ОсновноеСредство";
	
	ТаблицаОС.Индексы.Добавить("ОсновноеСредство");
	
	// Таблица параметров амортизации (БУ)
 	СтрокиДвижений = Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Выгрузить();
	Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Очистить();
	Для Каждого СтрокаДвижений Из СтрокиДвижений Цикл
		СтрокиТаблицыОС = ТаблицаОС.НайтиСтроки(Новый Структура("ОсновноеСредство", СтрокаДвижений.ОсновноеСредство));
		Для Каждого СтрокаСведений Из СтрокиТаблицыОС Цикл
			СтрокаДвижений.СтоимостьДляВычисленияАмортизации = СтрокаДвижений.СтоимостьДляВычисленияАмортизации + СтрокаСведений.НДС;
			Движение = Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижений);
		КонецЦикла;
	КонецЦикла;
    Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Записывать = Истина;
	
	// Таблица События ОС
 	СтрокиДвижений = Движения.СобытияОСОрганизаций.Выгрузить();
	Движения.СобытияОСОрганизаций.Очистить();
	Для Каждого СтрокаДвижений Из СтрокиДвижений Цикл
		СтрокиТаблицыОС = ТаблицаОС.НайтиСтроки(Новый Структура("ОсновноеСредство", СтрокаДвижений.ОсновноеСредство));
		Для Каждого СтрокаСведений Из СтрокиТаблицыОС Цикл
			СтрокаДвижений.СуммаЗатратБУ = СтрокаДвижений.СуммаЗатратБУ + СтрокаСведений.НДС;
			СтрокаДвижений.СуммаЗатратНУ = СтрокаДвижений.СуммаЗатратНУ + СтрокаСведений.НДС;
			Движение = Движения.СобытияОСОрганизаций.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижений);
		КонецЦикла;
	КонецЦикла;
    Движения.СобытияОСОрганизаций.Записывать = Истина;
	
	// Амортизационная премия
	ТаблицаАмортизационнойПремии = Новый ТаблицаЗначений;
	ТаблицаАмортизационнойПремии.Колонки.Добавить("ОсновноеСредство",
		Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
	ТаблицаАмортизационнойПремии.Колонки.Добавить("СчетУчета",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаАмортизационнойПремии.Колонки.Добавить("Сумма",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
		
	Для Каждого СтрокаДвижений Из ДанныеПроводок Цикл
		Если СтрокаДвижений.ПроцентАмортизационнойПремии > 0 Тогда
			НоваяСтрока = ТаблицаАмортизационнойПремии.Добавить();
			НоваяСтрока.СчетУчета = СтрокаДвижений.СчетЗатрат;
			НоваяСтрока.ОсновноеСредство = СтрокаДвижений.Субконто1;
			НоваяСтрока.Сумма = Окр(СтрокаДвижений.НДС * СтрокаДвижений.ПроцентАмортизационнойПремии / 100, 2);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаАмортизационнойПремии.Свернуть("ОсновноеСредство,СчетУчета","Сумма");
	
	УчетОС.СформироватьДвиженияОтражениеАмортизационнойПремии(ТаблицаАмортизационнойПремии, Реквизиты.Владелец(), Движения, Отказ);

	
	ТаблицаАмортизационнойПремии.Колонки.Сумма.Имя = "СуммаНУ";
	ТаблицаАмортизационнойПремии.Колонки.Добавить("СуммаПР",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаАмортизационнойПремии.Колонки.Добавить("СуммаВР",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
		
	Для Каждого СтрокаТаблицы Из ТаблицаАмортизационнойПремии Цикл
		СтрокаТаблицы.СуммаВР =  -СтрокаТаблицы.СуммаНУ; 		
	КонецЦикла;	
		
	ТаблицаАмортизационнойПремии.Колонки.Добавить("ДокументАмортизационнойПремии",
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыАмортизационнойПремии.ТипЗначения);
	ТаблицаАмортизационнойПремии.ЗаполнитьЗначения(Реквизиты.Регистратор, "ДокументАмортизационнойПремии");
	
	ТаблицаАмортизационнойПремии.Колонки.Добавить("СчетУчетаЗатратПоАмортизационнойПремии",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаАмортизационнойПремии.ЗаполнитьЗначения(Реквизиты.СчетУчетаЗатратПоАмортизационнойПремии,
		"СчетУчетаЗатратПоАмортизационнойПремии");

	ТаблицаАмортизационнойПремии.Колонки.Добавить("ПодразделениеПоАмортизационнойПремии",
		БухгалтерскийУчетКлиентСерверПереопределяемый.ОписаниеТиповПодразделения());
	ТаблицаАмортизационнойПремии.ЗаполнитьЗначения(Реквизиты.ПодразделениеПоАмортизационнойПремии,
		"ПодразделениеПоАмортизационнойПремии");

	ОписаниеТиповСубконто = Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип;

	ТаблицаАмортизационнойПремии.Колонки.Добавить("СубконтоПоАмортизационнойПремии1", ОписаниеТиповСубконто);
	ТаблицаАмортизационнойПремии.ЗаполнитьЗначения(Реквизиты.СубконтоПоАмортизационнойПремии1,
		"СубконтоПоАмортизационнойПремии1");

	ТаблицаАмортизационнойПремии.Колонки.Добавить("СубконтоПоАмортизационнойПремии2", ОписаниеТиповСубконто);
	ТаблицаАмортизационнойПремии.ЗаполнитьЗначения(Реквизиты.СубконтоПоАмортизационнойПремии2,
		"СубконтоПоАмортизационнойПремии2");

	ТаблицаАмортизационнойПремии.Колонки.Добавить("СубконтоПоАмортизационнойПремии3", ОписаниеТиповСубконто);
	ТаблицаАмортизационнойПремии.ЗаполнитьЗначения(Реквизиты.СубконтоПоАмортизационнойПремии3,
		"СубконтоПоАмортизационнойПремии3");

	ТаблицаАмортизационнойПремии.Колонки.Добавить("Подразделение",
		БухгалтерскийУчетКлиентСерверПереопределяемый.ОписаниеТиповПодразделения());
	ТаблицаАмортизационнойПремии.ЗаполнитьЗначения(Реквизиты.Подразделение,
		"Подразделение");

	УчетОС.СформироватьДвиженияНачислениеАмортизационнойПремии(ТаблицаАмортизационнойПремии, Реквизиты.Владелец(), Движения, Отказ);
		
КонецПроцедуры

#КонецОбласти

#Область ВыбытиеЦенностей

Процедура СформироватьДвиженияВыбытиеТоваров(СписанныеПартииНДС, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ) Экспорт

	Если СписанныеПартииНДС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если НЕ РаздельныйУчетНДСНаСчете19 Тогда
		Возврат;
	КонецЕсли;
		
	//ДВИЖЕНИЯ НДС ДЛЯ КОРРЕКТИРОВКИ ПРЕДЫДУЩЕГО СПОСОБА УЧЕТА НДС
	
	//Восстановим НДС
	СписанныеПартииНДСУменьшениеВычета = СписанныеПартииНДС.НайтиСтроки(Новый Структура(
		"СпособУчетаНДСИзменился, СпособУчетаНДС, ЕстьНДСПредъявленный",
		Истина, Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету, Истина));
		
	ПустаяТаблицаСписанныеПартии = СписанныеПартииНДС.СкопироватьКолонки();
	ТаблицаВосстановление = ПодготовитьТаблицуВосстановление(СписанныеПартииНДСУменьшениеВычета, ПустаяТаблицаСписанныеПартии);
	
	ЕстьВосстановление = ТаблицаВосстановление.Количество() > 0;
	
	Если ЕстьВосстановление Тогда
		СформироватьДвиженияНДСЗаписиКнигиПродажВосстановлениеНДС(
			ТаблицаВосстановление, Реквизиты, Движения, Отказ);
		ТаблицаНДСПредъявленный = ПодготовитьТаблицуНДСПредъявленныйВосстановление(
			ТаблицаВосстановление, Реквизиты);
		УчетНДС.СформироватьДвиженияНДСПредъявленный(ТаблицаНДСПредъявленный, Движения, Отказ, Истина);
	КонецЕсли;
	
	//Исключим НДС из стоимости
	СписанныеПартииНДСИсключениеИзСтоимости = СписанныеПартииНДС.Скопировать(Новый Структура(
		"СпособУчетаНДСИзменился, СпособУчетаНДС, ЕстьНДСПредъявленный",
		Истина, Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости, Истина));
		
	ЕстьИсключениеИзСтоимости = СписанныеПартииНДСИсключениеИзСтоимости.Количество() > 0;
		
	Если ЕстьИсключениеИзСтоимости Тогда
		Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуОС")
		 ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуНМА")
		 ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.МодернизацияОС") Тогда
			СформироватьПроводкиИсключениеНДСИзСтоимостиОСиНМА(
				СписанныеПартииНДСИсключениеИзСтоимости, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
		Иначе
			СформироватьПроводкиИсключениеНДСИзСтоимости(
				СписанныеПартииНДСИсключениеИзСтоимости, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
		КонецЕсли;
		ТаблицаНДСПредъявленный = ПодготовитьСторноТаблицуНДСПредъявленный(
			СписанныеПартииНДСИсключениеИзСтоимости, Реквизиты, Перечисления.СобытияПоНДСПокупки.НДСВключенВСтоимость);
		УчетНДСБП.СформироватьДвиженияНДСПредъявленныйИсключениеНДСИзСтоимости(
			ТаблицаНДСПредъявленный, Движения, Отказ);
	КонецЕсли;
		
	//Отменим отнесение к операциям, облагаемым по ставке 0%
	СписанныеПартииНДСОтменаСтавки0 = СписанныеПартииНДС.НайтиСтроки(Новый Структура(
		"СпособУчетаНДСИзменился, СпособУчетаНДС, ЕстьНДСПредъявленный",
		Истина, Перечисления.СпособыУчетаНДС.ДляОперацийПо0, Истина));
		
	ЕстьОтменаОперацийПо0 = СписанныеПартииНДСОтменаСтавки0.Количество() > 0;
		
	Если ЕстьОтменаОперацийПо0 Тогда
		ТаблицаНДСПредъявленный = ПодготовитьСторноТаблицуНДСПредъявленный(
			СписанныеПартииНДСОтменаСтавки0, Реквизиты, Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0);
		УчетНДСБП.СформироватьДвиженияНДСПредъявленныйИсключениеНДСИзСтоимости(
			ТаблицаНДСПредъявленный, Движения, Отказ);
	КонецЕсли;
	
	//Отменим включение НДС в распределение
	СписанныеПартииНДСОтменаРаспределения = СписанныеПартииНДС.НайтиСтроки(Новый Структура(
		"СпособУчетаНДСИзменился, СпособУчетаНДС, ЕстьНДСПредъявленный",
		Истина, Перечисления.СпособыУчетаНДС.Распределяется, Истина));
		
	ЕстьОтменаВключенияВРаспределение = СписанныеПартииНДСОтменаРаспределения.Количество() > 0;
		
	Если ЕстьОтменаВключенияВРаспределение Тогда
		ТаблицаНДСПредъявленный = ПодготовитьСторноТаблицуНДСПредъявленный(
			СписанныеПартииНДСОтменаРаспределения, Реквизиты, Перечисления.СобытияПоНДСПокупки.НДСПодлежитРаспределению);
		УчетНДСБП.СформироватьДвиженияНДСПредъявленныйИсключениеНДСИзСтоимости(
			ТаблицаНДСПредъявленный, Движения, Отказ);
	КонецЕсли;
	
	//Отмена распределения НДС, выполненного в прошлых периодах не выполняется (в соответствии с методикой)
	
	//Перенесем НДС на 19 счете на новые способы учета
	ТаблицаКорректировкиСпособаУчетаНаСчете19  = ПодготовитьПроводкиПоКорректировкеСпособаУчетаНДС(СписанныеПартииНДС);
	СформироватьПроводкиПоКорректировкеСпособаУчетаНДС(ТаблицаКорректировкиСпособаУчетаНаСчете19, Реквизиты, Движения, Отказ);
	
	//Спишем партии НДС из регистра НДС раздельный учет
	СформироватьДвиженияНДСРаздельныйУчетВыбытиеЦенностей(СписанныеПартииНДС, Движения, Отказ);
	
	//ДВИЖЕНИЯ НДС ПО ОТНЕСЕНИЮ К НОВОМУ СПОСОБУ УЧЕТА НДС
	
	//Распределим НДС
	РаспределяемыеПартииНДС = СписанныеПартииНДС.Скопировать(Новый Структура(
		"СпособУчетаНДСИзменился, НовыйСпособУчетаНДС, ЕстьНДСПредъявленный",
		Истина, Перечисления.СпособыУчетаНДС.Распределен, Истина));
		
	ЕстьРаспределяемыеПартииНДС = РаспределяемыеПартииНДС.Количество() > 0;
	
	Если ЕстьРаспределяемыеПартииНДС Тогда
		
		//Бух. проводки распределение НДС на 19 счете
		ДанныеПроводок = ПодготовитьДанныеПроводокРаспределениеНДСНаСчете19(РаспределяемыеПартииНДС);
		СформироватьПроводкиПоКорректировкеСпособаУчетаНДС(ДанныеПроводок, Реквизиты, Движения, Отказ);
		
		//Бух. проводки включение НДС в стоимость
		
		Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуОС")
		 ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуНМА")
		 ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.МодернизацияОС") Тогда
		    СформироватьПроводкиВключениеНДСВСтоимостьОСиНМА(РаспределяемыеПартииНДС, Реквизиты, Движения, Отказ);
	 	Иначе
			ДанныеПроводок = ПодготовитьДанныеПроводокВключениеВСтоимостьРаспределениеНДСВыбытиеТоваров(РаспределяемыеПартииНДС);
			СформироватьПроводкиВключениеНДСВСтоимость(ДанныеПроводок, Движения, Отказ);
		КонецЕсли;
		
		//Движения по НДСПредъявленный
		ДанныеДвижений = ПодготовитьДанныеДвиженийНДСПредъявленныйРаспределениеНДСВыбытиеТоваров(РаспределяемыеПартииНДС);
		УчетНДС.СформироватьДвиженияНДСПредъявленный(ДанныеДвижений, Движения, Отказ);
		
		//Движения НДСПредъявленныйРеализация0
		ДанныеДвижений = ПодготовитьДанныеДвиженийНДСПредъявленныйРеализация0ВыбытиеТоваров(РаспределяемыеПартииНДС);
		СформироватьДвиженияНДСПредъявленныйРеализация0ПоступлениеЦенностей(ДанныеДвижений, Движения, Отказ);
		
	КонецЕсли;
	
	//Отнесем НДС к новому способу учета
	Если ТипЗнч(Реквизиты.Регистратор) <> Тип("ДокументСсылка.КомплектацияНоменклатуры") Тогда
		
		ТаблицаДляИзмененияСпособаУчетаНДС = ПодготовитьТаблицуДляИзмененияСпособаУчетаНДС(
			СписанныеПартииНДС, Реквизиты);
		СформироватьДвиженияПоступлениеЦенностей(ТаблицаДляИзмененияСпособаУчетаНДС, Реквизиты, Движения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Функция НовыйТаблицаДокументыПоНеоблагаемымНДСОперациям()
	
	ТаблицаДокументыПоНеоблагаемымНДСОперациям = Новый ТаблицаЗначений;
	
	ТаблицаДокументыПоНеоблагаемымНДСОперациям.Колонки.Добавить("Организация", 
		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДокументыПоНеоблагаемымНДСОперациям.Колонки.Добавить("КодОперации", 
		Новый ОписаниеТипов("СправочникСсылка.КодыОперацийРаздела7ДекларацииПоНДС"));
	ТаблицаДокументыПоНеоблагаемымНДСОперациям.Колонки.Добавить("Контрагент", 
		Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаДокументыПоНеоблагаемымНДСОперациям.Колонки.Добавить("ДокументРеализации", Документы.ТипВсеСсылки());
	ТаблицаДокументыПоНеоблагаемымНДСОперациям.Колонки.Добавить("ТипПодтверждающегоДокумента", 
		Новый ОписаниеТипов("СправочникСсылка.ТипыДокументовПодтверждающихЛьготуНДС"));
	ТаблицаДокументыПоНеоблагаемымНДСОперациям.Колонки.Добавить("НомерДокумента", Новый ОписаниеТипов("Строка"));
	ТаблицаДокументыПоНеоблагаемымНДСОперациям.Колонки.Добавить("ДатаДокумента", Новый ОписаниеТипов("Дата"));
	
	Возврат ТаблицаДокументыПоНеоблагаемымНДСОперациям;
	
КонецФункции

Функция ПодготовитьПроводкиПоКорректировкеСпособаУчетаНДС(СписанныеПартииНДС)
	
	ТаблицаКорректировкиСпособаУчетаНДС = СписанныеПартииНДС.СкопироватьКолонки("
		|СчетУчетаНДС,Поставщик,СчетФактура,
		|СпособУчетаНДС,НовыйСпособУчетаНДС,НДС");
		
	Для Каждого СтрокаТаблицы Из СписанныеПартииНДС Цикл
		
		Если НЕ СтрокаТаблицы.СпособУчетаНДСИзменился
		    ИЛИ СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределен Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетФактура) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределен Тогда
		   
			Если СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету Тогда
				
				НДС = СтрокаТаблицы.НДС - СтрокаТаблицы.НДСПринимаетсяКВычету;
				Если НДС <> 0 Тогда
					
					НоваяСтрока = ТаблицаКорректировкиСпособаУчетаНДС.Добавить();
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					НоваяСтрока.НДС = НДС;
					НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется;
					
				КонецЕсли;
				
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости Тогда
				
				НДС = СтрокаТаблицы.НДС - СтрокаТаблицы.НДСУчитываетсяВCтоимости;
				Если НДС <> 0 Тогда
					
					НоваяСтрока = ТаблицаКорректировкиСпособаУчетаНДС.Добавить();
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					НоваяСтрока.НДС = НДС;
					НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется;
					
				КонецЕсли;
				
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 Тогда
				
				НДС = СтрокаТаблицы.НДС - СтрокаТаблицы.НДСДляОперацийПо0;
				Если НДС <> 0 Тогда
					
					НоваяСтрока = ТаблицаКорректировкиСпособаУчетаНДС.Добавить();
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
					НоваяСтрока.НДС = НДС;
					НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется;
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если СтрокаТаблицы.НДС <> 0 Тогда
				
				НоваяСтрока = ТаблицаКорректировкиСпособаУчетаНДС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаКорректировкиСпособаУчетаНДС.Свернуть("
		|СчетУчетаНДС,Поставщик,СчетФактура,СпособУчетаНДС,НовыйСпособУчетаНДС",
		"НДС");
	
	ТаблицаКорректировкиСпособаУчетаНДС.Колонки.Добавить("Содержание",
		Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)));
	ТаблицаКорректировкиСпособаУчетаНДС.ЗаполнитьЗначения(
		НСтр("ru = 'Корректировка способа учета НДС'"), "Содержание");	
	
	Возврат ТаблицаКорректировкиСпособаУчетаНДС;
	
КонецФункции

Функция ПодготовитьСторноТаблицуНДСПредъявленный(ТаблицаИсключениеИзСтоимости, Реквизиты, Событие)

	ТаблицаНДСПредъявленный = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленный");

	Если ТаблицаИсключениеИзСтоимости.Количество() = 0 Тогда
		Возврат ТаблицаНДСПредъявленный;
	КонецЕсли;

	Для каждого СтрокаТаблицы Из ТаблицаИсключениеИзСтоимости Цикл
		
		Если СтрокаТаблицы.СуммаБезНДС = 0 И СтрокаТаблицы.НДС = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаНДСПредъявленный.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.СуммаБезНДС = -НоваяСтрока.СуммаБезНДС;
		НоваяСтрока.НДС         = -НоваяСтрока.НДС;
		
	КонецЦикла;

	ТаблицаНДСПредъявленный.ЗаполнитьЗначения(Реквизиты.Период,      "Период");
	ТаблицаНДСПредъявленный.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");
	ТаблицаНДСПредъявленный.ЗаполнитьЗначения(Реквизиты.Период,      "ДатаСобытия");
	ТаблицаНДСПредъявленный.ЗаполнитьЗначения(Событие, "Событие");

	Возврат ТаблицаНДСПредъявленный;

КонецФункции

Функция ПодготовитьТаблицуНДСПредъявленныйВосстановление(ДанныеДвижений, Реквизиты)
	
	ДанныеДвиженийНДСПредъявленный = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленный");
		
	Для Каждого СтрокаДвижений Из ДанныеДвижений Цикл
		
		НоваяСтрока = ДанныеДвиженийНДСПредъявленный.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДвижений);
		
		НоваяСтрока.СуммаБезНДС = -СтрокаДвижений.СуммаБезНДС;
		НоваяСтрока.НДС = -СтрокаДвижений.НДС;
		
	КонецЦикла;

	ДанныеДвиженийНДСПредъявленный.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПокупки.ВосстановленНДС, "Событие");
		
	ДанныеДвиженийНДСПредъявленный.Свернуть("
		|Период,Организация,ДатаСобытия,Событие,
		|СчетФактура,ВидЦенности,СтавкаНДС,СчетУчетаНДС,
		|Поставщик,ДоговорКонтрагента,ИсправленныйСчетФактура",
		"СуммаБезНДС,НДС");
	
	Возврат ДанныеДвиженийНДСПредъявленный;
	
КонецФункции

Функция ПодготовитьТаблицуВосстановление(СтрокиВключениеВСтоимость, ПустаяТаблицаСписанныеПартии)

	ТаблицаВосстановление = ПустаяТаблицаСписанныеПартии.СкопироватьКолонки();

	Для каждого СтрокаТаблицы Из СтрокиВключениеВСтоимость Цикл

		Если СтрокаТаблицы.НДСПринятоКВычету > 0 Тогда

			НоваяСтрока = ТаблицаВосстановление.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСПринятоКВычету;
			НоваяСтрока.НДС         = СтрокаТаблицы.НДСПринятоКВычету;

		КонецЕсли;

	КонецЦикла;

	Возврат ТаблицаВосстановление;

КонецФункции

Функция ПодготовитьТаблицуДляИзмененияСпособаУчетаНДС(СписанныеПартииНДС, Реквизиты)
	
	ТаблицаЦенностей = СписанныеПартииНДС.СкопироватьКолонки("
		|АналитикаУчетаНДС,ВидЦенности,ДатаСобытия,ИсправленныйСчетФактура,Количество,АналитикаУчетаЗатрат,
		|НДС,Организация,Партия,Период,Поставщик,Регистратор,СпособУчетаНДС,СтавкаНДС,ДокументОтгрузки,
		|СуммаБезНДС,СчетУчетаНДС,СчетФактура,НовыйСпособУчетаНДС,СпособУчетаНДСИзменился,ЕстьНДСПредъявленный,
		|СтавкаНДСДокумента,СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,ПроцентАмортизационнойПремии,
		|БлокироватьДоПодтвержденияСтавки0");
	
	ТаблицаЦенностей.Колонки.Добавить("ДоговорКонтрагента",
		Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
		
	Для Каждого СтрокаТаблицы Из СписанныеПартииНДС Цикл
		
		СуммаБезНДС = СтрокаТаблицы.СуммаБезНДС - СтрокаТаблицы.СуммаБезНДСПринятоКВычету; 
		НДС = СтрокаТаблицы.НДС - СтрокаТаблицы.НДСПринятоКВычету;
		
		//Отсутствуют данные о сумме без НДС и сумме НДС. Например, ценность была оприходована.
		ОтсутствуютДанныеОСуммахБезНДСиНДС = СтрокаТаблицы.СуммаБезНДС = 0 И СтрокаТаблицы.НДС = 0;
		
		Если СуммаБезНДС > 0
		 ИЛИ НДС > 0
		 ИЛИ (ОтсутствуютДанныеОСуммахБезНДСиНДС И СтрокаТаблицы.Количество <> 0) Тогда
		
			НоваяСтрока = ТаблицаЦенностей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
			НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
			НоваяСтрока.НДС = НДС;
			
		КонецЕсли;
		
		Если СтрокаТаблицы.СуммаБезНДСПринятоКВычету > 0
		 ИЛИ СтрокаТаблицы.НДСПринятоКВычету > 0 Тогда
		 
			НоваяСтрока = ТаблицаЦенностей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			Если СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости Тогда
				НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ВосстановленУчитываетсяВCтоимости;
			ИначеЕсли СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 Тогда
				НоваяСтрока.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ВосстановленДляОперацийПо0;
			КонецЕсли;
			
			Если СуммаБезНДС > 0 ИЛИ НДС > 0 Тогда
				НоваяСтрока.Количество = 0;
			КонецЕсли;
			
			НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСПринятоКВычету;
			НоваяСтрока.НДС = СтрокаТаблицы.НДСПринятоКВычету;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаЦенностей;
	
КонецФункции

Функция ПодготовитьДанныеДвиженийНДСПредъявленныйРеализация0ВыбытиеТоваров(ПартииНДС)
	
	ДанныеДвижений = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленныйРеализация0");
	
	Для Каждого СтрокаТаблицы Из ПартииНДС Цикл
		
		Если СтрокаТаблицы.НДСДляОперацийПо0 = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ДанныеДвижений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		НоваяСтрока.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСДляОперацийПо0;
		НоваяСтрока.НДС = СтрокаТаблицы.НДСДляОперацийПо0;
		
	КонецЦикла;
	
	ДанныеДвижений.ЗаполнитьЗначения(Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение, "Состояние");
	ДанныеДвижений.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПокупки.ПредполагаетсяСтавка0, "Событие");
	
	ДанныеДвижений.Свернуть("Период,Организация,СчетФактура,Состояние,ДатаСобытия,Событие,
							|ДокументОтгрузки,ВидЦенности,СтавкаНДС,СчетУчетаНДС,
							|Поставщик,ИсправленныйСчетФактура",
							"СуммаБезНДС,НДС");
	
	Возврат ДанныеДвижений;
	
КонецФункции

Функция ПодготовитьДанныеПроводокВключениеВСтоимостьРаспределениеНДСВыбытиеТоваров(ПартииНДС)
	
	ДанныеПроводок = ПартииНДС.СкопироватьКолонки("
		|Организация,Период,
		|СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,
		|СчетУчетаНДС,СчетФактура,Поставщик,
		|НДС");
		
	ДанныеПроводок.Колонки.Добавить("Содержание",
		Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)));
		
	Для Каждого СтрокаТаблицы Из ПартииНДС Цикл
		
		Если СтрокаТаблицы.НДСУчитываетсяВCтоимости = 0
		 ИЛИ СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости Тогда
			Продолжить;
		КонецЕсли;
		
		//Считаем, что восстанавливаемый НДС в первую очередь относится к экспорту
		ОстатокВосстановленногоНДС = Макс(СтрокаТаблицы.НДСПринятоКВычету - СтрокаТаблицы.НДСДляОперацийПо0, 0);
		
		НоваяСтрока = ДанныеПроводок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.НДС = СтрокаТаблицы.НДСУчитываетсяВCтоимости - ОстатокВосстановленногоНДС;
		НоваяСтрока.Содержание = НСтр("ru = 'НДС включен в стоимость ценностей'");
		
		Если ОстатокВосстановленногоНДС > 0 Тогда
			
			НоваяСтрока = ДанныеПроводок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НДС = ОстатокВосстановленногоНДС;
			
			НоваяСтрока.СчетЗатрат = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
			
			СвойстваСчетаЗатрат = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(НоваяСтрока.СчетЗатрат);
			Если НЕ СвойстваСчетаЗатрат.УчетПоПодразделениям Тогда
				НоваяСтрока.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			
			НоваяСтрока.Субконто1 = Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("СписаниеНДСНаПрочиеРасходы");
			НоваяСтрока.Субконто2 = Неопределено;
			НоваяСтрока.Субконто3 = Неопределено;
			
			НоваяСтрока.Содержание = НСтр("ru = 'Списан на расходы НДС, ранее принятый к вычету'");
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеПроводок.Колонки.Добавить("НовыйСпособУчетаНДС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	ДанныеПроводок.ЗаполнитьЗначения(Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости, "НовыйСпособУчетаНДС");	
	
	ДанныеПроводок.Свернуть("
		|Организация,Период,Содержание,
		|СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,
		|СчетУчетаНДС,СчетФактура,Поставщик,НовыйСпособУчетаНДС",
		"НДС");
	
	Возврат ДанныеПроводок;
	
КонецФункции

Функция ПодготовитьДанныеДвиженийНДСПредъявленныйРаспределениеНДСВыбытиеТоваров(ПартииНДС)
	
	ДанныеДвижений = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСПредъявленный");
	
	Для Каждого СтрокаТаблицы Из ПартииНДС Цикл
		
		СуммаБезНДС = СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету;
		НДС = СтрокаТаблицы.НДСПринимаетсяКВычету;
		
		Если СуммаБезНДС <> 0 ИЛИ НДС <> 0 Тогда
			
			НоваяСтрока = ДанныеДвижений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			
			НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
			НоваяСтрока.НДС = НДС;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДвижений.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПокупки.НДСРаспределен, "Событие");
	
	ДанныеДвижений.Свернуть("Период,Организация,СчетФактура,ВидЦенности,
							|СтавкаНДС,СчетУчетаНДС,Поставщик,ДатаОплаты,Событие,
							|ДоговорКонтрагента,ИсправленныйСчетФактура,ДатаСобытия",
							"СуммаБезНДС,НДС");
							
	Возврат ДанныеДвижений;
	
КонецФункции

#КонецОбласти

#Область НеоблагаемыеНДСОперации

Процедура СформироватьДвиженияРеализацияБезНДС(СписанныеПартииНДС, РеализованныеТоварыУслуги, Реквизиты, Движения, Отказ) Экспорт
	
	Если РеализованныеТоварыУслуги.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если СписанныеПартииНДС = Неопределено Тогда 
		СписанныеПартииНДС = ПустаяТаблицаСписанныеПартииНДС();
	КонецЕсли;
	
	// Движения по НДСНеоблагаемыеОперации
	ДанныеДвижений = ПодготовитьДанныеДвиженийНДСНеоблагаемыеОперации(СписанныеПартииНДС, РеализованныеТоварыУслуги, Реквизиты);
	СформироватьДвиженияНДСНеоблагаемыеОперации(ДанныеДвижений, Движения, Отказ);
	ОперацииКПодтверждению = ДанныеДвижений.Скопировать(Новый Структура("КодВключаетсяВРеестр", Истина));
	ОперацииКПодтверждению.Свернуть("Организация, КодОперации, Контрагент, ДокументРеализации, Период, КодВключаетсяВРеестр");
	
	// Движения по ДокументыПоНеоблагаемымНДСОперациям
	ДанныеДвижений = ПодготовитьДанныеДвиженийДокументыПоНеоблагаемымНДСОперациям(ОперацииКПодтверждению, Реквизиты);
	СформироватьДвиженияДокументыПоНеоблагаемымНДСОперациям(ДанныеДвижений, Движения, Отказ)
	
КонецПроцедуры

Функция ПодготовитьДанныеДвиженийНДСНеоблагаемыеОперации(СписанныеПартииНДС, РеализованныеТоварыУслуги, Реквизиты)
	
	ТаблицаДвижений = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСНеоблагаемыеОперации");
	
	// Добавим колонку, чтобы заполнять подтверждающие документы только для попадающих в реестр операциях.
	ТаблицаДвижений.Колонки.Добавить("КодВключаетсяВРеестр", Новый ОписаниеТипов("Булево"));
	
	Если ЗначениеЗаполнено(Реквизиты.КодОперацииПоСделке) Тогда
		РеквизитыКода = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Реквизиты.КодОперацииПоСделке, "ВключаетсяВРеестрПодтверждающихДокументов, ОперацияНеПодлежитНалогообложению");
	КонецЕсли;
	
	КолонкиГруппировок = ""
	+ "Номенклатура,"
	+ "СтавкаНДС,"
	+ "КодРаздел7ДекларацииНДС,"
	+ "КодВключаетсяВРеестр,"
	+ "КодСоответствуетСт149НКРФ";
	
	Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ОказаниеУслуг") Тогда
		КолонкиГруппировок = КолонкиГруппировок + ",Контрагент";
	ИначеЕсли ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда 
		КолонкиГруппировок = КолонкиГруппировок + ",Покупатель";
		// Таблица для распределения сумм приобретения по покупателям.
		ПокупателиНоменклатуры = Новый ТаблицаЗначений;
		ПокупателиНоменклатуры.Колонки.Добавить("Покупатель", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
		ПокупателиНоменклатуры.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ПокупателиНоменклатуры.Колонки.Добавить("СуммаНДСНеПодлежащаяВычету", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
		ПокупателиНоменклатуры.Колонки.Добавить("СуммаПриобретенияБезНДС", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
		ПокупателиНоменклатуры.Индексы.Добавить("Покупатель, Номенклатура");
	КонецЕсли;
		
	КолонкиСуммирования = "СуммаБезНДСРуб";
	
	ТаблицаРеализации = РеализованныеТоварыУслуги.Скопировать(, КолонкиГруппировок + "," + КолонкиСуммирования);
	ТаблицаРеализации.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
	ПравилаЗаполненияДекларацииС4кв2020 = УчетНДС.ПравилаЗаполненияДекларацииС4кв2020(Реквизиты.Период);
	
	КодыРеализацийНеНаТерриторииРФ = Справочники.КодыОперацийРаздела7ДекларацииПоНДС.КодыРеализацииНеНаТерриторииРФ();
	Для Каждого СтрокаТаблицы Из ТаблицаРеализации Цикл
		// Код операции может быть указан в договоре или в номенклатуре
		Если ЗначениеЗаполнено(Реквизиты.КодОперацииПоСделке)
			ИЛИ ЗначениеЗаполнено(СтрокаТаблицы.КодРаздел7ДекларацииНДС) Тогда
			
			Если НЕ ОперацияПопадаетВ7РазделДекларацииНДС(СтрокаТаблицы) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаДвижений.Добавить();
			НоваяСтрока.Период       = Реквизиты.Период;
			НоваяСтрока.Организация  = Реквизиты.Организация;
			
			Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ОказаниеУслуг") Тогда // Контрагент в таблице
				НоваяСтрока.Контрагент   = СтрокаТаблицы.Контрагент;
			ИначеЕсли ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда // Контрагент-Покупатель в таблице
				НоваяСтрока.Контрагент   = СтрокаТаблицы.Покупатель;
			Иначе
				НоваяСтрока.Контрагент   = Реквизиты.Контрагент;
			КонецЕсли;
			
			НоваяСтрока.ДокументРеализации = Реквизиты.Регистратор;
			Если ЗначениеЗаполнено(Реквизиты.КодОперацииПоСделке) Тогда 
				КодОперации               = Реквизиты.КодОперацииПоСделке;
				КодВключаетсяВРеестр      = РеквизитыКода.ВключаетсяВРеестрПодтверждающихДокументов;
				КодСоответствуетСт149НКРФ = РеквизитыКода.ОперацияНеПодлежитНалогообложению;
				РеализацияНеНаТерриторииРФ = КодыРеализацийНеНаТерриторииРФ.Найти(Реквизиты.КодОперацииПоСделке) <> Неопределено;
			Иначе
				КодОперации               = СтрокаТаблицы.КодРаздел7ДекларацииНДС;
				КодВключаетсяВРеестр      = СтрокаТаблицы.КодВключаетсяВРеестр;
				КодСоответствуетСт149НКРФ = СтрокаТаблицы.КодСоответствуетСт149НКРФ;
				РеализацияНеНаТерриторииРФ = КодыРеализацийНеНаТерриторииРФ.Найти(СтрокаТаблицы.КодРаздел7ДекларацииНДС) <> Неопределено;
			КонецЕсли;
			
			НоваяСтрока.КодОперации = КодОперации;
			НоваяСтрока.СуммаРеализацииБезНДС = СтрокаТаблицы.СуммаБезНДСРуб;
			НоваяСтрока.КодВключаетсяВРеестр  = КодВключаетсяВРеестр;
			
			// СуммаПриобретенияБезНДСприобретения и СуммаНДСНеПодлежащаяВычету,
			// предназначенные для заполнения 3 и 4 граф 7 раздела декларации НДС заполняются:
			// с 01.10.2020 года по всем операциям 7 раздела декларации, кроме операций реализации не на территории РФ,
			// до 01.10.2020 года по операциям, соответствующим ст. 149 НК РФ
			Если ПравилаЗаполненияДекларацииС4кв2020 Тогда
				Если РеализацияНеНаТерриторииРФ Тогда
					Продолжить;
				КонецЕсли;
			Иначе // до 01.10.2020 года
				Если НЕ КодСоответствуетСт149НКРФ Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			// Для случая когда код операции указан в договоре считаем, что весь списанный НДС и вся стоимость списанных ценностей, 
			// приобретенных по ставке "Без НДС", относятся к льготной операции.
			Если ЗначениеЗаполнено(Реквизиты.КодОперацииПоСделке) Тогда 
				
				СписанныеТовары = СписанныеПартииНДС.Скопировать(
					Новый Структура("Номенклатура", СтрокаТаблицы.Номенклатура));
				НоваяСтрока.СуммаНДСНеПодлежащаяВычету = СписанныеТовары.Итог("НДС");
				
				СписанныеТоварыНеоблагаемыеНДС      = СписанныеТовары.Скопировать(
					Новый Структура("СтавкаНДС", Перечисления.СтавкиНДС.БезНДС));
				НоваяСтрока.СуммаПриобретенияБезНДС = СписанныеТоварыНеоблагаемыеНДС.Итог("СуммаБезНДС");
				
			Иначе
				
				Если ТипЗнч(Реквизиты.Регистратор) <> Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
					
					СписанныеТоварыПоКоду = СписанныеПартииНДС.Скопировать(
						Новый Структура("Номенклатура, КодРаздел7ДекларацииНДС", 
						СтрокаТаблицы.Номенклатура,СтрокаТаблицы.КодРаздел7ДекларацииНДС));
						
					НоваяСтрока.СуммаНДСНеПодлежащаяВычету  = СписанныеТоварыПоКоду.Итог("НДС");
					
					СписанныеТоварыНеоблагаемыеНДС      = СписанныеТоварыПоКоду.Скопировать(
						Новый Структура("СтавкаНДС", Перечисления.СтавкиНДС.БезНДС));
					НоваяСтрока.СуммаПриобретенияБезНДС = СписанныеТоварыНеоблагаемыеНДС.Итог("СуммаБезНДС");
				Иначе
					// Для документа "Отчет комиссионера о продажах" может дополнительно потребоваться распределить суммы приобретения.
					ЗаполнитьНеоблагаемыеСуммыПриобретенияПоОтчетуКомиссионера(
						НоваяСтрока, ПокупателиНоменклатуры, СтрокаТаблицы, ТаблицаРеализации, СписанныеПартииНДС);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаДвижений.Свернуть("Организация, КодОперации, Контрагент, ДокументРеализации, Период, КодВключаетсяВРеестр", 
		"СуммаРеализацииБезНДС,СуммаПриобретенияБезНДС,СуммаНДСНеПодлежащаяВычету");
		
	Возврат ТаблицаДвижений;
	
КонецФункции

Процедура ЗаполнитьНеоблагаемыеСуммыПриобретенияПоОтчетуКомиссионера(НоваяСтрока, ПокупателиНоменклатуры, СтрокаТаблицы, ТаблицаРеализации, СписанныеПартииНДС)
	
	Отбор = Новый Структура("Номенклатура, Покупатель", СтрокаТаблицы.Номенклатура, СтрокаТаблицы.Покупатель);
	НайденыеСтроки = ПокупателиНоменклатуры.НайтиСтроки(Отбор);
	
	Если НайденыеСтроки.Количество() > 0 Тогда 
		НоваяСтрока.СуммаНДСНеПодлежащаяВычету = НайденыеСтроки[0].СуммаНДСНеПодлежащаяВычету;
		НоваяСтрока.СуммаПриобретенияБезНДС = НайденыеСтроки[0].СуммаПриобретенияБезНДС;
	Иначе
		СписанныеТоварыПоКоду = СписанныеПартииНДС.Скопировать(
			Новый Структура("Номенклатура, КодРаздел7ДекларацииНДС", 
			СтрокаТаблицы.Номенклатура,СтрокаТаблицы.КодРаздел7ДекларацииНДС));
		ПокупателиНоменклатурыПредварительная = ТаблицаРеализации.Скопировать(
			Новый Структура("Номенклатура", СтрокаТаблицы.Номенклатура), "Номенклатура, Покупатель, СуммаБезНДСРуб");
		КоличествоСтрок = ПокупателиНоменклатурыПредварительная.Количество();
		Если КоличествоСтрок > 1 Тогда
			// Значит одна позиция номенклатуры реализуется нескольким покупателям.
			// Необходимо распределить суммы по приобретению пропорционально суммам реализации.
			
			СуммаНДСКРаспределению = СписанныеТоварыПоКоду.Итог("НДС");
			Если СуммаНДСКРаспределению > 0 Тогда 
				МассивСуммНДС = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
					СуммаНДСКРаспределению, ПокупателиНоменклатурыПредварительная.ВыгрузитьКолонку("СуммаБезНДСРуб"));
			КонецЕсли;
			
			СписанныеТоварыНеоблагаемыеНДС      = СписанныеТоварыПоКоду.Скопировать(
				Новый Структура("СтавкаНДС", Перечисления.СтавкиНДС.БезНДС));
			СуммаПриобретенияКРаспределению = СписанныеТоварыНеоблагаемыеНДС.Итог("СуммаБезНДС");
			
			Если СуммаПриобретенияКРаспределению > 0 Тогда 
				МассивСуммПриобретения = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
					СуммаПриобретенияКРаспределению, ПокупателиНоменклатурыПредварительная.ВыгрузитьКолонку("СуммаБезНДСРуб"));
			КонецЕсли;
			
			Для Сч = 0 По КоличествоСтрок - 1 Цикл
				
				СтрокаПокупатели = ПокупателиНоменклатуры.Добавить();
				СтрокаПокупатели.Покупатель = ПокупателиНоменклатурыПредварительная[Сч].Покупатель;
				СтрокаПокупатели.Номенклатура = ПокупателиНоменклатурыПредварительная[Сч].Номенклатура;
				
				Если МассивСуммНДС <> Неопределено Тогда 
					СтрокаПокупатели.СуммаНДСНеПодлежащаяВычету = МассивСуммНДС[Сч];
				Иначе
					СтрокаПокупатели.СуммаНДСНеПодлежащаяВычету = 0;
				КонецЕсли;
				
				Если МассивСуммПриобретения <> Неопределено Тогда 
					СтрокаПокупатели.СуммаПриобретенияБезНДС = МассивСуммПриобретения[Сч];
				Иначе
					СтрокаПокупатели.СуммаПриобретенияБезНДС = 0;
				КонецЕсли;
				
			КонецЦикла;
			
			НайденыеСтроки = ПокупателиНоменклатуры.НайтиСтроки(Отбор);
			
			НоваяСтрока.СуммаНДСНеПодлежащаяВычету = НайденыеСтроки[0].СуммаНДСНеПодлежащаяВычету;
			НоваяСтрока.СуммаПриобретенияБезНДС = НайденыеСтроки[0].СуммаПриобретенияБезНДС;
			
		Иначе
			
			НоваяСтрока.СуммаНДСНеПодлежащаяВычету  = СписанныеТоварыПоКоду.Итог("НДС");
			
			СписанныеТоварыНеоблагаемыеНДС      = СписанныеТоварыПоКоду.Скопировать(
				Новый Структура("СтавкаНДС", Перечисления.СтавкиНДС.БезНДС));
			НоваяСтрока.СуммаПриобретенияБезНДС = СписанныеТоварыНеоблагаемыеНДС.Итог("СуммаБезНДС");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ОперацияПопадаетВ7РазделДекларацииНДС(Операция)
	
	Если Операция.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура СформироватьДвиженияНДСНеоблагаемыеОперации(ДанныеДвижений, Движения, Отказ)
	
	Если Не ЗначениеЗаполнено(ДанныеДвижений) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ДанныеДвижений Цикл
		СуммыНеоблагаемыхОпераций = Новый Структура(
			"СуммаРеализацииБезНДС, СуммаПриобретенияБезНДС, СуммаНДСНеПодлежащаяВычету", 0,0,0);
		ЗаполнитьЗначенияСвойств(СуммыНеоблагаемыхОпераций, СтрокаТаблицы);
		Если СуммыНеоблагаемыхОпераций.СуммаРеализацииБезНДС <> 0
			ИЛИ СуммыНеоблагаемыхОпераций.СуммаПриобретенияБезНДС <> 0
			ИЛИ СуммыНеоблагаемыхОпераций.СуммаНДСНеПодлежащаяВычету <> 0 Тогда 
			Запись = Движения.НДСНеоблагаемыеОперации.ДобавитьПриход();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Движения.НДСНеоблагаемыеОперации.Записывать = Истина;
	
КонецПроцедуры

Процедура СформироватьДвиженияДокументыПоНеоблагаемымНДСОперациям(ДанныеДвижений, Движения, Отказ)
	
	Если Не ЗначениеЗаполнено(ДанныеДвижений) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ДанныеДвижений Цикл
		
		Запись = Движения.ДокументыПоНеоблагаемымНДСОперациям.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		
	КонецЦикла;
	
	Движения.ДокументыПоНеоблагаемымНДСОперациям.Записывать = Истина;
	
КонецПроцедуры

Функция ПодготовитьДанныеДвиженийДокументыПоНеоблагаемымНДСОперациям(ОперацииКПодтверждению, Реквизиты)
	
	Если ОперацииКПодтверждению.Количество() = 0 Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	ПодтверждающиеДокументы = ПодтверждающиеДокументыПоРегистратору(ОперацииКПодтверждению, Реквизиты.Регистратор);
	
	Возврат ПодтверждающиеДокументы;
	
КонецФункции

// Функция возвращает таблицу подтверждающих документов по регистратору.
// Каждому документу-регистратору соответствует перечень подтверждающих документов, 
// в большинстве случаев это договор и Акт/Накладная. 
// Если подключен ЭДО, то подтверждающий документ определяется по типу электронного документа.
// Если ЭДО не подключен или не поддерживается для документа и по документу возможно несколько типов
// подтверждающих документов, то тип определяется следующим образом:
//   Акт, если в документе есть услуги;
//   Накладная, если услуг в документе нет.
// 
Функция ПодтверждающиеДокументыПоРегистратору(ОперацииКПодтверждению, ДокументРегистратор)
	
	ТаблицаДвижений = НовыйТаблицаДокументыПоНеоблагаемымНДСОперациям();
	
	Запрос = Новый Запрос;
	ДокументИсточник = ДокументРегистратор;
	ОпределятьТипДокументаПоТЧ = Ложь;
	ЭтоУниверсальныйДокумент = Ложь;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ЭтоУниверсальныйДокумент", ДокументРегистратор.Метаданные()) Тогда
		ЭтоУниверсальныйДокумент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ДокументРегистратор, "ЭтоУниверсальныйДокумент");
	КонецЕсли;
		
	Если ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда 
		
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРегистратор, 
			"Организация, Контрагент, ДоговорКонтрагента, ВидЭлектронногоДокумента");
		
		Если ОбменСКонтрагентамиСлужебныйВызовСервера.НастройкаЭДСуществует(ДокументРегистратор)
			И ЗначениеЗаполнено(РеквизитыДокумента.ВидЭлектронногоДокумента) Тогда
			
			ИспользуетсяУПД = ОбменСКонтрагентами.ИспользованиеУниверсальногоПередаточногоДокумента(
				РеквизитыДокумента.Организация, РеквизитыДокумента.Контрагент, РеквизитыДокумента.ДоговорКонтрагента);
				
			Если ИспользуетсяУПД Тогда 
				ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.УПД;
			Иначе
				ТипПодтверждающегоДокумента = 
					Справочники.ТипыДокументовПодтверждающихЛьготуНДС.ТипПодтверждающегоДокументаПоВидуЭД(
					РеквизитыДокумента.ВидЭлектронногоДокумента);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ТипПодтверждающегоДокумента) Тогда
				ОпределятьТипДокументаПоТЧ = Истина;
			КонецЕсли;
		Иначе
			ОпределятьТипДокументаПоТЧ = Истина;
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапросаРеализацияТоваровУслуг(ОпределятьТипДокументаПоТЧ);

	ИначеЕсли ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.ПередачаТоваров") Тогда 
		
		ОпределятьТипДокументаПоТЧ = Истина;
		ТекстЗапроса = ТекстЗапросаПередачаТоваров();
		
		
	ИначеЕсли ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг") Тогда 
		
		ТекстЗапроса = ТекстЗапросаАктОбОказанииПроизводственныхУслуг();
		ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Акт;
		
	ИначеЕсли ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда 
		
		ТекстЗапроса = ТекстЗапросаОтчетКомиссионераОПродажах();
		ОпределятьТипДокументаПоТЧ = Истина;
		
	ИначеЕсли ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда 
		
		ТекстЗапроса = ТекстЗапросаОтчетОРозничныхПродажах();
		ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.СправкаРасчет;
		
	ИначеЕсли ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.РеализацияУслугПоПереработке") Тогда 
		
		ТекстЗапроса = ТекстЗапросаРеализацияУслугПоПереработке();
		ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Акт;
		
	ИначеЕсли ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.ПередачаНМА") Тогда 
		
		ТекстЗапроса = ТекстЗапросаПередачаНМА();
		ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Акт;
		
	ИначеЕсли ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		ТекстЗапроса = ТекстЗапросаВозвратТоваровПоставщику();
		ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Накладная;
		
	ИначеЕсли ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.ОказаниеУслуг") Тогда 
		
		ТекстЗапроса = ТекстЗапросаОказаниеУслуг();
		ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Акт;
		
	ИначеЕсли ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.ПередачаОС") Тогда 
		
		ТекстЗапроса = ТекстЗапросаПередачаОС();
		ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Акт;
		
	ИначеЕсли ТипЗнч(ДокументРегистратор) = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
		
		ДокументИсточник = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРегистратор, "ДокументОтгрузки");
		
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументИсточник, 
			"Организация, Контрагент, ДоговорКонтрагента, ВидЭлектронногоДокумента");
		
		Если ОбменСКонтрагентамиСлужебныйВызовСервера.НастройкаЭДСуществует(ДокументИсточник)
			И ЗначениеЗаполнено(РеквизитыДокумента.ВидЭлектронногоДокумента) Тогда
			
			ИспользуетсяУПД = ОбменСКонтрагентами.ИспользованиеУниверсальногоПередаточногоДокумента(
				РеквизитыДокумента.Организация, РеквизитыДокумента.Контрагент, РеквизитыДокумента.ДоговорКонтрагента);
			
			Если ИспользуетсяУПД Тогда 
				ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.УПД;
			Иначе
				ТипПодтверждающегоДокумента = 
					Справочники.ТипыДокументовПодтверждающихЛьготуНДС.ТипПодтверждающегоДокументаПоВидуЭД(
					РеквизитыДокумента.ВидЭлектронногоДокумента);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ТипПодтверждающегоДокумента) Тогда
				ОпределятьТипДокументаПоТЧ = Истина;
			КонецЕсли;
		Иначе
			ОпределятьТипДокументаПоТЧ = Истина;
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапросаРеализацияОтгруженныхТоваров(ОпределятьТипДокументаПоТЧ);
		
	Иначе
		Возврат ТаблицаДвижений;
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументИсточник);
	Если ОпределятьТипДокументаПоТЧ Тогда
		Результат = Запрос.ВыполнитьПакет();
		ВыборкаСтрокиДокумента = Результат[1].Выбрать();
		ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.ПустаяСсылка();
		Если ВыборкаСтрокиДокумента.Следующий() Тогда
			Если ЭтоУниверсальныйДокумент Тогда
				ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.УПД;
			ИначеЕсли ВыборкаСтрокиДокумента.УслугиКоличество > 0 Тогда 
				ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Акт;
			Иначе
				ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Накладная
			КонецЕсли;
		КонецЕсли;
		ВыборкаДокументы = Результат[2].Выбрать();
	Иначе
		Результат = Запрос.Выполнить();
		ВыборкаДокументы = Результат.Выбрать();
		Если ЭтоУниверсальныйДокумент Тогда
			ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.УПД;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого Операция Из ОперацииКПодтверждению Цикл
		
		Пока ВыборкаДокументы.Следующий() Цикл
			
			// Для документов "Оказание услуг" будет несколько Договоров и Актов по разным контрагентам.
			// Для документа "Отчет комиссионера о продажах" подтверждающий документ один для всех контрагентов (Акт/Накладная).
			// Документы с пустым номером не записываем.
			Если (ТипЗнч(ВыборкаДокументы.ПодтверждающийДокументИсточник) = Тип("СправочникСсылка.ДоговорыКонтрагентов")
				ИЛИ ТипЗнч(ВыборкаДокументы.ПодтверждающийДокументИсточник) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")
				ИЛИ ТипЗнч(ВыборкаДокументы.ПодтверждающийДокументИсточник) = Тип("ДокументСсылка.ОказаниеУслуг"))
				И ВыборкаДокументы.Контрагент <> Операция.Контрагент 
				ИЛИ НЕ ЗначениеЗаполнено(ВыборкаДокументы.Номер) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаДвижений.Добавить();
			НоваяСтрока.Организация = Операция.Организация;
			НоваяСтрока.КодОперации = Операция.КодОперации;
			НоваяСтрока.Контрагент  = Операция.Контрагент;
			НоваяСтрока.ДокументРеализации = Операция.ДокументРеализации;
			
			Если ТипЗнч(ВыборкаДокументы.ПодтверждающийДокументИсточник) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда 
				НоваяСтрока.ТипПодтверждающегоДокумента = Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Договор;
			Иначе
				НоваяСтрока.ТипПодтверждающегоДокумента = ТипПодтверждающегоДокумента;
			КонецЕсли;
			
			Если ТипЗнч(ВыборкаДокументы.ПодтверждающийДокументИсточник) = Тип("ДокументСсылка.ОказаниеУслуг") Тогда
				НоваяСтрока.НомерДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1/%2'"),ВыборкаДокументы.Номер, ВыборкаДокументы.НомерСтроки);
			Иначе
				НоваяСтрока.НомерДокумента = ВыборкаДокументы.Номер;
			КонецЕсли;
			
			НоваяСтрока.ДатаДокумента  = ВыборкаДокументы.Дата;
			
		КонецЦикла;
		
		ВыборкаДокументы.Сбросить();
		
	КонецЦикла;
	
	Возврат ТаблицаДвижений;
	
КонецФункции

Функция ТекстЗапросаАктОбОказанииПроизводственныхУслуг()
	
	Возврат
	"ВЫБРАТЬ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка КАК ПодтверждающийДокументИсточник,
	|	АктОбОказанииПроизводственныхУслуг.Контрагент КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг КАК АктОбОказанииПроизводственныхУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = АктОбОказанииПроизводственныхУслуг.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = АктОбОказанииПроизводственныхУслуг.Организация)
	|ГДЕ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка,
	|	АктОбОказанииПроизводственныхУслуг.Контрагент,
	|	ДоговорыКонтрагентов.Дата,
	|	ДоговорыКонтрагентов.Номер
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг КАК АктОбОказанииПроизводственныхУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО АктОбОказанииПроизводственныхУслуг.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка = &Ссылка";
		
КонецФункции

Функция ТекстЗапросаОтчетКомиссионераОПродажах()
	
	Возврат
	"ВЫБРАТЬ
	|	1 КАК УслугиКоличество,
	|	0 КАК ТоварыКоличество
	|ПОМЕСТИТЬ СтрокиДокументов
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Услуги КАК ОтчетКомиссионераОПродажахУслуги
	|ГДЕ
	|	ОтчетКомиссионераОПродажахУслуги.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	1
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Товары КАК ОтчетКомиссионераОПродажахТовары
	|ГДЕ
	|	ОтчетКомиссионераОПродажахТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(СтрокиДокументов.ТоварыКоличество) КАК ТоварыКоличество,
	|	СУММА(СтрокиДокументов.УслугиКоличество) КАК УслугиКоличество
	|ИЗ
	|	СтрокиДокументов КАК СтрокиДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажах.Ссылка КАК ПодтверждающийДокументИсточник,
	|	ОтчетКомиссионераОПродажах.Покупатель КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажах
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = ОтчетКомиссионераОПродажах.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = ОтчетКомиссионераОПродажах.Ссылка.Организация)
	|ГДЕ
	|	ОтчетКомиссионераОПродажах.Ссылка = &Ссылка";
	
КонецФункции

Функция ТекстЗапросаОтчетОРозничныхПродажах()
	
	Возврат
	"ВЫБРАТЬ
	|	ОтчетОРозничныхПродажах.Ссылка КАК ПодтверждающийДокументИсточник,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = ОтчетОРозничныхПродажах.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = ОтчетОРозничныхПродажах.Организация)
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка = &Ссылка";
	
КонецФункции

Функция ТекстЗапросаРеализацияУслугПоПереработке()
	
	Возврат
	"ВЫБРАТЬ
	|	РеализацияУслугПоПереработке.Ссылка КАК ПодтверждающийДокументИсточник,
	|	РеализацияУслугПоПереработке.Контрагент КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер
	|ИЗ
	|	Документ.РеализацияУслугПоПереработке КАК РеализацияУслугПоПереработке
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = РеализацияУслугПоПереработке.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = РеализацияУслугПоПереработке.Организация)
	|ГДЕ
	|	РеализацияУслугПоПереработке.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка,
	|	РеализацияУслугПоПереработке.Контрагент,
	|	ДоговорыКонтрагентов.Дата,
	|	ДоговорыКонтрагентов.Номер
	|ИЗ
	|	Документ.РеализацияУслугПоПереработке КАК РеализацияУслугПоПереработке
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО РеализацияУслугПоПереработке.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	РеализацияУслугПоПереработке.Ссылка = &Ссылка";
	
КонецФункции

Функция ТекстЗапросаПередачаНМА()
	
	Возврат
	"ВЫБРАТЬ
	|	ПередачаНМА.Ссылка КАК ПодтверждающийДокументИсточник,
	|	ПередачаНМА.Контрагент КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер
	|ИЗ
	|	Документ.ПередачаНМА КАК ПередачаНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = ПередачаНМА.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = ПередачаНМА.Организация)
	|ГДЕ
	|	ПередачаНМА.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка,
	|	ПередачаНМА.Контрагент,
	|	ДоговорыКонтрагентов.Дата,
	|	ДоговорыКонтрагентов.Номер
	|ИЗ
	|	Документ.ПередачаНМА КАК ПередачаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ПередачаНМА.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ПередачаНМА.Ссылка = &Ссылка";
	
КонецФункции

Функция ТекстЗапросаВозвратТоваровПоставщику()
	
	Возврат
	"ВЫБРАТЬ
	|	ВозвратТоваровПоставщику.Ссылка КАК ПодтверждающийДокументИсточник,
	|	ВозвратТоваровПоставщику.Контрагент КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = ВозвратТоваровПоставщику.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = ВозвратТоваровПоставщику.Организация)
	|ГДЕ
	|	ВозвратТоваровПоставщику.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка,
	|	ВозвратТоваровПоставщику.Контрагент,
	|	ДоговорыКонтрагентов.Дата,
	|	ДоговорыКонтрагентов.Номер
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ВозвратТоваровПоставщику.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ВозвратТоваровПоставщику.Ссылка = &Ссылка";
	
КонецФункции

Функция ТекстЗапросаОказаниеУслуг()
	
	Возврат
	"ВЫБРАТЬ
	|	ОказаниеУслугАкты.Ссылка КАК ПодтверждающийДокументИсточник,
	|	ОказаниеУслугАкты.Контрагент КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер,
	|	ОказаниеУслугАкты.НомерСтроки
	|ИЗ
	|	Документ.ОказаниеУслуг.Контрагенты КАК ОказаниеУслугАкты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = ОказаниеУслугАкты.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = ОказаниеУслугАкты.Ссылка.Организация)
	|ГДЕ
	|	ОказаниеУслугАкты.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка,
	|	ОказаниеУслугДоговоры.Контрагент,
	|	ДоговорыКонтрагентов.Дата,
	|	ДоговорыКонтрагентов.Номер,
	|	""""
	|ИЗ
	|	Документ.ОказаниеУслуг.Контрагенты КАК ОказаниеУслугДоговоры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ОказаниеУслугДоговоры.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ОказаниеУслугДоговоры.Ссылка = &Ссылка";
	
КонецФункции

Функция ТекстЗапросаПередачаОС()
	
	Возврат
	"ВЫБРАТЬ
	|	ПередачаОС.Ссылка КАК ПодтверждающийДокументИсточник,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер
	|ИЗ
	|	Документ.ПередачаОС КАК ПередачаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = ПередачаОС.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = ПередачаОС.Организация)
	|ГДЕ
	|	ПередачаОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка,
	|	ПередачаОС.Контрагент,
	|	ДоговорыКонтрагентов.Дата,
	|	ДоговорыКонтрагентов.Номер
	|ИЗ
	|	Документ.ПередачаОС КАК ПередачаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ПередачаОС.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ПередачаОС.Ссылка = &Ссылка";
	
КонецФункции

Функция ТекстЗапросаРеализацияОтгруженныхТоваров(ОпределятьТипДокументаПоТЧ)
	
	
	ТекстЗапроса = "";
	Если ОпределятьТипДокументаПоТЧ Тогда 
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	1 КАК УслугиКоличество,
		|	0 КАК ТоварыКоличество
		|ПОМЕСТИТЬ СтрокиДокументов
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
		|ГДЕ
		|	РеализацияТоваровУслугУслуги.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	1
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|ГДЕ
		|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	1,
		|	0
		|ИЗ
		|	Документ.ПередачаОС.ОС КАК ПередачаОСОС
		|ГДЕ
		|	ПередачаОСОС.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(СтрокиДокументов.ТоварыКоличество) КАК ТоварыКоличество,
		|	СУММА(СтрокиДокументов.УслугиКоличество) КАК УслугиКоличество
		|ИЗ
		|	СтрокиДокументов КАК СтрокиДокументов";
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК ПодтверждающийДокументИсточник,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = РеализацияТоваровУслуг.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = РеализацияТоваровУслуг.Организация)
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка,
	|	РеализацияТоваровУслуг.Контрагент,
	|	ДоговорыКонтрагентов.Дата,
	|	ДоговорыКонтрагентов.Номер
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО РеализацияТоваровУслуг.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПередачаОС.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер
	|ИЗ
	|	Документ.ПередачаОС КАК ПередачаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = ПередачаОС.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = ПередачаОС.Организация)
	|ГДЕ
	|	ПередачаОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка,
	|	ПередачаОС.Контрагент,
	|	ДоговорыКонтрагентов.Дата,
	|	ДоговорыКонтрагентов.Номер
	|ИЗ
	|	Документ.ПередачаОС КАК ПередачаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ПередачаОС.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ПередачаОС.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРеализацияТоваровУслуг(ОпределятьТипДокументаПоТЧ)
	
	ТекстЗапроса = "";
	Если ОпределятьТипДокументаПоТЧ Тогда 
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	1 КАК УслугиКоличество,
		|	0 КАК ТоварыКоличество
		|ПОМЕСТИТЬ СтрокиДокументов
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
		|ГДЕ
		|	РеализацияТоваровУслугУслуги.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	1
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|ГДЕ
		|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(СтрокиДокументов.ТоварыКоличество) КАК ТоварыКоличество,
		|	СУММА(СтрокиДокументов.УслугиКоличество) КАК УслугиКоличество
		|ИЗ
		|	СтрокиДокументов КАК СтрокиДокументов";
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК ПодтверждающийДокументИсточник,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = РеализацияТоваровУслуг.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = РеализацияТоваровУслуг.Организация)
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка,
	|	РеализацияТоваровУслуг.Контрагент,
	|	ДоговорыКонтрагентов.Дата,
	|	ДоговорыКонтрагентов.Номер
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО РеализацияТоваровУслуг.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПередачаТоваров()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	0 КАК УслугиКоличество,
	|	1 КАК ТоварыКоличество
	|ПОМЕСТИТЬ СтрокиДокументов
	|ИЗ
	|	Документ.ПередачаТоваров.Товары КАК ПередачаТоваровТовары
	|ГДЕ
	|	ПередачаТоваровТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(СтрокиДокументов.ТоварыКоличество) КАК ТоварыКоличество,
	|	СУММА(СтрокиДокументов.УслугиКоличество) КАК УслугиКоличество
	|ИЗ
	|	СтрокиДокументов КАК СтрокиДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПередачаТоваров.Ссылка КАК ПодтверждающийДокументИсточник,
	|	ПередачаТоваров.Контрагент КАК Контрагент,
	|	ДанныеПервичныхДокументов.Дата КАК Дата,
	|	ДанныеПервичныхДокументов.Номер КАК Номер
	|ИЗ
	|	Документ.ПередачаТоваров КАК ПередачаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДанныеПервичныхДокументов.Документ = ПередачаТоваров.Ссылка)
	|			И (ДанныеПервичныхДокументов.Организация = ПередачаТоваров.Организация)
	|ГДЕ
	|	ПередачаТоваров.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка,
	|	ПередачаТоваров.Контрагент,
	|	ДоговорыКонтрагентов.Дата,
	|	ДоговорыКонтрагентов.Номер
	|ИЗ
	|	Документ.ПередачаТоваров КАК ПередачаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ПередачаТоваров.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ПередачаТоваров.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПодготовкаТаблицыНДСПоСписаннымЦенностям

Функция ПолучитьТаблицуСписанныеПартииНДС(Товары, СписанныеТоварыБухУчет, Реквизиты, Отказ) Экспорт

	СписанныеПартииНДС = ПустаяТаблицаСписанныеПартииНДС();
	
	Если Товары.Количество() = 0 Тогда
		Возврат СписанныеПартииНДС;
	КонецЕсли;
	
	ПодготовитьТаблицуТовары(Товары);
	ПодготовитьТаблицуСписанныеТоварыБухУчет(СписанныеТоварыБухУчет, Реквизиты);
	ОтсутствуетКоличественныйУчет = СписанныеТоварыБухУчет.Итог("КоличественныйУчет") = 0;
	
	ТаблицыОстатковПартийНДС = ПолучитьОстаткиПартийНДС(СписанныеТоварыБухУчет, Реквизиты);
	
	КонтролироватьОстаток = НЕ БухгалтерскийУчетПереопределяемый.ОтключитьКонтрольОтрицательныхОстатков();
	СчетаИсключенияДляКонтроляОстатков = Новый Массив;
	СчетаИсключенияДляКонтроляОстатков.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);                                      // 21
	СчетаИсключенияДляКонтроляОстатков.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);                                   // 43
	СчетаИсключенияДляКонтроляОстатков.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукцияОтгруженная);                        //45.02
	СчетаИсключенияДляКонтроляОстатков.Добавить(ПланыСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежностиВЭксплуатации); //МЦ.04
	// Определяем списанные партии товаров
	Для каждого СтрокаДокумента Из Товары Цикл

		ОтборПартийБухУчет = Новый Структура("ИмяСписка,НомерСтроки",
			СтрокаДокумента.ИмяСписка, СтрокаДокумента.НомерСтрокиДокумента);
		СписанныеТоварыПоСтрокеБухУчет = СписанныеТоварыБухУчет.НайтиСтроки(ОтборПартийБухУчет);

		Для каждого СписаннаяПартияБухУчет Из СписанныеТоварыПоСтрокеБухУчет Цикл
			
			Если ОтсутствуетКоличественныйУчет Тогда
				
				КоэффициентСписанияДопРасходов = 1 / Товары.Количество(); 
				
				ОтборДопРасходов = Новый Структура("АналитикаУчетаЗатрат", СписаннаяПартияБухУчет.АналитикаУчетаЗатрат);
				ОстаткиДопРасходов = ТаблицыОстатковПартийНДС.ДопРасходы.Скопировать(ОтборДопРасходов);
				ЗаполнитьПорядокСписанияПартийПоСпособуУчетаНДС(ОстаткиДопРасходов, СтрокаДокумента.НовыйСпособУчетаНДС);
				ОстаткиДопРасходов.Сортировать(
					"ПорядокСписанияПартийПоСпособуУчетаНДС,ДатаПартии,Партия,ДатаСФ,СчетФактура", Новый СравнениеЗначений);

				Для каждого СтрокаОстаткаДопРасхода Из ОстаткиДопРасходов Цикл

					СписаннаяСуммаБезНДС	= Окр(СтрокаОстаткаДопРасхода.СуммаБезНДС * КоэффициентСписанияДопРасходов, 2);
					СписанныйНДС			= Окр(СтрокаОстаткаДопРасхода.НДС * КоэффициентСписанияДопРасходов, 2);

					НоваяСтрока = СписанныеПартииНДС.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОстаткаДопРасхода);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СписаннаяПартияБухУчет.КорАналитикаУчетаЗатрат);
					НоваяСтрока.Покупатель = НоваяСтрока.Поставщик;
					
					Если НЕ СтрокаДокумента.НовыйСпособУчетаНДСОпределенПоСтавке Тогда
						НоваяСтрока.БлокироватьДоПодтвержденияСтавки0 = Истина;
					КонецЕсли;
					
					ЗаполнитьСпособУчетаНДСИзменился(НоваяСтрока);
					
					НоваяСтрока.Количество	= 0;
					НоваяСтрока.СуммаБезНДС	= СписаннаяСуммаБезНДС;
					НоваяСтрока.НДС			= СписанныйНДС;
					
				КонецЦикла;
				
				Продолжить;
				
			КонецЕсли;
			
			ОтборПартийНДС = Новый Структура("АналитикаУчетаЗатрат", СписаннаяПартияБухУчет.АналитикаУчетаЗатрат);

			ОстаткиПартийНДСПоСтроке = ТаблицыОстатковПартийНДС.Товары.Скопировать(ОтборПартийНДС);
			ЗаполнитьПорядокСписанияПартийПоСпособуУчетаНДС(ОстаткиПартийНДСПоСтроке, СтрокаДокумента.НовыйСпособУчетаНДС);
			ОстаткиПартийНДСПоСтроке.Сортировать(
				"ПорядокСписанияПартийПоСпособуУчетаНДС,ДатаПартии,Партия,ДатаСФ,СчетФактура", Новый СравнениеЗначений);

			ОстатокПоАналитикеУчетаЗатрат =  ОстаткиПартийНДСПоСтроке.Итог("Количество");
			
			КоличествоОсталосьПогасить = СписаннаяПартияБухУчет.Количество;
			
			Для каждого ОстатокПартии Из ОстаткиПартийНДСПоСтроке Цикл

				Если КоличествоОсталосьПогасить <= 0 Тогда
					Прервать;
				ИначеЕсли ОстатокПартии.Количество <= 0 Тогда
					Продолжить;
				КонецЕсли;

				Количество = Мин(ОстатокПартии.Количество, КоличествоОсталосьПогасить);

				СуммаБезНДС	= Окр(ОстатокПартии.СуммаБезНДС * Количество / ОстатокПартии.Количество, 2);
				НДС	= Окр(ОстатокПартии.НДС * Количество / ОстатокПартии.Количество, 2);

				КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - Количество;

				СтрокаТаблицыОстатков = ТаблицыОстатковПартийНДС.Товары.Найти(ОстатокПартии.НомерСтроки, "НомерСтроки");
				СтрокаТаблицыОстатков.Количество = СтрокаТаблицыОстатков.Количество - Количество;
				СтрокаТаблицыОстатков.СуммаБезНДС  = СтрокаТаблицыОстатков.СуммаБезНДС - СуммаБезНДС;
				СтрокаТаблицыОстатков.НДС        = СтрокаТаблицыОстатков.НДС - НДС;

				НоваяСтрока = СписанныеПартииНДС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ОстатокПартии);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СписаннаяПартияБухУчет.КорАналитикаУчетаЗатрат);
				НоваяСтрока.Покупатель = НоваяСтрока.Поставщик;

				Если НЕ СтрокаДокумента.НовыйСпособУчетаНДСОпределенПоСтавке Тогда
					НоваяСтрока.БлокироватьДоПодтвержденияСтавки0 = Истина;
				КонецЕсли;
				
				ЗаполнитьСпособУчетаНДСИзменился(НоваяСтрока);
				
				НоваяСтрока.Количество = Количество;
				НоваяСтрока.СуммаБезНДС  = СуммаБезНДС;
				НоваяСтрока.НДС = НДС;
				
				// Определяем списанные доп.расходы (пропорционально списанному количеству партий товаров)
				КоэффициентСписанияДопРасходов = Количество / ОстатокПоАналитикеУчетаЗатрат;

				ЗаполнитьПорядокСписанияПартийПоСпособуУчетаНДС(
					ТаблицыОстатковПартийНДС.ДопРасходы, СтрокаДокумента.НовыйСпособУчетаНДС);
				ТаблицыОстатковПартийНДС.ДопРасходы.Сортировать(
					"ПорядокСписанияПартийПоСпособуУчетаНДС,ДатаПартии,Партия", Новый СравнениеЗначений);
				
				ОтборДопРасходов = Новый Структура("АналитикаУчетаЗатрат", ОстатокПартии.АналитикаУчетаЗатрат);
				ОстаткиДопРасходов = ТаблицыОстатковПартийНДС.ДопРасходы.НайтиСтроки(ОтборДопРасходов);

				Для каждого СтрокаОстаткаДопРасхода Из ОстаткиДопРасходов Цикл

					СписаннаяСуммаБезНДС	= Окр(СтрокаОстаткаДопРасхода.СуммаБезНДС * КоэффициентСписанияДопРасходов, 2);
					СписанныйНДС			= Окр(СтрокаОстаткаДопРасхода.НДС * КоэффициентСписанияДопРасходов, 2);

					НоваяСтрока = СписанныеПартииНДС.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОстаткаДопРасхода);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СписаннаяПартияБухУчет.КорАналитикаУчетаЗатрат);
					НоваяСтрока.Покупатель = НоваяСтрока.Поставщик;

					Если НЕ СтрокаДокумента.НовыйСпособУчетаНДСОпределенПоСтавке Тогда
						НоваяСтрока.БлокироватьДоПодтвержденияСтавки0 = Истина;
					КонецЕсли;
					
					ЗаполнитьСпособУчетаНДСИзменился(НоваяСтрока);
					
					НоваяСтрока.Количество	= 0;
					НоваяСтрока.СуммаБезНДС	= СписаннаяСуммаБезНДС;
					НоваяСтрока.НДС			= СписанныйНДС;

					СтрокаОстаткаДопРасхода.СписаннаяСуммаБезНДС = СтрокаОстаткаДопРасхода.СписаннаяСуммаБезНДС + СписаннаяСуммаБезНДС;
					СтрокаОстаткаДопРасхода.СписанныйНДС         = СтрокаОстаткаДопРасхода.СписанныйНДС + СписанныйНДС;
					
				КонецЦикла;
				
			КонецЦикла;
			
			Если КоличествоОсталосьПогасить > 0 И КонтролироватьОстаток 
				И СчетаИсключенияДляКонтроляОстатков.Найти(СтрокаДокумента.СчетУчета) = Неопределено Тогда

				СпособОценкиМПЗПоСредней = (УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период)
					= Перечисления.СпособыОценки.ПоСредней);
				
				СкладИзБухУчета  = БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(СтрокаДокумента.СчетУчета);
				ПартияИзБухУчета = НЕ СпособОценкиМПЗПоСредней
					И БухгалтерскийУчет.НаСчетеВедетсяПартионныйУчет(СтрокаДокумента.СчетУчета);
				
				ТекстОшибки1 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Для целей учета НДС не списано %1 товара %2, счет учета: %3'"),
					Формат(КоличествоОсталосьПогасить, "ЧЦ=15; ЧДЦ=3"),
					СтрокаДокумента.Номенклатура,
					СтрокаДокумента.СчетУчета);
				Если СкладИзБухУчета Тогда
					ТекстОшибки2 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru=', склад: %1'"),
						СписаннаяПартияБухУчет.Склад);
				Иначе
					ТекстОшибки2 = "";
				КонецЕсли;
				Если ПартияИзБухУчета Тогда
					ТекстОшибки3 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru=', партия: %1'"),
						СписаннаяПартияБухУчет.Партия);
				Иначе
					ТекстОшибки3 = "";
				КонецЕсли;
				
				Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
					ПолеКоличество	= "ДокументОтгрузки";
				Иначе
					ПолеКоличество	= СтрокаДокумента.ИмяСписка + "[" + Формат(СтрокаДокумента.НомерСтрокиДокумента - 1, "ЧН=0; ЧГ=") + "].Количество";
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки1 + ТекстОшибки2 + ТекстОшибки3,
					Реквизиты.Регистратор, ПолеКоличество, "Объект", Отказ);
				
			КонецЕсли;

			Для каждого СтрокаОстаткаДопРасхода Из ТаблицыОстатковПартийНДС.ДопРасходы Цикл
				
				СтрокаОстаткаДопРасхода.СуммаБезНДС = СтрокаОстаткаДопРасхода.СуммаБезНДС - СтрокаОстаткаДопРасхода.СписаннаяСуммаБезНДС;
				СтрокаОстаткаДопРасхода.НДС         = СтрокаОстаткаДопРасхода.НДС - СтрокаОстаткаДопРасхода.СписанныйНДС;
				
				СтрокаОстаткаДопРасхода.СписаннаяСуммаБезНДС = 0;
				СтрокаОстаткаДопРасхода.СписанныйНДС         = 0;
				
			КонецЦикла;

		КонецЦикла;
		
	КонецЦикла;
	
	СписанныеПартииНДС.ЗаполнитьЗначения(Реквизиты.Период,      "Период,ДатаСобытия");
	СписанныеПартииНДС.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");
	СписанныеПартииНДС.ЗаполнитьЗначения(Реквизиты.Регистратор, "Регистратор");
	
	//Проверка предъявляет ли поставщик НДС
	ЗаполнитьЗначениеЕстьНДСПредъявленный(СписанныеПартииНДС);
	СписанныеПартииНДС.Индексы.Добавить("ЕстьНДСПредъявленный");
	
	//Проверка необходимости распределения НДС
	СписанныеПартииНДС.Индексы.Добавить("НовыйСпособУчетаНДС");
	
	СтрокиДляРаспределения = СписанныеПартииНДС.НайтиСтроки(Новый Структура(
		"НовыйСпособУчетаНДС,ЕстьНДСПредъявленный", Перечисления.СпособыУчетаНДС.Распределяется, Истина));
		
	ОпределитьСуммыДляРаспределенияНДС(СтрокиДляРаспределения, Реквизиты, Отказ);	
		
	СписанныеПартииНДС.Индексы.Добавить("СпособУчетаНДСИзменился");
	
	ОтборНеЗаполненногоКонтрагента = Новый Структура;
	ОтборНеЗаполненногоКонтрагента.Вставить("Поставщик", Справочники.Контрагенты.ПустаяСсылка());
	ОтборНеЗаполненногоКонтрагента.Вставить("ЕстьНДСПредъявленный", Истина);
	СтрокиСПустымПоставщиком = СписанныеПартииНДС.НайтиСтроки(ОтборНеЗаполненногоКонтрагента);
	ТЗПоискаПоставщика = СписанныеПартииНДС.Скопировать(СтрокиСПустымПоставщиком, "СчетФактура");
	
	ОтборНеЗаполненногоКонтрагента = Новый Структура;
	ОтборНеЗаполненногоКонтрагента.Вставить("Поставщик", Справочники.Контрагенты.ПустаяСсылка());
	СтрокиСПустымПоставщикомДляИспрСФ = СписанныеПартииНДС.НайтиСтроки(ОтборНеЗаполненногоКонтрагента);
	ТЗПоискаПоставщикаДляИспрСФ = СписанныеПартииНДС.Скопировать(СтрокиСПустымПоставщикомДляИспрСФ, "СчетФактура");
	
	ОпределитьПоставщиковПоСписаннымПартиямНДС(СтрокиСПустымПоставщиком, ТЗПоискаПоставщика);
	
	УчетНДСБП.ОпределитьИсправленныеСчетаФактуры(СтрокиСПустымПоставщикомДляИспрСФ, Реквизиты, ТЗПоискаПоставщикаДляИспрСФ );
		
	//Проверка необходимости восстановления НДС
	
	СписанныеПартииНДС.Индексы.Добавить("СпособУчетаНДС");
	СтрокиДляРасчетаСуммыРанееПринятогоКВычетуНДС = СписанныеПартииНДС.НайтиСтроки(Новый Структура(
		"СпособУчетаНДСИзменился,СпособУчетаНДС,ЕстьНДСПредъявленный",
		Истина, Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету, Истина));
		
	ОпределитьСуммуРанееПринятогоКВычетуНДС(СтрокиДляРасчетаСуммыРанееПринятогоКВычетуНДС, Реквизиты);
		
	Возврат СписанныеПартииНДС;

КонецФункции

Функция ПустаяТаблицаСписанныеПартииНДС()
	
	СписанныеПартииНДС = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСРаздельныйУчет");

	//Добавим колонки для развернутой аналитики учета НДС
	СписанныеПартииНДС.Колонки.Добавить("СчетФактура",
		Новый ОписаниеТипов(Документы.ТипВсеСсылки()));
	СписанныеПартииНДС.Колонки.Добавить("ВидЦенности",
		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЦенностей"));
	СписанныеПартииНДС.Колонки.Добавить("СчетУчетаНДС",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	СписанныеПартииНДС.Колонки.Добавить("СтавкаНДС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	СписанныеПартииНДС.Колонки.Добавить("Поставщик",
		Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	СписанныеПартииНДС.Колонки.Добавить("ИсправленныйСчетФактура",
		Новый ОписаниеТипов("ДокументСсылка.КорректировкаПоступления"));
		
	//Добавим колонки для развернутой аналитики учета затрат
	СписанныеПартииНДС.Колонки.Добавить("СчетЗатрат",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	СписанныеПартииНДС.Колонки.Добавить("Подразделение",
		Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	СписанныеПартииНДС.Колонки.Добавить("Субконто1");
	СписанныеПартииНДС.Колонки.Добавить("Субконто2");
	СписанныеПартииНДС.Колонки.Добавить("Субконто3");
		
	СписанныеПартииНДС.Колонки.Добавить("ИмяСписка",
		Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	СписанныеПартииНДС.Колонки.Добавить("НомерСтрокиДокумента",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10,0)));
		
	СписанныеПартииНДС.Колонки.Добавить("Регистратор",
		Новый ОписаниеТипов(Документы.ТипВсеСсылки()));
	СписанныеПартииНДС.Колонки.Добавить("ДатаСобытия",
		Новый ОписаниеТипов("Дата"));
	СписанныеПартииНДС.Колонки.Добавить("ДатаПартии",
		Новый ОписаниеТипов("Дата"));
	СписанныеПартииНДС.Колонки.Добавить("ДатаСФ",
		Новый ОписаниеТипов("Дата"));
	СписанныеПартииНДС.Колонки.Добавить("НовыйСпособУчетаНДС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	СписанныеПартииНДС.Колонки.Добавить("СтавкаНДСДокумента",
		Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	СписанныеПартииНДС.Колонки.Добавить("СпособУчетаНДСИзменился",
		Новый ОписаниеТипов("Булево"));
		
	СписанныеПартииНДС.Колонки.Добавить("ДоговорКонтрагента",
		Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	
	//Колонки для распределения по периоду поступления	
	СписанныеПартииНДС.Колонки.Добавить("НДСПринимаетсяКВычету",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СписанныеПартииНДС.Колонки.Добавить("СуммаБезНДСПринимаетсяКВычету",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СписанныеПартииНДС.Колонки.Добавить("НДСУчитываетсяВCтоимости",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СписанныеПартииНДС.Колонки.Добавить("СуммаБезНДСУчитываетсяВCтоимости",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СписанныеПартииНДС.Колонки.Добавить("НДСДляОперацийПо0",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СписанныеПартииНДС.Колонки.Добавить("СуммаБезНДСДляОперацийПо0",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
		
	//Колонки для учета возможности восстановления
	СписанныеПартииНДС.Колонки.Добавить("СуммаБезНДСПринятоКВычету",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СписанныеПартииНДС.Колонки.Добавить("НДСПринятоКВычету",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	СписанныеПартииНДС.Колонки.Добавить("Покупатель",
		Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
		
	//Амортизационная премия
	СписанныеПартииНДС.Колонки.Добавить("ПроцентАмортизационнойПремии",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4,2)));
		
	СписанныеПартииНДС.Колонки.Добавить("ЕстьНДСПредъявленный",
		Новый ОписаниеТипов("Булево"));
		
	// Документ отгрузки для регистра "НДС предъявленный, реализация 0"
	СписанныеПартииНДС.Колонки.Добавить("ДокументОтгрузки", Документы.ТипВсеСсылки());
	
	СписанныеПартииНДС.Колонки.Добавить("БлокироватьДоПодтвержденияСтавки0",
		Новый ОписаниеТипов("Булево"));
		
	СписанныеПартииНДС.Колонки.Добавить("КодРаздел7ДекларацииНДС",
		Новый ОписаниеТипов("СправочникСсылка.КодыОперацийРаздела7ДекларацииПоНДС"));
		
	СписанныеПартииНДС.Колонки.Добавить("Номенклатура",
		Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		
	Возврат СписанныеПартииНДС;
	
КонецФункции

Процедура ПодготовитьТаблицуТовары(Товары)
	
	Товары.Колонки.Добавить("НовыйСпособУчетаНДСОпределенПоСтавке",
		Новый ОписаниеТипов("Булево"));
	
	Если Товары.Колонки.Найти("НовыйСпособУчетаНДС") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Товары.Колонки.Добавить("НовыйСпособУчетаНДС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	
	Если Товары.Колонки.Найти("СтавкаНДСДокумента") <> Неопределено Тогда
			
		Для Каждого СтрокаТаблицы Из Товары Цикл
			
			СтрокаТаблицы.НовыйСпособУчетаНДСОпределенПоСтавке = Истина;
			
			Если СтрокаТаблицы.СтавкаНДСДокумента = Перечисления.СтавкиНДС.БезНДС Тогда
				СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости;
			ИначеЕсли СтрокаТаблицы.СтавкаНДСДокумента = Перечисления.СтавкиНДС.НДС0 Тогда
				СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0;
			Иначе
				СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету;
			КонецЕсли;
		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьТаблицуСписанныеТоварыБухУчет(СписанныеТоварыБухУчет, Реквизиты)
	
	СпособОценкиМПЗПоСредней = (УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период)
		= Перечисления.СпособыОценки.ПоСредней);

	СписанныеТоварыБухУчет.Колонки.Добавить("АналитикаУчетаЗатрат",
		Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаЗатрат"));	
		
	СписанныеТоварыБухУчет.Колонки.Добавить("КоличественныйУчет",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1,0)));
		
	СписанныеТоварыБухУчет.Колонки.Добавить("КорАналитикаУчетаЗатрат",
		Новый ОписаниеТипов("Структура"));	
		
	Для Каждого СтрокаТаблицы Из СписанныеТоварыБухУчет Цикл
		
		АналитикаУчетаЗатрат = Новый Структура("Организация,СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3");
		
		АналитикаУчетаЗатрат.Организация = Реквизиты.Организация;
		АналитикаУчетаЗатрат.СчетЗатрат = СтрокаТаблицы.СчетУчета;
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(АналитикаУчетаЗатрат.СчетЗатрат);
		
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			АналитикаУчетаЗатрат.Подразделение = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура
			 ИЛИ СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства
			 ИЛИ СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы
			 ИЛИ СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыНаНИОКР Тогда
			 	АналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(АналитикаУчетаЗатрат.СчетЗатрат) Тогда
					АналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы.Склад;
				КонецЕсли;
	        ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
				Если НЕ СпособОценкиМПЗПоСредней Тогда
					АналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы.Партия;
				КонецЕсли;
	        ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты Тогда
				АналитикаУчетаЗатрат["Субконто" + Ном] = Реквизиты.Контрагент;
			КонецЕсли;
		КонецЦикла;
		
		СтрокаТаблицы.АналитикаУчетаЗатрат = Справочники.КлючиАналитикиУчетаЗатрат.КлючиАналитикиУчетаЗатратДокумента(АналитикаУчетаЗатрат);
		СтрокаТаблицы.КоличественныйУчет = Число(СвойстваСчета.Количественный);
		
		
		КорАналитикаУчетаЗатрат = Новый Структура("Организация,СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3");
		
		КорАналитикаУчетаЗатрат.Организация = Реквизиты.Организация;
		КорАналитикаУчетаЗатрат.СчетЗатрат = СтрокаТаблицы.КорСчетСписания;
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(КорАналитикаУчетаЗатрат.СчетЗатрат);
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			КорАналитикаУчетаЗатрат.Подразделение = СтрокаТаблицы.КорПодразделение;
		КонецЕсли;
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если НЕ БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(КорАналитикаУчетаЗатрат.СчетЗатрат) Тогда
					Продолжить;
				КонецЕсли;
	        ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
				Если СпособОценкиМПЗПоСредней Тогда
					Продолжить;
				Иначе
					КорАналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы.Партия;	
				КонецЕсли;
			КонецЕсли;
			Для КорНом = 1 По 3 Цикл
				Если СвойстваСчета["ВидСубконто" + Ном] = СтрокаТаблицы["ВидКорСубконто" + КорНом]
				 ИЛИ СтрокаТаблицы["ВидКорСубконто" + КорНом] = Ном Тогда
				 	КорАналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы["КорСубконто" + КорНом];
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		СтрокаТаблицы.КорАналитикаУчетаЗатрат = КорАналитикаУчетаЗатрат;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьОстаткиПартийНДС(СписанныеТовары, Реквизиты)

	// Блокируем регистр НДСРаздельныйУчет для получения остатков
	Если ТранзакцияАктивна() Тогда
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.НДСРаздельныйУчет");
		ЭлементБлокировки.УстановитьЗначение("Период",      Новый Диапазон(, Реквизиты.Период));
		ЭлементБлокировки.УстановитьЗначение("Организация", Реквизиты.Организация);
		ЭлементБлокировки.ИсточникДанных = СписанныеТовары;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("АналитикаУчетаЗатрат", "АналитикаУчетаЗатрат");
		Блокировка.Заблокировать();
		
	КонецЕсли;

	// Получаем остатки регистра НДСРаздельныйУчет
	Запрос = Новый Запрос;
	
	МоментВремениРегистратор = Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор);
	
	Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
		МоментСписания = Новый Граница(МоментВремениРегистратор, ВидГраницы.Включая);
	Иначе
		МоментСписания = МоментВремениРегистратор;
	КонецЕсли; 
	
	ТаблицаСписанныеТоварыБух = СписанныеТовары.Скопировать(,"СчетУчета,АналитикаУчетаЗатрат,Номенклатура");
	ТаблицаСписанныеТоварыБух.Свернуть("СчетУчета,АналитикаУчетаЗатрат,Номенклатура");
	
	Запрос.УстановитьПараметр("МоментСписания",            МоментСписания);
	Запрос.УстановитьПараметр("Организация",               Реквизиты.Организация);
	Запрос.УстановитьПараметр("ТаблицаСписанныеТоварыБух", ТаблицаСписанныеТоварыБух);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаСписанныеТоварыБух.СчетУчета КАК СчетУчета,
	|	ТаблицаСписанныеТоварыБух.АналитикаУчетаЗатрат КАК АналитикаУчетаЗатрат,
	|	ТаблицаСписанныеТоварыБух.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаСписанныеТоварыБух
	|ИЗ
	|	&ТаблицаСписанныеТоварыБух КАК ТаблицаСписанныеТоварыБух
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаСписанныеТоварыБух.АналитикаУчетаЗатрат КАК АналитикаУчетаЗатрат,
	|	ЕСТЬNULL(КлассификаторТНВЭД.СырьевойТовар, ЛОЖЬ) КАК ЭтоСырье,
	|	ЕСТЬNULL(СпрНоменклатура.КодРаздел7ДекларацииНДС, ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.Пустаяссылка)) КАК КодРаздел7ДекларацииНДС,
	|	ТаблицаСписанныеТоварыБух.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТРеквизитыНоменклатуры
	|ИЗ
	|	ТаблицаСписанныеТоварыБух КАК ТаблицаСписанныеТоварыБух
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ТаблицаСписанныеТоварыБух.Номенклатура = СпрНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|		ПО (СпрНоменклатура.КодТНВЭД = КлассификаторТНВЭД.Ссылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Организация КАК Организация,
	|	ТаблицаСписанныеТоварыБух.СчетУчета КАК СчетУчета,
	|	ТаблицаСписанныеТоварыБух.АналитикаУчетаЗатрат КАК АналитикаУчетаЗатрат
	|ПОМЕСТИТЬ ТаблицаСписанныхТоваров
	|ИЗ
	|	ТаблицаСписанныеТоварыБух КАК ТаблицаСписанныеТоварыБух
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ПО ТаблицаСписанныеТоварыБух.СчетУчета = Хозрасчетный.Ссылка
	|ГДЕ
	|	НЕ ЕСТЬNULL(Хозрасчетный.Забалансовый, ИСТИНА)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	АналитикаУчетаЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСРаздельныйУчетОстатки.АналитикаУчетаЗатрат КАК АналитикаУчетаЗатрат,
	|	НДСРаздельныйУчетОстатки.АналитикаУчетаНДС КАК АналитикаУчетаНДС,
	|	НДСРаздельныйУчетОстатки.Партия КАК Партия,
	|	НДСРаздельныйУчетОстатки.СпособУчетаНДС КАК СпособУчетаНДС,
	|	ВЫБОР
	|		КОГДА НДСРаздельныйУчетОстатки.КоличествоОстаток > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьКоличество,
	|	НДСРаздельныйУчетОстатки.КоличествоОстаток КАК Количество,
	|	НДСРаздельныйУчетОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСРаздельныйУчетОстатки.НДСОстаток КАК НДС
	|ПОМЕСТИТЬ ВТНДСРаздельныйУчетОстатки
	|ИЗ
	|	РегистрНакопления.НДСРаздельныйУчет.Остатки(
	|			&МоментСписания,
	|			(Организация, АналитикаУчетаЗатрат) В
	|				(ВЫБРАТЬ
	|					ТаблицаСписанныхТоваров.Организация,
	|					ТаблицаСписанныхТоваров.АналитикаУчетаЗатрат
	|				ИЗ
	|					ТаблицаСписанныхТоваров)) КАК НДСРаздельныйУчетОстатки
	|ГДЕ
	|	НДСРаздельныйУчетОстатки.СуммаБезНДСОстаток >= 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНДСРаздельныйУчетОстатки.АналитикаУчетаЗатрат КАК АналитикаУчетаЗатрат,
	|	ВТНДСРаздельныйУчетОстатки.АналитикаУчетаНДС КАК АналитикаУчетаНДС,
	|	ВТНДСРаздельныйУчетОстатки.СпособУчетаНДС КАК СпособУчетаНДС,
	|	ВТНДСРаздельныйУчетОстатки.Партия КАК Партия,
	|	РегистрАналитикаУчетаНДС.СчетФактура КАК СчетФактура,
	|	РегистрАналитикаУчетаНДС.ВидЦенности КАК ВидЦенности,
	|	РегистрАналитикаУчетаНДС.СчетУчетаНДС КАК СчетУчетаНДС,
	|	РегистрАналитикаУчетаНДС.СтавкаНДС КАК СтавкаНДС,
	|	ВТНДСРаздельныйУчетОстатки.ЕстьКоличество КАК ЕстьКоличество,
	|	ВТНДСРаздельныйУчетОстатки.Количество КАК Количество,
	|	ВТНДСРаздельныйУчетОстатки.СуммаБезНДС КАК СуммаБезНДС,
	|	ВТНДСРаздельныйУчетОстатки.НДС КАК НДС,
	|	ЕСТЬNULL(РеквизитыДокументаСчетФактура.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСФ,
	|	ЕСТЬNULL(РеквизитыДокументаПартии.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПартии,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РеквизитыДокументаСчетФактура.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) < ДАТАВРЕМЯ(2016, 7, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ВТРеквизитыНоменклатуры.ЭтоСырье
	|	КОНЕЦ КАК БлокироватьДоПодтвержденияСтавки0,
	|	ВТРеквизитыНоменклатуры.КодРаздел7ДекларацииНДС КАК КодРаздел7ДекларацииНДС,
	|	ВТРеквизитыНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(РегистрАналитикаУчетаНДС.Поставщик, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Поставщик,
	|	ЕСТЬNULL(РегистрАналитикаУчетаНДС.ИсправленныйСчетФактура, ЗНАЧЕНИЕ(Документ.КорректировкаПоступления.ПустаяСсылка)) КАК ИсправленныйСчетФактура
	|ИЗ
	|	ВТНДСРаздельныйУчетОстатки КАК ВТНДСРаздельныйУчетОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНДС КАК РегистрАналитикаУчетаНДС
	|		ПО ВТНДСРаздельныйУчетОстатки.АналитикаУчетаНДС = РегистрАналитикаУчетаНДС.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРеквизитыНоменклатуры КАК ВТРеквизитыНоменклатуры
	|		ПО ВТНДСРаздельныйУчетОстатки.АналитикаУчетаЗатрат = ВТРеквизитыНоменклатуры.АналитикаУчетаЗатрат
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыДокументаПартии
	|		ПО (РеквизитыДокументаПартии.Организация = &Организация)
	|			И ВТНДСРаздельныйУчетОстатки.Партия = РеквизитыДокументаПартии.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыДокументаСчетФактура
	|		ПО (РеквизитыДокументаСчетФактура.Организация = &Организация)
	|			И (РегистрАналитикаУчетаНДС.СчетФактура = РеквизитыДокументаСчетФактура.Документ)";
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРезультат.Индексы.Добавить("ЕстьКоличество");
	
	ТаблицыПартийНДС = Новый Структура("Товары, ДопРасходы");
	
	ТаблицыПартийНДС.Товары = ТаблицаРезультат.Скопировать(Новый Структура("ЕстьКоличество", Истина));
	ОбщегоНазначенияБПВызовСервера.ПронумероватьТаблицу(ТаблицыПартийНДС.Товары, "НомерСтроки");
	ТаблицыПартийНДС.Товары.Индексы.Добавить("АналитикаУчетаЗатрат");
	ТаблицыПартийНДС.Товары.Индексы.Добавить("НомерСтроки");
	
	ТаблицыПартийНДС.ДопРасходы = ТаблицаРезультат.Скопировать(Новый Структура("ЕстьКоличество", Ложь));
	ТаблицыПартийНДС.ДопРасходы.Колонки.Добавить("СписаннаяСуммаБезНДС",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицыПартийНДС.ДопРасходы.Колонки.Добавить("СписанныйНДС",
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицыПартийНДС.ДопРасходы.Индексы.Добавить("АналитикаУчетаЗатрат");

	Возврат ТаблицыПартийНДС;

КонецФункции

Процедура ЗаполнитьПорядокСписанияПартийПоСпособуУчетаНДС(ТаблицаОстатковПартийНДС, НовыйСпособУчетаНДС)
	
	Если ТаблицаОстатковПартийНДС.Колонки.Найти("ПорядокСписанияПартийПоСпособуУчетаНДС") = Неопределено Тогда
		ТаблицаОстатковПартийНДС.Колонки.Добавить("ПорядокСписанияПартийПоСпособуУчетаНДС",
			Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1,0)));
	КонецЕсли;	
	
	ПринимаетсяКВычету = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету;
	УчитываетсяВCтоимости = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости;
	ДляОперацийПо0 = Перечисления.СпособыУчетаНДС.ДляОперацийПо0;
	Распределяется = Перечисления.СпособыУчетаНДС.Распределяется;
	Распределен = Перечисления.СпособыУчетаНДС.Распределен;
	ВосстановленУчитываетсяВCтоимости = Перечисления.СпособыУчетаНДС.ВосстановленУчитываетсяВCтоимости;
	ВосстановленДляОперацийПо0 = Перечисления.СпособыУчетаНДС.ВосстановленДляОперацийПо0;
		
	Для Каждого СтрокаТаблицы Из ТаблицаОстатковПартийНДС Цикл
		
		Если НовыйСпособУчетаНДС = ПринимаетсяКВычету Тогда
			
			Если СтрокаТаблицы.СпособУчетаНДС = ПринимаетсяКВычету Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 1;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Распределен Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 2;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Распределяется Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 3;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ДляОперацийПо0 Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 4;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = УчитываетсяВCтоимости Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 5;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ВосстановленДляОперацийПо0 Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 6;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ВосстановленУчитываетсяВCтоимости Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 7;
			КонецЕсли;	
				
		ИначеЕсли НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости Тогда
			
			Если СтрокаТаблицы.СпособУчетаНДС = ВосстановленУчитываетсяВCтоимости Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 1;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = УчитываетсяВCтоимости Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 2;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Распределен Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 3;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Распределяется Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 4;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ДляОперацийПо0 Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 5;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ПринимаетсяКВычету Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 6;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ВосстановленДляОперацийПо0 Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 7;
			КонецЕсли;	
			
		ИначеЕсли НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 Тогда
			
			Если СтрокаТаблицы.СпособУчетаНДС = ВосстановленДляОперацийПо0 Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 1;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ДляОперацийПо0 Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 2;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Распределен Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 3;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Распределяется Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 4;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = УчитываетсяВCтоимости Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 5;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ПринимаетсяКВычету Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 6;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ВосстановленУчитываетсяВCтоимости Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 7;
			КонецЕсли;	
			
		ИначеЕсли НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределяется Тогда
			
			Если СтрокаТаблицы.СпособУчетаНДС = Распределен Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 1;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Распределяется Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 2;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = УчитываетсяВCтоимости Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 3;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ДляОперацийПо0 Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 4;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ПринимаетсяКВычету Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 5;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ВосстановленДляОперацийПо0 Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 6;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ВосстановленУчитываетсяВCтоимости Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 7;
			КонецЕсли;	
			
		Иначе
			
			Если СтрокаТаблицы.СпособУчетаНДС = УчитываетсяВCтоимости Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 1;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Распределен Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 2;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = Распределяется Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 3;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ДляОперацийПо0 Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 4;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ПринимаетсяКВычету Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 5;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ВосстановленДляОперацийПо0 Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 6;
			ИначеЕсли СтрокаТаблицы.СпособУчетаНДС = ВосстановленУчитываетсяВCтоимости Тогда
				СтрокаТаблицы.ПорядокСписанияПартийПоСпособуУчетаНДС = 7;
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСпособУчетаНДСИзменился(СтрокаСписаннойПартииНДС)
	
	Если СтрокаСписаннойПартииНДС.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0 
		И НЕ СтрокаСписаннойПартииНДС.БлокироватьДоПодтвержденияСтавки0 Тогда
		
		СтрокаСписаннойПартииНДС.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаСписаннойПартииНДС.НовыйСпособУчетаНДС)
		ИЛИ СтрокаСписаннойПартииНДС.НовыйСпособУчетаНДС = СтрокаСписаннойПартииНДС.СпособУчетаНДС
		ИЛИ СтрокаСписаннойПартииНДС.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределен 
		ИЛИ (СтрокаСписаннойПартииНДС.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ВосстановленУчитываетсяВCтоимости
		   И СтрокаСписаннойПартииНДС.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости)
		ИЛИ (СтрокаСписаннойПартииНДС.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ВосстановленДляОперацийПо0
		   И СтрокаСписаннойПартииНДС.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.ДляОперацийПо0) Тогда
		
		СтрокаСписаннойПартииНДС.СпособУчетаНДСИзменился = Ложь;
		
	Иначе
		
		СтрокаСписаннойПартииНДС.СпособУчетаНДСИзменился = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьПоставщиковПоСписаннымПартиямНДС(СписанныеПартии, ТаблицаСчетовФактур = Неопределено)
	Если ТаблицаСчетовФактур = Неопределено Тогда
		// Готовим таблицу счетов-фактур, по которым нужно найти поставщиков.
		ТаблицаСчетовФактур = СписанныеПартии.СкопироватьКолонки("СчетФактура");
		
		// Поставщики нужны только в случаях изменения способа учета НДС:
		ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(
		СписанныеПартии.Скопировать(
		Новый Структура("СпособУчетаНДСИзменился,ЕстьНДСПредъявленный", Истина, Истина), "СчетФактура"),
		ТаблицаСчетовФактур);
	КонецЕсли;

	Если ТаблицаСчетовФактур.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ПараметрыСчетовФактур = УчетНДСБП.ПолучитьПараметрыСчетовФактурПоНДСПредъявленный(ТаблицаСчетовФактур);

	ПараметрыСчетовФактур.Индексы.Добавить("СчетФактура");
	Для каждого СтрокаТаблицы Из СписанныеПартии Цикл
		СтрокаПараметровСФ = ПараметрыСчетовФактур.Найти(СтрокаТаблицы.СчетФактура, "СчетФактура");
		Если СтрокаПараметровСФ <> Неопределено Тогда
			СтрокаТаблицы.Поставщик          = СтрокаПараметровСФ.Поставщик;
			СтрокаТаблицы.Покупатель         = СтрокаПараметровСФ.Поставщик;
			СтрокаТаблицы.ДоговорКонтрагента = СтрокаПараметровСФ.ДоговорКонтрагента;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ОпределитьСуммуРанееПринятогоКВычетуНДС(СтрокиПартийНДС, Реквизиты)

	Если СтрокиПартийНДС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ОстаткиНепринятогоКВычетуНДС = ПолучитьОстаткиНепринятогоКВычетуНДС(СтрокиПартийНДС, Реквизиты);
	
	Отбор = Новый Структура("АналитикаУчетаНДС");

	Для каждого СтрокаТаблицы Из СтрокиПартийНДС Цикл
		
		СуммаБезНДСПринятоКВычету	= СтрокаТаблицы.СуммаБезНДС - СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету;
		НДСПринятоКВычету			= СтрокаТаблицы.НДС - СтрокаТаблицы.НДСПринимаетсяКВычету;
		
		ОстаткиПоСФ = ОстаткиНепринятогоКВычетуНДС.Строки.Найти(СтрокаТаблицы.СчетФактура, "СчетФактура");
		
		Если ОстаткиПоСФ <> Неопределено Тогда
			
			СтрокаТаблицы.ИсправленныйСчетФактура = ОстаткиПоСФ.ИсправленныйСчетФактура;
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
			НайденныеСтроки = ОстаткиПоСФ.Строки.НайтиСтроки(Отбор);
			
			Для каждого СтрокаОстатков Из НайденныеСтроки Цикл
				СуммаБезНДСНеПринятоКВычету	= СтрокаОстатков.СуммаБезНДС;
				НДСНеПринятоКВычету			= СтрокаОстатков.НДС;
				
				СуммаБезНДСПринятоКВычету	= СуммаБезНДСПринятоКВычету - СуммаБезНДСНеПринятоКВычету;
				НДСПринятоКВычету			= НДСПринятоКВычету - НДСНеПринятоКВычету;
			КонецЦикла;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.СчетФактура) Тогда
			СтрокаТаблицы.СуммаБезНДСПринятоКВычету = Макс(СуммаБезНДСПринятоКВычету, 0);
			СтрокаТаблицы.НДСПринятоКВычету = Макс(НДСПринятоКВычету, 0);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Функция ПолучитьОстаткиНепринятогоКВычетуНДС(СтрокиПартийНДС, Реквизиты)

	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("Организация",   Реквизиты.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор));
	
	АналитикиУчетаНДС = Новый Массив;
	Для каждого СтрокаТаблицы Из СтрокиПартийНДС Цикл
		АналитикиУчетаНДС.Добавить(СтрокаТаблицы.АналитикаУчетаНДС);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("АналитикиУчетаНДС", АналитикиУчетаНДС);

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Организация КАК Организация,
	|	РегистрАналитикаУчетаНДС.СчетФактура КАК СчетФактура,
	|	РегистрАналитикаУчетаНДС.ВидЦенности КАК ВидЦенности,
	|	РегистрАналитикаУчетаНДС.СчетУчетаНДС КАК СчетУчетаНДС,
	|	РегистрАналитикаУчетаНДС.СтавкаНДС КАК СтавкаНДС,
	|	РегистрАналитикаУчетаНДС.КлючАналитики КАК АналитикаУчетаНДС
	|ПОМЕСТИТЬ ВТРазвернутыеАналитики
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНДС КАК РегистрАналитикаУчетаНДС
	|ГДЕ
	|	РегистрАналитикаУчетаНДС.КлючАналитики В(&АналитикиУчетаНДС)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СчетФактура,
	|	ВидЦенности,
	|	СчетУчетаНДС,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйОстатки.Организация,
	|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйОстатки.ВидЦенности,
	|	НДСПредъявленныйОстатки.СтавкаНДС,
	|	НДСПредъявленныйОстатки.СчетУчетаНДС,
	|	НДСПредъявленныйОстатки.Поставщик,
	|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСПредъявленныйОстатки.НДСОстаток КАК НДС,
	|	НДСПредъявленныйОстатки.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ВТРазвернутыеАналитики.АналитикаУчетаНДС КАК АналитикаУчетаНДС 
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(
	|			&МоментВремени,
	|			(Организация, СчетФактура, ВидЦенности, СчетУчетаНДС, СтавкаНДС) В
	|				(ВЫБРАТЬ
	|					ВТРазвернутыеАналитики.Организация,
	|					ВТРазвернутыеАналитики.СчетФактура,
	|					ВТРазвернутыеАналитики.ВидЦенности,
	|					ВТРазвернутыеАналитики.СчетУчетаНДС,
	|					ВТРазвернутыеАналитики.СтавкаНДС
	|				ИЗ
	|					ВТРазвернутыеАналитики)) КАК НДСПредъявленныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазвернутыеАналитики КАК ВТРазвернутыеАналитики
	|		ПО НДСПредъявленныйОстатки.СчетФактура = ВТРазвернутыеАналитики.СчетФактура
	|			И НДСПредъявленныйОстатки.ВидЦенности = ВТРазвернутыеАналитики.ВидЦенности
	|			И НДСПредъявленныйОстатки.СчетУчетаНДС = ВТРазвернутыеАналитики.СчетУчетаНДС
	|			И НДСПредъявленныйОстатки.СтавкаНДС = ВТРазвернутыеАналитики.СтавкаНДС
	|ГДЕ
	|	НДСПредъявленныйОстатки.НДСОстаток + НДСПредъявленныйОстатки.СуммаБезНДСОстаток > 0
	|ИТОГИ
	|	МАКСИМУМ(ИсправленныйСчетФактура)
	|ПО
	|	СчетФактура";
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);

	Возврат ТаблицаРезультата;

КонецФункции

Процедура ОпределитьСуммыДляРаспределенияНДС(СтрокиПартийНДС, Реквизиты, Отказ)
	
	ТаблицаПериодовПоступления = Новый ТаблицаЗначений;
	ТаблицаПериодовПоступления.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	Для Каждого СтрокаТаблицы Из СтрокиПартийНДС Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаСФ) Тогда
			Если ЗначениеЗаполнено(СтрокаТаблицы.СчетФактура) Тогда
				СтрокаТаблицы.ДатаСФ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.СчетФактура, "Дата");
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ПериодПоступления = НачалоКвартала(СтрокаТаблицы.ДатаСФ);
		ТекущийПериод = НачалоКвартала(Реквизиты.Период);
		
		Если ПериодПоступления < ТекущийПериод Тогда
			НоваяСтрока = ТаблицаПериодовПоступления.Добавить();
			НоваяСтрока.Период = ПериодПоступления;
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаПериодовПоступления.Свернуть("Период");
	ПериодыПоступления = ТаблицаПериодовПоступления.ВыгрузитьКолонку("Период");
	
	Если ПериодыПоступления.Количество() > 0 Тогда 
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ПериодыПоступления", ПериодыПоступления);
		Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БазаРаспределенияНДС.Период,
		|	БазаРаспределенияНДС.ВыручкаНДС,
		|	БазаРаспределенияНДС.ВыручкаБезНДС + БазаРаспределенияНДС.ВыручкаЕНВД КАК ВыручкаБезНДС,
		|	БазаРаспределенияНДС.ВыручкаНДС0
		|ИЗ
		|	РегистрСведений.БазаРаспределенияНДС КАК БазаРаспределенияНДС
		|ГДЕ
		|	БазаРаспределенияНДС.Организация = &Организация
		|	И БазаРаспределенияНДС.Период В(&ПериодыПоступления)";
	
		ПропорцииРаспределения = Запрос.Выполнить().Выгрузить();
		ПропорцииРаспределения.Индексы.Добавить("Период");
		
		Для Каждого СтрокаТаблицы Из СтрокиПартийНДС Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ДатаСФ) Тогда
				Продолжить;
			КонецЕсли;
			
			Если НачалоКвартала(СтрокаТаблицы.ДатаСФ) >= ТекущийПериод Тогда
				Продолжить;
			КонецЕсли;
			
			Пропорции = ПропорцииРаспределения.НайтиСтроки(Новый Структура("Период", НачалоКвартала(СтрокаТаблицы.ДатаСФ)));
			
			Если Пропорции.Количество() = 0 Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Не обнаружена база распределения НДС за %1'"),
					Формат(СтрокаТаблицы.ДатаСФ, "ДФ='к ''Квартал'' гггг  ''г.'''"));
				
				Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
					ПолеСпособУчетаНДС	= "ДокументОтгрузки";
				Иначе
					ПолеСпособУчетаНДС	= СтрокаТаблицы.ИмяСписка + "[" + Формат(СтрокаТаблицы.НомерСтрокиДокумента - 1, "ЧН=0; ЧГ=") + "].СпособУчетаНДС";
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Реквизиты.Регистратор, ПолеСпособУчетаНДС, "Объект", Отказ);
				
				Прервать;
				
			Иначе
				
				СтрокаТаблицы.НовыйСпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределен;
				ЗаполнитьСпособУчетаНДСИзменился(СтрокаТаблицы);
				
				Пропорция = Пропорции[0];
				
				БазаРаспределения = Пропорция.ВыручкаНДС + Пропорция.ВыручкаБезНДС + Пропорция.ВыручкаНДС0;
				
				УчтеноСуммы   = 0;
				УчтеноНДС     = 0;
				УчтеноВыручки = 0;

				Если Пропорция.ВыручкаНДС <> 0 Тогда

					СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету = Окр(СтрокаТаблицы.СуммаБезНДС
						* (Пропорция.ВыручкаНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноСуммы;
					СтрокаТаблицы.НДСПринимаетсяКВычету = Окр(СтрокаТаблицы.НДС
						* (Пропорция.ВыручкаНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

					УчтеноСуммы     = УчтеноСуммы + СтрокаТаблицы.СуммаБезНДСПринимаетсяКВычету;
					УчтеноНДС       = УчтеноНДС + СтрокаТаблицы.НДСПринимаетсяКВычету;
					УчтеноВыручки   = УчтеноВыручки + Пропорция.ВыручкаНДС;

				КонецЕсли;

				Если Пропорция.ВыручкаБезНДС <> 0 Тогда

					СтрокаТаблицы.СуммаБезНДСУчитываетсяВCтоимости = Окр(СтрокаТаблицы.СуммаБезНДС
						* (Пропорция.ВыручкаБезНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноСуммы;
					СтрокаТаблицы.НДСУчитываетсяВCтоимости = Окр(СтрокаТаблицы.НДС
						* (Пропорция.ВыручкаБезНДС + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

					УчтеноСуммы   = УчтеноСуммы + СтрокаТаблицы.СуммаБезНДСУчитываетсяВCтоимости;
					УчтеноНДС     = УчтеноНДС + СтрокаТаблицы.НДСУчитываетсяВCтоимости;
					УчтеноВыручки = УчтеноВыручки + Пропорция.ВыручкаБезНДС;

				КонецЕсли;

				Если Пропорция.ВыручкаНДС0 <> 0 Тогда

					СтрокаТаблицы.СуммаБезНДСДляОперацийПо0 = Окр(СтрокаТаблицы.СуммаБезНДС
						* (Пропорция.ВыручкаНДС0 + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноСуммы;
					СтрокаТаблицы.НДСДляОперацийПо0 = Окр(СтрокаТаблицы.НДС
						* (Пропорция.ВыручкаНДС0 + УчтеноВыручки)/БазаРаспределения, 2) - УчтеноНДС;

					УчтеноСуммы   = УчтеноСуммы + СтрокаТаблицы.СуммаБезНДСДляОперацийПо0;
					УчтеноНДС     = УчтеноНДС + СтрокаТаблицы.НДСДляОперацийПо0;
					УчтеноВыручки = УчтеноВыручки + Пропорция.ВыручкаНДС0;

				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЗначениеЕстьНДСПредъявленный(СписанныеПартии)
	
	ТаблицаСчетовФактур = СписанныеПартии.Скопировать(,"СчетФактура");
	
	Если ТаблицаСчетовФактур.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаСчетовФактур", ТаблицаСчетовФактур);

	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаСчетовФактур.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ ТаблицаСчетовФактур
	|ИЗ
	|	&ТаблицаСчетовФактур КАК ТаблицаСчетовФактур
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаСчетовФактур.СчетФактура,
	|	ВЫБОР
	|		КОГДА НДСПредъявленный.Поставщик ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьНДСПредъявленный
	|ИЗ
	|	ТаблицаСчетовФактур КАК ТаблицаСчетовФактур
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|		ПО ТаблицаСчетовФактур.СчетФактура = НДСПредъявленный.СчетФактура";

	ПараметрыСчетовФактур = Запрос.Выполнить().Выгрузить();
	
	ПараметрыСчетовФактур.Индексы.Добавить("СчетФактура");
	Для каждого СтрокаТаблицы Из СписанныеПартии Цикл
		Если СтрокаТаблицы.НДС = 0 Тогда
			СтрокаПараметровСФ = ПараметрыСчетовФактур.Найти(СтрокаТаблицы.СчетФактура, "СчетФактура");
			Если СтрокаПараметровСФ <> Неопределено Тогда
				СтрокаТаблицы.ЕстьНДСПредъявленный = СтрокаПараметровСФ.ЕстьНДСПредъявленный;
			КонецЕсли;
		Иначе
			СтрокаТаблицы.ЕстьНДСПредъявленный = Истина;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Процедура ОпределитьПоставщиков(ТаблицаДанных, ТаблицаСчетовФактур) Экспорт
	Если ТаблицаСчетовФактур.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ПараметрыСчетовФактур = УчетНДСБП.ПолучитьПараметрыСчетовФактурПоНДСПредъявленный(ТаблицаСчетовФактур);

	ПараметрыСчетовФактур.Индексы.Добавить("СчетФактура");
	Для каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		СтрокаПараметровСФ = ПараметрыСчетовФактур.Найти(СтрокаТаблицы.СчетФактура, "СчетФактура");
		Если СтрокаПараметровСФ <> Неопределено Тогда
			СтрокаТаблицы.Поставщик          = СтрокаПараметровСФ.Поставщик;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ФормированиеДвиженийРегистров

#Область ФормированиеДвиженийРегистраНДСЗаписиКнигиПокупок

Процедура СформироватьДвиженияЗаписьКнигиПокупок(Реквизиты, ТаблицаДвиженийПоКнигеПокупок, Движения, Отказ)
	
	Если ТаблицаДвиженийПоКнигеПокупок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ТаблицаДвиженийПоКнигеПокупок Цикл
		
		Если СтрокаТаблицы.НДС = 0 И СтрокаТаблицы.СуммаБезНДС = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// НДС Покупки
		Запись = Движения.НДСЗаписиКнигиПокупок.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		
		Если Реквизиты.КорректироватьБУиНУ 
			И СтрокаТаблицы.НДС <> 0 Тогда

			// Хозрасчетный
			Проводка = Движения.Хозрасчетный.Добавить();
			
			Проводка.Период      = СтрокаТаблицы.Период;
			Проводка.Организация = СтрокаТаблицы.Организация;
			Проводка.Содержание  = СокрЛП(СтрокаТаблицы.Содержание);
			
			Проводка.СчетДт      = ПланыСчетов.Хозрасчетный.НДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
			
			Проводка.СчетКт      = СтрокаТаблицы.СчетУчетаНДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СФПолученные", СтрокаТаблицы.СчетФактура);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", СтрокаТаблицы.Поставщик);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СпособыУчетаНДС", Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету);
			
			Проводка.Сумма       = СтрокаТаблицы.НДС;
			
		КонецЕсли;
	
	КонецЦикла;
	
	Движения.НДСЗаписиКнигиПокупок.Записывать = Истина;
	Движения.Хозрасчетный.Записывать          = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеДвиженийРегистраНДСЗаписиКнигиПродаж

Процедура СформироватьДвиженияНДСЗаписиКнигиПродажВосстановлениеНДС(ТоварыУслуги, Реквизиты, Движения, Отказ)

	Если ТоварыУслуги.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ТаблицаЗаписиКнигиПродаж = ОбщегоНазначенияБПВызовСервера.ПустаяТаблицаРегистраНакопления("НДСЗаписиКнигиПродаж");

	Для каждого СтрокаТаблицы Из ТоварыУслуги Цикл
		НоваяСтрока = ТаблицаЗаписиКнигиПродаж.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла;
	
	ВерсияКодовВидовОпераций = УчетНДСКлиентСервер.ВерсияКодовВидовОпераций(Реквизиты.Период);
	Если ВерсияКодовВидовОпераций > 1 Тогда
		// При восстановлении НДС применяется код вида операции "21".
		КодВидаОперации = "21";
	Иначе
		// При восстановлении НДС применяется код вида операции, указанный в счете-фактуре.
		КодВидаОперации = "";
	КонецЕсли;

	ТаблицаЗаписиКнигиПродаж.ЗаполнитьЗначения(Реквизиты.Период,      "Период");
	ТаблицаЗаписиКнигиПродаж.ЗаполнитьЗначения(Реквизиты.Организация, "Организация");
	ТаблицаЗаписиКнигиПродаж.ЗаполнитьЗначения(Реквизиты.Период,      "ДатаСобытия");
	ТаблицаЗаписиКнигиПродаж.ЗаполнитьЗначения(КодВидаОперации,       "КодВидаОперации");
	ТаблицаЗаписиКнигиПродаж.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПродажи.ВосстановлениеНДС, "Событие");

	ТаблицаЗаписиКнигиПродаж.Свернуть("Период,Организация,Покупатель,СчетФактура,ВидЦенности,КодВидаОперации,СтавкаНДС,
		|ДатаОплаты,ДокументОплаты,Событие,ДатаСобытия,ДоговорКонтрагента,ИсправленныйСчетФактура",
		"СуммаБезНДС,НДС");

	Для каждого СтрокаТаблицы Из ТаблицаЗаписиКнигиПродаж Цикл
		Запись = Движения.НДСЗаписиКнигиПродаж.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
	КонецЦикла;

	Движения.НДСЗаписиКнигиПродаж.Записывать = Истина;
	
	ДанныеДвижений = ТоварыУслуги.Скопировать(,"Покупатель,СчетФактура,СчетУчетаНДС,НДС");
	ДанныеДвижений.Свернуть("Покупатель,СчетФактура,СчетУчетаНДС","НДС");
	
	Для каждого ТекСтрокаВычета Из ДанныеДвижений Цикл
		
		Если ТекСтрокаВычета.НДС = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Проводка по восстановлению НДС на 19 счет 
		Движение             = Движения.Хозрасчетный.Добавить();
		Движение.Период      = Реквизиты.Период;
		Движение.Организация = Реквизиты.Организация;
		Движение.Сумма       = ТекСтрокаВычета.НДС;
		Движение.Содержание  = НСтр("ru = 'Восстановление НДС'");
		
		Движение.СчетДт = ТекСтрокаВычета.СчетУчетаНДС; // 19.ХХ
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "Контрагенты", ТекСтрокаВычета.Покупатель);
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "СФполученные", ТекСтрокаВычета.СчетФактура);
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "СпособыУчетаНДС", Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету);
		
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДС; // 68.02
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
		
	КонецЦикла; 

КонецПроцедуры

Процедура СформироватьДвиженияНДСЗаписиКнигиПродажКорректировкаПоступления(Реквизиты, ТаблицаДвижений, Сторно = Ложь, Движения, Отказ)
	
	Если ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаДвижения Из ТаблицаДвижений Цикл
		
		// НДС Продажи
		Движение = Движения.НДСЗаписиКнигиПродаж.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижения);
		
		Если НЕ Сторно И Реквизиты.КорректироватьБУиНУ
			И СтрокаДвижения.НДС <> 0 Тогда
					
			// Хозрасчетный
			Проводка = Движения.Хозрасчетный.Добавить();
			
			Проводка.Период = СтрокаДвижения.Период;
			Проводка.Организация = СтрокаДвижения.Организация;
			Проводка.Содержание  = СтрокаДвижения.Содержание;
			
			Проводка.СчетДт = СтрокаДвижения.СчетУчетаНДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СФПолученные", СтрокаДвижения.СчетФактура);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Контрагенты" , СтрокаДвижения.Покупатель);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СпособыУчетаНДС" , Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету);
			
			Проводка.СчетКт = ПланыСчетов.Хозрасчетный.НДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
			
			Проводка.Сумма = СтрокаДвижения.НДС;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Сторно И Реквизиты.КорректироватьБУиНУ Тогда
		СформироватьПроводкиСторноВосстановленияНДС(Реквизиты, Движения, Отказ);
	КонецЕсли;
				
	Движения.НДСЗаписиКнигиПродаж.Записывать = Истина;
	Движения.Хозрасчетный.Записывать         = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеДвиженийРегистраНДСПредъявленный

Функция НулевыеСуммыВТаблице(Таблица, ОтслеживатьРасхождения)
	
	НулевыеСуммы = Ложь;
	
	Если Таблица = Неопределено Тогда 
		НулевыеСуммы = Истина;
		Возврат НулевыеСуммы;
	КонецЕсли;
	
	Если Таблица.Итог("НДС") = 0
		И Таблица.Итог("СуммаБезНДС") = 0 Тогда
		Если ОтслеживатьРасхождения Тогда
			НулевыеСуммы = Таблица.Итог("СуммаНДСПерепоставкиРуб") = 0 И Таблица.Итог("СуммаПерепоставкиРуб") = 0;
		Иначе 
			НулевыеСуммы = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НулевыеСуммы;
	
КонецФункции

Процедура СформироватьДвиженияНДСПредъявленный(ДанныеДвижений, Движения, Отказ, Сторно = Ложь)
	
	Если Не ЗначениеЗаполнено(ДанныеДвижений) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ДанныеДвижений Цикл
		
		Если СтрокаТаблицы.НДС = 0 И СтрокаТаблицы.СуммаБезНДС = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетФактура) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Сторно Тогда
			Запись = Движения.НДСПредъявленный.ДобавитьРасход();
		Иначе
			Запись = Движения.НДСПредъявленный.ДобавитьПриход();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		
	КонецЦикла;
	
	Движения.НДСПредъявленный.Записывать = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеДвиженийРегистраНДСПредъявленныйРеализация0

Процедура СформироватьДвиженияНДСПредъявленныйРеализация0ПоступлениеЦенностей(ДанныеДвижений, Движения, Отказ, Сторно = Ложь)

	Если ДанныеДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл

		Запись = Движения.НДСПредъявленныйРеализация0.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);

		Если Сторно Тогда
			Запись.СуммаБезНДС = -Запись.СуммаБезНДС;
		    Запись.НДС = -Запись.НДС;
		КонецЕсли;
		
	КонецЦикла;

	Движения.НДСПредъявленныйРеализация0.Записывать = Истина;

КонецПроцедуры

Процедура ОчиститьДвиженияНДСПредъявленныйРеализация0(Движения, Отказ)
	
	Движения.НДСПредъявленныйРеализация0.Записать();		
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеДвиженийРегистраНДСРаздельныйУчет

Процедура СформироватьДвиженияНДСРаздельныйУчетПоступлениеЦенностей(ДанныеДвижений, Движения, Отказ) Экспорт

	Если ДанныеДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл

		Запись = Движения.НДСРаздельныйУчет.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
	
	КонецЦикла;

	Движения.НДСРаздельныйУчет.Записывать = Истина;

КонецПроцедуры

Процедура СформироватьДвиженияНДСРаздельныйУчетВыбытиеЦенностей(СписанныеПартииНДС, Движения, Отказ)

	Если СписанныеПартииНДС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из СписанныеПартииНДС Цикл

		ЗаписьРасход = Движения.НДСРаздельныйУчет.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(ЗаписьРасход, СтрокаТаблицы);

	КонецЦикла;

	Движения.НДСРаздельныйУчет.Записывать = Истина;

КонецПроцедуры

Процедура СформироватьДвиженияНДСРаздельныйУчетПоступлениеИзНТТ(Товары, Реквизиты, Движения, Отказ) Экспорт

	Если Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	СпособОценкиМПЗПоСредней = (УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период)
		= Перечисления.СпособыОценки.ПоСредней);
	
	Для каждого СтрокаТаблицы Из Товары Цикл

		АналитикаУчетаЗатрат = Новый Структура("Организация,СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3");
		АналитикаУчетаЗатрат.Организация = Реквизиты.Организация;
		АналитикаУчетаЗатрат.СчетЗатрат = СтрокаТаблицы.СчетУчетаПолучатель;
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(АналитикаУчетаЗатрат.СчетЗатрат);
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			АналитикаУчетаЗатрат.Подразделение = Реквизиты.ПодразделениеПолучатель;
		КонецЕсли;
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура Тогда
			 	АналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(АналитикаУчетаЗатрат.СчетЗатрат) Тогда
					АналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы.СкладПолучатель;
				КонецЕсли;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
				Если НЕ СпособОценкиМПЗПоСредней Тогда
					АналитикаУчетаЗатрат["Субконто" + Ном] = Реквизиты.Регистратор;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		АналитикаУчетаНДС = Новый Структура("Организация,СчетФактура,ВидЦенности,СчетУчетаНДС,СтавкаНДС,
				|Поставщик, ИсправленныйСчетФактура");
		АналитикаУчетаНДС.Организация = Реквизиты.Организация;
		АналитикаУчетаНДС.ВидЦенности = СтрокаТаблицы.ВидЦенности;
		
		Запись = Движения.НДСРаздельныйУчет.ДобавитьПриход();

		ЗаполнитьЗначенияСвойств(Запись, Реквизиты, "Период,Организация");
		
		Запись.АналитикаУчетаЗатрат = Справочники.КлючиАналитикиУчетаЗатрат.КлючиАналитикиУчетаЗатратДокумента(АналитикаУчетаЗатрат);
		Запись.АналитикаУчетаНДС = Справочники.КлючиАналитикиУчетаНДС.КлючиАналитикиУчетаНДСДокумента(АналитикаУчетаНДС);
		Запись.Партия = Реквизиты.Регистратор;
		Запись.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределен;
		Запись.Количество = СтрокаТаблицы.Количество;
		
	КонецЦикла;

	Движения.НДСРаздельныйУчет.Записывать = Истина;

КонецПроцедуры

Процедура СформироватьДвиженияНДСРаздельныйУчетОприходованиеТоваров(Товары, Реквизиты, Движения, Отказ) Экспорт

	Если Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	СпособОценкиМПЗПоСредней = (УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период)
		= Перечисления.СпособыОценки.ПоСредней);
	
	Для каждого СтрокаТаблицы Из Товары Цикл

		АналитикаУчетаЗатрат = Новый Структура("Организация,СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3");
		АналитикаУчетаЗатрат.Организация = Реквизиты.Организация; 
		АналитикаУчетаЗатрат.СчетЗатрат = СтрокаТаблицы.СчетУчета;
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(АналитикаУчетаЗатрат.СчетЗатрат);
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			АналитикаУчетаЗатрат.Подразделение = Реквизиты.Подразделение;
		КонецЕсли;
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура Тогда
			 	АналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы.Номенклатура;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады Тогда
				Если БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(АналитикаУчетаЗатрат.СчетЗатрат) Тогда
					АналитикаУчетаЗатрат["Субконто" + Ном] = Реквизиты.Склад;
				КонецЕсли;
			ИначеЕсли СвойстваСчета["ВидСубконто" + Ном] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии Тогда
				Если НЕ СпособОценкиМПЗПоСредней Тогда
					АналитикаУчетаЗатрат["Субконто" + Ном] = СтрокаТаблицы.Партия;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		АналитикаУчетаНДС = Новый Структура("Организация,СчетФактура,ВидЦенности,СчетУчетаНДС,СтавкаНДС,
			|Поставщик,ИсправленныйСчетФактура");
		АналитикаУчетаНДС.Организация = Реквизиты.Организация;
		АналитикаУчетаНДС.ВидЦенности = УчетНДС.ОпределитьВидЦенностиПоОперации(СтрокаТаблицы.Номенклатура,
			СтрокаТаблицы.СчетУчета, , , , , Ложь);
		АналитикаУчетаНДС.СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ; // 19.03
		АналитикаУчетаНДС.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
		АналитикаУчетаНДС.Поставщик = Справочники.Контрагенты.ПустаяСсылка();
		АналитикаУчетаНДС.ИсправленныйСчетФактура = Документы.КорректировкаПоступления.ПустаяСсылка();
		
		Запись = Движения.НДСРаздельныйУчет.ДобавитьПриход();

		ЗаполнитьЗначенияСвойств(Запись, Реквизиты, "Период,Организация");
		
		Запись.АналитикаУчетаЗатрат = Справочники.КлючиАналитикиУчетаЗатрат.КлючиАналитикиУчетаЗатратДокумента(АналитикаУчетаЗатрат);
		Запись.АналитикаУчетаНДС = Справочники.КлючиАналитикиУчетаНДС.КлючиАналитикиУчетаНДСДокумента(АналитикаУчетаНДС);
		Запись.Партия = Реквизиты.Регистратор;
		Запись.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.Распределен;
		Запись.Количество = СтрокаТаблицы.Количество;

	КонецЦикла;

	Движения.НДСРаздельныйУчет.Записывать = Истина;

КонецПроцедуры

#КонецОбласти

#Область ФормированиеДвиженийРегистраБухгалтерии

Процедура СформироватьПроводкиВключениеНДСВСтоимость(ДанныеПроводок, Движения, Отказ) Экспорт
	
	Если ДанныеПроводок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СчетаУчетаОС = СчетаУчетаОС();
	
	// Проводки по включению НДС в стоимость
	// Дт <Счет учета затрат> Кт <Счет учета НДС>

	Для каждого СтрокаТаблицы Из ДанныеПроводок Цикл

		Если СтрокаТаблицы.НДС = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетФактура) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭтоОС = СчетаУчетаОС.Найти(СтрокаТаблицы.СчетЗатрат) <> Неопределено;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = СтрокаТаблицы.Период;
		Проводка.Организация = СтрокаТаблицы.Организация;
		Проводка.Сумма       = СтрокаТаблицы.НДС;
		Проводка.Содержание  = СтрокаТаблицы.Содержание;

		Если ЭтоОС Тогда
			Проводка.СчетДт = ПланыСчетов.Хозрасчетный.ПриобретениеОсновныхСредств;
		Иначе
			Проводка.СчетДт = СтрокаТаблицы.СчетЗатрат;
		КонецЕсли;
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
		Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
			
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
				СвойстваСчета["ВидСубконто" + Ном], СтрокаТаблицы["Субконто" + Ном]);	
			
		КонецЦикла;
			
		Если СвойстваСчета.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		
		Если СвойстваСчета.НалоговыйУчет Тогда
			Проводка.СуммаНУДт = Проводка.Сумма;
		КонецЕсли;
	
		Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"Контрагенты", СтрокаТаблицы.Поставщик);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СФПолученные", СтрокаТаблицы.СчетФактура);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СпособыУчетаНДС", СтрокаТаблицы.НовыйСпособУчетаНДС);
			
		Если ЭтоОС Тогда
			
			Проводка = Движения.Хозрасчетный.Добавить();
			Проводка.Период      = СтрокаТаблицы.Период;
			Проводка.Организация = СтрокаТаблицы.Организация;
			Проводка.Сумма       = СтрокаТаблицы.НДС;
			Проводка.Содержание  = СтрокаТаблицы.Содержание;
			
			Проводка.СчетДт = СтрокаТаблицы.СчетЗатрат;
			
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
			Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
				
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
					СвойстваСчета["ВидСубконто" + Ном], СтрокаТаблицы["Субконто" + Ном]);	
				
			КонецЦикла;
			
			Если СвойстваСчета.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если СвойстваСчета.НалоговыйУчет Тогда
				Проводка.СуммаНУДт = Проводка.Сумма;
			КонецЕсли;
			
			Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ПриобретениеОсновныхСредств;
			
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
				
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
					СвойстваСчета["ВидСубконто" + Ном], СтрокаТаблицы["Субконто" + Ном]);	
				
			КонецЦикла;
			
			Если СвойстваСчета.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Если СвойстваСчета.НалоговыйУчет Тогда
				Проводка.СуммаНУКт = Проводка.Сумма;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
		
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Процедура СформироватьПроводкиВключениеНДСВСтоимостьОСиНМА(ПартииНДС, Реквизиты, Движения, Отказ, Выбытие = Истина)
	
	ТаблицаОСиНМА = ПартииНДС.СкопироватьКолонки("СчетЗатрат,Субконто1,НДС,ПроцентАмортизационнойПремии");	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПартииНДС", ПартииНДС);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПартииНДС.АналитикаУчетаЗатрат КАК АналитикаУчетаЗатрат
	|ПОМЕСТИТЬ ПартииНДС
	|ИЗ
	|	&ПартииНДС КАК ПартииНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииНДС.АналитикаУчетаЗатрат,
	|	РегистрАналитикаУчетаЗатрат.СчетЗатрат,
	|	РегистрАналитикаУчетаЗатрат.Подразделение,
	|	РегистрАналитикаУчетаЗатрат.Субконто1,
	|	РегистрАналитикаУчетаЗатрат.Субконто2,
	|	РегистрАналитикаУчетаЗатрат.Субконто3
	|ИЗ
	|	ПартииНДС КАК ПартииНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|		ПО ПартииНДС.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.КлючАналитики";
	
	РазвернутыеАналитики = Запрос.Выполнить().Выгрузить();
	РазвернутыеАналитики.Индексы.Добавить("АналитикаУчетаЗатрат");
		
	ОтражатьВНалоговомУчете = (ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.МодернизацияОС")
		ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуОС"))
		И УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	
	Для Каждого СтрокаТаблицы Из ПартииНДС Цикл
		
		Если Выбытие Тогда
			
			Если СтрокаТаблицы.НДСУчитываетсяВCтоимости = 0
			 ИЛИ СтрокаТаблицы.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости Тогда
				Продолжить;
			КонецЕсли;
			
			//Считаем, что восстанавливаемый НДС в первую очередь относится к экспорту
			ОстатокВосстановленногоНДС = Макс(СтрокаТаблицы.НДСПринятоКВычету - СтрокаТаблицы.НДСДляОперацийПо0, 0);
			
			НДС = СтрокаТаблицы.НДСУчитываетсяВCтоимости - ОстатокВосстановленногоНДС;
			
		Иначе
			ОстатокВосстановленногоНДС = 0;
			НДС = СтрокаТаблицы.НДС;
		КонецЕсли;
		
		Если НДС <> 0 Тогда
			
			РазвернутаяАналитика = РазвернутыеАналитики.НайтиСтроки(
				Новый Структура("АналитикаУчетаЗатрат",СтрокаТаблицы.АналитикаУчетаЗатрат));
			АналитикаЗатрат = РазвернутаяАналитика[0];
			
			Проводка = Движения.Хозрасчетный.Добавить();
			
			Проводка.Период      = СтрокаТаблицы.Период;
			Проводка.Организация = СтрокаТаблицы.Организация;
			Проводка.Сумма       = НДС;
			Проводка.Содержание  = НСтр("ru = 'НДС включен в стоимость ценностей'");

			Проводка.СчетДт = АналитикаЗатрат.СчетЗатрат;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, АналитикаЗатрат.Субконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, АналитикаЗатрат.Субконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, АналитикаЗатрат.Субконто3);
			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = АналитикаЗатрат.Подразделение;
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"Контрагенты", СтрокаТаблицы.Поставщик);	
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"СФПолученные", СтрокаТаблицы.СчетФактура);	
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"СпособыУчетаНДС", Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости);	
				
			Если ОтражатьВНалоговомУчете Тогда
				Проводка.СуммаНУДт = Проводка.Сумма;		
			КонецЕсли;	
				
			Проводка = Движения.Хозрасчетный.Добавить();
			
			Проводка.Период      = СтрокаТаблицы.Период;
			Проводка.Организация = СтрокаТаблицы.Организация;
			Проводка.Сумма       = НДС;
			Проводка.Содержание  = НСтр("ru = 'НДС включен в стоимость ценностей'");
			
			Проводка.СчетДт = СтрокаТаблицы.СчетЗатрат;
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
			Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
					СвойстваСчета["ВидСубконто" + Ном], СтрокаТаблицы["Субконто" + Ном]);	
			КонецЦикла;
			Если СвойстваСчета.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
		
			Проводка.СчетКт = АналитикаЗатрат.СчетЗатрат;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, АналитикаЗатрат.Субконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, АналитикаЗатрат.Субконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, АналитикаЗатрат.Субконто3);
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = АналитикаЗатрат.Подразделение;
			КонецЕсли;
			
			Если ОтражатьВНалоговомУчете Тогда
				Проводка.СуммаНУДт = Проводка.Сумма;
				Проводка.СуммаНУКт = Проводка.Сумма;
			КонецЕсли;	
		
			
			НоваяСтрока = ТаблицаОСиНМА.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"НДС");
			НоваяСтрока.НДС = НДС;
			
		КонецЕсли;
		
		Если ОстатокВосстановленногоНДС > 0 Тогда
			
			Проводка = Движения.Хозрасчетный.Добавить();
			
			Проводка.Период      = СтрокаТаблицы.Период;
			Проводка.Организация = СтрокаТаблицы.Организация;
			Проводка.Сумма       = ОстатокВосстановленногоНДС;
			Проводка.Содержание  = НСтр("ru = 'Списан на расходы НДС, ранее принятый к вычету'");
			
			Проводка.СчетДт = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
			БухгалтерскийУчет.УстановитьСубконто(
				Проводка.СчетДт,
				Проводка.СубконтоДт,
				ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы,
				Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("СписаниеНДСНаПрочиеРасходы"));
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			Если СвойстваСчета.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"Контрагенты", СтрокаТаблицы.Поставщик);	
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"СФПолученные", СтрокаТаблицы.СчетФактура);	
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"СпособыУчетаНДС", Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости);
				
			Если ОтражатьВНалоговомУчете И СвойстваСчета.НалоговыйУчет Тогда
				Проводка.СуммаНУДт = Проводка.Сумма;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
	Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуОС") Тогда
		СформироватьДвиженияПоСтоимостиОСПриПринятииКУчету(ТаблицаОСиНМА, Реквизиты, Движения, Отказ);
	ИначеЕсли ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуНМА") Тогда
		СформироватьДвиженияПоСтоимостиНМАПриПринятииКУчету(ТаблицаОСиНМА, Реквизиты, Движения, Отказ);
	ИначеЕсли ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.МодернизацияОС") Тогда
		СформироватьДвиженияПоСтоимостиОСПриМодернизации(ТаблицаОСиНМА, Реквизиты, Движения, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьПроводкиВключениеНДСВСтоимостьОСиНМАРаспределениеНДС(ДанныеПроводок, Движения, Отказ)
	
	Если ДанныеПроводок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из ДанныеПроводок Цикл
		
		Если СтрокаТаблицы.НДС = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		
		ЗаполнитьЗначенияСвойств(Проводка, СтрокаТаблицы, "Период,Организация,Содержание");
		Проводка.Сумма = СтрокаТаблицы.НДС;
		
		Если СтрокаТаблицы.ЕстьКорАналитикаУчета Тогда
			
			Проводка.СчетДт = СтрокаТаблицы.КорСчетЗатрат;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СтрокаТаблицы.КорСубконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СтрокаТаблицы.КорСубконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СтрокаТаблицы.КорСубконто3);
			СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
			Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"Контрагенты", СтрокаТаблицы.Поставщик);	
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"СФПолученные", СтрокаТаблицы.СчетФактура);	
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"СпособыУчетаНДС", СтрокаТаблицы.НовыйСпособУчетаНДС);
				
			Проводка = Движения.Хозрасчетный.Добавить();
			ЗаполнитьЗначенияСвойств(Проводка, СтрокаТаблицы, "Период,Организация,Содержание");
			Проводка.Сумма = СтрокаТаблицы.НДС;
			Проводка.НеКорректироватьСтоимостьАвтоматически = Истина;
			
			Проводка.СчетДт = СтрокаТаблицы.СчетЗатрат;
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
			Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
					СвойстваСчета["ВидСубконто" + Ном], СтрокаТаблицы["Субконто" + Ном]);	
			КонецЦикла;
			Если СвойстваСчета.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаТаблицы.КорСчетЗатрат;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СтрокаТаблицы.КорСубконто1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.КорСубконто2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СтрокаТаблицы.КорСубконто3);
			СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
			Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
		Иначе
			
			Проводка.СчетДт = СтрокаТаблицы.СчетЗатрат;
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
			Для Ном = 1 По СвойстваСчета.КоличествоСубконто Цикл
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
					СвойстваСчета["ВидСубконто" + Ном], СтрокаТаблицы["Субконто" + Ном]);	
			КонецЦикла;
			Если СвойстваСчета.УчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"Контрагенты", СтрокаТаблицы.Поставщик);	
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"СФПолученные", СтрокаТаблицы.СчетФактура);	
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
				"СпособыУчетаНДС", СтрокаТаблицы.НовыйСпособУчетаНДС);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
		
КонецПроцедуры

Процедура СформироватьПроводкиПоКорректировкеСпособаУчетаНДС(ДанныеПроводок, Реквизиты, Движения, Отказ)
	
	Если ДанныеПроводок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ДанныеПроводок Цикл
		
		Если СтрокаТаблицы.НДС = 0 Тогда
			Продолжить;
		КонецЕсли;

		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Сумма       = СтрокаТаблицы.НДС;
		Проводка.Содержание  = СтрокаТаблицы.Содержание;

		Проводка.СчетДт = СтрокаТаблицы.СчетУчетаНДС;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
			"Контрагенты", СтрокаТаблицы.Поставщик);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
			"СФПолученные", СтрокаТаблицы.СчетФактура);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
			"СпособыУчетаНДС", СтрокаТаблицы.НовыйСпособУчетаНДС);	
		
		Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"Контрагенты", СтрокаТаблицы.Поставщик);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СФПолученные", СтрокаТаблицы.СчетФактура);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СпособыУчетаНДС", СтрокаТаблицы.СпособУчетаНДС);	
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Процедура СформироватьПроводкиВключениеНДСВРасходы(ДанныеПроводок, Движения, Отказ)

	// Проводки по отнесению НДС, ранее принятого к вычету, на расходы
	// Дт <Счет списания НДС> Кт <Счет учета НДС>

	Для каждого СтрокаТаблицы Из ДанныеПроводок Цикл

		Если СтрокаТаблицы.НДС = 0 Тогда
			Продолжить;
		КонецЕсли;

		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = СтрокаТаблицы.Период;
		Проводка.Организация = СтрокаТаблицы.Организация;
		Проводка.Сумма       = СтрокаТаблицы.НДС;
		Проводка.Содержание  = НСтр("ru = 'Списан на расходы НДС, ранее принятый к вычету'");

		Проводка.СчетДт = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
		
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаТаблицы.Подразделение;
		КонецЕсли;
		Если СвойстваСчетаДт.НалоговыйУчет Тогда
			Проводка.СуммаНУДт = Проводка.Сумма;
		КонецЕсли;
		
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетДт,
			Проводка.СубконтоДт,
			"ПрочиеДоходыИРасходы",
			Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("СписаниеНДСНаПрочиеРасходы"));

		Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"Контрагенты", СтрокаТаблицы.Поставщик);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СФПолученные", СтрокаТаблицы.СчетФактура);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СпособыУчетаНДС", Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости);	

	КонецЦикла;

	Движения.Хозрасчетный.Записывать = Истина;

КонецПроцедуры

Процедура СформироватьПроводкиИсключениеНДСИзСтоимости(ТаблицаИсключениеНДСИзСтоимости, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ)

	// Проводки по исключению НДС Из стоимости
	// дебет <Корсчет списания> Кт <Счет учета НДС> сторно

	Если ТаблицаИсключениеНДСИзСтоимости.Количество() = 0
		ИЛИ ТаблицаИсключениеНДСИзСтоимости.Итог("НДС") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СпособОценкиМПЗПоСредней = (УчетнаяПолитика.СпособОценкиМПЗ(Реквизиты.Организация, Реквизиты.Период)
		= Перечисления.СпособыОценки.ПоСредней);
	
	Для каждого СтрокаТаблицы Из ТаблицаИсключениеНДСИзСтоимости Цикл

		Если СтрокаТаблицы.НДС = 0 Тогда
			Продолжить;
		КонецЕсли;

		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Сумма       = СтрокаТаблицы.НДСУчитываетсяВCтоимости - СтрокаТаблицы.НДС;
		Проводка.Содержание  = "НДС исключен из стоимости МПЗ";

		Отбор = Новый Структура("ИмяСписка,НомерСтроки", СтрокаТаблицы.ИмяСписка, СтрокаТаблицы.НомерСтрокиДокумента);
		Если НЕ СпособОценкиМПЗПоСредней Тогда
			Отбор.Вставить("АналитикаУчетаЗатрат", СтрокаТаблицы.АналитикаУчетаЗатрат);
		КонецЕсли;
		
		СтрокиСписанияБухУчет = СписанныеТоварыБухУчет.НайтиСтроки(Отбор);
		СтрокаСписанияБухУчет = СтрокиСписанияБухУчет[0];
		
		Проводка.СчетДт = СтрокаСписанияБухУчет.КорСчетСписания;
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		
		Для НомерСубконто = 1 По 3 Цикл
			ВидСубконто = СтрокаСписанияБухУчет["ВидКорСубконто" + НомерСубконто];
			Если ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии
				И СпособОценкиМПЗПоСредней Тогда
				Продолжить;
			КонецЕсли;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
				ВидСубконто, СтрокаСписанияБухУчет["КорСубконто" + НомерСубконто]);
		КонецЦикла;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Номенклатура", СтрокаСписанияБухУчет.Номенклатура);
		Если НЕ СпособОценкиМПЗПоСредней Тогда
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Партии", СтрокаСписанияБухУчет.Партия);
		КонецЕсли;
		
		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаСписанияБухУчет.КорПодразделение;
		КонецЕсли;
		Если СвойстваСчетаДт.НалоговыйУчет Тогда
			Проводка.СуммаНУДт = Проводка.Сумма;
		КонецЕсли;

		Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"Контрагенты", СтрокаТаблицы.Поставщик);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СФПолученные", СтрокаТаблицы.СчетФактура);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СпособыУчетаНДС", СтрокаТаблицы.СпособУчетаНДС);	
	
	КонецЦикла;

	Движения.Хозрасчетный.Записывать = Истина;

КонецПроцедуры

Процедура СформироватьПроводкиИсключениеНДСИзСтоимостиОСиНМА(ТаблицаИсключениеНДСИзСтоимости, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ)

	Если ТаблицаИсключениеНДСИзСтоимости.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ТаблицаОСиНМА = ТаблицаИсключениеНДСИзСтоимости.СкопироватьКолонки("СчетЗатрат,Субконто1,НДС,ПроцентАмортизационнойПремии");	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаИсключениеНДСИзСтоимости", ТаблицаИсключениеНДСИзСтоимости);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаИсключениеНДСИзСтоимости.АналитикаУчетаЗатрат КАК АналитикаУчетаЗатрат
	|ПОМЕСТИТЬ ТаблицаИсключениеНДСИзСтоимости
	|ИЗ
	|	&ТаблицаИсключениеНДСИзСтоимости КАК ТаблицаИсключениеНДСИзСтоимости
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИсключениеНДСИзСтоимости.АналитикаУчетаЗатрат,
	|	РегистрАналитикаУчетаЗатрат.СчетЗатрат,
	|	РегистрАналитикаУчетаЗатрат.Подразделение,
	|	РегистрАналитикаУчетаЗатрат.Субконто1,
	|	РегистрАналитикаУчетаЗатрат.Субконто2,
	|	РегистрАналитикаУчетаЗатрат.Субконто3
	|ИЗ
	|	ТаблицаИсключениеНДСИзСтоимости КАК ТаблицаИсключениеНДСИзСтоимости
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаЗатрат КАК РегистрАналитикаУчетаЗатрат
	|		ПО ТаблицаИсключениеНДСИзСтоимости.АналитикаУчетаЗатрат = РегистрАналитикаУчетаЗатрат.КлючАналитики";
	
	РазвернутыеАналитики = Запрос.Выполнить().Выгрузить();
	РазвернутыеАналитики.Индексы.Добавить("АналитикаУчетаЗатрат");
	
	ОтражатьВНалоговомУчете = (ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.МодернизацияОС")
		ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуОС"))
		И УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	
	Для каждого СтрокаТаблицы Из ТаблицаИсключениеНДСИзСтоимости Цикл

		НДС = СтрокаТаблицы.НДСУчитываетсяВCтоимости - СтрокаТаблицы.НДС;
		
		Если НДС = 0 Тогда
			Продолжить;
		КонецЕсли;

		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Сумма       = НДС;
		Проводка.Содержание  = "НДС исключен из стоимости";

		РазвернутаяАналитика = РазвернутыеАналитики.НайтиСтроки(
			Новый Структура("АналитикаУчетаЗатрат",СтрокаТаблицы.АналитикаУчетаЗатрат));
		АналитикаЗатрат = РазвернутаяАналитика[0];
		
		Проводка.СчетДт = АналитикаЗатрат.СчетЗатрат;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, АналитикаЗатрат.Субконто1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, АналитикаЗатрат.Субконто2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, АналитикаЗатрат.Субконто3);
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = АналитикаЗатрат.Подразделение;
		КонецЕсли;

		Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"Контрагенты", СтрокаТаблицы.Поставщик);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СФПолученные", СтрокаТаблицы.СчетФактура);	
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СпособыУчетаНДС", СтрокаТаблицы.СпособУчетаНДС);
			
		Если ОтражатьВНалоговомУчете Тогда
			Проводка.СуммаНУДт = НДС;		
		КонецЕсли;	
		
			
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Сумма       = НДС;
		Проводка.Содержание  = "НДС исключен из стоимости";

		Отбор = Новый Структура("ИмяСписка,НомерСтроки", СтрокаТаблицы.ИмяСписка, СтрокаТаблицы.НомерСтрокиДокумента);
		СтрокиСписанияБухУчет = СписанныеТоварыБухУчет.НайтиСтроки(Отбор);
		СтрокаСписанияБухУчет = СтрокиСписанияБухУчет[0];

		Проводка.СчетДт = СтрокаСписанияБухУчет.КорСчетСписания;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
			"Партии", СтрокаСписанияБухУчет.Партия);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
			СтрокаСписанияБухУчет.ВидКорСубконто1, СтрокаСписанияБухУчет.КорСубконто1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
			СтрокаСписанияБухУчет.ВидКорСубконто2, СтрокаСписанияБухУчет.КорСубконто2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
			СтрокаСписанияБухУчет.ВидКорСубконто3, СтрокаСписанияБухУчет.КорСубконто3);
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаСписанияБухУчет.КорПодразделение;
		КонецЕсли;

		Проводка.СчетКт = АналитикаЗатрат.СчетЗатрат;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, АналитикаЗатрат.Субконто1);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, АналитикаЗатрат.Субконто2);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, АналитикаЗатрат.Субконто3);
		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
		Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = АналитикаЗатрат.Подразделение;
		КонецЕсли;
		
		Если ОтражатьВНалоговомУчете Тогда
			Проводка.СуммаНУДт = НДС;
			Проводка.СуммаНУКт = НДС;
		КонецЕсли;	
		
		
		НоваяСтрока = ТаблицаОСиНМА.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ,"НДС");
		НоваяСтрока.НДС = НДС;
		
	КонецЦикла;

	Движения.Хозрасчетный.Записывать = Истина;

	Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуОС") Тогда
		СформироватьДвиженияПоСтоимостиОСПриПринятииКУчету(ТаблицаОСиНМА, Реквизиты, Движения, Отказ);
	ИначеЕсли ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПринятиеКУчетуНМА") Тогда
		СформироватьДвиженияПоСтоимостиНМАПриПринятииКУчету(ТаблицаОСиНМА, Реквизиты, Движения, Отказ);
	ИначеЕсли ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.МодернизацияОС") Тогда
		СформироватьДвиженияПоСтоимостиОСПриМодернизации(ТаблицаОСиНМА, Реквизиты, Движения, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьПроводкиВычетНДСПоТоварамРеализованнымПоСтавке0(ДанныеДвижений, Движения, Отказ) Экспорт

	// Проводки Дт 68.хх Кт 19.хх

	Для каждого СтрокаТаблицы Из ДанныеДвижений Цикл

		Если СтрокаТаблицы.НДС = 0 Тогда
			Продолжить;
		КонецЕсли;

		Проводка = Движения.Хозрасчетный.Добавить();

		Проводка.Период      = СтрокаТаблицы.Период;
		Проводка.Организация = СтрокаТаблицы.Организация;

		Проводка.Содержание = СокрЛП(СтрокаТаблицы.Содержание);

		Если СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.ТоварыНалоговыйАгент Тогда
			Проводка.СчетДт = ПланыСчетов.Хозрасчетный.НДСНалоговогоАгентаПоОтдельнымВидамТоваров; // 68.52
		Иначе
			Проводка.СчетДт = ПланыСчетов.Хозрасчетный.НДС; // 68.02
		КонецЕсли;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт,
			"ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);

		Проводка.СчетКт = СтрокаТаблицы.СчетУчетаНДС;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СФПолученные", СтрокаТаблицы.СчетФактура);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"Контрагенты", СтрокаТаблицы.Поставщик);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт,
			"СпособыУчетаНДС", Перечисления.СпособыУчетаНДС.ДляОперацийПо0);

		Проводка.Сумма = СтрокаТаблицы.НДС;

	КонецЦикла;

	Движения.Хозрасчетный.Записывать = Истина;

КонецПроцедуры

Процедура СформироватьПроводкиСторноВосстановленияНДС(Реквизиты, Движения, Отказ)
	
	НаборЗаписейХозрасчетный = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	НаборЗаписейХозрасчетный.Отбор.Регистратор.Значение = Реквизиты.ДокументОснование;
	НаборЗаписейХозрасчетный.Прочитать();
	
	Для Каждого ДвижениеСторнируемое Из НаборЗаписейХозрасчетный Цикл

		Если ДвижениеСторнируемое.СчетКт = ПланыСчетов.Хозрасчетный.НДС
			И ДвижениеСторнируемое.Сумма > 0 Тогда
			
			Движение = Движения.Хозрасчетный.Добавить();
			
			Движение.Период = Реквизиты.Период;
			
			ЗаполнитьЗначенияСвойств(Движение, ДвижениеСторнируемое, , "Период, Регистратор");
			
			Для каждого Субконто Из ДвижениеСторнируемое.СубконтоДт Цикл
				Движение.СубконтоДт[Субконто.Ключ] = Субконто.Значение;
			КонецЦикла;
			
			Для каждого Субконто Из ДвижениеСторнируемое.СубконтоКт Цикл
				Движение.СубконтоКт[Субконто.Ключ] = Субконто.Значение;
			КонецЦикла;
			
			Движение.Сумма = -ДвижениеСторнируемое.Сумма;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// В процедуре подготоваливается таблица для включения стоимости ОС/НМА в расходы НУ при принятии к учету ОС/НМА
// Это требуется в случае, если в принятии к учету был изменен способ учета ОС/НМА на учитывается в стоимости
// и тогда в стоимость расходов включалась сумма без НДС, а сумма НДС терялась
// Для формирования недостающей проводки формируется таблица с данными и вызывается стандартная процедура
// в которой осуществляется непосредственное формирование проводок.
// Параметры:
//  СписанныеПартииНДС - таблица значений, по которой мы определяем по каким объектам было изменение способа учета НДС
//    и сумму НДС
//  ТаблицаЗатратПоВключениюНДСВСоставРасходов - таблица значений, в которой находятся данные об объекте ОС/НМА
//    для включения в стоимость
Процедура ВключитьНДСВРасходыНУ(СписанныеПартииНДС, ТаблицаЗатратПоВключениюНДСВСоставРасходов,
		Реквизиты, Движения, Отказ) Экспорт
	
	Если ТаблицаЗатратПоВключениюНДСВСоставРасходов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЗатрат = ТаблицаЗатратПоВключениюНДСВСоставРасходов.СкопироватьКолонки("ОбъектУчета, ПодразделениеЗатрат,
		|Подразделение,Субконто1, Субконто2, Субконто3, СуммаБУ, СуммаНУ, СуммаПР, СуммаВР, СчетЗатрат, СчетАмортизации");
	
	ТаблицаРеквизиты = Новый ТаблицаЗначений();
	ТаблицаРеквизиты.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаРеквизиты.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникССылка.Организации"));
	ТаблицаРеквизиты.Колонки.Добавить("Содержание",Новый ОписаниеТипов("Строка"));
	МассивТипов = Новый Массив();
	МассивТипов.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуОС"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуНМА"));
	ОписаниеТиповРегистратор = Новый ОписаниеТипов(МассивТипов);
	
	ТаблицаРеквизиты.Колонки.Добавить("Регистратор", ОписаниеТиповРегистратор);
	ТаблицаРеквизиты.Колонки.Добавить("ИмяСписка", Новый ОписаниеТипов("Строка"));
	
	Отбор = Новый Структура();
	Отбор.Вставить("СпособУчетаНДСИзменился", Истина);
	Отбор.Вставить("НовыйСпособУчетаНДС", Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости);
	СтрокиСИзмененнымСпособомУчетаНДС = СписанныеПартииНДС.НайтиСтроки(Отбор);
	
	Для каждого СтрокаПоСпособуУчета Из СтрокиСИзмененнымСпособомУчетаНДС Цикл
		СтрокаЗатрат = ТаблицаЗатратПоВключениюНДСВСоставРасходов.Найти(СтрокаПоСпособуУчета.Субконто1, "ОбъектУчета");
		Если СтрокаЗатрат <> Неопределено Тогда
			НовСтрока = ТаблицаЗатрат.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, СтрокаЗатрат);
			Если НовСтрока.СуммаБУ < 0 Тогда
				НовСтрока.СуммаБУ = - СтрокаПоСпособуУчета.НДС;
			ИначеЕсли НовСтрока.СуммаБУ > 0 Тогда
				НовСтрока.СуммаБУ = СтрокаПоСпособуУчета.НДС;
			КонецЕсли;
			Если НовСтрока.СуммаНУ < 0 Тогда
				НовСтрока.СуммаНУ = - СтрокаПоСпособуУчета.НДС;
			ИначеЕсли НовСтрока.СуммаНУ > 0 Тогда
				НовСтрока.СуммаНУ = СтрокаПоСпособуУчета.НДС;
			КонецЕсли;
			Если НовСтрока.СуммаВР < 0 Тогда
				НовСтрока.СуммаВР = - СтрокаПоСпособуУчета.НДС;
			ИначеЕсли НовСтрока.СуммаВР > 0 Тогда
				НовСтрока.СуммаВР = СтрокаПоСпособуУчета.НДС;
			КонецЕсли;
			Если НовСтрока.СуммаПР < 0 Тогда
				НовСтрока.СуммаПР = - СтрокаПоСпособуУчета.НДС;
			ИначеЕсли НовСтрока.СуммаПР > 0 Тогда
				НовСтрока.СуммаПР = СтрокаПоСпособуУчета.НДС;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СтрокаРеквизитов = ТаблицаРеквизиты.Добавить();
	СтрокаРеквизитов.Период = Реквизиты.Период;
	СтрокаРеквизитов.Организация = Реквизиты.Организация;
	СтрокаРеквизитов.Регистратор = Реквизиты.Регистратор;
	СтрокаРеквизитов.Содержание = НСтр("ru = 'Включение стоимости в состав расходов (НУ)'");
	Если СписанныеПартииНДС.Количество() > 0 Тогда 
		СтрокаРеквизитов.ИмяСписка = СписанныеПартииНДС[0].ИмяСписка;
	КонецЕсли;
	
	УправлениеВнеоборотнымиАктивами.СформироватьДвиженияНачислениеАмортизации(ТаблицаЗатрат, ТаблицаРеквизиты,
		Движения, Отказ);
	
КонецПроцедуры
	
#КонецОбласти

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

Процедура ЗаменитьАналитикуСписанияНДСПоАренднымОбязательствам(ДанныеПроводок, Реквизиты)
	
	ДанныеПроводокПоАренднымОбязательствам = Новый Массив;
	
	Контрагенты = Новый Массив;
	ДоговорыКонтрагентов = Новый Массив;
	
	Для каждого СтрокаТаблицы Из ДанныеПроводок Цикл
		
		Если НЕ УчетОС.СчетУчетаАрендныхОбязательств(СтрокаТаблицы.СчетЗатрат) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеПроводокПоАренднымОбязательствам.Добавить(СтрокаТаблицы);
		
		Контрагенты.Добавить(СтрокаТаблицы.Субконто1);
		ДоговорыКонтрагентов.Добавить(СтрокаТаблицы.Субконто2);
		
	КонецЦикла;
	
	Если ДанныеПроводокПоАренднымОбязательствам.Количество() > 0 Тогда
	
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Период", Реквизиты.Период);
		Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
		Запрос.УстановитьПараметр("Контрагенты", 
			ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(Контрагенты));
		Запрос.УстановитьПараметр("ДоговорыКонтрагентов", 
			ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(ДоговорыКонтрагентов));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПервоначальныеСведенияОСБУ.ОсновноеСредство КАК ОсновноеСредство,
		|	ПервоначальныеСведенияОСБУ.ПервоначальнаяСтоимость,
		|	ПервоначальныеСведенияОСБУ.Контрагент,
		|	ПервоначальныеСведенияОСБУ.ДоговорКонтрагента
		|ПОМЕСТИТЬ ТаблицаОС
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
		|			&Период,
		|			Организация = &Организация
		|				И Контрагент В (&Контрагенты)
		|				И ДоговорКонтрагента В (&ДоговорыКонтрагентов)) КАК ПервоначальныеСведенияОСБУ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияОСОрганизацийСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	СостоянияОСОрганизацийСрезПоследних.Состояние
		|ПОМЕСТИТЬ СостоянияОС
		|ИЗ
		|	РегистрСведений.СостоянияОСОрганизаций.СрезПоследних(
		|			&Период,
		|			Организация = &Организация
		|				И ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ТаблицаОС.ОсновноеСредство
		|					ИЗ
		|						ТаблицаОС)) КАК СостоянияОСОрганизацийСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СпособыОтраженияРасходовПоАренднымПлатежамОС.ОсновноеСредство КАК ОсновноеСредство,
		|	СпособыОтраженияРасходовПоАренднымПлатежамОС.СпособОтраженияРасходовПоАренднымПлатежам КАК СпособОтраженияРасходовПоАренднымПлатежам,
		|	СпособыОтраженияРасходовПоАмортизацииСпособы.СчетЗатрат,
		|	СпособыОтраженияРасходовПоАмортизацииСпособы.ПодразделениеОрганизации,
		|	СпособыОтраженияРасходовПоАмортизацииСпособы.Субконто1,
		|	СпособыОтраженияРасходовПоАмортизацииСпособы.Субконто2,
		|	СпособыОтраженияРасходовПоАмортизацииСпособы.Субконто3,
		|	СпособыОтраженияРасходовПоАмортизацииСпособы.Коэффициент
		|ПОМЕСТИТЬ СпособыОтраженияРасходовПоАренднымПлатежам
		|ИЗ
		|	РегистрСведений.СпособыОтраженияРасходовПоАренднымПлатежамОСНалоговыйУчет.СрезПоследних(
		|			&Период,
		|			Организация = &Организация
		|				И ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ТаблицаОС.ОсновноеСредство
		|					ИЗ
		|						ТаблицаОС)) КАК СпособыОтраженияРасходовПоАренднымПлатежамОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияРасходовПоАмортизации.Способы КАК СпособыОтраженияРасходовПоАмортизацииСпособы
		|		ПО СпособыОтраженияРасходовПоАренднымПлатежамОС.СпособОтраженияРасходовПоАренднымПлатежам = СпособыОтраженияРасходовПоАмортизацииСпособы.Ссылка
		|ГДЕ
		|	СпособыОтраженияРасходовПоАренднымПлатежамОС.СпособОтраженияРасходовПоАренднымПлатежам <> ЗНАЧЕНИЕ(справочник.способыотражениярасходовпоамортизации.пустаяссылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	СпособОтраженияРасходовПоАренднымПлатежам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СпособыОтраженияРасходовПоАренднымПлатежам.ОсновноеСредство КАК ОсновноеСредство,
		|	СпособыОтраженияРасходовПоАренднымПлатежам.СпособОтраженияРасходовПоАренднымПлатежам КАК СпособОтраженияРасходовПоАренднымПлатежам,
		|	КОЛИЧЕСТВО(СпособыОтраженияРасходовПоАренднымПлатежам.Коэффициент) КАК Коэффициент
		|ПОМЕСТИТЬ СпособыОтраженияРасходовКоличество
		|ИЗ
		|	СпособыОтраженияРасходовПоАренднымПлатежам КАК СпособыОтраженияРасходовПоАренднымПлатежам
		|
		|СГРУППИРОВАТЬ ПО
		|	СпособыОтраженияРасходовПоАренднымПлатежам.ОсновноеСредство,
		|	СпособыОтраженияРасходовПоАренднымПлатежам.СпособОтраженияРасходовПоАренднымПлатежам
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	СпособОтраженияРасходовПоАренднымПлатежам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство,
		|	ТаблицаОС.Контрагент,
		|	ТаблицаОС.ДоговорКонтрагента,
		|	СостоянияОС.Состояние,
		|	СпособыОтраженияРасходовПоАренднымПлатежам.СчетЗатрат,
		|	СпособыОтраженияРасходовПоАренднымПлатежам.ПодразделениеОрганизации КАК Подразделение,
		|	СпособыОтраженияРасходовПоАренднымПлатежам.Субконто1,
		|	СпособыОтраженияРасходовПоАренднымПлатежам.Субконто2,
		|	СпособыОтраженияРасходовПоАренднымПлатежам.Субконто3,
		|	ВЫБОР
		|		КОГДА СпособыОтраженияРасходовКоличество.Коэффициент = 0
		|			ТОГДА 0
		|		ИНАЧЕ СпособыОтраженияРасходовПоАренднымПлатежам.Коэффициент * ТаблицаОС.ПервоначальнаяСтоимость / СпособыОтраженияРасходовКоличество.Коэффициент
		|	КОНЕЦ КАК Коэффициент
		|ИЗ
		|	ТаблицаОС КАК ТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СостоянияОС КАК СостоянияОС
		|		ПО ТаблицаОС.ОсновноеСредство = СостоянияОС.ОсновноеСредство
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СпособыОтраженияРасходовПоАренднымПлатежам КАК СпособыОтраженияРасходовПоАренднымПлатежам
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СпособыОтраженияРасходовКоличество КАК СпособыОтраженияРасходовКоличество
		|			ПО СпособыОтраженияРасходовПоАренднымПлатежам.ОсновноеСредство = СпособыОтраженияРасходовКоличество.ОсновноеСредство
		|				И СпособыОтраженияРасходовПоАренднымПлатежам.СпособОтраженияРасходовПоАренднымПлатежам = СпособыОтраженияРасходовКоличество.СпособОтраженияРасходовПоАренднымПлатежам
		|		ПО ТаблицаОС.ОсновноеСредство = СпособыОтраженияРасходовПоАренднымПлатежам.ОсновноеСредство";
	
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			
			СпособыОтраженияРасходов = Результат.Выгрузить();
			
			НовыеДанныеПроводок = ДанныеПроводок.СкопироватьКолонки();
			
			Для Каждого Проводка Из ДанныеПроводокПоАренднымОбязательствам Цикл
				
				СпособыОтраженияРасходовСОтбором = СпособыОтраженияРасходов.Скопировать(
					Новый Структура("Контрагент, ДоговорКонтрагента, Состояние", Проводка.Субконто1, Проводка.Субконто2, Перечисления.СостоянияОС.ПринятоКУчету));
					
				КоличествоСтрок = СпособыОтраженияРасходовСОтбором.Количество();
				
				Если КоличествоСтрок = 0 Тогда
					СпособыОтраженияРасходовСОтбором = СпособыОтраженияРасходов.Скопировать(
						Новый Структура("Контрагент, ДоговорКонтрагента", Проводка.Субконто1, Проводка.Субконто2));
						
					КоличествоСтрок = СпособыОтраженияРасходовСОтбором.Количество();
				КонецЕсли;
				
				Если КоличествоСтрок > 0 Тогда
					
					МассивСуммНДС = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
						Проводка.НДС, СпособыОтраженияРасходовСОтбором.ВыгрузитьКолонку("Коэффициент"));
						
					Если МассивСуммНДС <> Неопределено Тогда
						
						Для Сч = 0 По КоличествоСтрок - 1 Цикл
							
							НДС = МассивСуммНДС[Сч];
							
							Если НДС = 0 Тогда
								Продолжить;
							КонецЕсли;
							
							НоваяСтрока = НовыеДанныеПроводок.Добавить();
							
							ЗаполнитьЗначенияСвойств(НоваяСтрока, Проводка,,
								"СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,НДС");
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СпособыОтраженияРасходовСОтбором[Сч], 
								"СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3");
								
							НоваяСтрока.НДС = НДС;
							
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Для Каждого СтрокаТаблицы Из ДанныеПроводокПоАренднымОбязательствам Цикл
				ДанныеПроводок.Удалить(СтрокаТаблицы);
			КонецЦикла;
			
			Для Каждого СтрокаТаблицы Из НовыеДанныеПроводок Цикл
				ЗаполнитьЗначенияСвойств(ДанныеПроводок.Добавить(), СтрокаТаблицы);
			КонецЦикла;
			
			ДанныеПроводок.Свернуть("
				|Организация,Период,Содержание,
				|СчетЗатрат,Подразделение,Субконто1,Субконто2,Субконто3,
				|СчетУчетаНДС,СчетФактура,Поставщик,НовыйСпособУчетаНДС",
				"НДС");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак того, есть ли в базе хоть одна организация с раздельным учетом НДС на 19 счете.
//
// Возвращаемое значение - булево:
//                         ИСТИНА - есть организация с раздельным учетом НДС на 19 счете;
//                         ЛОЖЬ   - нет организаций с раздельным учетом НДС на 19 счете.
//
Функция ЕстьУчетнаяПолитикаСРаздельнымУчетомНДСНаСчете19() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|ГДЕ
	|	НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19 = ИСТИНА";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ПолучитьСоответствиеСтавокИСпособовУчетаНДС() Экспорт
	
	СоответствиеСтавокИСпособовУчетаНДС	= Новый Соответствие;
	
	Ставки = Перечисления.СтавкиНДС;
	СпособыУчета = Перечисления.СпособыУчетаНДС;
	
	СоответствиеСтавокИСпособовУчетаНДС.Вставить(Ставки.БезНДС, СпособыУчета.УчитываетсяВCтоимости);
	СоответствиеСтавокИСпособовУчетаНДС.Вставить(Ставки.НДС0, СпособыУчета.ДляОперацийПо0);
	СоответствиеСтавокИСпособовУчетаНДС.Вставить(Ставки.НДС10, СпособыУчета.ПринимаетсяКВычету);
	СоответствиеСтавокИСпособовУчетаНДС.Вставить(Ставки.НДС10_110, СпособыУчета.ПринимаетсяКВычету);
	СоответствиеСтавокИСпособовУчетаНДС.Вставить(Ставки.НДС18, СпособыУчета.ПринимаетсяКВычету);
	СоответствиеСтавокИСпособовУчетаНДС.Вставить(Ставки.НДС18_118, СпособыУчета.ПринимаетсяКВычету);
	СоответствиеСтавокИСпособовУчетаНДС.Вставить(Ставки.НДС20, СпособыУчета.ПринимаетсяКВычету);
	СоответствиеСтавокИСпособовУчетаНДС.Вставить(Ставки.НДС20_120, СпособыУчета.ПринимаетсяКВычету);
	СоответствиеСтавокИСпособовУчетаНДС.Вставить(Ставки.ПустаяСсылка(), СпособыУчета.ПустаяСсылка());
	
	Возврат СоответствиеСтавокИСпособовУчетаНДС;
	
КонецФункции

Функция СчетаПартионногоУчетаЦенностейДляЦелейНДС()
	
	ПланСчетов = ПланыСчетов.Хозрасчетный;
	
	ПредопределенныеСчетаУчетаЦенностей = Новый Массив;
	ПредопределенныеСчетаУчетаЦенностей.Добавить(ПланСчетов.ОборудованиеКУстановке);           // 07
	ПредопределенныеСчетаУчетаЦенностей.Добавить(ПланСчетов.ВложенияВоВнеоборотныеАктивы);     // 08
	ПредопределенныеСчетаУчетаЦенностей.Добавить(ПланСчетов.Материалы);                        // 10
	ПредопределенныеСчетаУчетаЦенностей.Добавить(ПланСчетов.ПроизводствоИзДавальческогоСырья); // 20.02
	ПредопределенныеСчетаУчетаЦенностей.Добавить(ПланСчетов.Полуфабрикаты);                    // 21
	ПредопределенныеСчетаУчетаЦенностей.Добавить(ПланСчетов.Товары);                           // 41
	ПредопределенныеСчетаУчетаЦенностей.Добавить(ПланСчетов.ГотоваяПродукция);                 // 43
	ПредопределенныеСчетаУчетаЦенностей.Добавить(ПланСчетов.ТоварыОтгруженные);                // 45
	СчетаУчетаЦенностей = БухгалтерскийУчет.СформироватьМассивСубсчетов(ПредопределенныеСчетаУчетаЦенностей);
	
	ПредопределенныеИсключаемыеСчетаЦенностей = Новый Массив;
	ПредопределенныеИсключаемыеСчетаЦенностей.Добавить(ПланСчетов.ПереводМолоднякаЖивотныхВОсновноеСтадо);     // 08.06
	ПредопределенныеИсключаемыеСчетаЦенностей.Добавить(ПланСчетов.ПриобретениеВзрослыхЖивотных);               // 08.07
	ПредопределенныеИсключаемыеСчетаЦенностей.Добавить(ПланСчетов.НематериальныеПоисковыеАктивы);              // 08.11
	ПредопределенныеИсключаемыеСчетаЦенностей.Добавить(ПланСчетов.МатериальныеПоисковыеАктивы);                // 08.12
	ПредопределенныеИсключаемыеСчетаЦенностей.Добавить(ПланСчетов.ТопливоВБаке);                               // 10.03.2
	ПредопределенныеИсключаемыеСчетаЦенностей.Добавить(ПланСчетов.СпецоснасткаИСпецодеждаВЭксплуатации);       // 10.11
	ПредопределенныеИсключаемыеСчетаЦенностей.Добавить(ПланСчетов.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ); // 41.12
	ПредопределенныеИсключаемыеСчетаЦенностей.Добавить(ПланСчетов.КорректировкаТоваровПрошлогоПериода);        // 41.К
	
	СчетаУчетаЦенностей = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаУчетаЦенностей,
		БухгалтерскийУчет.СформироватьМассивСубсчетов(ПредопределенныеИсключаемыеСчетаЦенностей));
	
	Возврат СчетаУчетаЦенностей;
	
КонецФункции

Функция СчетаУчетаОС()
	
	ПланСчетов = ПланыСчетов.Хозрасчетный;
	
	ПредопределенныеСчетаУчетаОС = Новый Массив;
	ПредопределенныеСчетаУчетаОС.Добавить(ПланСчетов.ОСвОрганизации);        // 01.01
	ПредопределенныеСчетаУчетаОС.Добавить(ПланСчетов.АрендованноеИмущество); // 01.03
	ПредопределенныеСчетаУчетаОС.Добавить(ПланСчетов.ОСБезГосРегистрации);   // 01.08
	ПредопределенныеСчетаУчетаОС.Добавить(ПланСчетов.ДоходныеВложенияВ_МЦ);  // 03.01
	
	Возврат БухгалтерскийУчет.СформироватьМассивСубсчетов(ПредопределенныеСчетаУчетаОС);
	
КонецФункции

Функция СчетаУчетаНМА()
	
	ПланСчетов = ПланыСчетов.Хозрасчетный;
	
	МассивСчетов = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланСчетов.НематериальныеАктивы)); // 04
	
	Возврат МассивСчетов;
	
КонецФункции

// Возвращает признак того, есть ли в базе хоть одна организация с раздельным учетом НДС.
//
// Возвращаемое значение - булево:
//                         ИСТИНА - есть организация с раздельным учетом НДС;
//                         ЛОЖЬ   - нет организаций с раздельным учетом НДС.
//
Функция ЕстьУчетнаяПолитикаСРаздельнымУчетомНДС() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19,
	|	НастройкиУчетаНДС.РаздельныйУчетНДСДо2014Года
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|ГДЕ
	|	НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19
	|	ИЛИ НастройкиУчетаНДС.РаздельныйУчетНДСДо2014Года";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

// Аналитики учета НДС для регистра НДС Раздельный учет и регистр НДС Преъявленный, реализация 0% 
// дополнены реквизитами
//           Поставщик и Исправленный счет-фактура
// Функция возвращает период, с которого заполняются Поставщик и Исправленный счет-фактура в регистрах
//
// Возвращаемое значение:
//  Дата
Функция ДатаНачалаВеденияСамодостаточнойАналитики() Экспорт
	Возврат '20200701'
КонецФункции

// Аналитика учета НДС для регистра НДС Раздельный учет и регистр НДС Преъявленный, реализация 0% 
// дополнены реквизитами
//           Поставщик и Исправленный счет-фактура
// Функция проверяет период, чтобы определить, формировать полные или сокращенные аналитики по НДС Раздельный учет
// и нужно ли заполнять измерение ИсправленныйСчетФактура для регистра НДС предъявленный, реализация 0%
//
// Параметры
// Дата - тип дата, в данном параметре передается
//        дата на которую необходимо определить признак применения сокращенной аналитики в регистре НДС Раздельный учет
// Возвращаемое значение:
//  Булево - признак применения
//           Истина - записи с сокращенной аналитикой
//           Ложь   - записи с самодостаточной аналитикой (с поставщиком и исправленным счет-фактурой)
Функция СокращеннаяАналитика(Дата) Экспорт
	
	ДатаНачалаВеденияСамодостаточнойАналитики = ДатаНачалаВеденияСамодостаточнойАналитики();
	
	Если Дата < ДатаНачалаВеденияСамодостаточнойАналитики Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти