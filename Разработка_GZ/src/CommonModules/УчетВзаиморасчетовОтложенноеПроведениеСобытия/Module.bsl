
#Область СлужебныйПрограммныйИнтерфейс

// Если удаляемый документ зарегистрирован в регистре РасчетыСКонтрагентамиОтложенноеПроведение,
// то отметим для следующего за ним документа признак того, что по нему требуется выполнить актуализацию
// в связи с изменением состава впереди стоящих документов.
//
Процедура ЗарегистрироватьРасчетыСКонтрагентамиОтложенноеПроведениеПередУдалением(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.РасчетыСКонтрагентамиОтложенноеПроведение");
	ЭлементБлокировки.УстановитьЗначение("Документ", Источник.Ссылка);
	
	БлокировкаДанных.Заблокировать();

	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", Источник.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКонтрагентамиОтложенноеПроведение.Организация,
	|	РасчетыСКонтрагентамиОтложенноеПроведение.Контрагент,
	|	РасчетыСКонтрагентамиОтложенноеПроведение.ДоговорКонтрагента,
	|	РасчетыСКонтрагентамиОтложенноеПроведение.Дата
	|ИЗ
	|	РегистрСведений.РасчетыСКонтрагентамиОтложенноеПроведение КАК РасчетыСКонтрагентамиОтложенноеПроведение
	|ГДЕ
	|	РасчетыСКонтрагентамиОтложенноеПроведение.Документ = &Документ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	КешДокументыПоддерживающиеОтложенныеРасчетыСКонтрагентами = Неопределено;
	
	Пока Выборка.Следующий() Цикл
	
		УчетВзаиморасчетовОтложенноеПроведение.НайтиИУстановитьКВыполнениюСледующийДокументПоДоговору(
			Выборка.Организация,
			Выборка.Контрагент,
			Выборка.ДоговорКонтрагента,
			Выборка.Дата,
			КешДокументыПоддерживающиеОтложенныеРасчетыСКонтрагентами);
	
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

