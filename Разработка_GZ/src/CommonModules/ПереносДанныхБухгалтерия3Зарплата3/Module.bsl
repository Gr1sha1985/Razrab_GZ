#Область СлужебныеПроцедурыИФункции

Функция РезультатВыгрузкиДляЗарплатаКадры() Экспорт
	
	Результат = Новый Структура("ВыгруженныеДанные,ПротоколВыгрузки");
	
	ТекстПравил = ПолучитьОбщийМакет("ЗарплатаКадрыПереносДанныхВЗУП3");
	ИмяФайлаДанных    = ПолучитьИмяВременногоФайла("xml");
	ИмяФайлаПротокола = ПолучитьИмяВременногоФайла("txt");
	
	ОбработкаОбъект = Обработки.УниверсальныйОбменДаннымиXML.Создать();
	ОбработкаОбъект.ЗагрузитьПравилаОбмена(ТекстПравил.ПолучитьТекст(), "Строка");
	
	ОбработкаОбъект.НеВыводитьНикакихИнформационныхСообщенийПользователю	= Истина;
	ОбработкаОбъект.РежимОбмена			 	= "Выгрузка";
	ОбработкаОбъект.ИмяФайлаПравилОбмена 	= ИмяФайлаДанных;
	ОбработкаОбъект.ИмяФайлаОбмена 		 	= ИмяФайлаДанных;
	ОбработкаОбъект.ИмяФайлаПротоколаОбмена = ИмяФайлаПротокола;

	КорневыеСтрокиПравилВыгрузки	= ОбработкаОбъект.ТаблицаПравилВыгрузки.Строки;
	Для Каждого ПравилоВыгрузки Из КорневыеСтрокиПравилВыгрузки Цикл
		ПравилоВыгрузки.Включить = 1;
		ОбработкаОбъект.УстановитьПометкиПодчиненных(ПравилоВыгрузки, "Включить");
	КонецЦикла;
	
	ОбработкаОбъект.ВыполнитьВыгрузку();
	
	Протокол = Новый ТекстовыйДокумент;
	Протокол.Прочитать(ИмяФайлаПротокола, КодировкаТекста.ANSI);
	Результат.ПротоколВыгрузки  = Протокол.ПолучитьТекст();
	
	ДанныеВыгрузки = Новый ТекстовыйДокумент;
	ДанныеВыгрузки.Прочитать(ИмяФайлаДанных, КодировкаТекста.UTF8);
	Результат.ВыгруженныеДанные = ДанныеВыгрузки.ПолучитьТекст();
	
	Попытка
		УдалитьФайлы(ИмяФайлаДанных);
		УдалитьФайлы(ИмяФайлаПротокола);
	Исключение
	КонецПопытки;
	
	Возврат Результат;

КонецФункции

Функция ПараметрыИнформационнойБазы() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяКонфигурации", Метаданные.Имя);
	Результат.Вставить("ВерсияКонфигурации", Метаданные.Версия);
	Результат.Вставить("ВерсияКонфигурацииВПравилах", "");
	
	ТекстПравил = ПолучитьОбщийМакет("ЗарплатаКадрыПереносДанныхВЗУП3");
	ФайлПравил = Новый ЧтениеXML();
	ФайлПравил.УстановитьСтроку(ТекстПравил.ПолучитьТекст());
	Пока ФайлПравил.Прочитать() Цикл
		Если ФайлПравил.ЛокальноеИмя = "Источник" Тогда
			Результат.ВерсияКонфигурацииВПравилах = ФайлПравил.ПолучитьАтрибут("ВерсияКонфигурации");
			Прервать;
		КонецЕсли;
	КонецЦикла;
	ФайлПравил = "";
	
	Возврат Результат;

КонецФункции


#КонецОбласти
