// Рассчитывает сумму в строке табличной части документа
//
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа
//
Процедура РассчитатьСуммуТабЧасти(СтрокаТаблицы, ЗначениеПустогоКоличества = 0, СоответствиеРеквизитов = Неопределено) Экспорт
	Если СоответствиеРеквизитов <> Неопределено Тогда
		СтрокаТабличнойЧасти = ИзСтрокиТаблицыДокумента(СтрокаТаблицы, СоответствиеРеквизитов);
	Иначе
		СтрокаТабличнойЧасти = СтрокаТаблицы;
	КонецЕсли; 

	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Цена * ?(СтрокаТабличнойЧасти.Количество = 0, ЗначениеПустогоКоличества, СтрокаТабличнойЧасти.Количество);
	
	Если СоответствиеРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(
			СтрокаТаблицы, 
			ВСтрокуТаблицыДокумента(СтрокаТаблицы, СоответствиеРеквизитов));
	КонецЕсли; 
КонецПроцедуры

// Рассчитывает сумму в строке табличной части документа
//
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа
//
Процедура РассчитатьСуммуВРозницеТабЧасти(СтрокаТабличнойЧасти, ЗначениеПустогоКоличества = 0) Экспорт

	СтрокаТабличнойЧасти.СуммаВРознице = СтрокаТабличнойЧасти.ЦенаВРознице * ?(СтрокаТабличнойЧасти.Количество = 0, ЗначениеПустогоКоличества, СтрокаТабличнойЧасти.Количество);

КонецПроцедуры

// Расчет, исходя из постоянной суммы
//
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа.
//
Процедура РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, СуммаВключаетНДС = Ложь, ПрименяютсяСтавки4и2 = Ложь, СоответствиеРеквизитов = Неопределено) Экспорт
	Если СоответствиеРеквизитов <> Неопределено Тогда
		СтрокаТабличнойЧасти = ИзСтрокиТаблицыДокумента(СтрокаТаблицы, СоответствиеРеквизитов);
	Иначе
		СтрокаТабличнойЧасти = СтрокаТаблицы;
	КонецЕсли; 

	Если ТипЗнч(СтрокаТабличнойЧасти)=Тип("Структура") Тогда
		Если СтрокаТабличнойЧасти.Свойство("Сумма") И СтрокаТабличнойЧасти.Свойство("СтавкаНДС") Тогда
			СтрокаТабличнойЧасти.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
												СтрокаТабличнойЧасти.Сумма - ?(СтрокаТабличнойЧасти.Свойство("СуммаСкидки"), СтрокаТабличнойЧасти.СуммаСкидки, 0),
												СуммаВключаетНДС,
												УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС, ПрименяютсяСтавки4и2));
		КонецЕсли;
	Иначе // Строка табличной части
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
			СтрокаТабличнойЧасти.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
												СтрокаТабличнойЧасти.Сумма,
												СуммаВключаетНДС,
												УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС, ПрименяютсяСтавки4и2));
		КонецЕсли;
	КонецЕсли;
	
	Если СоответствиеРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(
			СтрокаТаблицы, 
			ВСтрокуТаблицыДокумента(СтрокаТаблицы, СоответствиеРеквизитов));
	КонецЕсли; 
КонецПроцедуры

// Функция выполняет поиск первой, удовлетворяющей условию поиска, строки табличной части.
//
// Параметры:
//  ТабличнаяЧасть - табличная часть документа, в которой осуществляется поиск,
//  СтруктураОтбора - структура - задает условие поиска.
//
// Возвращаемое значение: 
//  Строка табличной части - найденная строка табличной части,
//  Неопределено           - строка табличной части не найдена.
//
Функция НайтиСтрокуТабЧасти(Объект, ТабличнаяЧасть, СтруктураОтбора) Экспорт

	СтрокаТабличнойЧасти = Неопределено;
	МассивНайденныхСтрок = Объект[ТабличнаяЧасть].НайтиСтроки(СтруктураОтбора);
	Если МассивНайденныхСтрок.Количество() > 0 Тогда

		// Нашли. Вернем первую найденную строку.
		СтрокаТабличнойЧасти = МассивНайденныхСтрок[0];
	КонецЕсли;

	Возврат СтрокаТабличнойЧасти;

КонецФункции

// Процедура выполняет стандартные действия по расчету плановой суммы
// в строке табличной части документа.
//
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа,
//
Процедура ПересчитатьПлановуюСумму(СтрокаТабличнойЧасти, ЗначениеПустогоКоличества = 0) Экспорт

	СтрокаТабличнойЧасти.СуммаПлановая = 
		?(СтрокаТабличнойЧасти.Количество = 0, ЗначениеПустогоКоличества, СтрокаТабличнойЧасти.Количество)
		* СтрокаТабличнойЧасти.ПлановаяСтоимость;

КонецПроцедуры

// СоответствиеРеквизитов - Структура - Ключ - имя стандартного реквизита, Значение - имяПользовательского
Функция ВСтрокуТаблицыДокумента(ИсходнаяСтрока, СоответствиеРеквизитов)
	Результат = Новый Структура;
	
	Для каждого СоответствиеРеквизита Из СоответствиеРеквизитов Цикл
		Результат.Вставить(СоответствиеРеквизита.Значение, ИсходнаяСтрока[СоответствиеРеквизита.Ключ]);
	КонецЦикла; 
	
	Возврат Результат;
КонецФункции

Функция ИзСтрокиТаблицыДокумента(ИсходнаяСтрока, СоответствиеРеквизитов)
	Результат = Новый Структура;
	
	Для каждого СоответствиеРеквизита Из СоответствиеРеквизитов Цикл
		Результат.Вставить(СоответствиеРеквизита.Ключ, ИсходнаяСтрока[СоответствиеРеквизита.Значение]);
	КонецЦикла; 
	
	Возврат Результат;
КонецФункции

Процедура ПриИзмененииСуммыТабЧасти(СтрокаТабличнойЧасти, ЗначениеПустогоКоличества = 0, СоответствиеРеквизитов = Неопределено) Экспорт
	Если СоответствиеРеквизитов <> Неопределено Тогда
		СтрокаТаблицы = ИзСтрокиТаблицыДокумента(СтрокаТабличнойЧасти, СоответствиеРеквизитов);
	Иначе
		СтрокаТаблицы = СтрокаТабличнойЧасти;
	КонецЕсли; 

	РасчетноеКоличество = ?(СтрокаТабличнойЧасти.Количество = 0, ЗначениеПустогоКоличества, СтрокаТабличнойЧасти.Количество);
	Если РасчетноеКоличество = 0 Тогда
		СтрокаТабличнойЧасти.Цена = 0;
	Иначе
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / РасчетноеКоличество;
	КонецЕсли; 
	
	Если СоответствиеРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(
			СтрокаТабличнойЧасти, 
			ВСтрокуТаблицыДокумента(СтрокаТаблицы, СоответствиеРеквизитов));
	КонецЕсли; 
КонецПроцедуры

Процедура ПриИзмененииКоличествоЦена(Форма, ИмяТаблицы, ЗначениеПустогоКоличества = 0, ПрименяютсяСтавки4и2 = Ложь, СоответствиеРеквизитов = Неопределено) Экспорт
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Если СоответствиеРеквизитов <> Неопределено Тогда
		СтрокаТаблицы = ИзСтрокиТаблицыДокумента(Элементы[ИмяТаблицы].ТекущиеДанные, СоответствиеРеквизитов);
	Иначе
		СтрокаТаблицы = Элементы[ИмяТаблицы].ТекущиеДанные;
	КонецЕсли; 
	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТаблицы, ЗначениеПустогоКоличества);
	
	Если СтрокаТаблицы.Свойство("СуммаНДС") Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПокупательНалоговыйАгентПоНДС")
			И Форма.ПокупательНалоговыйАгентПоНДС = Истина
			И Форма.ВедетсяУчетНДСПоФЗ335 Тогда 
			СтрокаТаблицы.СуммаНДС = 0;
		Иначе
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС, ПрименяютсяСтавки4и2);
		КонецЕсли;
	КонецЕсли;

	Если СтрокаТаблицы.Свойство("СуммаВРознице") Тогда
		СтрокаТаблицы.СуммаВРознице = СтрокаТаблицы.Количество * СтрокаТаблицы.ЦенаВРознице;
	КонецЕсли;

	Если СтрокаТаблицы.Свойство("Всего") Тогда
		СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	КонецЕсли;
	
	Если СоответствиеРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(
			Элементы[ИмяТаблицы].ТекущиеДанные, 
			ВСтрокуТаблицыДокумента(СтрокаТаблицы, СоответствиеРеквизитов));
	КонецЕсли; 
КонецПроцедуры

Процедура ПриИзмененииСумма(Форма, ИмяТаблицы, ЗначениеПустогоКоличества = 0, ПрименяютсяСтавки4и2 = Ложь, СоответствиеРеквизитов = Неопределено) Экспорт

	Элементы = Форма.Элементы;
	Объект = Форма.Объект;

	Если СоответствиеРеквизитов <> Неопределено Тогда
		СтрокаТаблицы = ИзСтрокиТаблицыДокумента(Элементы[ИмяТаблицы].ТекущиеДанные, СоответствиеРеквизитов);
	Иначе
		СтрокаТаблицы = Элементы[ИмяТаблицы].ТекущиеДанные;
	КонецЕсли; 

	Если СтрокаТаблицы.Свойство("Количество") Тогда
		Если (СтрокаТаблицы.Количество = 0) И (ЗначениеПустогоКоличества = 0) Тогда
			СтрокаТаблицы.Цена = 0;
		Иначе
			СтрокаТаблицы.Цена = СтрокаТаблицы.Сумма / ?(СтрокаТаблицы.Количество = 0, ЗначениеПустогоКоличества, СтрокаТаблицы.Количество);
		КонецЕсли;
	КонецЕсли;

	Если СтрокаТаблицы.Свойство("СуммаНДС") Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПокупательНалоговыйАгентПоНДС")
			И Форма.ПокупательНалоговыйАгентПоНДС = Истина
			И Форма.ВедетсяУчетНДСПоФЗ335 Тогда 
			СтрокаТаблицы.СуммаНДС = 0;
		Иначе
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС, ПрименяютсяСтавки4и2);
		КонецЕсли;
	КонецЕсли;

	Если СтрокаТаблицы.Свойство("Всего") Тогда
		СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	КонецЕсли;
	
	Если СоответствиеРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(
			Элементы[ИмяТаблицы].ТекущиеДанные, 
			ВСтрокуТаблицыДокумента(СтрокаТаблицы, СоответствиеРеквизитов));
	КонецЕсли; 
		
КонецПроцедуры

Процедура ПриИзмененииСтавкаНДС(Форма, ИмяТаблицы, ПрименяютсяСтавки4и2 = Ложь, СоответствиеРеквизитов = Неопределено) Экспорт

	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Если СоответствиеРеквизитов <> Неопределено Тогда
		СтрокаТаблицы = ИзСтрокиТаблицыДокумента(Элементы[ИмяТаблицы].ТекущиеДанные, СоответствиеРеквизитов);
	Иначе
		СтрокаТаблицы = Элементы[ИмяТаблицы].ТекущиеДанные;
	КонецЕсли; 
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПокупательНалоговыйАгентПоНДС")
		И Форма.ПокупательНалоговыйАгентПоНДС = Истина
		И Форма.ВедетсяУчетНДСПоФЗ335 Тогда 
		СтрокаТаблицы.СуммаНДС = 0;
	Иначе
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС, ПрименяютсяСтавки4и2);
	КонецЕсли;
	Если СтрокаТаблицы.Свойство("Всего") Тогда
		СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	КонецЕсли;
	
	Если СоответствиеРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(
			Элементы[ИмяТаблицы].ТекущиеДанные, 
			ВСтрокуТаблицыДокумента(СтрокаТаблицы, СоответствиеРеквизитов));
	КонецЕсли; 

КонецПроцедуры

Процедура ПриИзмененииСуммаНДС(Форма, ИмяТаблицы,  СоответствиеРеквизитов = Неопределено) Экспорт

	Элементы = Форма.Элементы;
	Объект = Форма.Объект;

	Если СоответствиеРеквизитов <> Неопределено Тогда
		СтрокаТаблицы = ИзСтрокиТаблицыДокумента(Элементы[ИмяТаблицы].ТекущиеДанные, СоответствиеРеквизитов);
	Иначе
		СтрокаТаблицы = Элементы[ИмяТаблицы].ТекущиеДанные;
	КонецЕсли; 

	Если СтрокаТаблицы.Свойство("Всего") Тогда
		СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	КонецЕсли;
	
	Если СоответствиеРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(
			Элементы[ИмяТаблицы].ТекущиеДанные, 
			ВСтрокуТаблицыДокумента(СтрокаТаблицы, СоответствиеРеквизитов));
	КонецЕсли; 

КонецПроцедуры