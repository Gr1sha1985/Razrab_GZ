#Область СлужебныйПрограммныйИнтерфейс

Процедура ПередНачаломРаботыСистемы(Параметры) Экспорт
	
	Если Не ОбщегоНазначенияВызовСервераБТС.ЗагрузкаПрервана() Тогда
		Возврат;
	КонецЕсли;
		
	Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
		"ПоказатьДиалогПрерваннойЗагрузкиПередНачаломРаботыСистемы",
		ЭтотОбъект);
	
КонецПроцедуры

Процедура ПоказатьДиалогПрерваннойЗагрузкиПриНеобходимости() Экспорт
	
	ЗагрузкаПрервана = ОбщегоНазначенияВызовСервераБТС.ЗагрузкаПрервана();	
	
	Если Не ЗагрузкаПрервана Тогда
		Возврат;	
	КонецЕсли;
	 
	ПоказатьДиалогПрерваннойЗагрузки();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПоказатьДиалогПрерваннойЗагрузкиПередНачаломРаботыСистемы(Параметры, ДополнительныеПараметры) Экспорт
	ПоказатьДиалогПрерваннойЗагрузки();
КонецПроцедуры

Процедура ПоказатьДиалогПрерваннойЗагрузки()

	ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
	ПараметрыВопроса.Картинка = БиблиотекаКартинок.Предупреждение32;
	ПараметрыВопроса.Заголовок = НСтр("ru = 'Процедура загрузки была прервана'");
		
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения(
		"ВопросОПрерваннойЗагрузкеЗавершение",
		ЭтотОбъект);

	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
		ОписаниеОповещенияОЗавершении,
		НСтр("ru = 'Для продолжения нужно будет снова указать файл, из которого загружались данные.'"),
		РежимДиалогаВопрос.ОК,
		ПараметрыВопроса);
		
КонецПроцедуры

Процедура ВопросОПрерваннойЗагрузкеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ЗагрузкаДанныхИзСервиса",
		Новый Структура("ПрерванаПроцедураЗагрузки", Истина),,,,,,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
КонецПроцедуры

#КонецОбласти