#Область СлужебныйПрограммныйИнтерфейс

// Выводит оповещения о новых сообщениях от банков по заявкам на кредит
Процедура Подключаемый_ОповеститьОНовыхСообщенияхПоЗаявкамНаКредит() Экспорт
	
	// Если администратор сервиса вышел из области, но сеанс не закрыл,
	// то обработчик ожидания может быть вызван снова.
	// Поэтому проверим возможность обращения к разделенным данным области.
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске();
	Если НЕ ПараметрыРаботыКлиента.ДоступноИспользованиеРазделенныхДанных Тогда
		Возврат;
	КонецЕсли;

	ПовторятьЗапрос = Истина;
	
	НовыеСообщенияОтБанков = ЗаявкиНаКредитВызовСервера.НовыеСообщенияОтБанков(ПовторятьЗапрос);
	
	Если НовыеСообщенияОтБанков.Количество() > 0 Тогда
		ЗаявкиНаКредитКлиент.ОповеститьОНовыхСообщенияхОтБанков(НовыеСообщенияОтБанков);
	КонецЕсли;
	
	Если ПовторятьЗапрос Тогда
		// Рег.задание по проверке новых сообщений от банков запускается с "затухающим" расписанием,
		// сообщения же пользователю о том, что есть не прочитанные сообщения от банков показываем раз в 30 мин.
		ПодключитьОбработчикОжидания("Подключаемый_ОповеститьОНовыхСообщенияхПоЗаявкамНаКредит", 1800, Истина);
	КонецЕсли; 
	
КонецПроцедуры

// Выводит окно для подключения к интернет-поддержке
Процедура Подключаемый_НачатьПодключениеИнтернетПоддержкиИзЗаявкиНаКредит() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияИнтернетПоддержки", ЗаявкиНаКредитКлиент);	

	ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти
