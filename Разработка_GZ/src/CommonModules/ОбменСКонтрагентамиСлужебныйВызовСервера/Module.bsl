////////////////////////////////////////////////////////////////////////////////
// ОбменСКонтрагентамиСлужебныйВызовСервера: механизм обмена электронными документами.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область РаботаСВерсиямиЭД

// Получение соответствия документов и электронных документов.
//
// Параметры:
//  СписокВладельцев - Массив - владельцы электронных документов.
//  СписокЭлектронныхДокументов - Массив - электронные документы.
// 
// Возвращаемое значение:
//  Соответствие - ссылки на владельцев и электронных документов.
//
Функция ВладельцыИЭлектронныеДокументы(СписокВладельцев = Неопределено, СписокЭлектронныхДокументов = Неопределено) Экспорт
	
	СоответствиеВладельцевИЭД = ОбменСКонтрагентамиСлужебный.ВладельцыИЭлектронныеДокументы(
		СписокВладельцев, СписокЭлектронныхДокументов);
	
	Возврат СоответствиеВладельцевИЭД;
	
КонецФункции

// Устанавливает новую версию электронного документа для владельца.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на документ базы данных, номер версии электронного
//                                    документа которого надо изменить.
//  ЭлектронныйДокумент - ДокументСсылка.ИсходящийЭлектронныйДокумент, ВходящийЭлектронныйДокумент - ссылка на
//                                    электронный документ, который в данный момент является актуальным.
//  УдалятьСтаруюВерсию - Булево - если Истина, то старая версия ЭД будет удалена.
//
Процедура УстановитьНовуюВерсиюЭД(СсылкаНаОбъект, ЭлектронныйДокумент = Неопределено, УдалятьСтаруюВерсию = Ложь, Знач СоздатьНовуюВерсию = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		НаборЗаписей = РегистрыСведений.СостоянияЭД.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СсылкаНаОбъект.Установить(СсылкаНаОбъект);
		ЭлектронноеВзаимодействиеСлужебный.УстановитьУправляемуюБлокировкуПоНаборуЗаписей(НаборЗаписей);
		НаборЗаписей.Прочитать();
		
		ЗаписатьИзменения = Истина;
		
		Если НаборЗаписей.Количество() = 0 Тогда
			НоваяЗаписьНабора = НаборЗаписей.Добавить();
			НоваяЗаписьНабора.СсылкаНаОбъект = СсылкаНаОбъект;
			НоваяЗаписьНабора.ЭлектронныйДокумент = ЭлектронныйДокумент;
			НоваяЗаписьНабора.СостояниеВерсииЭД = ОбменСКонтрагентамиСлужебный.ПолучитьПервоеСостояниеВерсииЭДДляВладельца(
				СсылкаНаОбъект, ЗначениеЗаполнено(ЭлектронныйДокумент));
			Если НоваяЗаписьНабора.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.НаУтверждении Тогда
				НоваяЗаписьНабора.ДействияСНашейСтороны = Перечисления.СводныеСостоянияЭД.ТребуютсяДействия;
			КонецЕсли;
			ЗаписатьИзменения = СоздатьНовуюВерсию;
		Иначе
			
			НоваяЗаписьНабора = НаборЗаписей.Получить(0);
			
			НоваяЗаписьНабора.ЭлектронныйДокумент = ЭлектронныйДокумент;
			
			Если УдалятьСтаруюВерсию Тогда
				УдалитьСтаруюВерсиюЭД(НоваяЗаписьНабора);
			КонецЕсли;
			НоваяЗаписьНабора.СостояниеВерсииЭД = ОбменСКонтрагентамиСлужебный.ПолучитьПервоеСостояниеВерсииЭДДляВладельца(
				СсылкаНаОбъект, ЗначениеЗаполнено(ЭлектронныйДокумент));
			Если ЭлектронныйДокумент = Неопределено 
				Или ЭлектронныйДокумент.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий
				Или ЭлектронныйДокумент.НаправлениеЭД = Перечисления.НаправленияЭД.Интеркампани Тогда
				НоваяЗаписьНабора.ЭлектронныйДокумент = ЭлектронныйДокумент;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НоваяЗаписьНабора.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.НеСформирован Тогда
			НоваяЗаписьНабора.ДействияСНашейСтороны = Перечисления.СводныеСостоянияЭД.ТребуютсяДействия;
			НоваяЗаписьНабора.ДействияСоСтороныДругогоУчастника = Перечисления.СводныеСостоянияЭД.ДействийНеТребуется;
		ИначеЕсли НоваяЗаписьНабора.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.НеПолучен Тогда
			НоваяЗаписьНабора.ДействияСНашейСтороны = Перечисления.СводныеСостоянияЭД.ПустаяСсылка();
			НоваяЗаписьНабора.ДействияСоСтороныДругогоУчастника = Перечисления.СводныеСостоянияЭД.ПустаяСсылка();
		КонецЕсли;
		
		ОписаниеОснования = Неопределено;
		Для каждого Запись Из НаборЗаписей Цикл
			Если Запись.СостояниеВерсииЭД <> Перечисления.СостоянияВерсийЭД.НеСформирован Тогда
				Продолжить;
			КонецЕсли;
			Если ОписаниеОснования = Неопределено Тогда
				ОписаниеОснования = ОбменСКонтрагентамиСлужебный.ОписаниеОснованияЭлектронногоДокумента(Запись.СсылкаНаОбъект);
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Запись, ОписаниеОснования);
		КонецЦикла;
		
		Если Не СоздатьНовуюВерсию Тогда
			НаборЗаписей.Очистить();
		КонецЕсли;
		
		Если ЗаписатьИзменения Тогда
			НаборЗаписей.Записать();
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Обновляет статусы подключений настроек ЭДО
//
// Параметры:
//  СоотвСоглашенийИСтруктурСертификатов - Соответствие - параметры для обновления статусов.
//  КонтекстОперации - Структура - контекст операции, см. ЭлектронноеВзаимодействиеСлужебный.НовыйКонтекстОперации.
//
Процедура ОбновитьСтатусыПодключенияНастроекЭДО(СоотвСоглашенийИСтруктурСертификатов, КонтекстОперации = Неопределено) Экспорт
	
	ТаблицаПриглашений = ОбменСКонтрагентамиВнутренний.ТаблицаДанныхУчастниковОбмена(СоотвСоглашенийИСтруктурСертификатов, КонтекстОперации);
	ОбменСКонтрагентамиСлужебный.ОбработатьТаблицуПриглашений(ТаблицаПриглашений);
	
КонецПроцедуры

// Определение действующей настройки ЭДО.
//
// Параметры:
//  СсылкаНаВладельца - Ссылка - электронный документ.
//  ПараметрыЭД - Структура - параметры поиска и заполнения данными.
//  ВидЭД - ПеречислениеСсылка.ВидыЭД - вид электронного документа.
//  СообщатьОбОшибке - Булево - признак вывода сообщения об ошибке.
// 
// Возвращаемое значение:
//  Булево - Истина, если настройка определена.
//
Функция ОпределитьДействующуюНастройкуЭДО(СсылкаНаВладельца, ПараметрыЭД = Неопределено, ВидЭД = Неопределено, СообщатьОбОшибке = Ложь) Экспорт
	
	Результат = ОбменСКонтрагентамиСлужебный.ОпределитьДействующуюНастройкуЭДО(
		СсылкаНаВладельца, ПараметрыЭД, ВидЭД, СообщатьОбОшибке);
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак существования настройки ЭДО между контрагентом и организацией.
//
// Параметры:
//  СсылкаНаВладельца - Ссылка - ссылка на документ ИБ.
//  ПараметрыЭД - Структура - свойства электронного документа, соответствующие документу ИБ.
//
// Возвращаемое значение:
//  Булево - наличие настройки между организацией и контрагентом.
//
Функция НастройкаЭДСуществует(СсылкаНаВладельца, ПараметрыЭД = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Ложь;
	
	Если ПараметрыЭД = Неопределено Тогда
		ПараметрыЭД = ОбменСКонтрагентамиСлужебный.ЗаполнитьПараметрыЭДПоИсточнику(СсылкаНаВладельца);
	КонецЕсли;
	
	ПустойДоговорКонтрагента = Метаданные.ОпределяемыеТипы.ДоговорСКонтрагентом.Тип.ПривестиЗначение();
	
	Если ЗначениеЗаполнено(ПараметрыЭД.Контрагент) И ЗначениеЗаполнено(ПараметрыЭД.Организация)
		И ЗначениеЗаполнено(ПараметрыЭД.ВидЭД) Тогда
		
			ПрикладнойВидЭД =  ПараметрыЭД.ПрикладнойВидЭД;
			ПараметрВидЭД = ПараметрыЭД.ВидЭД;
			Если ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель Тогда
				ПараметрВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец;
			ИначеЕсли ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик Тогда
				ПараметрВидЭД = Перечисления.ВидыЭД.АктИсполнитель;
			ИначеЕсли ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиПолучатель Тогда
				ПараметрВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель;
			КонецЕсли;
			
			РеквизитыСтрока = "ИНН";
			РеквизитыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыЭД.Контрагент, РеквизитыСтрока);
			ИНН = РеквизитыКонтрагента.ИНН;
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПриглашенияКОбменуЭлектроннымиДокументами.Статус КАК СтатусПодключения,
			|	НастройкиОтправкиЭлектронныхДокументовПоВидам.СпособОбменаЭД КАК СпособОбменаЭД
			|ИЗ
			|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК НастройкиОтправкиЭлектронныхДокументовПоВидам
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
			|		ПО НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторОтправителя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации
			|			И (НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторПолучателя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента
			|				ИЛИ ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента = &ИНН)
			|ГДЕ
			|	НастройкиОтправкиЭлектронныхДокументовПоВидам.Отправитель = &Организация
			|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.Получатель = &Контрагент
			|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.Договор = &ДоговорКонтрагента
			|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.ВидДокумента = &ВидЭД
			|	И ВЫБОР
			|			КОГДА &ПрикладнойВидЭД = НЕОПРЕДЕЛЕНО
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ НастройкиОтправкиЭлектронныхДокументовПоВидам.ПрикладнойВидЭД = &ПрикладнойВидЭД
			|		КОНЕЦ
			|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.Формировать
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПриглашенияКОбменуЭлектроннымиДокументами.Статус,
			|	ДополнительныеНастройки.СпособОбменаЭД
			|ИЗ
			|	ДополнительныеНастройки КАК ДополнительныеНастройки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
			|		ПО ДополнительныеНастройки.ИдентификаторОтправителя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации
			|			И (ДополнительныеНастройки.ИдентификаторПолучателя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента
			|				ИЛИ ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента = &ИНН)
			|ГДЕ
			|	ДополнительныеНастройки.Отправитель = &Организация
			|	И ДополнительныеНастройки.Получатель = &Контрагент
			|	И ДополнительныеНастройки.Договор = &ДоговорКонтрагента
			|	И ДополнительныеНастройки.ВидДокумента = &ВидЭД
			|	И ВЫБОР
			|			КОГДА &ПрикладнойВидЭД = НЕОПРЕДЕЛЕНО
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ ДополнительныеНастройки.ПрикладнойВидЭД = &ПрикладнойВидЭД
			|		КОНЕЦ
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ
			|	ПриглашенияКОбменуЭлектроннымиДокументами.Статус,
			|	НастройкиОтправкиЭлектронныхДокументовПоВидам.СпособОбменаЭД
			|ИЗ
			|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам КАК НастройкиОтправкиЭлектронныхДокументовПоВидам
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
			|		ПО НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторОтправителя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации
			|			И (НастройкиОтправкиЭлектронныхДокументовПоВидам.ИдентификаторПолучателя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента
			|				ИЛИ ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента = &ИНН)
			|ГДЕ
			|	НастройкиОтправкиЭлектронныхДокументовПоВидам.Отправитель = &Организация
			|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.Получатель = &Контрагент
			|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.Договор = &ПустойДоговорКонтрагента
			|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.ВидДокумента = &ВидЭД
			|	И ВЫБОР
			|			КОГДА &ПрикладнойВидЭД = НЕОПРЕДЕЛЕНО
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ НастройкиОтправкиЭлектронныхДокументовПоВидам.ПрикладнойВидЭД = &ПрикладнойВидЭД
			|		КОНЕЦ
			|	И НастройкиОтправкиЭлектронныхДокументовПоВидам.Формировать
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПриглашенияКОбменуЭлектроннымиДокументами.Статус,
			|	ДополнительныеНастройки.СпособОбменаЭД
			|ИЗ
			|	ДополнительныеНастройки КАК ДополнительныеНастройки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
			|		ПО ДополнительныеНастройки.ИдентификаторОтправителя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации
			|			И (ДополнительныеНастройки.ИдентификаторПолучателя = ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента
			|				ИЛИ ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента = &ИНН)
			|ГДЕ
			|	ДополнительныеНастройки.Отправитель = &Организация
			|	И ДополнительныеНастройки.Получатель = &Контрагент
			|	И ДополнительныеНастройки.Договор = &ПустойДоговорКонтрагента
			|	И ДополнительныеНастройки.ВидДокумента = &ВидЭД
			|	И ВЫБОР
			|			КОГДА &ПрикладнойВидЭД = НЕОПРЕДЕЛЕНО
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ ДополнительныеНастройки.ПрикладнойВидЭД = &ПрикладнойВидЭД
			|		КОНЕЦ";
			Запрос.УстановитьПараметр("Контрагент",         ПараметрыЭД.Контрагент);
			Запрос.УстановитьПараметр("ДоговорКонтрагента", ПараметрыЭД.ДоговорКонтрагента);
			Запрос.УстановитьПараметр("Организация",        ПараметрыЭД.Организация);
			Запрос.УстановитьПараметр("ИНН",                ИНН);
			Запрос.УстановитьПараметр("ВидЭД",              ПараметрВидЭД);
			Запрос.УстановитьПараметр("ПрикладнойВидЭД",    ПрикладнойВидЭД);
			Запрос.УстановитьПараметр("ПустойДоговорКонтрагента",    ПустойДоговорКонтрагента);
			
			ОбменСКонтрагентамиСлужебный.ДобавитьНастройкиПоУмолчаниюДляНеактуальныхВидовЭД(Запрос);
			РезультатЗапроса = Запрос.Выполнить();
			Если Не РезультатЗапроса.Пустой() Тогда
				
				Выборка = РезультатЗапроса.Выбрать();
				Выборка.Следующий();
				
				Если Выборка.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.Внутренний Тогда
					ПараметрыЭД.Вставить("СтатусПодключения", Перечисления.СтатусыПриглашений.Принято);
				Иначе
					ПараметрыЭД.Вставить("СтатусПодключения", Выборка.СтатусПодключения);
				КонецЕсли;
				
				Результат = Истина;
				
			КонецЕсли;
			
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получает основной файл электронного документа, являющегося актуальным для документа учета
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на объект ИБ или на электронный документ, по которому нужно получить статус.
// 
// Возвращаемое значение:
//  СправочникСсылка.ЭДПрисоединенныеФайлы, Неопределено - ссылка на файл электронного документа или Неопределено, если
//  актуального электронного документа нет.
//
Функция ОсновнойФайлЭлектронногоДокументаПоДокументуУчета(Документ) Экспорт
	
	ОсновнойФайл = Неопределено;
	СоответствиеВладельцевИЭД = ОбменСКонтрагентамиСлужебный.ВладельцыИЭлектронныеДокументы(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Документ));
		
	Если СоответствиеВладельцевИЭД.Количество() И ЗначениеЗаполнено(СоответствиеВладельцевИЭД[Документ]) Тогда
		ОсновнойФайл = ОбменСКонтрагентамиСлужебный.ПрисоединенныйФайл(СоответствиеВладельцевИЭД[Документ]);
	КонецЕсли;
	
	Возврат ОсновнойФайл;
	
КонецФункции

#КонецОбласти

#Область РаспаковкаПакетовЭД

// Получение данных пакетов ЭД.
//
// Параметры:
//  МассивПакетовЭД	 - Массив - список Пакетов ЭД.
// 
// Возвращаемое значение:
//  Структура - данные пакетов ЭД.
//
Функция ДанныеПакетовЭД(МассивПакетовЭД, КонтекстОперации) Экспорт
	
	ДанныеПакетовЭД = ОбменСКонтрагентамиСлужебный.ДанныеПакетовЭД(МассивПакетовЭД,, КонтекстОперации);
	
	Возврат ДанныеПакетовЭД;
	
КонецФункции

// Запись данных пакетов ЭД.
// Не рекомендуется вызывать в транзакции, так как при падении переопределяемой части, может
// возникнуть ошибка "В данной транзакции уже происходили ошибки".
//
// Параметры:
//  ДанныеПакетовЭД	          - Массив - список пакетов ЭД.
//  РаспакованныеДокументы    - Структура - служебные данные:
//    * МассивСлужебныхЭД         - Массив - служебные электронные документы.
//    * НовыеДокументы            - Массив - добавляемые электронные документы.
//  КоличествоРаспакованныхЭД - Число - количество распакованных пакетов.
//  КонтекстОперации - Структура - контекст операции, см. ЭлектронноеВзаимодействиеСлужебный.НовыйКонтекстОперации.
//
Процедура ЗаписатьДанныеПакетовЭД(ДанныеПакетовЭД, РаспакованныеДокументы, КоличествоРаспакованныхЭД, КонтекстОперации = Неопределено) Экспорт
	
	ОбменСКонтрагентамиСлужебный.ЗаписатьДанныеПакетовЭД(ДанныеПакетовЭД, РаспакованныеДокументы, КоличествоРаспакованныхЭД, КонтекстОперации);
	
КонецПроцедуры

// Перебирает отпечатки подписей полученных пакетов ЭД,
// получает из них открытую часть сертификата и проверяет ее корректность.
// 
// Параметры:
//  ДанныеПакетовЭД - Структура - данные полученных пакетов ЭД. 
//  КонтекстОперации - Структура - контекст операции, см. ЭлектронноеВзаимодействиеСлужебный.НовыйКонтекстОперации.
//
Процедура ПроверитьСертификатыПодписей(ДанныеПакетовЭД, КонтекстОперации = Неопределено) Экспорт
	
	ОбменСКонтрагентамиСлужебный.ПроверитьСертификатыПодписей(ДанныеПакетовЭД, КонтекстОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ОтправкаПакетовЭД

// Выполняет действия с ЭД после установки электронной подписи.
//
// Параметры:
//  СоответствиеСертификатовПодписаннымЭд - Соответствие - ключ: СправочникСсылка.СертификатыЭлектроннойПодписиИШифрования,
//														   значение: массив, элементы - СправочникСсылка.ЭДПрисоединенныеФайлы;
//  КонтекстОперации - Структура - контекст операции, см. ЭлектронноеВзаимодействиеСлужебный.НовыйКонтекстОперации.
//  ЭДСНезавершеннойПодписью - Массив - массив документов, по которым подписание еще не завершено, 
//										элементы: СправочникСсылка.ЭДПрисоединенныеФайлы.
//
Процедура ДействияПослеПодписанияЭДНаСервере(СоответствиеСертификатовПодписаннымЭд, КонтекстОперации, ЭДСНезавершеннойПодписью = Неопределено) Экспорт
	
	ОбменСКонтрагентамиСлужебный.ДействияПослеПодписанияЭДНаСервере(
		СоответствиеСертификатовПодписаннымЭд, КонтекстОперации, ЭДСНезавершеннойПодписью);
	
КонецПроцедуры

// Создание и отправка пакетов электронных документов.
//
// Параметры:
//  ДобавленныеФайлы - Массив - список электронных документов.
//  ПризнакПодписи - Булево - признак подписи.
//  СоотвСоглашенийИПараметровСертификатов -Соответствие - соответствие соглашений и сертификатов.
//  КонтекстОперации - Структура - Контекст операции, см. ЭлектронноеВзаимодействиеСлужебный.НовыйКонтекстОперации.
// 
// Возвращаемое значение:
//  Структура - результат выполнения функции.
//
Функция СоздатьИОтправитьДокументыПЭД(Знач ДобавленныеФайлы,
									  Знач ПризнакПодписи,
									  Знач СоотвСоглашенийИПараметровСертификатов = Неопределено,
									  КонтекстОперации = Неопределено) Экспорт
	
	СтруктураРезультата = ОбменСКонтрагентамиСлужебный.СоздатьИОтправитьДокументыПЭД(
		ДобавленныеФайлы, ПризнакПодписи, СоотвСоглашенийИПараметровСертификатов,
		КонтекстОперации);
	
	Возврат СтруктураРезультата;
	
КонецФункции

// Отправка пакетов электронных документов.
//
// Параметры:
//  МассивПакетовЭД - Массив - отправляемые пакеты.
//  СоотвСоглашенийИПараметровСертификатов - Соответствие - Соответствие соглашений и сертификатов.
//  ОтправленныеДокументы - Массив - отправленные электронные документы.
//  КонтекстОперации - Структура - контекст операции, см. ЭлектронноеВзаимодействиеСлужебный.НовыйКонтекстОперации.
// 
// Возвращаемое значение:
//  Структура:
//    * КоличествоОтправленных - Число - количество отправленных пакетов.
//    * ПакетыДляПовторнойОтправки - Массив - пакеты, которые требуется отправить повторно из-за истечения срока действия маркера доступа.
//
Функция ОтправкаПакетовЭД(Знач МассивПакетовЭД, Знач СоотвСоглашенийИПараметровСертификатов,
	ОтправленныеДокументы = Неопределено, КонтекстОперации = Неопределено, ПовторнаяОтправка = Ложь) Экспорт
	
	РезультатОтправки = ОбменСКонтрагентамиСлужебный.ОтправкаПакетовЭД(
		МассивПакетовЭД, СоотвСоглашенийИПараметровСертификатов, ОтправленныеДокументы, ПовторнаяОтправка, КонтекстОперации);
	
	Возврат РезультатОтправки;
	
КонецФункции

// Параметры использования сертификатов при отправке и получении электронных документов.
//
// Параметры:
//  КонтекстОперации - Структура - контекст операции, см. ЭлектронноеВзаимодействиеСлужебный.НовыйКонтекстОперации.
//
// Возвращаемое значение:
//  Структура - параметры использования сертификатов.
//
Функция ПараметрыОтправкиПолученияЭД(КонтекстОперации = Неопределено) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.ПараметрыОтправкиПолученияЭД(КонтекстОперации);
	
КонецФункции

#КонецОбласти

#Область РаботаСФайлами

// Получает текстовое представление версии электронного документа.
//
// Параметры:
//  СсылкаНаВладельца - Ссылка - объект, состояние версии электронного документа которого необходимо получить.
//  Гиперссылка - Булево - Истина, если необходимо реквизит формы "СостояниеЭД" сделать гиперссылкой.
//
// Возвращаемое значение:
//  Строка - текстовое описание состояния электронного документа.
//
Функция ТекстСостоянияЭД(СсылкаНаВладельца, Гиперссылка) Экспорт
	
	Результат = ""; 
		
	Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
		
		Если ОбменСКонтрагентамиВнутренний.ДокументЯвляетсяСчетомФактурой(СсылкаНаВладельца) Тогда
			Результат = ПолучитьТекстСводногоСостоянияЭД(СсылкаНаВладельца);
		Иначе
			ТекущееСостояниеЭДО = СостояниеВерсииЭД(СсылкаНаВладельца);
			Результат = Строка(ТекущееСостояниеЭДО);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Результат) Тогда
			Если Не ЕстьПравоЧтенияЭД() Тогда
				ШаблонСостоянияЭД = НСтр("ru = '%1 «Недостаточно прав для чтения ЭДО»'");
				Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСостоянияЭД, Результат);
				Гиперссылка = Ложь;
			Иначе
				Гиперссылка = Истина;
			КонецЕсли;
		Иначе
			ПараметрыЭД = Неопределено;
			
			НастройкаСуществует = НастройкаЭДСуществует(СсылкаНаВладельца, ПараметрыЭД);
			
			НастройкиВнутреннегоЭДО = Неопределено;
			Если ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Внутренний Тогда
				НастройкиВнутреннегоЭДО = ОбменСКонтрагентамиСлужебный.НастройкиОсновногоВидаВнутреннегоДокумента(
					СсылкаНаВладельца, ПараметрыЭД.Организация);
			КонецЕсли; 
			
			СтатусПодключения = Неопределено;
			ПриглашениеОтклонено = ПараметрыЭД.Свойство("СтатусПодключения", СтатусПодключения)
				И СтатусПодключения = Перечисления.СтатусыПриглашений.Отклонено; 

			Если (Не НастройкаСуществует Или ПриглашениеОтклонено) И Не ЗначениеЗаполнено(НастройкиВнутреннегоЭДО) Тогда
				Гиперссылка = Истина;
				ЗаполнитьТекстПриглашенияКЭДО(Результат, ПараметрыЭД, СсылкаНаВладельца, Гиперссылка);
			ИначеЕсли ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Внутренний Тогда
				Гиперссылка = Ложь;
				Результат = НСтр("ru = 'Не начат'");
			Иначе
				Гиперссылка = Ложь;
				Результат = НСтр("ru = 'ЭДО не начат'");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет текст приглашения контрагента к ЭДО.
//
// Параметры:
//  ТекстПриглашения - Строка- заполняемый текст надписи формы.
//  ПараметрыЭД - Структура - свойства электронного документа, соответствующего документу ИБ.
//  СсылкаНаВладельца - ДокументСсылка - ссылка на документ ИБ.
//  Гиперссылка - Булево - определяет отображать ли надпись на форме как гиперссылку.
//
Процедура ЗаполнитьТекстПриглашенияКЭДО(ТекстПриглашения, ПараметрыЭД, СсылкаНаВладельца, Гиперссылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ПередачаТоваровМеждуОрганизациями
		Или ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ВозвратТоваровМеждуОрганизациями Тогда
		
		ТекстПриглашения = НСтр("ru = 'Нет действующей настройки ЭДО с организацией-получателем'");
	ИначеЕсли ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.Внутренний 
		И ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД")
		И ПолучитьФункциональнуюОпцию("ИспользоватьВнутренниеДокументыЭДО") Тогда
			ТекстПриглашения = НСтр("ru = 'Не начат'");
	ИначеЕсли ЗначениеЗаполнено(ПараметрыЭД.Контрагент) Тогда
		
		Гиперссылка = Истина;
		
		Если ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий Тогда
			ШаблонТекстаНавигационнойСсылки = НСтр("ru = 'Пригласить %1 к обмену электронными документами в 1С-ЭДО'");
		Иначе
		    ШаблонТекстаНавигационнойСсылки = НСтр("ru = 'Начать обмен электронными документами с %1'");
		КонецЕсли;
		
		ТекстПриглашения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаНавигационнойСсылки, ПараметрыЭД.Контрагент);
		
	КонецЕсли;
	
КонецПроцедуры

// Перезаполняет документ ИБ на основании актуального ЭД.
Функция ПерезаполнитьДокумент(Знач ПараметрыЗаполнения, КонтекстОперации) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Отказ", Ложь);
	Результат.Вставить("НетПраваОбработкиЭД", Ложь);
	Результат.Вставить("МассивДокументов", Новый Массив);
	Результат.Вставить("СопоставитьНоменклатуру", Ложь);
	Результат.Вставить("ПараметрыСопоставления", Неопределено);
	
	Если НЕ ЕстьПравоОбработкиЭД(Истина) Тогда
		Результат.Отказ = Истина;
		Результат.НетПраваОбработкиЭД = Истина;
		Возврат Результат;
	КонецЕсли;
	
	МассивПроведенныхДокументов = ОбменСКонтрагентамиСлужебный.МассивПроведенныхДокументов(
		ПараметрыЗаполнения.МассивСсылок);
		
	Шаблон = НСтр("ru = 'Перезаполнение документа %1.
						|Операция возможна только для непроведенных документов.'");
	Для Каждого Документ Из МассивПроведенныхДокументов Цикл
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Документ);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецЦикла;
	
	МассивСсылок = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ПараметрыЗаполнения.МассивСсылок, МассивПроведенныхДокументов);
	
	Если МассивСсылок.Количество() = 0 Тогда
		Результат.Отказ = Истина;
		Возврат Результат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗаполнения.ЭД) Тогда
		СоответствиеВладельцевИЭД = Новый Соответствие;
		Для Каждого ТекущийДокумент Из МассивСсылок Цикл
			СоответствиеВладельцевИЭД.Вставить(ТекущийДокумент, ПараметрыЗаполнения.ЭД);
		КонецЦикла;
	Иначе
		СоответствиеВладельцевИЭД = ВладельцыИЭлектронныеДокументы(МассивСсылок);
	КонецЕсли;
	
	Если СоответствиеВладельцевИЭД.Количество() = 0 Тогда
		Для Каждого ТекущийДокумент Из МассивСсылок Цикл
			Шаблон = НСтр("ru = 'Электронный документ для %1 не найден'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, ТекущийДокумент);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		КонецЦикла;
		
		Результат.Отказ = Истина;
		Возврат Результат;
	КонецЕсли;
	
	Для каждого ЭлементСоответствия Из СоответствиеВладельцевИЭД Цикл
		
		ДокументИБ = ЭлементСоответствия.Ключ;
		СсылкаНаЭД = ЭлементСоответствия.Значение;
		
		Если НЕ ЗначениеЗаполнено(ДокументИБ) ИЛИ НЕ ЗначениеЗаполнено(СсылкаНаЭД) Тогда
			Результат.Отказ = Истина;
			Возврат Результат;
		КонецЕсли;
		
		ПараметрыПерезаполнения = ОбменСКонтрагентамиСлужебный.ДополнительныеПараметрыПерезаполненияДокументаИБ();
		
		ОбменСКонтрагентамиСлужебный.ПерезаполнитьДокументыИБПоЭД(ДокументИБ, СсылкаНаЭД,
			ПараметрыЗаполнения.СпособОбработки, КонтекстОперации, ПараметрыПерезаполнения);
		
		Если ПараметрыПерезаполнения.ДокументЗагружен Тогда
			
			Результат.МассивДокументов.Добавить(ДокументИБ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает двоичные данные электронного документа.
//
// Параметры:
//  СсылкаНаФайлЭД - СправочникСсылка.ЭДПрисоединенныеФайлы - электронный документ.
//  СертификатПодписи - Ссылка - ссылка на элемент справочника Сертификаты электронной подписи.
//  КонтекстОперации - Структура - контекст операции, см. ЭлектронноеВзаимодействиеСлужебный.НовыйКонтекстОперации.
//
// Возвращаемое значение:
//  ДвоичныеДанные - двоичные данные электронного документа.
//
Функция ПолучитьДвоичныеДанныеФайла(СсылкаНаФайлЭД, СертификатПодписи, КонтекстОперации) Экспорт
	
	ДвоичныеДанныеЭД = ОбменСКонтрагентамиСлужебный.ПолучитьДвоичныеДанныеФайла(
		СсылкаНаФайлЭД, СертификатПодписи, КонтекстОперации);
	
	Возврат ДвоичныеДанныеЭД;
	
КонецФункции

// Выполняет последовательность действий для электронных документов.
//
// Параметры:
//  МассивСсылокНаОбъект - Массив - ссылки на электронные документы, для которых надо определить последовательность действий.
//  МассивОтпечатковСертификатов - Массив - содержит отпечатки доступных сертификатов.
//  Действия - Строка - строковое представление необходимых действий.
//  ДопПараметры - Структура - дополнительные параметры, определяющие последовательность действий с электронными документами.
//  ЭД - СправочникСсылка.ЭДПрисоединенныеФайлы, Массив - ссылки на элемент справочника ЭДПрисоединенныеФайлы.
//  СоотвСертификатовИПаролей - Соответствие - ключ - СертификатЭП, значение - пароль к сертификату.
//
// Возвращаемое значение:
//  Структура, Неопределено - результат выполненных действий.
//
Функция ВыполнитьДействияПоЭД(Знач МассивСсылокНаОбъект,
							  Знач МассивОтпечатковСертификатов,
							  Знач Действия,
							  ДопПараметры,
							  Знач ЭД,
							  Знач СоотвСертификатовИПаролей) Экспорт
	
	СтруктураВозврата = ОбменСКонтрагентамиСлужебный.ВыполнитьДействияПоЭД(
		МассивСсылокНаОбъект, МассивОтпечатковСертификатов, Действия, ДопПараметры, ЭД, СоотвСертификатовИПаролей);
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Отправка и получение электронных документов.
//
// Параметры:
//  СоответствиеПрофилейИПараметровСертификатов - Соответствие - данные профилей и параметров сертификатов.
//  ПовторноеПолучение - Булево - если Истина, то производится только получение данных.
// 
// Возвращаемое значение:
//  Структура - данные отправки и получения документов.
//
Функция ОтправитьИПолучитьДокументы(СоответствиеПрофилейИПараметровСертификатов, ПовторноеПолучение = Ложь, КонтекстОперации = Неопределено) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("КолОтправленныхПакетов", 0);
	КолОтправленныхПакетов = ОбменСКонтрагентамиСлужебный.ОтправкаСформированныхЭД(СоответствиеПрофилейИПараметровСертификатов);
	СтруктураВозврата.Вставить("КолОтправленныхПакетов", КолОтправленныхПакетов);
	
	НовыеДокументы = ОбменСКонтрагентамиСлужебный.ПолучитьНовыеЭД(СоответствиеПрофилейИПараметровСертификатов,
		ПовторноеПолучение, КонтекстОперации, Не ПовторноеПолучение);
	
	ОбменСКонтрагентамиСлужебный.ДополнитьПакетамиКРаспаковке(НовыеДокументы.МассивПакетовЭД, НовыеДокументы.Профили);
	СтруктураВозврата.Вставить("НовыеДокументы", НовыеДокументы);
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#Область Сертификаты

// Функция получает данные по сертификатам, разрешенным для использования при подписании ЭД и авторизации
// на сервере оператора ЭДО. Поиск сертификатов выполняется как пересечение массивов сертификатов установленных в
// личном хранилище (клиента либо сервера, в зависимости от настроек в 1с), с сертификатами импортированными в 1с и
// зарегистрированными у оператора ЭДО (регистрация у оператора отражается в таб.части соглашения об обмене ЭД
// "СертификатыПодписейОрганизации"). При необходимости, выборка может быть ограничена массивом соглашений, по которым
// требуется определить параметры сертификатов.
//
// Параметры:
//  ПрофилиНастроекЭДО - Массив - ссылки на профили настроек ЭДО, по которым требуется определить сертификаты;
//  ЭлектронныеДокументы - Массив - содержит ссылки на электронные документы;
//  СтМассивовСтруктурСертификатов - Структура - может содержать 2 элемента: МассивСтруктурСертификатовСервер
//    и МассивСтруктурСертификатовКлиент, соответственно массив структур сертификатов личного хранилища с сервера и
//    то же самое с клиента.
//  КонтекстОперации - Структура - контекст операции, см. ЭлектронноеВзаимодействиеСлужебный.НовыйКонтекстОперации.
//
// Возвращаемое значение:
//  Структура - пустая, либо содержит 3 элемента:
//    СоотвСоглашенийИСертификатовПодписи;
//    СоотвСоглашенийИСертификатовАвторизации;
//    СоотвСертификатовИИхСтруктур.
//
Функция НастройкиЭДОИСертификатыАвторизации(Знач ПрофилиНастроекЭДО, Знач ЭлектронныеДокументы = Неопределено,
	Знач СтМассивовСтруктурСертификатов = Неопределено, КонтекстОперации = Неопределено) Экспорт  
	
	СтруктураСоответствий = ОбменСКонтрагентамиСлужебный.НастройкиЭДОИСертификатыАвторизации(
		ПрофилиНастроекЭДО, КонтекстОперации, ЭлектронныеДокументы, СтМассивовСтруктурСертификатов);
	
	Возврат СтруктураСоответствий;
	
КонецФункции

// Возвращает соответствие с данными о доступных сертификатах.
//
// Параметры:
//   МассивОтпечатковСертификатов - Массив - массив структур сертификатов, установленных в личном хранилище 
//                                           на компьютере пользователя.
//   НастройкаЭДО - СправочникСсылка.СоглашенияОбОбменеЭД - будут выбраны сертификаты,
//                  зарегистрированные в указанном соглашении и доступные текущему пользователю,
//                - СправочникСсылка.ПрофилиНастроекЭДО - будут выбраны сертификаты,
//                  зарегистрированные в указанном профиле и доступные текущему пользователю,
//                - Неопределено - будут выбраны сертификаты доступные текущему пользователю.
//   ОграничиватьПоПользователям - Булево - определяет, будет ли применен фильтр по пользователям при выборе
//                                          доступных сертификатов.
//   ПоказыватьОшибку - Булево - если Ложь, то ошибка создания менеджера криптографии не отображается.
//
// Возвращаемое значение:
//  Соответствие - доступные сертификаты:
//    * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - ключ соответствия.
//    * Значение - Структура - данные сертификата.
//
Функция СоответствиеДоступныхСертификатовИПараметров(Знач МассивОтпечатковСертификатов,
	Знач НастройкаЭДО = Неопределено, ОграничиватьПоПользователям = Истина, ПоказыватьОшибку = Истина) Экспорт
	
	Если ЭлектронноеВзаимодействиеСлужебный.ВыполнятьКриптооперацииНаСервере() Тогда
		МассивОтпечатковСервера = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.МассивОтпечатковСертификатов(ПоказыватьОшибку);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивОтпечатковСертификатов, МассивОтпечатковСервера);
	КонецЕсли;
	МассивСтруктур = ОбменСКонтрагентамиСлужебный.МассивСтруктурДоступныхДляПодписиСертификатов(
														МассивОтпечатковСертификатов, НастройкаЭДО, ОграничиватьПоПользователям);
	
	СоответствиеСертификатовИПаролей = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ПарольКСертификату(,МассивОтпечатковСертификатов);
	ДанныеВозврата = Новый Соответствие;
	Если ТипЗнч(МассивСтруктур) = Тип("Массив") Тогда
		Для Каждого Элемент Из МассивСтруктур Цикл
			Если ЗначениеЗаполнено(СоответствиеСертификатовИПаролей) Тогда
				ПарольКСертификату = СоответствиеСертификатовИПаролей.Получить(Элемент.Сертификат);
				Если ПарольКСертификату <> Неопределено Тогда
					Элемент.ПарольПользователя = ПарольКСертификату;
					Элемент.ПарольПолучен = Истина;
				КонецЕсли;
			КонецЕсли;
			ДанныеВозврата.Вставить(Элемент.Сертификат, Элемент);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеВозврата;
	
КонецФункции

// Возвращает свойства сертификата, необходимые для открытия формы сертификата.
//
// Параметры:
//  АдресДанныхСертификата - Строка - адрес во временном хранилище, где находятся двоичные данные сертификата.
//
// Возвращаемое значение:
//  Структура - описание полей см. ЭлектроннаяПодписьКлиентСервер.ЗаполнитьСтруктуруСертификата(ВыбранныйСертификат).
//
Функция СвойстваСертификата(АдресДанныхСертификата) Экспорт
	
	ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(АдресДанныхСертификата);
	
	ВыбранныйСертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
	Если ВыбранныйСертификат=Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Сертификат не найден'"));
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураСертификата = ЭлектроннаяПодпись.СвойстваСертификата(ВыбранныйСертификат);
	
	Возврат СтруктураСертификата;
	
КонецФункции

// Метод проверяет наличие в регистре сведений записей о новых ЭД.
//
// Возвращаемое значение:
//  Булево - признак наличия в сервисе новых электронных документов.
//
Функция ЕстьСобытияЭДО() Экспорт
	
	ЕстьНовыеСобытия = Ложь;
	
	Если Не ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОповещатьОСобытияхЭДО() Тогда
		Возврат ЕстьНовыеСобытия;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК ЕстьНовыеЭД
	|ИЗ
	|	РегистрСведений.НаличиеНовыхЭлектронныхДокументов КАК НаличиеНовыхЭлектронныхДокументов";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		ЕстьНовыеСобытия = Истина;
	КонецЕсли;
	
	Возврат ЕстьНовыеСобытия;
	
КонецФункции

// Возвращает название и версию конфигурации.
// 
// Возвращаемое значение:
//  Строка - название и версия конфигурации.
//
Функция ПараметрыЕстьИдея() Экспорт

	ШаблонКонфигурации = НСтр("ru = '%1 (%2)'");
	Конфигурация = СтрШаблон(ШаблонКонфигурации, Метаданные.Синоним, Метаданные.Версия);
	
	СтрокаВозврата = "?conf=" + КодироватьСтроку(Конфигурация, СпособКодированияСтроки.КодировкаURL);
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеАутентификации <> Неопределено Тогда
		СтрокаВозврата = СтрокаВозврата + "&login="
			+ КодироватьСтроку(ДанныеАутентификации.Логин, СпособКодированияСтроки.КодировкаURL);
	КонецЕсли;
	
	Возврат СтрокаВозврата;

КонецФункции

// Возвращает признак использования сертификатов пользователя в облачном сервисе.
//
// Возвращаемое значение:
//  Булево - флаг использования в сертификатов пользователя в облачном сервисе.
//
Функция ИспользованиеСертификатовОблачногоСервисаВозможно() Экспорт
	
	ИспользованиеВозможно = ОбменСКонтрагентамиСлужебный.ИспользованиеСертификатовОблачногоСервисаВозможно();
	
	Возврат ИспользованиеВозможно;
	
КонецФункции

// Определяет принадлежность сертификата пользователя к облачному сервису.
//
// Параметры:
//  Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат электронной подписи и шифрования.
//
// Возвращаемое значение:
//  Булево - Истина, если сертификат является облачным.
//
Функция ЭтоСертификатОблачногоСервиса(Сертификат) Экспорт
	
	ВозвращаемоеЗначение = ОбменСКонтрагентамиСлужебный.ЭтоСертификатОблачногоСервиса(Сертификат);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Получает отпечатки сертификатов пользователя в облачном сервисе.
//
// Параметры:
//  ОтпечаткиОблачныхСертификатов - Соответствие - Ключ - отпечаток в формате строки Base64, а Значение - Истина;
//
Процедура ПолучитьЛичныеОтпечаткиВСервисе(ОтпечаткиСертификатов) Экспорт
	
	ОбменСКонтрагентамиСлужебный.ПолучитьЛичныеОтпечаткиВСервисе(ОтпечаткиСертификатов);
	
КонецПроцедуры
	
// Изменяет реквизиты элемента справочника ЭДПрисоединенныеФайлы.
//
// Параметры:
//  Файлы - Массив - массив ссылок на элементы справочника с электронным документом.
//  СтруктураЭД - Структура - Структура параметров, которые необходимо заполнить в справочнике.
//  ПоверятьОбязательныеРеквизиты - Булево - признак проверки обязательных реквизитов.
//
Процедура ИзменитьПоСсылкеПрисоединенныеФайлы(Файлы, СтруктураЭД, ПоверятьОбязательныеРеквизиты = Истина) Экспорт
	Для Каждого Файл Из Файлы Цикл
		ОбменСКонтрагентамиСлужебный.ИзменитьПоСсылкеПрисоединенныйФайл(Файл, СтруктураЭД, ПоверятьОбязательныеРеквизиты);
	КонецЦикла;	
КонецПроцедуры	

// Запускает длительную операцию по проверке организации в сервисе 1С-ЭДО.
//
// Параметры:
//   Организация - ОпределяемыйТип.Организация - ссылка на определяемый справочник Организация.
//   ИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы, 
//     во временное хранилище которой надо поместить результат выполнения процедуры.
//
Функция ЗапуститьЗаданиеНаПроверкуОрганизацииВ1СЭДО(Знач Организация, Знач ИдентификаторФормы) Экспорт
	
	Если ЗначениеЗаполнено(ИдентификаторФормы) Тогда
		УникальныйИдентификатор = ИдентификаторФормы;
	Иначе
		УникальныйИдентификатор = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка организации в сервисе 1С-ЭДО.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСКонтрагентамиСлужебный.ПроверитьОрганизациюВ1СЭДО", Организация, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти

#Область Права

// Возвращает признак наличия у пользователя прав на чтение электронных документов.
//
// Параметры:
//  ВыводитьСообщение - Булево - признак необходимости выводить сообщение о нарушении прав доступа.
//
// Возвращаемое значение:
//  Булево - Истина, если у пользователя есть необходимые права, иначе Ложь.
//
Функция ЕстьПравоЧтенияЭД(ВыводитьСообщение = Ложь) Экспорт
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЕстьПраво = Истина;
	
	ОбменСКонтрагентамиПереопределяемый.ПриПроверкеПраваЧтенияЭлектронныхДокументов(ЕстьПраво);
	
	Если ЕстьПраво Тогда
		
		// Основные объекты метаданных, доступ к которым определяет доступ к элементарной функции.
		ОбъектыЭлементарнойФункции = Новый Массив;
		ОбъектыЭлементарнойФункции.Добавить(Метаданные.Справочники.ЭДПрисоединенныеФайлы);
		
		Для каждого Объект Из ОбъектыЭлементарнойФункции Цикл
			
			Если Не ПравоДоступа("Чтение", Объект) Тогда
				ЕстьПраво = Ложь;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ЕстьПраво И ВыводитьСообщение Тогда
		
		ЭлектронноеВзаимодействиеСлужебный.СообщитьПользователюОНарушенииПравДоступа();
		
	КонецЕсли;
		
	Возврат ЕстьПраво;
		
КонецФункции

// Возвращает признак наличия у пользователя прав на добавление и изменение электронных документов.
//
// Параметры:
//  ВыводитьСообщение - Булево - признак необходимости выводить сообщение о нарушении прав доступа.
//
// Возвращаемое значение:
//  Булево - Истина, если у пользователя есть необходимые права, иначе Ложь.
//
Функция ЕстьПравоОбработкиЭД(ВыводитьСообщение = Ложь) Экспорт
	
	ЕстьПраво = ОбменСКонтрагентамиСлужебный.ЕстьПравоОбработкиЭД(ВыводитьСообщение);
		
	Возврат ЕстьПраво;
	
КонецФункции

// Возвращает признак наличия у пользователя прав на настройку ЭДО.
//
// Параметры:
//  ВыводитьСообщение - Булево - признак необходимости выводить сообщение о нарушении прав доступа.
//
// Возвращаемое значение:
//  Булево - Истина, если у пользователя есть необходимые права, иначе Ложь.
//
Функция ЕстьПравоНастройкиОбмена(ВыводитьСообщение = Ложь) Экспорт
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЕстьПраво = Истина;
	
	// Основные объекты метаданных, доступ к которым определяет доступ к элементарной функции.
	ОбъектыЭлементарнойФункции = Новый Массив;
	ОбъектыЭлементарнойФункции.Добавить(Метаданные.РегистрыСведений.УчетныеЗаписиЭДО);
	
	Для каждого Объект Из ОбъектыЭлементарнойФункции Цикл
		
		Если Не ПравоДоступа("Изменение", Объект) Тогда
			ЕстьПраво = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЕстьПраво И ВыводитьСообщение Тогда
		
		ЭлектронноеВзаимодействиеСлужебный.СообщитьПользователюОНарушенииПравДоступа();
		
	КонецЕсли;
		
	Возврат ЕстьПраво;
	
КонецФункции

// Возвращает признак наличия у пользователя прав на выполнение ЭДО.
//
// Параметры:
//  ВыводитьСообщение - Булево - признак необходимости выводить сообщение о нарушении прав доступа.
//
// Возвращаемое значение:
//  Булево - Истина, если у пользователя есть необходимые права, иначе Ложь.
//
Функция ЕстьПравоВыполненияОбмена(ВыводитьСообщение = Ложь) Экспорт
	
	ЕстьПраво = ОбменСКонтрагентамиСлужебный.ЕстьПравоВыполненияОбмена(ВыводитьСообщение);
		
	Возврат ЕстьПраво;
	
КонецФункции

#КонецОбласти

#Область Прочее

// см. ОбменСКонтрагентамиСлужебный.НастройкиВнутреннегоЭДО
Функция НастройкиВнутреннегоЭДО(Знач Организация, Знач ВидВнутреннегоДокумента) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.НастройкиВнутреннегоЭДО(Организация, ВидВнутреннегоДокумента);
	
КонецФункции

// см. ОбменСКонтрагентамиСлужебный.ЗаполнитьПараметрыЭДПоИсточнику 
Функция ЗаполнитьПараметрыЭДПоИсточнику(Источник, ФорматCML = Ложь, ВидЭД = Неопределено, БыстрыйОбмен = Ложь) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.ЗаполнитьПараметрыЭДПоИсточнику(Источник, ФорматCML, ВидЭД, БыстрыйОбмен);
	
КонецФункции 

// см. ОбменСКонтрагентамиСлужебный.ПакетЭДПоПрисоединенномуФайлу
Функция ПакетЭДПоПрисоединенномуФайлу(ПрисоединенныйФайл) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.ПакетЭДПоПрисоединенномуФайлу(ПрисоединенныйФайл);
	
КонецФункции

Функция ЗапуститьПолучениеНастроекЭДОИПараметровСертификатов(Знач ПараметрыЗадания, Знач УникальныйИдентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Обмен с контрагентами. Получение настроек ЭДО и параметров сертификатов.'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ОбменСКонтрагентамиСлужебный.ПолучитьНастройкиЭДОИПараметрыСертификатовВФоне",
		ПараметрыЗадания, ПараметрыВыполнения);
	
КонецФункции

Функция ЗапуститьОтправкуПолучениеПакетовЭлектронныхДокументов(Знач ПараметрыЗадания, Знач УникальныйИдентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Обмен с контрагентами. Отправка и получение электронных документов.'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ОбменСКонтрагентамиСлужебный.ОтправитьПолучитьПакетыЭлектронныхДокументовВФоне",
		ПараметрыЗадания, ПараметрыВыполнения);
	
КонецФункции

Функция ЗапуститьРаспаковкуПакетовЭлектронныхДокументов(Знач ПараметрыЗадания, Знач УникальныйИдентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Обмен с контрагентами. Распаковка пакетов электронных документов.'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ОбменСКонтрагентамиСлужебный.РаспаковатьПакетыЭлектронныхДокументовВФоне",
		ПараметрыЗадания, ПараметрыВыполнения);
	
КонецФункции

Процедура ОтменитьВыполнениеЗадания(Знач ИдентификаторЗадания) Экспорт
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

// Определяет форму просмотра электронного документа в зависимости от типа переданной ссылки.
//
// Параметры:
//  СсылкаДляПросмотра  - ДокументСсылка.ЭлектронныйДокументИсходящий, ДокументСсылка.ЭлектронныйДокументВходящий,
//        СправочникСсылка.ЭДПрисоединенныеФайлы, ОпределяемыйТип.ОснованияЭлектронныхДокументов - ссылка на объект для просмотра электронного документа.
//
// Возвращаемое значение:
//  Структура - данные для открытия формы просмотра электронного документа.
//  Неопределено - в случае, если не удалось определить форму просмотра.
//
Функция ФормаПросмотраЭлектронногоДокумента(Знач СсылкаДляПросмотра) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ЭлектронныйДокумент = Неопределено;
	
	ТипСсылкиДляПросмотра = ТипЗнч(СсылкаДляПросмотра);
	Если ТипСсылкиДляПросмотра = Тип("СправочникСсылка.ЭДПрисоединенныеФайлы") Тогда
		ЭлектронныйДокумент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаДляПросмотра, "ВладелецФайла");
		ПараметрыФормы.Вставить("ФайлЭлектронногоДокумента", СсылкаДляПросмотра);
		
	ИначеЕсли ТипСсылкиДляПросмотра = Тип("ДокументСсылка.ЭлектронныйДокументИсходящий")
		ИЛИ ТипСсылкиДляПросмотра = Тип("ДокументСсылка.ЭлектронныйДокументВходящий") Тогда
		ЭлектронныйДокумент = СсылкаДляПросмотра;
		
	Иначе
		СоответствиеВладельцевИЭД = ОбменСКонтрагентамиСлужебный.ВладельцыИЭлектронныеДокументы(СсылкаДляПросмотра);
		ЭлектронныйДокумент = СоответствиеВладельцевИЭД.Получить(СсылкаДляПросмотра);
		
	КонецЕсли;
	
	Если ЭлектронныйДокумент = Неопределено Тогда
		ТекстШаблона = НСтр("ru = '%1. Электронный документ не найден.'");
		ТекстСообщения = СтрШаблон(ТекстШаблона, СсылкаДляПросмотра);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
		
	ИначеЕсли ТипЗнч(ЭлектронныйДокумент) = Тип("ДокументСсылка.ЭлектронныйДокументИсходящий") Тогда
		ВыбраннаяФорма = "Документ.ЭлектронныйДокументИсходящий.Форма.ФормаПросмотраЭД";
		ПараметрыФормы.Вставить("Ключ", ЭлектронныйДокумент);
		
	Иначе
		ВыбраннаяФорма = "Документ.ЭлектронныйДокументВходящий.Форма.ФормаПросмотраЭД";
		ПараметрыФормы.Вставить("Ключ", ЭлектронныйДокумент);
	КонецЕсли;
	
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("ИмяФормы", ВыбраннаяФорма);
	ДанныеФормы.Вставить("ПараметрыФормы", ПараметрыФормы);
	
	Возврат ДанныеФормы;
	
КонецФункции

// Формирует новый произвольный электронный документ.
//
// Параметры:
//  ПараметрыФормирования	 - Структура - описывает стороны электронного документооборота:
//                                         * Контрагент - ОпределяемыйТип.КонтрагентБЭД - контрагент, которому нужно отправить документ.
//                                         * Организация - ОпределяемыйТип.Организация - организация, от имени которой нужно отправить документ.
//                                         * ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентом - договор, по которому отправляется документ (необязателен).
//  ПараметрыФайла           - Структура - описывает файл, который нужно отправить:
//                                         * ИмяФайла - имя файла вместе с расширением.
//                                         * АдресХранилища - адрес временного хранилища, в котором содержатся двоичные данные файла.
//
// Возвращаемое значение:
//  ДокументСсылка.ЭлектронныйДокументИсходящий, Неопределено - ссылка на созданный документ или Неопределено, если не удалось создать его.
//
Функция НовыйПроизвольныйДокумент(ПараметрыФормирования, ПараметрыФайла) Экспорт

	Результат = Неопределено;
	
	// Определим настройки.
	ПараметрыЭД = Новый Структура();
	ПараметрыЭД.Вставить("ВидЭД", 		       Перечисления.ВидыЭД.ПроизвольныйЭД);
	ПараметрыЭД.Вставить("Организация",        ПараметрыФормирования.Организация);
	ПараметрыЭД.Вставить("Контрагент",	       ПараметрыФормирования.Контрагент);
	ПараметрыЭД.Вставить("ДоговорКонтрагента", ПараметрыФормирования.ДоговорКонтрагента);
	ПараметрыЭД.Вставить("НаправлениеЭД",      Перечисления.НаправленияЭД.Исходящий);
	НастройкиОбменаЭД = ОбменСКонтрагентамиСлужебный.ОпределитьНастройкиОбменаЭД(ПараметрыЭД);
	Если ЗначениеЗаполнено(НастройкиОбменаЭД) Тогда
		НачатьТранзакцию();
		Попытка
			// Создадим произвольный документ.
			ПроизвольныйДокумент = Документы.ЭлектронныйДокументИсходящий.СоздатьДокумент();
			ЗаполнитьЗначенияСвойств(ПроизвольныйДокумент, ПараметрыФормирования);
			ПроизвольныйДокумент.Дата                      = ТекущаяДатаСеанса();
			ПроизвольныйДокумент.ВидЭД                     = Перечисления.ВидыЭД.ПроизвольныйЭД;
			ПроизвольныйДокумент.ТипДокумента              = Перечисления.ТипыЭД.Прочее;
			ПроизвольныйДокумент.ВерсияРегламентаЭДО       = Перечисления.ВерсииРегламентаОбмена1С.Версия10;
			ПроизвольныйДокумент.ТипЭлементаВерсииЭД       = Перечисления.ТипыЭлементовВерсииЭД.ПервичныйЭД;
			ПроизвольныйДокумент.СостояниеЭДО              = Перечисления.СостоянияВерсийЭД.НеСформирован;
			ПроизвольныйДокумент.ДатаИзмененияСостоянияЭДО = ТекущаяДатаСеанса();
			ПроизвольныйДокумент.УникальныйИД              = Новый УникальныйИдентификатор;
			
			// Заполним служебные реквизиты.
			ПроизвольныйДокумент.ВерсияРегламентаЭДО         = НастройкиОбменаЭД.ВерсияРегламентаЭДО;
			ПроизвольныйДокумент.ИдентификаторКонтрагента    = НастройкиОбменаЭД.ИдентификаторКонтрагента;
			ПроизвольныйДокумент.ИдентификаторОрганизации    = НастройкиОбменаЭД.ИдентификаторОрганизации;
			ПроизвольныйДокумент.СпособОбменаЭД              = НастройкиОбменаЭД.СпособОбменаЭД;
			
			ПроизвольныйДокумент.ТребуетсяПодтверждение = ?(ЗначениеЗаполнено(НастройкиОбменаЭД), 
				НастройкиОбменаЭД.ТребуетсяПодтверждение, Ложь);
			ПроизвольныйДокумент.ТребуетсяИзвещение     = ?(ЗначениеЗаполнено(НастройкиОбменаЭД), 
				НастройкиОбменаЭД.ТребуетсяИзвещение, Ложь);
			МаршрутПодписания = Неопределено;
			Если Не НастройкиОбменаЭД.Свойство("МаршрутПодписания", ПроизвольныйДокумент.МаршрутПодписания)
				ИЛИ Не ЗначениеЗаполнено(ПроизвольныйДокумент.МаршрутПодписания)
				ИЛИ ПроизвольныйДокумент.МаршрутПодписания = Справочники.МаршрутыПодписания.УказыватьПриСоздании Тогда
				
				ПроизвольныйДокумент.МаршрутПодписания = Справочники.МаршрутыПодписания.ОднойДоступнойПодписью;	
			КонецЕсли;
			ПроизвольныйДокумент.Записать();
			
			// Добавим присоединенный файл
			ДобавленныйФайл = Документы.ЭлектронныйДокументИсходящий.ДобавитьПрисоединенныйФайл(ПроизвольныйДокумент,
				ПараметрыФайла.АдресХранилища, ПараметрыФайла.ИмяФайла);
				
			Если ЗначениеЗаполнено(ДобавленныйФайл) Тогда
				Результат = ПроизвольныйДокумент.Ссылка;
				ЗафиксироватьТранзакцию();
			Иначе
				ОтменитьТранзакцию();
			КонецЕсли;
		Исключение
			ОтменитьТранзакцию();
			ТекстИсключения = НСтр("ru = 'Не удалось создать произвольный документ по причине:'") + Символы.ПС
				+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ВызватьИсключение ТекстИсключения;
		КонецПопытки;
	КонецЕсли;
	
	
	Возврат Результат;
	
КонецФункции

// Процедура закрывает электронный документооборот для переданного массива ссылок на документы ИБ.
//
// Параметры:
//  МассивСсылокНаВладельцев - Массив - массив ссылок на документы ИБ, для которых требуется закрыть ЭДО.
//  ПричинаЗакрытия - Строка - описание причины закрытия ЭДО.
//  КоличествоОбработанныхЭД - Число - число документов ИБ, для которых ЭДО был закрыт.
//
Процедура ЗакрытьДокументыПринудительно(Знач МассивСсылокНаВладельцев, Знач ПричинаЗакрытия, КоличествоОбработанныхЭД, ВидВнутреннегоДокумента = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СводнаяИнформацияПоСтатусу = Новый Структура;
	СводнаяИнформацияПоСтатусу.Вставить("СНашейСтороны",             Перечисления.СводныеСостоянияЭД.ДействийНеТребуется);
	СводнаяИнформацияПоСтатусу.Вставить("СоСтороныДругогоУчастника", Перечисления.СводныеСостоянияЭД.ДействийНеТребуется);
	СводнаяИнформацияПоСтатусу.Вставить("Комментарий",               ПричинаЗакрытия);
	
	МассивДляОчисткиРегистра = Новый Массив;
	МассивДляОчисткиМаршрута = Новый Массив;
	Для Каждого Документ Из МассивСсылокНаВладельцев Цикл
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйДокументВходящий")
			Или ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйДокументИсходящий") Тогда
			НачатьТранзакцию();
			Попытка
				ДокументОбъект = ЭлектронноеВзаимодействиеСлужебный.ОбъектПоСсылкеДляИзменения(Документ);
				ДокументОбъект.СостояниеЭДО = Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно;
				ДокументОбъект.ДатаИзмененияСостоянияЭДО = ТекущаяДатаСеанса();
				ДокументОбъект.ПричинаОтклонения = ?(ПустаяСтрока(ДокументОбъект.ПричинаОтклонения), "", Символы.ПС) 
					+ ПричинаЗакрытия;
				ДокументОбъект.Записать();
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки;
			
			ОбменСКонтрагентамиСлужебный.ОбновитьВерсиюЭДВРегистре(Документ, Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно, СводнаяИнформацияПоСтатусу);
			МассивДляОчисткиМаршрута.Добавить(Документ);
			
			КоличествоОбработанныхЭД = КоличествоОбработанныхЭД + 1;
		Иначе
			МассивДляОчисткиРегистра.Добавить(Документ);
		КонецЕсли;
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВнутренниеДокументыЭДО") Тогда
		ОбменСКонтрагентамиСлужебный.ЗакрытьВнутренниеДокументыПринудительно(МассивДляОчисткиРегистра, ПричинаЗакрытия,
			КоличествоОбработанныхЭД, ВидВнутреннегоДокумента);
	КонецЕсли;
	
	Если МассивДляОчисткиРегистра.Количество() > 0 Тогда
		НачатьТранзакцию();
		Попытка
			Для каждого СсылкаНаОбъект Из МассивДляОчисткиРегистра Цикл
				ОтборПоСсылкеНаОбъект = Новый Структура("СсылкаНаОбъект", СсылкаНаОбъект);
				ЭлектронноеВзаимодействиеСлужебный.УстановитьУправляемуюБлокировку("РегистрСведений.СостоянияЭД", ОтборПоСсылкеНаОбъект);
			КонецЦикла;
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СостоянияЭД.СсылкаНаОбъект,
			|	СостоянияЭД.ЭлектронныйДокумент
			|ИЗ
			|	РегистрСведений.СостоянияЭД КАК СостоянияЭД
			|ГДЕ
			|	СостоянияЭД.СсылкаНаОбъект В(&МассивСсылок)";
			Запрос.УстановитьПараметр("МассивСсылок", МассивДляОчисткиРегистра);
			ТЗ = Запрос.Выполнить().Выгрузить();
			
			ТЗОбъектов = Новый ТаблицаЗначений;
			ТЗОбъектов.Колонки.Добавить("СсылкаНаОбъект");
			ТЗОбъектов.Колонки.Добавить("ЭлектронныйДокумент");
			
			Для Каждого Элемент Из МассивДляОчисткиРегистра Цикл
				НовСтрока = ТЗОбъектов.Добавить();
				СтрокаТЗ = ТЗ.Найти(Элемент, "СсылкаНаОбъект");
				ЭлектронныйДокумент = Неопределено;
				Если СтрокаТЗ <> Неопределено Тогда
					ЭлектронныйДокумент = СтрокаТЗ.ЭлектронныйДокумент;
					Если ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
						ДокументОбъект = ЭлектронныйДокумент.ПолучитьОбъект();
						ДокументОбъект.СостояниеЭДО = Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно;
						ДокументОбъект.ДатаИзмененияСостоянияЭДО = ТекущаяДатаСеанса();
						ДокументОбъект.Записать();
					КонецЕсли;
				КонецЕсли;
				НовСтрока.СсылкаНаОбъект = Элемент;
				НовСтрока.ЭлектронныйДокумент = ЭлектронныйДокумент;
				
				МассивДляОчисткиМаршрута.Добавить(ЭлектронныйДокумент);
			КонецЦикла;
			
			Для Каждого Строка Из ТЗОбъектов Цикл
				ЗаписьРегистра = РегистрыСведений.СостоянияЭД.СоздатьМенеджерЗаписи();
				ЗаписьРегистра.СсылкаНаОбъект = Строка.СсылкаНаОбъект;
				ЗаписьРегистра.ДействияСНашейСтороны = Перечисления.СводныеСостоянияЭД.ДействийНеТребуется;
				ЗаписьРегистра.ДействияСоСтороныДругогоУчастника = Перечисления.СводныеСостоянияЭД.ДействийНеТребуется;
				ЗаписьРегистра.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно;
				ЗаписьРегистра.ЭлектронныйДокумент = Строка.ЭлектронныйДокумент;
				ЗаписьРегистра.Комментарий = ПричинаЗакрытия;
				ЗаписьРегистра.Записать();
			КонецЦикла;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
		
		КоличествоОбработанныхЭД = КоличествоОбработанныхЭД + ТЗОбъектов.Количество();
	КонецЕсли;
	
	// Найдем все файлы, подчиненные переданным электронным документам
	Если МассивДляОчисткиМаршрута.Количество() Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ЭлектронныеДокументы", МассивДляОчисткиМаршрута);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭДПрисоединенныеФайлы.Ссылка
		|ИЗ
		|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
		|ГДЕ
		|	ЭДПрисоединенныеФайлы.ВладелецФайла В(&ЭлектронныеДокументы)";
		ЭДДляОчистки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		ЭлектронноеВзаимодействиеСлужебный.ОчиститьМаршрутПодписания(ЭДДляОчистки);
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьУсловиеПоАлгоритму(ПараметрАлгоритма, ИмяАлгоритмаПроверкиУсловия) Экспорт

	СтрокаВычисления = СтрШаблон("%1(Параметры)", ИмяАлгоритмаПроверкиУсловия);
	Возврат ОбщегоНазначения.ВычислитьВБезопасномРежиме(СтрокаВычисления, ПараметрАлгоритма); 

КонецФункции

Функция ИмяОбъектаМетаданных(Тип) Экспорт
	ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
	Если ОбъектМетаданных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат ОбъектМетаданных.Имя;
КонецФункции

Функция СсылкаНаОбъектНастройкиЭДО(Знач Организация, Знач Контрагент, Знач Договор) Экспорт
	Возврат ОбменСКонтрагентамиСлужебный.СсылкаНаОбъектНастройкиЭДО(Организация, Контрагент, Договор);
КонецФункции

Функция ЗаполнитьСоглашениеОбОбменеЭД(Знач Организация, Знач Контрагент, Знач Договор) Экспорт
	Возврат ОбменСКонтрагентамиСлужебный.ЗаполнитьСоглашениеОбОбменеЭД(Организация, Контрагент, Договор);
КонецФункции

// см. ОбщегоНазначения.ИдентификаторОбъектаМетаданных
Функция ИдентификаторОбъектаМетаданных(ОписаниеОбъектаМетаданных, ВызыватьИсключение = Истина) Экспорт
	Возврат ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОписаниеОбъектаМетаданных, ВызыватьИсключение);
КонецФункции 

Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита, ВыбратьРазрешенные = Ложь) Экспорт
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита, ВыбратьРазрешенные);
КонецФункции 

Функция АктуальныеВнутренниеЭД(ОбъектыУчета, ВидВнутреннегоДокумента) Экспорт
	
	СоответствиеАктуальныхВидовЭД = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктуальностьЭлектронныхДокументов.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
		|	АктуальностьЭлектронныхДокументов.ОбъектУчета КАК ОбъектУчета
		|ИЗ
		|	РегистрСведений.АктуальностьЭлектронныхДокументов КАК АктуальностьЭлектронныхДокументов
		|ГДЕ
		|	АктуальностьЭлектронныхДокументов.ОбъектУчета В(&ОбъектыУчета)
		|	И АктуальностьЭлектронныхДокументов.ВидЭлектронногоДокумента = &ВидЭлектронногоДокумента";
	
	Запрос.УстановитьПараметр("ОбъектыУчета", ОбъектыУчета);
	Запрос.УстановитьПараметр("ВидЭлектронногоДокумента", ВидВнутреннегоДокумента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоответствиеАктуальныхВидовЭД.Вставить(Выборка.ОбъектУчета, Выборка.ЭлектронныйДокумент);
	КонецЦикла;
	
	Возврат СоответствиеАктуальныхВидовЭД;
	
КонецФункции

// см. ОбменСКонтрагентамиСлужебный.НайтиСоздатьВидВнутреннегоДокумента 
Функция НайтиСоздатьВидВнутреннегоДокумента(ИдентификаторОбъектаУчета, КомандаПечати) Экспорт
	Возврат ОбменСКонтрагентамиСлужебный.НайтиСоздатьВидВнутреннегоДокумента(ИдентификаторОбъектаУчета, КомандаПечати);
КонецФункции 

// см. ОбменСКонтрагентамиСлужебный.ПолучитьИтоговыйСтатусЭД 
Функция ИтоговоеСостояниеЭД(Знач ОбъектыУчета, ИдентификаторКомандыПечати = Неопределено, ЭДОНачат = Ложь) Экспорт
	Возврат ОбменСКонтрагентамиСлужебный.ИтоговоеСостояниеЭД(ОбъектыУчета, ИдентификаторКомандыПечати, ЭДОНачат);
КонецФункции

Процедура ОбработатьПростоеОтклонениеАннулирование(Знач Параметры) Экспорт
	ОбменСКонтрагентамиСлужебный.ОбработатьПростоеОтклонениеАннулирование(Параметры);
КонецПроцедуры

#КонецОбласти

#Область НоваяАрхитектураЭДО

// Формирует ЭД документа и помещает файл в двоичные данные.
//
// Параметры:
//  ДокументОснования	     - ЛюбаяСсылка - Документ для формирования
//  ТекущиеНастройкиОбмена	 - Структура   - Настройки ЭДО для переопределения.
//
Функция СформироватьЭДПоОснованиюИСохранитьВФайл(Знач ДокументОснования, Знач ТекущиеНастройкиОбмена, ДопПараметры) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.СформироватьЭДПоОснованиюИСохранитьВФайл(ДокументОснования, ТекущиеНастройкиОбмена, ДопПараметры);
	
КонецФункции

// Запуск длительной операции для проверки состояния регистрации на сервере 1С:ЭДО.
//
// Параметры:
// ПараметрыОперации - Структура - Данные авторизации на сервере.
// УникальныйИдентификатор - Строка - Идентификатор формы
// Возвращаемое значение:
//  Структура - Длительная операция.
//
Функция НачатьПолучениеСостоянияРегистрацииСертификатаВ1СЭДО(ПараметрыОперации, УникальныйИдентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение результата регистрации в сервисе 1С:ЭДО'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
				"ОбменСКонтрагентамиСлужебный.ПолучениеСостоянияРегистрацииСертификатаВ1СЭДО", ПараметрыОперации, ПараметрыВыполнения);
	
КонецФункции

// Получить ключ записи настройки обмена ЭД по договору
//
// Параметры:
//  Договор -ОпределяемыйТип.ДоговорыКонтрагентов - Договор
//
Функция НастройкаПоДоговоруКонтрагента(Знач Договор) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.НастройкаПоДоговоруКонтрагента(Договор);
	
КонецФункции

// Получить ключ записи настройки обмена ЭД по договору
//
// Параметры:
//  ПараметрыПроцедуры - Структура - данные для формирования регистрационного пакета ЭДО
//
Функция ДанныеДляРегистрационногоПакета1СЭДО(Знач ПараметрыПроцедуры) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.ДанныеДляРегистрационногоПакета1СЭДО(ПараметрыПроцедуры);
	
КонецФункции

// Запуск длительной операции отправки заявления на регистрацию в сервисе 1С:ЭДО.
//
// Параметры:
// ПараметрыОперации - Структура - Данные авторизации на сервере.
//
// Возвращаемое значение:
//  Структура - Длительная операция.
//
Функция НачатьОтправкуЗаявлениеНаРегистрациюВСервис1СЭДО(Знач ПараметрыПроцедуры) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.НачатьОтправкуЗаявлениеНаРегистрациюВСервис1СЭДО(ПараметрыПроцедуры);
	
КонецФункции

// Запуск длительной операции отправки параметров уведомления в сервис 1С:ЭДО
//
// Параметры:
// ПараметрыОперации - Структура - Данные авторизации на сервере и параметры отправки.
//
// Возвращаемое значение:
//  Структура - Длительная операция.
//
Функция НачатьОбновлениеПараметровУведомленийВСервисеЭДО(Знач ПараметрыПроцедуры) Экспорт
	
	ПараметрыПроцедуры.Вставить("ИмяПроцедуры", "ОбменСКонтрагентамиСлужебный.ОбновитьПараметрыУведомленийВСервисе1СЭДО");
	Возврат ОбменСКонтрагентамиСлужебный.НачатьОбновлениеПараметровУведомленийВСервисеЭДО(ПараметрыПроцедуры);
	
КонецФункции

// Запуск длительной операции получения параметров уведомления в сервис 1С:ЭДО
//
// Параметры:
// ПараметрыОперации - Структура - Данные авторизации на сервере и параметры отправки.
//
// Возвращаемое значение:
//  Структура - Длительная операция.
//
Функция НачатьПолучениеПараметровУведомленийВСервисеЭДО(Знач ПараметрыПроцедуры) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.НачатьПолучениеПараметровУведомленийВСервисеЭДО(ПараметрыПроцедуры);
	
КонецФункции

// Запуск длительной операции отправки данных о абоненте в сервис 1С:Сервис настроек
//
// Параметры:
// ПараметрыОперации - Структура - Данные авторизации на сервере и параметры отправки.
//
// Возвращаемое значение:
//  Структура - Длительная операция.
//
Функция НачатьОтправкуДанныхАбонентаВСервисНастроек(Знач ПараметрыПроцедуры) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.НачатьОтправкуДанныхАбонентаВСервисНастроек(ПараметрыПроцедуры);
	
КонецФункции

#КонецОбласти

#Область РаботаСПриглашениями

Функция ПолучитьШаблонПоложенияПЭП(Параметры) Экспорт
	Возврат ОбменСКонтрагентамиСлужебный.ПодготовитьШаблонПоложенияПЭП(Параметры);
КонецФункции

Функция ПолучитьШаблонЛистаОзнакомленияПЭП(Организация, СписокПользователей) Экспорт
	Возврат ОбменСКонтрагентамиСлужебный.ПодготовитьШаблонЛистаОзнакомленияПЭП(Организация, СписокПользователей);
КонецФункции

Функция ЗапуститьПодготовкуШаблонаСоглашенияНаРоуминг(Знач ИдентификаторЭДО) Экспорт
	
	ВидОперации = НСтр("ru = 'Получение шаблона соглашения на роуминг.'");
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
	ПараметрыЗадания.Вставить("ВидОперации", ВидОперации);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтрШаблон(НСтр("ru = 'Обмен с контрагентами. %1'"), ВидОперации);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ОбменСКонтрагентамиСлужебный.ПодготовитьШаблонСоглашенияНаРоумингВФоне",
		ПараметрыЗадания, ПараметрыВыполнения);
	
КонецФункции

Функция ЗаписатьДанныеСоглашенияНаРоуминг(Знач ИдентификаторОрганизации, Знач Адрес, Знач ИмяФайла) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.ЗаписатьДанныеСоглашенияНаРоуминг(ИдентификаторОрганизации, Адрес, ИмяФайла);
	
КонецФункции

Функция ДоступныеРасширенияСканаЗаявленияНаРоуминг(Знач ИдентификаторОрганизации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОператорыЭДО.РасширениеФайлаСоглашенияНаРоуминг КАК РасширениеФайлаСоглашенияНаРоуминг
		|ИЗ
		|	РегистрСведений.ОператорыЭДО КАК ОператорыЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|		ПО ОператорыЭДО.ИдентификаторОператора = УчетныеЗаписиЭДО.ОператорЭДО
		|			И (УчетныеЗаписиЭДО.ИдентификаторЭДО = &ИдентификаторЭДО)";
	
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторОрганизации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.РасширениеФайлаСоглашенияНаРоуминг;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Запуск длительной операции для проверки приглашений 1С:ЭДО.
//
// Параметры:
// ПараметрыОперации        - Соответствие - данные учетной записи и сертификата.
//    *Ключ     - Строка    - идентификатор учетной записи ЭДО.
//    *Значение - Структура - содержит данные сертификата.
// УникальныйИдентификатор  - Строка - идентификатор формы.
//
// Возвращаемое значение:
//  Структура - длительная операция.
//
Функция НачатьОбновлениеИнформацииПоПриглашениямВ1СЭДО(ПараметрыОперации, УникальныйИдентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление информации о приглашениях в сервисе 1С:ЭДО'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
				"ОбменСКонтрагентамиСлужебный.ПолучитьПриглашенияИзСервисаЭДО", ПараметрыОперации, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти

#Область РаботаССервисомНастроек

Процедура ОбновитьКешиОператоровЭДОИФорматов() Экспорт
	
	ОбменСКонтрагентамиСлужебный.ОбновитьКешиОператоровЭДОИФорматов();
	
КонецПроцедуры

#КонецОбласти

#Область Интеграция1СЭДОИ1СОтчетности

// Возвращает помещенную в строку операцию ЭДО.
//
// Параметры:
//  ОперацияЭДО - ФиксированнаяСтруктура - операция ЭДО. См. ОбменСКонтрагентамиСлужебныйКлиентСервер.НоваяОперацияЭДО.
//
// Возвращаемое значение:
//  Строка - помещенная в строку операция ЭДО.
//
Функция ОперацияЭДОВСтроку(Знач ОперацияЭДО) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.ОперацияЭДОВСтроку(ОперацияЭДО);
	
КонецФункции

// Возвращает извлеченную из строки операцию ЭДО.
//
// Параметры:
//  СтрокаОперацииЭДО - Строка - строка операции ЭДО. См. ОбменСКонтрагентамиСлужебный.ОперацияЭДОВСтроку.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура - извлеченная из строки операция ЭДО. См. ОбменСКонтрагентамиСлужебныйКлиентСервер.НоваяОперацияЭДО.
//
Функция ОперацияЭДОИзСтроки(Знач СтрокаОперацииЭДО) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.ОперацияЭДОИзСтроки(СтрокаОперацииЭДО);
	
КонецФункции

Функция СсылкаНаСертификатКриптографии(Знач Сертификат) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.СсылкаНаСертификатКриптографии(Сертификат);
	
КонецФункции

#КонецОбласти

#Область РаботаСУчетнымиЗаписямиЭДО

Функция СоздатьУчетнуюЗаписьЭДО(Знач Параметры) Экспорт
	
	ДанныеСохранены = Ложь;
	
	НачатьТранзакцию();
	Попытка
		
		НовыйПрофильНастроек = ОбменСКонтрагентамиСлужебныйКлиентСервер.НовыйДанныеЗаполненияУчетнойЗаписиЭДО();
		
		НовыйПрофильНастроек.НаименованиеУчетнойЗаписи = Параметры.НаименованиеУчетнойЗаписи;
		НовыйПрофильНастроек.НазначениеУчетнойЗаписи = Параметры.НазначениеУчетнойЗаписи;
		НовыйПрофильНастроек.ПодробноеОписаниеУчетнойЗаписи = Параметры.ПодробноеОписаниеУчетнойЗаписи;
		НовыйПрофильНастроек.Организация = Параметры.Организация;
		НовыйПрофильНастроек.АдресОрганизации = Параметры.АдресОрганизации;
		НовыйПрофильНастроек.СпособОбменаЭД = Параметры.СпособОбменаЭД;
		НовыйПрофильНастроек.ИдентификаторЭДО = Параметры.ИдентификаторЭДО;
		
		// Настройки сертификатов
		РегистрыСведений.СертификатыУчетныхЗаписейЭДО.ДобавитьСертификат(НовыйПрофильНастроек.ИдентификаторЭДО, Параметры.Сертификат);
		
		НовыйПрофильНастроек.ЭлектроннаяПочтаДляУведомлений = Параметры.ЭлектроннаяПочтаДляУведомлений;
		НовыйПрофильНастроек.КодНалоговогоОргана = Параметры.КодНалоговогоОргана;
		НовыйПрофильНастроек.ОператорЭДО = Параметры.ОператорЭДО;
		НовыйПрофильНастроек.ДатаПолученияЭД = НачалоДня(ТекущаяДатаСеанса());
		НовыйПрофильНастроек.УведомлятьОНовыхПриглашениях = Параметры.УведомлятьОНовыхПриглашениях;
		НовыйПрофильНастроек.УведомлятьОбОтветахНаПриглашения = Параметры.УведомлятьОбОтветахНаПриглашения;
		НовыйПрофильНастроек.УведомлятьОНовыхДокументах = Параметры.УведомлятьОНовыхДокументах;
		НовыйПрофильНастроек.УведомлятьОНеОбработанныхДокументах = Параметры.УведомлятьОНеОбработанныхДокументах;
		НовыйПрофильНастроек.УведомлятьОбОкончанииСрокаДействияСертификата = Параметры.УведомлятьОбОкончанииСрокаДействияСертификата;
		НовыйПрофильНастроек.ПринятыУсловияИспользования = Параметры.ПринятыУсловияИспользования;
		
		Отказ = Ложь;
		РегистрыСведений.УчетныеЗаписиЭДО.НоваяУчетнаяЗаписьЭДО(НовыйПрофильНастроек, Отказ);
		
		Если Отказ Тогда
			ОтменитьТранзакцию();
			ДанныеСохранены = Ложь;
			Возврат ДанныеСохранены;
		Иначе
			ДанныеСохранены = Истина;
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Создание учетной записи ЭДО'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), ТекстОшибки);
		ДанныеСохранены = Ложь;
		
		Возврат ДанныеСохранены;
		
	КонецПопытки;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат ДанныеСохранены;
	
КонецФункции

Функция УчетныеЗаписиЭДОДляОбновленияСертификата(Знач Организация, Знач Сертификат, Знач НовыйСертификат) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	УчетныеЗаписиЭДО.Организация КАК Организация,
	|	УчетныеЗаписиЭДО.НаименованиеУчетнойЗаписи КАК НаименованиеУчетнойЗаписи,
	|	УчетныеЗаписиЭДО.ВерсияКонфигурации КАК ВерсияКонфигурации,
	|	УчетныеЗаписиЭДО.ОператорЭДО КАК ОператорЭДО,
	|	УчетныеЗаписиЭДО.СпособОбменаЭД КАК СпособОбменаЭД,
	|	УчетныеЗаписиЭДО.ЭлектроннаяПочтаДляУведомлений КАК ЭлектроннаяПочтаДляУведомлений,
	|	УчетныеЗаписиЭДО.ОжидатьИзвещениеОПолучении КАК ОжидатьИзвещениеОПолучении,
	|	УчетныеЗаписиЭДО.УведомлятьОНовыхПриглашениях КАК УведомлятьОНовыхПриглашениях,
	|	УчетныеЗаписиЭДО.УведомлятьОбОтветахНаПриглашения КАК УведомлятьОбОтветахНаПриглашения,
	|	УчетныеЗаписиЭДО.УведомлятьОНовыхДокументах КАК УведомлятьОНовыхДокументах,
	|	УчетныеЗаписиЭДО.УведомлятьОНеОбработанныхДокументах КАК УведомлятьОНеОбработанныхДокументах,
	|	УчетныеЗаписиЭДО.УведомлятьОбОкончанииСрокаДействияСертификата КАК УведомлятьОбОкончанииСрокаДействияСертификата,
	|	УчетныеЗаписиЭДО.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	УчетныеЗаписиЭДО.АдресОрганизации КАК АдресОрганизации,
	|	УчетныеЗаписиЭДО.НазначениеУчетнойЗаписи КАК НазначениеУчетнойЗаписи,
	|	УчетныеЗаписиЭДО.ПодробноеОписаниеУчетнойЗаписи КАК ПодробноеОписаниеУчетнойЗаписи,
	|	УчетныеЗаписиЭДО.ОбновитьНастройкиУведомлений КАК ОбновитьНастройкиУведомлений,
	|	УчетныеЗаписиЭДО.ПринятыУсловияИспользования КАК ПринятыУсловияИспользования
	|ПОМЕСТИТЬ втУчетныеЗаписиЭДО
	|ИЗ
	|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК Сертификаты
	|		ПО УчетныеЗаписиЭДО.ИдентификаторЭДО = Сертификаты.ИдентификаторЭДО
	|			И (Сертификаты.Сертификат = &Сертификат)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыНовый
	|		ПО УчетныеЗаписиЭДО.ИдентификаторЭДО = СертификатыНовый.ИдентификаторЭДО
	|			И (СертификатыНовый.Сертификат = &НовыйСертификат)
	|ГДЕ
	|	СертификатыНовый.Сертификат ЕСТЬ NULL
	|	И УчетныеЗаписиЭДО.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втУчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	втУчетныеЗаписиЭДО.Организация КАК Организация,
	|	втУчетныеЗаписиЭДО.НаименованиеУчетнойЗаписи КАК НаименованиеУчетнойЗаписи,
	|	втУчетныеЗаписиЭДО.ВерсияКонфигурации КАК ВерсияКонфигурации,
	|	втУчетныеЗаписиЭДО.ОператорЭДО КАК ОператорЭДО,
	|	втУчетныеЗаписиЭДО.СпособОбменаЭД КАК СпособОбменаЭД,
	|	втУчетныеЗаписиЭДО.ЭлектроннаяПочтаДляУведомлений КАК ЭлектроннаяПочтаДляУведомлений,
	|	втУчетныеЗаписиЭДО.ОжидатьИзвещениеОПолучении КАК ОжидатьИзвещениеОПолучении,
	|	втУчетныеЗаписиЭДО.УведомлятьОНовыхПриглашениях КАК УведомлятьОНовыхПриглашениях,
	|	втУчетныеЗаписиЭДО.УведомлятьОбОтветахНаПриглашения КАК УведомлятьОбОтветахНаПриглашения,
	|	втУчетныеЗаписиЭДО.УведомлятьОНовыхДокументах КАК УведомлятьОНовыхДокументах,
	|	втУчетныеЗаписиЭДО.УведомлятьОНеОбработанныхДокументах КАК УведомлятьОНеОбработанныхДокументах,
	|	втУчетныеЗаписиЭДО.УведомлятьОбОкончанииСрокаДействияСертификата КАК УведомлятьОбОкончанииСрокаДействияСертификата,
	|	втУчетныеЗаписиЭДО.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	втУчетныеЗаписиЭДО.АдресОрганизации КАК АдресОрганизации,
	|	втУчетныеЗаписиЭДО.НазначениеУчетнойЗаписи КАК НазначениеУчетнойЗаписи,
	|	втУчетныеЗаписиЭДО.ПодробноеОписаниеУчетнойЗаписи КАК ПодробноеОписаниеУчетнойЗаписи,
	|	втУчетныеЗаписиЭДО.ОбновитьНастройкиУведомлений КАК ОбновитьНастройкиУведомлений,
	|	втУчетныеЗаписиЭДО.ПринятыУсловияИспользования КАК ПринятыУсловияИспользования
	|ИЗ
	|	втУчетныеЗаписиЭДО КАК втУчетныеЗаписиЭДО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
	|ИЗ
	|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУчетныеЗаписиЭДО КАК втУчетныеЗаписиЭДО
	|		ПО СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО = втУчетныеЗаписиЭДО.ИдентификаторЭДО";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Сертификат", Сертификат);
	Запрос.УстановитьПараметр("НовыйСертификат", НовыйСертификат);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	УчетныеЗаписи = Результаты[1].Выгрузить();
	УчетныеЗаписи.Колонки.Добавить("ВсеСертификаты");
	
	Сертификаты = Результаты[2].Выгрузить();
	Сертификаты.Индексы.Добавить("ИдентификаторЭДО");
	
	Для каждого УчетнаяЗапись Из УчетныеЗаписи Цикл
		
		ОтборПоУчетнойЗаписи = Новый Структура("ИдентификаторЭДО", УчетнаяЗапись.ИдентификаторЭДО);
		НайденныеСтроки = Сертификаты.НайтиСтроки(ОтборПоУчетнойЗаписи);
		
		УчетнаяЗапись.ВсеСертификаты = Новый Массив;
		УчетнаяЗапись.ВсеСертификаты.Добавить(НовыйСертификат);
		Для каждого СтрокаСертификата Из НайденныеСтроки Цикл
			УчетнаяЗапись.ВсеСертификаты.Добавить(СтрокаСертификата.Сертификат);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(УчетныеЗаписи);
	
КонецФункции

Процедура ДобавитьСертификатВУчетныеЗаписиЭДО(Знач НаборИдентификаторовЭДО, Знач Сертификат) Экспорт
	
	Для каждого ИдентификаторЭДО Из НаборИдентификаторовЭДО Цикл
		
		РегистрыСведений.СертификатыУчетныхЗаписейЭДО.ДобавитьСертификат(ИдентификаторЭДО, Сертификат, Истина);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Шифрование

// Только для внутреннего использования
Функция ЗашифрованныйМаркер(СтруктураПараметровЗапросаМаркера, ДополнительныеПараметры) Экспорт
	
	ПараметрыОбработки = Новый Структура("Расшифровывать, ПоказыватьОшибки, ПоказыватьОшибкуАутентификацииПриОтсутствииДанных",
		Ложь, ДополнительныеПараметры.ПоказыватьОшибки, ДополнительныеПараметры.ПоказыватьОшибкуАутентификацииПриОтсутствииДанных);
		
	Возврат ОбменСКонтрагентамиВнутренний.ПолучитьМаркерОЭДО(СтруктураПараметровЗапросаМаркера,
		ДополнительныеПараметры.ИнтернетПоддержкаПользователейПодключена, ПараметрыОбработки, ДополнительныеПараметры.КонтекстОперации);
	
КонецФункции

#КонецОбласти

#Область Оповещения

// Проверяет использование оповещений о новых документах в сервисе ЭДО
// 
// Возвращаемое значение:
//  Булево - если истина, то оповещения используются.
//
Функция ИспользоватьОповещенияОтСервисаЭДО() Экспорт
	
	Использовать = Ложь;
	ОбменСКонтрагентамиПереопределяемый.ПриПроверкеИспользованияУведомлений(Использовать);
	Возврат Использовать
	
КонецФункции

// Создает служебного пользователя для получения оповещений от сервиса ЭДО
//
Процедура СоздатьСлужебногоПользователяОповещенийЭДО() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Если ОбщегоНазначения.РазделениеВключено()
		И НЕ ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат
	КонецЕсли;
	
	Логин = ОбменСКонтрагентамиСлужебный.ИмяСлужебногоПользователяОповещенийЭДО();
	Пароль = "946135e8-a9a7-405f-a5df-19e734c712c7";
	
	ПользовательИзСправочника = Пользователи.НайтиПоИмени(Логин);
	
	Если ПользовательИзСправочника <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначения.РазделениеВключено()
		И НЕ ПользователиИнформационнойБазы.ПолучитьПользователей().Количество() Тогда
		Возврат
	КонецЕсли;
	
	Попытка
		
		ОписаниеПользователяИБ = Пользователи.НовоеОписаниеПользователяИБ();
		ОписаниеПользователяИБ.Имя = Логин;
		ОписаниеПользователяИБ.ПолноеИмя = НСтр("ru='Бот 1С-ЭДО'");
		ОписаниеПользователяИБ.АутентификацияСтандартная = Истина;
		ОписаниеПользователяИБ.ПоказыватьВСпискеВыбора = Ложь;
		ОписаниеПользователяИБ.Вставить("Действие", "Записать");
		ОписаниеПользователяИБ.Вставить("ВходВПрограммуРазрешен", Истина);
		ОписаниеПользователяИБ.ЗапрещеноИзменятьПароль = Истина;
		ОписаниеПользователяИБ.Пароль = Пароль;
		ОписаниеПользователяИБ.Роли = Новый Массив;
		ОписаниеПользователяИБ.Роли.Добавить(Метаданные.Роли.ИспользованиеУведомленийЭДО.Имя);
		
		НовыйПользователь = Справочники.Пользователи.СоздатьЭлемент();
		НовыйПользователь.Наименование = ОписаниеПользователяИБ.ПолноеИмя;
		НовыйПользователь.Служебный = Истина;
		НовыйПользователь.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", ОписаниеПользователяИБ);
		НовыйПользователь.Записать();
		
	Исключение
		
		МетаданныеОбъекта = Метаданные.Справочники.Пользователи;
		ТекстОшибки = НСтр("ru = 'Не удалось создать служебного пользователя по причине:'")
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			МетаданныеОбъекта,, ТекстОшибки);
		ТекстСообщения = НСтр("ru = 'При создании служебного пользователя для получения уведомлений от сервиса ЭДО произошла ошибка: %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

Функция СуществуютЭДНаОсновании(ДокументОснование) Экспорт
	Возврат ОбменСКонтрагентамиСлужебный.СуществуютЭДНаОсновании(ДокументОснование);
КонецФункции

Функция ПолучитьКомандуПечатиОбъекта(Объект, Идентификатор) Экспорт
	
	КомандыПечати = ОбменСКонтрагентамиСлужебный.КомандыПечатиДоступныеВнутреннемуЭДО(Объект.Метаданные()); 
	Команда = КомандыПечати.Найти(Идентификатор, "Идентификатор");
	
	Если ЗначениеЗаполнено(Команда) Тогда
		Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Команда);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Процедура УстановитьПризнакИспользованияВнутреннегоЭДО(Использовать) Экспорт
	
	ОбменСКонтрагентамиСлужебный.УстановитьПризнакИспользованияВнутреннегоЭДО(Использовать);
	
КонецПроцедуры

Функция ЕстьНастройкиВнутреннегоЭДО() Экспорт
		
	Возврат ОбменСКонтрагентамиСлужебный.ЕстьНастройкиВнутреннегоЭДО();

КонецФункции

Функция ДоступныеКомандыПечатиОбъекта(Объект) Экспорт
	
	Команды = ОбменСКонтрагентамиСлужебный.КомандыПечатиДоступныеВнутреннемуЭДО(Объект.Метаданные());
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Команды);
	
КонецФункции

Функция ПечатныеФормыДокументов(КомандаПечатиВФайл, ОбъектУчета, ТипФайлаТабличногоДокумента) Экспорт
	
	МассивОбъектовУчета = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектУчета);
	
	НастройкиСохранения = УправлениеПечатью.НастройкиСохранения();
	НастройкиСохранения.ФорматыСохранения.Добавить(ТипФайлаТабличногоДокумента);
	
	РезультатПечати = УправлениеПечатью.НапечататьВФайл(КомандаПечатиВФайл, МассивОбъектовУчета, НастройкиСохранения);
	Если Не ЗначениеЗаполнено(РезультатПечати) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(РезультатПечати);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСВерсиямиЭД

Процедура УдалитьСтаруюВерсиюЭД(ЗаписьНабора)
	
	СтруктураВерсии =  ОбменСКонтрагентамиСлужебный.ПараметрыВерсииЭлектронногоДокумента(ЗаписьНабора.СсылкаНаОбъект);
	
	Если ЗначениеЗаполнено(СтруктураВерсии.СсылкаНаДокумент)
		И ОбменСКонтрагентамиСлужебный.ВозможноУдалениеЭД(СтруктураВерсии.СостояниеВерсииЭД) Тогда
		
		НачатьТранзакцию();
		Попытка
			ДокументОбъект = ЭлектронноеВзаимодействиеСлужебный.ОбъектПоСсылкеДляИзменения(СтруктураВерсии.СсылкаНаДокумент);
			ДокументОбъект.ПометкаУдаления = Истина;
			ДокументОбъект.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
		
		// Удалим подчиненные электронные доп. файлы.
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭДПрисоединенныеФайлы.Ссылка
		|ИЗ
		|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
		|ГДЕ
		|	ЭДПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
		|	И НЕ ЭДПрисоединенныеФайлы.ПометкаУдаления";
		Запрос.УстановитьПараметр("ВладелецФайла", СтруктураВерсии.СсылкаНаДокумент);
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		Пока Выборка.Следующий() Цикл
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.ПометкаУдаления = Истина;
			ДокументОбъект.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция СостояниеВерсииЭД(СсылкаНаВладельца)
	
	ВозвращаемоеЗначение = Перечисления.СостоянияВерсийЭД.ПустаяСсылка();
	СтруктураВерсииЭД = ОбменСКонтрагентамиСлужебный.ДанныеСостоянияЭД(СсылкаНаВладельца);
	
	Если СтруктураВерсииЭД.Свойство("СостояниеВерсииЭД") Тогда
		
		Комментарий = Неопределено;
		ДобавитьПричинуЗакрытия = (СтруктураВерсииЭД.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно);
		
		Если СтруктураВерсииЭД.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.ОбменЗавершен Тогда
			СостояниеВерсии = НСтр("ru='ЭДО завершен'");
		ИначеЕсли СтруктураВерсииЭД.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением Тогда
			СостояниеВерсии = НСтр("ru='ЭДО завершен с исправлением'");
		ИначеЕсли СтруктураВерсииЭД.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.Аннулирован Тогда
			СостояниеВерсии = НСтр("ru='ЭДО аннулирован'");
		ИначеЕсли СтруктураВерсииЭД.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно Тогда
			СостояниеВерсии = НСтр("ru='ЭДО закрыт принудительно'");
		ИначеЕсли СтруктураВерсииЭД.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.Отклонен Тогда
			СостояниеВерсии = НСтр("ru='ЭДО закрыт с отклонением'");
		ИначеЕсли СтруктураВерсииЭД.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.НеСформирован Тогда
			СостояниеВерсии = НСтр("ru='ЭДО не начат'");
		ИначеЕсли НЕ СтруктураВерсииЭД.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.НеПолучен Тогда 
			СостояниеВерсии = Строка(СтруктураВерсииЭД.СостояниеВерсииЭД);
		КонецЕсли;
		
		ТипВладельца = ТипЗнч(СсылкаНаВладельца);
		Если ТипВладельца <> Тип("ДокументСсылка.ЭлектронныйДокументВходящий")
			И ТипВладельца <> Тип("ДокументСсылка.ЭлектронныйДокументИсходящий") Тогда 
			
			ПараметрыЭД = ЗаполнитьПараметрыЭДПоИсточнику(СсылкаНаВладельца);
			
			Если ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Внутренний Тогда
				СостояниеВерсии = СтруктураВерсииЭД.ПредставлениеСостояния;
			КонецЕсли;
			
		КонецЕсли; 
		
		Если ДобавитьПричинуЗакрытия Тогда
			СтруктураВерсииЭД.Свойство("КомментарийРС", Комментарий);
			Причина = СтрЗаменить(НСтр("ru = 'причина: %1'"), "%1", ?(ЗначениеЗаполнено(Комментарий), Комментарий, НСтр("ru ='не указана'")));
			ВозвращаемоеЗначение = СостояниеВерсии + "," + ?(ОбщегоНазначения.ЭтоМобильныйКлиент(), Символы.ПС, " ") + Причина;
		Иначе
			ВозвращаемоеЗначение = СостояниеВерсии;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПолучитьТекстСводногоСостоянияЭД(СсылкаНаВладельца)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстСводногоСостояния = "";
	
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(СсылкаНаВладельца);
	ТаблицаДанных = ОбменСКонтрагентамиСлужебный.ПолучитьДанныеЭДПоВладельцам(МассивСсылок);

	Если ТаблицаДанных.Количество() > 0 Тогда
		Строка = ТаблицаДанных[0];
		
		ТекстСНашейСтороны = "";
		ТекстСоСтороныДругогоУчастника = "";
		
		Если Строка.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.ЗакрытПринудительно Тогда
			ТекстСводногоСостояния = НСтр("ru = 'ЭДО закрыт принудительно'");
		Иначе
			Если ЗначениеЗаполнено(Строка.ДействияСНашейСтороны)
				И Строка.ДействияСНашейСтороны = Перечисления.СводныеСостоянияЭД.ТребуютсяДействия Тогда
				
				ТекстСНашейСтороны = НСтр("ru = 'с нашей стороны'");
			КонецЕсли;
			Если ЗначениеЗаполнено(Строка.ДействияСоСтороныДругогоУчастника)
				И Строка.ДействияСоСтороныДругогоУчастника = Перечисления.СводныеСостоянияЭД.ТребуютсяДействия Тогда
				
				ТекстСоСтороныДругогоУчастника = НСтр("ru = 'со стороны других участников'");
			КонецЕсли;
			Если ЗначениеЗаполнено(ТекстСНашейСтороны) 
				Или ЗначениеЗаполнено(ТекстСоСтороныДругогоУчастника) Тогда
				
				ТекстСводногоСостояния = НСтр("ru = 'Требуются действия'")+ " " + ТекстСНашейСтороны
					+ ?(ЗначениеЗаполнено(ТекстСНашейСтороны) И ЗначениеЗаполнено(ТекстСоСтороныДругогоУчастника), " и ", "")
					+ ТекстСоСтороныДругогоУчастника;
			ИначеЕсли ЗначениеЗаполнено(Строка.ДействияСНашейСтороны)
				И Строка.ДействияСНашейСтороны = Перечисления.СводныеСостоянияЭД.ВсеВыполнено
				И ЗначениеЗаполнено(Строка.ДействияСоСтороныДругогоУчастника)
				И Строка.ДействияСоСтороныДругогоУчастника = Перечисления.СводныеСостоянияЭД.ВсеВыполнено Тогда
				
				Если Строка.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.ОбменЗавершен Тогда
					ТекстСводногоСостояния = НСтр("ru = 'ЭДО завершен'");
				ИначеЕсли Строка.СостояниеВерсииЭД = Перечисления.СостоянияВерсийЭД.ОбменЗавершенСИсправлением Тогда
					ТекстСводногоСостояния = НСтр("ru = 'ЭДО завершен с исправлением'");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстСводногоСостояния;
	
КонецФункции

#КонецОбласти

#Область ОбработкаЭД

Функция ПолучитьСсылкиНаЭДДляПОА(Знач ПервичныйЭД) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЭДПрисоединенныеФайлы.Ссылка КАК ПОА,
	|	ЭДПрисоединенныеФайлы.ВладелецФайла.Организация КАК Организация,
	|	ЭДВладелецЭД.ВладелецФайла КАК ВладелецФайла,
	|	ВЫБОР
	|		КОГДА ЭДПрисоединенныеФайлы.НаправлениеЭД = значение(Перечисление.НаправленияЭД.Входящий)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭДПрисоединенныеФайлы КАК ЭДВладелецЭД
	|		ПО ЭДПрисоединенныеФайлы.ЭлектронныйДокументВладелец = ЭДВладелецЭД.Ссылка
	|ГДЕ
	|	ЭДПрисоединенныеФайлы.ТипЭлементаВерсииЭД = ЗНАЧЕНИЕ(Перечисление.ТипыЭлементовВерсииЭД.ПОА)
	|	И ЭДВладелецЭД.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Запрос.УстановитьПараметр("Ссылка", ПервичныйЭД);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("ПОА", Выборка.ПОА);
		СтруктураВозврата.Вставить("Организация", Выборка.Организация);
		СтруктураВозврата.Вставить("ВладелецФайла", Выборка.ВладелецФайла);
	Иначе
		СтруктураВозврата = Неопределено;
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Используется для получения представления ЭД с клиента.
//
// Параметры:
//  СсылкаНаЭД - СправочникСсылка.ЭДПрисоединенныеФайлы - ЭД для которого надо получить представление.
//
// Возвращаемое значение:
//  Строка - представление электронного документа.
//
Функция ПредставлениеЭД(СсылкаНаЭД) Экспорт
	
	Возврат ОбменСКонтрагентамиСлужебный.ПредставлениеЭД(СсылкаНаЭД);
	
КонецФункции

// Используется для получения списка представлений ЭД с клиента.
//
// Параметры:
//  МассивЭД - Массив - СправочникСсылка.ЭДПрисоединенныеФайлы, для которых надо сформировать список представлений.
//
// Возвращаемое значение:
//  СписокЗначений:
//     Значение - СправочникСсылка.ЭДПрисоединенныеФайлы.
//     Представление - Строка - представление электронного документа.
//
Функция СписокПредставленийЭД(МассивЭД) Экспорт
	
	СписокПредставлений = Новый СписокЗначений;
	Для Каждого ЭД Из МассивЭД Цикл
		Представление = ОбменСКонтрагентамиСлужебный.ПолучитьПредставлениеЭД(ЭД);
		СписокПредставлений.Добавить(ЭД, Представление);
	КонецЦикла;
	
	Возврат СписокПредставлений;
	
КонецФункции

// Создает присоединенные файлы пакетов, зашифрованных на клиенте.
//
// Параметры:
//  СоответствиеДанных - соответствие - содержит данные по пакетам и зашифрованным файлам.
//  СоответствиеПаролейДляЭДО - Соответствие - данные о паролях.
//
Процедура СохранитьИОтправитьЗашифрованныеДанные(СоответствиеДанных, СоотвСоглашенийИСтруктурСертификатов, КолОтправленных) Экспорт
	
	МассивПЭД = Новый Массив;
	Для Каждого ЭлементПЭД Из СоответствиеДанных Цикл
		ПакетЭД = ЭлементПЭД.Ключ;
		Если ПакетЭД.ВерсияФорматаПакета <> Перечисления.ВерсииФорматаПакетаЭД.Версия10 Тогда
			
			ОбменСКонтрагентамиВнутренний.СформироватьЭДПрисоединенныйФайлПакетаОператораЭДО(
								ПакетЭД,
								ЭлементПЭД.Значение[0].ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
		Иначе
			ОбменСКонтрагентамиСлужебный.СформироватьЭДПрисоединенныйФайлПакета(ПакетЭД, ЭлементПЭД.Значение);
		КонецЕсли;
		МассивПЭД.Добавить(ПакетЭД);
	КонецЦикла;
		
	Если СоответствиеДанных.Количество() > 0 И ОбменСКонтрагентамиСлужебный.НемедленнаяОтправкаЭД() Тогда
		РезультатОтправки = ОтправкаПакетовЭД(МассивПЭД, СоотвСоглашенийИСтруктурСертификатов);
		КолОтправленных = РезультатОтправки.КоличествоОтправленных;
	КонецЕсли;
	
КонецПроцедуры

// Предназначен для возврата на клиента двоичных данных ЭД, установленных подписей и сертификатов для дальнейшей
// проверки валидности подписей на клиенте.
//
// Параметры:
//  ЭД - СправочникСсылка.ЭДПрисоединенныеФайлы, ссылка на электронный документ.
//
// Возвращаемое значение:
//  Структура или неопределено - данные электронного документа, Неопределено - если нет подписей.
//
Функция СтруктураСодержимогоЭД(ЭлектронныйДокумент) Экспорт
	
	СтруктураВозврата = Новый Структура;
	
	СтруктураВозврата.Вставить("ВладелецФайла", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлектронныйДокумент, "ВладелецФайла"));
	СтруктураВозврата.Вставить("ДанныеЭД",      РаботаСФайлами.ДвоичныеДанныеФайла(ЭлектронныйДокумент));
	СтруктураВозврата.Вставить("Подписи",       ЭлектроннаяПодпись.УстановленныеПодписи(ЭлектронныйДокумент));
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ПолучитьДистрибутивCryptoProCSP(Параметры) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение дистрибутива CryptoPro CSP'");
	ПараметрыВыполнения.ДополнительныйРезультат = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ОбменСКонтрагентамиСлужебный.ПолучитьДистрибутивCryptoProCSP", Параметры, ПараметрыВыполнения);
	
КонецФункции

Функция ПолучитьДистрибутивViPNetCSP(Параметры) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение дистрибутива VipNet CSP'");
	ПараметрыВыполнения.ДополнительныйРезультат = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("ОбменСКонтрагентамиСлужебный.ПолучитьДистрибутивViPNetCSP", Параметры, ПараметрыВыполнения);
	
КонецФункции

Функция РезультатПолученияДистрибутиваКриптопровайдера(ДлительнаяОперация, ИдентификаторФормы) Экспорт
	
	Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
	
	ДанныеДистрибутива = Новый Структура;
	Если Результат.ПараметрыДистрибутива <> Неопределено И Результат.ПараметрыДистрибутива.Свойство("Дистрибутив") Тогда
		ДанныеДистрибутива.Вставить("НомерДистрибутива", "");
		ДанныеДистрибутива.Вставить("Версия", Результат.ПараметрыДистрибутива.Версия);
		ДанныеДистрибутива.Вставить("КонтрольнаяСумма", Результат.ПараметрыДистрибутива.КонтрольнаяСумма);
		
		Если Результат.ПараметрыДистрибутива.Свойство("СерийныйНомер") Тогда
			ДанныеДистрибутива.Вставить("СерийныйНомер", Результат.ПараметрыДистрибутива.СерийныйНомер);
		КонецЕсли;
		
		ОписаниеФайлов = Новый Массив;
		Для каждого Файл Из Результат.ПараметрыДистрибутива.Дистрибутив Цикл
			ОписаниеФайлов.Добавить(Новый ОписаниеПередаваемогоФайла(Файл.Имя,
			ПоместитьВоВременноеХранилище(Файл.ДвоичныеДанные, ИдентификаторФормы)));
		КонецЦикла;
		
		ДанныеДистрибутива.Вставить("Дистрибутив", ОписаниеФайлов);
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ДанныеДистрибутива", ДанныеДистрибутива);
	СтруктураВозврата.Вставить("КонтекстОперации", Результат.КонтекстОперации);
	
	Возврат СтруктураВозврата;
	
КонецФункции

Процедура ЗаполнитьДанныеЗаявки(Знач ДополнительныеПараметры, ДанныеЗаявки) Экспорт
	
	СертификатКриптографии = ДополнительныеПараметры.Сертификат;
	АдресОрганизации       = ДополнительныеПараметры.АдресОрганизации;
	
	ПараметрыСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(СертификатКриптографии);
	Если ДополнительныеПараметры.Свойство("АдресОрганизацииЗначение") 
		И ЗначениеЗаполнено(ДополнительныеПараметры.АдресОрганизацииЗначение) Тогда
		СтруктураАдреса = ОбменСКонтрагентамиСлужебный.СведенияОбАдресеПоЗначению(ДополнительныеПараметры.АдресОрганизацииЗначение);
	Иначе
		СтруктураАдреса = ОбменСКонтрагентамиСлужебный.СведенияОбАдресеПоПредставлению(ДополнительныеПараметры.АдресОрганизации);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтруктураАдреса.Город) Тогда
		СтруктураАдреса.Город = СтруктураАдреса.Регион;
	КонецЕсли;
	
	ДанныеЗаявки.КодНалоговогоОргана = ДополнительныеПараметры.КодНалоговогоОргана;
	
	ЗаполнитьЗначенияСвойств(ДанныеЗаявки, СтруктураАдреса);
	ЗаполнитьЗначенияСвойств(ДанныеЗаявки, ПараметрыСертификата,"Фамилия, Имя, Отчество");
	
КонецПроцедуры

Функция ЗаполнитьНачальныеНастройкиМаршрутов() Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	МаршрутыПодписания.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.МаршрутыПодписания КАК МаршрутыПодписания
		               |ГДЕ
		               |	(МаршрутыПодписания.СхемаПодписания = ЗНАЧЕНИЕ(Перечисление.СхемыПодписанияЭД.ПустаяСсылка)
		               |			ИЛИ МаршрутыПодписания.ВидПодписи = ЗНАЧЕНИЕ(Перечисление.ВидыЭлектронныхПодписей.ПустаяСсылка))
		               |	И МаршрутыПодписания.Предопределенный";
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Если ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.МаршрутыПодписания.ОднойДоступнойПодписью") = Неопределено Тогда
				МаршрутОбъект = Справочники.МаршрутыПодписания.СоздатьЭлемент();
				МаршрутОбъект.ИмяПредопределенныхДанных = "ОднойДоступнойПодписью";
				МаршрутОбъект.Наименование = НСтр("ru = 'Одной доступной подписью'");
				МаршрутОбъект.СхемаПодписания = Перечисления.СхемыПодписанияЭД.ОднойДоступнойПодписью;
				МаршрутОбъект.ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.УсиленнаяКвалифицированная;
				МаршрутОбъект.Записать();
			КонецЕсли;
			
			Если ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.МаршрутыПодписания.УказыватьПриСоздании") = Неопределено Тогда
				МаршрутОбъект = Справочники.МаршрутыПодписания.СоздатьЭлемент();
				МаршрутОбъект.ИмяПредопределенныхДанных = "УказыватьПриСоздании";
				МаршрутОбъект.Наименование = НСтр("ru = 'Указывать при создании документа'");
				МаршрутОбъект.СхемаПодписания = Перечисления.СхемыПодписанияЭД.УказыватьПриСоздании;
				МаршрутОбъект.ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.УсиленнаяКвалифицированная;
				МаршрутОбъект.Записать();
			КонецЕсли;
		Иначе
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				СхемаПодписания = ?(Выборка.Ссылка = Справочники.МаршрутыПодписания.ОднойДоступнойПодписью,
					Перечисления.СхемыПодписанияЭД.ОднойДоступнойПодписью, Перечисления.СхемыПодписанияЭД.УказыватьПриСоздании);
				
				БлокировкаДанных = Новый БлокировкаДанных;
				ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.МаршрутыПодписания");
				ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", Выборка.Ссылка);
				ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
				БлокировкаДанных.Заблокировать();
				
				ОбъектОбработки = Выборка.Ссылка.ПолучитьОбъект();
				
				ОбъектИзменен = Ложь;
				
				ОбъектОбработки.Заблокировать();
				Если ОбъектОбработки.СхемаПодписания <> СхемаПодписания Тогда
					ОбъектОбработки.СхемаПодписания = СхемаПодписания;
					ОбъектОбработки.КлючАвтоматическойНастройки = "";
					ОбъектИзменен = Истина; 
				КонецЕсли;
				
				Если ОбъектОбработки.ВидПодписи <> Перечисления.ВидыЭлектронныхПодписей.УсиленнаяКвалифицированная Тогда
					ОбъектОбработки.ВидПодписи = Перечисления.ВидыЭлектронныхПодписей.УсиленнаяКвалифицированная;
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				Если ОбъектИзменен Тогда
					ОбъектОбработки.Записать();
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Начальные настройки заполнены'"));
		
	Исключение
		ОтменитьТранзакцию();
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(НСтр("ru = 'Заполнение начальных настроек маршрутов'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;

КонецФункции

Функция ПараметрыПросмотраСтатусовЭлектронныхДокументов(Знач ПараметрыОбъектовУчета) Экспорт
	Возврат ОбменСКонтрагентамиСлужебный.ПараметрыПросмотраСтатусовЭлектронныхДокументов(ПараметрыОбъектовУчета);
КонецФункции

Функция ПараметрыПросмотраАктуальныхДокументов(Знач ОбъектыУчета) Экспорт
	Возврат ОбменСКонтрагентамиСлужебный.ПараметрыПросмотраАктуальныхДокументов(ОбъектыУчета);
КонецФункции

Процедура ОповеститьОбОтклоненииАннулированииЭлектронныхДокументов(ЭлектронныеДокументы, Отклонить) Экспорт
	
	// Не используем длительные операции для безусловного выполнения в фоне.
	Параметры = Новый Массив;
	Параметры.Добавить(ЭлектронныеДокументы);
	Параметры.Добавить(Отклонить);
	ФоновыеЗадания.Выполнить("ОбменСКонтрагентамиСлужебный.ОповеститьОбОтклоненииАннулированииЭлектронныхДокументов", Параметры,,
		НСтр("ru = 'Создание оповещения об отклоненных/аннулированных электронных документах'"));
	
КонецПроцедуры

#КонецОбласти

#Область Сертификаты

// Только для внутреннего использования
Функция СоответствиеДанныхПакетов(МассивПакетовЭД) Экспорт
	
	СоответствиеВозврата = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПакетЭДЭлектронныеДокументы.Ссылка КАК ПакетЭД,
	|	ПакетЭДЭлектронныеДокументы.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
	|	ПакетЭДЭлектронныеДокументы.ЭлектронныйДокумент.НаправлениеЭД КАК НаправлениеЭД
	|ИЗ
	|	Документ.ПакетЭД.ЭлектронныеДокументы КАК ПакетЭДЭлектронныеДокументы
	|ГДЕ
	|	ПакетЭДЭлектронныеДокументы.Ссылка В(&МассивПакетовЭД)
	|ИТОГИ ПО
	|	ПакетЭД";
	
	Запрос.УстановитьПараметр("МассивПакетовЭД", МассивПакетовЭД);
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	ВыборкаПакеты = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПакеты.Следующий() Цикл
		ВыборкаЭД = ВыборкаПакеты.Выбрать();
		МассивЭД = Новый Массив;
		Пока ВыборкаЭД.Следующий() Цикл
			СтруктураДанныхФайла = Новый Структура;
			ДанныеФайла = ОбменСКонтрагентамиСлужебный.ПолучитьДанныеФайла(ВыборкаЭД.ЭлектронныйДокумент);
			СтруктураДанныхФайла.Вставить("ЭлектронныйДокумент", ВыборкаЭД.ЭлектронныйДокумент);
			СтруктураДанныхФайла.Вставить("ДанныеФайла",         ДанныеФайла);
			СтруктураДанныхФайла.Вставить(
					"ЭтоОтправкаПодтверждения",
					ВыборкаЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий);
			ПараметрыШифрования = ОбменСКонтрагентамиСлужебный.АдресаСертификатовШифрования(
																			ВыборкаЭД.ЭлектронныйДокумент);
			СтруктураДанныхФайла.Вставить("ПараметрыШифрования", ПараметрыШифрования);
			МассивЭД.Добавить(СтруктураДанныхФайла);
		КонецЦикла;
		СоответствиеВозврата.Вставить(ВыборкаПакеты.ПакетЭД, МассивЭД);
	КонецЦикла;
	
	Возврат СоответствиеВозврата;
	
КонецФункции

#КонецОбласти

#Область ПодписьЭД

// Сохраняет результаты проверки ЭП, выполненные на клиенте.
//
// Параметры:
//  ЭД - СправочникСсылка.ЭДПрисоединенныеФайлы - ссылка на электронный документ.
//  МассивРезультатов - Массив - содержит данные установленных подписей.
//
Процедура СохранитьРезультатыПроверкиПодписей(ЭД, МассивРезультатов) Экспорт
	
	Если МассивРезультатов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановленныеПодписи = ОбменСКонтрагентамиСлужебный.УстановленныеПодписи(ЭД);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Элемент Из МассивРезультатов Цикл
		
		МенеджерЗаписи = РегистрыСведений.ЭлектронныеПодписи.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ПодписанныйОбъект = ЭД;
		МенеджерЗаписи.ПорядковыйНомер = Элемент.НомерСтроки;
		МенеджерЗаписи.Прочитать();
		МенеджерЗаписи.ПодписанныйОбъект = ЭД;
		МенеджерЗаписи.ПорядковыйНомер = Элемент.НомерСтроки;
		МенеджерЗаписи.ДатаПроверкиПодписи = ТекущаяДатаСеанса();
		МенеджерЗаписи.ПодписьВерна = Элемент.Результат;
		Если ИзменятьУстановившегоПодпись(ЭД, УстановленныеПодписи) Тогда
			МенеджерЗаписи.УстановившийПодпись = Пользователи.АвторизованныйПользователь();
		КонецЕсли;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИзменятьУстановившегоПодпись(ЭлектронныйДокумент, УстановленныеПодписи) 
	
	ЭтоИсходящийЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлектронныйДокумент, "НаправлениеЭД")
		= Перечисления.НаправленияЭД.Исходящий;
	
	Для каждого УстановленнаяПодпись Из УстановленныеПодписи Цикл
		УсловиеСравнения = ?(УстановленнаяПодпись.Свойство("ПодписанныйОбъект"),
			ЭлектронныйДокумент = УстановленнаяПодпись.ПодписанныйОбъект,
			Истина);
		Если УсловиеСравнения Тогда
			ЭтоОтветнаяПодпись = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(УстановленнаяПодпись, "ОтветнаяПодпись", Ложь);
			Если (ЭтоИсходящийЭД И ЭтоОтветнаяПодпись) Или (Не ЭтоИсходящийЭД И Не ЭтоОтветнаяПодпись) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ФормированиеЭД

// Процедура используется для минимизации серверных вызовов, при необходимости
// получения на клиенте всех или нескольких значений, перечисленных в параметрах констант.
//
Процедура ИнициализироватьПеременные(ВыполнятьКриптооперацииНаСервере, НемедленнаяОтправкаЭД = Неопределено) Экспорт
	
	НемедленнаяОтправкаЭД = ОбменСКонтрагентамиСлужебный.НемедленнаяОтправкаЭД();
	ВыполнятьКриптооперацииНаСервере = ЭлектронноеВзаимодействиеСлужебный.ВыполнятьКриптооперацииНаСервере();

КонецПроцедуры

// Только для внутреннего использования
Функция СформироватьСлужебныеЭД(МассивЭД, ВидЭД, ТекстУточнения = "") Экспорт
	
	МассивВозврата = ОбменСКонтрагентамиСлужебный.СформироватьСлужебныеЭД(МассивЭД, ВидЭД, ТекстУточнения);
	
	Возврат МассивВозврата;
	
КонецФункции

Функция ПараметрыОтклоненияЭД(МассивЭД, ФормироватьУОУ = Ложь, КонтекстОперации = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = ОбменСКонтрагентамиСлужебный.ПараметрыОтклоненияЭД(МассивЭД, ФормироватьУОУ, КонтекстОперации);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПараметрыАннулированияЭД(МассивЭД, КонтекстОперации = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = ОбменСКонтрагентамиСлужебный.ПараметрыАннулированияЭД(МассивЭД, КонтекстОперации);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции 

Функция МассивПрофилейПакетовЭД(МассивПЭД) Экспорт 
	
	МассивПрофилейНастроекЭДО = Новый Массив;
	
	СоотвЭДИСтруктурыСоглашений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивПЭД, "Отправитель");
	Для Каждого ЭДВладелец Из СоотвЭДИСтруктурыСоглашений Цикл
		Отправитель = ЭДВладелец.Значение.Отправитель;
		Если МассивПрофилейНастроекЭДО.Найти(Отправитель) = Неопределено Тогда
			МассивПрофилейНастроекЭДО.Добавить(Отправитель);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивПрофилейНастроекЭДО;
	
КонецФункции

// Только для внутреннего использования
Функция РеквизитыУчетнойЗаписи(УчетнаяЗаписьЭДО) Экспорт
	
	Возврат РегистрыСведений.УчетныеЗаписиЭДО.ДанныеУчетнойЗаписиЭДОПоИдентификатору(УчетнаяЗаписьЭДО);
	
КонецФункции

Функция ОтправитьПриглашенияВАрхив(Знач ВыбранныеИдентификаторы) Экспорт
	
	Счетчик = 0;
	
	НачатьТранзакцию();
	Попытка
		
		Для Каждого Идентификаторы Из ВыбранныеИдентификаторы Цикл
			МенеджерЗаписи = РегистрыСведений.ПриглашенияКОбменуЭлектроннымиДокументами.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.ИдентификаторОрганизации = Идентификаторы.ИдентификаторОрганизации;
			МенеджерЗаписи.ИдентификаторКонтрагента = Идентификаторы.ИдентификаторКонтрагента;
			МенеджерЗаписи.Ознакомиться = Истина;
			МенеджерЗаписи.Прочитать();
			
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Ознакомиться = Ложь;
				МенеджерЗаписи.Записать();
				Счетчик = Счетчик + 1;
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Ошибка = ИнформацияОбОшибке();
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Отправка приглашений в архив'"),
			ПодробноеПредставлениеОшибки(Ошибка),
			НСтр("ru = 'При отправке приглашений в архив возникла ошибка.'"));
		
	КонецПопытки;
	
	Возврат Счетчик;
КонецФункции

#КонецОбласти

#КонецОбласти








