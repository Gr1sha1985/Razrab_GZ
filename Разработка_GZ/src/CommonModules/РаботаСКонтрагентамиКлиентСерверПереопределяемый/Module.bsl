///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ПроверкаКонтрагентовВДокументах

// Определение вида документа.
//
// Параметры:
//  Форма								 - ФормаКлиентскогоПриложения - Форма документа, для которого необходимо получить описание.
//	Результат							 - Структура - Описывает вид документа. Ключи:
//  		"КонтрагентНаходитсяВШапке"			 	- Булево - Признак того, есть у документа контрагент в шапке
//  		"КонтрагентНаходитсяВТабличнойЧасти"	- Булево - Признак того, есть у документа контрагенты в табличных частях
//  		"СчетФактураНаходитсяВПодвале"		 	- Булево - Признак того, есть у документа счет-фактура в подвале
//  		"ЯвляетсяСчетомФактурой"				- Булево - Признак того, является ли сам документ счетом-фактурой.
//
//@skip-warning
Процедура ОпределитьВидДокумента(Форма, Результат) Экспорт
	
	Если ТипЗнч(Форма) = Тип("ФормаКлиентскогоПриложения") Тогда
		ДокументСсылка = Форма.Объект.Ссылка;
	Иначе
		ДокументСсылка = Форма;
	КонецЕсли;
	
	ТипСсылки = ТипЗнч(ДокументСсылка);
	
	Если ТипСсылки = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") И Найти(Форма.ИмяФормы, "ФормаДокументаОбщая") <> 0
		ИЛИ ТипСсылки = Тип("ДокументСсылка.РеализацияТоваровУслуг") И Найти(Форма.ИмяФормы, "ФормаДокументаОбщая") <> 0
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
		
		Результат.Вставить("КонтрагентНаходитсяВШапке", Истина);
		Результат.Вставить("КонтрагентНаходитсяВТабличнойЧасти", Истина);
		Результат.Вставить("СчетФактураНаходитсяВПодвале", Истина);
		
	ИначеЕсли ТипСсылки = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ВыкупПредметовЛизинга")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ПередачаНМА")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ПередачаОС")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ПоступлениеДопРасходов")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ПоступлениеИзПереработки")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ПоступлениеНМА")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.РеализацияУслугПоПереработке")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") И Найти(Форма.ИмяФормы, "ФормаДокументаОбщая") = 0
		ИЛИ ТипСсылки = Тип("ДокументСсылка.РеализацияТоваровУслуг") И Найти(Форма.ИмяФормы, "ФормаДокументаОбщая") = 0 Тогда
		
		
		Результат.Вставить("КонтрагентНаходитсяВШапке", Истина);
		Результат.Вставить("СчетФактураНаходитсяВПодвале", Истина);
		
	ИначеЕсли ТипСсылки = Тип("ДокументСсылка.СчетНаОплатуПокупателю")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.СчетНаОплатуПоставщика")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.ПередачаЗадолженностиНаФакторинг") Тогда
		
		Результат.Вставить("КонтрагентНаходитсяВШапке", Истина);
		
	ИначеЕсли ТипСсылки = Тип("ДокументСсылка.ОказаниеУслуг")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
		
		Результат.Вставить("КонтрагентНаходитсяВТабличнойЧасти", Истина);
		
	ИначеЕсли ТипСсылки = Тип("ДокументСсылка.КорректировкаПоступления")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		Результат.Вставить("КонтрагентНаходитсяВТабличнойЧасти", Истина);
		Результат.Вставить("СчетФактураНаходитсяВПодвале", Истина);
		
	ИначеЕсли ТипСсылки = Тип("ДокументСсылка.СчетФактураПолученный")
		ИЛИ ТипСсылки = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		
		Результат.Вставить("ЯвляетсяСчетомФактурой", Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Получение счета-фактуры, находящегося в подвале документа-основания, чья форма передана в качестве
//             параметра.
//
// Параметры:
//  Форма		 - ФормаКлиентскогоПриложения - Форма документа-основания, для которой необходимо получить счет-фактуру.
//  СчетФактура	 - ДокументСсылка - Счет-фактура, полученная для данного документа-основания.
//
//@skip-warning
Процедура ПолучитьСчетФактуру(Форма, СчетФактура) Экспорт
	
	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	Если ПроверкаКонтрагентовКлиентСервер.ЭтоДокументСоСчетомФактуройВПодвале(Форма) Тогда
		СчетФактура = Форма.СчетФактура;
	Иначе
		СчетФактура = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Возможность доопределить сформированную подсказку для формы документа.
//
// Параметры:
//  Результат            - Структура - содержит текст подсказки и цвет фона подсказки.
//  СостояниеКонтрагента - ПеречислениеСсылка.СостоянияСуществованияКонтрагента - текущее состояние контрагента.
//  Цвета                - Структура - содержит цвета, используемые при выводе информации о состоянии контрагента.
//
//@skip-warning
Процедура ПослеФормированияПодсказкиВДокументе(Результат, СостояниеКонтрагента, Цвета) Экспорт
	
	
	
КонецПроцедуры 

#КонецОбласти

#Область ПроверкаКонтрагентовВОтчетах

// Вывод панели проверки в отчете.
//
// Параметры:
//  Форма	 				- ФормаКлиентскогоПриложения - Форма отчета, для которого выводится результат проверки контрагента.
//  СостояниеПроверки		- Строка - Текущее состояние проверки, может принимать следующие значения, либо быть пустой
//                                строкой: ВсеКонтрагентыКорректные
// 			НайденыНекорректныеКонтрагенты
// 			ДопИнформацияПоПроверке
// 			ПроверкаВПроцессеВыполнения
// 			НетДоступаКСервису.
//  СтандартнаяОбработка	- Булево - Если Ложь, то игнорируется стандартное действие и выполняется указанное в данной
//                                  процедуре.
//@skip-warning
Процедура УстановитьВидПанелиПроверкиКонтрагентовВОтчете(Форма, СтандартнаяОбработка, СостояниеПроверки = "") Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаКонтрагентовВСправочнике

// Отображение результата проверки контрагента в справочнике.
// Реализация тела метода является обязательной.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма справочника, в котором выполнялась проверка контрагента.
//  	Результат проверки хранится в реквизите РеквизитыПроверкиКонтрагентов(Структура) формы контрагента.
//  	Структуру полей РеквизитыПроверкиКонтрагентов см. в процедуре ИнициализироватьРеквизитыФормыКонтрагент ОМ
//  	ПроверкаКонтрагентов.
//  ПредставлениеРезультатаПроверки	 - ФорматированнаяСтрока, Строка - представление результата проверки
//  					контрагента.
//
//@skip-warning
Процедура ОтобразитьРезультатПроверкиКонтрагентаВСправочнике(Форма, ПредставлениеРезультатаПроверки) Экспорт
	
	ОбщегоНазначенияБПКлиентСервер.ОтобразитьРезультатПроверкиКонтрагентовВФорме(Форма, ПредставлениеРезультатаПроверки);
	
КонецПроцедуры

// Определяет строковое представление результата проверки контрагента.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма справочника, в котором выполнялась проверка контрагента.
//  	Результат проверки хранится в реквизите РеквизитыПроверкиКонтрагентов(Структура) формы контрагента.
//  	Структуру полей РеквизитыПроверкиКонтрагентов см. в процедуре ИнициализироватьРеквизитыФормыКонтрагент ОМ
//  	ПроверкаКонтрагентов.
//  Текст - Строка - представление результата проверки контрагента.
//
Процедура ПриЗаполненииТекстаРезультатаПроверки(Форма, Текст) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеПроцедуры

// Получение объекта (ДанныеФормыСтруктура) и ссылки(ДокументСсылка, СправочникСсылка) документа или
// справочника,  в котором выполняется проверка контрагента, по форме.
// Обязательна к заполнению.
//
// Параметры:
//	Форма     - ФормаКлиентскогоПриложения - Форма документа или справочника, в котором выполняется проверка контрагента.
//	Результат - Структура - Объект и Ссылка, полученные по форме документа.
//		Ключи: "Объект" (Тип ДанныеФормыСтруктура) и "Ссылка" (Тип ДокументСсылка, СправочникСсылка).
//
//@skip-warning
Процедура ПриОпределенииОбъектаИСсылкиПоФорме(Форма, Результат) Экспорт
	
	ИсточникОбъект = Форма.Объект;
	ИсточникСсылка = ИсточникОбъект.Ссылка;
	
	Результат.Вставить("Объект", ИсточникОбъект);
	Результат.Вставить("Ссылка", ИсточникСсылка);
	
КонецПроцедуры

// Возможность дополнить параметры запуска фонового задания при проверке справочника.
//
// Параметры:
//  ДополнительныеПараметрыЗапуска - Структура - содержит параметры запуска.
//  Форма                          - ФормаКлиентскогоПриложения - форма, из которой запускается фоновое задание.
//
//@skip-warning
Процедура ДополнитьПараметрыЗапускаФоновогоЗадания(ДополнительныеПараметрыЗапуска, Форма) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
