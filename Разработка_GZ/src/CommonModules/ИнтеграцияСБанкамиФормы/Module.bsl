#Область ПрограммныйИнтерфейс

// Показывает баннер нового документа.
//
// Параметры:
//  Форма			 - ФормаКлиентскогоПриложения - Форма платежного поручения.
//                     Форма должна иметь группу ГруппаБаннерСостоянияОтправки с элементом: БаннерСостоянияОтправкиТекст
//
Процедура ПоказатьБаннерНовогоДокумента(Форма, СчетОрганизации) Экспорт
	
	Если ЗначениеЗаполнено(СчетОрганизации)
		И ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВключена(СчетОрганизации) Тогда
		ДанныеБаннера = РегистрыСведений.ДокументыИнтеграцииСБанком.ДанныеБаннераИнтеграцииСБанкомНовыйДокумент();
	Иначе
		ДанныеБаннера = РегистрыСведений.ДокументыИнтеграцииСБанком.ДанныеБаннераИнтеграцииСБанкомПустойБаннер();
	КонецЕсли;
	
	ПоказатьБаннерСостоянияОтправки(Форма, ДанныеБаннера);
	
КонецПроцедуры

// Получает данные для вывода баннера по состоянию интеграции документов
// и выводит баннер на форму помощника.
//
// Параметры:
//  Форма							 - ФормаКлиентскогоПриложения - Форма, на которую выводится баннер.
//  СостоянияИнтеграцииДокументов	 - ТаблицаЗначений - Состояния документов оплаты на форме помощника.
//                                     См. РегистрыСведений.ДокументыИнтеграцииСБанком.СостоянияИнтеграцииДокументов.
//
Процедура ПолучитьДанныеИПоказатьБаннерСостоянияОтправки(Форма, СостоянияИнтеграцииДокументов) Экспорт
	
	Если СостоянияИнтеграцииДокументов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеБаннера = РегистрыСведений.ДокументыИнтеграцииСБанком.ДанныеБаннераИнтеграцииСБанкомДляПомощников(
		СостоянияИнтеграцииДокументов);
	
	ПоказатьБаннерСостоянияОтправки(Форма, ДанныеБаннера);
	
КонецПроцедуры

// Показывает баннер по предварительно полученным данным баннера.
//
// Параметры:
//  Форма			 - ФормаКлиентскогоПриложения - Форма помощника.
//                     Форма должна иметь группу ГруппаБаннерСостоянияОтправки с 2 элементами: БаннерСостоянияОтправкиТекст и ДекорацияБаннерСостоянияОтправкиКартинка.
//                     Также форма должна иметь реквизиты формы ИнтервалПроверкиСостоянияИнтеграцииСБанком (Число) и ХешДанныхБаннера (Число).
//  ДанныеБаннера	 - Структура - Структура с данными баннера.
//                     См. РегистрыСведений.ДокументыИнтеграцииСБанком.ДанныеБаннераИнтеграцииСБанкомДляПомощников.
//
Процедура ПоказатьБаннерСостоянияОтправки(Форма, ДанныеБаннера) Экспорт
	
	Если НЕ ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВИнформационнойБазеВключена() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	БаннерВиден = ЗначениеЗаполнено(ДанныеБаннера.ТекстБаннера);
	Элементы.ГруппаБаннерСостоянияОтправки.Видимость = БаннерВиден;
	Если БаннерВиден Тогда
		Элементы.БаннерСостоянияОтправкиТекст.Заголовок = ДанныеБаннера.ТекстБаннера;
		
		ВидимостьКартинкиБаннера = ЗначениеЗаполнено(ДанныеБаннера.ИмяКартинкиБаннера);
		Если ВидимостьКартинкиБаннера Тогда
			Элементы.ДекорацияБаннерСостоянияОтправкиКартинка.Картинка = БиблиотекаКартинок[ДанныеБаннера.ИмяКартинкиБаннера];
		КонецЕсли;
		Элементы.ДекорацияБаннерСостоянияОтправкиКартинка.Видимость = ВидимостьКартинкиБаннера;
		
		Элементы.ГруппаБаннерСостоянияОтправки.ЦветФона = ДанныеБаннера.ЦветБаннера;
		
	КонецЕсли;
	
	Форма.ИнтервалПроверкиСостоянияИнтеграцииСБанком = ДанныеБаннера.ИнтервалПроверкиСостоянияИнтеграцииСБанком;
	
	Форма.ХешДанныхБаннера = ДанныеБаннера.ХешДанныхБаннера;
	
КонецПроцедуры

// Возвращает хеш по данным баннера для списка документов оплаты.
//
// Параметры:
//  ДокументыОплаты	 - Массив - Массив документов оплаты помощника, для которых выводится баннер.
// 
// Возвращаемое значение:
//   - Число - CRC32 значений данных баннера.
//
Функция ХэшДанныхБаннера(Знач ДокументыОплаты) Экспорт
	
	Если НЕ ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВИнформационнойБазеВключена() Тогда
		Возврат 0;
	КонецЕсли;
	
	СостоянияИнтеграцииДокументов = РегистрыСведений.ДокументыИнтеграцииСБанком.СостоянияИнтеграцииДокументов(ДокументыОплаты);
	
	ДанныеБаннера = РегистрыСведений.ДокументыИнтеграцииСБанком.ДанныеБаннераИнтеграцииСБанкомДляПомощников(
		СостоянияИнтеграцииДокументов);
	
	Возврат ДанныеБаннера.ХешДанныхБаннера;
	
КонецФункции

#КонецОбласти
