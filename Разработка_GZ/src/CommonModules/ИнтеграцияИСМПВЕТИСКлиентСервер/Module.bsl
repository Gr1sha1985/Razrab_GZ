/////////////////////////////////////////////////////////////////////////////
// Совместная работа подсистем ВетИС и ИСМП.
//   * Если подсистема ВетИС отсутствует, очистить модуль (нет клиент-серверной проверки существования подсистемы).
//   * Если подсистема ВетИС внешняя по отношению к библиотеке ГосИС, модуль следует рассматривать как переопределяемый
//       для связи с библиотечной подсистемой ИСМП.
//

#Область ПрограммныйИнтерфейс

// Дополняет параметры сканирования для документа Маркировка товаров при необходимости передавать специфику ВетИС:
//   * Взводит флаг "ЗаполнятьДанныеВЕТИС".
//   * Определяет тип идентификатора происхождения ВетИС.
// 
// Параметры:
//   Операция              - ПеречислениеСсылка.ВидыОперацийИСМП - операция маркировки.
//   ПараметрыСканирования - См. ШтрихкодированиеИСКлиент.ПараметрыСканирования.
//
Процедура ДополнитьПараметрыСканированияМаркировкаТоваров(Операция, ПараметрыСканирования) Экспорт
	
	СтандартнаяОбработка = Истина;
	ИнтеграцияИСМПВЕТИСКлиентСерверПереопределяемый.ДополнитьПараметрыСканированияМаркировкаТоваров(Операция, ПараметрыСканирования, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	ОперацииМаркировки = ИнтеграцияИСМПСлужебныйКлиентСервер.ОперацииМаркировки(Операция);
	Если ОперацииМаркировки.ТребуетсяЗаполнениеИдентификаторовПримененияВЕТИС Тогда
		ПараметрыСканирования.ЗаполнятьДанныеВЕТИС = Истина; // заполнение обязательно
	Иначе
		ПараметрыСканирования.ЗаполнятьДанныеВЕТИС = Ложь; // заполнение возможно
	КонецЕсли;
	
	Если Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоВнеЕАЭС")
		Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотТрансграничнаяТорговля") Тогда
		ПараметрыСканирования.ТипИдентификатораПроисхожденияВЕТИС = Тип("СправочникСсылка.ЗаписиСкладскогоЖурналаВЕТИС");
	Иначе
		ПараметрыСканирования.ТипИдентификатораПроисхожденияВЕТИС = Тип("СправочникСсылка.ВетеринарноСопроводительныйДокументВЕТИС");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает уточненный заголовок поля "Идентификатор происхождения ВетИС" по типу идентификатора
// 
// Параметры:
//   ТипПоля - Тип - тип идентификатора происхождения ВетИС.
// Возвращаемое значение:
//   Строка - заголовок поля.
//
Функция ЗаголовокПоляИдентификаторПроисхожденияВЕТИСПоТипу(ТипПоля) Экспорт
	
	Заголовок = "";
	СтандартнаяОбработка = Истина;
	ИнтеграцияИСМПВЕТИСКлиентСерверПереопределяемый.ПриПолученииЗаголовкаПоляИдентификаторПроисхожденияВЕТИСПоТипу(
		ТипПоля, Заголовок, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат Заголовок;
	КонецЕсли;
	
	Если ТипПоля = Тип("СправочникСсылка.ЗаписиСкладскогоЖурналаВЕТИС") Тогда
		Возврат НСтр("ru = 'Запись журнала'");
	Иначе
		Возврат НСтр("ru = 'ВСД'");
	КонецЕсли;
	
КонецФункции

// Возвращает тип идентификатора происхождения ВетИС для документа "Маркировка товаров ИСМП"
// 
// Параметры:
//   Форма              - ФормаКлиентскогоПриложения - форма документа "Маркировка товаров ИСМП"
//   ЭтоОперацияИмпорта - Булево                     - признак операции импорта
//
// Возвращаемое значение:
//   Тип - тип идентификатора происхождения ВетИС
//
Функция ТипИдентификатораПроисхожденияВДокументеМаркировкаТоваров(Форма, ЭтоОперацияИмпорта) Экспорт
	
	ТипПоля = Тип("Строка");
	СтандартнаяОбработка = Истина;
	ИнтеграцияИСМПВЕТИСКлиентСерверПереопределяемый.ПриПолученииТипаПоляИдентификаторПроисхожденияВЕТИСВДокументеМаркировка(
		ТипПоля, Форма, ЭтоОперацияИмпорта, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат ТипПоля;
	КонецЕсли;
	
	Если ТипЗнч(Форма.Объект.ДокументОснование) = Тип("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС") Тогда
		Возврат Тип("СправочникСсылка.ЗаписиСкладскогоЖурналаВЕТИС");
	ИначеЕсли ТипЗнч(Форма.Объект.ДокументОснование) = Тип("ДокументСсылка.ПроизводственнаяОперацияВЕТИС") Тогда
		Возврат Тип("СправочникСсылка.ВетеринарноСопроводительныйДокументВЕТИС");
	ИначеЕсли ЭтоОперацияИмпорта Тогда
		Возврат Тип("СправочникСсылка.ЗаписиСкладскогоЖурналаВЕТИС");
	Иначе
		Возврат Тип("СправочникСсылка.ВетеринарноСопроводительныйДокументВЕТИС");
	КонецЕсли;
	
КонецФункции

#КонецОбласти