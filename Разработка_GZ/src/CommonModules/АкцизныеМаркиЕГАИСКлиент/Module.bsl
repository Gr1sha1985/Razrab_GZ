
#Область СлужебныеПроцедурыИФункции

// Обрабатывает событие клика на гиперссылку сохраенного выбораю
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Источник события.
// 	НавигационнаяСсылкаФорматированнойСтроки - Строка - Навигационная ссылка элемента формы.
//
Процедура ПредставлениеСохраненногоВыбораОбработкаНавигационнойСсылки(Форма, НавигационнаяСсылкаФорматированнойСтроки) Экспорт
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьАлкогольнуюПродукцию" Тогда
		
		ПоказатьЗначение(, Форма.ДанныеВыбораПоМаркируемойПродукции.АлкогольнаяПродукция);
		
	Иначе
		
		ИнтеграцияИСКлиент.ПредставлениеСохраненногоВыбораОбработкаНавигационнойСсылки(Форма, НавигационнаяСсылкаФорматированнойСтроки);
		
	КонецЕсли;
	
КонецПроцедуры

// Открывате форму, сообщающую о невозможности добавления отсканированного штрихкода.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма-источник события.
// 	ДанныеОПроблеме- Строка - Данные штрихкода или адрес во временном хранилище данных штрихкода.
//
Процедура ОткрытьФормуНевозможностиДобавленияОтсканированного(Форма, ДанныеОПроблеме) Экспорт
	
	Если ТипЗнч(ДанныеОПроблеме) = Тип("Структура") Тогда
		ПараметрыОткрытияФормы = Новый Структура();
		ПараметрыОткрытияФормы.Вставить("ДанныеШтрихкода", ДанныеОПроблеме);
	Иначе
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("АдресХранилищаДереваУпаковки", ДанныеОПроблеме);
	КонецЕсли;
	
	ОткрытьФорму(
		"Обработка.ПроверкаИПодборАлкогольнойПродукцииЕГАИС.Форма.ИнформацияОНевозможностиДобавленияОтсканированного",
		ПараметрыОткрытияФормы,
		Форма,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти