
#Область СлужебныйПрограммныйИнтерфейс

#Область РасширенныйНалоговыйПериод

// Определяет нужно или нет представлять отчетность за квартал, внутри которого находится переданная дата.
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Период      - Дата - на которую определяется необходимость представления отчетности по НДС.
//
// Возвращаемое значение:
//  Булево - Истина - отчетность по НДС необходимо представить,
//           Ложь   - отчетность по НДС не представляется.
Функция ОтчетностьПоНДСПредставляется(Организация, Период) Экспорт
	
	БлижайшийНалоговыйПериод = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(
		Организация,
		Период,
		Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВПоследние10ДнейКвартала,
		Перечисления.Периодичность.Квартал);
	
	Возврат БлижайшийНалоговыйПериод.Конец = КонецДня(КонецКвартала(Период));

КонецФункции

// Определяет соответствие выбранного в отчете периода налоговому.
// Параметры:
//  Организация   - СправочникСсылка.Организация - организация, для которой определяется налоговый период,
//  НачалоПериода - Дата - начало периода отчета,
//  КонецПериода  - Дата - конец периода отчета.
//
// Возвращаемое значение:
//  Булево - Истина - период соответствует налоговому, можно формировать отчет по стандартной форме (с шапкой и подписями)
//           Ложь   - период не соответствует налоговому, отчет формируется без шапки и подписей.
Функция ПериодОтчетаСоответствуетНалоговому(Организация, НачалоПериода, КонецПериода) Экспорт
	
	БлижайшийНалоговыйПериод = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(
		Организация,
		КонецПериода,
		Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВПоследние10ДнейКвартала,
		Перечисления.Периодичность.Квартал);
		
	Если НачалоКвартала(БлижайшийНалоговыйПериод.Начало) = НачалоКвартала(БлижайшийНалоговыйПериод.Конец) Тогда
		// Сокращение налогового периода внутри одного квартала не учитываем.
		НачалоНалоговогоПериода = НачалоКвартала(БлижайшийНалоговыйПериод.Начало);
	Иначе
		НачалоНалоговогоПериода = БлижайшийНалоговыйПериод.Начало;
	КонецЕсли;
		
	Возврат НачалоНалоговогоПериода = НачалоПериода
	      И БлижайшийНалоговыйПериод.Конец = КонецДня(КонецПериода);
	
КонецФункции

// Определяет начало налогового периода по НДС.
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Период      - Дата - внутри налогового периода, для которого необходимо определить начало.
//
// Возвращаемое значение:
//  Дата - начало налогового периода по НДС, если отчетность в данном периоде представляется
//         или начало квартала, если отчетность не представляется.
Функция НачалоНалоговогоПериода(Организация, Период) Экспорт
	
	НачалоНалоговогоПериода = ИнтерфейсыВзаимодействияБРО.НачалоНалоговогоПериода(
		Организация,
		Период,
		Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВПоследние10ДнейКвартала,
		Перечисления.Периодичность.Квартал,
		Ложь);
		
	Если ЗначениеЗаполнено(НачалоНалоговогоПериода) Тогда
		Возврат НачалоНалоговогоПериода;
	Иначе
		Возврат НачалоКвартала(Период);
	КонецЕсли;
	
КонецФункции

// Определяет, находятся ли переданные даты внутри одного налогового периода по НДС.
// Параметры:
//  Организация   - СправочникСсылка.Организация - организация.
//  НачалоПериода - Дата - начало периода отчета.
//  КонецПериода  - Дата - конец периода отчета.
//
// Возвращаемое значение:
//  Булево - Истина - переданные даты находятся внутри одного налогового периода,
//           Ложь   - даты расположены в разных налоговых периодах.
Функция ДатыРасположеныВнутриОдногоНалоговогоПериода(Организация, НачалоПериода, КонецПериода) Экспорт
	
	БлижайшийНалоговыйПериод = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(
		Организация,
		КонецПериода,
		Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВПоследние10ДнейКвартала,
		Перечисления.Периодичность.Квартал);
		
	Если КонецДня(КонецКвартала(КонецПериода)) < БлижайшийНалоговыйПериод.Конец Тогда
		// Учет еще не ведется, необходимо просто проверить, что даты находятся в одном квартале.
		Возврат НачалоКвартала(НачалоПериода) = НачалоКвартала(НачалоПериода);
	КонецЕсли;
	
	Если НачалоКвартала(БлижайшийНалоговыйПериод.Начало) = НачалоКвартала(БлижайшийНалоговыйПериод.Конец) Тогда
		// Сокращение налогового периода внутри одного квартала не учитываем.
		НачалоНалоговогоПериода = НачалоКвартала(БлижайшийНалоговыйПериод.Начало);
	Иначе
		НачалоНалоговогоПериода = БлижайшийНалоговыйПериод.Начало;
	КонецЕсли;
		
	Возврат НачалоНалоговогоПериода <= НачалоПериода
	      И КонецДня(КонецПериода) <= БлижайшийНалоговыйПериод.Конец;
	
КонецФункции

#КонецОбласти

#Область ОбщиеПроцедурыИФункцииОтчетов

Процедура ФормаОтчетаПриСозданииНаСервере(Форма) Экспорт
	
	Отчет = Форма.Отчет;
	
	Если НЕ ЗначениеЗаполнено(Отчет.Организация) Тогда
		Отчет.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Отчет.КонецПериода) Тогда
		Отчет.КонецПериода = КонецКвартала(ОбщегоНазначения.ТекущаяДатаПользователя());
		Если НЕ ЗначениеЗаполнено(Отчет.НачалоПериода) Тогда
			// В случае расширенного налогового периода необходимо правильно указать дату начала.
			Отчет.НачалоПериода = НачалоНалоговогоПериода(
				Отчет.Организация, Отчет.КонецПериода);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Отчет.НачалоПериода) Тогда
		Отчет.НачалоПериода = НачалоКвартала(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецЕсли;
	
	УчетНДСКлиентСервер.ОтобразитьПоясненияКПериодуОтчета(Форма);

	БухгалтерскиеОтчеты.УстановитьАктивностьКнопокКоманднойПанели(Форма);
	
КонецПроцедуры

Процедура ФормаОтчетаОбработатьВыборПериода(Форма) Экспорт
	
	Отчет = Форма.Отчет;
	
	Если НачалоКвартала(Отчет.КонецПериода) = Отчет.НачалоПериода
		И КонецКвартала(Отчет.КонецПериода) = КонецДня(Отчет.КонецПериода) Тогда
		// Выбран квартал. В случае расширенного налогового периода необходимо правильно указать дату начала.
		Отчет.НачалоПериода = НачалоНалоговогоПериода(
			Отчет.Организация, Отчет.КонецПериода);
	КонецЕсли;
	
	УчетНДСКлиентСервер.ОтобразитьПоясненияКПериодуОтчета(Форма);
	
КонецПроцедуры

#КонецОбласти

Функция ОпределитьВидСчетаФактурыВыданногоПоТипуОснования(ДокументОснование, ВидСчетаФактурыПоУмолчанию = Ложь) Экспорт

	СписокТиповНаАванс			= ПолучитьСписокТиповПоВидуСчетаФактурыВыставленного(Перечисления.ВидСчетаФактурыВыставленного.НаАванс);
	СписокТиповНалоговыйАгент	= ПолучитьСписокТиповПоВидуСчетаФактурыВыставленного(Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент);
	СписокТиповКорректировочный	= ПолучитьСписокТиповПоВидуСчетаФактурыВыставленного(Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		
		Возврат ДокументОснование.ВидСчетаФактуры;
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		
		Возврат Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента;
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		
		Возврат Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;
		
	// По отчету комиссионера может быть выписан как счет-фактура выданный 
	// так и корректировочный счет-фактура при установленном реквизите НаВозвратыВыставляетсяКорректировочныйСчетФактура
	// поэтому возвращаем пустую ссылку 
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда

		НаВозвратыВыставляетсяКорректировочныйСчетФактура = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "НаВозвратыВыставляетсяКорректировочныйСчетФактура");
			
		Если НаВозвратыВыставляетсяКорректировочныйСчетФактура И НЕ ВидСчетаФактурыПоУмолчанию Тогда
			Возврат Перечисления.ВидСчетаФактурыВыставленного.ПустаяСсылка();
		Иначе
			Возврат Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;
		КонецЕсли;
		
	ИначеЕсли СписокТиповНаАванс.Найти(ТипЗнч(ДокументОснование)) <> Неопределено 
		И ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		Возврат Перечисления.ВидСчетаФактурыВыставленного.НаАванс;

	ИначеЕсли СписокТиповНалоговыйАгент.Найти(ТипЗнч(ДокументОснование)) <> Неопределено Тогда
		
		Возврат Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент;
		
	ИначеЕсли СписокТиповКорректировочный.Найти(ТипЗнч(ДокументОснование)) <> Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
		
		НомераТаблиц	= Новый Структура;
		Запрос.Текст	= 
		"ВЫБРАТЬ
		|	КорректировкаРеализации.Ссылка КАК ДокументОснование,
		|	КорректировкаРеализации.Дата,
		|	КорректировкаРеализации.Организация,
		|	КорректировкаРеализации.Контрагент,
		|	КорректировкаРеализации.ДоговорКонтрагента,
		|	КорректировкаРеализации.ВидОперации,
		|	КорректировкаРеализации.ДокументРеализации,
		|	КорректировкаРеализации.ИсправляемыйДокументРеализации,
		|	КорректировкаРеализации.ИсправляемыйДокументРеализации.ВидОперации КАК ИсправляемыйДокументРеализацииВидОперации
		|ИЗ
		|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|ГДЕ
		|	КорректировкаРеализации.Ссылка = &ДокументОснование";
		
		Результат	= Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;
		КонецЕсли;
		
		РеквизитыОснования	= Результат.Выбрать();
		РеквизитыОснования.Следующий();		
		
		Если ЗначениеЗаполнено(РеквизитыОснования.ИсправляемыйДокументРеализации)
			И (ТипЗнч(РеквизитыОснования.ИсправляемыйДокументРеализации) = Тип("ДокументСсылка.КорректировкаРеализации")) 
			И РеквизитыОснования.ИсправляемыйДокументРеализацииВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение 
			И РеквизитыОснования.ВидОперации <> Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
			
			Возврат Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;
			
		ИначеЕсли РеквизитыОснования.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
			
			Возврат Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;
			
		Иначе		
			
			ОснованиеДляВидаОперации	= ПолучитьИсправляемыйДокументРеализации(ДокументОснование);
			
			Если СписокТиповНалоговыйАгент.Найти(ТипЗнч(ОснованиеДляВидаОперации)) <> Неопределено Тогда
				
				Возврат	Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент;
				
			ИначеЕсли СписокТиповНаАванс.Найти(ТипЗнч(ОснованиеДляВидаОперации)) <> Неопределено Тогда			
				
				Если ТипЗнч(ОснованиеДляВидаОперации) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")
					ИЛИ ТипЗнч(ОснованиеДляВидаОперации) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
					
					Возврат Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;
				Иначе
					Возврат Перечисления.ВидСчетаФактурыВыставленного.НаАванс;
				КонецЕсли;
				
			ИначеЕсли СписокТиповКорректировочный.Найти(ТипЗнч(ОснованиеДляВидаОперации)) <> Неопределено Тогда
				
				Возврат Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;	
				
			Иначе
				
				Возврат Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;	
				
			КонецЕсли;
			
		КонецЕсли;
	
	Иначе
		
		Возврат Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;
		
	КонецЕсли;

КонецФункции

Функция ПолучитьСписокТиповПоВидуСчетаФактурыВыставленного(ВидСчетаФактурыСписка, ЭтоИсправление = Ложь, НаАвансДляКомитентаНаЗакупку = Ложь) Экспорт
	
	СписокТипов = Новый Массив;
	
	Если ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный 
		ИЛИ ЭтоИсправление И ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию Тогда
		
		СписокТипов.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
		
		Если ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный И Не ЭтоИсправление Тогда
			СписокТипов.Добавить(Тип("ДокументСсылка.ВозвратТоваровОтПокупателя"));
			СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
		КонецЕсли;
		
	ИначеЕсли ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент 
		ИЛИ НаАвансДляКомитентаНаЗакупку Тогда 
		
		СписокТипов.Добавить(Тип("ДокументСсылка.ДокументРасчетовСКонтрагентом"));
		СписокТипов.Добавить(Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
		СписокТипов.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
		СписокТипов.Добавить(Тип("ДокументСсылка.АвансовыйОтчет"));
		УчетОбособленныхПодразделений.ДобавитьТипДокументаОбособленныхПодразделений(СписокТипов, "АвизоРасчетыВходящее");
		
	ИначеЕсли ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.СводнаяСправка Тогда
		
		СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОплатаПлатежнойКартой"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
		
	ИначеЕсли ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.КорректировочнаяСправка Тогда
		
		СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ВозвратТоваровОтПокупателя"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
		
	Иначе
		
		СписокТипов.Добавить(Тип("ДокументСсылка.ДокументРасчетовСКонтрагентом"));
		СписокТипов.Добавить(Тип("ДокументСсылка.КорректировкаДолга"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОплатаПлатежнойКартой"));
				
		Если ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.НаАванс
			ИЛИ ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента
			ИЛИ ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.КорректировочныйНаАванс Тогда
			
			СписокТипов.Добавить(Тип("ДокументСсылка.ВозвратТоваровОтПокупателя"));
			
			Если ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.НаАванс Тогда
				СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
			КонецЕсли;
			
		ИначеЕсли ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу Тогда
			// Список типов не требует корректировки
		ИначеЕсли ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию Тогда 
			
			ВычитаемыеТипы = Новый Массив;
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.НачислениеНДСпоСМРхозспособом"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ОказаниеУслуг"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуОС"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ВозвратТоваровОтПокупателя"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ДокументРасчетовСКонтрагентом"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.КорректировкаДолга"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ПодтверждениеНулевойСтавкиНДС"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ОплатаПлатежнойКартой"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.АвансовыйОтчет"));
			ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.СчетФактураВыданный"));
			
			УчетОбособленныхПодразделений.ДобавитьТипДокументаОбособленныхПодразделений(ВычитаемыеТипы, "АвизоРасчетыВходящее");
			УчетОбособленныхПодразделений.ДобавитьТипДокументаОбособленныхПодразделений(ВычитаемыеТипы, "АвизоРасчетыИсходящее");
			
			СписокТипов = Новый ОписаниеТипов(
				Метаданные.Документы.СчетФактураВыданный.ТабличныеЧасти.ДокументыОснования.Реквизиты.ДокументОснование.Тип,
				,
				ВычитаемыеТипы);
			СписокТипов = СписокТипов.Типы();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СписокТипов;
	
КонецФункции

Функция ОпределитьВидСчетаФактурыПолученногоПоТипуОснования(ДокументОснование) Экспорт

	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета")
		ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") 
		ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.АвансовыйОтчет")
		ИЛИ ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		
		Возврат Перечисления.ВидСчетаФактурыПолученного.НаАванс;
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
		
		НомераТаблиц	= Новый Структура;
		Запрос.Текст	= 
		"ВЫБРАТЬ
		|	КорректировкаПоступления.Ссылка КАК ДокументОснование,
		|	КорректировкаПоступления.Дата,
		|	КорректировкаПоступления.Организация,
		|	КорректировкаПоступления.Контрагент,
		|	КорректировкаПоступления.ДоговорКонтрагента,
		|	КорректировкаПоступления.ВидОперации,
		|	КорректировкаПоступления.ДокументПоступления,
		|	КорректировкаПоступления.ИсправляемыйДокументПоступления,
		|	КорректировкаПоступления.ИсправляемыйДокументПоступления.ВидОперации КАК ИсправляемыйДокументПоступленияВидОперации
		|ИЗ
		|	Документ.КорректировкаПоступления КАК КорректировкаПоступления
		|ГДЕ
		|	КорректировкаПоступления.Ссылка = &ДокументОснование";
		
		Результат	= Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат Перечисления.ВидСчетаФактурыПолученного.НаПоступление;
		КонецЕсли;
		
		РеквизитыОснования	= Результат.Выбрать();
		РеквизитыОснования.Следующий();		
		
		Если ЗначениеЗаполнено(РеквизитыОснования.ИсправляемыйДокументПоступления)
			И (ТипЗнч(РеквизитыОснования.ИсправляемыйДокументПоступления) = Тип("ДокументСсылка.КорректировкаПоступления")) 
			И РеквизитыОснования.ИсправляемыйДокументПоступленияВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение 
			И РеквизитыОснования.ВидОперации <> Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
			
			Возврат Перечисления.ВидСчетаФактурыПолученного.Корректировочный;
			
		ИначеЕсли РеквизитыОснования.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
			
			Возврат Перечисления.ВидСчетаФактурыПолученного.Корректировочный;
			
		Иначе
			
			ОснованиеДляВидаОперации	= ПолучитьИсправляемыйДокументПоступления(ДокументОснование);
			
			Если ТипЗнч(ОснованиеДляВидаОперации) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета")
				ИЛИ ТипЗнч(ОснованиеДляВидаОперации) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") 
				ИЛИ ТипЗнч(ОснованиеДляВидаОперации) = Тип("ДокументСсылка.АвансовыйОтчет")
				ИЛИ ТипЗнч(ОснованиеДляВидаОперации) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				
				Возврат Перечисления.ВидСчетаФактурыПолученного.НаАванс;
				
			ИначеЕсли ТипЗнч(ОснованиеДляВидаОперации) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
				
				Возврат Перечисления.ВидСчетаФактурыПолученного.Корректировочный;
				
			Иначе
				
				Возврат Перечисления.ВидСчетаФактурыПолученного.НаПоступление;	
				
			КонецЕсли;
			
		КонецЕсли;

	ИначеЕсли  ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
	
		Возврат Перечисления.ВидСчетаФактурыПолученного.Корректировочный;

	ИначеЕсли  ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		Возврат Перечисления.ВидСчетаФактурыПолученного.НаПоступление;
		
	КонецЕсли;

КонецФункции

//////////////////////////////////////////////////////////////////////////
// РАБОТА СО СЧЕТАМИ-ФАКТУРАМИ

// Документ - СчетФактураПолученный или основание для него
Функция РеквизитыДляНадписиОСчетеФактуреПолученном(Знач Документ, СтруктураОтбора = Неопределено) Экспорт

	ДокументОснованияПроведен = Истина;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		СчетФактура = Документ;
		СчетФактураДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетФактура, "ДокументОснование");
		Если ЗначениеЗаполнено(СчетФактураДокументОснование) Тогда
			ДокументОснованияПроведен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетФактураДокументОснование, "Проведен");
		Иначе
			ДокументОснованияПроведен = Ложь;
		КонецЕсли;
	Иначе
		СчетФактура = НайтиПодчиненныйСчетФактуруПолученный(Документ, , , СтруктураОтбора);
		ДокументОснованияПроведен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Проведен");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СчетФактура) Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	РеквизитыСФ = Новый Структура(
		"Ссылка,Представление,Исправление,ИсправлениеСобственнойОшибки,НомерИсправления,
		|ДатаИсправления,НомерСчетаФактуры,ДатаСчетаФактуры,Дата,Корректировка,Проведен,
		|КодВидаОперации,НДСПредъявленКВычету",
		СчетФактура, "", Ложь, Ложь, 0, '00010101');
		
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактура,
		"НомерВходящегоДокумента,ДатаВходящегоДокумента,Исправление,ИсправлениеСобственнойОшибки,
		|НомерИсправления,ДатаИсправления,Представление,Дата,ВидСчетаФактуры,Проведен,
		|КодВидаОперации,НДСПредъявленКВычету");
		
	ЗаполнитьЗначенияСвойств(РеквизитыСФ, ЗначенияРеквизитов);
	Если НЕ ДокументОснованияПроведен Тогда // Если документ-основание не проведен, то не фиксируем проведение счета-фактуры.
		РеквизитыСФ.Проведен = Неопределено;
	КонецЕсли;
	РеквизитыСФ.НомерСчетаФактуры = ЗначенияРеквизитов.НомерВходящегоДокумента;
	РеквизитыСФ.ДатаСчетаФактуры  = ЗначенияРеквизитов.ДатаВходящегоДокумента;
	РеквизитыСФ.Корректировка     = ЗначенияРеквизитов.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный;
	
	РеквизитыСФ.Вставить("НадписьСчетФактура",   ПредставлениеСчетФактураПолученный(РеквизитыСФ));
	РеквизитыСФ.Вставить("КраткоеПредставление", ПредставлениеСчетФактураПолученный(РеквизитыСФ));
	РеквизитыСФ.Вставить("Ссылка",               СчетФактура);
	РеквизитыСФ.Вставить("СчетФактура",          СчетФактура);
	
	Возврат РеквизитыСФ;
	
КонецФункции 

// Документ - СчетФактураВыданный или основание для него
Функция РеквизитыДляНадписиОСчетеФактуреВыданном(Знач Документ, СтруктураОтбора = Неопределено) Экспорт
	
	ДокументОснованияПроведен = Истина;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		СчетФактура = Документ;
		СчетФактураДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетФактура, "ДокументОснование");
		Если ЗначениеЗаполнено(СчетФактураДокументОснование) Тогда
			ДокументОснованияПроведен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетФактураДокументОснование, "Проведен");
		Иначе
			ДокументОснованияПроведен = Ложь;
		КонецЕсли;
	Иначе
		СчетФактура = НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(Документ, , , СтруктураОтбора);
		ДокументОснованияПроведен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Проведен");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетФактура) Тогда

		РеквизитыСФ = Новый Структура(
			"Ссылка,ПредставлениеНомера,Представление,НомерСчетаФактуры,ДатаСчетаФактуры,Исправление,
			| НомерИсправления,ДатаИсправления,Корректировка,Проведен,КодВидаОперации",
			СчетФактура, "");
			
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактура,
			"Номер,Дата,ПредставлениеНомера,ВидСчетаФактуры,Исправление,НомерИсправления,
			|НомерИсходногоДокумента,ДатаИсходногоДокумента,
			|НомерИсправляемогоКорректировочногоДокумента,ДатаИсправляемогоКорректировочногоДокумента,
			|Представление,Проведен,КодВидаОперации");
			
		ЗаполнитьЗначенияСвойств(РеквизитыСФ, ЗначенияРеквизитов, 
			"Исправление,НомерИсправления,Представление,ПредставлениеНомера,Проведен,КодВидаОперации");
		РеквизитыСФ.Корректировка = ЗначенияРеквизитов.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;

		Если НЕ ДокументОснованияПроведен Тогда // Если документ основания не проведен, то не фиксируем проведение счет-фактуры.
			РеквизитыСФ.Проведен = Неопределено;
		КонецЕсли;
		
		РеквизитыСФ.Вставить("СчетФактура", СчетФактура);
	
		Если ЗначенияРеквизитов.Исправление Тогда
			РеквизитыСФ.ДатаИсправления = ЗначенияРеквизитов.Дата;
			Если ЗначенияРеквизитов.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
				РеквизитыСФ.НомерСчетаФактуры = ЗначенияРеквизитов.НомерИсправляемогоКорректировочногоДокумента;
				РеквизитыСФ.ДатаСчетаФактуры  = ЗначенияРеквизитов.ДатаИсправляемогоКорректировочногоДокумента;
			Иначе
				РеквизитыСФ.НомерСчетаФактуры = ЗначенияРеквизитов.НомерИсходногоДокумента;
				РеквизитыСФ.ДатаСчетаФактуры  = ЗначенияРеквизитов.ДатаИсходногоДокумента;
			КонецЕсли;
		Иначе
			РеквизитыСФ.НомерСчетаФактуры = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
				ЗначенияРеквизитов.Номер, Истина, Ложь);
			РеквизитыСФ.ДатаСчетаФактуры  = ЗначенияРеквизитов.Дата;
		КонецЕсли;
	Иначе
		РеквизитыСФ = Неопределено;
	КонецЕсли;
	
	Возврат РеквизитыСФ;
	
КонецФункции 

// Функция возвращает структуру реквизитов счета-фактуры для вывода в табличные документы книги покупок и книги продаж.

// Параметры:
// ЗаписьКниги - строка выборки, содержащая регистрационную запись книги покупок или продаж.
// Возвращаемое значение:
// ПредставлениеСчетаФактуры - структура см. функцию НовыйПредставлениеСчетаФактуры()
Функция ОпределитьДатуИНомерСчетаФактурыДляПечати(ЗаписьКниги) Экспорт
	
	ПредставлениеСчетаФактуры = НовыйПредставлениеСчетаФактуры();
	
	Если НЕ ЗначениеЗаполнено(ЗаписьКниги.СчетФактура) Тогда
		Возврат ПредставлениеСчетаФактуры;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаписьКниги.ДатаСчетаФактуры) И ЗначениеЗаполнено(ЗаписьКниги.НомерСчетаФактуры) Тогда
		
		Если ЗаписьКниги.ОбрабатыватьНомерДокумента Тогда
			НомерСФ = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ЗаписьКниги.НомерСчетаФактуры, Истина, Ложь);
		Иначе
			НомерСФ = СокрЛП(ЗаписьКниги.НомерСчетаФактуры);
		КонецЕсли;
		ДатаСФ = Формат(ЗаписьКниги.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy");
		
		Если ТипЗнч(ЗаписьКниги.СчетФактура) = Тип("ДокументСсылка.ЗаявлениеОВвозеТоваров")
			И ЗаписьКниги.ОбрабатыватьНомерДокумента Тогда
			
			НомерСФ = ?(ЗначениеЗаполнено(ДатаСФ), "Заявление № ", "") + НомерСФ;
			Представление = СтрШаблон(НСтр("ru='%1 № %2 от %3'"),
				НСтр("ru='Заявление о ввозе товаров'"), НомерСФ, ДатаСФ);
				
		ИначеЕсли ТипЗнч(ЗаписьКниги.СчетФактура) = Тип("ДокументСсылка.ГТДИмпорт") Тогда

			ПредставлениеСчетаФактуры.Дата          = ДатаСФ;
			ПредставлениеСчетаФактуры.Номер         = НомерСФ;
			ПредставлениеСчетаФактуры.НомерДата     = НомерСФ;
			ПредставлениеСчетаФактуры.ДатаНомер     = НомерСФ;
			ПредставлениеСчетаФактуры.Представление = СтрШаблон(НСтр("ru='%1 № %2 от %3'"),
				НСтр("ru='Таможенная декларация'"), НомерСФ, ДатаСФ);
			ПредставлениеСчетаФактуры.НомерОтДата   = НомерСФ;
			Возврат ПредставлениеСчетаФактуры;
			
		Иначе
			
			Если ЗначениеЗаполнено(ЗаписьКниги.СчетФактураДокумент) Тогда
				Если ТипЗнч(ЗаписьКниги.СчетФактураДокумент) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
					Представление = СтрШаблон(НСтр("ru='%1 № %2 от %3'"),
						НСтр("ru='Счет-фактура выданный'"),
						НомерСФ,
						ДатаСФ);
				ИначеЕсли ТипЗнч(ЗаписьКниги.СчетФактураДокумент) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
					Представление = СтрШаблон(НСтр("ru='%1 № %2 от %3'"),
						НСтр("ru='Счет-фактура полученный'"),
						НомерСФ,
						ДатаСФ);
				Иначе
					ИмяДокумента = ЗаписьКниги.СчетФактураДокумент.Метаданные().Синоним;
					Представление = СтрШаблон(НСтр("ru='%1 № %2 от %3'"),
						ИмяДокумента,
						НомерСФ,
						ДатаСФ);
				КонецЕсли;
			Иначе
				ИмяДокумента = ЗаписьКниги.СчетФактура.Метаданные().Синоним;
				Представление = СтрШаблон(НСтр("ru='%1 № %2 от %3'"),
					ИмяДокумента,
					НомерСФ,
					ДатаСФ);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(ЗаписьКниги.НомерСчетаФактуры) Тогда
		
		Если ЗаписьКниги.ОбрабатыватьНомерДокумента Тогда
			НомерСФ = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ЗаписьКниги.НомерСчетаФактуры, Истина, Ложь);
		Иначе
			НомерСФ = СокрЛП(ЗаписьКниги.НомерСчетаФактуры);
		КонецЕсли;
		
		ПредставлениеСчетаФактуры.Номер         = НомерСФ;
		ПредставлениеСчетаФактуры.НомерДата     = НомерСФ;
		ПредставлениеСчетаФактуры.ДатаНомер     = НомерСФ;
		
	ИначеЕсли ЗаписьКниги.СводныйКорректировочный Тогда
		// Если для сводного корректировочного счета-фактуры не указаны номер и дата,
		// значит он регистрируется по постановлению № 981 без указания данных граф в книгах.
		Возврат ПредставлениеИсправленногоКорректировочногоСчетаФактуры(ПредставлениеСчетаФактуры, ЗаписьКниги);
	Иначе
		
		Если ТипЗнч(ЗаписьКниги.СчетФактура) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаписьКниги.СчетФактура, "Дата,Номер");
			ДатаСФ  = Формат(РеквизитыОснования.Дата, "ДФ=dd.MM.yyyy");
			НомерСФ = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыОснования.Номер, Истина, Ложь);
			Представление = СтрШаблон(НСтр("ru='%1 № %2 от %3'"),
				НСтр("ru='Счет-фактура выданный'"),
				НомерСФ,
				ДатаСФ);
		ИначеЕсли ТипЗнч(ЗаписьКниги.СчетФактураДокумент) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			РеквизитыСчетаФактуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаписьКниги.СчетФактураДокумент, "Дата,Номер");
			ДатаСФ  = Формат(РеквизитыСчетаФактуры.Дата, "ДФ=dd.MM.yyyy");
			НомерСФ = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыСчетаФактуры.Номер, Истина, Ложь);
			Представление = СтрШаблон(НСтр("ru='%1 № %2 от %3'"),
				НСтр("ru='Счет-фактура выданный'"),
				НомерСФ,
				ДатаСФ);
		ИначеЕсли ТипЗнч(ЗаписьКниги.СчетФактура) = Тип("ДокументСсылка.ГТДИмпорт") Тогда
			
			ДатаГТД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаписьКниги.СчетФактураДокумент, "Дата");
			
			ПредставлениеСчетаФактуры.Дата          = ДатаГТД;
			ПредставлениеСчетаФактуры.Номер         = ЗаписьКниги.НомерСчетаФактуры;
			ПредставлениеСчетаФактуры.НомерДата     = ЗаписьКниги.НомерСчетаФактуры;
			ПредставлениеСчетаФактуры.ДатаНомер     = ЗаписьКниги.НомерСчетаФактуры;
			ПредставлениеСчетаФактуры.Представление = СтрШаблон(НСтр("ru='%1 № %2 от %3'"),
				НСтр("ru='Таможенная декларация'"), ЗаписьКниги.НомерСчетаФактуры, Формат(ДатаГТД, "ДФ=dd.MM.yyyy"));
			ПредставлениеСчетаФактуры.НомерОтДата   = ЗаписьКниги.НомерСчетаФактуры;
			Возврат ПредставлениеСчетаФактуры;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СокрЛП(НомерСФ)) Тогда
		Если ЗначениеЗаполнено(ДатаСФ) Тогда 
			ПредставлениеСчетаФактуры.ДатаНомер   = СтрШаблон(НСтр("ru='%1;%2'"), ДатаСФ, НомерСФ);
			ПредставлениеСчетаФактуры.НомерДата   = СтрШаблон(НСтр("ru='%1;%2'"), НомерСФ, ДатаСФ);
			ПредставлениеСчетаФактуры.НомерОтДата = СтрШаблон(НСтр("ru='%1 от %2'"), НомерСФ, ДатаСФ);
		Иначе
			ПредставлениеСчетаФактуры.ДатаНомер   = НомерСФ;
			ПредставлениеСчетаФактуры.НомерДата   = НомерСФ;
			ПредставлениеСчетаФактуры.НомерОтДата = НомерСФ;
		КонецЕсли;
	КонецЕсли;
	
	ПредставлениеСчетаФактуры.Номер         = СокрЛП(НомерСФ);
	ПредставлениеСчетаФактуры.Дата          = ДатаСФ;
	ПредставлениеСчетаФактуры.Представление = Представление;
	
	Возврат ПредставлениеИсправленногоКорректировочногоСчетаФактуры(ПредставлениеСчетаФактуры, ЗаписьКниги);

КонецФункции

// Функция производит поиск счета-фактуры полученного с видом "на поступление" или "корректировочный" 
// по документу-основанию.
//
// Параметры:
//  ДокументОснование	- ДокументСсылка.* - Ссылка на документ, для которого надо найти счет-фактуру.
//  ИсключаемыйСФ		- ДокументСсылка.* - Ссылка на счет-фактуру, исключаемый при поиске.
//  ПометкаУдаления		- Булево - Значение пометки счета-фактуры для отбора при поиске.
//	СтруктураОтбора     - Структура - Дополнительный отбор счетов-фактур.
//
// Возвращаемое значение:
//  ДокументСсылка.СчетФактураПолученный - Если нашли, то возвращаем ссылку, не нашли - Неопределено.
//
Функция НайтиПодчиненныйСчетФактуруПолученный(
	ДокументОснование, ИсключаемыйСФ = Неопределено, ПометкаУдаления = Ложь, СтруктураОтбора = Неопределено) Экспорт

	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		// Если в качестве документа-основания указана счет-фактура, то искать нет необходимости.
		
		Если ДокументОснование = ИсключаемыйСФ Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		РеквизитыСчетаФактуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДокументОснование, "ПометкаУдаления, ВидСчетаФактуры");
		Если РеквизитыСчетаФактуры.ПометкаУдаления <> ПометкаУдаления
		 Или РеквизитыСчетаФактуры.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.НаПоступление
			И РеквизитыСчетаФактуры.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Возврат ДокументОснование;
		
	КонецЕсли;
	
	НайденныеСФ = НайтиПодчиненныеСчетаФактурыПолученные(ДокументОснование, ИсключаемыйСФ, ПометкаУдаления, СтруктураОтбора);

	Возврат НайденныеСФ[ДокументОснование];

КонецФункции

// Функция производит поиск счета-фактуры полученного на аванс с указанным документом-основанием
//
// Параметры:
//  ДокументСсылка  - ссылка на документ, для которого надо найти подчиненный документ,
//  Отбор 			- структура с именами и значениями реквизитов СФ для дополнительного отбора
//
// Возвращаемое значение:
//  Если нашли, то возвращаем ссылку, не нашли - Неопределено
//
Функция НайтиПодчиненныйСчетФактуруПолученныйНаАванс(ДокументОснование, ИсключаемыйСФ = Неопределено, Контрагент = Неопределено, ДоговорКонтрагента = Неопределено, НомерИсправления = 0) Экспорт
	Перем СчетФактура;
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.СчетФактураПолученный) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыСчетовФактур",   Перечисления.ВидСчетаФактурыПолученного.СчетаФактурыНаАванс());
	Запрос.УстановитьПараметр("ДокументОснование",  ДокументОснование);
	Запрос.УстановитьПараметр("ИсключаемыйСФ",      ИсключаемыйСФ);
	Запрос.УстановитьПараметр("Контрагент",         Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("НомерИсправления",   НомерИсправления);

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СчетФактура.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактура
	|ГДЕ
	|	СчетФактура.ДокументОснование = &ДокументОснование
	|	И НЕ СчетФактура.ПометкаУдаления
	|	И СчетФактура.ВидСчетаФактуры В(&ВидыСчетовФактур)
	|	И СчетФактура.НомерИсправления = &НомерИсправления
	|	И НЕ СчетФактура.ИсправлениеСобственнойОшибки";
	
	Если ЗначениеЗаполнено(ИсключаемыйСФ) Тогда
		Запрос.Текст = Запрос.Текст 
		+ " И СчетФактура.Ссылка <> &ИсключаемыйСФ"
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Запрос.Текст = Запрос.Текст 
		+ " И СчетФактура.Контрагент = &Контрагент"
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Запрос.Текст = Запрос.Текст 
		+ " И СчетФактура.ДоговорКонтрагента = &ДоговорКонтрагента"
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СчетФактура = Выборка.СчетФактура;
	КонецЕсли;
	
	Возврат СчетФактура;

КонецФункции

// Функция производит поиск счета-фактуры выданного на реализацию или корректировочного с указанным документом-основанием.
//
// Параметры:
//  ДокументОснование	- ДокументСсылка.* - Ссылка на документ, для которого надо найти счет-фактурую.
//  ИсключаемыйСФ		- ДокументСсылка.СчетФактураВыданный - Ссылка на счет-фактуру, исключаемый при поиске.
//  ПометкаУдаления		- Булево - Значение пометки счета-фактуры для отбора при поиске.
//	СтруктураОтбора     - Структура - Дополнительный отбор счетов-фактур.
//
// Возвращаемое значение:
//  ДокументСсылка.СчетФактураВыданный - Если нашли, то возвращаем ссылку, не нашли - Неопределено.
//
Функция НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(ДокументОснование, ИсключаемыйСФ = Неопределено, ПометкаУдаления = Ложь, СтруктураОтбора = Неопределено) Экспорт
	
	НайденныеСФ	= НайтиПодчиненныеСчетаФактурыВыданныеНаРеализацию(ДокументОснование, ИсключаемыйСФ, ПометкаУдаления, СтруктураОтбора);

	Возврат НайденныеСФ[ДокументОснование];

КонецФункции

// Функция производит поиск счета-фактуры выданного на аванс с указанным документом-основанием
//
// Параметры:
//  ДокументОснование	- ссылка на документ, для которого надо найти счет-фактуру
//  ИсключаемыйСФ		- ссылка на счет-фактуру, исключаемый при поиске
//  ДоговорКонтрагента  - договор счета-фактуры для отбора при поиске
//  НомерИсправления    - номер исправления счета-фактуры  для отбора при поиске
//
// Возвращаемое значение:
//  Если нашли, то возвращаем ссылку, не нашли - Неопределено
//
Функция НайтиПодчиненныйСчетФактуруВыданныйНаАванс(ДокументОснование, ИсключаемыйСФ = Неопределено, Контрагент = Неопределено, ДоговорКонтрагента = Неопределено, НомерИсправления = 0) Экспорт
	Перем СчетФактура;
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.СчетФактураВыданный) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыСчетовФактур",   Перечисления.ВидСчетаФактурыВыставленного.СчетаФактурыНаАванс());
	Запрос.УстановитьПараметр("ДокументОснование",  ДокументОснование);
	Запрос.УстановитьПараметр("ИсключаемыйСФ",      ИсключаемыйСФ);
	Запрос.УстановитьПараметр("Контрагент",         Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("НомерИсправления",   НомерИсправления);

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СчетФактура.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактура
	|ГДЕ
	|	СчетФактура.ДокументОснование = &ДокументОснование
	|	И НЕ СчетФактура.ПометкаУдаления
	|	И СчетФактура.ВидСчетаФактуры В (&ВидыСчетовФактур)
	|	И СчетФактура.НомерИсправления = &НомерИсправления";
	
	Если ЗначениеЗаполнено(ИсключаемыйСФ) Тогда
		Запрос.Текст = Запрос.Текст 
		+ " И СчетФактура.Ссылка <> &ИсключаемыйСФ"
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Запрос.Текст = Запрос.Текст 
		+ " И СчетФактура.Контрагент = &Контрагент"
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Запрос.Текст = Запрос.Текст 
		+ " И СчетФактура.ДоговорКонтрагента = &ДоговорКонтрагента"
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СчетФактура = Выборка.СчетФактура;
	КонецЕсли;
	
	Возврат СчетФактура;

КонецФункции

// Функция производит поиск счета-фактуры выданного налогового агента с указанным документом-основанием
//
// Параметры:
//  ДокументОснование	- ссылка на документ, для которого надо найти счет-фактуру
//  ИсключаемыйСФ		- ссылка на счет-фактуру, исключаемый при поиске
//  ДоговорКонтрагента  - договор счета-фактуры для отбора при поиске
//  НомерИсправления    - номер исправления счета-фактуры для отбора при поиске
//
// Возвращаемое значение:
//  Если нашли, то возвращаем ссылку, не нашли - Неопределено
//
Функция НайтиПодчиненныйСчетФактуруВыданныйНалоговыйАгент(ДокументОснование, ИсключаемыйСФ = Неопределено, ДоговорКонтрагента = Неопределено, НомерИсправления = 0) Экспорт
	Перем СчетФактура;
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.СчетФактураВыданный) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидСчетаФактуры",    Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент);
	Запрос.УстановитьПараметр("ДокументОснование",  ДокументОснование);
	Запрос.УстановитьПараметр("ИсключаемыйСФ",      ИсключаемыйСФ);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("НомерИсправления",   НомерИсправления);

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СчетФактура.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактура
	|ГДЕ
	|	СчетФактура.ДокументОснование = &ДокументОснование
	|	И СчетФактура.ВидСчетаФактуры = &ВидСчетаФактуры
	|	И НЕ СчетФактура.ПометкаУдаления
	|	И СчетФактура.НомерИсправления = &НомерИсправления";
	
	Если ЗначениеЗаполнено(ИсключаемыйСФ) Тогда
		Запрос.Текст = Запрос.Текст 
		+ " И СчетФактура.Ссылка <> &ИсключаемыйСФ"
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Запрос.Текст = Запрос.Текст 
		+ " И СчетФактура.ДоговорКонтрагента = &ДоговорКонтрагента"
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СчетФактура = Выборка.СчетФактура;
	КонецЕсли;
	
	Возврат СчетФактура;

КонецФункции

// Функция производит поиск счета-фактуры выданного на суммовую разницу с указанным документом-основанием
// Параметры:
//  ДокументОснование   - ссылка на документ, для которого надо найти счет-фактуру
//  ИсключаемыйСФ		- ссылка на счет-фактуру, исключаемый при поиске
//  ДоговорКонтрагента  - договор счета-фактуры для отбора при поиске
//
// Возвращаемое значение:
//  Если нашли, то возвращаем ссылку, не нашли - Неопределено
//
Функция НайтиПодчиненныйСчетФактуруВыданныйНаСуммовуюРазницу(ДокументОснование, ИсключаемыйСФ = Неопределено, ДоговорКонтрагента = Неопределено) Экспорт
	Перем СчетФактура;
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.СчетФактураВыданный) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидСчетаФактуры",    Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу);
	Запрос.УстановитьПараметр("ДокументОснование",  ДокументОснование);
	Запрос.УстановитьПараметр("ИсключаемыйСФ",      ИсключаемыйСФ);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СчетФактура.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактура
	|ГДЕ
	|	СчетФактура.ДокументОснование = &ДокументОснование
	|	И СчетФактура.ВидСчетаФактуры = &ВидСчетаФактуры
	|	И НЕ СчетФактура.ПометкаУдаления";
	
	Если ЗначениеЗаполнено(ИсключаемыйСФ) Тогда
		Запрос.Текст = Запрос.Текст 
		+ " И СчетФактура.Ссылка <> &ИсключаемыйСФ"
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Запрос.Текст = Запрос.Текст 
		+ " И СчетФактура.ДоговорКонтрагента = &ДоговорКонтрагента"
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СчетФактура = Выборка.СчетФактура;
	КонецЕсли;
	
	Возврат СчетФактура;

КонецФункции

// Функция производит поиск счета-фактуры полученного по заданному основанию, номеру и дате
// и дополняет его переданным документом-основанием
Функция ДобавитьОснованиеВСчетФактуруПолученный(Основание, НомерСчетаФактурыПолученного, ДатаСчетаФактурыПолученного,
	ОбновлятьСтатусСчетаФактурыПоДокументу = Истина, ОригиналСчетаФактуры = Неопределено, ВидСчетаФактуры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СчетФактураПолученный.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.ПометкаУдаления = ЛОЖЬ
	|	И СчетФактураПолученный.ВидСчетаФактуры = &ВидСчетаФактуры
	|	И СчетФактураПолученный.Контрагент = &Контрагент
	|	И СчетФактураПолученный.Организация = &Организация
	|	И СчетФактураПолученный.НомерВходящегоДокумента = &НомерСчетаФактурыПолученного
	|	И СчетФактураПолученный.ДатаВходящегоДокумента = &ДатаСчетаФактурыПолученного
	|	И НЕ СчетФактураПолученный.Исправление
	|	И НЕ СчетФактураПолученный.ИсправлениеСобственнойОшибки";
	
	
	Запрос.УстановитьПараметр("ВидСчетаФактуры",
		?(ВидСчетаФактуры = Неопределено, Перечисления.ВидСчетаФактурыПолученного.НаПоступление, ВидСчетаФактуры));
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание,
		"Контрагент,Организация,Проведен");
	
	Запрос.УстановитьПараметр("Контрагент", РеквизитыОснования.Контрагент);
	Запрос.УстановитьПараметр("Организация", РеквизитыОснования.Организация);
	Запрос.УстановитьПараметр("НомерСчетаФактурыПолученного", НомерСчетаФактурыПолученного);
	Запрос.УстановитьПараметр("ДатаСчетаФактурыПолученного", ДатаСчетаФактурыПолученного);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() > 0 Тогда
		Выборка.Следующий();
		
		СчетФактура = Выборка.СчетФактура.ПолучитьОбъект();
		НоваяСтрока = СчетФактура.ДокументыОснования.Добавить();
		НоваяСтрока.ДокументОснование = Основание;
		
		ПараметрыСчетаФактуры = ПараметрыСчетаФактуры(СчетФактура);
		ЗаполнитьЗначенияСвойств(СчетФактура, ПараметрыСчетаФактуры);
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Дата",                     СчетФактура.Дата);
		СтруктураПараметров.Вставить("ВидСчетаФактуры",          СчетФактура.ВидСчетаФактуры);
		СтруктураПараметров.Вставить("Исправление",              СчетФактура.Исправление);
		СтруктураПараметров.Вставить("КодВидаОперацииОснования", "");
		СтруктураПараметров.Вставить("ДокументыОснования",       СчетФактура.ДокументыОснования);
		СтруктураПараметров.Вставить("НДСПоСтавкам4и2",          Ложь);
		
		Если ЗначениеЗаполнено(СчетФактура.ДоговорКонтрагента) Тогда
			СвойстваДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				СчетФактура.ДоговорКонтрагента, "УчетАгентскогоНДС, ВидАгентскогоДоговора");
			СтруктураПараметров.Вставить("НДСИсчисляетсяНалоговымАгентом",
				СвойстваДоговора.УчетАгентскогоНДС = Истина
				И СвойстваДоговора.ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.РеализацияТоваров);
		КонецЕсли; 
		
		СчетФактура.КодВидаОперации = Документы.СчетФактураПолученный.ПолучитьКодВидаОперации(СтруктураПараметров);
		
		РежимЗаписи = ?(РеквизитыОснования.Проведен,
			РежимЗаписиДокумента.Проведение,
			РежимЗаписиДокумента.Запись);
			
		Если НЕ ОбновлятьСтатусСчетаФактурыПоДокументу Тогда 
			// Передаем документ-основание по которому уже установлен статус счета-фактуры для того, 
			// чтобы счет-фактура не актуализировал статус повторно.
			СчетФактура.ДополнительныеСвойства.Вставить("ДокументСУстановленнымСтатусом", Основание)
		КонецЕсли;
	
		Если ЗначениеЗаполнено(ОригиналСчетаФактуры) Тогда 
			СтатусДокумента = ?(ОригиналСчетаФактуры, 
				Перечисления.СтатусыДокументовПоступления.ОригиналПолучен, 
				Перечисления.СтатусыДокументовПоступления.ОригиналНеПолучен);
			СчетФактура.ДополнительныеСвойства.Вставить("СтатусДокумента", СтатусДокумента);
		КонецЕсли;
		
		СчетФактура.Записать(РежимЗаписи);
		
		Возврат Выборка.СчетФактура;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Функция производит поиск исправленного и/или корректировочного счета-фактуры полученного по заданному основанию, номеру и дате
// и дополняет его переданным документом-основанием
Функция ДобавитьОснованиеВИсправленныйСчетФактуруПолученный(
	Основание, НомерСчетаФактурыПолученного, ДатаСчетаФактурыПолученного, Исправление, ВидСчетаФактуры) Экспорт
	
	СтруктураПараметровЗапроса = Новый Структура(
		"НомерСчетаФактурыПолученного,ДатаСчетаФактурыПолученного,
		|ВидСчетаФактуры,Исправление,ДатаИсправления,НомерИсправления");
	
	СтруктураПараметровЗапроса.Исправление = Исправление;
	СтруктураПараметровЗапроса.ВидСчетаФактуры = ?(ВидСчетаФактуры = Неопределено,
		Перечисления.ВидСчетаФактурыПолученного.НаПоступление,
		ВидСчетаФактуры);
		
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание,
		"ДокументПоступления,Контрагент,Организация,Проведен");

	Если Исправление Тогда
	
		Если ТипЗнч(РеквизитыОснования.ДокументПоступления) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			ИсходныйСчетФактура = РеквизитыОснования.ДокументПоступления;
		// Необходимо найти исходный счет-фактуру	
		Иначе
			ИсходныйСчетФактура = НайтиПодчиненныйСчетФактуруПолученный(РеквизитыОснования.ДокументПоступления);
		КонецЕсли;
		
		Если ИсходныйСчетФактура <> Неопределено Тогда
			
			РеквизитыСчетаФактуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсходныйСчетФактура, 
				"НомерВходящегоДокумента, ДатаВходящегоДокумента");
			
			СтруктураПараметровЗапроса.НомерСчетаФактурыПолученного = РеквизитыСчетаФактуры.НомерВходящегоДокумента;
			СтруктураПараметровЗапроса.ДатаСчетаФактурыПолученного  = РеквизитыСчетаФактуры.ДатаВходящегоДокумента;
			СтруктураПараметровЗапроса.ДатаИсправления              = ДатаСчетаФактурыПолученного;
			СтруктураПараметровЗапроса.НомерИсправления             = Число(НомерСчетаФактурыПолученного);
			
		Иначе
			Возврат Неопределено;
		КонецЕсли; 
		
	Иначе
			СтруктураПараметровЗапроса.НомерСчетаФактурыПолученного = НомерСчетаФактурыПолученного;
			СтруктураПараметровЗапроса.ДатаСчетаФактурыПолученного  = ДатаСчетаФактурыПолученного;
			СтруктураПараметровЗапроса.ДатаИсправления              = '00010101';
			СтруктураПараметровЗапроса.НомерИсправления             = 0;
			
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СчетФактураПолученный.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.ПометкаУдаления = ЛОЖЬ
	|	И СчетФактураПолученный.ВидСчетаФактуры = &ВидСчетаФактуры
	|	И СчетФактураПолученный.Контрагент = &Контрагент
	|	И СчетФактураПолученный.Организация = &Организация
	|	И СчетФактураПолученный.НомерВходящегоДокумента = &НомерСчетаФактурыПолученного
	|	И СчетФактураПолученный.ДатаВходящегоДокумента = &ДатаСчетаФактурыПолученного
	|	И СчетФактураПолученный.Исправление = &Исправление
	|	И ВЫБОР
	|			КОГДА &Исправление
	|				ТОГДА СчетФактураПолученный.НомерИсправления = &НомерИсправления
	|						И СчетФактураПолученный.ДатаИсправления = &ДатаИсправления
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И НЕ СчетФактураПолученный.ИсправлениеСобственнойОшибки";
	
	Запрос.УстановитьПараметр("Контрагент",                   РеквизитыОснования.Контрагент);
	Запрос.УстановитьПараметр("Организация",                  РеквизитыОснования.Организация);
	Запрос.УстановитьПараметр("НомерСчетаФактурыПолученного", СтруктураПараметровЗапроса.НомерСчетаФактурыПолученного);
	Запрос.УстановитьПараметр("ДатаСчетаФактурыПолученного",  СтруктураПараметровЗапроса.ДатаСчетаФактурыПолученного);
	Запрос.УстановитьПараметр("Исправление",                  СтруктураПараметровЗапроса.Исправление);
	Запрос.УстановитьПараметр("НомерИсправления",             СтруктураПараметровЗапроса.НомерИсправления);
	Запрос.УстановитьПараметр("ДатаИсправления",              СтруктураПараметровЗапроса.ДатаИсправления);
	Запрос.УстановитьПараметр("ВидСчетаФактуры",              СтруктураПараметровЗапроса.ВидСчетаФактуры);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() > 0 Тогда
		
		Выборка.Следующий();
		
		СчетФактура = Выборка.СчетФактура.ПолучитьОбъект();
		НоваяСтрока = СчетФактура.ДокументыОснования.Добавить();
		НоваяСтрока.ДокументОснование = Основание;
		
		ПараметрыСчетаФактуры = ПараметрыСчетаФактуры(СчетФактура);
		ЗаполнитьЗначенияСвойств(СчетФактура, ПараметрыСчетаФактуры);
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Дата",                     СчетФактура.Дата);
		СтруктураПараметров.Вставить("ВидСчетаФактуры",          СчетФактура.ВидСчетаФактуры);
		СтруктураПараметров.Вставить("Исправление",              СчетФактура.Исправление);
		СтруктураПараметров.Вставить("КодВидаОперацииОснования", "");
		СтруктураПараметров.Вставить("ДокументыОснования",       СчетФактура.ДокументыОснования);
		СтруктураПараметров.Вставить("НДСПоСтавкам4и2",          Ложь);
		
		ТаблицаОснований = ПараметрыСчетаФактуры.РеквизитыОснований;
		ТаблицаОснований.Свернуть("НомерИсходногоДокумента, ДатаИсходногоДокумента");
		
		Если ТаблицаОснований.Количество() > 1 Тогда
			СводныйКорректировочный = Истина;
		Иначе
			СводныйКорректировочный = Ложь;
		КонецЕсли;
		
		Если СводныйКорректировочный 
		   И УчетНДСКлиентСервер.ВерсияКодовВидовОпераций(ДатаСчетаФактурыПолученного) >= 3 Тогда
			// Получение единого корректировочного счета-фактуры
			СчетФактура.КодВидаОперации             = "01";
			СчетФактура.КодВидаОперацииНаУменьшение = "01";
		Иначе
			СчетФактура.КодВидаОперации = Документы.СчетФактураПолученный.ПолучитьКодВидаОперации(СтруктураПараметров);
		КонецЕсли;
		
		РежимЗаписи = ?(РеквизитыОснования.Проведен,
			РежимЗаписиДокумента.Проведение,
			РежимЗаписиДокумента.Запись);
		
		СчетФактура.Записать(РежимЗаписи);
		
		Возврат Выборка.СчетФактура;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Функция определяет наличие счетов-фактур, полученных по документам,
// которые могут являться основаниями для ввода счетов-фактур полученных.
//
// Применяется для контроля за наличием в ИБ информации о наличии счетов-фактур.
//
// Параметры:
//  ПараметрыПоискаСчетовФактур - Структура - параметры поиска счетов-фактур.
//                                См. конструктор параметров УчетНДС.НовыйПараметрыПоискаСчетовФактур().
//   * НачалоПериода - Дата - Начальная дата выборки (включительно, с 00:00:00)
//   * КонецПериода  - Дата - Конечная дата выборки (включительно, по 23:59:59)
//   * Организация   - массив - организации, по которым осуществляется поиск. 
//                     Если организация не задана, то поиск ведется по всем организациям.
//   * Фильтр        - Документ.Ссылка, Массив, Неопределено - Документ или список документов,
//                     по которым осуществляется поиск.
//                     Если не задан, поиск осуществляется по всем документам,
//                     которые могут являться основаниями для ввода счетов-фактур
//                     полученных.
//
//   * НаличиеСчетаФактуры - Булево, Неопределено - Признак отбора документов:
//                           Истина - искать документы, по которым существуют счета-фактуры.
//                           Ложь - искать документы, по которым счета-фактуры отсутствуют.
//                           Неопределено - поиск осуществляется для всех документов.
//   * СчетФактураПроведен - Булево, Неопределено - Признак отбора счетов-фактур:
//                           Истина - искать проведенные счета-фактуры.
//                           Ложь - искать непроведенные счета-фактуры.
//                           Неопределено - осуществляется поиск всех счетов-фактур.
//   * ДатаСФНеБолее    - Дата, Неопределено - крайняя дата поиска счетов-фактур.
//   * ИскатьПоОборотам - Булево - Истина - поиск осуществляется по оборотам регистра накопления НДСПредъявленный,
//                                 Ложь - поиск осуществляется по остаткам.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица значений - Состав колонок:
//   * Документ - ДокументСсылка - Документ, по которому производился поиск счета-фактуры полученного.
//   * СчетФактура - ДокументСсылка, Неопределено - Ссылка на счет-фактуру полученный.
//
Функция ОпределитьНаличиеСчетовФактурПолученных(ПараметрыПоискаСчетовФактур) Экспорт
	
	ЭтоОтчет            = ПараметрыПоискаСчетовФактур.ИскатьПоОборотам;
	Организация         = ПараметрыПоискаСчетовФактур.Организация;
	Фильтр              = ПараметрыПоискаСчетовФактур.Фильтр;
	НаличиеСчетаФактуры = ПараметрыПоискаСчетовФактур.НаличиеСчетаФактуры;
	УчитыватьОригиналы  = ПараметрыПоискаСчетовФактур.УчитыватьОригиналы;
	
	Если ЗначениеЗаполнено(ПараметрыПоискаСчетовФактур.НачалоПериода) Тогда
		НачПериода = ПараметрыПоискаСчетовФактур.НачалоПериода;
	Иначе
		НачПериода = ОбщегоНазначенияБПСобытия.КорректныйПериодВводаДокументов().НачалоКорректногоПериода;
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыПоискаСчетовФактур.КонецПериода) Тогда
		КонПериода = ПараметрыПоискаСчетовФактур.КонецПериода;
		НачалоНалоговогоПериода = НачалоКвартала(КонПериода);
	Иначе
		КонПериода = ОбщегоНазначенияБПСобытия.КорректныйПериодВводаДокументов().КонецКорректногоПериода;
		НачалоНалоговогоПериода = '00010101';
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыПоискаСчетовФактур.ДатаСФНеБолее) Тогда
		ДатаСФНеБолее     = КонецДня(ПараметрыПоискаСчетовФактур.ДатаСФНеБолее);
		КонПериодаГраница = Новый Граница(ДатаСФНеБолее, ВидГраницы.Включая);
	Иначе
		ДатаСФНеБолее     = Неопределено;
		КонПериодаГраница = Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",             Организация);
	Запрос.УстановитьПараметр("НачПериода",              НачПериода);
	Запрос.УстановитьПараметр("КонПериода",              КонПериода);
	Запрос.УстановитьПараметр("КонПериодаГраница",       КонПериодаГраница);
	Запрос.УстановитьПараметр("ДатаСФНеБолее",           ДатаСФНеБолее);
	Запрос.УстановитьПараметр("НачалоНалоговогоПериода", НачалоНалоговогоПериода);
	Запрос.УстановитьПараметр("ЭтоОтчет",                ЭтоОтчет);
	Запрос.УстановитьПараметр("УчитыватьОригиналы",      УчитыватьОригиналы);
	
	// Получаем список счетов-фактур.
	
	ТекстЗапроса = "";
	ПереданМенеджерТаблиц = (ТипЗнч(Фильтр) = Тип("МенеджерВременныхТаблиц"));
	Если Не ПереданМенеджерТаблиц Тогда
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	КонецЕсли;
	Если ПереданМенеджерТаблиц Тогда // берём из готовой временной таблицы
		
		Запрос.МенеджерВременныхТаблиц = Фильтр;
		
		Если ЭтоОтчет Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	СписокСчетовФактур.СчетФактура КАК СчетФактура
			|ПОМЕСТИТЬ СписокСчетовФактурОтбор
			|ИЗ
			|	СписокСчетовФактур КАК СписокСчетовФактур
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
			|		ПО СписокСчетовФактур.СчетФактура = ВозвратТоваровОтПокупателя.Ссылка
			|			И (ВозвратТоваровОтПокупателя.Организация В (&Организация))
			|			И (ВозвратТоваровОтПокупателя.Сделка ССЫЛКА Документ.ОтчетОРозничныхПродажах)
			|ГДЕ
			|	ВозвратТоваровОтПокупателя.Ссылка ЕСТЬ NULL 
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	СчетФактура";
		Иначе
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	СписокСчетовФактур.СчетФактура КАК СчетФактура
			|ПОМЕСТИТЬ СписокСчетовФактурОтбор
			|ИЗ
			|	СписокСчетовФактур КАК СписокСчетовФактур
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	СчетФактура";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета()
			+ "УНИЧТОЖИТЬ СписокСчетовФактур";
			
	ИначеЕсли ЗначениеЗаполнено(Фильтр) Тогда // создаём временную таблицу
		
		Запрос.УстановитьПараметр("Фильтр", Фильтр);
		
		Если ЭтоОтчет Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	НДСПредъявленный.СчетФактура КАК СчетФактура
			|ПОМЕСТИТЬ СписокСчетовФактурОтбор
			|ИЗ
			|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
			|		ПО НДСПредъявленный.СчетФактура = ВозвратТоваровОтПокупателя.Ссылка
			|			И (ВозвратТоваровОтПокупателя.Организация В (&Организация))
			|			И (ВозвратТоваровОтПокупателя.Сделка ССЫЛКА Документ.ОтчетОРозничныхПродажах)
			|ГДЕ
			|	НДСПредъявленный.СчетФактура В(&Фильтр)
			|	И ВозвратТоваровОтПокупателя.Ссылка ЕСТЬ NULL 
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	СчетФактура";
		Иначе
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	НДСПредъявленный.СчетФактура КАК СчетФактура
			|ПОМЕСТИТЬ СписокСчетовФактурОтбор
			|ИЗ
			|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
			|ГДЕ
			|	НДСПредъявленный.СчетФактура В(&Фильтр)
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	СчетФактура";
		КонецЕсли;
		
	КонецЕсли;
	Если Не ПустаяСтрока(ТекстЗапроса) Тогда
		Запрос.Текст = ТекстЗапроса;
		Запрос.Выполнить();
	КонецЕсли;
	
	// Получаем данные счетов-фактур.
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НДСПредъявленныйОбороты.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйОбороты.Поставщик КАК Контрагент,
	|	НДСПредъявленныйОбороты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	НДСПредъявленныйОбороты.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОбороты.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|				ИЛИ НДСПредъявленныйОбороты.СчетФактура ССЫЛКА Документ.ГТДИмпорт
	|				ИЛИ НДСПредъявленныйОбороты.СчетФактура ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОсобыйВариантСчетаФактуры,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОбороты.СчетФактура ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоОтчетКомиссионераОПродажах
	|ПОМЕСТИТЬ ВТ_ДанныеСчетовФактур
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Обороты(
	|			,
	|			&КонПериода,
	|			Период,
	|			Организация В (&Организация)
	|				И &СписокСчетовФактурОтбор
	|				И &ОтборПоВидуЦенности) КАК НДСПредъявленныйОбороты
	|ГДЕ
	|	НЕ НДСПредъявленныйОбороты.СчетФактура ССЫЛКА Документ.КорректировкаРеализации
	|	И НЕ(НДСПредъявленныйОбороты.СчетФактура ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И НДСПредъявленныйОбороты.ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|				И НДСПредъявленныйОбороты.ДоговорКонтрагента.УчетАгентскогоНДС
	|				И НДСПредъявленныйОбороты.ДоговорКонтрагента.ВидАгентскогоДоговора <> ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.РеализацияТоваров))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ИсправленныйСчетФактура,
	|	ЛОЖЬ,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачПериода,
	|			&КонПериода,
	|			Период,
	|			Организация В (&Организация)
	|				И СчетФактура ССЫЛКА Документ.КорректировкаПоступления
	|				И &СписокСчетовФактурОтбор
	|				И &ОтборПоВидуЦенности) КАК НДСЗаписиКнигиПродажОбороты
	|ГДЕ
	|	&ЭтоОтчет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеСчетовФактур.ИсправленныйСчетФактура = НЕОПРЕДЕЛЕНО
	|			ТОГДА ВТ_ДанныеСчетовФактур.СчетФактура
	|		ИНАЧЕ ВТ_ДанныеСчетовФактур.ИсправленныйСчетФактура
	|	КОНЕЦ КАК СчетФактура,
	|	ВТ_ДанныеСчетовФактур.Контрагент КАК Контрагент,
	|	ВТ_ДанныеСчетовФактур.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_ДанныеСчетовФактур.ОсобыйВариантСчетаФактуры КАК ОсобыйВариантСчетаФактуры,
	|	ВТ_ДанныеСчетовФактур.ЭтоОтчетКомиссионераОПродажах КАК ЭтоОтчетКомиссионераОПродажах,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, &НачПериода) КАК ДатаРегистратора,
	|	ВТ_ДанныеСчетовФактур.СчетФактура КАК СчетФактураДокумент
	|ПОМЕСТИТЬ ВременнаяТаблицаОстатки
	|ИЗ
	|	ВТ_ДанныеСчетовФактур КАК ВТ_ДанныеСчетовФактур
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ВТ_ДанныеСчетовФактур.СчетФактура = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	&ОтборПоПериоду
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ДанныеСчетовФактур";
	
	Если НЕ ЭтоОтчет Тогда // используем остатки для заполнения документа
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"НДСПредъявленный.Обороты(
		|			,
		|			&КонПериода,
		|			Период",
		"НДСПредъявленный.Остатки(
		|			&КонПериодаГраница");
	КонецЕсли;
	
	// Задаём условие отбора по организации.
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Организация В (&Организация)", "ИСТИНА");
	КонецЕсли;
	
	// Задаём условие отбора по списку документов-оснований.
	Если ПереданМенеджерТаблиц
	 Или ЗначениеЗаполнено(Фильтр) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СписокСчетовФактурОтбор",
			"СчетФактура В
			|						(ВЫБРАТЬ
			|							СписокСчетовФактурОтбор.СчетФактура
			|						ИЗ
			|							СписокСчетовФактурОтбор КАК СписокСчетовФактурОтбор)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &СписокСчетовФактурОтбор", "");
	КонецЕсли;
	
	// Задаём условие отбора по виду ценности.
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ОтборПоВидуЦенности",
		?(ЭтоОтчет, 
		"И ВидЦенности НЕ В(ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныйСоюз), ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ЭлектронныеУслуги))", ""));
		
	// Задаём условие отбора по периоду.
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоПериоду",
		"ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, &НачПериода) МЕЖДУ &НачПериода И "
			+ ?(ЗначениеЗаполнено(ДатаСФНеБолее), "&ДатаСФНеБолее", "&КонПериода"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоПериоду", "ИСТИНА");
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	// Готовим таблицу с данными заявлений о ввозе на территорию Таможенного союза.
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДатыОплатВКнигеПокупок.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ ОплаченныеЗаявленияОВвозе
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК ДатыОплатВКнигеПокупок
	|ГДЕ
	|	ДатыОплатВКнигеПокупок.Организация В (&Организация)
	|	И ДатыОплатВКнигеПокупок.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныйСоюз)
	|	И ДатыОплатВКнигеПокупок.Период <= &КонПериода
	|	И ДатыОплатВКнигеПокупок.ДатаДокументаОплаты <= &КонПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура";
	
	// Задаём условие отбора по организации.
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДатыОплатВКнигеПокупок.Организация В (&Организация)", "ИСТИНА");
	КонецЕсли;
		
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаОстатки.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактурыПредварительная
	|ИЗ
	|	ВременнаяТаблицаОстатки КАК ВременнаяТаблицаОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОстатки.СчетФактураДокумент
	|ИЗ
	|	ВременнаяТаблицаОстатки КАК ВременнаяТаблицаОстатки
	|ГДЕ
	|	ВременнаяТаблицаОстатки.СчетФактура <> ВременнаяТаблицаОстатки.СчетФактураДокумент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВременнаяТаблицаСчетФактурыПредварительная.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактуры
	|ИЗ
	|	ВременнаяТаблицаСчетФактурыПредварительная КАК ВременнаяТаблицаСчетФактурыПредварительная
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураПолученный.Ссылка КАК СчетФактура,
	|	СчетФактураПолученный.Ссылка КАК ССылка,
	|	СчетФактураПолученный.ДокументОснование КАК ДокументОснование,
	|	СчетФактураПолученный.Ссылка.Проведен КАК СчетФактураПроведен,
	|	ВЫБОР
	|		КОГДА &ЭтоОтчет
	|			ТОГДА СчетФактураПолученный.Ссылка.ДатаВходящегоДокумента
	|		ИНАЧЕ СчетФактураПолученный.Ссылка.Дата
	|	КОНЕЦ КАК СчетФактураДата,
	|	ВЫБОР
	|		КОГДА &ЭтоОтчет
	|			ТОГДА СчетФактураПолученный.Ссылка.НомерВходящегоДокумента
	|		ИНАЧЕ СчетФактураПолученный.Ссылка.Номер
	|	КОНЕЦ КАК СчетФактураНомер,
	|	СчетФактураПолученный.Ссылка.КодВидаОперации КАК КодВидаОперации,
	|	ИСТИНА КАК ТребуетсяСчетФактура,
	|	СчетФактураПолученный.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	СчетФактураПолученный.Ссылка.ВалютаДокумента КАК ВалютаДокумента
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураПолученный
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактуры КАК ВременнаяТаблицаСчетФактуры
	|		ПО СчетФактураПолученный.Ссылка = ВременнаяТаблицаСчетФактуры.СчетФактура
	|ГДЕ
	|	НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураПолученный.ДокументОснование,
	|	СчетФактураПолученный.Ссылка,
	|	СчетФактураПолученный.ДокументОснование,
	|	СчетФактураПолученный.Ссылка.Проведен,
	|	ВЫБОР
	|		КОГДА &ЭтоОтчет
	|			ТОГДА СчетФактураПолученный.Ссылка.ДатаВходящегоДокумента
	|		ИНАЧЕ СчетФактураПолученный.Ссылка.Дата
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ЭтоОтчет
	|			ТОГДА СчетФактураПолученный.Ссылка.НомерВходящегоДокумента
	|		ИНАЧЕ СчетФактураПолученный.Ссылка.Номер
	|	КОНЕЦ,
	|	СчетФактураПолученный.Ссылка.КодВидаОперации,
	|	ИСТИНА,
	|	СчетФактураПолученный.Ссылка.СуммаДокумента,
	|	СчетФактураПолученный.Ссылка.ВалютаДокумента
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактуры КАК ВременнаяТаблицаСчетФактуры
	|		ПО СчетФактураПолученный.ДокументОснование = ВременнаяТаблицаСчетФактуры.СчетФактура
	|ГДЕ
	|	НЕ СчетФактураПолученный.Ссылка.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	ВозвратТоваровОтПокупателя.Проведен,
	|	ВозвратТоваровОтПокупателя.Дата,
	|	ВозвратТоваровОтПокупателя.Номер,
	|	"""",
	|	ЛОЖЬ,
	|	ВозвратТоваровОтПокупателя.СуммаДокумента,
	|	ВозвратТоваровОтПокупателя.ВалютаДокумента
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактуры КАК ВременнаяТаблицаСчетФактуры
	|		ПО ВозвратТоваровОтПокупателя.Ссылка = ВременнаяТаблицаСчетФактуры.СчетФактура
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Сделка ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|	И ВЫБОР
	|			КОГДА ВозвратТоваровОтПокупателя.Дата < ДАТАВРЕМЯ(2019, 1, 1)
	|				ТОГДА ВозвратТоваровОтПокупателя.НомерРасходногоКассовогоОрдера <> """"
	|						И ВозвратТоваровОтПокупателя.ДатаРасходногоКассовогоОрдера <> ДАТАВРЕМЯ(1, 1, 1)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаявлениеОВвозеТоваров.Ссылка,
	|	ЗаявлениеОВвозеТоваров.Ссылка,
	|	ЗаявлениеОВвозеТоваров.Ссылка,
	|	ЗаявлениеОВвозеТоваров.Проведен,
	|	ЗаявлениеОВвозеТоваров.Дата,
	|	ЗаявлениеОВвозеТоваров.Номер,
	|	""19"",
	|	ЛОЖЬ,
	|	ЗаявлениеОВвозеТоваров.СуммаДокумента,
	|	ЗаявлениеОВвозеТоваров.ВалютаДокумента
	|ИЗ
	|	Документ.ЗаявлениеОВвозеТоваров КАК ЗаявлениеОВвозеТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОплаченныеЗаявленияОВвозе КАК ОплаченныеЗаявленияОВвозе
	|		ПО ЗаявлениеОВвозеТоваров.Ссылка = ОплаченныеЗаявленияОВвозе.СчетФактура
	|ГДЕ
	|	НЕ ЗаявлениеОВвозеТоваров.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ГТДИмпорт.Ссылка,
	|	ГТДИмпорт.Ссылка,
	|	ГТДИмпорт.Ссылка,
	|	ГТДИмпорт.Проведен,
	|	ГТДИмпорт.Дата,
	|	ГТДИмпорт.НомерГТД,
	|	""20"",
	|	ЛОЖЬ,
	|	0,
	|	ГТДИмпорт.ВалютаДокумента
	|ИЗ
	|	Документ.ГТДИмпорт КАК ГТДИмпорт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактуры КАК ВременнаяТаблицаСчетФактуры
	|		ПО ГТДИмпорт.Ссылка = ВременнаяТаблицаСчетФактуры.СчетФактура
	|ГДЕ
	|	НЕ ГТДИмпорт.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка КАК СчетФактура,
	|	СчетФактураВыданный.Ссылка КАК Ссылка,
	|	СчетФактураВыданный.ДокументОснование КАК ДокументОснование,
	|	СчетФактураВыданный.Ссылка.ДокументОснование КАК СчетФактураДокументОснование,
	|	СчетФактураВыданный.Ссылка.Проведен КАК СчетФактураПроведен,
	|	СчетФактураВыданный.Ссылка.Дата КАК СчетФактураДата,
	|	СчетФактураВыданный.Ссылка.КодВидаОперации КАК КодВидаОперации,
	|	ИСТИНА КАК ТребуетсяСчетФактура,
	|	СчетФактураВыданный.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	СчетФактураВыданный.Ссылка.ВалютаДокумента КАК ВалютаДокумента
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетФактураВыданный
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактуры КАК ВременнаяТаблицаСчетФактуры
	|		ПО СчетФактураВыданный.Ссылка = ВременнаяТаблицаСчетФактуры.СчетФактура
	|ГДЕ
	|	СчетФактураВыданный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|	И (СчетФактураВыданный.Ссылка.Продавец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ИЛИ СчетФактураВыданный.Ссылка.Продавец = СчетФактураВыданный.Ссылка.Контрагент)
	|	И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.Проведен,
	|	СчетФактураВыданный.Ссылка.Дата,
	|	СчетФактураВыданный.Ссылка.КодВидаОперации,
	|	ИСТИНА,
	|	СчетФактураВыданный.Ссылка.СуммаДокумента,
	|	СчетФактураВыданный.Ссылка.ВалютаДокумента
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактуры КАК ВременнаяТаблицаСчетФактуры
	|		ПО СчетФактураВыданный.ДокументОснование = ВременнаяТаблицаСчетФактуры.СчетФактура
	|ГДЕ
	|	СчетФактураВыданный.Ссылка.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаРеализацию)
	|	И (СчетФактураВыданный.Ссылка.Продавец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ИЛИ СчетФактураВыданный.Ссылка.Продавец = СчетФактураВыданный.Ссылка.Контрагент)
	|	И НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураВыданный.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.ДокументОснование,
	|	СчетФактураВыданный.Ссылка.Проведен,
	|	СчетФактураВыданный.Ссылка.Дата,
	|	СчетФактураВыданный.Ссылка.КодВидаОперации,
	|	ИСТИНА,
	|	СчетФактураВыданный.Ссылка.СуммаДокумента,
	|	СчетФактураВыданный.Ссылка.ВалютаДокумента
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактуры КАК ВременнаяТаблицаСчетФактуры
	|		ПО СчетФактураВыданный.ДокументОснование = ВременнаяТаблицаСчетФактуры.СчетФактура
	|ГДЕ
	|	НЕ СчетФактураВыданный.Ссылка.ПометкаУдаления
	|	И СчетФактураВыданный.ДокументОснование ССЫЛКА Документ.ВозвратТоваровОтПокупателя
	|	И ВЫРАЗИТЬ(СчетФактураВыданный.ДокументОснование КАК Документ.ВозвратТоваровОтПокупателя).ПокупателюВыставляетсяКорректировочныйСчетФактура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура";
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	ТекстЗапроса = ТекстЗапроса + 	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НДСПредъявленныйОбороты.ДатаРегистратора КАК Дата,
	|	НДСПредъявленныйОбороты.Контрагент КАК Контрагент,
	|	НДСПредъявленныйОбороты.СчетФактураДокумент КАК Документ,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОбороты.СчетФактура = НДСПредъявленныйОбороты.СчетФактураДокумент
	|			ТОГДА ВЫБОР
	|					КОГДА НДСПредъявленныйОбороты.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
	|						ТОГДА ЕСТЬNULL(СчетФактураПолученный.ДокументОснование, НЕОПРЕДЕЛЕНО)
	|					КОГДА НДСПредъявленныйОбороты.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
	|						ТОГДА ЕСТЬNULL(СчетФактураВыданный.СчетФактураДокументОснование, НЕОПРЕДЕЛЕНО)
	|					ИНАЧЕ НДСПредъявленныйОбороты.СчетФактура
	|				КОНЕЦ
	|		ИНАЧЕ НДСПредъявленныйОбороты.СчетФактура
	|	КОНЕЦ КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОбороты.ОсобыйВариантСчетаФактуры
	|			ТОГДА НДСПредъявленныйОбороты.СчетФактураДокумент
	|		КОГДА СчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА СчетФактураПолученный.Ссылка
	|		КОГДА НЕ НДСПредъявленныйОбороты.ЭтоОтчетКомиссионераОПродажах
	|				И СчетФактураВыданный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА СчетФактураВыданный.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОбороты.ОсобыйВариантСчетаФактуры
	|			ТОГДА ИСТИНА
	|		КОГДА СчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА СчетФактураПолученный.СчетФактураПроведен
	|		КОГДА НЕ НДСПредъявленныйОбороты.ЭтоОтчетКомиссионераОПродажах
	|				И СчетФактураВыданный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА СчетФактураВыданный.СчетФактураПроведен
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетФактураПроведен,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОбороты.ОсобыйВариантСчетаФактуры
	|			ТОГДА НДСПредъявленныйОбороты.ДатаРегистратора
	|		КОГДА СчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА СчетФактураПолученный.СчетФактураДата
	|		КОГДА НЕ НДСПредъявленныйОбороты.ЭтоОтчетКомиссионераОПродажах
	|				И СчетФактураВыданный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА СчетФактураВыданный.СчетФактураДата
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетФактураДата,
	|	ЕСТЬNULL(СчетФактураПолученный.СчетФактураНомер, """") КАК СчетФактураНомер,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОбороты.ОсобыйВариантСчетаФактуры
	|			ТОГДА ЛОЖЬ
	|		КОГДА СчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ИСТИНА
	|		КОГДА НЕ НДСПредъявленныйОбороты.ЭтоОтчетКомиссионераОПродажах
	|				И СчетФактураВыданный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСчетФактураПолученный,
	|	ЕСТЬNULL(СчетФактураПолученный.КодВидаОперации, СчетФактураВыданный.КодВидаОперации) КАК КодВидаОперации,
	|	ЕСТЬNULL(СчетФактураПолученный.ТребуетсяСчетФактура, ЕСТЬNULL(СчетФактураВыданный.ТребуетсяСчетФактура, ИСТИНА)) КАК ТребуетсяСчетФактура,
	|	ВЫБОР
	|		КОГДА СчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА СчетФактураПолученный.СуммаДокумента
	|		КОГДА НЕ НДСПредъявленныйОбороты.ЭтоОтчетКомиссионераОПродажах
	|				И СчетФактураВыданный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА СчетФактураВыданный.СуммаДокумента
	|		КОГДА НДСПредъявленныйОбороты.СчетФактураДокумент ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(НДСПредъявленныйОбороты.СчетФактураДокумент КАК Документ.ПоступлениеТоваровУслуг).СуммаДокумента
	|		КОГДА НДСПредъявленныйОбороты.СчетФактураДокумент ССЫЛКА Документ.ПоступлениеДопРасходов
	|			ТОГДА ВЫРАЗИТЬ(НДСПредъявленныйОбороты.СчетФактураДокумент КАК Документ.ПоступлениеДопРасходов).СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДокумента,
	|	НДСПредъявленныйОбороты.ОсобыйВариантСчетаФактуры КАК ОсобыйВариантСчетаФактуры,
	|	ВЫБОР
	|		КОГДА СчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА СчетФактураПолученный.ВалютаДокумента
	|		КОГДА НЕ НДСПредъявленныйОбороты.ЭтоОтчетКомиссионераОПродажах
	|				И СчетФактураВыданный.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА СчетФактураВыданный.ВалютаДокумента
	|		КОГДА НДСПредъявленныйОбороты.СчетФактураДокумент ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(НДСПредъявленныйОбороты.СчетФактураДокумент КАК Документ.ПоступлениеТоваровУслуг).ВалютаДокумента
	|		КОГДА НДСПредъявленныйОбороты.СчетФактураДокумент ССЫЛКА Документ.ПоступлениеДопРасходов
	|			ТОГДА ВЫРАЗИТЬ(НДСПредъявленныйОбороты.СчетФактураДокумент КАК Документ.ПоступлениеДопРасходов).ВалютаДокумента
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Валюта
	|ПОМЕСТИТЬ НаличиеСчетовФактур
	|ИЗ
	|	ВременнаяТаблицаОстатки КАК НДСПредъявленныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураПолученный КАК СчетФактураПолученный
	|		ПО НДСПредъявленныйОбороты.СчетФактура = СчетФактураПолученный.СчетФактура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетФактураВыданный КАК СчетФактураВыданный
	|		ПО НДСПредъявленныйОбороты.СчетФактура = СчетФактураВыданный.СчетФактура
	|ГДЕ
	|	&УсловиеЗапроса
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаличиеСчетовФактур.Дата КАК Дата,
	|	НаличиеСчетовФактур.Контрагент КАК Контрагент,
	|	НаличиеСчетовФактур.Документ КАК Документ,
	|	НаличиеСчетовФактур.ДокументОснование КАК ДокументОснование,
	|	НаличиеСчетовФактур.СчетФактура КАК СчетФактура,
	|	НаличиеСчетовФактур.СчетФактураПроведен КАК СчетФактураПроведен,
	|	НаличиеСчетовФактур.СчетФактураДата КАК СчетФактураДата,
	|	НаличиеСчетовФактур.СчетФактураНомер КАК СчетФактураНомер,
	|	НаличиеСчетовФактур.ЭтоСчетФактураПолученный КАК ЭтоСчетФактураПолученный,
	|	НаличиеСчетовФактур.КодВидаОперации КАК КодВидаОперации,
	|	НаличиеСчетовФактур.ТребуетсяСчетФактура КАК ТребуетсяСчетФактура,
	|	НаличиеСчетовФактур.СуммаДокумента КАК СуммаДокумента,
	|	НаличиеСчетовФактур.ОсобыйВариантСчетаФактуры КАК ОсобыйВариантСчетаФактуры,
	|	ВЫБОР
	|		КОГДА НаличиеСчетовФактур.ЭтоСчетФактураПолученный = ЛОЖЬ
	|				И НаличиеСчетовФактур.СчетФактура <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЕСТЬNULL(СтатусыДокументов.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДокументовПоступления.ОригиналНеПолучен)) = ЗНАЧЕНИЕ(Перечисление.СтатусыДокументовПоступления.ОригиналПолучен)
	|	КОНЕЦ КАК Оригинал,
	|	НаличиеСчетовФактур.Валюта КАК Валюта
	|ИЗ
	|	НаличиеСчетовФактур КАК НаличиеСчетовФактур
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументов КАК СтатусыДокументов
	|		ПО (&УчитыватьОригиналы)
	|			И НаличиеСчетовФактур.СчетФактура = СтатусыДокументов.Документ";
	
	// Задаём условие по заполненным отборам.
	УсловиеЗапроса =
	"	ВЫБОР
	|		КОГДА НДСПредъявленныйОбороты.ОсобыйВариантСчетаФактуры
	|			ТОГДА &УсловиеОсобогоВарианта
	|		КОГДА СчетФактураПолученный.Ссылка ЕСТЬ НЕ NULL И СчетФактураПолученный.СчетФактураПроведен
	|			ТОГДА &УсловиеПолученный
	|		КОГДА НЕ НДСПредъявленныйОбороты.ЭтоОтчетКомиссионераОПродажах
	|			И СчетФактураВыданный.Ссылка ЕСТЬ НЕ NULL И СчетФактураВыданный.СчетФактураПроведен
	|			ТОГДА &УсловиеВыданный
	|		ИНАЧЕ &УсловиеИначе
	|	КОНЕЦ";
	УсловиеОсобогоВарианта = "ИСТИНА";
	УсловиеПолученный      = "ИСТИНА";
	УсловиеВыданный        = "ИСТИНА";
	УсловиеИначе           = "ИСТИНА";
	// Дополняем условие запроса отбором по признаку наличия/отсутствия счета-фактуры
	Если НаличиеСчетаФактуры <> Неопределено Тогда
		УсловиеОсобогоВарианта = УсловиеОсобогоВарианта + " И " + ?(НаличиеСчетаФактуры, "ИСТИНА", "ЛОЖЬ");
		УсловиеПолученный      = УсловиеПолученный + " И " + ?(НаличиеСчетаФактуры, "ИСТИНА", "ЛОЖЬ");
		УсловиеВыданный        = УсловиеВыданный + " И " + ?(НаличиеСчетаФактуры, "ИСТИНА", "ЛОЖЬ");
		УсловиеИначе           = УсловиеИначе + " И " + ?(НаличиеСчетаФактуры, "ЛОЖЬ", "ИСТИНА");
	КонецЕсли;
	// Дополняем условие запроса отбором по дате СФ
	Если ЗначениеЗаполнено(ДатаСФНеБолее) Тогда
		УсловиеОсобогоВарианта = УсловиеОсобогоВарианта + " И НДСПредъявленныйОбороты.ДатаРегистратора <= &КонПериода";
		УсловиеПолученный      = УсловиеПолученный + " И ВЫБОР 
			|					КОГДА НДСПредъявленныйОбороты.ДатаРегистратора >= &НачалоНалоговогоПериода 
			|						ТОГДА СчетФактураПолученный.СчетФактураДата <= &ДатаСФНеБолее
			|					ИНАЧЕ СчетФактураПолученный.СчетФактураДата <= &КонПериода
			|				КОНЕЦ";
		УсловиеВыданный        = УсловиеВыданный + " И СчетФактураВыданный.СчетФактураДата <= &КонПериода";
		УсловиеИначе           = УсловиеИначе + " И &НачПериода <= &КонПериода";
	КонецЕсли;
	УсловиеЗапроса = СтрЗаменить(УсловиеЗапроса, "&УсловиеОсобогоВарианта", СтрЗаменить(УсловиеОсобогоВарианта, "ИСТИНА И", ""));
	УсловиеЗапроса = СтрЗаменить(УсловиеЗапроса, "&УсловиеПолученный", СтрЗаменить(УсловиеПолученный, "ИСТИНА И", ""));
	УсловиеЗапроса = СтрЗаменить(УсловиеЗапроса, "&УсловиеВыданный", СтрЗаменить(УсловиеВыданный, "ИСТИНА И", ""));
	УсловиеЗапроса = СтрЗаменить(УсловиеЗапроса, "&УсловиеИначе", СтрЗаменить(УсловиеИначе, "ИСТИНА И", ""));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеЗапроса", УсловиеЗапроса); 
	
	Запрос.Текст = ТекстЗапроса
		+ "
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Документ";
		
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаДокументов;
	
КонецФункции

// Формирует массив строк из таблицы ОстаткиПоРегистру, которые удовлетворяют отбору. Анализируются строки от указаной
// до первой неудовлетворяющей отбору.
//
// Параметры:
//  ОстаткиПоРегистру - ТаблицаЗначений - источник анализируемых строк.
//  ИндексСтрокиОстаткаПоРегистру - Число - индекс первой из анализируемых строк.
//  СтруктураОтбора - Структура - см. СтруктураОтбораСтрок() . Если СчетФактура = Неопределено, то без отбора по нему.
//  ИмяРеквизитаКонтрагента - Строка - в книге покупок это "Покупатель", в книге продаж - "Поставщик".
//  СравнениеПоИдентификатору - СравнениеЗначений - для сравнения по идентификаторам, а не наименованиям.
//
// Возвращаемое значение:
//   Массив      - список строк, удовлетворяющих отбору.
//
Функция НайтиСтрокиПоОтбору(ОстаткиПоРегистру, ИндексСтрокиОстаткаПоРегистру, СтруктураОтбора, ИмяРеквизитаКонтрагента, СравнениеПоИдентификатору) Экспорт

	ПодходящиеСтроки = Новый Массив;
	
	КоличествоСтрокОстаткаПоРегистру = ОстаткиПоРегистру.Количество();
	Пока ИндексСтрокиОстаткаПоРегистру < КоличествоСтрокОстаткаПоРегистру Цикл
		
		СтрокаОстаткаПоРегистру = ОстаткиПоРегистру[ИндексСтрокиОстаткаПоРегистру];
		
		РезультатСравнения = СравнениеПоИдентификатору.Сравнить(СтрокаОстаткаПоРегистру[ИмяРеквизитаКонтрагента], СтруктураОтбора[ИмяРеквизитаКонтрагента]);
		Если РезультатСравнения < 0 Тогда
			ИндексСтрокиОстаткаПоРегистру = ИндексСтрокиОстаткаПоРегистру + 1;
			Продолжить; // в остатках по регистру есть контрагент, которого нет в источнике отбора
		ИначеЕсли РезультатСравнения > 0 Тогда
			Прервать; // строки соответствуют другому отбору
		КонецЕсли;
		
		Если СтруктураОтбора.СчетФактура <> Неопределено Тогда
		
			РезультатСравнения = СравнениеПоИдентификатору.Сравнить(СтрокаОстаткаПоРегистру.СчетФактура, СтруктураОтбора.СчетФактура);
			Если РезультатСравнения < 0 Тогда
				ИндексСтрокиОстаткаПоРегистру = ИндексСтрокиОстаткаПоРегистру + 1;
				Продолжить; // в остатках по регистру есть счет-фактура, которой нет в источнике отбора
			ИначеЕсли РезультатСравнения > 0 Тогда
				Прервать; // строки соответствуют другому отбору
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураОтбора.ДоговорКонтрагента) Тогда 
			РезультатСравнения = СравнениеПоИдентификатору.Сравнить(СтрокаОстаткаПоРегистру.ДоговорКонтрагента, СтруктураОтбора.ДоговорКонтрагента);
			Если РезультатСравнения < 0 Тогда
				ИндексСтрокиОстаткаПоРегистру = ИндексСтрокиОстаткаПоРегистру + 1;
				Продолжить; // в остатках по регистру есть договор, которого нет в источнике отбора
			ИначеЕсли РезультатСравнения > 0 Тогда
				Прервать; // строки соответствуют другому отбору
			КонецЕсли;
		КонецЕсли;
		
		ПодходящиеСтроки.Добавить(СтрокаОстаткаПоРегистру);
		ИндексСтрокиОстаткаПоРегистру = ИндексСтрокиОстаткаПоРегистру + 1;
	
	КонецЦикла;
	
	Возврат ПодходящиеСтроки;

КонецФункции

// Устарела. См. УстановитьСостояниеСчетаФактуры().
// Выполняет общие для всех документов действия связанные с пометкой на удаление
// счета-фактуры при удалении документа, являющегося основание данного счета-фактуры.
//
// Параметры:
//  ДокументОбъект  - объект документа,
//  ВидСчетаФактуры - строка, вид счета-фактуры, по умолчанию "СчетФактураВыданный"
//
Процедура СинхронизацияПометкиНаУдалениеУСчетаФактуры(ДокументОбъект, ВидСчетаФактуры = "СчетФактураВыданный", Отказ = Ложь) Экспорт
	
	ПараметрыДействия = НовыеПараметрыСостоянияСчетаФактуры("ПометкаУдаления", ДокументОбъект, ВидСчетаФактуры);
	
	УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);
	
КонецПроцедуры 

// Устарела. См. УстановитьСостояниеСчетаФактуры().
// Выполняет общие для всех документов действия связанные с пометкой на удаление
// счета-фактуры при удалении документа, являющегося основание данного счета-фактуры.
//
// Параметры:
//  ДокументОбъект  - объект документа,
//  ВидСчетаФактуры - строка, вид счета-фактуры, по умолчанию "СчетФактураВыданный"
//
Процедура УстановкаПометкиНаУдалениеУСчетаФактуры(ДокументОбъект, ВидСчетаФактуры = "СчетФактураВыданный", ПометкаНаУдаление = Истина) Экспорт

	ПараметрыДействия = НовыеПараметрыСостоянияСчетаФактуры("ПометкаУдаления", ДокументОбъект, ВидСчетаФактуры);
	ПараметрыДействия.СостояниеФлага  = ПометкаНаУдаление;
	
	УстановитьСостояниеСчетаФактуры(ПараметрыДействия);

КонецПроцедуры

// Устарела. См. УстановитьСостояниеСчетаФактуры().
// Выполняет общие для всех документов действия, связанные с пометкой на удаление
// счета-фактуры при удалении документа, являющегося основанием данного счета-фактуры.
//
// Параметры:
//  ДокументСсылка  - ссылка на документ,
//  ВидСчетаФактуры - строка, вид счета-фактуры, по умолчанию "СчетФактураВыданный"
//
Процедура УстановкаПроведенияУСчетаФактуры(ДокументСсылка, ВидСчетаФактуры = "СчетФактураВыданный", Проведение = Ложь, Отказ = Ложь, ГрупповоеПерепроведение = Ложь, СтруктураОтбора = Неопределено) Экспорт
	
	ПараметрыДействия = НовыеПараметрыСостоянияСчетаФактуры("Проведен", , ВидСчетаФактуры);
	ПараметрыДействия.ДокументОснование       = ДокументСсылка;
	ПараметрыДействия.СостояниеФлага          = Проведение;
	ПараметрыДействия.ГрупповоеПерепроведение = ГрупповоеПерепроведение;
	ПараметрыДействия.СтруктураОтбора         = СтруктураОтбора;
	
	УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);

КонецПроцедуры // УстановкаПроведенияУСчетаФактуры()

// Устарела. См. УстановитьСостояниеСчетаФактуры().
// Процедура помечает на удаление подчиненный счет-фактуру. 
//
Процедура ПометитьНаУдалениеСчетФактуру(СчетФактура) Экспорт

	ВидСчетаФактуры = ?(ТипЗнч(СчетФактура) = Тип("ДокументСсылка.СчетФактураПолученный"),
		"СчетФактураПолученный", "СчетФактураВыданный");
	
	ПараметрыДействия = НовыеПараметрыСостоянияСчетаФактуры("ПометкаУдаления", , ВидСчетаФактуры);
	ПараметрыДействия.СчетФактура    = СчетФактура;
	ПараметрыДействия.СостояниеФлага = Истина;
		
	УстановитьСостояниеСчетаФактуры(ПараметрыДействия);
	
КонецПроцедуры

// Инициализирует параметры, необходимые для проведения или пометки на удаление счета-фактуры.
//
// Параметры:
//  Флаг         - Строка - название свойства, изменение которого требуется.
//  ОбъектОснование - ДокументОбъект - документ-основание для счета-фактуры. Может быть не заполнено.
//  ВидСчетаФактуры - Строка - признак учета входящего или исходящего НДС.
//
Функция НовыеПараметрыСостоянияСчетаФактуры(Флаг, ОбъектОснование = Неопределено, ВидСчетаФактуры = "СчетФактураВыданный") Экспорт
	
	ПараметрыДействия = Новый Структура;
	ПараметрыДействия.Вставить("СчетФактура");          // если не заполнено, то выполняется поиск
	ПараметрыДействия.Вставить("ДокументОснование");    // значение для поиска, если счет-фактура не указан явно
	
	ПараметрыДействия.Вставить("Флаг",           Флаг); // "Проведен" или "ПометкаУдаления"
	ПараметрыДействия.Вставить("СостояниеФлага", Ложь); // требуемое состояние флага
	
	ПараметрыДействия.Вставить("ВидСчетаФактуры", ВидСчетаФактуры); // "СчетФактураВыданный" или "СчетФактураПолученный"
	
	// Признак выполнения действия во время группового перепроведения.
	ПараметрыДействия.Вставить("ГрупповоеПерепроведение", Ложь);
	
	// См. ПроведениеСервер.РегистрыТребующиеОчисткиДляОрганизации().
	ПараметрыДействия.Вставить("РегистрыТребующиеОчисткиПоДокументам");
	
	// См. параметр в НайтиПодчиненныеСчетаФактурыПолученные().
	ПараметрыДействия.Вставить("СтруктураОтбора", Новый Структура);
		
	Если ОбъектОснование <> Неопределено Тогда
		
		ПараметрыДействия.ДокументОснование       = ОбъектОснование.Ссылка;
		ПараметрыДействия.ГрупповоеПерепроведение = ПроведениеСервер.ГрупповоеПерепроведение(ОбъектОснование);
		ПараметрыДействия.СостояниеФлага          = ОбъектОснование[Флаг];
		
		ОбъектОснование.ДополнительныеСвойства.Свойство("РегистрыТребующиеОчисткиПоДокументам",
			ПараметрыДействия.РегистрыТребующиеОчисткиПоДокументам);
		
	КонецЕсли;
	
	Возврат ПараметрыДействия;
	
КонецФункции

// Выполняет общие для всех документов действия связанные с проведением или отменой проведения счета-фактуры.
// При отсутствии явного указания счета-фактуры выполняется её поиск по документу-основанию.
//
// Параметры:
//  ПараметрыДействия - Структура - см. НовыеПараметрыСостоянияСчетаФактуры().
//  ОбновлятьСтатусСчетаФактурыПоДокументу - признак того, что по документу-основанию не установлен статус счета-фактуры, 
//                                           следовательно статус счета-фактуры должен установить сам счет-фактура.
//  Отказ        - Булево - в случае ошибки получает значение Истина при выполнении процедуры.
//
Процедура УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ = Ложь, ОбновлятьСтатусСчетаФактурыПоДокументу = Истина) Экспорт
	
	Перем СчетФактураОбъект;
	
	// Определяем счет-фактуру, чье состояние меняется. Выполняем проверки.
	Если ЗначениеЗаполнено(ПараметрыДействия.СчетФактура) Тогда
		
		СчетФактура = ПараметрыДействия.СчетФактура;
		Если ТипЗнч(СчетФактура) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			ПараметрыДействия.ВидСчетаФактуры = "СчетФактураВыданный";
		ИначеЕсли ТипЗнч(СчетФактура) <> Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			ПараметрыДействия.ВидСчетаФактуры = "СчетФактураПолученный";
		Иначе
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПараметрыДействия.ДокументОснование) Тогда
			ПараметрыДействия.ДокументОснование = СчетФактура;
		КонецЕсли;
		
		СчетФактураОбъект = СчетФактура.ПолучитьОбъект();
		
	Иначе
		
		Если Не ЗначениеЗаполнено(ПараметрыДействия.ДокументОснование) Тогда
			Возврат;
		КонецЕсли;
		
		Если ПараметрыДействия.Флаг = "ПометкаУдаления" Тогда
			// Нет необходимости проверять счет-фактуру, если не сменилась пометка удаления основания.
			ПометкаУдаленияОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				ПараметрыДействия.ДокументОснование, "ПометкаУдаления");
			Если ПараметрыДействия.СостояниеФлага = ПометкаУдаленияОснования Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПараметрыДействия.ВидСчетаФактуры = "СчетФактураВыданный"
		   И Не ПравоДоступа("Изменение", Метаданные.Документы.СчетФактураВыданный)
		 Или ПараметрыДействия.ВидСчетаФактуры = "СчетФактураПолученный"
		   И Не ПравоДоступа("Изменение", Метаданные.Документы.СчетФактураПолученный) Тогда
			Возврат;
		КонецЕсли;
		
		ПометкаУдаления = (ПараметрыДействия.Флаг = "ПометкаУдаления" И Не ПараметрыДействия.СостояниеФлага);
		Если ПараметрыДействия.ВидСчетаФактуры = "СчетФактураВыданный" Тогда
			СчетФактура = НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(
				ПараметрыДействия.ДокументОснование,
				,
				ПометкаУдаления);
		Иначе
			СчетФактура = НайтиПодчиненныйСчетФактуруПолученный(
				ПараметрыДействия.ДокументОснование,
				ПараметрыДействия.ДокументОснование,
				ПометкаУдаления,
				ПараметрыДействия.СтруктураОтбора);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СчетФактура) Тогда
			Возврат;
		КонецЕсли;
		
		СчетФактураОбъект = СчетФактура.ПолучитьОбъект();
		Если ПараметрыДействия.Флаг = "ПометкаУдаления"
			И ПараметрыДействия.СостояниеФлага = СчетФактураОбъект.ПометкаУдаления
		 Или ПараметрыДействия.Флаг = "Проведен"
			И (Не ПараметрыДействия.СостояниеФлага И Не СчетФактураОбъект.Проведен
				Или СчетФактураОбъект.ПометкаУдаления) Тогда
			// Флаг уже имеет требуемое значение.
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	// Выполняем требуемое действие.
	Если ПараметрыДействия.Флаг = "ПометкаУдаления" Тогда // изменение состояния "ПометкаУдаления"
		
		ТекстЖурналаВСлучаеОшибки = НСтр("ru = 'Обновление реквизитов счетов-фактур'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		Если ПараметрыДействия.СостояниеФлага Тогда // пометка на удаление
			
			// Если в счете-фактуре несколько оснований, счет-фактура не помечается на удаление,
			// а очищается ссылка на текущее основание
			СтрокиТекОснования = СчетФактураОбъект.ДокументыОснования.НайтиСтроки(
				Новый Структура("ДокументОснование", ПараметрыДействия.ДокументОснование));
			Если СчетФактураОбъект.ДокументыОснования.Количество() > СтрокиТекОснования.Количество() Тогда
				
				ТекстСообщенияВСлучаеОшибки = НСтр("ru = 'Не удалось записать документ %1'");
				РежимЗаписиСчетаФактуры = ?(СчетФактураОбъект.Проведен,
					РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
				
				Для каждого СтрокаКУдалению Из СтрокиТекОснования Цикл
					СчетФактураОбъект.ДокументыОснования.Удалить(СтрокаКУдалению);
				КонецЦикла;
				
			Иначе
				
				ТекстСообщенияВСлучаеОшибки = НСтр("ru = 'Не удалось пометить на удаление документ %1'");
				РежимЗаписиСчетаФактуры = ?(СчетФактураОбъект.Проведен,
					РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписиДокумента.Запись);
					
				СчетФактураОбъект.ПометкаУдаления = Истина;
				СчетФактураОбъект.ДокументОснование = Неопределено;
				
			КонецЕсли;
			
		Иначе // снятие пометки на удаление
			
			ТекстСообщенияВСлучаеОшибки = НСтр("ru = 'Не удалось снять пометку на удаление с документа %1'");
			РежимЗаписиСчетаФактуры = РежимЗаписиДокумента.Запись;
			
			СчетФактураОбъект.ПометкаУдаления = Ложь;
			
		КонецЕсли;
		
	Иначе // изменение состояния "Проведен"
		
		ТекстЖурналаВСлучаеОшибки = НСтр("ru = 'Установка проведения у счета-фактуры'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		Если ПараметрыДействия.СостояниеФлага Тогда // проведение
			
			// Если в счете-фактуре несколько оснований, счет-фактура проводится, только если все основания проведены.
			Если СчетФактураОбъект.ДокументыОснования.Количество() > 1 
			   И УчетНДСБП.ЕстьНепроведеныеОснованияСчетаФактуры(
			   		СчетФактураОбъект.ДокументыОснования, ПараметрыДействия.ДокументОснование) Тогда
				Возврат;
			КонецЕсли;
			
			ТекстСообщенияВСлучаеОшибки = НСтр("ru = 'Не удалось провести документ %1'");
			РежимЗаписиСчетаФактуры = РежимЗаписиДокумента.Проведение;
			
			Если ПараметрыДействия.ГрупповоеПерепроведение Тогда
				
				ДополнительныеСвойства = СчетФактураОбъект.ДополнительныеСвойства;
				ДополнительныеСвойства.Вставить("ГрупповоеПерепроведение", Истина);
				
				ДополнительныеСвойства.Вставить("РегистрыТребующиеОчисткиПоДокументам",
					ПараметрыДействия.РегистрыТребующиеОчисткиПоДокументам);
				
				// Контроль даты запрета изменения нужен только для самостоятельных счетов-фактур, а не привязанных к документам.
				// Для счетов-фактур, привязанных к документам, контроль будет выполняться по документу.
				ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);
				
			КонецЕсли;
			
			Если ПараметрыДействия.ВидСчетаФактуры = "СчетФактураВыданный"
			   И СчетФактураОбъект.ПлатежноРасчетныеДокументы.Количество() = 0 Тогда
				// Заполним данные о платежно-расчетном документе.
				СчетФактураОбъект.ПлатежноРасчетныеДокументы.Очистить();
				ДатыНомераПРД = Документы.СчетФактураВыданный.ДатыНомераПлатежноРасчетныхДокументов(
					СчетФактураОбъект.ДокументыОснования.ВыгрузитьКолонку("ДокументОснование"));
				Для каждого СтрокаТаблицы Из ДатыНомераПРД Цикл
					НоваяСтрока = СчетФактураОбъект.ПлатежноРасчетныеДокументы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе // отмена проведения
			
			ТекстСообщенияВСлучаеОшибки = НСтр("ru = 'Не удалось отменить проведение документа %1'");
			РежимЗаписиСчетаФактуры = РежимЗаписиДокумента.ОтменаПроведения;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ОбновлятьСтатусСчетаФактурыПоДокументу Тогда 
		// Передаем документ-основание по которому уже установлен статус счета-фактуры для того, 
		// чтобы счет-фактура не актуализировал статус повторно.
		СчетФактураОбъект.ДополнительныеСвойства.Вставить("ДокументСУстановленнымСтатусом", ПараметрыДействия.ДокументОснование)
	КонецЕсли;
	
	Попытка
			
		СчетФактураОбъект.Записать(РежимЗаписиСчетаФактуры);
		
	Исключение
		
		ТекстСообщенияПользователю = СтрШаблон(ТекстСообщенияВСлучаеОшибки, СчетФактура);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияПользователю);
		
		ТекстСообщения = СтрШаблон(ТекстСообщенияВСлучаеОшибки,
			Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ТекстЖурналаВСлучаеОшибки, 
			УровеньЖурналаРегистрации.Ошибка,
			СчетФактураОбъект.Метаданные(),
			СчетФактура, 
			ТекстСообщения);
		
		Отказ = Истина;
		
	КонецПопытки;
	
КонецПроцедуры

// Определяет ставку НДС документа
//
// Параметры: 
//  ДокументОбъект    - объект документа, для которого надо определить ставку НДС
//
Функция ОпределитьСтавкуБезНДС(ДокументОбъект) Экспорт

	СтавкаБезНДС = Истина;
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
		
	Для каждого ТЧОбъекта Из ДокументОбъект.Метаданные().ТабличныеЧасти Цикл
		Если ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СтавкаНДС", МетаданныеДокумента, ТЧОбъекта.Имя) Тогда
			Для Каждого СтрокаДокумента ИЗ ДокументОбъект[ТЧОбъекта.Имя] Цикл
				Если СтрокаДокумента.СтавкаНДС <> Перечисления.СтавкиНДС.БезНДС Тогда
					СтавкаБезНДС = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СтавкаНДС", МетаданныеДокумента) Тогда
		Если ДокументОбъект.СтавкаНДС <> Перечисления.СтавкиНДС.БезНДС Тогда
			СтавкаБезНДС = Ложь;
		КонецЕсли;
	КонецЕсли;   	

	Возврат СтавкаБезНДС;
	
КонецФункции

// Функция возвращает курс ставку НДС
//
// Параметры:
//  Валюта - СправочникСсылка.Валюты, валюта, по которой необходимо получить курс
//  ДатаКурса - Дата, календарная дата, на которую необходимо получить курс валюты
//
// Возвращаемое значение:
//	Курс переданной валюты на переданную дату, 1 в случае отсутствия значения.
//
Функция ПолучитьСтавкуНДС(СтавкаНДС, ПрименяютсяСтавки4и2 = Ложь) Экспорт

	Если СтавкаНДС = Перечисления.СтавкиНДС.НДС20 ИЛИ СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120 Тогда
		Возврат 20;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС10 ИЛИ СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110 Тогда
		Ставка = ?(ПрименяютсяСтавки4и2, 2, 10);
		Возврат Ставка;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС18 ИЛИ СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118 Тогда
		Ставка = ?(ПрименяютсяСтавки4и2, 4, 18);
		Возврат Ставка;
	КонецЕсли;

	Возврат 0;

КонецФункции

// ЗАПОЛНЕНИЕ СЧЕТОВ-ФАКТУР

// Возвращает структуру с датой и номером переданного платежно-расчетного документа для подстановки в счет-фактуру
//
Функция ДатаНомерПлатежноРасчетногоДокумента(ДокументОснование) Экспорт
	
	ДатаНомер = Новый Структура("ДатаПлатежноРасчетногоДокумента, НомерПлатежноРасчетногоДокумента", '00010101', "");
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат ДатаНомер;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", ДокументОснование);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеПервичныхДокументов.Номер КАК НомерПлатежноРасчетногоДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаПлатежноРасчетногоДокумента
	|ИЗ
	|	РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|ГДЕ
	|	ДанныеПервичныхДокументов.Документ = &Документ
	|	И ДанныеПервичныхДокументов.Номер <> """"
	|	И ДанныеПервичныхДокументов.Дата <> ДАТАВРЕМЯ(1, 1, 1)";
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ДатаНомер, РезультатЗапроса);
	КонецЕсли;
	
	Возврат ДатаНомер;
	
КонецФункции

// Процедура проверяет соответствие реквизитов счета-фактуры полученного и документа-основания.
// В случае несоответствия выдается сообщение пользователю.
//
// Параметры:
//		- ДокОбъект - документ-основание
//		- ОбновлятьСтатусСчетаФактурыПоДокументу - признак того, что по документу-основанию не установлен статус счета-фактуры, 
//        следовательно статус счета-фактуры должен установить сам счет-фактура.
//
// Возврат:
//		- Истина, если различий не найдено, Ложь в противном случае
//
Процедура СинхронизироватьРеквизитыСчетаФактурыПолученного(ДокОбъект, ОбновлятьСтатусСчетаФактурыПоДокументу = Истина) Экспорт
	
	Если Не ПравоДоступа("Изменение", Метаданные.Документы.СчетФактураПолученный) Тогда
		Возврат;
	КонецЕсли;
	
	СчетФактура = НайтиПодчиненныйСчетФактуруПолученный(ДокОбъект.Ссылка);
	Если СчетФактура = Неопределено ИЛИ СчетФактура = ДокОбъект.Ссылка Тогда
		Возврат;
	КонецЕсли;

	СчетФактураОбъект = СчетФактура.ПолучитьОбъект();
	Если НЕ ОбновлятьСтатусСчетаФактурыПоДокументу Тогда 
		// Передаем документ-основание по которому уже установлен статус счета-фактуры для того, 
		// чтобы счет-фактура не актуализировал статус повторно.
		СчетФактураОбъект.ДополнительныеСвойства.Вставить("ДокументСУстановленнымСтатусом", ДокОбъект.Ссылка)
	КонецЕсли;
	
	Попытка
		СчетФактураОбъект.Заблокировать();
		СчетФактураОбъект.Записать();
	Исключение
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Реквизиты документа ""%1"" автоматически не перезаполнены и могут быть неактуальными'"),
				СчетФактура);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, СчетФактура);
	КонецПопытки;
	
КонецПроцедуры

Функция ПроверитьСоответствиеРеквизитовСчетаФактурыВыданного(ДокОбъект, Сообщать = Истина, СтруктураОтбора = Неопределено, ОбновлятьСтатусСчетаФактурыПоДокументу = Истина) Экспорт

	СчетФактура = НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(ДокОбъект.Ссылка, , , СтруктураОтбора);
	Если СчетФактура = Неопределено ИЛИ СчетФактура = ДокОбъект.Ссылка Тогда
		Возврат Истина;
	КонецЕсли;
	
	СчетФактураОбъект = СчетФактура.ПолучитьОбъект();
	Если НЕ ОбновлятьСтатусСчетаФактурыПоДокументу Тогда 
		// Передаем документ-основание по которому уже установлен статус счета-фактуры для того, 
		// чтобы счет-фактура не актуализировал статус повторно.
		СчетФактураОбъект.ДополнительныеСвойства.Вставить("ДокументСУстановленнымСтатусом", ДокОбъект.Ссылка)
	КонецЕсли;
	
	Попытка
		СчетФактураОбъект.Заблокировать();
		СчетФактураОбъект.Записать();
	Исключение
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Реквизиты документа ""%1"" автоматически не перезаполнены и могут быть неактуальными'"),
				СчетФактура);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, СчетФактура);
	КонецПопытки;
	
	Возврат Истина;

КонецФункции

// КОРРЕКТИРОВОЧНЫЕ И ИСПРАВИТЕЛЬНЫЕ СЧЕТА-ФАКТУРЫ

// Функция находит и возвращает документ, являющийся отправной точкой исправлений (либо ПТУ либо корректировку ПТУ)
// либо первоначальный документ ПТУ при передаче истины в параметр Исходный
Функция ПолучитьИсправляемыйДокументПоступления(ДокументПоступления,
		Исходный = Ложь, ОбработанныеДокументы = Неопределено) Экспорт
		
	Если ЗначениеЗаполнено(ДокументПоступления) 
		И ТипЗнч(ДокументПоступления) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		Если Исходный Тогда
			Возврат Документы.ВозвратТоваровПоставщику.СделкаПоДокументу(ДокументПоступления);
		Иначе
			Возврат ДокументПоступления;
		КонецЕсли; 
		
	ИначеЕсли Не ЗначениеЗаполнено(ДокументПоступления) 
		Или ТипЗнч(ДокументПоступления) <> Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		
		Возврат ДокументПоступления;
	КонецЕсли;
	
	СогласованноеИзменение = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение;
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументПоступления,
		"ВидОперации,ДокументПоступления");

	// При согласованном изменении первоначальный документ возвращается только при установленом флаге Исходный.
	Если Не Исходный И РеквизитыДокумента.ВидОперации = СогласованноеИзменение Тогда
		Возврат ДокументПоступления;
	КонецЕсли;

	Если ОбработанныеДокументы = Неопределено Тогда
		ОбработанныеДокументы = Новый Массив;
	ИначеЕсли ОбработанныеДокументы.Найти(РеквизитыДокумента.ДокументПоступления) <> Неопределено Тогда
		Возврат ОбработанныеДокументы[0]; // Если обнаружена зацикленность, возвращаем первый документ в массиве.
	КонецЕсли;

	ОбработанныеДокументы.Добавить(ДокументПоступления); // Добавляем обработанный документ, для поиска зацикленности
		
	// Если документ не первоначальный, то вызываем рекурсию.
	Возврат ПолучитьИсправляемыйДокументПоступления(РеквизитыДокумента.ДокументПоступления,
			Исходный, ОбработанныеДокументы);
	
КонецФункции

// Функция находит и возвращает документ, являющийся отправной точкой исправлений (либо РТУ либо корректировку РТУ)
// либо первоначальный документ РТУ при передаче истины в параметр Исходный
Функция ПолучитьИсправляемыйДокументРеализации(ДокументРеализации,
		Исходный = Ложь, ОбработанныеДокументы = Неопределено) Экспорт

	Если ЗначениеЗаполнено(ДокументРеализации) 
			И ТипЗнч(ДокументРеализации) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда 
			
			Если Исходный Тогда
				Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРеализации, "Сделка");
			Иначе
				Возврат ДокументРеализации;
			КонецЕсли; 
		
	ИначеЕсли НЕ ЗначениеЗаполнено(ДокументРеализации) 
		ИЛИ ТипЗнч(ДокументРеализации) <> Тип("ДокументСсылка.КорректировкаРеализации") Тогда 
		
		Возврат ДокументРеализации;
	КонецЕсли;

	СогласованноеИзменение = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение;
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРеализации,
		"ВидОперации,ДокументРеализации");

	// При согласованном изменении первоначальный документ возвращается только при установленом флаге Исходный.
	Если Не Исходный И РеквизитыДокумента.ВидОперации = СогласованноеИзменение Тогда
		Возврат ДокументРеализации;
	КонецЕсли;

	Если ОбработанныеДокументы = Неопределено Тогда
		ОбработанныеДокументы = Новый Массив;
	ИначеЕсли ОбработанныеДокументы.Найти(РеквизитыДокумента.ДокументРеализации) <> Неопределено Тогда
		Возврат ОбработанныеДокументы[0]; // Если обнаружена зацикленность, возвращаем первый документ в массиве.
	КонецЕсли;

	ОбработанныеДокументы.Добавить(ДокументРеализации); // Добавляем обработанный документ, для поиска зацикленности
		
	// Если документ не первоначальный, то вызываем рекурсию.
	Возврат ПолучитьИсправляемыйДокументРеализации(РеквизитыДокумента.ДокументРеализации,
			Исходный, ОбработанныеДокументы);
	
КонецФункции

// Функция возвращает массив пустых ссылок на документы, являющиеся типами для ИсправленногоСчетаФактуры
//
Функция ПолучитьМассивПустыхИсправленныхСчетовФактур() Экспорт

	МассивДокументовИсправлений = Новый Массив;
	
	МассивТипов = Метаданные.РегистрыНакопления.НДСЗаписиКнигиПокупок.Измерения.ИсправленныйСчетФактура.Тип.Типы();
	Для Каждого ТипДокумента Из МассивТипов Цикл
		МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипДокумента);
		Если МетаданныеДокумента <> Неопределено Тогда
			ИмяДокумента = МетаданныеДокумента.Имя;
			МассивДокументовИсправлений.Добавить(Документы[ИмяДокумента].ПустаяСсылка());
		КонецЕсли;
	КонецЦикла;
	
	МассивДокументовИсправлений.Добавить(Неопределено);

	Возврат МассивДокументовИсправлений;

КонецФункции // ПолучитьМассивПустыхИсправленныхСчетовФактур()

//////////////////////////////////////////////////////////////////////////
// ПОЛУЧЕНИЕ ДАННЫХ ПО ДОКУМЕНТАМ

// Параметр ДляКнигиПродаж устанавливается в случае, когда для документа могут быть получены данные как
// для книги покупок, так и для книги продаж
Функция ПолучитьТаблицуДокументаНДС(ДокументСсылка, Ошибка = Ложь, ДляКнигиПродаж = Ложь) Экспорт

	Возврат УчетНДСБП.ПолучитьТаблицуДокументаНДС(ДокументСсылка, Ошибка, ДляКнигиПродаж);

КонецФункции

// Рассчитываем сумму документа со всеми налогами
//
// Параметры:
//  ДокументОбъект    - объект документа, сумму которого надо рассчитать
//  ИмяТабличнойЧасти - строка, имя табличной части, сумму которой надо рассчитать.
//                      Если она не заполнена, считаем по всем табличным частям, в которых есть "Сумма"
//  НеУчитыватьТару   - булево, если Истина и ИмяТабличнойЧасти неопределено, то в расчете сумм тару не учитываем
//	Отбор			  - Структура - Задает отбор строк табличной части
//						* Ключ - имя реквизита табличной части, по которому отбирать
//						* Значение - Значение отбора
//
// Возвращаемое значение:
//  Сумма документа со всеми налогами.
//
Функция ПолучитьСуммуДокументаСНДС(ДокументОбъект, ИмяТабличнойЧасти = Неопределено, НеУчитыватьТару = Истина, Отбор = Неопределено) Экспорт

	МетаданныеДокумента = ДокументОбъект.Ссылка.Метаданные();

	ЕстьСуммаВключаетНДС = ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаВключаетНДС", МетаданныеДокумента);

	СуммаДокумента = 0;
	Если ИмяТабличнойЧасти <> Неопределено Тогда
	
		ЕстьСуммаНДС = ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СуммаНДС", МетаданныеДокумента, ИмяТабличнойЧасти);
		Если Отбор <> Неопределено Тогда
			ИменаРеквизитов = "Сумма"
				+ ?(ЕстьСуммаНДС, ", СуммаНДС", "");
				
			ДанныеТабличнойЧасти = ДокументОбъект[ИмяТабличнойЧасти].Выгрузить(Отбор, ИменаРеквизитов);
		Иначе
			ДанныеТабличнойЧасти = ДокументОбъект[ИмяТабличнойЧасти];
		КонецЕсли;
	
		СуммаДокумента = СуммаДокумента + ДанныеТабличнойЧасти.Итог("Сумма");
		Если ЕстьСуммаВключаетНДС
			И ЕстьСуммаНДС
			И НЕ ДокументОбъект.СуммаВключаетНДС Тогда
			СуммаДокумента = СуммаДокумента + ДанныеТабличнойЧасти.Итог("СуммаНДС");
		КонецЕсли;

	Иначе
		Для каждого ТЧОбъекта Из МетаданныеДокумента.ТабличныеЧасти Цикл
			Если НеУчитыватьТару и ТЧОбъекта.Имя = "ВозвратнаяТара" Тогда
				Продолжить;
			КонецЕсли;
			Если ТЧОбъекта.Имя = "ВыданныеАвансы" Тогда
				Продолжить;
			КонецЕсли;
			Если ТЧОбъекта.Имя = "ДенежныеСредства" Тогда
				Продолжить;
			КонецЕсли;
			
			ЕстьСумма       = ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("Сумма", МетаданныеДокумента, ТЧОбъекта.Имя);
			ЕстьСуммаНДС    = ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СуммаНДС", МетаданныеДокумента, ТЧОбъекта.Имя);
			ЕстьСуммаСкидки = ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СуммаСкидки", МетаданныеДокумента, ТЧОбъекта.Имя);
			
			Если НЕ (ЕстьСумма ИЛИ ЕстьСуммаНДС ИЛИ ЕстьСуммаСкидки) Тогда
				Продолжить;
			КонецЕсли;
			
			Если Отбор <> Неопределено Тогда
				ИменаРеквизитов =
					?(ЕстьСумма, ", Сумма", "")
					+ ?(ЕстьСуммаНДС, ", СуммаНДС", "")
					+ ?(ЕстьСуммаСкидки, ", СуммаСкидки", "");
				ИменаРеквизитов = Сред(ИменаРеквизитов, 3); // Удалим первую запятую.
			
				ДанныеТабличнойЧасти = ДокументОбъект[ИмяТабличнойЧасти].Выгрузить(Отбор, ИменаРеквизитов);
			Иначе
				ДанныеТабличнойЧасти = ДокументОбъект[ТЧОбъекта.Имя];
			КонецЕсли;
			
			Если ЕстьСумма Тогда
				СуммаДокумента = СуммаДокумента + ДанныеТабличнойЧасти.Итог("Сумма");
				Если ЕстьСуммаВключаетНДС
					И ЕстьСуммаНДС
					И НЕ ДокументОбъект.СуммаВключаетНДС Тогда
					СуммаДокумента = СуммаДокумента + ДанныеТабличнойЧасти.Итог("СуммаНДС");
				КонецЕсли;
			КонецЕсли;
			Если ЕстьСуммаСкидки Тогда
				СуммаДокумента = СуммаДокумента - ДанныеТабличнойЧасти.Итог("СуммаСкидки");
			КонецЕсли;
		КонецЦикла;
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Сумма", МетаданныеДокумента) Тогда
			СуммаДокумента = СуммаДокумента + ДокументОбъект.Сумма;
			Если ЕстьСуммаВключаетНДС
				И ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаНДС", МетаданныеДокумента)
				И НЕ ДокументОбъект.СуммаВключаетНДС Тогда
				СуммаДокумента = СуммаДокумента + ДокументОбъект.СуммаНДС;
			КонецЕсли;
		КонецЕсли;
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаСкидки", МетаданныеДокумента) Тогда
			СуммаДокумента = СуммаДокумента - ДокументОбъект.СуммаСкидки;
		КонецЕсли;
	КонецЕсли;

	Возврат СуммаДокумента;

КонецФункции

// Рассчитываем сумму документа со всеми налогами в разрезе ставок НДС
//
// Параметры:
//  ДокументОбъект    - ссылка документа, сумму которого надо рассчитать
//  ИмяТабличнойЧасти - строка, имя табличной части, сумму которой надо рассчитать.
//                      Если она не заполнена, считаем по всем табличным частям, в которых есть "Сумма"
//  НеУчитыватьТару   - булево, если Истина и ИмяТабличнойЧасти неопределено, то в расчете сумм тару не учитываем
//
// Возвращаемое значение:
//  ТаблицаЗначений
//		Колонки:
//			Сумма		- Сумма документа со всеми налогами
//			СтавкаНДС	- ПеречислениеСсылка.СтавкиНДС
//			СуммаНДС	- Сумма НДС по ставке
//
Функция ПолучитьСуммуДокументаСНДСВРазрезеСтавокНДС(ДокументОбъект, ИмяТабличнойЧасти = Неопределено, НеУчитыватьТару = Истина) Экспорт

	МетаданныеДокумента = ДокументОбъект.Метаданные();
	СуммаДокументаПоСтавкам = Новый ТаблицаЗначений();
	СуммаДокументаПоСтавкам.Колонки.Добавить("Сумма",     ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	СуммаДокументаПоСтавкам.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	СуммаДокументаПоСтавкам.Колонки.Добавить("СуммаНДС",  ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	СуммаДокументаПоСтавкам.Индексы.Добавить("СтавкаНДС");
	
	СуммаСкидкиПоДокументу = 0;
	
	Если НЕ (ИмяТабличнойЧасти = Неопределено) Тогда

		Для каждого СтрокаДокумента Из ДокументОбъект[ИмяТабличнойЧасти] Цикл

			Если ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("Сумма", МетаданныеДокумента, ИмяТабличнойЧасти) Тогда
				СуммаДокумента = СтрокаДокумента.Сумма;
			Иначе
				СуммаДокумента = 0;
			КонецЕсли;

			СуммаНДС  = 0;
			СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();

			Если ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СуммаНДС", МетаданныеДокумента, ИмяТабличнойЧасти) тогда
				СуммаНДС = СтрокаДокумента.СуммаНДС;
			КонецЕсли;

			Если ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СтавкаНДС", МетаданныеДокумента, ИмяТабличнойЧасти) тогда
				СтавкаНДС = СтрокаДокумента.СтавкаНДС;
			КонецЕсли;

			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаВключаетНДС", МетаданныеДокумента)
			   И Не ДокументОбъект.СуммаВключаетНДС Тогда

				СуммаДокумента = СуммаДокумента + СуммаНДС;

			КонецЕслИ;
			
			Если ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СуммаСкидки", МетаданныеДокумента, ИмяТабличнойЧасти) Тогда
				СуммаДокумента = СуммаДокумента - СтрокаДокумента.СуммаСкидки;
			КонецЕсли;

			СтрокаТаблицыИтогов = СуммаДокументаПоСтавкам.Найти(СтавкаНДС,"СтавкаНДС");

			Если СтрокаТаблицыИтогов = Неопределено тогда
				СтрокаТаблицыИтогов = СуммаДокументаПоСтавкам.Добавить();
				СтрокаТаблицыИтогов.СтавкаНДС = СтавкаНДС;
			КонецЕсли;

			СтрокаТаблицыИтогов.Сумма    = СтрокаТаблицыИтогов.Сумма    + СуммаДокумента;
			СтрокаТаблицыИтогов.СуммаНДС = СтрокаТаблицыИтогов.СуммаНДС + СуммаНДС;

		КонецЦикла;
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаСкидки", МетаданныеДокумента) И ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаДокумента", МетаданныеДокумента) Тогда
			
			СуммаСкидкиПоДокументу = ?(ДокументОбъект.СуммаДокумента = 0, 0, ДокументОбъект.СуммаСкидки*СуммаДокументаПоСтавкам.Итог("Сумма")/(ДокументОбъект.СуммаДокумента + ДокументОбъект.СуммаСкидки));
			
		КонецЕсли;

	Иначе

		Для каждого ТЧОбъекта Из ДокументОбъект.Метаданные().ТабличныеЧасти Цикл

			Если НеУчитыватьТару и ТЧОбъекта.Имя = "ВозвратнаяТара" Тогда
				Продолжить;
			ИначеЕсли (МетаданныеДокумента.Имя = "ОтчетКомиссионераОПродажах" или МетаданныеДокумента.Имя = "ОтчетКомитентуОПродажах") и ТЧОбъекта.Имя = "ДенежныеСредства" Тогда
				Продолжить;
			ИначеЕсли МетаданныеДокумента.Имя = "ОтчетОРозничныхПродажах" И ТЧОбъекта.Имя = "Возвраты" Тогда
				// не складываем продажи с возвратами
				Продолжить;
			ИначеЕсли МетаданныеДокумента.Имя = "ОтчетОРозничныхПродажах" И ТЧОбъекта.Имя = "ПодарочныеСертификаты" Тогда
				// НДС по проданным подарочным сертификатам определяется расчетным методом
				ДобавитьСуммуСНДСПоПодарочнымСертификатам(ДокументОбъект, СуммаДокументаПоСтавкам);
				Продолжить;
			КонецЕсли;

			ИмяТабличнойЧасти = ТЧОбъекта.Имя;

			Для каждого СтрокаДокумента  Из ДокументОбъект[ИмяТабличнойЧасти] Цикл

				Если ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("Сумма", МетаданныеДокумента, ИмяТабличнойЧасти) Тогда
					СуммаДокумента = СтрокаДокумента.Сумма;
				Иначе
					СуммаДокумента = 0;
				КонецЕсли;

				СуммаНДС  = 0;
				СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();

				Если ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СуммаНДС", МетаданныеДокумента, ИмяТабличнойЧасти) тогда
					СуммаНДС = СтрокаДокумента.СуммаНДС;
				КонецЕсли;

				Если ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СтавкаНДС", МетаданныеДокумента, ИмяТабличнойЧасти) тогда
					СтавкаНДС = СтрокаДокумента.СтавкаНДС;
				КонецЕсли;

				Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаВключаетНДС", МетаданныеДокумента)
				   И НЕ ДокументОбъект.СуммаВключаетНДС Тогда
					СуммаДокумента = СуммаДокумента+СуммаНДС;
				КонецЕсли;
				
				Если ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СуммаСкидки", МетаданныеДокумента, ИмяТабличнойЧасти) Тогда
					СуммаДокумента = СуммаДокумента - СтрокаДокумента.СуммаСкидки;
				КонецЕсли;
				
				СтрокаТаблицыИтогов = СуммаДокументаПоСтавкам.Найти(СтавкаНДС,"СтавкаНДС");

				Если СтрокаТаблицыИтогов = Неопределено тогда
					СтрокаТаблицыИтогов = СуммаДокументаПоСтавкам.Добавить();
					СтрокаТаблицыИтогов.СтавкаНДС = СтавкаНДС;
				КонецЕсли;

				СтрокаТаблицыИтогов.Сумма    = СтрокаТаблицыИтогов.Сумма    + СуммаДокумента;
				СтрокаТаблицыИтогов.СуммаНДС = СтрокаТаблицыИтогов.СуммаНДС + СуммаНДС;

			КонецЦикла;

		КонецЦикла;
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаСкидки", МетаданныеДокумента) Тогда
			
			СуммаСкидкиПоДокументу =  ДокументОбъект.СуммаСкидки;
			
		КонецЕсли;
		

	КонецЕсли;
	
	// Обрабатываем скидки по документу
	Если СуммаСкидкиПоДокументу <> 0 Тогда
	
		СуммаСоСкидкой = СуммаДокументаПоСтавкам.Итог("Сумма") - СуммаСкидкиПоДокументу;
		СуммаНДССоСкидкой = СуммаДокументаПоСтавкам.Итог("СуммаНДС") * СуммаСоСкидкой / СуммаДокументаПоСтавкам.Итог("Сумма");
		
		ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(СуммаСоСкидкой, СуммаДокументаПоСтавкам, "Сумма");
		ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(СуммаНДССоСкидкой, СуммаДокументаПоСтавкам, "СуммаНДС");
	
	КонецЕсли; 
	

	СтрокиКУдалению = Новый Массив;

	Для каждого СтрокаТаблицы Из СуммаДокументаПоСтавкам Цикл
	    Если СтрокаТаблицы.Сумма = 0 и СтрокаТаблицы.СуммаНДС = 0  Тогда
			СтрокиКУдалению.Добавить( СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;

	Для НомерСтроки = 1 По СтрокиКУдалению.Количество() Цикл
		СуммаДокументаПоСтавкам.Удалить(СтрокиКУдалению[НомерСтроки-1]);
	КонецЦикла;

	Возврат СуммаДокументаПоСтавкам;

КонецФункции // ПолучитьСуммуДокументаСНДС()

// Рассчитываем сумму НДС документа
//
// Параметры:
//  ДокументОбъект    - объект документа, сумму которого надо рассчитать
//  ИмяТабличнойЧасти - строка, имя табличной части, сумму которой надо рассчитать.
//                      Если она не заполнена, считаем по всем табличным частям, в которых есть "Сумма"
//
// Возвращаемое значение:
//  НДС документа
//
Функция ПолучитьНДСДокумента(ДокументОбъект, ИмяТабличнойЧасти = Неопределено) Экспорт

	МетаданныеДокумента = ДокументОбъект.Метаданные();

	СуммаНДС = 0;

	Если ИмяТабличнойЧасти <> Неопределено Тогда
		СуммаНДС = СуммаНДС + ДокументОбъект[ИмяТабличнойЧасти].Итог("СуммаНДС");
	Иначе
		Для каждого ТЧОбъекта Из ДокументОбъект.Метаданные().ТабличныеЧасти Цикл
			Если ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента("СуммаНДС", МетаданныеДокумента, ТЧОбъекта.Имя) Тогда
				СуммаНДС = СуммаНДС + ДокументОбъект[ТЧОбъекта.Имя].Итог("СуммаНДС");
			КонецЕсли;
		КонецЦикла;
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаНДС", МетаданныеДокумента) Тогда
			СуммаНДС = СуммаНДС + ДокументОбъект.СуммаНДС;
		КонецЕсли;
	КонецЕсли;

	Возврат СуммаНДС;

КонецФункции // ПолучитьСуммуДокументаСНДС()

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	ПРОЦЕДУРЫ, ВЫПОЛНЯЮЩИЕ ДВИЖЕНИЯ ПО РЕГИСТРАМ

// РЕАЛИЗАЦИЯ ТОВАРОВ И УСЛУГ

Процедура СформироватьДвиженияОтгрузкаТоваров(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	УчетНДСБП.СформироватьДвиженияОтгрузкаТоваров(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияВыбытиеТоваров(СписанныеПартииНДС, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ) Экспорт

	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если РаздельныйУчетНДСНаСчете19 Тогда
		
		УчетНДСРаздельный.СформироватьДвиженияВыбытиеТоваров(СписанныеПартииНДС, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
		
	Иначе
	
		УчетНДСБП.СформироватьДвиженияВыбытиеТоваров(СписанныеПартииНДС, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
		
	КонецЕсли;

КонецПроцедуры

Процедура СформироватьДвиженияРеализацияБезНДС(СписанныеПартииНДС, РеализованныеТоварыУслуги, Реквизиты, Движения, Отказ) Экспорт
	
	РаздельныйУчетНДСНаСчете19      = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	ЗаполняетсяРаздел7ДекларацииНДС = ЗаполняетсяРаздел7ДекларацииНДС(Реквизиты.Период);
	
	Если Не РаздельныйУчетНДСНаСчете19
		ИЛИ Не ЗаполняетсяРаздел7ДекларацииНДС Тогда 
		Возврат;
	КонецЕсли;
	
	УчетНДСРаздельный.СформироватьДвиженияРеализацияБезНДС(СписанныеПартииНДС, РеализованныеТоварыУслуги, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

// ПЕРЕМЕЩЕНИЕ ТОВАРОВ

Процедура СформироватьДвиженияСписаниеТоваровПрочее(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияСписаниеТоваровПрочее(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, ТаблицаРеквизиты, Движения, Отказ);

КонецПроцедуры

Функция ПолучитьТаблицуСписанныеПартииНДС(Товары, СписанныеТоварыБухУчет, Реквизиты, Отказ) Экспорт

	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если РаздельныйУчетНДСНаСчете19 Тогда
		
		Возврат УчетНДСРаздельный.ПолучитьТаблицуСписанныеПартииНДС(Товары, СписанныеТоварыБухУчет, Реквизиты, Отказ);

	Иначе
	
		Возврат УчетНДСБП.ПолучитьТаблицуСписанныеПартииНДС(Товары, СписанныеТоварыБухУчет, Реквизиты, Отказ);

	КонецЕсли;

КонецФункции 

// НАЛОГОВЫЙ АГЕНТ КУРСОВЫЕ РАЗНИЦЫ

// Функция вызывается Из процедуры УчетНДС.КорректировкаРегистровНДСПоСуммовымРазницам.
// Цель работы процедуры - сформировать дерево остатков по регистрам НДС (приобретение), содержащее
// суммы зарегистрированных событий, для последующей их корректировки
//
Функция ПолучитьДанныеПоКорректируемымСчетамФактурам(ТаблицаРеквизиты, ТаблицаСуммовыхРазниц) Экспорт

	Реквизиты = ТаблицаРеквизиты[0];

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",           Реквизиты.Организация);
	Запрос.УстановитьПараметр("ТаблицаСуммовыхРазниц", ТаблицаСуммовыхРазниц);
	Запрос.УстановитьПараметр("КонецПериода",          Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор));
	Запрос.УстановитьПараметр("Предположение0",        Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);

	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаСуммовыхРазниц.ДокументРасчетов КАК СчетФактура
	|ПОМЕСТИТЬ СписокСчетовФактур
	|ИЗ
	|	&ТаблицаСуммовыхРазниц КАК ТаблицаСуммовыхРазниц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаСуммовыхРазниц.ДокументРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСРеализация0Обороты.СчетФактура КАК СчетФактура,
	|	НДСРеализация0Обороты.ВидЦенности КАК ВидЦенности,
	|	НДСРеализация0Обороты.Покупатель КАК Покупатель,
	|	НДСРеализация0Обороты.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ВЫБОР
	|			КОГДА НДСРеализация0Обороты.Состояние = &Предположение0
	|				ТОГДА ЕСТЬNULL(НДСРеализация0Обороты.СуммаБезНДСОборот, 0)
	|			ИНАЧЕ ЕСТЬNULL(НДСРеализация0Обороты.СуммаБезНДСПриход, 0)
	|		КОНЕЦ) КАК Реализация0_БезНДС,
	|	СУММА(ВЫБОР
	|			КОГДА НДСРеализация0Обороты.Состояние = &Предположение0
	|				ТОГДА ЕСТЬNULL(НДСРеализация0Обороты.НДСОборот, 0)
	|			ИНАЧЕ ЕСТЬNULL(НДСРеализация0Обороты.НДСПриход, 0)
	|		КОНЕЦ) КАК Реализация0_НДС
	|ПОМЕСТИТЬ ВТНДСРеализация0
	|ИЗ
	|	РегистрНакопления.НДСРеализация0.Обороты(
	|			,
	|			&КонецПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						СписокСчетовФактур.СчетФактура
	|					ИЗ
	|						СписокСчетовФактур)) КАК НДСРеализация0Обороты
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСРеализация0Обороты.СчетФактура,
	|	НДСРеализация0Обороты.ВидЦенности,
	|	НДСРеализация0Обороты.Покупатель,
	|	НДСРеализация0Обороты.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСРеализация0Обороты.СчетФактура,
	|	НДСРеализация0Обороты.ВидЦенности,
	|	НДСРеализация0Обороты.Покупатель,
	|	НДСРеализация0Обороты.СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСНачисленныйОбороты.Организация,
	|	НДСНачисленныйОбороты.СчетФактура КАК СчетФактура,
	|	НДСНачисленныйОбороты.ВидЦенности,
	|	НДСНачисленныйОбороты.СтавкаНДС,
	|	СУММА(ЕСТЬNULL(НДСНачисленныйОбороты.СуммаБезНДСПриход, 0) + ЕСТЬNULL(НДСНачисленныйОбороты.НДСПриход, 0)) КАК Базис_СНДС,
	|	СУММА(ЕСТЬNULL(НДСНачисленныйОбороты.СуммаБезНДСПриход, 0)) КАК Базис_БезНДС,
	|	СУММА(ЕСТЬNULL(НДСНачисленныйОбороты.НДСПриход, 0)) КАК Базис_НДС,
	|	СУММА(НДСРеализация0.Реализация0_БезНДС) КАК Реализация0_БезНДС,
	|	СУММА(НДСРеализация0.Реализация0_НДС) КАК Реализация0_НДС,
	|	НДСНачисленныйОбороты.Покупатель,
	|	НДСНачисленныйОбороты.ВидНачисления,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НДСРеализация0.СчетФактура ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК ЕстьРеализация0
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Обороты(
	|			,
	|			&КонецПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						СписокСчетовФактур.СчетФактура
	|					ИЗ
	|						СписокСчетовФактур)) КАК НДСНачисленныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСРеализация0 КАК НДСРеализация0
	|		ПО НДСНачисленныйОбороты.СчетФактура = НДСРеализация0.СчетФактура
	|			И НДСНачисленныйОбороты.ВидЦенности = НДСРеализация0.ВидЦенности
	|			И НДСНачисленныйОбороты.Покупатель = НДСРеализация0.Покупатель
	|			И НДСНачисленныйОбороты.СтавкаНДС = НДСРеализация0.СтавкаНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНачисленныйОбороты.Организация,
	|	НДСНачисленныйОбороты.СчетФактура,
	|	НДСНачисленныйОбороты.ВидЦенности,
	|	НДСНачисленныйОбороты.СтавкаНДС,
	|	НДСНачисленныйОбороты.Покупатель,
	|	НДСНачисленныйОбороты.ВидНачисления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСПредъявленныйОбороты.Организация,
	|	НДСПредъявленныйОбороты.СчетФактура,
	|	НДСПредъявленныйОбороты.ВидЦенности,
	|	НДСПредъявленныйОбороты.СтавкаНДС,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОбороты.СуммаБезНДСПриход, 0) + ЕСТЬNULL(НДСПредъявленныйОбороты.НДСПриход, 0)),
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОбороты.СуммаБезНДСПриход, 0)),
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОбороты.НДСПриход, 0)),
	|	0,
	|	0,
	|	НДСПредъявленныйОбороты.Поставщик,
	|	NULL,
	|	0
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Обороты(
	|			,
	|			&КонецПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						СписокСчетовФактур.СчетФактура
	|					ИЗ
	|						СписокСчетовФактур)) КАК НДСПредъявленныйОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленныйОбороты.Организация,
	|	НДСПредъявленныйОбороты.СчетФактура,
	|	НДСПредъявленныйОбороты.ВидЦенности,
	|	НДСПредъявленныйОбороты.СтавкаНДС,
	|	НДСПредъявленныйОбороты.Поставщик";

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ПолучитьДанныеПоКорректируемымСчетамФактурамНалоговыйАгент(Реквизиты, ТаблицаВзаиморасчетовНалоговыйАгент) Экспорт

	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("Организация", 		   Реквизиты.Организация);
	Запрос.УстановитьПараметр("КонецПериода",          Новый Граница(Реквизиты.Период, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Предположение0",        Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);
	Запрос.УстановитьПараметр("ТаблицаСуммовыхРазниц", ТаблицаВзаиморасчетовНалоговыйАгент);

	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаСуммовыхРазниц.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ СписокСчетовФактур
	|ИЗ
	|	&ТаблицаСуммовыхРазниц КАК ТаблицаСуммовыхРазниц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйОбороты.Организация,
	|	НДСПредъявленныйОбороты.Поставщик,
	|	НДСПредъявленныйОбороты.СчетФактура,
	|	НДСПредъявленныйОбороты.ВидЦенности,
	|	НДСПредъявленныйОбороты.СтавкаНДС,
	|	НДСПредъявленныйОбороты.СчетУчетаНДС,
	|	НДСПредъявленныйОбороты.СуммаБезНДСПриход,
	|	НДСПредъявленныйОбороты.НДСПриход
	|ПОМЕСТИТЬ ВТНДСПредъявленныйОбороты
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Обороты(
	|			,
	|			&КонецПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						СписокСчетовФактур.СчетФактура
	|					ИЗ
	|						СписокСчетовФактур)) КАК НДСПредъявленныйОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСПредъявленныйОбороты.СчетФактура,
	|	НДСПредъявленныйОбороты.ВидЦенности,
	|	НДСПредъявленныйОбороты.СтавкаНДС,
	|	НДСПредъявленныйОбороты.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПоПриобретеннымЦенностямОстатки.СчетФактура,
	|	НДСПоПриобретеннымЦенностямОстатки.ВидЦенности,
	|	НДСПоПриобретеннымЦенностямОстатки.СтавкаНДС,
	|	НДСПоПриобретеннымЦенностямОстатки.СчетУчетаНДС,
	|	НДСПоПриобретеннымЦенностямОстатки.СтоимостьОстаток,
	|	НДСПоПриобретеннымЦенностямОстатки.НДСОстаток
	|ПОМЕСТИТЬ ВТНДСПоПриобретеннымЦенностямОстатки
	|ИЗ
	|	РегистрНакопления.НДСПоПриобретеннымЦенностям.Остатки(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						СписокСчетовФактур.СчетФактура
	|					ИЗ
	|						СписокСчетовФактур)) КАК НДСПоПриобретеннымЦенностямОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСПоПриобретеннымЦенностямОстатки.СчетФактура,
	|	НДСПоПриобретеннымЦенностямОстатки.ВидЦенности,
	|	НДСПоПриобретеннымЦенностямОстатки.СтавкаНДС,
	|	НДСПоПриобретеннымЦенностямОстатки.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0Обороты.СчетФактура,
	|	НДСПредъявленныйРеализация0Обороты.ВидЦенности,
	|	НДСПредъявленныйРеализация0Обороты.СтавкаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход,
	|	НДСПредъявленныйРеализация0Обороты.НДСПриход
	|ПОМЕСТИТЬ ВТНДСПредъявленныйРеализация0Обороты
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Обороты(
	|			,
	|			&КонецПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						СписокСчетовФактур.СчетФактура
	|					ИЗ
	|						СписокСчетовФактур)
	|				И Состояние = &Предположение0) КАК НДСПредъявленныйРеализация0Обороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСПредъявленныйРеализация0Обороты.СчетФактура,
	|	НДСПредъявленныйРеализация0Обороты.ВидЦенности,
	|	НДСПредъявленныйРеализация0Обороты.СтавкаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСКосвенныеРасходыОстатки.СчетФактура,
	|	НДСКосвенныеРасходыОстатки.ВидЦенности,
	|	НДСКосвенныеРасходыОстатки.СтавкаНДС,
	|	НДСКосвенныеРасходыОстатки.СчетУчетаНДС,
	|	НДСКосвенныеРасходыОстатки.СуммаБезНДСОстаток,
	|	НДСКосвенныеРасходыОстатки.НДСОстаток
	|ПОМЕСТИТЬ ВТНДСКосвенныеРасходыОстатки
	|ИЗ
	|	РегистрНакопления.НДСКосвенныеРасходы.Остатки(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						СписокСчетовФактур.СчетФактура
	|					ИЗ
	|						СписокСчетовФактур)) КАК НДСКосвенныеРасходыОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСКосвенныеРасходыОстатки.СчетФактура,
	|	НДСКосвенныеРасходыОстатки.ВидЦенности,
	|	НДСКосвенныеРасходыОстатки.СтавкаНДС,
	|	НДСКосвенныеРасходыОстатки.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПредъявленныйОбороты.Организация,
	|	НДСПредъявленныйОбороты.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйОбороты.ВидЦенности,
	|	НДСПредъявленныйОбороты.СтавкаНДС,
	|	НДСПредъявленныйОбороты.СчетУчетаНДС,
	|	НДСПредъявленныйОбороты.Поставщик,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОбороты.СуммаБезНДСПриход, 0) + ЕСТЬNULL(НДСПредъявленныйОбороты.НДСПриход, 0)) КАК Базис_СНДС,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОбороты.СуммаБезНДСПриход, 0)) КАК Базис_БезНДС,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйОбороты.НДСПриход, 0)) КАК Базис_НДС,
	|	СУММА(ЕСТЬNULL(НДСКосвенныеРасходыОстатки.СуммаБезНДСОстаток, 0)) КАК Косвенные_БезНДС,
	|	СУММА(ЕСТЬNULL(НДСКосвенныеРасходыОстатки.НДСОстаток, 0)) КАК Косвенные_НДС,
	|	СУММА(ЕСТЬNULL(НДСПоПриобретеннымЦенностямОстатки.СтоимостьОстаток, 0)) КАК Партии_СНДС,
	|	СУММА(ЕСТЬNULL(НДСПоПриобретеннымЦенностямОстатки.НДСОстаток, 0)) КАК Партии_НДС,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход, 0)) КАК Реализация0_БезНДС,
	|	СУММА(ЕСТЬNULL(НДСПредъявленныйРеализация0Обороты.НДСПриход, 0)) КАК Реализация0_НДС,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НДСПредъявленныйРеализация0Обороты.СчетФактура ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход = 0
	|							И НДСПредъявленныйРеализация0Обороты.НДСПриход = 0
	|						ТОГДА 0
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|		КОНЕЦ) КАК ЕстьРеализация0,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НДСКосвенныеРасходыОстатки.СчетФактура ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА НДСКосвенныеРасходыОстатки.СуммаБезНДСОстаток = 0
	|							И НДСКосвенныеРасходыОстатки.НДСОстаток = 0
	|						ТОГДА 0
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|		КОНЕЦ) КАК ЕстьКосвенныеРасходы,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НДСПоПриобретеннымЦенностямОстатки.СчетФактура ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА НДСПоПриобретеннымЦенностямОстатки.СтоимостьОстаток = 0
	|							И НДСПоПриобретеннымЦенностямОстатки.НДСОстаток = 0
	|						ТОГДА 0
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|		КОНЕЦ) КАК ЕстьОстаткиПоПартиям,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК ДоговорКонтрагента
	|ИЗ
	|	ВТНДСПредъявленныйОбороты КАК НДСПредъявленныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСПоПриобретеннымЦенностямОстатки КАК НДСПоПриобретеннымЦенностямОстатки
	|		ПО НДСПредъявленныйОбороты.СчетФактура = НДСПоПриобретеннымЦенностямОстатки.СчетФактура
	|			И НДСПредъявленныйОбороты.ВидЦенности = НДСПоПриобретеннымЦенностямОстатки.ВидЦенности
	|			И НДСПредъявленныйОбороты.СтавкаНДС = НДСПоПриобретеннымЦенностямОстатки.СтавкаНДС
	|			И НДСПредъявленныйОбороты.СчетУчетаНДС = НДСПоПриобретеннымЦенностямОстатки.СчетУчетаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСПредъявленныйРеализация0Обороты КАК НДСПредъявленныйРеализация0Обороты
	|		ПО НДСПредъявленныйОбороты.СчетФактура = НДСПредъявленныйРеализация0Обороты.СчетФактура
	|			И НДСПредъявленныйОбороты.ВидЦенности = НДСПредъявленныйРеализация0Обороты.ВидЦенности
	|			И НДСПредъявленныйОбороты.СтавкаНДС = НДСПредъявленныйРеализация0Обороты.СтавкаНДС
	|			И НДСПредъявленныйОбороты.СчетУчетаНДС = НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНДСКосвенныеРасходыОстатки КАК НДСКосвенныеРасходыОстатки
	|		ПО НДСПредъявленныйОбороты.СчетФактура = НДСКосвенныеРасходыОстатки.СчетФактура
	|			И НДСПредъявленныйОбороты.ВидЦенности = НДСКосвенныеРасходыОстатки.ВидЦенности
	|			И НДСПредъявленныйОбороты.СтавкаНДС = НДСКосвенныеРасходыОстатки.СтавкаНДС
	|			И НДСПредъявленныйОбороты.СчетУчетаНДС = НДСКосвенныеРасходыОстатки.СчетУчетаНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленныйОбороты.Организация,
	|	НДСПредъявленныйОбороты.СчетФактура,
	|	НДСПредъявленныйОбороты.ВидЦенности,
	|	НДСПредъявленныйОбороты.СтавкаНДС,
	|	НДСПредъявленныйОбороты.СчетУчетаНДС,
	|	НДСПредъявленныйОбороты.Поставщик";

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ПодготовитьТаблицыПоВидамРегистровНДС(РеестрСчетовФактур, Реквизиты) Экспорт

	Запрос = Новый Запрос;

	СостоянияПредъявленный = Новый СписокЗначений;
	СостоянияПредъявленный.Добавить(Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0);
	СостоянияПредъявленный.Добавить(Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0);

	Запрос.УстановитьПараметр("РеестрСчетовФактур", 	РеестрСчетовФактур);
	Запрос.УстановитьПараметр("Организация",        	Реквизиты.Организация);
	Запрос.УстановитьПараметр("СписокСчетовФактур",		ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(РеестрСчетовФактур.ВыгрузитьКолонку("СчетФактура"), Истина));
	Запрос.УстановитьПараметр("СписокВидыЦенностей",	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(РеестрСчетовФактур.ВыгрузитьКолонку("ВидЦенности"), Истина));
	Запрос.УстановитьПараметр("СписокСтавкиНДС",		ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(РеестрСчетовФактур.ВыгрузитьКолонку("СтавкаНДС"), Истина));
	Запрос.УстановитьПараметр("СписокСчетаУчетаНДС",	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(РеестрСчетовФактур.ВыгрузитьКолонку("СчетУчетаНДС"), Истина));
	Запрос.УстановитьПараметр("СостоянияПредъявленный", СостоянияПредъявленный);
	Запрос.УстановитьПараметр("СостоянияОжидание", 		Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);
	Запрос.УстановитьПараметр("НачалоПериода",      	НачалоКвартала(Реквизиты.Период));
	Запрос.УстановитьПараметр("КонецПериода",       	Новый Граница(Реквизиты.Период, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецПериодаДата",   	Реквизиты.Период);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеестрСчетовФактур.СчетФактура КАК СчетФактура,
	|	РеестрСчетовФактур.СчетУчетаНДС КАК СчетУчетаНДС,
	|	РеестрСчетовФактур.СтавкаНДС КАК СтавкаНДС,
	|	РеестрСчетовФактур.СуммоваяРазница КАК СуммоваяРазница,
	|	РеестрСчетовФактур.ВидЦенности КАК ВидЦенности,
	|	РеестрСчетовФактур.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РеестрСчетовФактур.Базис_БезНДС КАК БазисБезНДС,
	|	РеестрСчетовФактур.Базис_СНДС КАК БазисСНДС,
	|	РеестрСчетовФактур.Базис_НДС КАК БазисНДС,
	|	РеестрСчетовФактур.ЕстьКосвенныеРасходы КАК ЕстьКосвенныеРасходы,
	|	РеестрСчетовФактур.ЕстьОстаткиПоПартиям КАК ЕстьОстаткиПоПартиям,
	|	РеестрСчетовФактур.ЕстьРеализация0 КАК ЕстьРеализация0,
	|	РеестрСчетовФактур.Косвенные_БезНДС КАК Косвенные_БезНДС,
	|	РеестрСчетовФактур.Косвенные_НДС КАК Косвенные_НДС,
	|	РеестрСчетовФактур.Партии_НДС КАК Партии_НДС,
	|	РеестрСчетовФактур.Партии_СНДС КАК Партии_СНДС,
	|	РеестрСчетовФактур.Реализация0_БезНДС КАК Реализация0_БезНДС,
	|	РеестрСчетовФактур.Реализация0_НДС КАК Реализация0_НДС,
	|	РеестрСчетовФактур.СуммаБезНДС КАК СуммаБезНДС,
	|	РеестрСчетовФактур.НДС КАК НДС
	|ПОМЕСТИТЬ ВТРеестрСчетовФактур
	|ИЗ
	|	&РеестрСчетовФактур КАК РеестрСчетовФактур
	|ГДЕ
	|	РеестрСчетовФактур.Базис_БезНДС <> 0
	|	И РеестрСчетовФактур.Базис_НДС <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТРеестрСчетовФактур.Косвенные_БезНДС * ВТРеестрСчетовФактур.СуммаБезНДС / ВТРеестрСчетовФактур.БазисБезНДС) КАК СуммаБезНДС,
	|	СУММА(ВТРеестрСчетовФактур.Косвенные_НДС * ВТРеестрСчетовФактур.НДС / ВТРеестрСчетовФактур.БазисНДС) КАК НДС,
	|	ВТРеестрСчетовФактур.СчетФактура КАК СчетФактура,
	|	ВТРеестрСчетовФактур.ВидЦенности КАК ВидЦенности,
	|	ВТРеестрСчетовФактур.СтавкаНДС КАК СтавкаНДС,
	|	ВТРеестрСчетовФактур.СчетУчетаНДС КАК СчетУчетаНДС
	|ИЗ
	|	ВТРеестрСчетовФактур КАК ВТРеестрСчетовФактур
	|ГДЕ
	|	ВТРеестрСчетовФактур.ЕстьКосвенныеРасходы = 1
	|	И ВТРеестрСчетовФактур.Косвенные_БезНДС * ВТРеестрСчетовФактур.СуммаБезНДС / ВТРеестрСчетовФактур.БазисБезНДС <> 0
	|	И ВТРеестрСчетовФактур.Косвенные_НДС * ВТРеестрСчетовФактур.НДС / ВТРеестрСчетовФактур.БазисНДС <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРеестрСчетовФактур.СчетФактура,
	|	ВТРеестрСчетовФактур.ВидЦенности,
	|	ВТРеестрСчетовФактур.СтавкаНДС,
	|	ВТРеестрСчетовФактур.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТРеестрСчетовФактур.Партии_СНДС * ВТРеестрСчетовФактур.СуммоваяРазница / ВТРеестрСчетовФактур.БазисСНДС) КАК СуммаБезНДС,
	|	СУММА(ВТРеестрСчетовФактур.Партии_НДС * ВТРеестрСчетовФактур.НДС / ВТРеестрСчетовФактур.БазисНДС) КАК НДС,
	|	ВТРеестрСчетовФактур.СчетФактура КАК СчетФактура,
	|	ВТРеестрСчетовФактур.ВидЦенности КАК ВидЦенности,
	|	ВТРеестрСчетовФактур.СтавкаНДС КАК СтавкаНДС,
	|	ВТРеестрСчетовФактур.СчетУчетаНДС КАК СчетУчетаНДС
	|ИЗ
	|	ВТРеестрСчетовФактур КАК ВТРеестрСчетовФактур
	|ГДЕ
	|	ВТРеестрСчетовФактур.ЕстьОстаткиПоПартиям = 1
	|	И ВТРеестрСчетовФактур.Партии_СНДС * ВТРеестрСчетовФактур.СуммоваяРазница / ВТРеестрСчетовФактур.БазисСНДС <> 0
	|	И ВТРеестрСчетовФактур.Партии_НДС * ВТРеестрСчетовФактур.НДС / ВТРеестрСчетовФактур.БазисНДС <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРеестрСчетовФактур.СчетФактура,
	|	ВТРеестрСчетовФактур.ВидЦенности,
	|	ВТРеестрСчетовФактур.СтавкаНДС,
	|	ВТРеестрСчетовФактур.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТРеестрСчетовФактур.Реализация0_БезНДС * ВТРеестрСчетовФактур.СуммаБезНДС / ВТРеестрСчетовФактур.БазисБезНДС) КАК СуммаБезНДС,
	|	СУММА(ВТРеестрСчетовФактур.Реализация0_НДС * ВТРеестрСчетовФактур.НДС / ВТРеестрСчетовФактур.БазисНДС) КАК НДС,
	|	ВТРеестрСчетовФактур.СчетФактура КАК СчетФактура,
	|	ВТРеестрСчетовФактур.ВидЦенности КАК ВидЦенности,
	|	ВТРеестрСчетовФактур.СтавкаНДС КАК СтавкаНДС,
	|	ВТРеестрСчетовФактур.СчетУчетаНДС КАК СчетУчетаНДС
	|ИЗ
	|	ВТРеестрСчетовФактур КАК ВТРеестрСчетовФактур
	|ГДЕ
	|	ВТРеестрСчетовФактур.ЕстьРеализация0 = 1
	|	И ВТРеестрСчетовФактур.Реализация0_БезНДС * ВТРеестрСчетовФактур.СуммаБезНДС / ВТРеестрСчетовФактур.БазисБезНДС <> 0
	|	И ВТРеестрСчетовФактур.Реализация0_НДС * ВТРеестрСчетовФактур.НДС / ВТРеестрСчетовФактур.БазисНДС <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРеестрСчетовФактур.СчетФактура,
	|	ВТРеестрСчетовФактур.ВидЦенности,
	|	ВТРеестрСчетовФактур.СтавкаНДС,
	|	ВТРеестрСчетовФактур.СчетУчетаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КонецПериодаДата КАК Период,
	|	НДСКосвенныеРасходыОбороты.Организация КАК Организация,
	|	НДСКосвенныеРасходыОбороты.СчетФактура КАК СчетФактура,
	|	НДСКосвенныеРасходыОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСКосвенныеРасходыОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСКосвенныеРасходыОбороты.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСКосвенныеРасходыОбороты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	НДСКосвенныеРасходыОбороты.СуммаБезНДСОборот КАК СуммаБезНДС,
	|	НДСКосвенныеРасходыОбороты.НДСОборот КАК НДС
	|ПОМЕСТИТЬ ВТНДСКосвенныеРасходыОбороты
	|ИЗ
	|	РегистрНакопления.НДСКосвенныеРасходы.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			Организация = &Организация
	|				И СчетФактура В (&СписокСчетовФактур)
	|				И СтавкаНДС В (&СписокСтавкиНДС)
	|				И СчетУчетаНДС В (&СписокСчетаУчетаНДС)) КАК НДСКосвенныеРасходыОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СчетФактура,
	|	ВидЦенности,
	|	СтавкаНДС,
	|	СчетУчетаНДС,
	|	НДСВключенВСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КонецПериодаДата КАК Период,
	|	&КонецПериодаДата КАК ДатаСобытия,
	|	НДСКосвенныеРасходыОбороты.Организация КАК Организация,
	|	НДСКосвенныеРасходыОбороты.СчетФактура КАК СчетФактура,
	|	НДСКосвенныеРасходыОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСКосвенныеРасходыОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСКосвенныеРасходыОбороты.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСКосвенныеРасходыОбороты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	НДСКосвенныеРасходы.СуммаБезНДС КАК СуммаБезНДС,
	|	НДСКосвенныеРасходы.НДС КАК НДС,
	|	НДСКосвенныеРасходы.СтатьяЗатрат КАК СтатьяЗатрат,
	|	НДСКосвенныеРасходы.СчетЗатрат КАК СчетЗатрат,
	|	НДСКосвенныеРасходы.Субконто1 КАК Субконто1,
	|	НДСКосвенныеРасходы.Субконто2 КАК Субконто2,
	|	НДСКосвенныеРасходы.Субконто3 КАК Субконто3
	|ИЗ
	|	ВТНДСКосвенныеРасходыОбороты КАК НДСКосвенныеРасходыОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСКосвенныеРасходы КАК НДСКосвенныеРасходы
	|		ПО НДСКосвенныеРасходыОбороты.Организация = НДСКосвенныеРасходы.Организация
	|			И НДСКосвенныеРасходыОбороты.СчетФактура = НДСКосвенныеРасходы.СчетФактура
	|			И НДСКосвенныеРасходыОбороты.ВидЦенности = НДСКосвенныеРасходы.ВидЦенности
	|			И НДСКосвенныеРасходыОбороты.СтавкаНДС = НДСКосвенныеРасходы.СтавкаНДС
	|			И НДСКосвенныеРасходыОбороты.СчетУчетаНДС = НДСКосвенныеРасходы.СчетУчетаНДС
	|			И НДСКосвенныеРасходыОбороты.НДСВключенВСтоимость = НДСКосвенныеРасходы.НДСВключенВСтоимость
	|			И (НДСКосвенныеРасходы.Период >= &НачалоПериода)
	|			И (НДСКосвенныеРасходы.Период <= &КонецПериодаДата)
	|			И НДСКосвенныеРасходыОбороты.НДСВключенВСтоимость = НДСКосвенныеРасходы.НДСВключенВСтоимость
	|ГДЕ
	|	НЕ(ЕСТЬNULL(НДСКосвенныеРасходыОбороты.СуммаБезНДС, 0) = 0
	|				И ЕСТЬNULL(НДСКосвенныеРасходыОбороты.НДС, 0) = 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КонецПериодаДата КАК Период,
	|	&КонецПериодаДата КАК ДатаСобытия,
	|	НДСПоПриобретеннымЦенностямОстатки.Организация КАК Организация,
	|	НДСПоПриобретеннымЦенностямОстатки.СчетУчета КАК СчетУчета,
	|	НДСПоПриобретеннымЦенностямОстатки.Номенклатура КАК Номенклатура,
	|	НДСПоПриобретеннымЦенностямОстатки.Партия КАК Партия,
	|	НДСПоПриобретеннымЦенностямОстатки.Склад КАК Склад,
	|	НДСПоПриобретеннымЦенностямОстатки.СчетФактура КАК СчетФактура,
	|	НДСПоПриобретеннымЦенностямОстатки.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	НДСПоПриобретеннымЦенностямОстатки.ВидЦенности КАК ВидЦенности,
	|	НДСПоПриобретеннымЦенностямОстатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПоПриобретеннымЦенностямОстатки.СтавкаНДС КАК СтавкаНДС,
	|	НДСПоПриобретеннымЦенностямОстатки.СтоимостьОстаток КАК СуммаБезНДС,
	|	НДСПоПриобретеннымЦенностямОстатки.НДСОстаток КАК НДС
	|ИЗ
	|	РегистрНакопления.НДСПоПриобретеннымЦенностям.Остатки(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И СчетФактура В (&СписокСчетовФактур)
	|				И СтавкаНДС В (&СписокСтавкиНДС)
	|				И СчетУчетаНДС В (&СписокСчетаУчетаНДС)) КАК НДСПоПриобретеннымЦенностямОстатки
	|ГДЕ
	|	НЕ(ЕСТЬNULL(НДСПоПриобретеннымЦенностямОстатки.СтоимостьОстаток, 0) = 0
	|				И ЕСТЬNULL(НДСПоПриобретеннымЦенностямОстатки.НДСОстаток, 0) = 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КонецПериодаДата КАК Период,
	|	&КонецПериодаДата КАК ДатаСобытия,
	|	НДСПредъявленныйРеализация0Обороты.Организация КАК Организация,
	|	НДСПредъявленныйРеализация0Обороты.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Обороты.Состояние КАК Состояние,
	|	НДСПредъявленныйРеализация0Обороты.ДокументОтгрузки КАК ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Обороты.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленныйРеализация0Обороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние В (&СостоянияПредъявленный)
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход)
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние = &СостоянияОжидание
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход - НДСПредъявленныйРеализация0Обороты.СуммаБезНДСРасход)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние В (&СостоянияПредъявленный)
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.НДСПриход)
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние = &СостоянияОжидание
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.НДСПриход - НДСПредъявленныйРеализация0Обороты.НДСРасход)
	|	КОНЕЦ КАК НДС,
	|	НДСПредъявленныйРеализация0Обороты.Поставщик КАК Поставщик,
	|	НДСПредъявленныйРеализация0Обороты.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Обороты(
	|			,
	|			&КонецПериода,
	|			,
	|			Организация = &Организация
	|				И СчетФактура В (&СписокСчетовФактур)
	|				И СтавкаНДС В (&СписокСтавкиНДС)
	|				И СчетУчетаНДС В (&СписокСчетаУчетаНДС)) КАК НДСПредъявленныйРеализация0Обороты
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСПредъявленныйРеализация0Обороты.Организация,
	|	НДСПредъявленныйРеализация0Обороты.СчетФактура,
	|	НДСПредъявленныйРеализация0Обороты.Состояние,
	|	НДСПредъявленныйРеализация0Обороты.ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Обороты.ВидЦенности,
	|	НДСПредъявленныйРеализация0Обороты.СтавкаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Обороты.Поставщик,
	|	НДСПредъявленныйРеализация0Обороты.ИсправленныйСчетФактура
	|
	|ИМЕЮЩИЕ
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние В (&СостоянияПредъявленный)
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход)
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние = &СостоянияОжидание
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.СуммаБезНДСПриход - НДСПредъявленныйРеализация0Обороты.СуммаБезНДСРасход)
	|	КОНЕЦ <> 0 И
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние В (&СостоянияПредъявленный)
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.НДСПриход)
	|		КОГДА НДСПредъявленныйРеализация0Обороты.Состояние = &СостоянияОжидание
	|			ТОГДА СУММА(НДСПредъявленныйРеализация0Обороты.НДСПриход - НДСПредъявленныйРеализация0Обороты.НДСРасход)
	|	КОНЕЦ <> 0";
	
	Результат = Запрос.ВыполнитьПакет();

	СтруктураТаблиц = Новый Структура;

	СтруктураТаблиц.Вставить("КосвенныеРасходы", 			Результат[1].Выгрузить());
	СтруктураТаблиц.Вставить("Партии",						Результат[2].Выгрузить());
	СтруктураТаблиц.Вставить("Реализация0",         		Результат[3].Выгрузить());
	СтруктураТаблиц.Вставить("КосвенныеРасходыОбороты",     Результат[5].Выгрузить());
	СтруктураТаблиц.Вставить("ПартииОбороты",			    Результат[6].Выгрузить());
	СтруктураТаблиц.Вставить("НДСПредъявленныйРеализация0", Результат[7].Выгрузить());

	СтруктураСкорректированныхТаблиц = Новый Структура();

	ТаблицаДанныхПартии = РаспределитьПоБазису(СтруктураТаблиц.Партии, СтруктураТаблиц.ПартииОбороты);
	ТаблицаДанныхПартии.Колонки.СуммаБезНДС.Имя = "Стоимость";

	СтруктураСкорректированныхТаблиц.Вставить("Партии", ТаблицаДанныхПартии);
	СтруктураСкорректированныхТаблиц.Вставить("КосвенныеРасходы", РаспределитьПоБазису(СтруктураТаблиц.КосвенныеРасходы, СтруктураТаблиц.КосвенныеРасходыОбороты));
	СтруктураСкорректированныхТаблиц.Вставить("НДСПредъявленныйРеализация0", РаспределитьПоБазису(СтруктураТаблиц.Реализация0, СтруктураТаблиц.НДСПредъявленныйРеализация0));

	Возврат СтруктураСкорректированныхТаблиц;

КонецФункции

Функция РаспределитьПоБазису(Источник, Базис) Экспорт

	Распределение = Новый Структура("СуммаБезНДС, НДС");
	Отбор         = Новый Структура("СчетФактура, ВидЦенности, СтавкаНДС, СчетУчетаНДС");

	ТаблицаДанных = УчетНДС.СформироватьКорректирующиеЗаписи(Источник, Базис, Распределение, Отбор);

	СтрокиКУдалению = Новый Массив();

	Для каждого СтрокаДанных Из ТаблицаДанных Цикл
		Если СтрокаДанных.СуммаБезНДС = 0 И СтрокаДанных.НДС = 0 Тогда
			СтрокиКУдалению.Добавить(СтрокаДанных);
			Продолжить;
		КонецЕсли;
	КонецЦикла;

	Если СтрокиКУдалению.Количество() > 0 Тогда
		Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			ТаблицаДанных.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЕсли;

	Возврат ТаблицаДанных;

КонецФункции

//	ДВИЖЕНИЯ РЕГИСТРА "НДС НАЧИСЛЕННЫЙ"

Процедура СформироватьДвиженияНДСНачисленныйРеализацияТоваровУслуг(ТоварыУслуги, Реквизиты, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйРеализацияТоваровУслуг(ТоварыУслуги, Реквизиты, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйОказаниеУслуг(ТоварыУслуги, Реквизиты, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйОказаниеУслуг(ТоварыУслуги, Реквизиты, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйВозвратТоваровОтПокупателя(ДанныеДвижений, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйВозвратТоваровОтПокупателя(ДанныеДвижений, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйПолученныйАванс(ТаблицаРеквизиты, ТаблицаАвансов, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйПолученныйАванс(ТаблицаРеквизиты, ТаблицаАвансов, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйПолученныйАвансРасход(ТаблицаРеквизиты, ТаблицаАвансов, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйПолученныйАвансРасход(ТаблицаРеквизиты, ТаблицаАвансов, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйСуммовыеРазницы(ТаблицаСуммовыеРазницы, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйСуммовыеРазницы(ТаблицаСуммовыеРазницы, ТаблицаРеквизиты, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйРозничнаяПродажа(ТаблицаЦенности, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйРозничнаяПродажа(ТаблицаЦенности, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйНачисленНДСНалоговымАгентом(Реквизиты, ТаблицаАвансы, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйНачисленНДСНалоговымАгентом(Реквизиты, ТаблицаАвансы, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйНачислениеНДСПоРеализации(Реквизиты, ТаблицаПоРеализации, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйНачислениеНДСПоРеализации(Реквизиты, ТаблицаПоРеализации, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйСтавка0НеПодтверждена(Реквизиты, ТаблицаНДСНачисленный, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйСтавка0НеПодтверждена(Реквизиты, ТаблицаНДСНачисленный, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйСтавка0КурсоваяРазница(Реквизиты, ТаблицаДвижений, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйСтавка0КурсоваяРазница(Реквизиты, ТаблицаДвижений, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйНачислениеНДСпоСМРХозспособом(Реквизиты, СМРХозспособом, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСНачисленныйНачислениеНДСпоСМРХозспособом(Реквизиты, СМРХозспособом, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСНачисленныйРеализацияТоваровУслугКомитентаНерезидента(Реквизиты, ТаблицаДвижений, Движения, Отказ) Экспорт
	
	УчетНДСБП.СформироватьДвиженияНДСНачисленныйРеализацияТоваровУслугКомитентаНерезидента(Реквизиты, ТаблицаДвижений, Движения, Отказ);
	
КонецПроцедуры	

//	ДВИЖЕНИЯ РЕГИСТРА "НДС ПРЕДЪЯВЛЕННЫЙ, РЕАЛИЗАЦИЯ 0%"

Процедура СформироватьДвиженияНДСПредъявленныйРеализация0ВычетПоРеализации(ТаблицаДвижений, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСПредъявленныйРеализация0ВычетПоРеализации(ТаблицаДвижений, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСПредъявленныйРеализация0СнятиеБлокировкиВычета(Реквизиты, ТаблицаСнятиеБлокировкиВычета, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСПредъявленныйРеализация0СнятиеБлокировкиВычета(Реквизиты, ТаблицаСнятиеБлокировкиВычета, Движения, Отказ);

КонецПроцедуры

//	ДВИЖЕНИЯ РЕГИСТРА "НДС ПО ПРИОБРЕТЕННЫМ ЦЕННОСТЯМ"

Процедура СформироватьДвиженияНДСПоПриобретеннымЦенностямВозвратТоваровПокупателем(ТаблицаВозвращаемыеТовары, Реквизиты, Движения, Отказ) Экспорт
	
	УчетНДСБП.СформироватьДвиженияНДСПоПриобретеннымЦенностямВозвратТоваровПокупателем(ТаблицаВозвращаемыеТовары, Реквизиты, Движения, Отказ);
	
КонецПроцедуры

Процедура СформироватьДвиженияНДСПоПриобретеннымЦенностямПоступлениеТоваров(Товары, Реквизиты, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСПоПриобретеннымЦенностямПоступлениеТоваров(Товары, Реквизиты, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСПоПриобретеннымЦенностям(ТаблицаДвижений, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСПоПриобретеннымЦенностям(ТаблицаДвижений, Движения, Отказ);

КонецПроцедуры

//	ДВИЖЕНИЯ РЕГИСТРА "НДС ПО КОСВЕННЫМ РАСХОДАМ"

Процедура СформироватьДвиженияНДСКосвенныеРасходыПоступлениеУслуг(РаспределеляемыеРасходы, Реквизиты, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСКосвенныеРасходыПоступлениеУслуг(РаспределеляемыеРасходы, Реквизиты, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСКосвенныеРасходы(ТаблицаДвижений, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСКосвенныеРасходы(ТаблицаДвижений, Движения, Отказ);

КонецПроцедуры

//	ДВИЖЕНИЯ РЕГИСТРА "НДС С АВАНСОВ"

Процедура СформироватьДвиженияНДССАвансовВосстановлениеНДСВыданныйАванс(ТаблицаРеквизиты, ТаблицаАвансов, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДССАвансовВосстановлениеНДСВыданныйАванс(ТаблицаРеквизиты, ТаблицаАвансов, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСсАвансовПолученныйАванс(ТаблицаРеквизиты, ТаблицаАвансов, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСсАвансовПолученныйАванс(ТаблицаРеквизиты, ТаблицаАвансов, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьДвиженияНДСсАвансовЗачетПолученногоАванса(ТаблицаРеквизиты, ТаблицаАвансов, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСсАвансовЗачетПолученногоАванса(ТаблицаРеквизиты, ТаблицаАвансов, Движения, Отказ);

КонецПроцедуры

//	ДВИЖЕНИЯ РЕГИСТРА БУХГАЛТЕРИИ

Процедура СформироватьПроводкиПоступлениеГТД(НомераГТД, Реквизиты, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьПроводкиПоступлениеГТД(НомераГТД, Реквизиты, Движения, Отказ);

КонецПроцедуры

Процедура СформироватьПроводкиСписаниеГТД(Товары, Реквизиты, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьПроводкиСписаниеГТД(Товары, Реквизиты, Движения, Отказ);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	ДВИЖЕНИЯ НДС ДОКУМЕНТА КОРРЕКТИРОВКА ПОСТУПЛЕНИЯ

Процедура СформироватьДвиженияКорректировкаНДСПоПартиямЗапасов(РеквизитыДокумента, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияКорректировкаНДСПоПартиямЗапасов(РеквизитыДокумента, Движения, Отказ);

КонецПроцедуры         

Процедура СформироватьДвиженияКорректировкаНДСКосвенныхРасходов(РеквизитыДокумента, ТаблицаКорректировкаНДСКосвенныхРасходов, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияКорректировкаНДСКосвенныхРасходов(РеквизитыДокумента, ТаблицаКорректировкаНДСКосвенныхРасходов, Движения, Отказ);
	
КонецПроцедуры	

Функция ПодготовитьТаблицуКоэффициентовКорректировкиПоступления(Реквизиты) Экспорт

	Возврат УчетНДСБП.ПодготовитьТаблицуКоэффициентовКорректировкиПоступления(Реквизиты);

КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ОпределитьРаспределеляемыеРасходы(ТаблицаРасходов) Экспорт

	Возврат УчетНДСБП.ОпределитьРаспределеляемыеРасходы(ТаблицаРасходов);

КонецФункции

// Функция возвращает ссылку на документ, который требуется записывать в измерение 
// СчетФактура регистров НДС.
//
Функция ПолучитьСчетФактуруДляЗаписиВРегистрыНДС(ИсходныйДокумент) Экспорт

	СчетФактура = Неопределено;

	Если ТипЗнч(ИсходныйДокумент) = Тип("ДокументСсылка.ОтражениеНДСКВычету") Тогда

		РеквизитыИсходногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсходныйДокумент, "РасчетныйДокумент, ИспользоватьДокументРасчетовКакСчетФактуру");
		
		Если РеквизитыИсходногоДокумента.ИспользоватьДокументРасчетовКакСчетФактуру Тогда
			
			Если ТипЗнч(РеквизитыИсходногоДокумента.РасчетныйДокумент) = Тип("ДокументСсылка.СчетФактураВыданный")
				 ИЛИ ТипЗнч(РеквизитыИсходногоДокумента.РасчетныйДокумент) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")
				 ИЛИ ТипЗнч(РеквизитыИсходногоДокумента.РасчетныйДокумент) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			
				СчетФактура = ПолучитьСчетФактуруДляЗаписиВРегистрыНДС(РеквизитыИсходногоДокумента.РасчетныйДокумент);
			
			Иначе
				
				СчетФактура = РеквизитыИсходногоДокумента.РасчетныйДокумент;
				
			КонецЕсли;
			
		Иначе // т.е. НЕ ИспользоватьДокументРасчетовКакСчетФактуру
		
			СчетФактура = ИсходныйДокумент;
			
		КонецЕсли;
	
	ИначеЕсли ТипЗнч(ИсходныйДокумент) = Тип("ДокументСсылка.ОтражениеНачисленияНДС") Тогда
	
		РеквизитыИсходногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсходныйДокумент, 
			"РасчетныйДокумент, ИспользоватьДокументРасчетовКакСчетФактуру");
		Если РеквизитыИсходногоДокумента.ИспользоватьДокументРасчетовКакСчетФактуру Тогда
			СчетФактура = РеквизитыИсходногоДокумента.РасчетныйДокумент;
		Иначе
			СчетФактура = ИсходныйДокумент;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ИсходныйДокумент) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			
		РеквизитыИсходногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсходныйДокумент, 
			"ДокументОснование, ВидСчетаФактуры");
		Если РеквизитыИсходногоДокумента.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс Тогда
			СчетФактура = РеквизитыИсходногоДокумента.ДокументОснование;
		Иначе
			СчетФактура = ИсходныйДокумент;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ИсходныйДокумент) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
	
		РеквизитыИсходногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсходныйДокумент, 
			"ДокументОснование, ВидСчетаФактуры");
		Если РеквизитыИсходногоДокумента.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс
			ИЛИ РеквизитыИсходногоДокумента.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАвансКомитента Тогда
			
			СчетФактура = РеквизитыИсходногоДокумента.ДокументОснование;
			
		Иначе
			СчетФактура = ИсходныйДокумент;
		КонецЕсли;
				
	ИначеЕсли ТипЗнч(ИсходныйДокумент) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		
		РеквизитыИсходногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсходныйДокумент,
			"ПокупателемВыставляетсяСчетФактураНаВозврат, Сделка");
		Если РеквизитыИсходногоДокумента.ПокупателемВыставляетсяСчетФактураНаВозврат Тогда
			СчетФактура = РеквизитыИсходногоДокумента.Сделка;
		Иначе
			СчетФактура = ИсходныйДокумент;
		КонецЕсли;
		
	Иначе
	
		СчетФактура = ИсходныйДокумент;
		
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(СчетФактура) Тогда
		СчетФактура = ИсходныйДокумент;
	КонецЕсли;

	Возврат СчетФактура;

КонецФункции

// Функция возращает Истина, если для переданных СчетаФактуры и ВидаЦенности
// необходимо указание контрагента при записи в регистры НДС.
//
Функция НеобходимоУказаниеКонтрагентаДляСчетаФактуры(СчетФактура, ВидЦенности) Экспорт

	ТипСФ = ТипЗнч(СчетФактура);
	Если ВидЦенности = Перечисления.ВидыЦенностей.ВнутреннееПотребление
		ИЛИ ВидЦенности = Перечисления.ВидыЦенностей.СМРСобственнымиСилами
		ИЛИ ТипСФ = Тип("ДокументСсылка.ОтчетОРозничныхПродажах")
		ИЛИ (ТипСФ = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
		И НЕ (ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные 
		ИЛИ ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные0
		ИЛИ ВидЦенности = Перечисления.ВидыЦенностей.ВозвратАвансовПолученных
		ИЛИ ВидЦенности = Перечисления.ВидыЦенностей.СуммыСвязанныеСРасчетамиПоОплате))	Тогда
		
		Возврат Ложь;
		
	Иначе
		Возврат Истина;
	КонецЕсли; 

КонецФункции // НеобходимоУказаниеКонтрагентаДляСчетаФактуры()

// Функция возвращает признак ведения учета по постановлению Правительства РФ № 1137 от 26.12.2011
//
Функция ИспользуетсяПостановлениеНДС1137(Период) Экспорт

	Результат = ПолучитьФункциональнуюОпцию("ИспользуетсяПостановлениеНДС1137", 
		Новый Структура("Период", НачалоДня(Период)));

	Возврат Результат;

КонецФункции

// Возвращает версию постановления Правительства РФ от 26.12.2011 г. № 1137
//
// Параметры
//  Период  -  тип дата, в данном параметре передается
//             дата на которую необходимо определить версию постановления
// Возвращаемое значение:
//  Число   -  версия постановления,
//              1 - исходная версия постановления Правительства РФ от 26.12.2011 г. № 1137
//              2 - постановление Правительства РФ от 26.12.2011 г. № 1137 в редакции постановления № 952
//              3 - постановление Правительства РФ от 26.12.2011 г. № 1137 в редакции постановления № 735
//              4 - постановление Правительства РФ от 26.12.2011 г. № 1137 в редакции постановления № 981
//
Функция ВерсияПостановленияНДС1137(Период) Экспорт
	
	Если Период >= '20171001' Тогда      // Постановление № 981 вступает в силу с 1 октября 2017 года.
		Возврат 4;
	ИначеЕсли Период >= '20141001' Тогда // Постановление № 735 вступает в силу с 1 октября 2014 года.
		Возврат 3;
	ИначеЕсли Период >= '20131106' Тогда // Постановление № 952 вступает в силу с 6 ноября 2013 года.
		Возврат 2;
	Иначе
		Возврат 1;                       // Исходная версия Постановления Правительства РФ от 26.12.2011 г. № 1137.
	КонецЕсли;
	
КонецФункции

// Возвращает признак применения постановления Правительства от 19.01.2019 г. № 15
// в котором утверждена форма книги продаж с добавлением граф по ставке 20%
// и отменена обязанность по выставлению покупателем счета-фактуры при возврате товаров, принятых на учет.
// Дата вступления в силу постановления 01.04.2019.
// Однако постановление можно применять с 1 января 2019 года,
// т.к. в соответствии с НК РФ ставка 20% применяется именно с этого момента.
Функция ПрименяетсяПостановление15(Период) Экспорт
	
	Возврат Период >= '20190101';
	
КонецФункции

// Функция возвращает признак автоматического заполнения раздела 7 Декларации по НДС
//
Функция ЗаполняетсяРаздел7ДекларацииНДС(Период) Экспорт
	
	Если Период >= '20170401' Тогда 
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Определяет наличие соглашения с контрагентом, на электронный обмен документами
//
Функция НаличиеСоглашенияОбменаЭД(СсылкаНаВладельца) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД")
		ИЛИ НЕ ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыЭД = Новый Структура("ВидЭд,НаправлениеЭД,Организация,Контрагент");
	
	ЗаполнитьЗначенияСвойств(ПараметрыЭД, СсылкаНаВладельца);
	
	ПараметрыЭД.ВидЭД         = Перечисления.ВидыЭД.СчетФактура;
	ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;

	Результат = ОбменСКонтрагентамиСлужебный.ОпределитьНастройкиОбменаЭД(ПараметрыЭД);
	
	Возврат ЗначениеЗаполнено(Результат);
	
КонецФункции

Процедура ВключитьНДСВРасходыНУ(СписанныеПартииНДС, ТаблицаЗатратПоВключениюНДСВСоставРасходов,
		Реквизиты, Движения, Отказ) Экспорт
		
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если РаздельныйУчетНДСНаСчете19 Тогда
		УчетНДСРаздельный.ВключитьНДСВРасходыНУ(СписанныеПартииНДС, ТаблицаЗатратПоВключениюНДСВСоставРасходов,
		Реквизиты, Движения, Отказ);
	КонецЕсли;
		
КонецПроцедуры
	
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	ОТПРАВКА ДОКУМЕНТОВ НДС В НАЛОГОВЫЕ ОРГАНЫ

Функция СформироватьЭлектронныеДокументы(ВыгружаемыеДокументы, УникальныйИдентификатор = Неопределено) Экспорт
	
	Если УникальныйИдентификатор = Неопределено Тогда
		УникальныйИдентификатор = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	Результат = Новый Соответствие;
	
	Для Каждого ВыгружаемыйДокумент Из ВыгружаемыеДокументы Цикл
		
		ЭтоДокументРегистраУчетаНДС = (ВыгружаемыйДокумент.Метаданные().Имя = "КнигаПокупокДляПередачиВЭлектронномВиде"
		                           ИЛИ ВыгружаемыйДокумент.Метаданные().Имя = "КнигаПродажДляПередачиВЭлектронномВиде"
		                           ИЛИ ВыгружаемыйДокумент.Метаданные().Имя = "ДопЛистКнигиПокупокДляПередачиВЭлектронномВиде"
		                           ИЛИ ВыгружаемыйДокумент.Метаданные().Имя = "ДопЛистКнигиПродажДляПередачиВЭлектронномВиде"
		                           ИЛИ ВыгружаемыйДокумент.Метаданные().Имя = "ЖурналУчетаСчетовФактурДляПередачиВЭлектронномВиде");
		Если ЭтоДокументРегистраУчетаНДС Тогда
			ОбъектВыгрузки = ВыгружаемыйДокумент.ПолучитьОбъект();
			ФайлыВыгрузки = ОбъектВыгрузки.ВыгрузитьДокумент(УникальныйИдентификатор);
			
			МассивВыгрузки = Новый Массив;
			Для Каждого ФайлВыгрузки Из ФайлыВыгрузки Цикл
				
				ЭлементВыгрузки = Новый Структура;
				ЭлементВыгрузки.Вставить("ТипФайла", "ФайлВыгрузки");
				ЭлементВыгрузки.Вставить("ИмяФайла", ФайлВыгрузки.ИмяФайлаВыгрузки);
				ЭлементВыгрузки.Вставить("АдресВременногоХранилища", ФайлВыгрузки.АдресФайлаВыгрузки);
				
				МассивВыгрузки.Добавить(ЭлементВыгрузки);
			КонецЦикла;
			
			Результат.Вставить(ВыгружаемыйДокумент, МассивВыгрузки);
			
		Иначе
			Результат.Вставить(ВыгружаемыйДокумент, Неопределено);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

//////////////////////////////////////////////////////////////////////////
// РАБОТА СО СЧЕТАМИ-ФАКТУРАМИ

Функция СоздатьСчетФактуруВыданныйНаОсновании(ПараметрыСоздания) Экспорт
	
	Возврат Документы.СчетФактураВыданный.СоздатьДокументНаОсновании(ПараметрыСоздания);
	
КонецФункции

Функция СоздатьСчетФактуруПолученныйНаОсновании(ПараметрыСоздания, ОбновлятьСтатусСчетаФактурыПоДокументу = Истина,
	ОригиналСчетаФактуры = Неопределено) Экспорт
	
	Возврат Документы.СчетФактураПолученный.СоздатьДокументНаОсновании(ПараметрыСоздания,
		ОбновлятьСтатусСчетаФактурыПоДокументу, ОригиналСчетаФактуры);
	
КонецФункции

Функция СоздатьСчетФактуруНаОснованииИсправления(Основание, НомерИсправления, ДатаИсправления) Экспорт

	Возврат Документы.СчетФактураПолученный.СоздатьДокументНаОснованииИсправления(Основание, НомерИсправления, ДатаИсправления);

КонецФункции // СоздатьСчетФактуруНаОснованииИсправления()

// Функция производит поиск счета-фактуры полученного по реквизитам
// Параметры:
//  РеквизитыСчетаФактуры - структура с реквизитами счета-фактуры полученного,
//                          структура должна обязательно содержать поля Организация, Контрагент, НомерСчетаФактуры, ДатаСчетаФактуры
//
// Возвращаемое значение:
//  Если нашли, то возвращаем ссылку, не нашли - Неопределено
//
Функция НайтиСчетФактуруПолученный(РеквизитыСчетаФактуры) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Организация", РеквизитыСчетаФактуры.Организация);
	Запрос.Параметры.Вставить("Контрагент",  РеквизитыСчетаФактуры.Контрагент);
	Запрос.Параметры.Вставить("НомерСчетаФактурыПолученного", РеквизитыСчетаФактуры.НомерСчетаФактурыПолученного);
	Запрос.Параметры.Вставить("ДатаСчетаФактурыПолученного", РеквизитыСчетаФактуры.ДатаСчетаФактурыПолученного);
	
	Продавец = ?(РеквизитыСчетаФактуры.Свойство("Продавец"), РеквизитыСчетаФактуры.Продавец, Справочники.Контрагенты.ПустаяСсылка());
	Запрос.Параметры.Вставить("Продавец", Продавец);
	Запрос.Параметры.Вставить("БланкСтрогойОтчетности", 
		?(РеквизитыСчетаФактуры.Свойство("БланкСтрогойОтчетности"), РеквизитыСчетаФактуры.БланкСтрогойОтчетности, Ложь));
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СчетФактураПолученный.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.ВидСчетаФактуры В (ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.НаПоступление), ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыПолученного.Корректировочный))
	|	И СчетФактураПолученный.Организация = &Организация
	|	И СчетФактураПолученный.Контрагент = &Контрагент
	|	И СчетФактураПолученный.Продавец = &Продавец
	|	И СчетФактураПолученный.НомерВходящегоДокумента = &НомерСчетаФактурыПолученного
	|	И СчетФактураПолученный.ДатаВходящегоДокумента = &ДатаСчетаФактурыПолученного
	|	И СчетФактураПолученный.БланкСтрогойОтчетности = &БланкСтрогойОтчетности
	|	И НЕ СчетФактураПолученный.ИсправлениеСобственнойОшибки
	|	И НЕ СчетФактураПолученный.ПометкаУдаления";
	
	Если РеквизитыСчетаФактуры.Свойство("ДоговорКонтрагента") Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	И СчетФактураПолученный.ДоговорКонтрагента = &ДоговорКонтрагента";
		Запрос.Параметры.Вставить("ДоговорКонтрагента", РеквизитыСчетаФактуры.ДоговорКонтрагента);
	КонецЕсли;
	
	Если РеквизитыСчетаФактуры.Свойство("Исправление") 
		И РеквизитыСчетаФактуры.Исправление = Истина Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	И СчетФактураПолученный.Исправление = ИСТИНА
		|	И СчетФактураПолученный.НомерИсправления = &НомерИсправления
		|	И СчетФактураПолученный.ДатаИсправления = &ДатаИсправления";
		Запрос.Параметры.Вставить("Исправление", Истина);
		Запрос.Параметры.Вставить("НомерИсправления", РеквизитыСчетаФактуры.НомерИсправления);
		Запрос.Параметры.Вставить("ДатаИсправления", РеквизитыСчетаФактуры.ДатаИсправления);
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
		|	И СчетФактураПолученный.Исправление = ЛОЖЬ";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ЭтоСчетФактураНаАванс(ВидСчетаФактуры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВидСчетаФактуры) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СчетаФактурыНаАванс = Новый Массив();
	СчетаФактурыНаАванс.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаАванс);
	СчетаФактурыНаАванс.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента);
	СчетаФактурыНаАванс.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку);
	
	Возврат СчетаФактурыНаАванс.Найти(ВидСчетаФактуры) <> Неопределено;
	
КонецФункции

Функция ПолучитьСписокТиповПоВидуСчетаФактурыПолученного(ВидСчетаФактурыСписка, ЭтоИсправление = Ложь, НаАвансДляКомитентаНаЗакупку = Ложь) Экспорт
	
	СписокТипов = Новый Массив;
	
	Если ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыПолученного.НаПоступление Тогда 
		
		Если ЭтоИсправление Тогда
			СписокТипов.Добавить(Тип("ДокументСсылка.КорректировкаПоступления"));
		Иначе
			СписокТипов.Добавить(Тип("ДокументСсылка.ВозвратТоваровОтПокупателя"));
			СписокТипов.Добавить(Тип("ДокументСсылка.ВыкупПредметовЛизинга"));
			СписокТипов.Добавить(Тип("ДокументСсылка.ДокументРасчетовСКонтрагентом"));
			СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
			СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомитентуОПродажах"));
			СписокТипов.Добавить(Тип("ДокументСсылка.ПоступлениеИзПереработки"));
			СписокТипов.Добавить(Тип("ДокументСсылка.ПоступлениеДопРасходов"));
			СписокТипов.Добавить(Тип("ДокументСсылка.ПоступлениеНМА"));
			СписокТипов.Добавить(Тип("ДокументСсылка.ПоступлениеТоваровУслуг"));
			СписокТипов.Добавить(Тип("ДокументСсылка.ОтражениеНДСКВычету"));
		КонецЕсли;
  
	ИначеЕсли ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыПолученного.НаАванс 
		ИЛИ ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыПолученного.НаАвансКомитента
		ИЛИ ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыПолученного.КорректировочныйНаАванс Тогда 
		
		СписокТипов.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
		СписокТипов.Добавить(Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
		СписокТипов.Добавить(Тип("ДокументСсылка.КорректировкаДолга"));
		СписокТипов.Добавить(Тип("ДокументСсылка.АвансовыйОтчет"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ДокументРасчетовСКонтрагентом"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомитентуОПродажах"));
		СписокТипов.Добавить(Тип("ДокументСсылка.КорректировкаПоступления"));
		
	ИначеЕсли ВидСчетаФактурыСписка = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда 
		
		СписокТипов.Добавить(Тип("ДокументСсылка.КорректировкаПоступления"));
				
	КонецЕсли;
	
	Возврат СписокТипов;
	
КонецФункции

// Функция находит и возвращает последнее испраление счета-фактуры выданного
// либо исходный документ, если счет-фактура не исправлялся
Функция ПолучитьПоследнееИсправлениеСчетаФактурыВыданного(СчетФактураВыданный) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсправляемыйДокумент", СчетФактураВыданный);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СчетФактураВыданный.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ИсправляемыйСчетФактура = &ИсправляемыйДокумент
	|	И СчетФактураВыданный.ПометкаУдаления = ЛОЖЬ
	|	И СчетФактураВыданный.Исправление = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураВыданный.Дата УБЫВ";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат СчетФактураВыданный;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.СчетФактура;
	КонецЕсли;
		
КонецФункции

// Функция находит и возвращает последнее испраление счета-фактуры полученного
// либо исходный документ, если счет-фактура не исправлялся
Функция ПолучитьПоследнееИсправлениеСчетаФактурыПолученного(СчетФактураПолученный) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсправляемыйДокумент", СчетФактураПолученный);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СчетФактураПолученный.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.ИсправляемыйСчетФактура = &ИсправляемыйДокумент
	|	И СчетФактураПолученный.ПометкаУдаления = ЛОЖЬ
	|	И СчетФактураПолученный.Исправление = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураПолученный.Дата УБЫВ";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат СчетФактураПолученный;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.СчетФактура;
	КонецЕсли;
		
КонецФункции


//////////////////////////////////////////////////////////////////////////
// РАБОТА С КНИГАМИ ПОКУПОК И ПРОДАЖ

Процедура ПодготовитьПараметрыКнигиПокупок(ПараметрыОтчета, АдресХранилища) Экспорт
	
	Если ИспользуетсяПостановлениеНДС1137(ПараметрыОтчета.КонецПериода) Тогда  
		Отчеты.КнигаПокупок.СформироватьОтчет(ПараметрыОтчета, АдресХранилища);	
	Иначе
		УчетНДСБП.СформироватьОтчетКнигаПокупок914(ПараметрыОтчета, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьПараметрыКнигиПродаж(ПараметрыОтчета, АдресХранилища) Экспорт
	
	Если ИспользуетсяПостановлениеНДС1137(ПараметрыОтчета.КонецПериода) Тогда  
		Отчеты.КнигаПродаж.СформироватьОтчет(ПараметрыОтчета, АдресХранилища);
	Иначе
		УчетНДСБП.СформироватьОтчетКнигаПродаж914(ПараметрыОтчета, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////
// РАБОТА С ЖУРНАЛОМ УЧЕТА СЧЕТОВ-ФАКТУР

Процедура ПодготовитьПараметрыЖурналаУчетаСчетовФактур (ПараметрыОтчета, АдресХранилища) Экспорт
	
	Если ИспользуетсяПостановлениеНДС1137(ПараметрыОтчета.НалоговыйПериод) Тогда  
		Отчеты.ЖурналУчетаСчетовФактур.СформироватьОтчет(ПараметрыОтчета, АдресХранилища);
	Иначе
		УчетНДСБП.СформироватьОтчетЖурналУчетаСчетовФактур914(ПараметрыОтчета, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

// ПОСТУПЛЕНИЕ ТОВАРОВ И УСЛУГ

Процедура СформироватьДвиженияОприходованиеТоваров(ТаблицаТовары, Реквизиты, Движения, Отказ) Экспорт
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если РаздельныйУчетНДСНаСчете19 Тогда
		
		УчетНДСРаздельный.СформироватьДвиженияНДСРаздельныйУчетОприходованиеТоваров(ТаблицаТовары, Реквизиты, Движения, Отказ);
		
	Иначе
		
		УчетНДСБП.СформироватьДвиженияНДСПоПриобретеннымЦенностямОприходованиеТоваров(ТаблицаТовары, Реквизиты, Движения, Отказ);
		
	КонецЕсли;

	
КонецПроцедуры

Процедура СформироватьДвиженияНДСПоПриобретеннымЦенностямПоступлениеИзНТТ(Товары, Реквизиты, Движения, Отказ) Экспорт
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);	
	
	Если РаздельныйУчетНДСНаСчете19 Тогда
		
		УчетНДСРаздельный.СформироватьДвиженияНДСРаздельныйУчетПоступлениеИзНТТ(Товары, Реквизиты, Движения, Отказ);

	Иначе
	
		УчетНДСБП.СформироватьДвиженияНДСПоПриобретеннымЦенностямПоступлениеИзНТТ(Товары, Реквизиты, Движения, Отказ);

	КонецЕсли;
	
	
КонецПроцедуры

Процедура СформироватьДвиженияНДСПоПриобретеннымЦенностямПоступлениеНаДругойСклад(СписанныеПартииНДС, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ) Экспорт

	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если НЕ РаздельныйУчетНДСНаСчете19 Тогда
		
		УчетНДСБП.СформироватьДвиженияНДСПоПриобретеннымЦенностямПоступлениеНаДругойСклад(СписанныеПартииНДС, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияКомплектацияТоваров(ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, Реквизиты, Движения, Отказ) Экспорт
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если НЕ РаздельныйУчетНДСНаСчете19 Тогда
		
		СписанныеПартииНДС = УчетНДСБП.ПолучитьТаблицуСписанныеПартииНДС(
			ТаблицаТовары, ТаблицаСписанныеТоварыБухУчет, Реквизиты, Отказ);
		УчетНДСБП.СформироватьДвиженияВыбытиеТоваров(
			СписанныеПартииНДС, ТаблицаСписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
		УчетНДСБП.СформироватьДвиженияНДСПоПриобретеннымЦенностямПоступлениеКомплекта(
			СписанныеПартииНДС, Реквизиты, Движения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияРазукомплектацияТоваров(ТаблицаКомплектующие, ТаблицаКомплектующиеБухУчет, ТаблицаСписанныеТоварыБухУчет, Реквизиты, Движения, Отказ) Экспорт
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если НЕ РаздельныйУчетНДСНаСчете19 Тогда
		
		ТаблицаКомплекта = ТаблицаКомплектующие.СкопироватьКолонки();
		НоваяСтрока = ТаблицаКомплекта.Добавить();
		НоваяСтрока.ИмяСписка    = "Комплектующие";
		НоваяСтрока.Номенклатура = Реквизиты.Комплект;
		НоваяСтрока.Количество   = Реквизиты.КоличествоКомплектов;
		НоваяСтрока.СчетУчета    = Реквизиты.СчетУчетаКомплектов;
		НоваяСтрока.НомерСтрокиДокумента = 1;
		
		СписанныеПартииНДС = УчетНДСБП.ПолучитьТаблицуСписанныеПартииНДС(
			ТаблицаКомплекта, ТаблицаКомплектующиеБухУчет, Реквизиты, Отказ);
			
		СписанныеПартииНДС.Свернуть("ВидЦенности, ДатаСобытия, Номенклатура, Организация, Партия, Период, СтавкаНДС, 
			| СтавкаНДСДокумента, СчетУчета, СчетУчетаНДС, СчетФактура, НДСБылВключенВСтоимость, НДСВключенВСтоимость, 
			| ИмяСписка, НомерСтрокиДокумента, Склад, БлокироватьДоПодтвержденияСтавки0", 
			"Количество, НДС, Стоимость, СтоимостьСписанияНаРасходы, НДССписанияНаРасходы, 
			|СтрокаКУменьшениюВычетаПриВключенииВСтоимость, СтрокаКВосстановлениюПриВключенииВСтоимость");
			
		УчетНДСБП.СформироватьДвиженияВыбытиеТоваров(
			СписанныеПартииНДС, ТаблицаСписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
		УчетНДСБП.СформироватьДвиженияНДСПоПриобретеннымЦенностямПоступлениеКомплектующих(
			ТаблицаКомплектующие, СписанныеПартииНДС, Реквизиты, Движения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

// ВОЗВРАТ ТОВАРОВ ОТ ПОКУПАТЕЛЯ

Процедура СформироватьДвиженияВозвратТоваровОтПокупателя(ТаблицаВозвращаемыеТовары, Реквизиты, Движения, Отказ) Экспорт
	
	УчетНДСРаздельный.СформироватьДвиженияВозвратТоваровОтПокупателя(ТаблицаВозвращаемыеТовары, Реквизиты, Движения, Отказ);	
	
КонецПроцедуры

// ВОЗВРАТ ТОВАРОВ КОМИССИОНЕРУ

Процедура СформироватьДвиженияВозвратТоваровКомиссионеру(ТаблицаВозвращаемыеТовары, Реквизиты, Движения, Отказ) Экспорт
	
	УчетНДСРаздельный.СформироватьДвиженияВозвратТоваровКомиссионеру(ТаблицаВозвращаемыеТовары, Реквизиты, Движения, Отказ);	
	
КонецПроцедуры


// РАСПРЕДЕЛЕНИЕ НДС

Процедура СформироватьДвиженияНДСПоПриобретеннымЦенностямПоступлениеОтгруженныхТоваров(СписанныеПартииНДС, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ) Экспорт

	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если НЕ РаздельныйУчетНДСНаСчете19 Тогда
		
		УчетНДСБП.СформироватьДвиженияНДСПоПриобретеннымЦенностямПоступлениеОтгруженныхТоваров(СписанныеПартииНДС, СписанныеТоварыБухУчет, Движения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияНДСКосвенныеРасходыСписаниеТоваровНаРасходы(СписанныеПартииНДС, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ) Экспорт
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если НЕ РаздельныйУчетНДСНаСчете19 Тогда
	
		УчетНДСБП.СформироватьДвиженияНДСКосвенныеРасходыСписаниеТоваровНаРасходы(СписанныеПартииНДС, СписанныеТоварыБухУчет, Реквизиты, Движения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияНДСКосвенныеРасходыПередачаМатериаловВЭксплуатацию(СписанныеПартииНДС, ТаблицаМатериалы, Реквизиты, Движения, Отказ) Экспорт
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период);
	
	Если НЕ РаздельныйУчетНДСНаСчете19 Тогда
		
		ТаблицаОтражениеРасходов = УчетНДСБП.ПодготовитьТаблицуОтражениеРасходовПередачаМатериаловВЭксплуатацию(ТаблицаМатериалы);
		УчетНДСБП.СформироватьДвиженияНДСКосвенныеРасходыПередачаМатериаловВЭксплуатацию(
			СписанныеПартииНДС, ТаблицаОтражениеРасходов, Реквизиты, Движения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

//	ДВИЖЕНИЯ РЕГИСТРА "НДС РАЗДЕЛЬНЫЙ УЧЕТ"

Процедура СформироватьПроводкиВычетНДСПоТоварамРеализованнымПоСтавке0(ДанныеДвижений, Движения, Отказ) Экспорт
	
	УчетНДСРаздельный.СформироватьПроводкиВычетНДСПоТоварамРеализованнымПоСтавке0(ДанныеДвижений, Движения, Отказ);
	
КонецПроцедуры

// Процедура обрабатывает записи учетоной политики 
// Для элементов регистра сведений "Учетная политика организации", у которых ресурс "СложныйУчетНДС" имеет значение "Истина",
// а ресурс "РаздельныйУчетНДСДо2014Года" имеет значение "Ложь" ресурс "РаздельныйУчетНДСДо2014Года" устанавливается в значение "Истина"
Процедура ОбработатьУчетнуюПолитикуРаздельныйУчетНДСДо2014Года() Экспорт

	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда // В подчиненных узлах РИБ не выполняется
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиУчетаНДС.СложныйУчетНДС
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|ГДЕ
	|	НастройкиУчетаНДС.СложныйУчетНДС
	|	И НЕ НастройкиУчетаНДС.РаздельныйУчетНДСДо2014Года";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		НаборЗаписей = РегистрыСведений.НастройкиУчетаНДС.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		
		Для каждого СтрокаЗаписи Из НаборЗаписей Цикл
		
			Если СтрокаЗаписи.СложныйУчетНДС И НЕ СтрокаЗаписи.РаздельныйУчетНДСДо2014Года Тогда
				СтрокаЗаписи.РаздельныйУчетНДСДо2014Года = Истина;
			КонецЕсли; 
		
		КонецЦикла; 
		
		НаборЗаписей.Записать(Истина);
		
	КонецЕсли;

КонецПроцедуры

Процедура СформироватьДвиженияНДСПоПриобретеннымЦенностямВвозТоваровИзТаможенногоСоюза(Товары, Реквизиты, Движения, Отказ) Экспорт

	УчетНДСБП.СформироватьДвиженияНДСПоПриобретеннымЦенностямВвозТоваровИзТаможенногоСоюза(Товары, Реквизиты, Движения, Отказ);

КонецПроцедуры

// Определяет реквизиты счета-фактуры по основаниям, подобранным в него
//
//
// Параметры:
//
// СчетФактура            - ДокументСсылка.СчетФактураПолученный, ДокументСсылка.СчетФактураВыданный 
//                          либо одноименные объекты
// Возвращаемое значение: - Результат - структура реквизитов счета-фактуры
//
//
Функция ПараметрыСчетаФактуры(СчетФактура) Экспорт

	Результат = Новый Структура(
		"Организация, Контрагент, КППКонтрагента, КПППродавца, Продавец,
		|ДоговорКонтрагента, СчетФактураБезНДС, НомерСчетаФактурыПродавца,
		|СуммаДокумента, СуммаДокументаКомиссия, ВалютаДокумента, СуммаНДСДокумента, СуммаНДСДокументаКомиссия,
		|СуммаУвеличение, СуммаУвеличениеКомиссия, СуммаУменьшение, СуммаУменьшениеКомиссия, СуммаНДСУвеличение, 
		|СуммаНДСУвеличениеКомиссия, СуммаНДСУменьшение, СуммаНДСУменьшениеКомиссия, РеквизитыОснований, ИдентификаторГосКонтракта");
	
	Результат.СуммаДокумента     = 0;
	Результат.СуммаНДСДокумента  = 0;
	Результат.СуммаУвеличение    = 0;
	Результат.СуммаНДСУвеличение = 0;
	Результат.СуммаУменьшение    = 0;
	Результат.СуммаНДСУменьшение = 0;
	
	Результат.СуммаДокументаКомиссия     = 0;
	Результат.СуммаНДСДокументаКомиссия  = 0;
	Результат.СуммаУвеличениеКомиссия    = 0;
	Результат.СуммаНДСУвеличениеКомиссия = 0;
	Результат.СуммаУменьшениеКомиссия    = 0;
	Результат.СуммаНДСУменьшениеКомиссия = 0;
	Результат.СчетФактураБезНДС          = Ложь;
	
	РеквизитыОснований = Новый ТаблицаЗначений;
	РеквизитыОснований.Колонки.Добавить("ДокументОснование");
	РеквизитыОснований.Колонки.Добавить("НомерИсходногоДокумента",                ОбщегоНазначения.ОписаниеТипаСтрока(50));
	РеквизитыОснований.Колонки.Добавить("ДатаИсходногоДокумента",                 ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	РеквизитыОснований.Колонки.Добавить("НомерИсправленияИсходногоДокумента",     ОбщегоНазначения.ОписаниеТипаЧисло(10, , ДопустимыйЗнак.Неотрицательный));
	РеквизитыОснований.Колонки.Добавить("ДатаИсправленияИсходногоДокумента",      ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	РеквизитыОснований.Колонки.Добавить("СуммаУвеличение",                        ОбщегоНазначения.ОписаниеТипаЧисло(14, 2));
	РеквизитыОснований.Колонки.Добавить("СуммаУменьшение",                        ОбщегоНазначения.ОписаниеТипаЧисло(14, 2));
	РеквизитыОснований.Колонки.Добавить("СуммаНДСУвеличение",                     ОбщегоНазначения.ОписаниеТипаЧисло(14, 2));
	РеквизитыОснований.Колонки.Добавить("СуммаНДСУменьшение",                     ОбщегоНазначения.ОписаниеТипаЧисло(14, 2));
	РеквизитыОснований.Колонки.Добавить("УчитыватьИсправлениеИсходногоДокумента", Новый ОписаниеТипов("Булево"));
	РеквизитыОснований.Колонки.Добавить("ИсходныйДокумент",                       Новый ОписаниеТипов(Документы.ТипВсеСсылки()));
	
	Результат.РеквизитыОснований = РеквизитыОснований;
	
	Если ТипЗнч(СчетФактура)= Тип("ДокументСсылка.СчетФактураПолученный")
		ИЛИ ТипЗнч(СчетФактура)= Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		Ссылка = СчетФактура;
	Иначе
		Ссылка = СчетФактура.Ссылка;
	КонецЕсли;
	
	СводнаяСправка = ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураВыданный") 
		И (СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.СводнаяСправка);
		
	КорректировочнаяСправка = ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураВыданный") 
		И (СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.КорректировочнаяСправка);
	
	ЭтоПолученныйСФ = Ложь;
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураПолученный") тогда
		ЭтоПолученныйСФ = Истина;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураВыданный") 
		И (СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс
		ИЛИ СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента
		ИЛИ СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаСуммовуюРазницу) Тогда
		// Реквизиты заново не определяются, получаются из счета-фактуры
		Результат.Вставить("Организация",        СчетФактура.Организация);
		Результат.Вставить("Контрагент",         СчетФактура.Контрагент);
		Результат.Вставить("ДоговорКонтрагента", СчетФактура.ДоговорКонтрагента);
		Результат.Вставить("ВалютаДокумента",    СчетФактура.ВалютаДокумента);
		Результат.Вставить("СуммаДокумента",     СчетФактура.СуммаДокумента);
		Возврат Результат;
	КонецЕсли;
	
	МассивДокументовОснований = СчетФактура.ДокументыОснования.ВыгрузитьКолонку("ДокументОснование");
	ДокументыОснования = ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивДокументовОснований, Истина);
	
	ТипыОснований = Новый Соответствие();
	Для каждого Основание Из ДокументыОснования Цикл
		Если НЕ ЗначениеЗаполнено(Основание) Тогда
			Продолжить;
		КонецЕсли;
		МассивДокументов = ТипыОснований[ТипЗнч(Основание)];
		Если МассивДокументов = Неопределено Тогда
			МассивДокументов = Новый Массив();
			ТипыОснований.Вставить(ТипЗнч(Основание),МассивДокументов);
		КонецЕсли;
		МассивДокументов.Добавить(Основание);
	КонецЦикла; 

	Если ТипыОснований.Количество() = 0 Тогда
		// Параметры определить нельзя
		Возврат Результат;
	КонецЕсли;
	
	КорректировкаИВозвратВОдномСписке = ПроверитьТипыОснованийНаКорректировки(ТипыОснований);

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", 
		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	Запрос.УстановитьПараметр("ДокументыОснования", ДокументыОснования);
	
	Если СводнаяСправка Тогда
		Запрос.Текст = ТекстЗапросаДокументыРозничныеРеализации();
	ИначеЕсли КорректировочнаяСправка Тогда 
		Запрос.УстановитьПараметр("ИсходнаяСправка", СчетФактура.ИсправляемыйСчетФактура);
		Запрос.Текст = ТекстЗапросаКорректировочныеСправки();
	Иначе
		Для каждого ТипОснования Из ТипыОснований Цикл
			
			ТипДокументаОснования = ТипОснования.Ключ;
			ТекстЗапроса = "";
			
			Если ТипДокументаОснования = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
				
				ТекстЗапроса = ТекстЗапросаАвансовыйОтчет();
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ОказаниеУслуг") Тогда
				
				ТекстЗапроса = ТекстЗапросаОказаниеУслуг();
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ПодтверждениеНулевойСтавкиНДС") Тогда
				
				ТекстЗапроса = ТекстЗапросаПодтверждениеНулевойСтавкиНДС();
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")
				И НЕ ЭтоПолученныйСФ Тогда
				
				Если СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный Тогда
					ТекстЗапроса = ТекстЗапросаКорректировочныеВыданныеСчетаФактурыПоОтчетуКомиссионера();
				Иначе
					ТекстЗапроса = ТекстЗапросаВыданныеСчетаФактурыПокупателямПоОтчетуКомиссионера();
				КонецЕсли;
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ОтчетКомитентуОПродажах")
				И НЕ ЭтоПолученныйСФ 
				И ЗначениеЗаполнено(Ссылка.Продавец) Тогда
				
				ТекстЗапроса = ТекстЗапросаВыданныеСчетаФактурыПеревыставленныеКомитенту();
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
				
				Запрос.УстановитьПараметр("КорректировочныйСчетФактура", 
				СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
				
				ТекстЗапроса = ТекстЗапросаКорректировкаПоступления();
				
				Если СчетФактура.Исправление ИЛИ СчетФактура.ИсправлениеСобственнойОшибки Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДоИзменения", "ДоКорректировки");
				КонецЕсли;
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
				
				Запрос.УстановитьПараметр("КорректировочныйСчетФактура", 
				СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
				
				КорректировкаСчетаФактурыДляКомитента = Ложь;
				РеквизитыСчетаФактуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ДоговорКонтрагента.ВидДоговора, ДокументОснование");
				Если РеквизитыСчетаФактуры.ДоговорКонтрагентаВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку Тогда 
					ИсходныйДокументРеализации = ПолучитьИсправляемыйДокументРеализации(РеквизитыСчетаФактуры.ДокументОснование, Истина);
					Если ТипЗнч(ИсходныйДокументРеализации) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда 
						КорректировкаСчетаФактурыДляКомитента = Истина;
					КонецЕсли;
				КонецЕсли;
				Запрос.УстановитьПараметр("ВидыСчетовФактурИсправляемогоДокумента",
					 ВидыСчетовФактурДляИсправляемогоДокумента());
				Запрос.УстановитьПараметр("КорректировкаСчетаФактурыДляКомитента", КорректировкаСчетаФактурыДляКомитента);
				
				// Сохраним текст запроса
				ТекстЗапроса = Запрос.Текст;
				
				// Запишем временную таблицу в МенеджерВременныхТаблиц
				Запрос.Текст = ТекстЗапросаКорректировкаРеализации(Истина);
				Запрос.Выполнить();
				
				// Восстановим текст запроса
				Запрос.Текст = ТекстЗапроса;
				
				ТекстЗапроса = ТекстЗапросаКорректировкаРеализации();
				
				Если СчетФактура.Исправление Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДоИзменения", "ДоКорректировки");
				КонецЕсли;
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику") И ЭтоПолученныйСФ Тогда
				
				ТекстЗапроса = ТекстЗапросаВозвратПоставщику();
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") И НЕ ЭтоПолученныйСФ Тогда 
				
				СчетаУчетаТоваров = Новый Массив;
				СчетаУчетаТоваров.Добавить(ПланыСчетов.Хозрасчетный.ТоварыНаСкладе);
				СчетаУчетаТоваров.Добавить(ПланыСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию);
				СчетаУчетаТоваров.Добавить(ПланыСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение);
				Запрос.УстановитьПараметр("СчетаУчетаТоваров", СчетаУчетаТоваров);
				Запрос.УстановитьПараметр("ВидыСчетовФактурИсправляемогоДокумента", ВидыСчетовФактурДляИсправляемогоДокумента());
				
				ТекстЗапроса = ТекстЗапросаВозвратОтПокупателя();
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.НачислениеНДСпоСМРхозспособом") тогда 
				
				ТекстЗапроса = ТекстЗапросаНачислениеНДСпоСМР();
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
				
				Запрос.УстановитьПараметр("НачислятьНДСПоОтгрузке", 
					УчетнаяПолитика.НачислятьНДСПоОтгрузке(СчетФактура.Организация, СчетФактура.Дата));
				Запрос.УстановитьПараметр("НачислятьНДСПриПередачеНедвижимости", 
					УчетнаяПолитика.НачислятьНДСПриПередачеНедвижимости(СчетФактура.Организация, СчетФактура.Дата));
				
				ТекстЗапроса = ТекстЗапросаРеализацияОтгруженныхТоваров();
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ПоступлениеДопРасходов") Тогда
				
				ТекстЗапроса = ТекстЗапросаПоступлениеДопРасходов();
				
			ИначеЕсли ЭтоПолученныйСФ 
				И Ссылка.ВозвратЧерезКомиссионера Тогда 
				
				ТекстЗапроса = ТекстЗапросаВозвратТоваровЧерезКомиссионера();
				
			ИначеЕсли ЭтоПолученныйСФ 
				И ТипДокументаОснования = Тип("ДокументСсылка.ДокументРасчетовСКонтрагентом") Тогда
				
				Запрос.УстановитьПараметр("Дата",                       СчетФактура.Дата);
				Запрос.УстановитьПараметр("Организация",                СчетФактура.Организация);
				Запрос.УстановитьПараметр("Контрагент",                 СчетФактура.Контрагент);
				Запрос.УстановитьПараметр("ВалютаДокумента",            СчетФактура.ВалютаДокумента);
				Запрос.УстановитьПараметр("ОснованияДокументыРасчетов", ТипОснования.Значение);
				
				ТекстЗапроса = ТекстЗапросаДокументРасчетовСКонтрагентом();
				
			ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда 
				
				ТекстЗапроса = ТекстЗапросаПоступлениеТоваровУслуг();
				
			Иначе
				
				ТекстЗапроса = ТекстЗапросаНаОснованииМетаданных(
					ТипОснования.Значение, ЭтоПолученныйСФ, Запрос.Параметры, ТипДокументаОснования);
				
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
				Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
					
					Если КорректировкаИВозвратВОдномСписке Тогда
						
						ИндексИтогов = Найти(Запрос.Текст, "ИТОГИ");
						
						Если ИндексИтогов > 0 Тогда
							Запрос.Текст = Лев(Запрос.Текст, ИндексИтогов-1);
						КонецЕсли;
												
					КонецЕсли;
					
					Запрос.Текст = Запрос.Текст + Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
				КонецЕсли;
				Запрос.Текст = Запрос.Текст + ТекстЗапроса;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Если ПустаяСтрока(Запрос.Текст) Тогда
		Возврат Результат;
	Иначе
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Возврат Результат;
		КонецЕсли;
		
		ПерваяСтрока = Истина;
		РазныеОрганизации = Ложь;
		РазныеКонтрагенты = Ложь;
		РазныеДоговоры = Ложь;
		РазныеВалюты = Ложь;
		РазныеПродавцы = Ложь;
		ЕстьКолонкаПродавец = РезультатЗапроса.Колонки.Найти("Продавец") <> Неопределено;
		Корректировка = Ложь;
		ЭтоКорректировочный = Ложь;
		
		Если ТипДокументаОснования = Тип("ДокументСсылка.КорректировкаРеализации") 
			ИЛИ (ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") И Не ЭтоПолученныйСФ)
			ИЛИ ТипДокументаОснования = Тип("ДокументСсылка.КорректировкаПоступления")
			ИЛИ (ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику") И ЭтоПолученныйСФ) 
			ИЛИ (ТипДокументаОснования = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") И НЕ ЭтоПолученныйСФ
				И СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный) Тогда
			
			Корректировка = Истина;
			
		КонецЕсли;
		
		Если Корректировка Тогда
			
			Если ЭтоПолученныйСФ Тогда
				ЭтоКорректировочный = СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный;
			Иначе
				ЭтоКорректировочный = СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;
			КонецЕсли;

			ВыборкаОбщиеИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ВыборкаОбщиеИтоги.Следующий();
			
			Результат.СуммаУвеличение    = ВыборкаОбщиеИтоги.СуммаУвеличение;
			Результат.СуммаУменьшение    = ВыборкаОбщиеИтоги.СуммаУменьшение;
			Результат.СуммаНДСУвеличение = ВыборкаОбщиеИтоги.СуммаНДСУвеличение;
			Результат.СуммаНДСУменьшение = ВыборкаОбщиеИтоги.СуммаНДСУменьшение;
			Результат.СуммаДокумента     = ВыборкаОбщиеИтоги.СуммаДокумента;
			Результат.СуммаНДСДокумента  = ВыборкаОбщиеИтоги.СуммаНДСДокумента;
			
			Результат.СуммаУвеличениеКомиссия    = ВыборкаОбщиеИтоги.СуммаУвеличениеКомиссия;
			Результат.СуммаУменьшениеКомиссия    = ВыборкаОбщиеИтоги.СуммаУменьшениеКомиссия;
			Результат.СуммаНДСУвеличениеКомиссия = ВыборкаОбщиеИтоги.СуммаНДСУвеличениеКомиссия;
			Результат.СуммаНДСУменьшениеКомиссия = ВыборкаОбщиеИтоги.СуммаНДСУменьшениеКомиссия;
			Результат.СуммаДокументаКомиссия     = ВыборкаОбщиеИтоги.СуммаДокументаКомиссия;
			Результат.СуммаНДСДокументаКомиссия  = ВыборкаОбщиеИтоги.СуммаНДСДокументаКомиссия;
			
			ВыборкаДокументы = ВыборкаОбщиеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДокументы.Следующий() Цикл
				
				Если ПерваяСтрока Тогда
					СтрокаРеквизитов = "Организация,Контрагент,ВалютаДокумента,ДоговорКонтрагента, ИдентификаторГосКонтракта" 
						+ ?(ЕстьКолонкаПродавец, ",Продавец", "");
					ЗаполнитьЗначенияСвойств(Результат, ВыборкаДокументы, СтрокаРеквизитов);
					Результат.СчетФактураБезНДС = ВыборкаДокументы.ЕстьНДС = 0;
					ПерваяСтрока = Ложь;
					Если НЕ ЭтоПолученныйСФ Тогда
						Результат.Продавец = УчетНДСБП.ПолучитьПродавцаИзСчетаФактурыОснованияКорректировкиРеализации(ДокументыОснования);
					КонецЕсли;
				КонецЕсли;
				
				РазныеОрганизации = РазныеОрганизации ИЛИ Результат.Организация <> ВыборкаДокументы.Организация;
				РазныеКонтрагенты = РазныеКонтрагенты ИЛИ Результат.Контрагент <> ВыборкаДокументы.Контрагент;
				РазныеВалюты      = РазныеВалюты ИЛИ Результат.ВалютаДокумента <> ВыборкаДокументы.ВалютаДокумента;
				
				РазныеДоговоры = ?(ЭтоПолученныйСФ, Ложь, РазныеДоговоры ИЛИ Результат.ДоговорКонтрагента <> ВыборкаДокументы.ДоговорКонтрагента);
				РазныеПродавцы = ?(ЕстьКолонкаПродавец, РазныеПродавцы ИЛИ Результат.Продавец <> ВыборкаДокументы.Продавец, Ложь);
				
				Если Результат.СчетФактураБезНДС Тогда
					Результат.СчетФактураБезНДС = ВыборкаДокументы.ЕстьНДС = 0;
				КонецЕсли;
				
				Если ЭтоКорректировочный Тогда
					Если ЭтоПолученныйСФ Тогда
						ВыборкаДокументПоступления = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаДокументПоступления.Следующий() Цикл
							
							СтрокаСРеквизитами = РеквизитыОснований.Добавить();
							СтрокаСРеквизитами.ДокументОснование  = ВыборкаДокументПоступления.Документ;
							СтрокаСРеквизитами.СуммаУвеличение    = ВыборкаДокументПоступления.СуммаУвеличение;
							СтрокаСРеквизитами.СуммаУменьшение    = ВыборкаДокументПоступления.СуммаУменьшение;
							СтрокаСРеквизитами.СуммаНДСУвеличение = ВыборкаДокументПоступления.СуммаНДСУвеличение;
							СтрокаСРеквизитами.СуммаНДСУменьшение = ВыборкаДокументПоступления.СуммаНДСУменьшение;

							РеквизитыИсходногоСчетаФактуры = Документы.СчетФактураПолученный.ПолучитьРеквизитыИсходногоСчетаФактурыДляКорректировки(
								ВыборкаДокументПоступления.Документ, СчетФактура,,,ВыборкаДокументПоступления.ДокументПоступления);
							
							Если РеквизитыИсходногоСчетаФактуры <> Неопределено Тогда 

								СтрокаСРеквизитами.НомерИсходногоДокумента = РеквизитыИсходногоСчетаФактуры.НомерСчетаФактуры;
								СтрокаСРеквизитами.ДатаИсходногоДокумента  = РеквизитыИсходногоСчетаФактуры.ДатаСчетаФактуры;
								СтрокаСРеквизитами.УчитыватьИсправлениеИсходногоДокумента = РеквизитыИсходногоСчетаФактуры.Исправление;
								СтрокаСРеквизитами.НомерИсправленияИсходногоДокумента     = РеквизитыИсходногоСчетаФактуры.НомерИсправления;
								СтрокаСРеквизитами.ДатаИсправленияИсходногоДокумента      = РеквизитыИсходногоСчетаФактуры.ДатаИсправления;
								СтрокаСРеквизитами.ИсходныйДокумент                       = РеквизитыИсходногоСчетаФактуры.СчетФактура;
								
							КонецЕсли;
						КонецЦикла;
						
					Иначе
						ВыборкаИсходныеСчетФактуры = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаИсходныеСчетФактуры.Следующий() Цикл
					
							СтрокаСРеквизитами = РеквизитыОснований.Добавить();
							
							СтрокаСРеквизитами.ДокументОснование  = ВыборкаИсходныеСчетФактуры.Документ;
							СтрокаСРеквизитами.ИсходныйДокумент   = ВыборкаИсходныеСчетФактуры.ИсходныйДокумент;
							СтрокаСРеквизитами.СуммаУвеличение    = ВыборкаИсходныеСчетФактуры.СуммаУвеличение;
							СтрокаСРеквизитами.СуммаУменьшение    = ВыборкаИсходныеСчетФактуры.СуммаУменьшение;
							СтрокаСРеквизитами.СуммаНДСУвеличение = ВыборкаИсходныеСчетФактуры.СуммаНДСУвеличение;
							СтрокаСРеквизитами.СуммаНДСУменьшение = ВыборкаИсходныеСчетФактуры.СуммаНДСУменьшение;
							
							РеквизитыИсходногоСчетаФактуры = Документы.СчетФактураВыданный.ПолучитьРеквизитыИсходногоСчетаФактурыДляКорректировки(
							ВыборкаИсходныеСчетФактуры.Документ, СчетФактура,,, ВыборкаИсходныеСчетФактуры.ИсходныйДокумент);
							
							Если РеквизитыИсходногоСчетаФактуры <> Неопределено Тогда 
								
								СтрокаСРеквизитами.НомерИсходногоДокумента = РеквизитыИсходногоСчетаФактуры.НомерСчетаФактуры;
								СтрокаСРеквизитами.ДатаИсходногоДокумента  = РеквизитыИсходногоСчетаФактуры.ДатаСчетаФактуры;
								СтрокаСРеквизитами.УчитыватьИсправлениеИсходногоДокумента = РеквизитыИсходногоСчетаФактуры.Исправление;
								СтрокаСРеквизитами.НомерИсправленияИсходногоДокумента     = РеквизитыИсходногоСчетаФактуры.НомерИсправления;
								СтрокаСРеквизитами.ДатаИсправленияИсходногоДокумента      = РеквизитыИсходногоСчетаФактуры.ДатаИсправления;
								
							КонецЕсли;
							
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				Если ПерваяСтрока Тогда
					ПерваяСтрока = Ложь;
					ЗаполнитьЗначенияСвойств(Результат, Выборка);
					Результат.СчетФактураБезНДС = Выборка.ЕстьНДС = 0;
					Если ТипЗнч(ДокументыОснования[0]) <> Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
						Результат.КППКонтрагента = УчетНДСБП.ПолучитьКПППодразделенияКонтрагента(ДокументыОснования[0], "Грузоотправитель");
					КонецЕсли;
				Иначе
					РазныеОрганизации = РазныеОрганизации ИЛИ Результат.Организация <> Выборка.Организация;
					Если НЕ СводнаяСправка
						И НЕ КорректировочнаяСправка Тогда
						
						РазныеВалюты      = РазныеВалюты ИЛИ Результат.ВалютаДокумента <> Выборка.ВалютаДокумента;
						
						РазныеКонтрагенты = РазныеКонтрагенты ИЛИ Результат.Контрагент <> Выборка.Контрагент;
						РазныеДоговоры    = ?(ЭтоПолученныйСФ, Ложь, РазныеДоговоры ИЛИ Результат.ДоговорКонтрагента <> Выборка.ДоговорКонтрагента);
						РазныеПродавцы    = ?(ЕстьКолонкаПродавец, РазныеПродавцы ИЛИ Результат.Продавец <> Выборка.Продавец, Ложь);
						
						Результат.СуммаДокументаКомиссия    = Результат.СуммаДокументаКомиссия + Выборка.СуммаДокументаКомиссия;
						Результат.СуммаНДСДокументаКомиссия = Результат.СуммаНДСДокументаКомиссия + Выборка.СуммаНДСДокументаКомиссия;
						
						Если Результат.СчетФактураБезНДС Тогда
							Результат.СчетФактураБезНДС = Выборка.ЕстьНДС = 0;
						КонецЕсли;
						
					КонецЕсли;
					
					Результат.СуммаДокумента    = Результат.СуммаДокумента + Выборка.СуммаДокумента;
					Результат.СуммаНДСДокумента = Результат.СуммаНДСДокумента + Выборка.СуммаНДСДокумента;
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		Если РазныеОрганизации ИЛИ РазныеКонтрагенты ИЛИ РазныеДоговоры ИЛИ РазныеВалюты ИЛИ РазныеПродавцы Тогда
			
			ТекстСообщения = НСтр("ru='Реквизиты документов, на основании которых зарегистрирован счет-фактура, не совпадают:'")
				+ ?(РазныеОрганизации, Символы.ПС + НСтр("ru=' - не совпадает организация'"), "")
				+ ?(РазныеКонтрагенты, Символы.ПС + НСтр("ru=' - не совпадает контрагент'"), "")
				+ ?(РазныеДоговоры, Символы.ПС + НСтр("ru=' - не совпадает договор'"), "")
				+ ?(РазныеВалюты, Символы.ПС + НСтр("ru=' - не совпадает валюта документа'"), "")
				+ ?(РазныеПродавцы, Символы.ПС + НСтр("ru=' - не совпадает продавец, от имени которого выписан счета-фактура'"), "") 
				+ Символы.ПС 
				+ НСтр("ru='Необходимо изменить параметры документов-оснований или зарегистрировать по документам с расхождениями отдельные счета-фактуры.'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения); 
			
			Если РазныеОрганизации Тогда
				Результат.Организация = Неопределено;
			КонецЕсли;
			Если РазныеКонтрагенты Тогда
				Результат.Контрагент = Неопределено;
			КонецЕсли;
			Если РазныеВалюты Тогда
				Результат.ВалютаДокумента = Неопределено;
			КонецЕсли;
			Если РазныеДоговоры Тогда
				Результат.ДоговорКонтрагента = Неопределено;
			КонецЕсли;
			Если РазныеПродавцы Тогда
				Результат.Продавец = Неопределено;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Функция возвращает массив видов счетов фактур, 
// по которым будут отбираться счета-фактуры исправляемого документа
// 
// Возвращаемое значение:
// Массив - массив видов счетов фактур
Функция ВидыСчетовФактурДляИсправляемогоДокумента()

	ВидыСчетовФактур = Новый Массив;
	ВидыСчетовФактур.Добавить(Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
	ВидыСчетовФактур.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию);
	
	Возврат ВидыСчетовФактур;
				
КонецФункции

// Функция находит и возвращает исходный счет-фактуру в цепочке исправлений собственных ошибок
Функция ПолучитьИсходныйСчетФактуру(СчетФактура) Экспорт
	
	Если ЗначениеЗаполнено(СчетФактура) Тогда
		РеквизитыСчетаФактуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактура,
			"ИсправлениеСобственнойОшибки, ИсправляемыйСчетФактура");
		
		Если РеквизитыСчетаФактуры.ИсправлениеСобственнойОшибки Тогда
			Возврат ПолучитьИсходныйСчетФактуру(РеквизитыСчетаФактуры.ИсправляемыйСчетФактура)
		Иначе
			Возврат СчетФактура;
		КонецЕсли;
	Иначе
		Возврат СчетФактура;
	КонецЕсли;
	
КонецФункции

Функция СоздатьАктуализироватьИсправлениеСобственнойОшибки(Параметры) Экспорт
	
	Возврат Документы.СчетФактураПолученный.СоздатьАктуализироватьИсправлениеСобственнойОшибки(Параметры);
	
КонецФункции

// ПЕЧАТЬ

Функция СведенияОПодразделенииОрганизации(ПодразделениеОрганизации) Экспорт
	
	Возврат УчетНДСБП.СведенияОПодразделенииОрганизации(ПодразделениеОрганизации);
	
КонецФункции

// Функция производит поиск счетов-фактур полученных с видом "на поступление" или "корректировочный" 
// по документам-основаниям.
//
// Параметры:
//  СписокДокументовОснований - ДокументСсылка.*, Массив, СписокЗначений - Ссылки на документы, для которых надо найти счета-фактуры.
//  ИсключаемыеСФ		- ДокументСсылка.СчетФактураПолученный, Массив, СписокЗначений - Ссылки на счета-фактуры, исключаемые при поиске.
//  ПометкаУдаления		- Булево - Значение пометки счета-фактуры для отбора при поиске.
//	СтруктураОтбора     - Структура - Дополнительный отбор счетов-фактур.
//
// Возвращаемое значение:
//	Соответствие - Содержит:
//		* Ключ - ДокументСсылка.* - Документ основание
//  	* Значение - ДокументСсылка.СчетФактураПолученный - Найденный счет-фактура для документа-основания.
//
Функция НайтиПодчиненныеСчетаФактурыПолученные(СписокДокументовОснований, ИсключаемыеСФ = Неопределено, ПометкаУдаления = Ложь, СтруктураОтбора = Неопределено) Экспорт
	
	Результат = Новый Соответствие;
	
	Если Не ЗначениеЗаполнено(СписокДокументовОснований) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.СчетФактураПолученный) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВидыСчетовФактур = Новый Массив;
	ВидыСчетовФактур.Добавить(Перечисления.ВидСчетаФактурыПолученного.НаПоступление);
	ВидыСчетовФактур.Добавить(Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыСчетовФактур",  ВидыСчетовФактур);
	Запрос.УстановитьПараметр("СписокДокументовОснований", СписокДокументовОснований);
	Запрос.УстановитьПараметр("ИсключаемыеСФ",     ИсключаемыеСФ);
	Запрос.УстановитьПараметр("ПометкаУдаления",   ПометкаУдаления);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СчетФактураПолученный.ДокументОснование КАК ДокументОснование,
	|	СчетФактураПолученный.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.ДокументОснование В(&СписокДокументовОснований)
	|	И СчетФактураПолученный.Ссылка.ПометкаУдаления = &ПометкаУдаления
	|	И СчетФактураПолученный.Ссылка.ВидСчетаФактуры В(&ВидыСчетовФактур)
	|	И НЕ СчетФактураПолученный.Ссылка В (&ИсключаемыеСФ)
	|	И &ДопУсловия";

	// Отбор одной записи.
	Если ТипЗнч(СписокДокументовОснований) = Тип("Массив") 
		ИЛИ ТипЗнч(СписокДокументовОснований) = Тип("СписокЗначений") Тогда
		ВыбиратьПервые = СписокДокументовОснований.Количество() = 1;
	Иначе
		ВыбиратьПервые = Истина;
	КонецЕсли;
	Если НЕ ВыбиратьПервые Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1", "");
	КонецЕсли;
	
	// Фильтрация исключаемых счетов-фактур.
	Если НЕ ЗначениеЗаполнено(ИсключаемыеСФ) Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"НЕ СчетФактураПолученный.Ссылка В (&ИсключаемыеСФ)",
			"ИСТИНА");
	КонецЕсли;
	
	// Дополнительные отборы.
	Если ТипЗнч(СтруктураОтбора) = Тип("Структура") И СтруктураОтбора.Количество() > 0 Тогда
		ТекстУсловия = "";
		Для Каждого КлючЗначение Из СтруктураОтбора Цикл
			Если НЕ ПустаяСтрока(ТекстУсловия) Тогда
				ТекстУсловия = ТекстУсловия + "
				| И ";
			КонецЕсли;
			
			Если ТипЗнч(КлючЗначение.Значение) = Тип("СписокЗначений") 
				ИЛИ ТипЗнч(КлючЗначение.Значение) = Тип("Массив") Тогда
				ТекстУсловия = ТекстУсловия + "СчетФактураПолученный.Ссылка." + КлючЗначение.Ключ + " В (&" + КлючЗначение.Ключ + ")";
			Иначе
				ТекстУсловия = ТекстУсловия + "СчетФактураПолученный.Ссылка." + КлючЗначение.Ключ + " = &" + КлючЗначение.Ключ;
			КонецЕсли;
			
			Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
			
		КонецЦикла;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловия",  ТекстУсловия);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловия",  "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.ДокументОснование, Выборка.СчетФактура);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Функция производит поиск счетов-фактур выданных на реализацию или корректировочного с указанными документами-основаниями.
//
// Параметры:
//  СписокДокументовОснований - ДокументСсылка.*, Массив, СписокЗначений - Ссылки на документы, для которых надо найти счета-фактуры.
//  ИсключаемыеСФ		- ДокументСсылка.СчетФактураВыданный, Массив, СписокЗначений - Ссылки на счета-фактуры, исключаемые при поиске.
//  ПометкаУдаления		- Булево - Значение пометки счета-фактуры для отбора при поиске.
//	СтруктураОтбора     - Структура - Дополнительный отбор счетов-фактур.
//
// Возвращаемое значение:
//	Соответствие - Содержит:
//		* Ключ - ДокументСсылка.* - Документ основание
//  	* Значение - ДокументСсылка.СчетФактураВыданный - Найденный счет-фактура для документа-основания.
//
Функция НайтиПодчиненныеСчетаФактурыВыданныеНаРеализацию(СписокДокументовОснований, ИсключаемыеСФ = Неопределено, ПометкаУдаления = Ложь, СтруктураОтбора = Неопределено) Экспорт

	Результат = Новый Соответствие;
	
	Если НЕ ЗначениеЗаполнено(СписокДокументовОснований) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.СчетФактураВыданный) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВидыСчетовФактур = Новый Массив;
	ВидыСчетовФактур.Добавить(Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию);
	ВидыСчетовФактур.Добавить(Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
	ВидыСчетовФактур.Добавить(Перечисления.ВидСчетаФактурыВыставленного.СводнаяСправка);
	ВидыСчетовФактур.Добавить(Перечисления.ВидСчетаФактурыВыставленного.КорректировочнаяСправка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыСчетовФактур",  ВидыСчетовФактур);
	Запрос.УстановитьПараметр("СписокДокументовОснований", СписокДокументовОснований);
	Запрос.УстановитьПараметр("ИсключаемыеСФ",     ИсключаемыеСФ);
	Запрос.УстановитьПараметр("ПометкаУдаления",   ПометкаУдаления);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ДокументыОснования.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК ДокументыОснования
	|ГДЕ
	|	ДокументыОснования.ДокументОснование В (&СписокДокументовОснований)
	|	И ДокументыОснования.Ссылка.ПометкаУдаления = &ПометкаУдаления
	|	И ДокументыОснования.Ссылка.ВидСчетаФактуры В(&ВидыСчетовФактур)
	|	И НЕ ДокументыОснования.Ссылка В (&ИсключаемыеСФ)
	|	И &ДопУсловия";

	// Отбор одной записи.
	Если ТипЗнч(СписокДокументовОснований) = Тип("Массив") 
		ИЛИ ТипЗнч(СписокДокументовОснований) = Тип("СписокЗначений") Тогда
		ВыбиратьПервые = СписокДокументовОснований.Количество() = 1;
	Иначе
		ВыбиратьПервые = Истина;
	КонецЕсли;
	Если НЕ ВыбиратьПервые Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1", "");
	КонецЕсли;

	// Фильтрация исключаемых счетов-фактур.
	Если НЕ ЗначениеЗаполнено(ИсключаемыеСФ) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"НЕ ДокументыОснования.Ссылка В (&ИсключаемыеСФ)",
			"ИСТИНА");
	КонецЕсли;

	// Дополнительные отборы.
	Если ТипЗнч(СтруктураОтбора) = Тип("Структура") И СтруктураОтбора.Количество() > 0 Тогда
		ТекстУсловия = "";
		Для Каждого КлючЗначение Из СтруктураОтбора Цикл
			Если НЕ ПустаяСтрока(ТекстУсловия) Тогда
				ТекстУсловия = ТекстУсловия + "
				| И ";
			КонецЕсли;
			
			Если ТипЗнч(КлючЗначение.Значение) = Тип("СписокЗначений") 
				ИЛИ ТипЗнч(КлючЗначение.Значение) = Тип("Массив") Тогда
				ТекстУсловия = ТекстУсловия + "ДокументыОснования.Ссылка." + КлючЗначение.Ключ + " В (&" + КлючЗначение.Ключ + ")";
			Иначе
				ТекстУсловия = ТекстУсловия + "ДокументыОснования.Ссылка." + КлючЗначение.Ключ + " = &" + КлючЗначение.Ключ;
			КонецЕсли;
			
			Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
			
		КонецЦикла;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловия",  ТекстУсловия);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловия",  "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.ДокументОснование, Выборка.СчетФактура);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Процедура СформироватьДвиженияВозвратТоваровВРознице(ТаблицаВозвращаемыеТовары, ТаблицаПартииВозвратовРаздельныйУчет, Реквизиты, Движения, Отказ) Экспорт
	
	УчетНДСРаздельный.СформироватьДвиженияВозвратТоваровВРознице(ТаблицаВозвращаемыеТовары, ТаблицаПартииВозвратовРаздельныйУчет, Реквизиты, Движения, Отказ);	
	
КонецПроцедуры

// Определяет модифицирована ли декларация
// Параметры
//    ДанныеОтчета - Структура - см. Документ.РегламентированныйОтчет реквизит "ДанныеОтчета".
// Возвращаемое значение
//    Булево - признак модификации декларации.
Функция ДекларацияПоНДСМодифицирована(ДанныеОтчета) Экспорт
	
	ДекларацияПоНДСМодифицирована = Ложь;
	
	Если ТипЗнч(ДанныеОтчета) <> Тип("Структура") Тогда 
		Возврат ДекларацияПоНДСМодифицирована;
	КонецЕсли;
	
	Если ДанныеОтчета.Свойство("Модифицированность") Тогда
		ДекларацияПоНДСМодифицирована = ДанныеОтчета.Модифицированность;
	КонецЕсли;
	
	Возврат ДекларацияПоНДСМодифицирована;
	
КонецФункции

// Определяет заполнена ли декларация автоматически
// Параметры
//    ДанныеОтчета - Структура - см. Документ.РегламентированныйОтчет реквизит "ДанныеОтчета".
// Возвращаемое значение
//    Булево - признак автозаполнения декларации
Функция ДекларацияПоНДСАвтозаполнена(ДанныеОтчета) Экспорт
	
	ДекларацияПоНДСАвтозаполнена = Ложь;
	
	Если ТипЗнч(ДанныеОтчета) <> Тип("Структура") Тогда 
		Возврат ДекларацияПоНДСАвтозаполнена;
	КонецЕсли;
	
	Если ДанныеОтчета.Свойство("УспешноеАвтозаполнение")
		И ТипЗнч(ДанныеОтчета.УспешноеАвтозаполнение) = Тип("Булево") Тогда
		ДекларацияПоНДСАвтозаполнена = ДанныеОтчета.УспешноеАвтозаполнение;
	КонецЕсли;
	
	Возврат ДекларацияПоНДСАвтозаполнена;
	
КонецФункции

// Функция производит поиск счета-фактуры выданного по реквизитам
// Параметры:
//  РеквизитыСчетаФактуры - структура с реквизитами счета-фактуры выданного,
//                          структура должна обязательно содержать поля Организация, Контрагент, ДатаСчетаФактуры
//
// Возвращаемое значение:
//  Если нашли, то возвращаем ссылку, не нашли - Неопределено
//
Функция НайтиСчетФактуруВыданный(РеквизитыСчетаФактуры) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Организация", РеквизитыСчетаФактуры.Организация);
	Запрос.Параметры.Вставить("Контрагент",  РеквизитыСчетаФактуры.Контрагент);
	Запрос.Параметры.Вставить("ДатаСчетаФактуры", РеквизитыСчетаФактуры.ДатаСчетаФактуры);
	Запрос.Параметры.Вставить("Продавец", РеквизитыСчетаФактуры.Продавец);
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СчетФактураВыданный.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ВидСчетаФактуры В (ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.Корректировочный))
	|	И СчетФактураВыданный.Организация = &Организация
	|	И СчетФактураВыданный.Контрагент = &Контрагент
	|	И СчетФактураВыданный.Продавец = &Продавец
	|	И СчетФактураВыданный.ДатаИсходногоДокумента = &ДатаСчетаФактуры";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет в таблицу сумм документа в разрезе ставок НДС данные о проданных подарочных сертификатах
// и о сумме НДС по сертификатам, определенной расчетным методом
//
//  ДокументОбъект      - ссылка документа "Отчет о розничных продажах"
//  ТаблицаСумм - таблица значений, в которую добавляются суммы по сертификатам.
//		обязательно должна содержать колонки:
//			Сумма		- Сумма документа со всеми налогами
//			СтавкаНДС	- ПеречислениеСсылка.СтавкиНДС
//			СуммаНДС	- Сумма НДС по ставке
//
Процедура ДобавитьСуммуСНДСПоПодарочнымСертификатам(ДокументОбъект, ТаблицаСумм)

	ПлательщикНДС     = УчетнаяПолитика.ПлательщикНДС(ДокументОбъект.Организация, ДокументОбъект.Дата);
	СтавкаНДС         = УчетНДСКлиентСервер.СтавкаНДСПоУмолчанию(ДокументОбъект.Дата, ПлательщикНДС);
	ЗначениеСтавкиНДС = ПолучитьСтавкуНДС(СтавкаНДС);
	
	Для каждого СтрокаТЧ Из ДокументОбъект.ПодарочныеСертификаты Цикл
		
		Сумма = СтрокаТЧ.Сумма;
		СуммаНДС = Окр(Сумма * ЗначениеСтавкиНДС / (100 + ЗначениеСтавкиНДС), 2);
		
		СтрокаТаблицыИтогов = ТаблицаСумм.Найти(СтавкаНДС,"СтавкаНДС");
		
		Если СтрокаТаблицыИтогов = Неопределено тогда
			СтрокаТаблицыИтогов = ТаблицаСумм.Добавить();
			СтрокаТаблицыИтогов.СтавкаНДС = СтавкаНДС;
		КонецЕсли;
		
		СтрокаТаблицыИтогов.Сумма    = СтрокаТаблицыИтогов.Сумма    + Сумма;
		СтрокаТаблицыИтогов.СуммаНДС = СтрокаТаблицыИтогов.СуммаНДС + СуммаНДС;
		
	КонецЦикла;

КонецПроцедуры

Функция ТекстЗапросаДокументРасчетовСКонтрагентом()
	
	Возврат 
	"ВЫБРАТЬ
	|	НДСПредъявленныйОстатки.Организация,
	|	НДСПредъявленныйОстатки.Поставщик КАК Контрагент,
	|	ВЫБОР
	|		КОГДА НДСПредъявленныйОстатки.СчетФактура ССЫЛКА Документ.ДокументРасчетовСКонтрагентом
	|			ТОГДА ВЫРАЗИТЬ(НДСПредъявленныйОстатки.СчетФактура КАК Документ.ДокументРасчетовСКонтрагентом).ДоговорКонтрагента
	|	КОНЕЦ КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток + НДСПредъявленныйОстатки.НДСОстаток КАК СуммаДокумента,
	|	&ВалютаДокумента КАК ВалютаДокумента,
	|	НДСПредъявленныйОстатки.НДСОстаток КАК СуммаНДСДокумента,
	|	0 КАК СуммаДокументаКомиссия,
	|	0 КАК СуммаНДСДокументаКомиссия,
	|	1 КАК ЕстьНДС
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(
	|			&Дата,
	|			Организация = &Организация
	|				И СчетФактура В (&ОснованияДокументыРасчетов)) КАК НДСПредъявленныйОстатки";

КонецФункции

Функция ТекстЗапросаВозвратТоваровЧерезКомиссионера()
	
	Возврат
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеквизитыДокумента.Ссылка КАК Ссылка,
	|	РеквизитыДокумента.Организация КАК Организация,
	|	РеквизитыДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	РеквизитыДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	РеквизитыДокумента.Контрагент,
	|	РеквизитыДокумента.ДоговорКонтрагента
	|ПОМЕСТИТЬ ВТ_ОтчетКомиссионераОПродажахВозвраты
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Возвраты КАК ОтчетКомиссионераОПродажахВозвраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах КАК РеквизитыДокумента
	|		ПО ОтчетКомиссионераОПродажахВозвраты.Ссылка = РеквизитыДокумента.Ссылка
	|ГДЕ
	|	ОтчетКомиссионераОПродажахВозвраты.Ссылка В(&ДокументыОснования)
	|	И ОтчетКомиссионераОПродажахВозвраты.ВыставленСФ
	|	И ОтчетКомиссионераОПродажахВозвраты.СчетФактура = &ТекущийДокумент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.КлючСтроки КАК КлючСтроки,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВложенныйЗапрос.ЕстьНДС) КАК ЕстьНДС
	|ПОМЕСТИТЬ ВТ_Суммы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОтчетКомиссионераОПродажахТоварыВозвращенные.КлючСтроки КАК КлючСтроки,
	|		ОтчетКомиссионераОПродажахТоварыВозвращенные.Сумма КАК Сумма,
	|		ОтчетКомиссионераОПродажахТоварыВозвращенные.СуммаНДС КАК СуммаНДС,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионераОПродажахТоварыВозвращенные.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК ЕстьНДС
	|	ИЗ
	|		ВТ_ОтчетКомиссионераОПродажахВозвраты КАК ВТ_ОтчетКомиссионераОПродажахВозвраты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.ТоварыВозвращенные КАК ОтчетКомиссионераОПродажахТоварыВозвращенные
	|			ПО ВТ_ОтчетКомиссионераОПродажахВозвраты.Ссылка = ОтчетКомиссионераОПродажахТоварыВозвращенные.Ссылка) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.КлючСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОтчетКомиссионераОПродажахВозвраты.Организация КАК Организация,
	|	ОтчетКомиссионераОПродажахПокупатели.Покупатель КАК Продавец,
	|	ОтчетКомиссионераОПродажахПокупатели.ДатаСФ КАК Дата,
	|	ВТ_ОтчетКомиссионераОПродажахВозвраты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	СУММА(ВТ_Суммы.Сумма + ВЫБОР
	|			КОГДА ВТ_ОтчетКомиссионераОПродажахВозвраты.СуммаВключаетНДС
	|				ТОГДА 0
	|			ИНАЧЕ ВТ_Суммы.СуммаНДС
	|		КОНЕЦ) КАК СуммаДокумента,
	|	СУММА(ВТ_Суммы.СуммаНДС) КАК СуммаНДСДокумента,
	|	СУММА(0) КАК СуммаДокументаКомиссия,
	|	СУММА(0) КАК СуммаНДСДокументаКомиссия,
	|	СУММА(ВТ_Суммы.ЕстьНДС) КАК ЕстьНДС,
	|	ВТ_ОтчетКомиссионераОПродажахВозвраты.ВалютаДокумента КАК ВалютаДокумента,
	|	ВТ_ОтчетКомиссионераОПродажахВозвраты.Контрагент
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Возвраты КАК ОтчетКомиссионераОПродажахПокупатели
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОтчетКомиссионераОПродажахВозвраты КАК ВТ_ОтчетКомиссионераОПродажахВозвраты
	|		ПО ОтчетКомиссионераОПродажахПокупатели.Ссылка = ВТ_ОтчетКомиссионераОПродажахВозвраты.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Суммы КАК ВТ_Суммы
	|		ПО ОтчетКомиссионераОПродажахПокупатели.КлючСтроки = ВТ_Суммы.КлючСтроки
	|ГДЕ
	|	ОтчетКомиссионераОПродажахПокупатели.СчетФактура = &ТекущийДокумент
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОтчетКомиссионераОПродажахВозвраты.ВалютаДокумента,
	|	ОтчетКомиссионераОПродажахПокупатели.Покупатель,
	|	ОтчетКомиссионераОПродажахПокупатели.ДатаСФ,
	|	ВТ_ОтчетКомиссионераОПродажахВозвраты.Организация,
	|	ВТ_ОтчетКомиссионераОПродажахВозвраты.ДоговорКонтрагента,
	|	ВТ_ОтчетКомиссионераОПродажахВозвраты.Контрагент";

КонецФункции

Функция ТекстЗапросаПоступлениеДопРасходов()
	
	Возврат 
	"ВЫБРАТЬ
	|	Таблица.Ссылка.Организация КАК Организация,
	|	Таблица.Ссылка.Контрагент КАК Контрагент,
	|	Таблица.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	СУММА(Таблица.Сумма + ВЫБОР
	|			КОГДА Таблица.Ссылка.СуммаВключаетНДС
	|				ТОГДА 0
	|			ИНАЧЕ Таблица.СуммаНДС
	|		КОНЕЦ) КАК СуммаДокумента,
	|	Таблица.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|	СУММА(Таблица.СуммаНДС) КАК СуммаНДСДокумента,
	|	СУММА(0) КАК СуммаДокументаКомиссия,
	|	СУММА(0) КАК СуммаНДСДокументаКомиссия,
	|	СУММА(ВЫБОР
	|			КОГДА Таблица.Ссылка.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК ЕстьНДС
	|ИЗ
	|	Документ.ПоступлениеДопРасходов.Товары КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В(&ДокументыОснования)
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Ссылка,
	|	Таблица.Ссылка.Организация,
	|	Таблица.Ссылка.Контрагент,
	|	Таблица.Ссылка.ДоговорКонтрагента,
	|	Таблица.Ссылка.ВалютаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеДопРасходов.Организация,
	|	ПоступлениеДопРасходов.Контрагент,
	|	ПоступлениеДопРасходов.ДоговорКонтрагента,
	|	"""",
	|	СУММА(ПоступлениеДопРасходов.Сумма + ВЫБОР
	|			КОГДА ПоступлениеДопРасходов.СуммаВключаетНДС
	|				ТОГДА 0
	|			ИНАЧЕ ПоступлениеДопРасходов.СуммаНДС
	|		КОНЕЦ),
	|	ПоступлениеДопРасходов.ВалютаДокумента,
	|	СУММА(ПоступлениеДопРасходов.СуммаНДС),
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(ВЫБОР
	|			КОГДА ПоступлениеДопРасходов.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ)
	|ИЗ
	|	Документ.ПоступлениеДопРасходов КАК ПоступлениеДопРасходов
	|ГДЕ
	|	ПоступлениеДопРасходов.Ссылка В(&ДокументыОснования)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеДопРасходов.Организация,
	|	ПоступлениеДопРасходов.Контрагент,
	|	ПоступлениеДопРасходов.ДоговорКонтрагента,
	|	ПоступлениеДопРасходов.ВалютаДокумента";

КонецФункции

Функция ТекстЗапросаРеализацияОтгруженныхТоваров()
	
	Возврат 
	"ВЫБРАТЬ
	|	РеализацияОтгруженныхТоваров.Организация КАК Организация,
	|	РеализацияОтгруженныхТоваров.Контрагент КАК Контрагент,
	|	РеализацияОтгруженныхТоваров.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РеализацияОтгруженныхТоваров.СуммаДокумента КАК СуммаДокумента,
	|	РеализацияОтгруженныхТоваров.ДокументОтгрузки.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияОтгруженныхТоваров.ДокументОтгрузки КАК ДокументОтгрузки
	|ПОМЕСТИТЬ ВТ_РеализацияОтгруженныхТоваров
	|ИЗ
	|	Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
	|ГДЕ
	|	РеализацияОтгруженныхТоваров.Ссылка В(&ДокументыОснования)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
	|	СУММА(ВЫБОР
	|			КОГДА РеализацияТоваровУслугТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|					ИЛИ РеализацияТоваровУслугТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
	|				ТОГДА РеализацияТоваровУслугТовары.Сумма + ВЫБОР
	|						КОГДА РеализацияТоваровУслугТовары.Ссылка.СуммаВключаетНДС
	|							ТОГДА 0
	|						ИНАЧЕ РеализацияТоваровУслугТовары.СуммаНДС
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаДокументаКомиссия,
	|	СУММА(ВЫБОР
	|			КОГДА РеализацияТоваровУслугТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|					ИЛИ РеализацияТоваровУслугТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
	|				ТОГДА РеализацияТоваровУслугТовары.СуммаНДС
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНДСДокументаКомиссия,
	|	СУММА(ВЫБОР
	|			КОГДА РеализацияТоваровУслугТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА РеализацияТоваровУслугТовары.Сумма
	|			ИНАЧЕ РеализацияТоваровУслугТовары.Сумма + РеализацияТоваровУслугТовары.СуммаНДС
	|		КОНЕЦ) КАК СуммаДокумента,
	|	СУММА(РеализацияТоваровУслугТовары.СуммаНДС) КАК СуммаНДСДокумента,
	|	СУММА(ВЫБОР
	|			КОГДА РеализацияТоваровУслугТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК ЕстьНДС
	|ПОМЕСТИТЬ ВТ_ТаблицыНДС
	|ИЗ
	|	ВТ_РеализацияОтгруженныхТоваров КАК ВТ_РеализацияОтгруженныхТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО ВТ_РеализацияОтгруженныхТоваров.ДокументОтгрузки = РеализацияТоваровУслугТовары.Ссылка
	|ГДЕ
	|	НЕ &НачислятьНДСПоОтгрузке
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугТовары.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугУслуги.Ссылка,
	|	0,
	|	0,
	|	СУММА(ВЫБОР
	|			КОГДА РеализацияТоваровУслугУслуги.Ссылка.СуммаВключаетНДС
	|				ТОГДА РеализацияТоваровУслугУслуги.Сумма
	|			ИНАЧЕ РеализацияТоваровУслугУслуги.Сумма + РеализацияТоваровУслугУслуги.СуммаНДС
	|		КОНЕЦ),
	|	СУММА(РеализацияТоваровУслугУслуги.СуммаНДС),
	|	СУММА(ВЫБОР
	|			КОГДА РеализацияТоваровУслугУслуги.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ)
	|ИЗ
	|	ВТ_РеализацияОтгруженныхТоваров КАК ВТ_РеализацияОтгруженныхТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|		ПО ВТ_РеализацияОтгруженныхТоваров.ДокументОтгрузки = РеализацияТоваровУслугУслуги.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугУслуги.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПередачаОСОС.Ссылка,
	|	СУММА(0),
	|	СУММА(0),
	|	СУММА(ВЫБОР
	|			КОГДА ПередачаОСОС.Ссылка.СуммаВключаетНДС
	|				ТОГДА ПередачаОСОС.Сумма
	|			ИНАЧЕ ПередачаОСОС.Сумма + ПередачаОСОС.СуммаНДС
	|		КОНЕЦ),
	|	СУММА(ПередачаОСОС.СуммаНДС),
	|	СУММА(ВЫБОР
	|			КОГДА ПередачаОСОС.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ)
	|ИЗ
	|	ВТ_РеализацияОтгруженныхТоваров КАК ВТ_РеализацияОтгруженныхТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаОС.ОС КАК ПередачаОСОС
	|		ПО ВТ_РеализацияОтгруженныхТоваров.ДокументОтгрузки = ПередачаОСОС.Ссылка
	|ГДЕ
	|	НЕ &НачислятьНДСПриПередачеНедвижимости
	|
	|СГРУППИРОВАТЬ ПО
	|	ПередачаОСОС.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РеализацияОтгруженныхТоваров.Организация КАК Организация,
	|	ВТ_РеализацияОтгруженныхТоваров.Контрагент КАК Контрагент,
	|	ВТ_РеализацияОтгруженныхТоваров.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДоговорыКонтрагентов.ГосударственныйКонтракт.Код КАК ИдентификаторГосКонтракта,
	|	ВТ_РеализацияОтгруженныхТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	СУММА(ВТ_ТаблицыНДС.СуммаДокумента) КАК СуммаДокумента,
	|	СУММА(ВТ_ТаблицыНДС.СуммаНДСДокумента) КАК СуммаНДСДокумента,
	|	СУММА(ВТ_ТаблицыНДС.СуммаДокументаКомиссия) КАК СуммаДокументаКомиссия,
	|	СУММА(ВТ_ТаблицыНДС.СуммаНДСДокументаКомиссия) КАК СуммаНДСДокументаКомиссия,
	|	СУММА(ВТ_ТаблицыНДС.ЕстьНДС) КАК ЕстьНДС
	|ИЗ
	|	ВТ_РеализацияОтгруженныхТоваров КАК ВТ_РеализацияОтгруженныхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицыНДС КАК ВТ_ТаблицыНДС
	|		ПО ВТ_РеализацияОтгруженныхТоваров.ДокументОтгрузки = ВТ_ТаблицыНДС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ВТ_РеализацияОтгруженныхТоваров.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РеализацияОтгруженныхТоваров.Организация,
	|	ВТ_РеализацияОтгруженныхТоваров.Контрагент,
	|	ВТ_РеализацияОтгруженныхТоваров.ДоговорКонтрагента,
	|	ВТ_РеализацияОтгруженныхТоваров.ВалютаДокумента,
	|	ДоговорыКонтрагентов.ГосударственныйКонтракт.Код";

КонецФункции

Функция ТекстЗапросаНачислениеНДСпоСМР()
	
	Возврат 
	"ВЫБРАТЬ
	|	НачислениеНДСпоСМРхозспособом.Ссылка.Организация,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	СУММА(НачислениеНДСпоСМРхозспособом.СуммаБезНДС + НачислениеНДСпоСМРхозспособом.НДС) КАК СуммаДокумента,
	|	&ВалютаРегламентированногоУчета КАК ВалютаДокумента,
	|	СУММА(НачислениеНДСпоСМРхозспособом.НДС) КАК СуммаНДСДокумента,
	|	СУММА(0) КАК СуммаДокументаКомиссия,
	|	СУММА(0) КАК СуммаНДСДокументаКомиссия,
	|	1 КАК ЕстьНДС
	|ИЗ
	|	Документ.НачислениеНДСпоСМРхозспособом.СМРхозспособом КАК НачислениеНДСпоСМРхозспособом
	|ГДЕ
	|	НачислениеНДСпоСМРхозспособом.Ссылка В(&ДокументыОснования)
	|
	|СГРУППИРОВАТЬ ПО
	|	НачислениеНДСпоСМРхозспособом.Ссылка.Организация,
	|	НачислениеНДСпоСМРхозспособом.НДС";

КонецФункции

Функция ТекстЗапросаКорректировкаРеализации(ТолькоПредварительнуюТаблицу = Ложь)
	
	Если ТолькоПредварительнуюТаблицу Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СчетФактураВыданный.Ссылка КАК ИсходныйСчетФактура,
		|	КорректировкаРеализации.Ссылка КАК ТекущаяКорректировкаРеализации
		|ПОМЕСТИТЬ ВТ_СчетФактураВыданный
		|ИЗ
		|	Документ.СчетФактураВыданный.ДокументыОснования КАК ОснованияСчетаФактуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|		ПО ОснованияСчетаФактуры.Ссылка = СчетФактураВыданный.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|		ПО (КорректировкаРеализации.ДокументРеализации = ОснованияСчетаФактуры.ДокументОснование)
		|ГДЕ
		|	НЕ СчетФактураВыданный.ПометкаУдаления
		|	И СчетФактураВыданный.ВидСчетаФактуры В(&ВидыСчетовФактурИсправляемогоДокумента)
		|	И КорректировкаРеализации.Ссылка В(&ДокументыОснования)
		|	И НЕ(ОснованияСчетаФактуры.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
		|				ИЛИ ОснованияСчетаФактуры.ДокументОснование ССЫЛКА Документ.ОтчетКомитентуОПродажах
		|					И НЕ СчетФактураВыданный.Продавец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
		|	И НЕ КорректировкаРеализации.ДокументРеализации ССЫЛКА Документ.СчетФактураВыданный
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СчетФактураВыданный.Ссылка,
		|	КорректировкаРеализации.Ссылка
		|ИЗ
		|	Документ.СчетФактураВыданный.ДокументыОснования КАК ОснованияСчетаФактуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|		ПО ОснованияСчетаФактуры.Ссылка = СчетФактураВыданный.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|		ПО (КорректировкаРеализации.ДокументРеализации = СчетФактураВыданный.Ссылка)
		|ГДЕ
		|	НЕ СчетФактураВыданный.ПометкаУдаления
		|	И СчетФактураВыданный.ВидСчетаФактуры В(&ВидыСчетовФактурИсправляемогоДокумента)
		|	И КорректировкаРеализации.Ссылка В(&ДокументыОснования)
		|	И НЕ(ОснованияСчетаФактуры.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
		|				ИЛИ ОснованияСчетаФактуры.ДокументОснование ССЫЛКА Документ.ОтчетКомитентуОПродажах
		|					И НЕ СчетФактураВыданный.Продавец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
		|	И КорректировкаРеализации.ДокументРеализации ССЫЛКА Документ.СчетФактураВыданный
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СчетФактураВыданный.Ссылка,
		|	КорректировкаРеализации.Ссылка
		|ИЗ
		|	Документ.СчетФактураВыданный.ДокументыОснования КАК ОснованияСчетаФактуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|		ПО ОснованияСчетаФактуры.Ссылка = СчетФактураВыданный.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|		ПО (КорректировкаРеализации.ДокументРеализации = ОснованияСчетаФактуры.Ссылка)
		|ГДЕ
		|	НЕ СчетФактураВыданный.ПометкаУдаления
		|	И СчетФактураВыданный.ВидСчетаФактуры В(&ВидыСчетовФактурИсправляемогоДокумента)
		|	И КорректировкаРеализации.Ссылка В(&ДокументыОснования)
		|	И (ОснованияСчетаФактуры.ДокументОснование ССЫЛКА Документ.ОтчетКомиссионераОПродажах
		|			ИЛИ ОснованияСчетаФактуры.ДокументОснование ССЫЛКА Документ.ОтчетКомитентуОПродажах
		|				И НЕ СчетФактураВыданный.Продавец = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ТекущаяКорректировкаРеализации";

	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Товары.Ссылка.Организация КАК Организация,
		|	Товары.Ссылка.Контрагент КАК Контрагент,
		|	Товары.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ЕСТЬNULL(ДоговорыКонтрагентов.ГосударственныйКонтракт.Код, """") КАК ИдентификаторГосКонтракта,
		|	Товары.Ссылка.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
		|	Товары.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.Ссылка.СуммаВключаетНДС
		|					ТОГДА ВЫБОР
		|							КОГДА Товары.Сумма - Товары.СуммаДоИзменения > 0
		|								ТОГДА Товары.Сумма - Товары.СуммаДоИзменения
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения) > 0
		|							ТОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения)
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаУвеличение,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.Ссылка.СуммаВключаетНДС
		|					ТОГДА ВЫБОР
		|							КОГДА Товары.Сумма - Товары.СуммаДоИзменения < 0
		|								ТОГДА Товары.СуммаДоИзменения - Товары.Сумма
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения) < 0
		|							ТОГДА Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения - (Товары.Сумма + Товары.СуммаНДС)
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаУменьшение,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения > 0
		|					ТОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДСУвеличение,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения < 0
		|					ТОГДА Товары.СуммаНДСДоИзменения - Товары.СуммаНДС
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДСУменьшение,
		|	ВЫБОР
		|		КОГДА Товары.Ссылка.СуммаВключаетНДС
		|			ТОГДА Товары.Сумма
		|		ИНАЧЕ Товары.Сумма + Товары.СуммаНДС
		|	КОНЕЦ КАК СуммаДокумента,
		|	Товары.СуммаНДС КАК СуммаНДСДокумента,
		|	ВЫБОР
		|		КОГДА Товары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ЕстьНДС,
		|	Товары.Ссылка КАК Документ,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|				ИЛИ НЕ(Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
		|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
		|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение))
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.Ссылка.СуммаВключаетНДС
		|					ТОГДА ВЫБОР
		|							КОГДА Товары.Сумма - Товары.СуммаДоИзменения > 0
		|								ТОГДА Товары.Сумма - Товары.СуммаДоИзменения
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения) > 0
		|							ТОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения)
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаУвеличениеКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|				ИЛИ НЕ(Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
		|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
		|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение))
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.Ссылка.СуммаВключаетНДС
		|					ТОГДА ВЫБОР
		|							КОГДА Товары.Сумма - Товары.СуммаДоИзменения < 0
		|								ТОГДА Товары.СуммаДоИзменения - Товары.Сумма
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения) < 0
		|							ТОГДА Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения - (Товары.Сумма + Товары.СуммаНДС)
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаУменьшениеКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|				ИЛИ НЕ(Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
		|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
		|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение))
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения > 0
		|					ТОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДСУвеличениеКомиссия,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|				ИЛИ НЕ(Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
		|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
		|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение))
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения < 0
		|					ТОГДА Товары.СуммаНДСДоИзменения - Товары.СуммаНДС
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаНДСУменьшениеКомиссия,
		|	ВЫБОР
		|		КОГДА Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
		|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
		|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
		|			ТОГДА ВЫБОР
		|					КОГДА Товары.Ссылка.СуммаВключаетНДС
		|						ТОГДА Товары.Сумма
		|					ИНАЧЕ Товары.Сумма + Товары.СуммаНДС
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаДокументаКомиссия,
		|	ВЫБОР
		|		КОГДА Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
		|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
		|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
		|			ТОГДА Товары.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаНДСДокументаКомиссия,
		|	СчетФактураВыданный.ИсходныйСчетФактура КАК ИсходныйДокумент
		|ИЗ
		|	Документ.КорректировкаРеализации.Товары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ПО Товары.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетФактураВыданный КАК СчетФактураВыданный
		|		ПО Товары.Ссылка = СчетФактураВыданный.ТекущаяКорректировкаРеализации
		|ГДЕ
		|	Товары.Ссылка В(&ДокументыОснования)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Услуги.Ссылка.Организация,
		|	Услуги.Ссылка.Контрагент,
		|	Услуги.Ссылка.ДоговорКонтрагента,
		|	ЕСТЬNULL(ДоговорыКонтрагентов.ГосударственныйКонтракт.Код, """"),
		|	Услуги.Ссылка.ДоговорКонтрагента.ВидДоговора,
		|	Услуги.Ссылка.ВалютаДокумента,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Услуги.Ссылка.СуммаВключаетНДС
		|					ТОГДА ВЫБОР
		|							КОГДА Услуги.Сумма - Услуги.СуммаДоИзменения > 0
		|								ТОГДА Услуги.Сумма - Услуги.СуммаДоИзменения
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА Услуги.Сумма + Услуги.СуммаНДС - (Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения) > 0
		|							ТОГДА Услуги.Сумма + Услуги.СуммаНДС - (Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения)
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Услуги.Ссылка.СуммаВключаетНДС
		|					ТОГДА ВЫБОР
		|							КОГДА Услуги.Сумма - Услуги.СуммаДоИзменения < 0
		|								ТОГДА Услуги.СуммаДоИзменения - Услуги.Сумма
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА Услуги.Сумма + Услуги.СуммаНДС - (Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения) < 0
		|							ТОГДА Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения - (Услуги.Сумма + Услуги.СуммаНДС)
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Услуги.СуммаНДС - Услуги.СуммаНДСДоИзменения > 0
		|					ТОГДА Услуги.СуммаНДС - Услуги.СуммаНДСДоИзменения
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Услуги.СуммаНДС - Услуги.СуммаНДСДоИзменения < 0
		|					ТОГДА Услуги.СуммаНДСДоИзменения - Услуги.СуммаНДС
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА Услуги.Ссылка.СуммаВключаетНДС
		|			ТОГДА Услуги.Сумма
		|		ИНАЧЕ Услуги.Сумма + Услуги.СуммаНДС
		|	КОНЕЦ,
		|	Услуги.СуммаНДС,
		|	ВЫБОР
		|		КОГДА Услуги.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ,
		|	Услуги.Ссылка,
		|	ВЫБОР
		|		КОГДА &КорректировкаСчетаФактурыДляКомитента
		|			ТОГДА ВЫБОР
		|					КОГДА Услуги.Ссылка.СуммаВключаетНДС
		|						ТОГДА ВЫБОР
		|								КОГДА Услуги.Сумма - Услуги.СуммаДоИзменения > 0
		|									ТОГДА Услуги.Сумма - Услуги.СуммаДоИзменения
		|								ИНАЧЕ 0
		|							КОНЕЦ
		|					ИНАЧЕ ВЫБОР
		|							КОГДА Услуги.Сумма + Услуги.СуммаНДС - (Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения) > 0
		|								ТОГДА Услуги.Сумма + Услуги.СуммаНДС - (Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения)
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &КорректировкаСчетаФактурыДляКомитента
		|			ТОГДА ВЫБОР
		|					КОГДА Услуги.Ссылка.СуммаВключаетНДС
		|						ТОГДА ВЫБОР
		|								КОГДА Услуги.Сумма - Услуги.СуммаДоИзменения < 0
		|									ТОГДА Услуги.СуммаДоИзменения - Услуги.Сумма
		|								ИНАЧЕ 0
		|							КОНЕЦ
		|					ИНАЧЕ ВЫБОР
		|							КОГДА Услуги.Сумма + Услуги.СуммаНДС - (Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения) < 0
		|								ТОГДА Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения - (Услуги.Сумма + Услуги.СуммаНДС)
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &КорректировкаСчетаФактурыДляКомитента
		|			ТОГДА ВЫБОР
		|					КОГДА Услуги.СуммаНДС - Услуги.СуммаНДСДоИзменения > 0
		|						ТОГДА Услуги.СуммаНДС - Услуги.СуммаНДСДоИзменения
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &КорректировкаСчетаФактурыДляКомитента
		|			ТОГДА ВЫБОР
		|					КОГДА Услуги.СуммаНДС - Услуги.СуммаНДСДоИзменения < 0
		|						ТОГДА Услуги.СуммаНДСДоИзменения - Услуги.СуммаНДС
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &КорректировкаСчетаФактурыДляКомитента
		|			ТОГДА ВЫБОР
		|					КОГДА Услуги.Ссылка.СуммаВключаетНДС
		|						ТОГДА Услуги.Сумма
		|					ИНАЧЕ Услуги.Сумма + Услуги.СуммаНДС
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &КорректировкаСчетаФактурыДляКомитента
		|			ТОГДА Услуги.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	СчетФактураВыданный.ИсходныйСчетФактура
		|ИЗ
		|	Документ.КорректировкаРеализации.Услуги КАК Услуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ПО Услуги.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетФактураВыданный КАК СчетФактураВыданный
		|		ПО Услуги.Ссылка = СчетФактураВыданный.ТекущаяКорректировкаРеализации
		|ГДЕ
		|	Услуги.Ссылка В(&ДокументыОснования)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АгентскиеУслуги.Ссылка.Организация,
		|	АгентскиеУслуги.Ссылка.Контрагент,
		|	АгентскиеУслуги.Ссылка.ДоговорКонтрагента,
		|	ЕСТЬNULL(ДоговорыКонтрагентов.ГосударственныйКонтракт.Код, """"),
		|	АгентскиеУслуги.Ссылка.ДоговорКонтрагента.ВидДоговора,
		|	АгентскиеУслуги.Ссылка.ВалютаДокумента,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
		|					ТОГДА ВЫБОР
		|							КОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения > 0
		|								ТОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения) > 0
		|							ТОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения)
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
		|					ТОГДА ВЫБОР
		|							КОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения < 0
		|								ТОГДА АгентскиеУслуги.СуммаДоИзменения - АгентскиеУслуги.Сумма
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения) < 0
		|							ТОГДА АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения - (АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС)
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения > 0
		|					ТОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения < 0
		|					ТОГДА АгентскиеУслуги.СуммаНДСДоИзменения - АгентскиеУслуги.СуммаНДС
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
		|			ТОГДА АгентскиеУслуги.Сумма
		|		ИНАЧЕ АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС
		|	КОНЕЦ,
		|	АгентскиеУслуги.СуммаНДС,
		|	ВЫБОР
		|		КОГДА АгентскиеУслуги.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ,
		|	АгентскиеУслуги.Ссылка,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
		|					ТОГДА ВЫБОР
		|							КОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения > 0
		|								ТОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения) > 0
		|							ТОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения)
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
		|					ТОГДА ВЫБОР
		|							КОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения < 0
		|								ТОГДА АгентскиеУслуги.СуммаДоИзменения - АгентскиеУслуги.Сумма
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения) < 0
		|							ТОГДА АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения - (АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС)
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения > 0
		|					ТОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ &КорректировочныйСчетФактура
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения < 0
		|					ТОГДА АгентскиеУслуги.СуммаНДСДоИзменения - АгентскиеУслуги.СуммаНДС
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
		|			ТОГДА АгентскиеУслуги.Сумма
		|		ИНАЧЕ АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС
		|	КОНЕЦ,
		|	АгентскиеУслуги.СуммаНДС,
		|	СчетФактураВыданный.ИсходныйСчетФактура
		|ИЗ
		|	Документ.КорректировкаРеализации.АгентскиеУслуги КАК АгентскиеУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ПО АгентскиеУслуги.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетФактураВыданный КАК СчетФактураВыданный
		|		ПО АгентскиеУслуги.Ссылка = СчетФактураВыданный.ТекущаяКорректировкаРеализации
		|ГДЕ
		|	АгентскиеУслуги.Ссылка В(&ДокументыОснования)
		|ИТОГИ
		|	МАКСИМУМ(ИдентификаторГосКонтракта),
		|	СУММА(СуммаУвеличение),
		|	СУММА(СуммаУменьшение),
		|	СУММА(СуммаНДСУвеличение),
		|	СУММА(СуммаНДСУменьшение),
		|	СУММА(СуммаДокумента),
		|	СУММА(СуммаНДСДокумента),
		|	СУММА(ЕстьНДС),
		|	СУММА(СуммаУвеличениеКомиссия),
		|	СУММА(СуммаУменьшениеКомиссия),
		|	СУММА(СуммаНДСУвеличениеКомиссия),
		|	СУММА(СуммаНДСУменьшениеКомиссия),
		|	СУММА(СуммаДокументаКомиссия),
		|	СУММА(СуммаНДСДокументаКомиссия)
		|ПО
		|	ОБЩИЕ,
		|	Документ,
		|	ИсходныйДокумент";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаКорректировкаПоступления()
	
	Возврат 
	"ВЫБРАТЬ
	|	Товары.Ссылка.Организация КАК Организация,
	|	Товары.Ссылка.Контрагент КАК Контрагент,
	|	Товары.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА Товары.Сумма - Товары.СуммаДоИзменения > 0
	|								ТОГДА Товары.Сумма - Товары.СуммаДоИзменения
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения) > 0
	|							ТОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения)
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаУвеличение,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА Товары.Сумма - Товары.СуммаДоИзменения < 0
	|								ТОГДА Товары.СуммаДоИзменения - Товары.Сумма
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения) < 0
	|							ТОГДА Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения - (Товары.Сумма + Товары.СуммаНДС)
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаУменьшение,
	|	Товары.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения > 0
	|					ТОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения < 0
	|					ТОГДА Товары.СуммаНДСДоИзменения - Товары.СуммаНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаНДСУменьшение,
	|	ВЫБОР
	|		КОГДА Товары.Ссылка.СуммаВключаетНДС
	|			ТОГДА Товары.Сумма
	|		ИНАЧЕ Товары.Сумма + Товары.СуммаНДС
	|	КОНЕЦ КАК СуммаДокумента,
	|	Товары.СуммаНДС КАК СуммаНДСДокумента,
	|	ВЫБОР
	|		КОГДА Товары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЕстьНДС,
	|	Товары.Ссылка КАК Документ,
	|	ВЫБОР
	|		КОГДА (Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦпринятыеНаОтветственноеХранение)
	|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|				И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Товары.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНДСДокументаКомиссия,
	|	ВЫБОР
	|		КОГДА (Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|				И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Ссылка.СуммаВключаетНДС
	|						ТОГДА Товары.Сумма
	|					ИНАЧЕ Товары.Сумма + Товары.СуммаНДС
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДокументаКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|				ИЛИ НЕ((Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|						И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА Товары.Сумма - Товары.СуммаДоИзменения > 0
	|								ТОГДА Товары.Сумма - Товары.СуммаДоИзменения
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения) > 0
	|							ТОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения)
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаУвеличениеКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|				ИЛИ НЕ((Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|						И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА Товары.Сумма - Товары.СуммаДоИзменения < 0
	|								ТОГДА Товары.СуммаДоИзменения - Товары.Сумма
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Сумма + Товары.СуммаНДС - (Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения) < 0
	|							ТОГДА Товары.СуммаДоИзменения + Товары.СуммаНДСДоИзменения - (Товары.Сумма + Товары.СуммаНДС)
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаУменьшениеКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|				ИЛИ НЕ((Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|						И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения > 0
	|					ТОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаНДСУвеличениеКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|				ИЛИ НЕ((Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|						ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|						И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.СуммаНДС - Товары.СуммаНДСДоИзменения < 0
	|					ТОГДА Товары.СуммаНДСДоИзменения - Товары.СуммаНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаНДСУменьшениеКомиссия,
	|	Товары.Ссылка.ДокументПоступления КАК ДокументПоступления,
	|	ВЫБОР
	|		КОГДА Товары.Ссылка.ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(Товары.Ссылка.ДокументПоступления КАК Документ.ПоступлениеТоваровУслуг).ЕстьРасхождения
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьРасхождения,
	|	Товары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	(Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦпринятыеНаОтветственноеХранение)
	|		ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|		ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|		И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК ЭтоКомиссия
	|ПОМЕСТИТЬ ТаблицыКорректировки
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В(&ДокументыОснования)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Услуги.Ссылка.Организация,
	|	Услуги.Ссылка.Контрагент,
	|	Услуги.Ссылка.ДоговорКонтрагента,
	|	"""",
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Услуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА Услуги.Сумма - Услуги.СуммаДоИзменения > 0
	|								ТОГДА Услуги.Сумма - Услуги.СуммаДоИзменения
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Услуги.Сумма + Услуги.СуммаНДС - (Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения) > 0
	|							ТОГДА Услуги.Сумма + Услуги.СуммаНДС - (Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения)
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Услуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА Услуги.Сумма - Услуги.СуммаДоИзменения < 0
	|								ТОГДА Услуги.СуммаДоИзменения - Услуги.Сумма
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Услуги.Сумма + Услуги.СуммаНДС - (Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения) < 0
	|							ТОГДА Услуги.СуммаДоИзменения + Услуги.СуммаНДСДоИзменения - (Услуги.Сумма + Услуги.СуммаНДС)
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ,
	|	Услуги.Ссылка.ВалютаДокумента,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Услуги.СуммаНДС - Услуги.СуммаНДСДоИзменения > 0
	|					ТОГДА Услуги.СуммаНДС - Услуги.СуммаНДСДоИзменения
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Услуги.СуммаНДС - Услуги.СуммаНДСДоИзменения < 0
	|					ТОГДА Услуги.СуммаНДСДоИзменения - Услуги.СуммаНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Услуги.Ссылка.СуммаВключаетНДС
	|			ТОГДА Услуги.Сумма
	|		ИНАЧЕ Услуги.Сумма + Услуги.СуммаНДС
	|	КОНЕЦ,
	|	Услуги.СуммаНДС,
	|	ВЫБОР
	|		КОГДА Услуги.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	Услуги.Ссылка,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	Услуги.Ссылка.ДокументПоступления,
	|	ВЫБОР
	|		КОГДА Услуги.Ссылка.ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(Услуги.Ссылка.ДокументПоступления КАК Документ.ПоступлениеТоваровУслуг).ЕстьРасхождения
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	Услуги.ИдентификаторСтроки,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.КорректировкаПоступления.Услуги КАК Услуги
	|ГДЕ
	|	Услуги.Ссылка В(&ДокументыОснования)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АгентскиеУслуги.Ссылка.Организация,
	|	АгентскиеУслуги.Ссылка.Контрагент,
	|	АгентскиеУслуги.Ссылка.ДоговорКонтрагента,
	|	"""",
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения > 0
	|								ТОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения) > 0
	|							ТОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения)
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения < 0
	|								ТОГДА АгентскиеУслуги.СуммаДоИзменения - АгентскиеУслуги.Сумма
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения) < 0
	|							ТОГДА АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения - (АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС)
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ,
	|	АгентскиеУслуги.Ссылка.ВалютаДокумента,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения > 0
	|					ТОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения < 0
	|					ТОГДА АгентскиеУслуги.СуммаНДСДоИзменения - АгентскиеУслуги.СуммаНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|			ТОГДА АгентскиеУслуги.Сумма
	|		ИНАЧЕ АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС
	|	КОНЕЦ,
	|	АгентскиеУслуги.СуммаНДС,
	|	ВЫБОР
	|		КОГДА АгентскиеУслуги.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	АгентскиеУслуги.Ссылка,
	|	АгентскиеУслуги.СуммаНДС,
	|	ВЫБОР
	|		КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|			ТОГДА АгентскиеУслуги.Сумма
	|		ИНАЧЕ АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения > 0
	|								ТОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения) > 0
	|							ТОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения)
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА АгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА АгентскиеУслуги.Сумма - АгентскиеУслуги.СуммаДоИзменения < 0
	|								ТОГДА АгентскиеУслуги.СуммаДоИзменения - АгентскиеУслуги.Сумма
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС - (АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения) < 0
	|							ТОГДА АгентскиеУслуги.СуммаДоИзменения + АгентскиеУслуги.СуммаНДСДоИзменения - (АгентскиеУслуги.Сумма + АгентскиеУслуги.СуммаНДС)
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения > 0
	|					ТОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &КорректировочныйСчетФактура
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА АгентскиеУслуги.СуммаНДС - АгентскиеУслуги.СуммаНДСДоИзменения < 0
	|					ТОГДА АгентскиеУслуги.СуммаНДСДоИзменения - АгентскиеУслуги.СуммаНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	АгентскиеУслуги.Ссылка.ДокументПоступления,
	|	ВЫБОР
	|		КОГДА АгентскиеУслуги.Ссылка.ДокументПоступления ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(АгентскиеУслуги.Ссылка.ДокументПоступления КАК Документ.ПоступлениеТоваровУслуг).ЕстьРасхождения
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	АгентскиеУслуги.ИдентификаторСтроки,
	|	ИСТИНА
	|ИЗ
	|	Документ.КорректировкаПоступления.АгентскиеУслуги КАК АгентскиеУслуги
	|ГДЕ
	|	АгентскиеУслуги.Ссылка В(&ДокументыОснования)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхТовары.Ссылка.СуммаВключаетНДС
	|			ТОГДА АктОРасхожденияхТовары.Сумма - АктОРасхожденияхТовары.СуммаПоДокументу
	|		ИНАЧЕ АктОРасхожденияхТовары.Сумма + АктОРасхожденияхТовары.СуммаНДС - (АктОРасхожденияхТовары.СуммаПоДокументу + АктОРасхожденияхТовары.СуммаНДСПоДокументу)
	|	КОНЕЦ КАК СуммаПоДокументу,
	|	АктОРасхожденияхТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	АктОРасхожденияхТовары.СуммаНДС - АктОРасхожденияхТовары.СуммаНДСПоДокументу КАК СуммаНДСПоДокументу
	|ПОМЕСТИТЬ АктыККорректировкам
	|ИЗ
	|	Документ.АктОРасхождениях.Товары КАК АктОРасхожденияхТовары
	|ГДЕ
	|	НЕ АктОРасхожденияхТовары.Ссылка.ПометкаУдаления
	|	И АктОРасхожденияхТовары.Ссылка.ДокументПоступления В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ТаблицыКорректировки.ДокументПоступления
	|			ИЗ
	|				ТаблицыКорректировки)
	|	И АктОРасхожденияхТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхУслуги.Ссылка.СуммаВключаетНДС
	|			ТОГДА АктОРасхожденияхУслуги.Сумма - АктОРасхожденияхУслуги.СуммаПоДокументу
	|		ИНАЧЕ АктОРасхожденияхУслуги.Сумма + АктОРасхожденияхУслуги.СуммаНДС - (АктОРасхожденияхУслуги.СуммаПоДокументу + АктОРасхожденияхУслуги.СуммаНДСПоДокументу)
	|	КОНЕЦ,
	|	АктОРасхожденияхУслуги.ИдентификаторСтроки,
	|	АктОРасхожденияхУслуги.СуммаНДС - АктОРасхожденияхУслуги.СуммаНДСПоДокументу
	|ИЗ
	|	Документ.АктОРасхождениях.Услуги КАК АктОРасхожденияхУслуги
	|ГДЕ
	|	НЕ АктОРасхожденияхУслуги.Ссылка.ПометкаУдаления
	|	И АктОРасхожденияхУслуги.Ссылка.ДокументПоступления В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ТаблицыКорректировки.ДокументПоступления
	|			ИЗ
	|				ТаблицыКорректировки)
	|	И АктОРасхожденияхУслуги.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхАгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|			ТОГДА АктОРасхожденияхАгентскиеУслуги.Сумма - АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу
	|		ИНАЧЕ АктОРасхожденияхАгентскиеУслуги.Сумма + АктОРасхожденияхАгентскиеУслуги.СуммаНДС - (АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу + АктОРасхожденияхАгентскиеУслуги.СуммаНДСПоДокументу)
	|	КОНЕЦ,
	|	АктОРасхожденияхАгентскиеУслуги.ИдентификаторСтроки,
	|	АктОРасхожденияхАгентскиеУслуги.СуммаНДС - АктОРасхожденияхАгентскиеУслуги.СуммаНДСПоДокументу
	|ИЗ
	|	Документ.АктОРасхождениях.АгентскиеУслуги КАК АктОРасхожденияхАгентскиеУслуги
	|ГДЕ
	|	НЕ АктОРасхожденияхАгентскиеУслуги.Ссылка.ПометкаУдаления
	|	И АктОРасхожденияхАгентскиеУслуги.Ссылка.ДокументПоступления В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ТаблицыКорректировки.ДокументПоступления
	|			ИЗ
	|				ТаблицыКорректировки)
	|	И АктОРасхожденияхАгентскиеУслуги.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицыКорректировки.Организация КАК Организация,
	|	ТаблицыКорректировки.Контрагент КАК Контрагент,
	|	ТаблицыКорректировки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ТаблицыКорректировки.ИдентификаторГосКонтракта КАК ИдентификаторГосКонтракта,
	|	ВЫБОР
	|		КОГДА НЕ АктыККорректировкам.СуммаПоДокументу ЕСТЬ NULL
	|				И АктыККорректировкам.СуммаПоДокументу > 0
	|			ТОГДА АктыККорректировкам.СуммаПоДокументу
	|		ИНАЧЕ ТаблицыКорректировки.СуммаУвеличение
	|	КОНЕЦ КАК СуммаУвеличение,
	|	ВЫБОР
	|		КОГДА НЕ АктыККорректировкам.СуммаПоДокументу ЕСТЬ NULL
	|				И АктыККорректировкам.СуммаПоДокументу < 0
	|			ТОГДА -АктыККорректировкам.СуммаПоДокументу
	|		ИНАЧЕ ТаблицыКорректировки.СуммаУменьшение
	|	КОНЕЦ КАК СуммаУменьшение,
	|	ТаблицыКорректировки.ВалютаДокумента КАК ВалютаДокумента,
	|	ВЫБОР
	|		КОГДА НЕ АктыККорректировкам.СуммаНДСПоДокументу ЕСТЬ NULL
	|				И АктыККорректировкам.СуммаНДСПоДокументу > 0
	|			ТОГДА АктыККорректировкам.СуммаНДСПоДокументу
	|		ИНАЧЕ ТаблицыКорректировки.СуммаНДСУвеличение
	|	КОНЕЦ КАК СуммаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА НЕ АктыККорректировкам.СуммаНДСПоДокументу ЕСТЬ NULL
	|				И АктыККорректировкам.СуммаНДСПоДокументу < 0
	|			ТОГДА -АктыККорректировкам.СуммаНДСПоДокументу
	|		ИНАЧЕ ТаблицыКорректировки.СуммаНДСУменьшение
	|	КОНЕЦ КАК СуммаНДСУменьшение,
	|	ТаблицыКорректировки.СуммаДокумента КАК СуммаДокумента,
	|	ТаблицыКорректировки.СуммаНДСДокумента КАК СуммаНДСДокумента,
	|	ТаблицыКорректировки.ЕстьНДС КАК ЕстьНДС,
	|	ТаблицыКорректировки.Документ КАК Документ,
	|	ТаблицыКорректировки.СуммаНДСДокументаКомиссия КАК СуммаНДСДокументаКомиссия,
	|	ТаблицыКорректировки.СуммаДокументаКомиссия КАК СуммаДокументаКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ АктыККорректировкам.СуммаПоДокументу ЕСТЬ NULL
	|				И ТаблицыКорректировки.ЭтоКомиссия
	|				И АктыККорректировкам.СуммаПоДокументу > 0
	|			ТОГДА АктыККорректировкам.СуммаПоДокументу
	|		ИНАЧЕ ТаблицыКорректировки.СуммаУвеличениеКомиссия
	|	КОНЕЦ КАК СуммаУвеличениеКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ АктыККорректировкам.СуммаПоДокументу ЕСТЬ NULL
	|				И ТаблицыКорректировки.ЭтоКомиссия
	|				И АктыККорректировкам.СуммаПоДокументу < 0
	|			ТОГДА -АктыККорректировкам.СуммаПоДокументу
	|		ИНАЧЕ ТаблицыКорректировки.СуммаУменьшениеКомиссия
	|	КОНЕЦ КАК СуммаУменьшениеКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ АктыККорректировкам.СуммаНДСПоДокументу ЕСТЬ NULL
	|				И ТаблицыКорректировки.ЭтоКомиссия
	|				И АктыККорректировкам.СуммаНДСПоДокументу > 0
	|			ТОГДА АктыККорректировкам.СуммаНДСПоДокументу
	|		ИНАЧЕ ТаблицыКорректировки.СуммаНДСУвеличениеКомиссия
	|	КОНЕЦ КАК СуммаНДСУвеличениеКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ АктыККорректировкам.СуммаНДСПоДокументу ЕСТЬ NULL
	|				И ТаблицыКорректировки.ЭтоКомиссия
	|				И АктыККорректировкам.СуммаНДСПоДокументу < 0
	|			ТОГДА -АктыККорректировкам.СуммаНДСПоДокументу
	|		ИНАЧЕ ТаблицыКорректировки.СуммаНДСУменьшениеКомиссия
	|	КОНЕЦ КАК СуммаНДСУменьшениеКомиссия,
	|	ТаблицыКорректировки.ДокументПоступления КАК ДокументПоступления
	|ИЗ
	|	ТаблицыКорректировки КАК ТаблицыКорректировки
	|		ЛЕВОЕ СОЕДИНЕНИЕ АктыККорректировкам КАК АктыККорректировкам
	|		ПО ТаблицыКорректировки.ИдентификаторСтроки = АктыККорректировкам.ИдентификаторСтроки
	|ИТОГИ
	|	МАКСИМУМ(Организация),
	|	МАКСИМУМ(Контрагент),
	|	МАКСИМУМ(ДоговорКонтрагента),
	|	МАКСИМУМ(ИдентификаторГосКонтракта),
	|	СУММА(СуммаУвеличение),
	|	СУММА(СуммаУменьшение),
	|	МАКСИМУМ(ВалютаДокумента),
	|	СУММА(СуммаНДСУвеличение),
	|	СУММА(СуммаНДСУменьшение),
	|	СУММА(СуммаДокумента),
	|	СУММА(СуммаНДСДокумента),
	|	СУММА(ЕстьНДС),
	|	СУММА(СуммаНДСДокументаКомиссия),
	|	СУММА(СуммаДокументаКомиссия),
	|	СУММА(СуммаУвеличениеКомиссия),
	|	СУММА(СуммаУменьшениеКомиссия),
	|	СУММА(СуммаНДСУвеличениеКомиссия),
	|	СУММА(СуммаНДСУменьшениеКомиссия)
	|ПО
	|	ОБЩИЕ,
	|	Документ,
	|	ДокументПоступления";

КонецФункции

Функция ТекстЗапросаВыданныеСчетаФактурыПеревыставленныеКомитенту()
	
	Возврат 
	"ВЫБРАТЬ
	|	ОтчетКомитентуОПродажахПоставщики.ДатаСФ КАК ДатаСФ,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ ВТ_КоличествоПоступленийПоСФ
	|ИЗ
	|	Документ.ОтчетКомитентуОПродажах.Поставщики КАК ОтчетКомитентуОПродажахПоставщики
	|ГДЕ
	|	ОтчетКомитентуОПродажахПоставщики.Ссылка В(&ДокументыОснования)
	|	И ОтчетКомитентуОПродажахПоставщики.ПолученСФ
	|	И ОтчетКомитентуОПродажахПоставщики.СчетФактура = &ТекущийДокумент
	|	И ОтчетКомитентуОПродажахПоставщики.Ссылка.ВыписыватьСчетаФактурыСводно
	|	И НЕ ОтчетКомитентуОПродажахПоставщики.Ссылка.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетКомитентуОПродажахПоставщики.ДатаСФ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаСФ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетКомитентуОПродажах.Ссылка КАК Ссылка,
	|	ОтчетКомитентуОПродажах.Ссылка.Организация КАК Организация,
	|	ОтчетКомитентуОПродажах.Ссылка.Контрагент КАК Контрагент,
	|	ОтчетКомитентуОПродажах.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_КоличествоПоступленийПоСФ.Количество, 1) > 1
	|				ИЛИ ДоговорыКонтрагентов.СчетаФактурыОтИмениОрганизации
	|			ТОГДА ОтчетКомитентуОПродажах.Ссылка.Контрагент
	|		ИНАЧЕ ОтчетКомитентуОПродажах.Поставщик
	|	КОНЕЦ КАК Продавец,
	|	ОтчетКомитентуОПродажах.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|	ОтчетКомитентуОПродажах.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ОтчетКомитентуОПродажах.ДатаСФ КАК ДатаСФ,
	|	ОтчетКомитентуОПродажах.КлючСтроки КАК КлючСтроки,
	|	ОтчетКомитентуОПродажах.НомерСчетаФактуры КАК НомерСчетаФактурыПродавца,
	|	ОтчетКомитентуОПродажах.КПППоставщика КАК КПППродавца,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ГосударственныйКонтракт.Код, """") КАК ИдентификаторГосКонтракта
	|ПОМЕСТИТЬ ВТ_ОтчетКомитентуОПродажах
	|ИЗ
	|	Документ.ОтчетКомитентуОПродажах.Поставщики КАК ОтчетКомитентуОПродажах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоПоступленийПоСФ КАК ВТ_КоличествоПоступленийПоСФ
	|		ПО ОтчетКомитентуОПродажах.ДатаСФ = ВТ_КоличествоПоступленийПоСФ.ДатаСФ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ОтчетКомитентуОПродажах.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ОтчетКомитентуОПродажах.Ссылка В(&ДокументыОснования)
	|	И ОтчетКомитентуОПродажах.ПолученСФ
	|	И ОтчетКомитентуОПродажах.СчетФактура = &ТекущийДокумент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	КлючСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетКомитентуОПродажахТовары.Ссылка КАК Ссылка,
	|	ОтчетКомитентуОПродажахТовары.КлючСтроки КАК КлючСтроки,
	|	СУММА(ОтчетКомитентуОПродажахТовары.Сумма) КАК Сумма,
	|	СУММА(ОтчетКомитентуОПродажахТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ОтчетКомитентуОПродажахТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК ЕстьНДС
	|ПОМЕСТИТЬ ВТ_Суммы
	|ИЗ
	|	Документ.ОтчетКомитентуОПродажах.Товары КАК ОтчетКомитентуОПродажахТовары
	|ГДЕ
	|	ОтчетКомитентуОПродажахТовары.Ссылка В(&ДокументыОснования)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетКомитентуОПродажахТовары.Ссылка,
	|	ОтчетКомитентуОПродажахТовары.КлючСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	КлючСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОтчетКомитентуОПродажах.Организация КАК Организация,
	|	ВТ_ОтчетКомитентуОПродажах.Контрагент КАК Контрагент,
	|	ВТ_ОтчетКомитентуОПродажах.ДатаСФ КАК Дата,
	|	ВТ_ОтчетКомитентуОПродажах.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_ОтчетКомитентуОПродажах.ИдентификаторГосКонтракта КАК ИдентификаторГосКонтракта,
	|	ВТ_ОтчетКомитентуОПродажах.Продавец КАК Продавец,
	|	СУММА(ВТ_Суммы.Сумма + ВЫБОР
	|			КОГДА ВТ_ОтчетКомитентуОПродажах.СуммаВключаетНДС
	|				ТОГДА 0
	|			ИНАЧЕ ВТ_Суммы.СуммаНДС
	|		КОНЕЦ) КАК СуммаДокумента,
	|	СУММА(ВТ_Суммы.СуммаНДС) КАК СуммаНДСДокумента,
	|	ВТ_ОтчетКомитентуОПродажах.ВалютаДокумента КАК ВалютаДокумента,
	|	СУММА(ВТ_Суммы.ЕстьНДС) КАК ЕстьНДС,
	|	ВТ_ОтчетКомитентуОПродажах.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
	|	ВТ_ОтчетКомитентуОПродажах.КПППродавца КАК КПППродавца,
	|	ВТ_Суммы.Сумма + ВЫБОР
	|		КОГДА ВТ_ОтчетКомитентуОПродажах.СуммаВключаетНДС
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_Суммы.СуммаНДС
	|	КОНЕЦ КАК СуммаДокументаКомиссия,
	|	ВТ_Суммы.СуммаНДС КАК СуммаНДСДокументаКомиссия
	|ИЗ
	|	ВТ_ОтчетКомитентуОПродажах КАК ВТ_ОтчетКомитентуОПродажах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Суммы КАК ВТ_Суммы
	|		ПО ВТ_ОтчетКомитентуОПродажах.Ссылка = ВТ_Суммы.Ссылка
	|			И ВТ_ОтчетКомитентуОПродажах.КлючСтроки = ВТ_Суммы.КлючСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОтчетКомитентуОПродажах.Организация,
	|	ВТ_ОтчетКомитентуОПродажах.Контрагент,
	|	ВТ_ОтчетКомитентуОПродажах.ДатаСФ,
	|	ВТ_ОтчетКомитентуОПродажах.ДоговорКонтрагента,
	|	ВТ_ОтчетКомитентуОПродажах.Продавец,
	|	ВТ_ОтчетКомитентуОПродажах.ВалютаДокумента,
	|	ВТ_ОтчетКомитентуОПродажах.НомерСчетаФактурыПродавца,
	|	ВТ_ОтчетКомитентуОПродажах.КПППродавца,
	|	ВТ_Суммы.СуммаНДС,
	|	ВТ_Суммы.Сумма + ВЫБОР
	|		КОГДА ВТ_ОтчетКомитентуОПродажах.СуммаВключаетНДС
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_Суммы.СуммаНДС
	|	КОНЕЦ,
	|	ВТ_ОтчетКомитентуОПродажах.ИдентификаторГосКонтракта";

КонецФункции

Функция ТекстЗапросаВыданныеСчетаФактурыПокупателямПоОтчетуКомиссионера()
	
	Возврат 
	"ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажахПокупатели.ДатаСФ КАК ДатаСФ,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ ВТ_КоличествоРеализацийПоСФ
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажахПокупатели
	|ГДЕ
	|	ОтчетКомиссионераОПродажахПокупатели.Ссылка В(&ДокументыОснования)
	|	И ОтчетКомиссионераОПродажахПокупатели.ВыставленСФ
	|	И ОтчетКомиссионераОПродажахПокупатели.СчетФактура = &ТекущийДокумент
	|	И ОтчетКомиссионераОПродажахПокупатели.Ссылка.ВыписыватьСчетаФактурыСводно
	|	И НЕ ОтчетКомиссионераОПродажахПокупатели.Ссылка.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетКомиссионераОПродажахПокупатели.ДатаСФ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаСФ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеквизитыДокумента.Ссылка КАК Ссылка,
	|	РеквизитыДокумента.Организация КАК Организация,
	|	РеквизитыДокумента.Контрагент КАК Контрагент,
	|	РеквизитыДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	РеквизитыДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_КоличествоРеализацийПоСФ.Количество, 1) > 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СводныйСФ
	|ПОМЕСТИТЬ ВТ_ОтчетКомиссионераОПродажах
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах КАК РеквизитыДокумента
	|		ПО ОтчетКомиссионераОПродажах.Ссылка = РеквизитыДокумента.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоРеализацийПоСФ КАК ВТ_КоличествоРеализацийПоСФ
	|		ПО ОтчетКомиссионераОПродажах.ДатаСФ = ВТ_КоличествоРеализацийПоСФ.ДатаСФ
	|ГДЕ
	|	ОтчетКомиссионераОПродажах.Ссылка В(&ДокументыОснования)
	|	И ОтчетКомиссионераОПродажах.ВыставленСФ
	|	И ОтчетКомиссионераОПродажах.СчетФактура = &ТекущийДокумент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.КлючСтроки КАК КлючСтроки,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВложенныйЗапрос.ЕстьНДС) КАК ЕстьНДС,
	|	СУММА(ВложенныйЗапрос.СуммаКомиссия) КАК СуммаКомиссия,
	|	СУММА(ВложенныйЗапрос.СуммаНДСКомиссия) КАК СуммаНДСКомиссия
	|ПОМЕСТИТЬ ВТ_Суммы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОтчетКомиссионераОПродажахТовары.КлючСтроки КАК КлючСтроки,
	|		ОтчетКомиссионераОПродажахТовары.Сумма КАК Сумма,
	|		ОтчетКомиссионераОПродажахТовары.СуммаНДС КАК СуммаНДС,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионераОПродажахТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК ЕстьНДС,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионераОПродажахТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
	|				ТОГДА ОтчетКомиссионераОПродажахТовары.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаКомиссия,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионераОПродажахТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
	|				ТОГДА ОтчетКомиссионераОПродажахТовары.СуммаНДС
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаНДСКомиссия
	|	ИЗ
	|		ВТ_ОтчетКомиссионераОПродажах КАК ВТ_ОтчетКомиссионераОПродажах
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Товары КАК ОтчетКомиссионераОПродажахТовары
	|			ПО ВТ_ОтчетКомиссионераОПродажах.Ссылка = ОтчетКомиссионераОПродажахТовары.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОтчетКомиссионераОПродажахУслуги.КлючСтроки,
	|		ОтчетКомиссионераОПродажахУслуги.Сумма,
	|		ОтчетКомиссионераОПродажахУслуги.СуммаНДС,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионераОПродажахУслуги.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ,
	|		0,
	|		0
	|	ИЗ
	|		ВТ_ОтчетКомиссионераОПродажах КАК ВТ_ОтчетКомиссионераОПродажах
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Услуги КАК ОтчетКомиссионераОПродажахУслуги
	|			ПО ВТ_ОтчетКомиссионераОПродажах.Ссылка = ОтчетКомиссионераОПродажахУслуги.Ссылка) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.КлючСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОтчетКомиссионераОПродажах.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ВТ_ОтчетКомиссионераОПродажах.СводныйСФ
	|			ТОГДА ВТ_ОтчетКомиссионераОПродажах.Контрагент
	|		ИНАЧЕ ОтчетКомиссионераОПродажахПокупатели.Покупатель
	|	КОНЕЦ КАК Контрагент,
	|	ОтчетКомиссионераОПродажахПокупатели.ДатаСФ КАК Дата,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	СУММА(ВТ_Суммы.Сумма + ВЫБОР
	|			КОГДА ВТ_ОтчетКомиссионераОПродажах.СуммаВключаетНДС
	|				ТОГДА 0
	|			ИНАЧЕ ВТ_Суммы.СуммаНДС
	|		КОНЕЦ) КАК СуммаДокумента,
	|	СУММА(ВТ_Суммы.СуммаНДС) КАК СуммаНДСДокумента,
	|	СУММА(ВТ_Суммы.ЕстьНДС) КАК ЕстьНДС,
	|	ВТ_ОтчетКомиссионераОПродажах.ВалютаДокумента КАК ВалютаДокумента,
	|	ВТ_Суммы.СуммаНДСКомиссия КАК СуммаНДСДокументаКомиссия,
	|	ВТ_Суммы.СуммаКомиссия + ВЫБОР
	|		КОГДА ВТ_ОтчетКомиссионераОПродажах.СуммаВключаетНДС
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_Суммы.СуммаНДСКомиссия
	|	КОНЕЦ КАК СуммаДокументаКомиссия
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажахПокупатели
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОтчетКомиссионераОПродажах КАК ВТ_ОтчетКомиссионераОПродажах
	|		ПО ОтчетКомиссионераОПродажахПокупатели.Ссылка = ВТ_ОтчетКомиссионераОПродажах.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Суммы КАК ВТ_Суммы
	|		ПО ОтчетКомиссионераОПродажахПокупатели.КлючСтроки = ВТ_Суммы.КлючСтроки
	|ГДЕ
	|	ОтчетКомиссионераОПродажахПокупатели.СчетФактура = &ТекущийДокумент
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОтчетКомиссионераОПродажах.ВалютаДокумента,
	|	ОтчетКомиссионераОПродажахПокупатели.ДатаСФ,
	|	ВТ_ОтчетКомиссионераОПродажах.Организация,
	|	ВТ_Суммы.СуммаНДСКомиссия,
	|	ВЫБОР
	|		КОГДА ВТ_ОтчетКомиссионераОПродажах.СводныйСФ
	|			ТОГДА ВТ_ОтчетКомиссионераОПродажах.Контрагент
	|		ИНАЧЕ ОтчетКомиссионераОПродажахПокупатели.Покупатель
	|	КОНЕЦ,
	|	ВТ_Суммы.СуммаКомиссия + ВЫБОР
	|		КОГДА ВТ_ОтчетКомиссионераОПродажах.СуммаВключаетНДС
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_Суммы.СуммаНДСКомиссия
	|	КОНЕЦ";

КонецФункции

Функция ТекстЗапросаПодтверждениеНулевойСтавкиНДС()
	
	Возврат 
	"ВЫБРАТЬ
	|	ПодтверждениеНулевойСтавки.Ссылка.Организация КАК Организация,
	|	ПодтверждениеНулевойСтавки.Покупатель КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	СУММА(ПодтверждениеНулевойСтавки.ПродажиСНДС0 + ПодтверждениеНулевойСтавки.СуммаНДС) КАК СуммаДокумента,
	|	СУММА(ПодтверждениеНулевойСтавки.СуммаНДС) КАК СуммаНДСДокумента,
	|	СУММА(0) КАК СуммаДокументаКомиссия,
	|	СУММА(0) КАК СуммаНДСДокументаКомиссия,
	|	&ВалютаРегламентированногоУчета КАК ВалютаДокумента,
	|	ИСТИНА КАК ЕстьНДС
	|ИЗ
	|	Документ.ПодтверждениеНулевойСтавкиНДС.Состав КАК ПодтверждениеНулевойСтавки
	|ГДЕ
	|	ПодтверждениеНулевойСтавки.Ссылка В(&ДокументыОснования)
	|	И ПодтверждениеНулевойСтавки.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НеПодтвержденаСтавка0)
	|	И ПодтверждениеНулевойСтавки.СчетФактураВыданный = &ТекущийДокумент
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодтверждениеНулевойСтавки.Ссылка.Организация,
	|	ПодтверждениеНулевойСтавки.Покупатель";
	
	
КонецФункции

Функция ТекстЗапросаОказаниеУслуг()
	
	Возврат 
	"ВЫБРАТЬ
	|	ОказаниеУслуг.Ссылка.Организация КАК Организация,
	|	ОказаниеУслуг.Контрагент КАК Контрагент,
	|	ОказаниеУслуг.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ГосударственныйКонтракт.Код, """") КАК ИдентификаторГосКонтракта,
	|	СУММА(ОказаниеУслуг.Сумма + ВЫБОР
	|			КОГДА ОказаниеУслуг.Ссылка.СуммаВключаетНДС
	|				ТОГДА 0
	|			ИНАЧЕ ОказаниеУслуг.СуммаНДС
	|		КОНЕЦ) КАК СуммаДокумента,
	|	&ВалютаРегламентированногоУчета КАК ВалютаДокумента,
	|	СУММА(ОказаниеУслуг.СуммаНДС) КАК СуммаНДСДокумента,
	|	СУММА(0) КАК СуммаДокументаКомиссия,
	|	СУММА(0) КАК СуммаНДСДокументаКомиссия,
	|	ВЫБОР
	|		КОГДА ОказаниеУслуг.Ссылка.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЕстьНДС
	|ИЗ
	|	Документ.ОказаниеУслуг.Контрагенты КАК ОказаниеУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ОказаниеУслуг.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ОказаниеУслуг.Ссылка В(&ДокументыОснования)
	|	И ОказаниеУслуг.ВыданСФ
	|	И ОказаниеУслуг.СчетФактура = &ТекущийДокумент
	|
	|СГРУППИРОВАТЬ ПО
	|	ОказаниеУслуг.Контрагент,
	|	ОказаниеУслуг.ДоговорКонтрагента,
	|	ОказаниеУслуг.Ссылка.Организация,
	|	ОказаниеУслуг.СуммаНДС,
	|	ВЫБОР
	|		КОГДА ОказаниеУслуг.Ссылка.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ГосударственныйКонтракт.Код, """")";

КонецФункции

Функция ТекстЗапросаАвансовыйОтчет()
	
	Возврат 
	"ВЫБРАТЬ
	|	АвансовыйОтчетПрочее.Ссылка.Организация КАК Организация,
	|	АвансовыйОтчетПрочее.Поставщик КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	СУММА(АвансовыйОтчетПрочее.Сумма + ВЫБОР
	|			КОГДА АвансовыйОтчетПрочее.Ссылка.СуммаВключаетНДС
	|				ТОГДА 0
	|			ИНАЧЕ АвансовыйОтчетПрочее.СуммаНДС
	|		КОНЕЦ) КАК СуммаДокумента,
	|	АвансовыйОтчетПрочее.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|	СУММА(АвансовыйОтчетПрочее.СуммаНДС) КАК СуммаНДСДокумента,
	|	СУММА(0) КАК СуммаДокументаКомиссия,
	|	СУММА(0) КАК СуммаНДСДокументаКомиссия,
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетПрочее.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЕстьНДС
	|ИЗ
	|	Документ.АвансовыйОтчет.Прочее КАК АвансовыйОтчетПрочее
	|ГДЕ
	|	АвансовыйОтчетПрочее.Ссылка В(&ДокументыОснования)
	|	И АвансовыйОтчетПрочее.ПредъявленСФ
	|	И АвансовыйОтчетПрочее.СчетФактура = &ТекущийДокумент
	|
	|СГРУППИРОВАТЬ ПО
	|	АвансовыйОтчетПрочее.Поставщик,
	|	АвансовыйОтчетПрочее.Ссылка.ВалютаДокумента,
	|	АвансовыйОтчетПрочее.Ссылка.Организация,
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетПрочее.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АвансовыйОтчетТовары.Ссылка.Организация,
	|	АвансовыйОтчетТовары.Поставщик,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	СУММА(АвансовыйОтчетТовары.Сумма + ВЫБОР
	|			КОГДА АвансовыйОтчетТовары.Ссылка.СуммаВключаетНДС
	|				ТОГДА 0
	|			ИНАЧЕ АвансовыйОтчетТовары.СуммаНДС
	|		КОНЕЦ),
	|	АвансовыйОтчетТовары.Ссылка.ВалютаДокумента,
	|	СУММА(АвансовыйОтчетТовары.СуммаНДС),
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|ИЗ
	|	Документ.АвансовыйОтчет.Товары КАК АвансовыйОтчетТовары
	|ГДЕ
	|	АвансовыйОтчетТовары.Ссылка В(&ДокументыОснования)
	|	И АвансовыйОтчетТовары.СчетФактура = &ТекущийДокумент
	|	И АвансовыйОтчетТовары.ПредъявленСФ
	|
	|СГРУППИРОВАТЬ ПО
	|	АвансовыйОтчетТовары.Поставщик,
	|	АвансовыйОтчетТовары.Ссылка.ВалютаДокумента,
	|	АвансовыйОтчетТовары.Ссылка.Организация,
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АвансовыйОтчетБилеты.Ссылка.Организация,
	|	АвансовыйОтчетБилеты.Билет.Перевозчик,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	СУММА(АвансовыйОтчетБилеты.Сумма),
	|	&ВалютаРегламентированногоУчета,
	|	СУММА(АвансовыйОтчетБилеты.СуммаНДС),
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетБилеты.СуммаНДС > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|ИЗ
	|	Документ.АвансовыйОтчет.Билеты КАК АвансовыйОтчетБилеты
	|ГДЕ
	|	АвансовыйОтчетБилеты.Ссылка В(&ДокументыОснования)
	|	И АвансовыйОтчетБилеты.СчетФактура = &ТекущийДокумент
	|
	|СГРУППИРОВАТЬ ПО
	|	АвансовыйОтчетБилеты.Билет.Перевозчик,
	|	АвансовыйОтчетБилеты.Ссылка.Организация,
	|	ВЫБОР
	|		КОГДА АвансовыйОтчетБилеты.СуммаНДС > 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ";

КонецФункции

Функция ТекстЗапросаНаОснованииМетаданных(ДокументыОснования, ЭтоПолученныйСФ, ПараметрыЗапроса, ТипДокументаОснования)

	ИгнорироватьТЧ = Новый Массив;
	ИгнорироватьТЧ.Добавить("ВозвратнаяТара");
	ИгнорироватьТЧ.Добавить("ВыданныеАвансы");
	ИгнорироватьТЧ.Добавить("ДенежныеСредства");
	
	Если ТипДокументаОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ЭтоОтгрузка = (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументыОснования[0], "ВидОперации") 
			= Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности);
			
		Если ЭтоОтгрузка Тогда
			ИгнорироватьТЧ.Добавить("Услуги");
		КонецЕсли;
	КонецЕсли;
	
	МетаданныеДокумента = ДокументыОснования[0].Метаданные();

	ИмяОбъекта = МетаданныеДокумента.Имя;
	ПараметрыЗапроса.Вставить("ДокументОснование_" + ИмяОбъекта, ДокументыОснования);

	ПостфиксСумм = "";
	ТекстЗапросаКомиссияПустой	= "0 КАК СуммаДокументаКомиссия, 0 КАК СуммаНДСДокументаКомиссия,";
	ТекстЗапросаКомиссияПоСчету = "0 КАК СуммаДокументаКомиссия, 0 КАК СуммаНДСДокументаКомиссия,";
	ТекстЗапросаКомиссия		= "0 КАК СуммаДокументаКомиссия, 0 КАК СуммаНДСДокументаКомиссия,";
	Множитель = 1;
	ИмяРеквизитаСтавкаНДС = "СтавкаНДС";
		
	Если ТипДокументаОснования = Тип("ДокументСсылка.ОтчетКомитентуОПродажах")
		ИЛИ ТипДокументаОснования = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") И ЭтоПолученныйСФ Тогда

		ПостфиксСумм = "Вознаграждения";
		ИмяРеквизитаСтавкаНДС = "Ссылка.СтавкаНДСВознаграждения";
	ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") И НЕ ЭтоПолученныйСФ
		ИЛИ ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику") И ЭтоПолученныйСФ Тогда

		Множитель = - 1;
	ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		
		ТекстЗапросаКомиссияПоСчету =
		" СУММА(ВЫБОР
		|			КОГДА Таблица.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
		|					И Таблица.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|				ТОГДА Таблица.Сумма + ВЫБОР
		|						КОГДА Таблица.Ссылка.СуммаВключаетНДС
		|							ТОГДА 0
		|						ИНАЧЕ Таблица.СуммаНДС
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаДокументаКомиссия,
		|	СУММА(ВЫБОР
		|			КОГДА Таблица.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
		|					И Таблица.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|				ТОГДА Таблица.СуммаНДС
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаНДСДокументаКомиссия,";
		
		ТекстЗапросаКомиссия = 
		" СУММА(Таблица.Сумма + ВЫБОР
		|					КОГДА Таблица.Ссылка.СуммаВключаетНДС
		|						ТОГДА 0
		|					ИНАЧЕ Таблица.СуммаНДС КОНЕЦ) КАК СуммаДокументаКомиссия,
		| СУММА(Таблица.СуммаНДС) КАК СуммаНДСДокументаКомиссия,";
				
	ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ТекстЗапросаКомиссияПоСчету = 
		" СУММА(ВЫБОР КОГДА Таблица.СчетУчета = Значение(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
		|						ИЛИ Таблица.СчетУчета = Значение(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
		|					ТОГДА Таблица.Сумма + ВЫБОР
		|						КОГДА Таблица.Ссылка.СуммаВключаетНДС
		|					ТОГДА 0
		|					ИНАЧЕ Таблица.СуммаНДС КОНЕЦ
		|				ИНАЧЕ 0 КОНЕЦ) КАК СуммаДокументаКомиссия,
		| СУММА(ВЫБОР КОГДА Таблица.СчетУчета = Значение(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
		|						ИЛИ Таблица.СчетУчета = Значение(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию) 
		|					ТОГДА Таблица.СуммаНДС  ИНАЧЕ 0 КОНЕЦ) КАК СуммаНДСДокументаКомиссия,";
		
		ТекстЗапросаКомиссия = 
		"ВЫБОР 
		|	КОГДА ДоговорыКонтрагентовКомиссия.УчетАгентскогоНДС И Таблица.Ссылка.КурсВзаиморасчетов > 1 
		|		ТОГДА СУММА(Таблица.Сумма * (Таблица.Ссылка.КурсВзаиморасчетов/
		|			ВЫБОР 
		|				КОГДА Таблица.Ссылка.КратностьВзаиморасчетов = 0
		|					ТОГДА 1
		|				ИНАЧЕ Таблица.Ссылка.КратностьВзаиморасчетов
		|			КОНЕЦ)
		|+ ВЫБОР
		|					КОГДА Таблица.Ссылка.СуммаВключаетНДС
		|						ТОГДА 0
		|					ИНАЧЕ Таблица.СуммаНДС * (Таблица.Ссылка.КурсВзаиморасчетов/
		|								ВЫБОР 
		|									КОГДА Таблица.Ссылка.КратностьВзаиморасчетов = 0
		|										ТОГДА 1
		|									ИНАЧЕ Таблица.Ссылка.КратностьВзаиморасчетов
		|								КОНЕЦ)
		|					КОНЕЦ)
		|ИНАЧЕ
		|	СУММА(Таблица.Сумма + ВЫБОР
		|			КОГДА Таблица.Ссылка.СуммаВключаетНДС
		|			ТОГДА 0
		|			ИНАЧЕ Таблица.СуммаНДС
		|			КОНЕЦ)
		|КОНЕЦ КАК СуммаДокументаКомиссия,
		|ВЫБОР 
		|	КОГДА ДоговорыКонтрагентовКомиссия.УчетАгентскогоНДС И Таблица.Ссылка.КурсВзаиморасчетов > 1 
		|	ТОГДА СУММА(Таблица.СуммаНДС * (Таблица.Ссылка.КурсВзаиморасчетов/
		|								ВЫБОР 
		|									КОГДА Таблица.Ссылка.КратностьВзаиморасчетов = 0
		|										ТОГДА 1
		|									ИНАЧЕ Таблица.Ссылка.КратностьВзаиморасчетов
		|								КОНЕЦ)
		|)
		|	ИНАЧЕ
		|	СУММА(Таблица.СуммаНДС)
		|КОНЕЦ КАК СуммаНДСДокументаКомиссия,";
		
	КонецЕсли;

	ТекстЗапроса = "";

	ТекстСекцииПоместить = "";
	
	Если ЭтоПолученныйСФ Тогда 
		ТекстИдентификаторГосКонтракта = """""";
	Иначе
		ТекстИдентификаторГосКонтракта = "ЕСТЬNULL(ДоговорыКонтрагентов.ГосударственныйКонтракт.Код, """")";
	КонецЕсли;
	
	Для каждого МетаданныеТЧ Из МетаданныеДокумента.ТабличныеЧасти Цикл
		Если ИгнорироватьТЧ.Найти(МетаданныеТЧ.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Если МетаданныеТЧ.Реквизиты.Найти("Сумма") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если МетаданныеДокумента.Реквизиты.Найти("Контрагент") <> Неопределено Тогда
			ТекстКонтрагент         = "Таблица.Ссылка.Контрагент";
			ТекстДоговорКонтрагента = "Таблица.Ссылка.ДоговорКонтрагента";
		Иначе 
			ТекстКонтрагент         = "Значение(Справочник.Контрагенты.ПустаяСсылка)";
			ТекстДоговорКонтрагента = "Значение(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)";
		КонецЕсли;
		
		ТекстДоговорКонтрагентаКомиссия = "Значение(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)";
		ТекстСоединенияДоговорКонтрагентаКомиссия = "";

		Если МетаданныеТЧ.Имя = "Товары" Тогда
			ТекстСуммыКомиссии = ТекстЗапросаКомиссияПоСчету;
		ИначеЕсли МетаданныеТЧ.Имя = "АгентскиеУслуги" Тогда
			ТекстСуммыКомиссии = ТекстЗапросаКомиссия;
			ТекстДоговорКонтрагентаКомиссия = "ДоговорыКонтрагентовКомиссия.УчетАгентскогоНДС";
			ТекстСоединенияДоговорКонтрагентаКомиссия = 
			"ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентовКомиссия
			|		ПО (Таблица.ДоговорКонтрагента = ДоговорыКонтрагентовКомиссия.Ссылка)";
		Иначе 
			ТекстСуммыКомиссии = ТекстЗапросаКомиссияПустой;
		КонецЕсли;
		
		МножительПоТабличнойЧасти = Множитель;
		Если МетаданныеТЧ.Имя = "ТоварыВозвращенные" Тогда
			МножительПоТабличнойЧасти = -1;
		КонецЕсли;
		
		ПараметрыЗапроса.Вставить("Множитель_" + ИмяОбъекта + "_" + МетаданныеТЧ.Имя, МножительПоТабличнойЧасти);
		
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
			ТекстСекцииПоместить = "";
		КонецЕсли;

		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	Таблица.Ссылка.Организация КАК Организация,
		|	" + ТекстКонтрагент + " КАК Контрагент,
		|	" + ТекстДоговорКонтрагента + " КАК ДоговорКонтрагента,
		|	" + ТекстИдентификаторГосКонтракта + " КАК ИдентификаторГосКонтракта,
		|	СУММА(Таблица.Сумма" + ПостфиксСумм + " + ВЫБОР
		|			КОГДА Таблица.Ссылка.СуммаВключаетНДС
		|				ТОГДА 0
		|			ИНАЧЕ Таблица.СуммаНДС" + ПостфиксСумм + "
		|		КОНЕЦ) * &Множитель_" + ИмяОбъекта + "_" + МетаданныеТЧ.Имя + " КАК СуммаДокумента,
		|	Таблица.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
		|	СУММА(Таблица.СуммаНДС" + ПостфиксСумм + ") * &Множитель_" + ИмяОбъекта + "_" + МетаданныеТЧ.Имя + " КАК СуммаНДСДокумента,
		| " + ТекстСуммыКомиссии + "
		|	СУММА(ВЫБОР
		|			КОГДА Таблица." + ИмяРеквизитаСтавкаНДС + " = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|				ТОГДА 0
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК ЕстьНДС
		| " + ТекстСекцииПоместить + "
		|ИЗ
		|	Документ." + МетаданныеДокумента.Имя + "." + МетаданныеТЧ.Имя + " КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ПО Таблица.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
		|		" + ТекстСоединенияДоговорКонтрагентаКомиссия + "
		|ГДЕ
		|	Таблица.Ссылка В (&ДокументОснование_" + ИмяОбъекта + ") 
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Ссылка,
		|	Таблица.Ссылка.Организация,
		|	" + ТекстКонтрагент + ",
		|	" + ТекстДоговорКонтрагента + ",
		|	" + ТекстИдентификаторГосКонтракта + ",
		|	" + ТекстДоговорКонтрагентаКомиссия + ",
		|	Таблица.Ссылка.ВалютаДокумента";
	КонецЦикла;

	Если НЕ ЗначениеЗаполнено(ТекстЗапроса) Тогда // в документе не оказалось табличных частей

		Если МетаданныеДокумента.Реквизиты.Найти("СуммаНДС") <> Неопределено Тогда

			Если МетаданныеДокумента.Реквизиты.Найти("ВалютаДокумента") <> Неопределено Тогда
				ТекстВалюта = "ДокОснование.ВалютаДокумента";
			Иначе
				ТекстВалюта = "&ВалютаРегламентированногоУчета";
			КонецЕсли;
			
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ДокОснование.Организация КАК Организация,
			|	ДокОснование.Контрагент КАК Контрагент,
			|	ДокОснование.ДоговорКонтрагента КАК ДоговорКонтрагента,
			|	" + ТекстИдентификаторГосКонтракта + " КАК ИдентификаторГосКонтракта,
			|	СУММА(ДокОснование.Сумма + ВЫБОР
			|			КОГДА ДокОснование.Ссылка.СуммаВключаетНДС
			|				ТОГДА 0
			|			ИНАЧЕ ДокОснование.СуммаНДС
			|		КОНЕЦ) КАК СуммаДокумента,
			|	" + ТекстВалюта + " КАК ВалютаДокумента,
			|	ДокОснование.СуммаНДС КАК СуммаНДСДокумента,
			|	0 КАК СуммаДокументаКомиссия, 
			|	0 КАК СуммаНДСДокументаКомиссия, 
			|	ВЫБОР
			|		КОГДА ДокОснование.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
			|			ТОГДА 0
			|		ИНАЧЕ 1
			|	КОНЕЦ КАК ЕстьНДС
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + " КАК ДокОснование
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
			|		ПО ДокОснование.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
			|ГДЕ
			|	ДокОснование.Ссылка В (&ДокументОснование_" + МетаданныеДокумента.Имя + ")
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокОснование.Ссылка,
			|	ДокОснование.ДоговорКонтрагента,
			|	" + ТекстИдентификаторГосКонтракта + ",
			|	" + ТекстВалюта + ",
			|	ДокОснование.СуммаНДС,
			|	ДокОснование.Организация,
			|	ДокОснование.Контрагент,
			|	ВЫБОР
			|		КОГДА ДокОснование.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
			|			ТОГДА 0
			|		ИНАЧЕ 1
			|	КОНЕЦ";

		Иначе

			Если (ТипДокументаОснования = Тип("ДокументСсылка.ОтчетКомитентуОПродажах"))
				или ((ТипДокументаОснования = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")) И ЭтоПолученныйСФ) Тогда
				ИдРеквСумма = "СуммаВознаграждения";
			ИначеЕсли (ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") И не ЭтоПолученныйСФ) тогда
				ИдРеквСумма = "СуммаДокумента*(-1)";
			ИначеЕсли (ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику") И ЭтоПолученныйСФ) тогда
				ИдРеквСумма = "СуммаДокумента*(-1)";
			Иначе
				ИдРеквСумма = "СуммаДокумента";
			КонецЕсли;

			ИмяОбъекта = МетаданныеДокумента.Имя;

			ТекстЗапроса =
			"ВЫБРАТЬ "
			+ ИмяОбъекта + ".Организация,"
			+ ?(МетаданныеДокумента.Реквизиты.Найти("Контрагент") <> Неопределено,
					ИмяОбъекта + ".Контрагент,",
					"НЕОПРЕДЕЛЕНО КАК Контрагент,")
			+ ?(МетаданныеДокумента.Реквизиты.Найти("ДоговорКонтрагента") <> Неопределено,
					ИмяОбъекта + ".ДоговорКонтрагента КАК ДоговорКонтрагента,",
					"НЕОПРЕДЕЛЕНО КАК ДоговорКонтрагента,")
			+ """"" КАК ИдентификаторГосКонтракта,"
			+ ?(МетаданныеДокумента.Реквизиты.Найти(ИдРеквСумма) <> Неопределено,
					ИмяОбъекта + "." + ИдРеквСумма + " Как СуммаДокумента,",
					"0 Как СуммаДокумента,")
			+ ?(МетаданныеДокумента.Реквизиты.Найти("ВалютаДокумента") <> Неопределено,
					ИмяОбъекта + ".ВалютаДокумента Как ВалютаДокумента,",
					"НЕОПРЕДЕЛЕНО КАК ВалютаДокумента,")
			+ "0 Как СуммаДокументаКомиссия,"
			+ "0 Как СуммаНДСДокументаКомиссия,"
			+ "0 Как СуммаНДСДокумента,"
			+ "1 Как ЕстьНДС
			|ИЗ
			|	Документ." + ИмяОбъекта + " КАК " + ИмяОбъекта + "
			|ГДЕ
			|	" + ИмяОбъекта + ".Ссылка В (&ДокументОснование_" + ИмяОбъекта + ")";

		КонецЕсли;

	КонецЕсли;

	Если МетаданныеДокумента.Реквизиты.Найти("ПодразделениеОрганизации") = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Таблица.Ссылка.ПодразделениеОрганизации", "Неопределено");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДокументОснование.ПодразделениеОрганизации", "Неопределено");
	КонецЕсли;
	
	Если ТипДокументаОснования = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ТекстЗапросаИсключениеНалоговогоАгента = 
		"ВЫБРАТЬ
		|	НДСЗаписиКнигиПродаж.Организация КАК Организация,
		|	НДСЗаписиКнигиПродаж.Покупатель КАК Контрагент,
		|	РеализацияТоваровУслуг.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	"""" КАК ИдентификаторГосКонтракта,
		|	0 КАК СуммаДокумента,
		|	РеализацияТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
		|	0 КАК СуммаНДСДокумента,
		|	-СУММА(НДСЗаписиКнигиПродаж.СуммаБезНДС + НДСЗаписиКнигиПродаж.НДС) КАК СуммаДокументаКомиссия,
		|	-СУММА(НДСЗаписиКнигиПродаж.НДС) КАК СуммаНДСДокументаКомиссия,
		|	1 КАК ЕстьНДС
		|ИЗ
		|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО НДСЗаписиКнигиПродаж.Регистратор = РеализацияТоваровУслуг.Ссылка
		|ГДЕ
		|	НДСЗаписиКнигиПродаж.Активность
		|	И НДСЗаписиКнигиПродаж.Регистратор В(&ДокументыОснования)
		|	И НДСЗаписиКнигиПродаж.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентКомитент)
		|
		|СГРУППИРОВАТЬ ПО
		|	НДСЗаписиКнигиПродаж.Покупатель,
		|	НДСЗаписиКнигиПродаж.Организация,
		|	РеализацияТоваровУслуг.ДоговорКонтрагента,
		|	РеализацияТоваровУслуг.ВалютаДокумента";
		
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС 
			+ ТекстЗапросаИсключениеНалоговогоАгента + Символы.ПС + Символы.ПС;
		КонецЕсли;
		
	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции

Функция НовыйПредставлениеСчетаФактуры()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ДатаНомер"    , "");
	Результат.Вставить("НомерДата"    , "");
	Результат.Вставить("НомерОтДата"  , "");
	Результат.Вставить("Дата"         , "");
	Результат.Вставить("Номер"        , "");
	Результат.Вставить("Представление", "");
	
	Результат.Вставить("НомерДатаКорректировочный"  , "");
	Результат.Вставить("НомерОтДатаКорректировочный", "");
	Результат.Вставить("ДатаКорректировочный"       , "");
	Результат.Вставить("НомерКорректировочный"      , "");
	
	Результат.Вставить("НомерДатаИсправленный"  , "");
	Результат.Вставить("НомерОтДатаИсправленный", "");
	Результат.Вставить("ДатаИсправленный"       , "");
	Результат.Вставить("НомерИсправленный"      , "");
	
	Результат.Вставить("НомерДатаИсправленныйКорректировочный"  , "");
	Результат.Вставить("НомерОтДатаИсправленныйКорректировочный", "");
	Результат.Вставить("ДатаИсправленныйКорректировочный"       , "");
	Результат.Вставить("НомерИсправленныйКорректировочный"      , "");
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеИсправленногоКорректировочногоСчетаФактуры(ПредставлениеСчетаФактуры, ЗаписьКниги)
	
	Если ЗначениеЗаполнено(СокрЛП(ЗаписьКниги.НомерКорректировки)) Тогда
		ПредставлениеСчетаФактуры.НомерКорректировочный = СокрЛП(ЗаписьКниги.НомерКорректировки);
		ПредставлениеСчетаФактуры.ДатаКорректировочный = Формат(ЗаписьКниги.ДатаКорректировки, "ДФ=dd.MM.yyyy");
		ПредставлениеСчетаФактуры.НомерДатаКорректировочный = СтрШаблон(НСтр("ru='%1;%2'"), 
			ПредставлениеСчетаФактуры.НомерКорректировочный, ПредставлениеСчетаФактуры.ДатаКорректировочный);
		ПредставлениеСчетаФактуры.НомерОтДатаКорректировочный = СтрШаблон(НСтр("ru='%1 от %2'"), 
			ПредставлениеСчетаФактуры.НомерКорректировочный, ПредставлениеСчетаФактуры.ДатаКорректировочный);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СокрЛП(ЗаписьКниги.НомерИсправления)) Тогда
		ПредставлениеСчетаФактуры.НомерИсправленный = СокрЛП(ЗаписьКниги.НомерИсправления);
		ПредставлениеСчетаФактуры.ДатаИсправленный = Формат(ЗаписьКниги.ДатаИсправления, "ДФ=dd.MM.yyyy");
		ПредставлениеСчетаФактуры.НомерДатаИсправленный = СтрШаблон(НСтр("ru='%1;%2'"), 
			ПредставлениеСчетаФактуры.НомерИсправленный, ПредставлениеСчетаФактуры.ДатаИсправленный);
		ПредставлениеСчетаФактуры.НомерОтДатаИсправленный = СтрШаблон(НСтр("ru='%1 от %2'"), 
			ПредставлениеСчетаФактуры.НомерИсправленный, ПредставлениеСчетаФактуры.ДатаИсправленный);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СокрЛП(ЗаписьКниги.НомерИсправленияКорректировки)) Тогда
		ПредставлениеСчетаФактуры.НомерИсправленныйКорректировочный = СокрЛП(ЗаписьКниги.НомерИсправленияКорректировки);
		ПредставлениеСчетаФактуры.ДатаИсправленныйКорректировочный = 
			Формат(ЗаписьКниги.ДатаИсправленияКорректировки, "ДФ=dd.MM.yyyy");
		ПредставлениеСчетаФактуры.НомерДатаИсправленныйКорректировочный = СтрШаблон(НСтр("ru='%1;%2'"),
			ПредставлениеСчетаФактуры.НомерИсправленныйКорректировочный, 
			ПредставлениеСчетаФактуры.ДатаИсправленныйКорректировочный);
		ПредставлениеСчетаФактуры.НомерОтДатаИсправленныйКорректировочный = СтрШаблон(НСтр("ru='%1 от %2'"),
			ПредставлениеСчетаФактуры.НомерИсправленныйКорректировочный, 
			ПредставлениеСчетаФактуры.ДатаИсправленныйКорректировочный);
	КонецЕсли;

	Возврат ПредставлениеСчетаФактуры;
	
КонецФункции

Функция ТекстЗапросаДокументыРозничныеРеализации()
	
	Возврат 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.Организация КАК Организация,
	|	СУММА(НДСЗаписиКнигиПродаж.СуммаБезНДС + НДСЗаписиКнигиПродаж.НДС) КАК СуммаДокумента,
	|	СУММА(НДСЗаписиКнигиПродаж.НДС) КАК СуммаНДСДокумента,
	|	СУММА(1) КАК ЕстьНДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|ГДЕ
	|	НДСЗаписиКнигиПродаж.СчетФактура В(&ДокументыОснования)
	|	И НДСЗаписиКнигиПродаж.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродаж.Организация";
	
КонецФункции

Функция ТекстЗапросаКорректировочныеСправки()
	
	// Вычеты для корректировочной справки регистрируются документом возврата.
	// Отберем вычеты, зарегистрированные документами-основаниями по счетам-фактурам из исходной справки.
	Возврат
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ОснованияИсходнойСправки
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ПО СчетФактураВыданныйДокументыОснования.ДокументОснование = ОтчетОРозничныхПродажах.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &ИсходнаяСправка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|		ПО СчетФактураВыданныйДокументыОснования.ДокументОснование = ОтчетКомиссионераОПродажах.Ссылка
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &ИсходнаяСправка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВозвратыПоОРП
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Сделка В
	|			(ВЫБРАТЬ
	|				ОснованияИсходнойСправки.ДокументОснование
	|			ИЗ
	|				ОснованияИсходнойСправки)
	|	И ВозвратТоваровОтПокупателя.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОснованияИсходнойСправки.ДокументОснование КАК СчетФактура
	|ПОМЕСТИТЬ СчетаФактуры
	|ИЗ
	|	ОснованияИсходнойСправки КАК ОснованияИсходнойСправки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратыПоОРП.Ссылка
	|ИЗ
	|	ВозвратыПоОРП КАК ВозвратыПоОРП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.Организация КАК Организация,
	|	СУММА(НДСЗаписиКнигиПокупок.СуммаБезНДС + НДСЗаписиКнигиПокупок.НДС) КАК СуммаДокумента,
	|	СУММА(НДСЗаписиКнигиПокупок.НДС) КАК СуммаНДСДокумента,
	|	СУММА(1) КАК ЕстьНДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписиКнигиПокупок
	|ГДЕ
	|	НДСЗаписиКнигиПокупок.Регистратор В(&ДокументыОснования)
	|	И НДСЗаписиКнигиПокупок.СчетФактура В
	|			(ВЫБРАТЬ
	|				СчетаФактуры.СчетФактура
	|			ИЗ
	|				СчетаФактуры)
	|	И НДСЗаписиКнигиПокупок.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПокупок.Организация";
	
КонецФункции

Функция ПроверитьТипыОснованийНаКорректировки(ТипыОснований)

	Если ТипыОснований.Количество() < 2 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьКорректировкаРеализации = 
		?(ТипыОснований[Тип("ДокументСсылка.КорректировкаРеализации")] = Неопределено, Ложь, Истина);
		
	ЕстьВозвратОтПокупателя = 
		?(ТипыОснований[Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")] = Неопределено, Ложь, Истина);

	ЕстьКорректировкаПоступления = 
		?(ТипыОснований[Тип("ДокументСсылка.КорректировкаПоступления")] = Неопределено, Ложь, Истина);
		
	ЕстьВозвратПоставщику = 
		?(ТипыОснований[Тип("ДокументСсылка.ВозвратТоваровПоставщику")] = Неопределено, Ложь, Истина);
	
	Если (ЕстьВозвратОтПокупателя И ЕстьКорректировкаРеализации)
		ИЛИ (ЕстьКорректировкаПоступления И ЕстьВозвратПоставщику) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

Функция ТекстЗапросаВозвратОтПокупателя()
	Возврат 
	"ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Организация КАК Организация,
	|	ВозвратТоваровОтПокупателя.Контрагент КАК Контрагент,
	|	ВозвратТоваровОтПокупателя.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЕСТЬNULL(ВозвратТоваровОтПокупателя.ДоговорКонтрагента.ГосударственныйКонтракт.Код, """") КАК ИдентификаторГосКонтракта,
	|	ВозвратТоваровОтПокупателя.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	ВозвратТоваровОтПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	0 КАК СуммаУвеличение,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровОтПокупателя.СуммаВключаетНДС
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.Сумма
	|		ИНАЧЕ ВозвратТоваровОтПокупателяТовары.Сумма + ВозвратТоваровОтПокупателяТовары.СуммаНДС
	|	КОНЕЦ КАК СуммаУменьшение,
	|	0 КАК СуммаНДСУвеличение,
	|	ВозвратТоваровОтПокупателяТовары.СуммаНДС КАК СуммаНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровОтПокупателя.СуммаВключаетНДС
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.СуммаПослеИзменения
	|		ИНАЧЕ ВозвратТоваровОтПокупателяТовары.СуммаПослеИзменения + ВозвратТоваровОтПокупателяТовары.СуммаНДСПослеИзменения
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВозвратТоваровОтПокупателяТовары.СуммаНДСПослеИзменения КАК СуммаНДСДокумента,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровОтПокупателяТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЕстьНДС,
	|	ВозвратТоваровОтПокупателя.Ссылка КАК Документ,
	|	0 КАК СуммаУвеличениеКомиссия,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровОтПокупателяТовары.СчетУчета В (&СчетаУчетаТоваров)
	|			ТОГДА ВЫБОР
	|					КОГДА ВозвратТоваровОтПокупателя.СуммаВключаетНДС
	|						ТОГДА 0
	|					ИНАЧЕ ВозвратТоваровОтПокупателяТовары.СуммаНДС
	|				КОНЕЦ + ВозвратТоваровОтПокупателяТовары.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУменьшениеКомиссия,
	|	0 КАК СуммаНДСУвеличениеКомиссия,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровОтПокупателяТовары.СчетУчета В (&СчетаУчетаТоваров)
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНДСУменьшениеКомиссия,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровОтПокупателяТовары.СчетУчета В (&СчетаУчетаТоваров)
	|			ТОГДА ВЫБОР
	|					КОГДА ВозвратТоваровОтПокупателя.СуммаВключаетНДС
	|						ТОГДА 0
	|					ИНАЧЕ ВозвратТоваровОтПокупателяТовары.СуммаНДС
	|				КОНЕЦ + ВозвратТоваровОтПокупателяТовары.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДокументаКомиссия,
	|	ВЫБОР
	|		КОГДА ВозвратТоваровОтПокупателяТовары.СчетУчета В (&СчетаУчетаТоваров)
	|			ТОГДА ВозвратТоваровОтПокупателяТовары.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНДСДокументаКомиссия,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК ИсходныйДокумент
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО ВозвратТоваровОтПокупателяТовары.ИсправляемыйДокумент = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ПО ВозвратТоваровОтПокупателяТовары.Ссылка = ВозвратТоваровОтПокупателя.Ссылка
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка В(&ДокументыОснования)
	|	И НЕ СчетФактураВыданныйДокументыОснования.Ссылка.ПометкаУдаления
	|	И СчетФактураВыданныйДокументыОснования.Ссылка.ВидСчетаФактуры В(&ВидыСчетовФактурИсправляемогоДокумента)
	|ИТОГИ
	|	МАКСИМУМ(ИдентификаторГосКонтракта),
	|	СУММА(СуммаУвеличение),
	|	СУММА(СуммаУменьшение),
	|	СУММА(СуммаНДСУвеличение),
	|	СУММА(СуммаНДСУменьшение),
	|	СУММА(СуммаДокумента),
	|	СУММА(СуммаНДСДокумента),
	|	СУММА(ЕстьНДС),
	|	СУММА(СуммаУвеличениеКомиссия),
	|	СУММА(СуммаУменьшениеКомиссия),
	|	СУММА(СуммаНДСУвеличениеКомиссия),
	|	СУММА(СуммаНДСУменьшениеКомиссия),
	|	СУММА(СуммаДокументаКомиссия),
	|	СУММА(СуммаНДСДокументаКомиссия)
	|ПО
	|	ОБЩИЕ,
	|	Документ,
	|	ИсходныйДокумент";

КонецФункции

Функция ТекстЗапросаВозвратПоставщику()
	
	Возврат 
	"ВЫБРАТЬ
	|	Товары.Ссылка.Организация КАК Организация,
	|	Товары.Ссылка.Контрагент КАК Контрагент,
	|	Товары.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	0 КАК СуммаУвеличение,
	|	ВЫБОР
	|		КОГДА Товары.Ссылка.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Сумма > 0
	|						ТОГДА Товары.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.Сумма + Товары.СуммаНДС > 0
	|					ТОГДА Товары.Сумма + Товары.СуммаНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаУменьшение,
	|	Товары.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|	0 КАК СуммаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА Товары.СуммаНДС > 0
	|			ТОГДА Товары.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНДСУменьшение,
	|	ВЫБОР
	|		КОГДА Товары.Ссылка.СуммаВключаетНДС
	|			ТОГДА Товары.Сумма
	|		ИНАЧЕ Товары.Сумма + Товары.СуммаНДС
	|	КОНЕЦ КАК СуммаДокумента,
	|	Товары.СуммаНДС КАК СуммаНДСДокумента,
	|	ВЫБОР
	|		КОГДА Товары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЕстьНДС,
	|	Товары.Ссылка КАК Документ,
	|	ВЫБОР
	|		КОГДА (Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦпринятыеНаОтветственноеХранение)
	|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|				И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Товары.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНДСДокументаКомиссия,
	|	ВЫБОР
	|		КОГДА (Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|				ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|				И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Ссылка.СуммаВключаетНДС
	|						ТОГДА Товары.Сумма
	|					ИНАЧЕ Товары.Сумма + Товары.СуммаНДС
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДокументаКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ((Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|					ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|					ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|					И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА Товары.Сумма > 0
	|								ТОГДА Товары.Сумма
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Сумма + Товары.СуммаНДС > 0
	|							ТОГДА Товары.Сумма + Товары.СуммаНДС
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаУвеличениеКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ((Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|					ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|					ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|					И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.Ссылка.СуммаВключаетНДС
	|					ТОГДА ВЫБОР
	|							КОГДА Товары.Сумма > 0
	|								ТОГДА Товары.Сумма
	|							ИНАЧЕ 0
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Сумма + Товары.СуммаНДС > 0
	|							ТОГДА Товары.Сумма + Товары.СуммаНДС
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаУменьшениеКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ((Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|					ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|					ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|					И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.СуммаНДС > 0
	|					ТОГДА Товары.СуммаНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаНДСУвеличениеКомиссия,
	|	ВЫБОР
	|		КОГДА НЕ((Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|					ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыНаСкладе)
	|					ИЛИ Товары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию))
	|					И Товары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.СуммаНДС > 0
	|					ТОГДА Товары.СуммаНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаНДСУменьшениеКомиссия,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Документ, Товары.Ссылка.Сделка) КАК ДокументПоступления
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО Товары.Сделка = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	Товары.Ссылка В(&ДокументыОснования)
	|ИТОГИ
	|	СУММА(СуммаУвеличение),
	|	СУММА(СуммаУменьшение),
	|	СУММА(СуммаНДСУвеличение),
	|	СУММА(СуммаНДСУменьшение),
	|	СУММА(СуммаДокумента),
	|	СУММА(СуммаНДСДокумента),
	|	СУММА(ЕстьНДС),
	|	СУММА(СуммаНДСДокументаКомиссия),
	|	СУММА(СуммаДокументаКомиссия),
	|	СУММА(СуммаУвеличениеКомиссия),
	|	СУММА(СуммаУменьшениеКомиссия),
	|	СУММА(СуммаНДСУвеличениеКомиссия),
	|	СУММА(СуммаНДСУменьшениеКомиссия)
	|ПО
	|	ОБЩИЕ,
	|	Документ,
	|	ДокументПоступления";

КонецФункции

// Функция возвращает текст запроса по корректировочным счетам-фактурам
// с документом основания ОтчетКомиссионераОПродажах
// Возвращаемое значение:
//	Текст - текст сформированного запроса
Функция ТекстЗапросаКорректировочныеВыданныеСчетаФактурыПоОтчетуКомиссионера()
	
	Возврат 
	"ВЫБРАТЬ
	|	ОтчетКомиссионераОВозвратахПокупатели.ДатаСФ КАК ДатаСФ,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ ВТ_КоличествоВозвратовПоСФ
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Возвраты КАК ОтчетКомиссионераОВозвратахПокупатели
	|ГДЕ
	|	ОтчетКомиссионераОВозвратахПокупатели.Ссылка В(&ДокументыОснования)
	|	И ОтчетКомиссионераОВозвратахПокупатели.ВыставленСФ
	|	И ОтчетКомиссионераОВозвратахПокупатели.СчетФактура = &ТекущийДокумент
	|	И ОтчетКомиссионераОВозвратахПокупатели.Ссылка.ВыписыватьСчетаФактурыСводно
	|	И НЕ ОтчетКомиссионераОВозвратахПокупатели.Ссылка.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетКомиссионераОВозвратахПокупатели.ДатаСФ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДатаСФ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеквизитыДокумента.Ссылка КАК Ссылка,
	|	РеквизитыДокумента.Организация КАК Организация,
	|	РеквизитыДокумента.Контрагент КАК Контрагент,
	|	РеквизитыДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	РеквизитыДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_КоличествоВозвратовПоСФ.Количество, 1) > 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СводныйСФ
	|ПОМЕСТИТЬ ВТ_ОтчетКомиссионераОВозвратах
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Возвраты КАК ОтчетКомиссионераОВозвратах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах КАК РеквизитыДокумента
	|		ПО ОтчетКомиссионераОВозвратах.Ссылка = РеквизитыДокумента.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоВозвратовПоСФ КАК ВТ_КоличествоВозвратовПоСФ
	|		ПО ОтчетКомиссионераОВозвратах.ДатаСФ = ВТ_КоличествоВозвратовПоСФ.ДатаСФ
	|ГДЕ
	|	ОтчетКомиссионераОВозвратах.Ссылка В(&ДокументыОснования)
	|	И ОтчетКомиссионераОВозвратах.ВыставленСФ
	|	И ОтчетКомиссионераОВозвратах.СчетФактура = &ТекущийДокумент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.КлючСтроки КАК КлючСтроки,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВложенныйЗапрос.ЕстьНДС) КАК ЕстьНДС,
	|	СУММА(ВложенныйЗапрос.СуммаКомиссия) КАК СуммаКомиссия,
	|	СУММА(ВложенныйЗапрос.СуммаНДСКомиссия) КАК СуммаНДСКомиссия,
	|	СУММА(ВложенныйЗапрос.КоличествоДоИзменения) КАК КоличествоДоИзменения,
	|	СУММА(ВложенныйЗапрос.СуммаДоИзменения) КАК СуммаДоИзменения,
	|	СУММА(ВложенныйЗапрос.СуммаНДСДоИзменения) КАК СуммаНДСДоИзменения,
	|	ВложенныйЗапрос.ИсходныйДокумент КАК ИсходныйДокумент
	|ПОМЕСТИТЬ ВТ_Суммы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОтчетКомиссионераОВозвратахТовары.КлючСтроки КАК КлючСтроки,
	|		ОтчетКомиссионераОВозвратахТовары.Сумма КАК Сумма,
	|		ОтчетКомиссионераОВозвратахТовары.СуммаНДС КАК СуммаНДС,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионераОВозвратахТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК ЕстьНДС,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионераОВозвратахТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
	|				ТОГДА ОтчетКомиссионераОВозвратахТовары.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаКомиссия,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионераОВозвратахТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыПереданныеНаКомиссию)
	|				ТОГДА ОтчетКомиссионераОВозвратахТовары.СуммаНДС
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаНДСКомиссия,
	|		ОтчетКомиссионераОВозвратахТовары.КоличествоПослеИзменения - ОтчетКомиссионераОВозвратахТовары.Количество КАК КоличествоДоИзменения,
	|		ОтчетКомиссионераОВозвратахТовары.СуммаПослеИзменения - ОтчетКомиссионераОВозвратахТовары.Сумма КАК СуммаДоИзменения,
	|		ОтчетКомиссионераОВозвратахТовары.СуммаНДСПослеИзменения - ОтчетКомиссионераОВозвратахТовары.СуммаНДС КАК СуммаНДСДоИзменения,
	|		ОтчетКомиссионераОВозвратахТовары.ИсправляемыйДокумент КАК ИсходныйДокумент
	|	ИЗ
	|		ВТ_ОтчетКомиссионераОВозвратах КАК ВТ_ОтчетКомиссионераОВозвратах
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.ТоварыВозвращенные КАК ОтчетКомиссионераОВозвратахТовары
	|			ПО ВТ_ОтчетКомиссионераОВозвратах.Ссылка = ОтчетКомиссионераОВозвратахТовары.Ссылка) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.КлючСтроки,
	|	ВложенныйЗапрос.ИсходныйДокумент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОтчетКомиссионераОВозвратах.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ВТ_ОтчетКомиссионераОВозвратах.СводныйСФ
	|			ТОГДА ВТ_ОтчетКомиссионераОВозвратах.Контрагент
	|		ИНАЧЕ ОтчетКомиссионераОВозвратахПокупатели.Покупатель
	|	КОНЕЦ КАК Контрагент,
	|	ОтчетКомиссионераОВозвратахПокупатели.ДатаСФ КАК Дата,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК ДоговорКонтрагента,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	ВТ_Суммы.Сумма + ВЫБОР
	|		КОГДА ВТ_ОтчетКомиссионераОВозвратах.СуммаВключаетНДС
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_Суммы.СуммаНДС
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВТ_Суммы.СуммаНДС КАК СуммаНДСДокумента,
	|	ВТ_Суммы.ЕстьНДС КАК ЕстьНДС,
	|	ВТ_ОтчетКомиссионераОВозвратах.ВалютаДокумента КАК ВалютаДокумента,
	|	ВТ_Суммы.СуммаНДСКомиссия КАК СуммаНДСДокументаКомиссия,
	|	ВТ_Суммы.СуммаКомиссия + ВЫБОР
	|		КОГДА ВТ_ОтчетКомиссионераОВозвратах.СуммаВключаетНДС
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_Суммы.СуммаНДСКомиссия
	|	КОНЕЦ КАК СуммаДокументаКомиссия,
	|	0 КАК СуммаУвеличение,
	|	ВТ_Суммы.Сумма + ВЫБОР
	|		КОГДА ВТ_ОтчетКомиссионераОВозвратах.СуммаВключаетНДС
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_Суммы.СуммаНДС
	|	КОНЕЦ КАК СуммаУменьшение,
	|	0 КАК СуммаНДСУвеличение,
	|	ВТ_Суммы.СуммаНДС КАК СуммаНДСУменьшение,
	|	ВТ_Суммы.СуммаНДСКомиссия КАК СуммаНДСУменьшениеКомиссия,
	|	0 КАК СуммаНДСУвеличениеКомиссия,
	|	0 КАК СуммаУвеличениеКомиссия,
	|	ВТ_Суммы.СуммаКомиссия + ВЫБОР
	|		КОГДА ВТ_ОтчетКомиссионераОВозвратах.СуммаВключаетНДС
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_Суммы.СуммаНДСКомиссия
	|	КОНЕЦ КАК СуммаУменьшениеКомиссия,
	|	ВТ_ОтчетКомиссионераОВозвратах.Ссылка КАК Документ,
	|	ВТ_Суммы.ИсходныйДокумент КАК ИсходныйДокумент
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Возвраты КАК ОтчетКомиссионераОВозвратахПокупатели
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОтчетКомиссионераОВозвратах КАК ВТ_ОтчетКомиссионераОВозвратах
	|		ПО ОтчетКомиссионераОВозвратахПокупатели.Ссылка = ВТ_ОтчетКомиссионераОВозвратах.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Суммы КАК ВТ_Суммы
	|		ПО ОтчетКомиссионераОВозвратахПокупатели.КлючСтроки = ВТ_Суммы.КлючСтроки
	|ГДЕ
	|	ОтчетКомиссионераОВозвратахПокупатели.СчетФактура = &ТекущийДокумент
	|ИТОГИ
	|	МАКСИМУМ(Организация),
	|	МАКСИМУМ(Контрагент),
	|	МАКСИМУМ(Дата),
	|	МАКСИМУМ(ДоговорКонтрагента),
	|	МАКСИМУМ(ИдентификаторГосКонтракта),
	|	СУММА(СуммаДокумента),
	|	СУММА(СуммаНДСДокумента),
	|	СУММА(ЕстьНДС),
	|	МАКСИМУМ(ВалютаДокумента),
	|	СУММА(СуммаНДСДокументаКомиссия),
	|	СУММА(СуммаДокументаКомиссия),
	|	СУММА(СуммаУвеличение),
	|	СУММА(СуммаУменьшение),
	|	СУММА(СуммаНДСУвеличение),
	|	СУММА(СуммаНДСУменьшение),
	|	СУММА(СуммаНДСУменьшениеКомиссия),
	|	СУММА(СуммаНДСУвеличениеКомиссия),
	|	СУММА(СуммаУвеличениеКомиссия),
	|	СУММА(СуммаУменьшениеКомиссия)
	|ПО
	|	ОБЩИЕ,
	|	Документ,
	|	ИсходныйДокумент";

КонецФункции

Функция ТекстЗапросаПоступлениеТоваровУслуг()
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицыПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ТаблицыПоступлениеТоваровУслуг.Контрагент КАК Контрагент,
	|	ТаблицыПоступлениеТоваровУслуг.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ТаблицыПоступлениеТоваровУслуг.ИдентификаторГосКонтракта КАК ИдентификаторГосКонтракта,
	|	ЕСТЬNULL(АктыОРасхождениях.СуммаПоДокументу, ТаблицыПоступлениеТоваровУслуг.СуммаДокумента) КАК СуммаДокумента,
	|	ТаблицыПоступлениеТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
	|	ЕСТЬNULL(АктыОРасхождениях.СуммаНДСПоДокументу, ТаблицыПоступлениеТоваровУслуг.СуммаНДСДокумента) КАК СуммаНДСДокумента,
	|	ВЫБОР
	|		КОГДА ТаблицыПоступлениеТоваровУслуг.СуммаДокументаКомиссия <> 0
	|			ТОГДА ЕСТЬNULL(АктыОРасхождениях.СуммаПоДокументу, ТаблицыПоступлениеТоваровУслуг.СуммаДокументаКомиссия)
	|		ИНАЧЕ ТаблицыПоступлениеТоваровУслуг.СуммаДокументаКомиссия
	|	КОНЕЦ КАК СуммаДокументаКомиссия,
	|	ВЫБОР
	|		КОГДА ТаблицыПоступлениеТоваровУслуг.СуммаНДСДокументаКомиссия <> 0
	|			ТОГДА ЕСТЬNULL(АктыОРасхождениях.СуммаНДСПоДокументу, ТаблицыПоступлениеТоваровУслуг.СуммаНДСДокументаКомиссия)
	|		ИНАЧЕ ТаблицыПоступлениеТоваровУслуг.СуммаНДСДокументаКомиссия
	|	КОНЕЦ КАК СуммаНДСДокументаКомиссия,
	|	ТаблицыПоступлениеТоваровУслуг.ЕстьНДС КАК ЕстьНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаОборудование.Ссылка.Организация КАК Организация,
	|		ТаблицаОборудование.Ссылка.Контрагент КАК Контрагент,
	|		ТаблицаОборудование.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|		"""" КАК ИдентификаторГосКонтракта,
	|		СУММА(ТаблицаОборудование.Сумма + ВЫБОР
	|				КОГДА ТаблицаОборудование.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ ТаблицаОборудование.СуммаНДС
	|			КОНЕЦ) КАК СуммаДокумента,
	|		ТаблицаОборудование.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|		СУММА(ТаблицаОборудование.СуммаНДС) КАК СуммаНДСДокумента,
	|		0 КАК СуммаДокументаКомиссия,
	|		0 КАК СуммаНДСДокументаКомиссия,
	|		СУММА(ВЫБОР
	|				КОГДА ТаблицаОборудование.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ) КАК ЕстьНДС,
	|		НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|		ТаблицаОборудование.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.Оборудование КАК ТаблицаОборудование
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ТаблицаОборудование.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|	ГДЕ
	|		ТаблицаОборудование.Ссылка В(&ДокументыОснования)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаОборудование.Ссылка.Организация,
	|		ТаблицаОборудование.Ссылка.Контрагент,
	|		ТаблицаОборудование.Ссылка.ДоговорКонтрагента,
	|		ТаблицаОборудование.Ссылка.ВалютаДокумента,
	|		ТаблицаОборудование.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаОбъектыСтроительства.Ссылка.Организация,
	|		ТаблицаОбъектыСтроительства.Ссылка.Контрагент,
	|		ТаблицаОбъектыСтроительства.Ссылка.ДоговорКонтрагента,
	|		"""",
	|		СУММА(ТаблицаОбъектыСтроительства.Сумма + ВЫБОР
	|				КОГДА ТаблицаОбъектыСтроительства.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ ТаблицаОбъектыСтроительства.СуммаНДС
	|			КОНЕЦ),
	|		ТаблицаОбъектыСтроительства.Ссылка.ВалютаДокумента,
	|		СУММА(ТаблицаОбъектыСтроительства.СуммаНДС),
	|		0,
	|		0,
	|		СУММА(ВЫБОР
	|				КОГДА ТаблицаОбъектыСтроительства.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ),
	|		НЕОПРЕДЕЛЕНО,
	|		ТаблицаОбъектыСтроительства.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК ТаблицаОбъектыСтроительства
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ТаблицаОбъектыСтроительства.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|	ГДЕ
	|		ТаблицаОбъектыСтроительства.Ссылка В(&ДокументыОснования)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаОбъектыСтроительства.Ссылка.Организация,
	|		ТаблицаОбъектыСтроительства.Ссылка.Контрагент,
	|		ТаблицаОбъектыСтроительства.Ссылка.ДоговорКонтрагента,
	|		ТаблицаОбъектыСтроительства.Ссылка.ВалютаДокумента,
	|		ТаблицаОбъектыСтроительства.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Ссылка.Организация,
	|		ТаблицаТовары.Ссылка.Контрагент,
	|		ТаблицаТовары.Ссылка.ДоговорКонтрагента,
	|		"""",
	|		СУММА(ТаблицаТовары.Сумма + ВЫБОР
	|				КОГДА ТаблицаТовары.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ ТаблицаТовары.СуммаНДС
	|			КОНЕЦ),
	|		ТаблицаТовары.Ссылка.ВалютаДокумента,
	|		СУММА(ТаблицаТовары.СуммаНДС),
	|		СУММА(ВЫБОР
	|				КОГДА ТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|						И ТаблицаТовары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|					ТОГДА ТаблицаТовары.Сумма + ВЫБОР
	|							КОГДА ТаблицаТовары.Ссылка.СуммаВключаетНДС
	|								ТОГДА 0
	|							ИНАЧЕ ТаблицаТовары.СуммаНДС
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦПринятыеНаОтветственноеХранение)
	|						И ТаблицаТовары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|					ТОГДА ТаблицаТовары.СуммаНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ТаблицаТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ),
	|		ТаблицаТовары.ИдентификаторСтроки,
	|		ТаблицаТовары.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаТовары
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ТаблицаТовары.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|	ГДЕ
	|		ТаблицаТовары.Ссылка В(&ДокументыОснования)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаТовары.Ссылка.Организация,
	|		ТаблицаТовары.Ссылка.Контрагент,
	|		ТаблицаТовары.Ссылка.ДоговорКонтрагента,
	|		ТаблицаТовары.Ссылка.ВалютаДокумента,
	|		ТаблицаТовары.Ссылка,
	|		ТаблицаТовары.ИдентификаторСтроки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаУслуги.Ссылка.Организация,
	|		ТаблицаУслуги.Ссылка.Контрагент,
	|		ТаблицаУслуги.Ссылка.ДоговорКонтрагента,
	|		"""",
	|		СУММА(ТаблицаУслуги.Сумма + ВЫБОР
	|				КОГДА ТаблицаУслуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ ТаблицаУслуги.СуммаНДС
	|			КОНЕЦ),
	|		ТаблицаУслуги.Ссылка.ВалютаДокумента,
	|		СУММА(ТаблицаУслуги.СуммаНДС),
	|		0,
	|		0,
	|		СУММА(ВЫБОР
	|				КОГДА ТаблицаУслуги.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ),
	|		ТаблицаУслуги.ИдентификаторСтроки,
	|		ТаблицаУслуги.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.Услуги КАК ТаблицаУслуги
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ТаблицаУслуги.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|	ГДЕ
	|		ТаблицаУслуги.Ссылка В(&ДокументыОснования)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаУслуги.Ссылка.Организация,
	|		ТаблицаУслуги.Ссылка.Контрагент,
	|		ТаблицаУслуги.Ссылка.ДоговорКонтрагента,
	|		ТаблицаУслуги.Ссылка.ВалютаДокумента,
	|		ТаблицаУслуги.Ссылка,
	|		ТаблицаУслуги.ИдентификаторСтроки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаАгентскиеУслуги.Ссылка.Организация,
	|		ТаблицаАгентскиеУслуги.Ссылка.Контрагент,
	|		ТаблицаАгентскиеУслуги.Ссылка.ДоговорКонтрагента,
	|		"""",
	|		СУММА(ТаблицаАгентскиеУслуги.Сумма + ВЫБОР
	|				КОГДА ТаблицаАгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ ТаблицаАгентскиеУслуги.СуммаНДС
	|			КОНЕЦ),
	|		ТаблицаАгентскиеУслуги.Ссылка.ВалютаДокумента,
	|		СУММА(ТаблицаАгентскиеУслуги.СуммаНДС),
	|		СУММА(ТаблицаАгентскиеУслуги.Сумма + ВЫБОР
	|				КОГДА ТаблицаАгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ ТаблицаАгентскиеУслуги.СуммаНДС
	|			КОНЕЦ),
	|		СУММА(ТаблицаАгентскиеУслуги.СуммаНДС),
	|		СУММА(ВЫБОР
	|				КОГДА ТаблицаАгентскиеУслуги.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ),
	|		ТаблицаАгентскиеУслуги.ИдентификаторСтроки,
	|		ТаблицаАгентскиеУслуги.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ТаблицаАгентскиеУслуги
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ТаблицаАгентскиеУслуги.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|	ГДЕ
	|		ТаблицаАгентскиеУслуги.Ссылка В(&ДокументыОснования)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаАгентскиеУслуги.Ссылка.Организация,
	|		ТаблицаАгентскиеУслуги.Ссылка.Контрагент,
	|		ТаблицаАгентскиеУслуги.Ссылка.ДоговорКонтрагента,
	|		ТаблицаАгентскиеУслуги.Ссылка.ВалютаДокумента,
	|		ТаблицаАгентскиеУслуги.Ссылка,
	|		ТаблицаАгентскиеУслуги.ИдентификаторСтроки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаОсновныеСредства.Ссылка.Организация,
	|		ТаблицаОсновныеСредства.Ссылка.Контрагент,
	|		ТаблицаОсновныеСредства.Ссылка.ДоговорКонтрагента,
	|		"""",
	|		СУММА(ТаблицаОсновныеСредства.Сумма + ВЫБОР
	|				КОГДА ТаблицаОсновныеСредства.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ ТаблицаОсновныеСредства.СуммаНДС
	|			КОНЕЦ),
	|		ТаблицаОсновныеСредства.Ссылка.ВалютаДокумента,
	|		СУММА(ТаблицаОсновныеСредства.СуммаНДС),
	|		0,
	|		0,
	|		СУММА(ВЫБОР
	|				КОГДА ТаблицаОсновныеСредства.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ),
	|		НЕОПРЕДЕЛЕНО,
	|		ТаблицаОсновныеСредства.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ТаблицаОсновныеСредства
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ТаблицаОсновныеСредства.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|	ГДЕ
	|		ТаблицаОсновныеСредства.Ссылка В(&ДокументыОснования)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаОсновныеСредства.Ссылка.Организация,
	|		ТаблицаОсновныеСредства.Ссылка.Контрагент,
	|		ТаблицаОсновныеСредства.Ссылка.ДоговорКонтрагента,
	|		ТаблицаОсновныеСредства.Ссылка.ВалютаДокумента,
	|		ТаблицаОсновныеСредства.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаТопливо.Ссылка.Организация,
	|		ТаблицаТопливо.Ссылка.Контрагент,
	|		ТаблицаТопливо.Ссылка.ДоговорКонтрагента,
	|		"""",
	|		СУММА(ТаблицаТопливо.Сумма + ВЫБОР
	|				КОГДА ТаблицаТопливо.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ ТаблицаТопливо.СуммаНДС
	|			КОНЕЦ),
	|		ТаблицаТопливо.Ссылка.ВалютаДокумента,
	|		СУММА(ТаблицаТопливо.СуммаНДС),
	|		0,
	|		0,
	|		СУММА(ВЫБОР
	|				КОГДА ТаблицаТопливо.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ),
	|		НЕОПРЕДЕЛЕНО,
	|		ТаблицаТопливо.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.Топливо КАК ТаблицаТопливо
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ТаблицаТопливо.Ссылка.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|	ГДЕ
	|		ТаблицаТопливо.Ссылка В(&ДокументыОснования)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаТопливо.Ссылка.Организация,
	|		ТаблицаТопливо.Ссылка.Контрагент,
	|		ТаблицаТопливо.Ссылка.ДоговорКонтрагента,
	|		ТаблицаТопливо.Ссылка.ВалютаДокумента,
	|		ТаблицаТопливо.Ссылка) КАК ТаблицыПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			АктОРасхожденияхТовары.СуммаПоДокументу + ВЫБОР
	|				КОГДА АктОРасхожденияхТовары.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ АктОРасхожденияхТовары.СуммаНДСПоДокументу
	|			КОНЕЦ КАК СуммаПоДокументу,
	|			АктОРасхожденияхТовары.СуммаНДСПоДокументу КАК СуммаНДСПоДокументу,
	|			АктОРасхожденияхТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
	|		ИЗ
	|			Документ.АктОРасхождениях.Товары КАК АктОРасхожденияхТовары
	|		ГДЕ
	|			НЕ АктОРасхожденияхТовары.Ссылка.ПометкаУдаления
	|			И АктОРасхожденияхТовары.Ссылка.ДокументПоступления В(&ДокументыОснования)
	|			И АктОРасхожденияхТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			АктОРасхожденияхУслуги.СуммаПоДокументу + ВЫБОР
	|				КОГДА АктОРасхожденияхУслуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ АктОРасхожденияхУслуги.СуммаНДСПоДокументу
	|			КОНЕЦ,
	|			АктОРасхожденияхУслуги.СуммаНДСПоДокументу,
	|			АктОРасхожденияхУслуги.ИдентификаторСтроки
	|		ИЗ
	|			Документ.АктОРасхождениях.Услуги КАК АктОРасхожденияхУслуги
	|		ГДЕ
	|			НЕ АктОРасхожденияхУслуги.Ссылка.ПометкаУдаления
	|			И АктОРасхожденияхУслуги.Ссылка.ДокументПоступления В(&ДокументыОснования)
	|			И АктОРасхожденияхУслуги.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу + ВЫБОР
	|				КОГДА АктОРасхожденияхАгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|					ТОГДА 0
	|				ИНАЧЕ АктОРасхожденияхАгентскиеУслуги.СуммаНДСПоДокументу
	|			КОНЕЦ,
	|			АктОРасхожденияхАгентскиеУслуги.СуммаНДСПоДокументу,
	|			АктОРасхожденияхАгентскиеУслуги.ИдентификаторСтроки
	|		ИЗ
	|			Документ.АктОРасхождениях.АгентскиеУслуги КАК АктОРасхожденияхАгентскиеУслуги
	|		ГДЕ
	|			НЕ АктОРасхожденияхАгентскиеУслуги.Ссылка.ПометкаУдаления
	|			И АктОРасхожденияхАгентскиеУслуги.Ссылка.ДокументПоступления В(&ДокументыОснования)
	|			И АктОРасхожденияхАгентскиеУслуги.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)) КАК АктыОРасхождениях
	|		ПО ТаблицыПоступлениеТоваровУслуг.ИдентификаторСтроки = АктыОРасхождениях.ИдентификаторСтроки";
	
КонецФункции

// РеквизитыСФ - Структура, см. РеквизитыДляНадписиОСчетеФактуреПолученном
Функция ПредставлениеСчетФактураПолученный(РеквизитыСФ)
	
	ПараметрыСтроки = Новый Структура;
	ПараметрыСтроки.Вставить("НомерСчетаФактуры", ?(ЗначениеЗаполнено(РеквизитыСФ.НомерСчетаФактуры), РеквизитыСФ.НомерСчетаФактуры, "..."));
	ПараметрыСтроки.Вставить("ДатаСчетаФактуры", ?(ЗначениеЗаполнено(РеквизитыСФ.ДатаСчетаФактуры),  Формат(РеквизитыСФ.ДатаСчетаФактуры, "ДЛФ=D"), "..."));
	
	Если РеквизитыСФ.ИсправлениеСобственнойОшибки Тогда
		ШаблонСтроки = НСтр("ru='[НомерСчетаФактуры] от [ДатаСчетаФактуры] (исправление собственной ошибки от [ДатаИсправления])'");
		ПараметрыСтроки.Вставить("ДатаИсправления", ?(ЗначениеЗаполнено(РеквизитыСФ.Дата),  Формат(РеквизитыСФ.Дата, "ДЛФ=D"), "..."));
	ИначеЕсли РеквизитыСФ.Исправление Тогда
		ШаблонСтроки = НСтр("ru='[НомерСчетаФактуры] (испр. [НомерИсправления]) от [ДатаИсправления]'");
		ПараметрыСтроки.Вставить("НомерИсправления", ?(ЗначениеЗаполнено(РеквизитыСФ.НомерИсправления), РеквизитыСФ.НомерИсправления, "..."));
		ПараметрыСтроки.Вставить("ДатаИсправления", ?(ЗначениеЗаполнено(РеквизитыСФ.ДатаИсправления),  Формат(РеквизитыСФ.ДатаИсправления, "ДЛФ=D"), "..."));
	Иначе
		Если НачалоДня(РеквизитыСФ.ДатаСчетаФактуры) = НачалоДня(РеквизитыСФ.Дата) Тогда
			ШаблонСтроки = НСтр("ru='[НомерСчетаФактуры] от [ДатаСчетаФактуры]'");
		Иначе
			ПараметрыСтроки.Вставить("ДатаПолученияСчетаФактуры", Формат(РеквизитыСФ.Дата, "ДЛФ=D"));
			ШаблонСтроки = НСтр("ru='[НомерСчетаФактуры] от [ДатаСчетаФактуры], получен [ДатаПолученияСчетаФактуры]'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонСтроки, ПараметрыСтроки);
	
КонецФункции

#КонецОбласти

