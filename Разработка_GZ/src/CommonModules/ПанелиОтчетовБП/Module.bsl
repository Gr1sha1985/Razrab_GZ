// Использование подсистемы ВариантыОтчетов для вывода панелей отчетов.

#Область ПрограммныйИнтерфейс

// Определяет разделы глобального командного интерфейса, в которых предусмотрены панели отчетов.
//
// Параметры:
//   Разделы - СписокЗначений - разделы, в которые выведены команды открытия панели отчетов.
//       * Значение - ОбъектМетаданных: Подсистема, Строка - подсистема раздела глобального командного интерфейса,
//           либо ВариантыОтчетовКлиентСервер.ИдентификаторНачальнойСтраницы для начальной страницы.
//       * Представление - Строка - заголовок панели отчетов в этом разделе.
//
Процедура ОпределитьРазделыСВариантамиОтчетов(Разделы) Экспорт
	
	// Каждая панель может быть представлена в нескольких подсистемах,
	// соответствующих разным вариантам интерфейса программы:
	// - простому
	// - полному
	// - версии 7.7.
	
	ПредставлениеПанели = НСтр("ru = 'Отчеты для руководителя'");
	Разделы.Добавить(Метаданные.Подсистемы.Руководителю,   ПредставлениеПанели);
	
	ПредставлениеПанели = НСтр("ru = 'Регистры бухгалтерского учета субъектов малого предпринимательства'");
	Разделы.Добавить(
		Метаданные.Подсистемы.Отчеты.Подсистемы.РегистрыБУСубъектовМалогоПредпринимательства,
		ПредставлениеПанели);
	Разделы.Добавить(
		Метаданные.Подсистемы.БухгалтерияПредприятияПодсистемы.Подсистемы.ПростойИнтерфейс.Подсистемы.Отчеты.Подсистемы.РегистрыБУСубъектовМалогоПредпринимательства,
		ПредставлениеПанели);
		
	ПредставлениеПанели = НСтр("ru = 'Регистры налогового учета'");
	Разделы.Добавить(
		Метаданные.Подсистемы.Отчеты.Подсистемы.НалогНаПрибыль,
		ПредставлениеПанели);
	Разделы.Добавить(
		Метаданные.Подсистемы.БухгалтерияПредприятияПодсистемы.Подсистемы.ПростойИнтерфейс.Подсистемы.Отчеты.Подсистемы.НалогНаПрибыль,
		ПредставлениеПанели);
	Разделы.Добавить(Метаданные.Подсистемы.Интерфейс77НалоговыйУчет,
		ПредставлениеПанели);
	
	// Справки-расчеты
	ПредставлениеПанели = НСтр("ru = 'Справки-расчеты'");
	Для Каждого Подсистема Из РазделыИнтерфейсаСправкиРасчеты() Цикл
		Разделы.Добавить(Подсистема, ПредставлениеПанели);
	КонецЦикла;
	
	ПредставлениеПанели = НСтр("ru = 'Отчеты администратора'");
	Разделы.Добавить(Метаданные.Подсистемы.БухгалтерияПредприятияПодсистемы.Подсистемы.ПростойИнтерфейс.Подсистемы.Администрирование, ПредставлениеПанели);
	
КонецПроцедуры

// Подключает отчеты к подсистеме ВариантыОтчетов для отображения с помощью нее панели отчетов.
//
// Параметры:
//  Настройки - Коллекция - настройки, работа с которыми ведется с помощью методов подсистемы ВариантыОтчетов.
//
Процедура Настроить(Настройки) Экспорт
	
	// Часть отчетов (некоторые справки-расчеты) подключаются непосредственно, часть - требуют настройки модулем менеджера.
	
	ТребуетсяНастройкаМодулемМенеджера = ТребуетсяНастройкаМодулемМенеджера();
	
	Для Каждого ИмяОтчета Из СправкиРасчетыКлиентСервер.ПодключенныеОтчеты() Цикл
		
		Если ТребуетсяНастройкаМодулемМенеджера.Найти(ИмяОтчета) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		МетаданныеОтчета = Метаданные.Отчеты.Найти(ИмяОтчета);
		
		Если МетаданныеОтчета = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НастройкаОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, МетаданныеОтчета);
		
	КонецЦикла;
	
	Для Каждого ИмяОтчета Из ТребуетсяНастройкаМодулемМенеджера() Цикл
		
		МетаданныеОтчета = Метаданные.Отчеты.Найти(ИмяОтчета);
		
		Если МетаданныеОтчета = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, МетаданныеОтчета);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Настройки

// Определяет перечень отчетов, порядок вывода которых в панели отчетов
// настраивается в модуле менеджера отчета - процедуре НастроитьВариантыОтчета().
// Если из основной схемы отчета нельзя определить варианты отчета
// (для отчетов с отложенной инициализацией СКД),
// то дополнительно следует определить в менеджере отчета функцию ВариантыНастроек().
//
// Возвращаемое значение:
//  Массив - имена отчетов.
//
Функция ТребуетсяНастройкаМодулемМенеджера()
	
	ИменаОтчетов =
		"ВаловаяПрибыль
		|Продажи
		|СравнениеПродаж
		|ЗадолженностьПокупателей
		|ЗадолженностьПоставщикам
		|ЗадолженностьПокупателейПоСрокамДолга
		|ЗадолженностьПоставщикамПоСрокамДолга
		|АнализНеоплаченныхСчетовПокупателям
		|АнализНеоплаченныхСчетовПоставщиков
		|НадежностьДебиторовСПАРКРискиБП
		|ОборотныеСредства
		|ДинамикаЗадолженностиПокупателей
		|ДинамикаЗадолженностиПоставщикам
		|ОстаткиДенежныхСредств
		|ПоступленияДенежныхСредств
		|РасходыДенежныхСредств
		|АнализДвиженийДенежныхСредств
		|РегистрыБУСубъектовМалогоПредпринимательства
		|ДоходыРасходы
		|ОстаткиТоваров
		|ДвижениеТоваров
		|ОборачиваемостьТоваров
		|ОстаткиТоваровПоСрокамХранения
		|ОбъектыПереданныеВАренду
		|РегистрыНалоговогоУчета
		|РегистрИнформацииОбОбъектеОС
		|РегистрНалоговогоУчетаФинРезультатовОтРеализацииАмортизируемогоИмущества
		|СправкаРасчетАмортизации
		|СправкаРасчетРезервыОтпусков
		|СправкаРасчетТранспортныхРасходов
		|СправкаРасчетСписаниеРБП
		|СправкаРасчетУбытковПрошлыхЛет
		|СправкаРасчетНормированияРасходов
		|СправкаРасчетСписанияКосвенныхРасходов
		|СправкаРасчетНалогаНаПрибыль
		|СправкаРасчетСебестоимостиПродукции
		|СправкаРасчетРаспределенияКосвенныхРасходов
		|СправкаРасчетРезервыПоСомнительнымДолгам
		|СправкаРасчетАмортизационнойПремии
		|СправкаРасчетПереоценкаВалютныхСредств
		|СправкаРасчетПостоянныхИВременныхРазниц
		|СправкаРасчетПересчетСтоимостиОтложенныхНалоговыхАктивовИОбязательств
		|СправкаРасчетТорговойНаценки
		|СправкаРасчетНалогаУСН
		|СправкаРасчетНалогаНаИмущество
		|СправкаРасчетТранспортногоНалога
		|СправкаРасчетЗемельногоНалога
		|СправкаРасчетТорговогоСбора
		|СправкаРасчетНДФЛДляИП
		|СправкаРасчетПризнаниеРасходовПоОСПоступившимВЛизинг
		|СправкаРасчетЕдиногоНалогаНаВмененныйДоход
		|СправкаРасчетРасходовУменьшающихОтдельныеНалоги
		|СправкаРасчетДолиНалоговойБазы";
	
	Возврат Новый ФиксированныйМассив(СтрРазделить(ИменаОтчетов, Символы.ПС));
	
КонецФункции

Функция РазделыИнтерфейсаСправкиРасчеты()
	
	Разделы = Новый Массив;
	
	Разделы.Добавить(Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты);
	Разделы.Добавить(Метаданные.Подсистемы.БухгалтерияПредприятияПодсистемы.Подсистемы.ПростойИнтерфейс.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты);
	
	// Для включения отчетов в панели рекомендуется
	// - поместить отчет в каждую из перечисленных подсистем или подчиненные им
	// - отключить использование стандартных команд.
	
	Возврат Разделы;
	
КонецФункции

#КонецОбласти

#КонецОбласти
