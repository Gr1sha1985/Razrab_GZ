#Область СлужебныйПрограммныйИнтерфейс

// В процедуре нужно реализовать возможность работы с видами продукции, с которыми предполагается работа объектов.
// (См. ШтрихкодированиеИСКлиентСервер.ВключитьПоддержкуВидовПродукцииИС).
Процедура ПриЗаполненииПараметровСканирования(ПараметрыСканирования, Контекст, ВидПродукции) Экспорт
	
	Если ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.РозничнаяПродажа") Тогда
		ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "Товары";
		ПараметрыСканирования.Вставить("СохранятьКодыМаркировкиВПулИСМП", Истина);
		
		// В розничной продаже сканирование маркируемой продукции происходит из формы документа, 
		// поэтому там может быть как маркированная так и немаркированная продукция
		// Во всех остальных документах - из формы "Проверка и подбор" 
		// куда попадает только маркируемая продукция определенного вида
		ШтрихкодированиеИСКлиентСервер.ВключитьПоддержкуПрочейПродукции(ПараметрыСканирования);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.АктОРасхождениях") 
		ИЛИ ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.АктОРасхожденияхПолученный") 
		ИЛИ ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.КорректировкаПоступления") 
		ИЛИ ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.КорректировкаРеализации") 
		ИЛИ ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ПоступлениеТоваровУслуг") 
		ИЛИ ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.РеализацияТоваровУслуг") Тогда
		ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
		ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	КонецЕсли;
	
	ПараметрыСканирования.Вставить("КонтрольРасхожденийСДокументомОснованием", Ложь);
	
	ИнтеграцияИСБПКлиентСервер.ВключитьПоддержкуВидовПродукцииИС(Контекст, ПараметрыСканирования, ВидПродукции);
	
	Если ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.КорректировкаПоступления") 
		ИЛИ ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ПоступлениеТоваровУслуг") Тогда
		
		ПараметрыСканирования.СопоставлятьНоменклатуру  = Ложь;
		ПараметрыСканирования.СоздаватьШтрихкодУпаковки = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
