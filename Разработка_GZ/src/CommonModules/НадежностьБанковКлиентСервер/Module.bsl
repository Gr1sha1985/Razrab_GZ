#Область СлужебныеПроцедурыИФункции

Процедура ПолучитьИнформациюНадежностьБанков(Форма, БИК, ПоказыватьИнформацию) Экспорт
	
	Если НЕ ПоказыватьИнформацию Тогда
		Форма.ИнформацияНадежностьБанков = Неопределено;
	ИначеЕсли ПоказыватьИнформациюНадежностьБанков(Форма) Тогда
		Форма.ИнформацияНадежностьБанков = НадежностьБанковВызовСервера.ИнформацияОКритичномСобытии(БИК);
	КонецЕсли;
	
	ПоказатьИнформациюНадежностьБанков(Форма);
	
КонецПроцедуры

Процедура ПоказатьИнформациюНадежностьБанков(Форма) Экспорт

	Если ТипЗнч(Форма.ИнформацияНадежностьБанков) = Тип("Структура")
		И ЗначениеЗаполнено(Форма.ИнформацияНадежностьБанков.Событие.Событие) Тогда
		СтрокаСобытия = Новый ФорматированнаяСтрока(
			Строка(Форма.ИнформацияНадежностьБанков.Событие.Событие),
			, 
			Форма.ИнформацияНадежностьБанков.КритичноеСобытиеЦветТекста);
	Иначе
		СтрокаСобытия = Новый ФорматированнаяСтрока("");
	КонецЕсли;
	
	Форма.КритичноеСобытиеНадежностьБанков = СтрокаСобытия;
	Форма.Элементы.КритичноеСобытиеНадежностьБанков.Видимость = ЗначениеЗаполнено(Форма.КритичноеСобытиеНадежностьБанков);

КонецПроцедуры

Функция ПоказыватьИнформациюНадежностьБанков(Форма)

	Если ТипЗнч(Форма.ИнформацияНадежностьБанков) = Тип("Структура")
		И НЕ Форма.ИнформацияНадежностьБанков.Используется Тогда
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
	
КонецФункции

#КонецОбласти 
