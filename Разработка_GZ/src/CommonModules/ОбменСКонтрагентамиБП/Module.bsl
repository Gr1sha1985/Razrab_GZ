
#Область СлужебныйПрограммныйИнтерфейс

#Область ЗаполнениеЭД

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляУПДИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ЭтоКомиссионнаяТорговля  = Ложь;
	ЭтоСчетФактураНаАванс    = Ложь;
	ОснованиеСФУчаствуетВЭДО = Истина;
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		СчетФактура = СсылкаНаОбъект;
		ЭтоКомиссионнаяТорговля  = ЭтоКомиссионнаяТорговля(СчетФактура);
		ЭтоСчетФактураНаАванс    = ЭтоСчетФактураНаАванс(СчетФактура);
		ОснованиеСФУчаствуетВЭДО = ОснованиеСФУчаствуетВЭДО(СчетФактура);
			
		Если ЭтоКомиссионнаяТорговля
			ИЛИ ЭтоСчетФактураНаАванс
			ИЛИ Не ОснованиеСФУчаствуетВЭДО Тогда // если основание СФ не участвует в ЭДО, тогда формируем ЭД СФ вместо УПД
			
			СтруктураЭД.Функция = "СЧФ";
		КонецЕсли;
	Иначе
		Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ДоговорКонтрагента.ВидДоговора")
				= Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
			
			СтруктураЭД.Вставить("ВидОперацииЭД", Перечисления.ВидыОперацийЭД.Комиссия);
			ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
			
			Возврат;
			
		КонецЕсли;
		
		СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(СсылкаНаОбъект);
	КонецЕсли;
	
	МассивСчетовФактур = Новый Массив();
	МассивСчетовФактур.Добавить(СчетФактура);
	Если ЭтоКомиссионнаяТорговля
		Или ЭтоСчетФактураНаАванс
		Или Не ОснованиеСФУчаствуетВЭДО Тогда
		
		ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиСчетаФактуры1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур());
	Иначе
		ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиСчетаФактуры1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур(Истина, Истина), Истина);
	КонецЕсли;
	Если ТаблицаСчетовФактур = Неопределено
			ИЛИ ТаблицаСчетовФактур.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЭД.Вставить("ЭтоСчетФактураНаАванс", ЭтоСчетФактураНаАванс);
	ДанныеДляФормированияЭД = ТаблицаСчетовФактур[0];
	ЗаполнитьОсновнуюЧастьУПД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	
	Если Не (ЭтоСчетФактураНаАванс
		ИЛИ ЭтоКомиссионнаяТорговля
		ИЛИ Не ОснованиеСФУчаствуетВЭДО) Тогда
		
		ЗаполнитьРасширеннуюЧастьУПД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	Если ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры") = "Авансовый" Тогда
		Возврат;
	КонецЕсли;
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить(СсылкаНаОбъект);
	ТаблицаДанных = УчетНДС.ПолучитьДанныеДляПечатиУниверсальногоПередаточногоДокумента(
		МассивДокументов, Документы[СсылкаНаОбъект.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов(), Истина);
	ДанныеДляФормированияЭД = ТаблицаДанных[0];
	
	Если Не СтруктураЭД.Свойство("ВидОперацииЭД") 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ДоговорКонтрагента.ВидДоговора")= Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
		
		СтруктураЭД.Вставить("ВидОперацииЭД", Перечисления.ВидыОперацийЭД.Комиссия);
		
	КонецЕсли;
	
	СтруктураЭД.Вставить("ЭтоСчетФактураНаАванс", Ложь);
	ЗаполнитьОсновнуюЧастьУПД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	ЗаполнитьРасширеннуюЧастьУПД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация покупателя).
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляУПДИнформацииПокупателяФНС(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВладелецЭД", СтруктураЭД.ВладелецЭД);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭлектронныйДокументВходящийДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЭлектронныйДокументВходящийДокументыОснования.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Порядок
	|ПОМЕСТИТЬ ВТОснование
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий.ДокументыОснования КАК ЭлектронныйДокументВходящийДокументыОснования
	|ГДЕ
	|	ЭлектронныйДокументВходящийДокументыОснования.Ссылка = &ВладелецЭД
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Дата КАК Дата,
	|	ПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ПоступлениеТоваровУслуг.Товары.(
	|		НомерСтроки КАК НомерСтроки
	|	) КАК Товары,
	|	ПоступлениеТоваровУслуг.Услуги.(
	|		НомерСтроки КАК НомерСтроки
	|	) КАК Услуги,
	|	ПоступлениеТоваровУслуг.АгентскиеУслуги.(
	|		НомерСтроки КАК НомерСтроки
	|	) КАК АгентскиеУслуги
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОснование КАК ВТОснование
	|		ПО ПоступлениеТоваровУслуг.Ссылка = ВТОснование.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураПолученный.Дата КАК Дата,
	|	СчетФактураПолученный.Организация КАК Организация
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОснование КАК ВТОснование
	|		ПО СчетФактураПолученный.Ссылка = ВТОснование.ДокументОснование";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	СодержаниеОперации   = "-";
	ДатаПолученияТоваров = ТекущаяДатаСеанса();
	Организация          = Неопределено;
	Если Не РезультатЗапроса[1].Пустой() Тогда // В основаниях есть документ поступления
		
		Выборка = РезультатЗапроса[1].Выбрать();
		Выборка.Следующий();
		СоставСодержания = Новый Массив;
		Если Не Выборка.Товары.Пустой() Тогда
			СоставСодержания.Добавить(НСтр("ru = 'Товары принял без претензий.'"));
		КонецЕсли;
		Если Не (Выборка.Услуги.Пустой() И Выборка.АгентскиеУслуги.Пустой()) Тогда
			СоставСодержания.Добавить(НСтр("ru = 'Услуги получены, претензий нет.'"));
		КонецЕсли;
		СодержаниеОперации   = СтрСоединить(СоставСодержания, " ");
		ДатаПолученияТоваров = Выборка.Дата;
		Организация          = Выборка.Организация;
		
	ИначеЕсли Не РезультатЗапроса[2].Пустой() Тогда // основание - СФ
		
		Выборка = РезультатЗапроса[2].Выбрать();
		Выборка.Следующий();
		Организация = Выборка.Организация;
		
	КонецЕсли;
	
	СоставительДокумента = "-";
	Если Организация <> Неопределено Тогда
		СоставительДокумента = СоставительДокумента(
			ПолучитьДанныеЮрФизЛица(Организация, ДатаПолученияТоваров));
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СоставительДокументаНаименование", СоставительДокумента);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаПолученияТоваров", ДатаПолученияТоваров);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации",   СодержаниеОперации);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	Если СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.СЧФДОПУПД
		И СсылкаНаОбъект.ВидОперации = Перечисления.ВидыОперацийЭД.Комиссия Тогда
		Возврат;
	КонецЕсли;
	
	СчетФактура = СсылкаНаОбъект;
	ЭтоКомиссионнаяТорговля  = ЭтоКомиссионнаяТорговля(СчетФактура);
	ЭтоСчетФактураНаАванс    = ЭтоСчетФактураНаАванс(СчетФактура);
	ОснованиеСФУчаствуетВЭДО = ОснованиеСФУчаствуетВЭДО(СчетФактура);
	
	МассивСчетовФактур = Новый Массив();
	МассивСчетовФактур.Добавить(СчетФактура);
	Если ЭтоКомиссионнаяТорговля
		Или ЭтоСчетФактураНаАванс
		Или Не ОснованиеСФУчаствуетВЭДО Тогда
		
		ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиСчетаФактуры1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур());
	Иначе
		ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиСчетаФактуры1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур(Истина, Истина), Истина);
	КонецЕсли;
	Если ТаблицаСчетовФактур = Неопределено
			ИЛИ ТаблицаСчетовФактур.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЭД.Вставить("ЭтоСчетФактураНаАванс", ЭтоСчетФактураНаАванс);
	ДанныеДляФормированияЭД = ТаблицаСчетовФактур[0];
	ЗаполнитьОсновнуюЧастьУПД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УКД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляУКДИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ПроверитьГотовностьОснованияУКД(СсылкаНаОбъект, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		СчетФактура = СсылкаНаОбъект;
	Иначе
		СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(СсылкаНаОбъект);
		
		// Если счета-фактуры не требуется, то формируем УКД со статусом 2.
		Если Не ЗначениеЗаполнено(СчетФактура) И СчетФактураНеТребуется(СсылкаНаОбъект) Тогда
			СтруктураЭД.Функция = "ДИС";
			СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ДИСУКД;
			ЗаполнитьДанныеДляДИСИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СчетФактура) Тогда
		Возврат;
	КонецЕсли;
	
	МассивСчетовФактур = Новый Массив();
	МассивСчетовФактур.Добавить(СчетФактура);
	ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиКорректировочныхСчетовФактур1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьКорректировочныхСчетовФактур(Истина, Истина, Истина), Истина);
	Если ТаблицаСчетовФактур = Неопределено
			ИЛИ ТаблицаСчетовФактур.Количество()=0 Тогда
		
		СообщитьОбОшибкеПриФормированииЭД(
			СсылкаНаОбъект, НСтр("ru = 'Возможно, не была произведена корректировка табличной части документа.'"), Отказ);
		Возврат;
	КонецЕсли;
	
	ДанныеДляФормированияЭД = ТаблицаСчетовФактур[0];
	
	ЗаполнитьОсновнуюЧастьУКД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	ЗаполнитьРасширеннуюЧастьУКД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УКД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляДИСИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ПроверитьГотовностьОснованияУКД(СсылкаНаОбъект, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить(СсылкаНаОбъект);
	ТаблицаДанных = УчетНДС.ПолучитьДанныеДляПечатиУниверсальногоКорректировочногоДокумента(
		МассивДокументов, Документы[СсылкаНаОбъект.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов());
	Если ТаблицаДанных.Количество() = 0 Тогда
		СообщитьОбОшибкеПриФормированииЭД(
			СсылкаНаОбъект, НСтр("ru = 'Возможно, не была произведена корректировка табличной части документа.'"), Отказ);
		Возврат;
	КонецЕсли;
	ДанныеДляФормированияЭД = ТаблицаДанных[0];
	
	ЗаполнитьОсновнуюЧастьУКД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	ЗаполнитьРасширеннуюЧастьУКД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УКД (информация покупателя).
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляУКДИнформацииПокупателяФНС(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	СчетФактураПолученный = СсылкаНаЭД.ВладелецФайла;
	РеквизитыСчетаФактуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактураПолученный, "Дата, Организация");
	
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыСчетаФактуры.Организация);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СоставительДокументаНаименование", СоставительДокумента(СведенияОПокупателе));
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаСогласования", РеквизитыСчетаФактуры.Дата);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", НСтр("ru = 'С изменением стоимости согласен.'"));
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УКД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляКСЧФИнформацияПродавцаФНС(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ПроверитьГотовностьОснованияУКД(СсылкаНаОбъект, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивСчетовФактур = Новый Массив();
	МассивСчетовФактур.Добавить(СсылкаНаОбъект);
	ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиКорректировочныхСчетовФактур1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьКорректировочныхСчетовФактур(Истина, Истина, Истина), Истина);
	Если ТаблицаСчетовФактур = Неопределено
			ИЛИ ТаблицаСчетовФактур.Количество()=0 Тогда
		
		СообщитьОбОшибкеПриФормированииЭД(
			СсылкаНаОбъект, НСтр("ru = 'Возможно, не была произведена корректировка табличной части документа-основания.'"), Отказ);
		Возврат;
	КонецЕсли;
	
	ДанныеДляФормированияЭД = ТаблицаСчетовФактур[0];
	
	ЗаполнитьОсновнуюЧастьУКД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Торг12 титул покупателя.
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеПоТорг12ПокупательФНС(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных,
		"ДатаПолученияГруза",
		ДатаПолученияТовараПриемкиРабот(СтруктураЭД.ВладелецЭД));
		
КонецПроцедуры

// Подготавливает данные титула заказчика для электронного документа типа Акт выполненных работ формата 5.01.
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеПоАкт501ЗаказчикФНС(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных,
		"СведенияПоВыполнениюУслуг.ДатаЗаказа",
		ДатаПолученияТовараПриемкиРабот(СтруктураЭД.ВладелецЭД));
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа СоглашениеОбИзмененииСтоимостиПолучатель.
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеПоКорректировочномуДокументуПолучатель(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаПолученияГруза", ТекущаяДатаСеанса());
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ПередачаТоваровПродавец.
//
// Параметры:
//  СсылкаНаОбъект - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеПередачаТоваровПродавец(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	МассивОбъектов = Новый Массив();
	МассивОбъектов.Добавить(СсылкаНаОбъект);
	
	ВключатьУслуги = Истина;
	
	Если ТипЗнч(СсылкаНаОбъект)=Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		ВидОперации = Перечисления.ВидыОперацийЭД.Исправление;
		ДанныеДляПечати = Документы.КорректировкаРеализации.ПолучитьТаблицуСведенийТОРГ12(МассивОбъектов, ВключатьУслуги);
	Иначе
		ВидОперации = Перечисления.ВидыОперацийЭД.ПродажаКомиссия;
		ДанныеДляПечати = Документы.РеализацияТоваровУслуг.ПолучитьТаблицуСведенийТОРГ12(МассивОбъектов, ВключатьУслуги);
	КонецЕсли;
	
	Если ДанныеДляПечати.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыШапки = ДанныеДляПечати[0];
	ТабличнаяЧасть = РеквизитыШапки.ТаблицаДокумента;
	
	НомерТоварнойНакладной = ?(ЗначениеЗаполнено(РеквизитыШапки.ГосударственныйКонтракт), РеквизитыШапки.ГосударственныйКонтракт+"/", "")+РеквизитыШапки.НомерДокумента;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерТоварнойНакладной", НомерТоварнойНакладной);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаТоварнойНакладной",  РеквизитыШапки.ДатаДокумента);
	
	Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления",
			СтроковыеФункцииКлиентСервер.СтрокаВЧисло(РеквизитыШапки.НомерИсправления));
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления",  РеквизитыШапки.ДатаИсправления);
	КонецЕсли;
	
	// Выводим общие реквизиты шапки
	СведенияОПоставщике       = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Поставщик, РеквизитыШапки.ДатаДляПолученияСведений, БанковскийСчетПродавца(РеквизитыШапки));
	СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Грузоотправитель, РеквизитыШапки.ДатаДляПолученияСведений);
	СведенияОПокупателе       = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Покупатель, РеквизитыШапки.ДатаДляПолученияСведений);
	СведенияОГрузополучателе  = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Грузополучатель,  РеквизитыШапки.ДатаДляПолученияСведений);
	
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОПоставщике, "Поставщик", "Юр", РеквизитыШапки.ДатаДляПолученияСведений);
	Если СведенияОПоставщике.Свойство("КодПоОКПО") И ЗначениеЗаполнено(СведенияОПоставщике.КодПоОКПО) Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			"Поставщик.КодОКПО",
			СведенияОПоставщике.КодПоОКПО);
		
	КонецЕсли;
		
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОГрузоотправителе, "Грузоотправитель", "Факт", РеквизитыШапки.ДатаДляПолученияСведений);
	Если СведенияОГрузоотправителе.Свойство("КодПоОКПО") И ЗначениеЗаполнено(СведенияОГрузоотправителе.КодПоОКПО) Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			"Грузоотправитель.КодОКПО",
			СведенияОГрузоотправителе.КодПоОКПО);
		
	КонецЕсли;
		
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОПокупателе, "Плательщик", "Юр", РеквизитыШапки.ДатаДляПолученияСведений);
	Если СведенияОПокупателе.Свойство("КодПоОКПО") И ЗначениеЗаполнено(СведенияОПокупателе.КодПоОКПО) Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			"Плательщик.КодОКПО",
			СведенияОПокупателе.КодПоОКПО);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыШапки.АдресДоставки) Тогда
		СведенияОГрузополучателе.Вставить("АдресДоставки", РеквизитыШапки.АдресДоставки);
	КонецЕсли;
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОГрузополучателе,  "Грузополучатель", "Факт", РеквизитыШапки.ДатаДляПолученияСведений);
	Если СведенияОГрузополучателе.Свойство("КодПоОКПО") И ЗначениеЗаполнено(СведенияОГрузополучателе.КодПоОКПО) Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных,
			"Грузополучатель.КодОКПО",
			СведенияОГрузополучателе.КодПоОКПО);
		
	КонецЕсли;
	
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить код валюты документа. Проверьте, что:
	|	- в документе указана валюта,
	|	- для нее заполнен код по Общероссийскому классификатору валют.'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", РеквизитыШапки.ВалютаКод, ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаНаименование",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыШапки.Валюта, "НаименованиеПолное"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации", ВидОперации);
	
	Если ЗначениеЗаполнено(РеквизитыШапки.ДокументОснование) Тогда
		
		МассивОснований = Новый Массив;
		МассивОснований.Добавить(РеквизитыШапки.ДокументОснование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									"ДокументыОснования",
									МассивОснований);
		
	КонецЕсли;
	
	// Данные договора
	ДанныеОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		РеквизитыШапки.Договор,
		"Номер, Дата, ВидДоговора, Наименование");
	ДокументыОснования = ПодготовитьТаблицуДанныхДоговора(
		ДанныеОснования.Наименование,
		ДанныеОснования.Номер,
		ДанныеОснования.Дата);
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ДокументыОснования, "ДокОснованиеДата",,,
			НСтр("ru = 'Необходимо указать дату договора.'"));
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ДокументыОснования, "Основание");
	
	УчетАгентскогоНДСПокупателем = Ложь; // используется для реализации металлолома, сырых шкур животных.
	Если ЗначениеЗаполнено(РеквизитыШапки.Договор) Тогда
		
		УчетАгентскогоНДСПокупателем = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			РеквизитыШапки.Договор,
			"УчетАгентскогоНДСПокупателем");
			
	КонецЕсли;
		
	ТаблицаТоваров = Новый ТаблицаЗначений();
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("НаименованиеНоменклатуры");
	ТаблицаТоваров.Колонки.Добавить("НаименованиеХарактеристики");
	ТаблицаТоваров.Колонки.Добавить("Сорт");
	ТаблицаТоваров.Колонки.Добавить("Артикул");
	ТаблицаТоваров.Колонки.Добавить("КодТовара");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод");
	ТаблицаТоваров.Колонки.Добавить("ВидУпаковки");
	ТаблицаТоваров.Колонки.Добавить("КоличествоВОдномМесте");
	ТаблицаТоваров.Колонки.Добавить("КоличествоМест");
	ТаблицаТоваров.Колонки.Добавить("МассаБрутто");
	ТаблицаТоваров.Колонки.Добавить("МассаНетто");
	ТаблицаТоваров.Колонки.Добавить("Цена");
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДС");
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаНДС");
	ТаблицаТоваров.Колонки.Добавить("СуммаСНДС");
	ТаблицаТоваров.Колонки.Добавить("ДокументОснование");
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеПодписанные");
	ТаблицаТоваров.Колонки.Добавить("ДопДанныеНеПодписанные");
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	
	Для Каждого ДанныеСтроки Из ТабличнаяЧасть Цикл
		
		// Чтобы удалить строку товара в исправлении количеству товара присваивают 0. 
		Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление
			И ДанныеСтроки.Количество = 0
			И ДанныеСтроки.СуммаБезНДС = 0 Тогда
			
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицыДокумента = ТаблицаТоваров.Добавить();
		СтрокаТаблицыДокумента.Номенклатура                 = ДанныеСтроки.Товар;
		СтрокаТаблицыДокумента.НаименованиеНоменклатуры     = ДанныеСтроки.ТоварНаименование;
		СтрокаТаблицыДокумента.КодТовара                    = ДанныеСтроки.ТоварКод;
		СтрокаТаблицыДокумента.ЕдиницаИзмеренияНаименование = ДанныеСтроки.ЕдиницаИзмеренияНаименование;
		СтрокаТаблицыДокумента.БазоваяЕдиницаКод            = СокрЛП(ДанныеСтроки.ЕдиницаИзмеренияКод);
		СтрокаТаблицыДокумента.МассаНетто                   = ДанныеСтроки.Количество;
		
		СтрокаТаблицыДокумента.СуммаСНДС   = ?(УчетАгентскогоНДСПокупателем = Истина, 0, ДанныеСтроки.СуммаСНДС);
		СтрокаТаблицыДокумента.СуммаНДС    = ДанныеСтроки.СуммаНДС;
		СтрокаТаблицыДокумента.СуммаБезНДС = ДанныеСтроки.СуммаБезНДС;
		СтрокаТаблицыДокумента.СтавкаНДС   = ?(УчетАгентскогоНДСПокупателем = Истина, СтавкаНДСИсчисляетсяНалоговымАгентом(), ДанныеСтроки.СтавкаНДС);
		СтрокаТаблицыДокумента.Цена        = ДанныеСтроки.Цена;
		
		СтруктураДопДанных = Новый Структура;
		СтруктураДопДанных.Вставить("СтавкаНДС", ДанныеСтроки.СтавкаНДС);
		СтруктураДопДанных.Вставить("НомерТД", ДанныеСтроки.НомерТД);
		СтруктураДопДанных.Вставить("КодСтраныПроисхождения", ДанныеСтроки.КодСтраныПроисхождения);
		
		// Значение единицы измерения для возвратной тары
		Если ДанныеСтроки.ТабличнаяЧасть = Перечисления.ТабличныеЧастиДокументов.Товары Тогда
			СтруктураДопДанных.Вставить("ТаблицаДокумента", "Товары");
		ИначеЕсли ДанныеСтроки.ТабличнаяЧасть = Перечисления.ТабличныеЧастиДокументов.Услуги Тогда
			СтруктураДопДанных.Вставить("ТаблицаДокумента", "Услуги");
		ИначеЕсли ДанныеСтроки.ТабличнаяЧасть = Перечисления.ТабличныеЧастиДокументов.ВозвратнаяТара Тогда
			СтрокаТаблицыДокумента.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
			СтруктураДопДанных.Вставить("ТаблицаДокумента", "ВозвратнаяТара");
		КонецЕсли;
		СтрокаТаблицыДокумента.ДопДанныеПодписанные = СтруктураДопДанных;
		
		СтрокаТаблицыДокумента.Сопоставление = СтруктураДляСопоставленияНоменклатурыЭД(
			ДанныеСтроки.Товар, СтрокаТаблицыДокумента.СтавкаНДС);
		
	КонецЦикла;
	
	ДобавитьОбработкуОшибокЗаполненияТабличнойЧастиЭД(ТаблицаТоваров);
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "ТаблицаТоваров");
	
	ИтоговыеСуммы = РассчитатьИтоговыеСуммыДокумента(ТаблицаТоваров);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияПоОтпускуГруза.ДатаОтпуска",
			РеквизитыШапки.ДатаПодписанияДокумента);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОбщиеСведенияОТоварнойНакладной.КоличествоПорядковыхНомеровЗаписей",
			ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписей);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОбщиеСведенияОТоварнойНакладной.МассаГрузаНетто",
			ИтоговыеСуммы.ИтогоМассаНетто);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОбщиеСведенияОТоварнойНакладной.МассаГрузаБрутто",
			ИтоговыеСуммы.ИтогоМассаБрутто);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоПоНакладной.МассаБрутто",
			ИтоговыеСуммы.ИтогоМассаБрутто);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоПоНакладной.МассаНетто",
			ИтоговыеСуммы.ИтогоМассаНетто);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоПоНакладной.СуммаБезНДС",
			ИтоговыеСуммы.ИтогоСуммаБезНДС);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоПоНакладной.СуммаНДС",
			ИтоговыеСуммы.ИтогоНДС);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоПоНакладной.СуммаСНДС",
			?(УчетАгентскогоНДСПокупателем, 0, ИтоговыеСуммы.ИтогоСуммаСНДС));
			
	КурсВзаиморасчетов = 1;
	Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		
		Если ЗначениеЗаполнено(РеквизитыШапки.ДокументОснование)
			И ТипЗнч(РеквизитыШапки.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			
			КурсВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				РеквизитыШапки.ДокументОснование,
				"КурсВзаиморасчетов");
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		КурсВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "КурсВзаиморасчетов");
		
	КонецЕсли;
	СтрокаДопДанных = ДеревоДанных.Строки.Найти("ДопДанные", "ПолныйПуть", Истина);
	ЭлектронноеВзаимодействие.ДобавитьДопДанныеВДерево(СтрокаДопДанных, 
		Новый Структура("КурсВзаиморасчетов", КурсВзаиморасчетов), Истина);
	Если ДанныеОснования.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
		
		ЭлектронноеВзаимодействие.ДобавитьДопДанныеВДерево(СтрокаДопДанных,
			Новый Структура("ПередачаТовараКомитентом", Истина), Истина);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыШапки.КладовщикДолжностьНаименование)
		И ЗначениеЗаполнено(РеквизитыШапки.Кладовщик) Тогда
		
		// Кладовщик работает в организации
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Должность", РеквизитыШапки.КладовщикДолжностьНаименование);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Фамилия", РеквизитыШапки.КладовщикСтруктураФИО.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Имя", РеквизитыШапки.КладовщикСтруктураФИО.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Отчество", РеквизитыШапки.КладовщикСтруктураФИО.Отчество);
	ИначеЕсли ЗначениеЗаполнено(РеквизитыШапки.Кладовщик) Тогда
		// Кладовщик не работает в организации
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ.Фамилия", РеквизитыШапки.КладовщикСтруктураФИО.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ.Имя", РеквизитыШапки.КладовщикСтруктураФИО.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ.Отчество", РеквизитыШапки.КладовщикСтруктураФИО.Отчество);
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа ПередачаРаботИсполнитель.
//
// Параметры:
//  СсылкаНаОбъект - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеПередачаРаботИсполнитель(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	МассивОбъектов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СсылкаНаОбъект);
	ТаблицаСведенийАктаОбОказанииУслуг = Документы[СсылкаНаОбъект.Метаданные().Имя].ПолучитьТаблицуСведенийАктаОбОказанииУслуг(МассивОбъектов);
	
	Если ТаблицаСведенийАктаОбОказанииУслуг.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	ВидОперации = ?(ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.КорректировкаРеализации"),
		Перечисления.ВидыОперацийЭД.Исправление, Перечисления.ВидыОперацийЭД.ПродажаКомиссия); 
	РеквизитыШапки = ТаблицаСведенийАктаОбОказанииУслуг[0];
	ТабличнаяЧасть = РеквизитыШапки.ТаблицаДокумента;
	
	НомерДокумента = ?(ЗначениеЗаполнено(РеквизитыШапки.ГосударственныйКонтракт), РеквизитыШапки.ГосударственныйКонтракт+"/", "")+РеквизитыШапки.НомерДокумента;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерАкта",   НомерДокумента);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаАкта",    РеквизитыШапки.ДатаДокумента);
	
	Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления",
			СтроковыеФункцииКлиентСервер.СтрокаВЧисло(РеквизитыШапки.НомерИсправления));
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления",  РеквизитыШапки.ДатаИсправления);
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации", ВидОперации);
	Если ЗначениеЗаполнено(РеквизитыШапки.ВалютаКод) Тогда
		
		ТекстОшибки = НСтр("ru = 'Не удалось заполнить код валюты документа. Проверьте, что:
			|	- в документе указана валюта,
			|	- для нее заполнен код по Общероссийскому классификатору валют.'");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", РеквизитыШапки.ВалютаКод, ТекстОшибки);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаНаименование",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыШапки.Валюта, "НаименованиеПолное"));
	КонецЕсли;
	
	ТекстЗаголовка = "Мы, нижеподписавшиеся, представитель ИСПОЛНИТЕЛЯ, с одной стороны и представитель ЗАКАЗЧИКА с другой"
		+ " стороны, составили настоящий акт в том, что ИСПОЛНИТЕЛЬ выполнил, а ЗАКАЗЧИК принял следующие работы (услуги).";
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Заголовок", ТекстЗаголовка);
	
	// Выводим общие реквизиты шапки
	СведенияОПоставщике = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Поставщик,  РеквизитыШапки.ДатаДляПолученияСведений, БанковскийСчетПродавца(РеквизитыШапки));
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Получатель, РеквизитыШапки.ДатаДляПолученияСведений);
	
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОПоставщике, "Исполнитель", "Юр", РеквизитыШапки.ДатаДляПолученияСведений);
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОПокупателе, "Заказчик", "Юр", РеквизитыШапки.ДатаДляПолученияСведений);
	
	Если ЗначениеЗаполнено(РеквизитыШапки.ДокументОснование) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									"ДокументыОснования",
									РеквизитыШапки.ДокументОснование);
	КонецЕсли;
	
	// Данные договора
	ДанныеОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СсылкаНаОбъект,
		"ДоговорКонтрагента.Номер, ДоговорКонтрагента.Дата, ДоговорКонтрагента.Наименование");
	Основания = ПодготовитьТаблицуДанныхДоговора(
		ДанныеОснования.ДоговорКонтрагентаНаименование,
		ДанныеОснования.ДоговорКонтрагентаНомер,
		ДанныеОснования.ДоговорКонтрагентаДата);
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(Основания, "ДокОснованиеДата",,,
			НСтр("ru = 'Необходимо указать дату договора.'"));
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, Основания, "Основание");
	
	ТаблицаУслуг = Новый ТаблицаЗначений();
	ТаблицаУслуг.Колонки.Добавить("Номенклатура");
	ТаблицаУслуг.Колонки.Добавить("НаименованиеНоменклатуры");
	ТаблицаУслуг.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	ТаблицаУслуг.Колонки.Добавить("ЕдиницаИзмеренияКод");
	ТаблицаУслуг.Колонки.Добавить("Количество");
	ТаблицаУслуг.Колонки.Добавить("Цена");
	ТаблицаУслуг.Колонки.Добавить("СуммаБезНДС");
	ТаблицаУслуг.Колонки.Добавить("СтавкаНДС");
	ТаблицаУслуг.Колонки.Добавить("СуммаНДС");
	ТаблицаУслуг.Колонки.Добавить("СуммаСНДС");
	ТаблицаУслуг.Колонки.Добавить("Описание");
	ТаблицаУслуг.Колонки.Добавить("ДокументОснование");
	ТаблицаУслуг.Колонки.Добавить("ДопДанныеПодписанные");
	ТаблицаУслуг.Колонки.Добавить("ДопДанныеНеПодписанные");
	ТаблицаУслуг.Колонки.Добавить("ИдТовараУКонтрагента");
	ТаблицаУслуг.Колонки.Добавить("Сопоставление");
	
	ЭтоПередачаРабот = ?(СтруктураЭД.Свойство("ЭтоПередачаРабот"), СтруктураЭД.ЭтоПередачаРабот, Ложь);
	Для Каждого Строка Из ТабличнаяЧасть Цикл
		
		Если НЕ ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			ТекстСообщения = НСтр("ru='В строке %1 табличной части %2 не заполнена номенклатура. Для передачи электронного документа заполнение номенклатуры обязательно.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.НомерСтроки, Строка.ТабличнаяЧасть);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, СсылкаНаОбъект);
			Продолжить;
		КонецЕсли;
		
		Если Строка.Сумма + Строка.СуммаНДС = 0 Тогда
			Продолжить;
		КонецЕсли;
	
		СтрокаТаблицыДокумента = ТаблицаУслуг.Добавить();
	
		СтрокаТаблицыДокумента.Номенклатура                 = Строка.Номенклатура;
		СтрокаТаблицыДокумента.ЕдиницаИзмеренияКод          = ?(ЗначениеЗаполнено(Строка.ЕдиницаИзмерения),СокрЛП(Строка.ЕдиницаИзмеренияКод), "");
		СтрокаТаблицыДокумента.ЕдиницаИзмеренияНаименование = Строка.ЕдиницаИзмеренияНаименование;
		СтрокаТаблицыДокумента.Количество                   = Строка.Количество;
		СтрокаТаблицыДокумента.Описание                     = Строка.НоменклатураНаименование;
		
		СтрокаТаблицыДокумента.СуммаБезНДС     = Строка.СуммаБезНДС;
		СтрокаТаблицыДокумента.СуммаСНДС       = Строка.СуммаСНДС;
		СтрокаТаблицыДокумента.СтавкаНДС       = Строка.СтавкаНДС;
		СтрокаТаблицыДокумента.СуммаНДС        = Строка.СуммаНДС;
		СтрокаТаблицыДокумента.Цена            = Строка.Цена;
		
		СтрокаТаблицыДокумента.ИдТовараУКонтрагента = ПолучитьИДНоменклатуры(
			Строка.Номенклатура, Неопределено, Неопределено);
			
		СтрокаТаблицыДокумента.Сопоставление = СтруктураДляСопоставленияНоменклатурыЭД(
			Строка.Номенклатура, СтрокаТаблицыДокумента.СтавкаНДС, Строка.НоменклатураНаименование);
		
	КонецЦикла;
	
	ДобавитьОбработкуОшибокЗаполненияТабличнойЧастиЭД(ТаблицаУслуг);
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаУслуг, "ТаблицаУслуг");
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОписаниеУслуги.СуммаБезНДСИтого",
		ТаблицаУслуг.Итог("СуммаБезНДС"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОписаниеУслуги.СуммаНДСИтого",
		ТаблицаУслуг.Итог("СуммаНДС"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ОписаниеУслуги.СуммаСНДСИтого",
		ТаблицаУслуг.Итог("СуммаСНДС"));
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияПоВыполнениюУслуг.ДатаИсполнения",
		РеквизитыШапки.ДатаДокумента);
		
	КурсВзаиморасчетов = 1;
	Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		
		Если ЗначениеЗаполнено(РеквизитыШапки.ДокументОснование)
			И ТипЗнч(РеквизитыШапки.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			
			КурсВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				РеквизитыШапки.ДокументОснование,
				"КурсВзаиморасчетов");
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		КурсВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "КурсВзаиморасчетов");
		
	КонецЕсли;
	СтрокаДопДанных = ДеревоДанных.Строки.Найти("ДопДанные", "ПолныйПуть", Истина);
	ЭлектронноеВзаимодействие.ДобавитьДопДанныеВДерево(СтрокаДопДанных, 
		Новый Структура("КурсВзаиморасчетов", КурсВзаиморасчетов), Истина);
		
КонецПроцедуры

// Подготавливает данные для электронного документа типа Реквизиты организации формата CML 2.
//
// Параметры:
//  СсылкаНаОбъект - СправочникСсылка - ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево значений, соответствующее макету РеквизитыОрганизации обработки ОбменСКонтрагентами.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеРеквизитыОрганизации(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	СведенияОбОрганизации = ПолучитьДанныеЮрФизЛица(СсылкаНаОбъект);
	
	ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОбОрганизации, "Организация", "Юр");
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	БанковскиеСчета.Наименование КАК Комментарий,
	|	ЕСТЬNULL(Банки.Наименование, """") КАК БанкНаименование,
	|	ЕСТЬNULL(Банки.Код, """") КАК БанкБИК,
	|	ЕСТЬNULL(БанковскиеСчета.Банк.КоррСчет, """") КАК БанкСчетКорр,
	|	"""" КАК БанкSWIFT,
	|	ЕСТЬNULL(БанкиКорреспондент.Наименование, """") КАК БанкКоррНаименование,
	|	ЕСТЬNULL(БанкиКорреспондент.Код, """") КАК БанкКоррБИК,
	|	ЕСТЬNULL(БанкиКорреспондент.КоррСчет, """") КАК БанкКоррБанкСчетКорр,
	|	"""" КАК БанкКоррSWIFT
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Банки КАК Банки
	|		ПО БанковскиеСчета.Банк = Банки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Банки КАК БанкиКорреспондент
	|		ПО БанковскиеСчета.БанкДляРасчетов = БанкиКорреспондент.Ссылка
	|ГДЕ
	|	БанковскиеСчета.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Владелец", СсылкаНаОбъект);
	Результат = Запрос.Выполнить();
	БанковскиеСчета = Результат.Выгрузить();
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, БанковскиеСчета, "РасчетныеСчета");
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаФормирования", ТекущаяДатаСеанса());
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "Наименование", "Реквизиты "+СсылкаНаОбъект.Наименование);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Счет формата CML 2.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДокумента - ДеревоЗначений - дерево значений, соответствующее макету СчетНаОплату обработки ОбменСКонтрагентами.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеПоСчету(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента, Отказ) Экспорт
	
	МассивДокументов = Новый Массив();
	МассивДокументов.Добавить(СсылкаНаОбъект);
	
	ДанныеДляФормированияЭД = Документы.СчетНаОплатуПокупателю.ПолучитьТаблицуСведенийСчетаНаОплату(МассивДокументов);
	
	РеквизитыШапки   = ДанныеДляФормированияЭД[0];
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Номер", РеквизитыШапки.НомерДокумента);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Дата", РеквизитыШапки.ДатаДокумента);
	
	ТаблицаДокумента = ДанныеДляФормированияЭД[0].ТаблицаДокумента;
	
	ТаблицаТоваров = Новый ТаблицаЗначений();	
	ТаблицаТоваров.Колонки.Добавить("Артикул");
	ТаблицаТоваров.Колонки.Добавить("Наименование");
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("ШтрихКод");
	ТаблицаТоваров.Колонки.Добавить("ИдТовараУКонтрагента");
	ТаблицаТоваров.Колонки.Добавить("Описание");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименование");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаМеждународноеСокращение");
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименованиеПолное");
	ТаблицаТоваров.Колонки.Добавить("Цена");
	ТаблицаТоваров.Колонки.Добавить("Количество");
	ТаблицаТоваров.Колонки.Добавить("Сумма");
	ТаблицаТоваров.Колонки.Добавить("НДСУчтеноВСумме");
	ТаблицаТоваров.Колонки.Добавить("СуммаНДС");
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	
	Для Каждого ДанныеСтроки Из ТаблицаДокумента Цикл
		
		СтрокаТаблицыТоваров = ТаблицаТоваров.Добавить();
		
		СуммаСУчетомСкидки = ДанныеСтроки.Сумма - ДанныеСтроки.СуммаСкидки;
		Цена = Окр(ДанныеСтроки.Цена * ?(ДанныеСтроки.Сумма = 0, 1, СуммаСУчетомСкидки/ДанныеСтроки.Сумма), 2);
		
		СтрокаТаблицыТоваров.Артикул 					= ДанныеСтроки.НоменклатураАртикул;
		СтрокаТаблицыТоваров.Наименование				= ПолучитьНаименованиеНоменклатуры(
			ДанныеСтроки.НоменклатураНаименование);
		СтрокаТаблицыТоваров.Описание 					= ДанныеСтроки.Содержание;
		СтрокаТаблицыТоваров.Номенклатура 				= ДанныеСтроки.Номенклатура;
		СтрокаТаблицыТоваров.БазоваяЕдиницаКод 			= СокрЛП(ДанныеСтроки.ЕдиницаИзмеренияКод);
		СтрокаТаблицыТоваров.БазоваяЕдиницаНаименование = ДанныеСтроки.ЕдиницаИзмеренияНаименование;
		СтрокаТаблицыТоваров.БазоваяЕдиницаНаименованиеПолное		 = ДанныеСтроки.ЕдиницаИзмеренияНаименование;
		СтрокаТаблицыТоваров.БазоваяЕдиницаМеждународноеСокращение	 = "-";
		СтрокаТаблицыТоваров.Цена 						= Цена;
		СтрокаТаблицыТоваров.Количество 				= ДанныеСтроки.Количество;
		СтрокаТаблицыТоваров.Сумма 						= СуммаСУчетомСкидки;
		СтрокаТаблицыТоваров.НДСУчтеноВСумме 			= РеквизитыШапки.СуммаВключаетНДС;
		СтрокаТаблицыТоваров.СуммаНДС = ДанныеСтроки.СуммаНДС;
		СтрокаТаблицыТоваров.СтавкаНДС = ДанныеСтроки.СтавкаНДС;
		СтрокаТаблицыТоваров.Сопоставление = СтруктураДляСопоставленияНоменклатурыЭД(
			ДанныеСтроки.Номенклатура, СтрокаТаблицыТоваров.СтавкаНДС, ДанныеСтроки.Содержание);
		
	КонецЦикла;
	
	// Если скидка задана в целом по документу
	Если РеквизитыШапки.СуммаСкидки <> 0  Тогда
	
		СуммаСУчетомСкидки = ТаблицаТоваров.Итог("Сумма") - РеквизитыШапки.СуммаСкидки;
		
		КоэфСкидки = ?(ТаблицаТоваров.Итог("Сумма") = 0, 1, СуммаСУчетомСкидки/ТаблицаТоваров.Итог("Сумма"));
		
		ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(СуммаСУчетомСкидки, ТаблицаТоваров, "Сумма");
		ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(Окр(ТаблицаТоваров.Итог("СуммаНДС")*КоэфСкидки, 2), ТаблицаТоваров, "СуммаНДС");
		ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(Окр(ТаблицаТоваров.Итог("Цена")*КоэфСкидки, 2), ТаблицаТоваров, "Цена");
	
	КонецЕсли;
	
	СведенияОПоставщике = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Поставщик, РеквизитыШапки.ДатаДляПолученияСведений, БанковскийСчетПродавца(РеквизитыШапки));
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Покупатель, РеквизитыШапки.ДатаДляПолученияСведений);
	
	ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОПоставщике, "Продавец",   "Юр", РеквизитыШапки.ДатаДляПолученияСведений);
	ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОПокупателе, "Покупатель", "Юр", РеквизитыШапки.ДатаДляПолученияСведений);
	
	СуммаИтог     = ТаблицаТоваров.Итог("Сумма");
	СуммаНДСИтог  = ТаблицаТоваров.Итог("СуммаНДС");
	СуммаСНДСИтог = СуммаИтог + ?(РеквизитыШапки.СуммаВключаетНДС, 0, СуммаНДСИтог);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Валюта", РеквизитыШапки.ВалютаКод);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Курс",   РеквизитыШапки.КурсВзаиморасчетов);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДокумента,
		"Сумма",
		СуммаСНДСИтог);
	
	Если ЗначениеЗаполнено(РеквизитыШапки.ДоговорКонтрагента) Тогда
		ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыШапки.ДоговорКонтрагента, "Наименование, Номер, Дата");
		НазначениеПлатежа = НазначениеПлатежаДляСчетаНаОплату(ДанныеДоговора.Наименование, ДанныеДоговора.Номер, ДанныеДоговора.Дата);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "НазначениеПлатежа", НазначениеПлатежа);
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "СрокПлатежа", РеквизитыШапки.СрокОплаты); 
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДокумента, ТаблицаТоваров, "Товары");
	
	// Расчетный счет
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.НомерСчета", РеквизитыШапки.НомерСчета);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.Банк.Наименование", РеквизитыШапки.БанкНаименование);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.Банк.БИК", РеквизитыШапки.БИК);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.Банк.СчетКорреспондентский", РеквизитыШапки.КоррСчет);
	
	Если ЗначениеЗаполнено(РеквизитыШапки.БанкДляРасчетов) Тогда
		
		РеквизитыБанка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыШапки.БанкДляРасчетов, "Наименование,КоррСчет,Код");
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.Наименование", РеквизитыБанка.Наименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.БИК", РеквизитыБанка.Код);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РасчетныйСчет.БанкКорреспондент.СчетКорреспондентский", РеквизитыБанка.КоррСчет);
	
	КонецЕсли;
	
	// Итоги по документу
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.Сумма", СуммаСНДСИтог);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаСкидки", 0);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаБезСкидки",  СуммаСНДСИтог);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.СуммаНДС",        СуммаНДСИтог);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогоПоДокументу.ЦенаВключаетНДС", РеквизитыШапки.СуммаВключаетНДС);
		
	// Отвественные лица
	РуководительДолжность = ?(ЗначениеЗаполнено(РеквизитыШапки.РуководительДолжностьНаименование), 
		РеквизитыШапки.РуководительДолжностьНаименование, "Руководитель");
	ЗаполнитьДанныеФизическогоЛица(ДеревоДокумента, "Продавец.Руководитель", РеквизитыШапки.РуководительСтруктураФИО, РуководительДолжность);
		
	// Итоговая строка
	ШаблонСтроки = НСтр("ru = 'Всего наименований %1, на сумму %2'");
	
	ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки,
		ТаблицаТоваров.Количество(),
		ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаСНДСИтог, РеквизитыШапки.Валюта));
		
	ИтоговаяСтрока = ИтоговаяСтрока +  Символы.ПС + ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(СуммаСНДСИтог, РеквизитыШапки.Валюта);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ИтогиПрописью", ИтоговаяСтрока);

КонецПроцедуры

// Подготавливает данные для электронного документа типа ОтчетКомитенту формата CML 2.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//    Параметр ДополнительныеРеквизитыДляТаблицыТоваров в общей структуре параметров предназначен для заполнения
//    колонки ДополнительныеРеквизиты в таблице товаров.
//  ДеревоДокумента - ДеревоЗначений - дерево значений, соответствующее макету ОтчетКомиссионераОПродажах обработки ОбменСКонтрагентами.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеПоОтчетуОПродажахКомиссионногоТовара(СсылкаНаОбъект, СтруктураЭД, ДеревоДокумента, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтчетКомитентуОПродажах.Дата КАК Дата,
	|	ОтчетКомитентуОПродажах.ВалютаДокумента КАК ВалютаДокумента,
	|	ОтчетКомитентуОПродажах.ВалютаДокумента.Код КАК ВалютаКод,
	|	ОтчетКомитентуОПродажах.СуммаДокумента КАК СуммаДокумента,
	|	ОтчетКомитентуОПродажах.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ОтчетКомитентуОПродажах.СуммаВознаграждения КАК СуммаВознаграждения,
	|	ОтчетКомитентуОПродажах.СпособРасчетаКомиссионногоВознаграждения КАК СпособРасчетаКомиссионногоВознаграждения,
	|	ОтчетКомитентуОПродажах.ПроцентКомиссионногоВознаграждения КАК ПроцентКомиссионногоВознаграждения,
	|	ОтчетКомитентуОПродажах.СтавкаНДСВознаграждения КАК СтавкаНДСВознаграждения,
	|	ОтчетКомитентуОПродажах.Организация КАК Организация,
	|	ОтчетКомитентуОПродажах.Контрагент КАК Контрагент,
	|	ОтчетКомитентуОПродажах.УдержатьВознаграждение КАК УдержатьВознаграждение,
	|	ОтчетКомитентуОПродажах.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ОтчетКомитентуОПродажах.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов
	|ИЗ
	|	Документ.ОтчетКомитентуОПродажах КАК ОтчетКомитентуОПродажах
	|ГДЕ
	|	ОтчетКомитентуОПродажах.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Сумма КАК СуммаПродажи,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	Товары.СуммаНДС КАК СуммаНДС,
	|	Товары.СуммаВознаграждения КАК СуммаВознаграждения,
	|	Товары.Покупатель КАК ПокупательСсылка,
	|	Товары.Цена КАК ЦенаПродажи,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Количество КАК Количество,
	|	Товары.ЦенаПоступления КАК Цена,
	|	Товары.СуммаПоступления КАК Сумма,
	|	Товары.Номенклатура.Наименование КАК НоменклатураНаименование,
	|	Товары.ЕдиницаИзмерения.Код КАК БазоваяЕдиницаКод,
	|	Товары.ЕдиницаИзмерения.Наименование КАК БазоваяЕдиницаНаименование,
	|	Товары.ЕдиницаИзмерения.НаименованиеПолное КАК БазоваяЕдиницаНаименованиеПолное,
	|	Товары.Номенклатура.Наименование КАК Наименование,
	|	Товары.ДатаРеализации КАК ДатаРеализации,
	|	""-"" КАК БазоваяЕдиницаМеждународноеСокращение,
	|	Товары.Ссылка.СуммаВключаетНДС КАК НДСУчтеноВСумме,
	|	Товары.Ссылка.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.ОтчетКомитентуОПродажах.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетКомитентуОПродажахДенежныеСредства.ВидОтчетаПоПлатежам КАК ВидОтчетаПоПлатежам,
	|	ОтчетКомитентуОПродажахДенежныеСредства.Сумма КАК Сумма,
	|	ОтчетКомитентуОПродажахДенежныеСредства.СтавкаНДС КАК СтавкаНДС,
	|	ОтчетКомитентуОПродажахДенежныеСредства.СуммаНДС КАК СуммаНДС,
	|	ОтчетКомитентуОПродажахДенежныеСредства.Покупатель КАК Покупатель,
	|	ОтчетКомитентуОПродажахДенежныеСредства.ДатаСобытия КАК ДатаСобытия
	|ИЗ
	|	Документ.ОтчетКомитентуОПродажах.ДенежныеСредства КАК ОтчетКомитентуОПродажахДенежныеСредства
	|ГДЕ
	|	ОтчетКомитентуОПродажахДенежныеСредства.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыШапки = РезультатЗапроса[0].Выбрать();
	РеквизитыШапки.Следующий();
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДокумента, 
			"ДатаФормирования", 
			ТекущаяДатаСеанса());
			
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДокумента, 
			"Курс", 
			РеквизитыШапки.КурсВзаиморасчетов);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДокумента, 
			"НачалоПериода", 
			РеквизитыШапки.Дата);
			
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДокумента, 
			"КонецПериода", 
			РеквизитыШапки.Дата);
			
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДокумента, 
			"Валюта", 
			РеквизитыШапки.ВалютаКод);	
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДокумента, 
			"Сумма", 
			РеквизитыШапки.СуммаДокумента);
			
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДокумента, 
			"ИтогоПоДокументу.ЦенаВключаетНДС", 
			РеквизитыШапки.СуммаВключаетНДС);
			
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДокумента, 
			"ИтогоПоДокументу.СтавкаНДСВознаграждения", 
			?(РеквизитыШапки.СтавкаНДСВознаграждения = Перечисления.СтавкиНДС.ПустаяСсылка(), 
													Перечисления.СтавкиНДС.БезНДС, РеквизитыШапки.СтавкаНДСВознаграждения));
			
	СведенияОКомитенте = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Контрагент);
	ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОКомитенте, "Комитент", "Юр", РеквизитыШапки.Дата);
	
	СведенияОКомиссионере = ПолучитьДанныеЮрФизЛица(РеквизитыШапки.Организация);
	ЗаполнитьДанныеУчастника(ДеревоДокумента, СведенияОКомиссионере, "Комиссионер", "Юр", РеквизитыШапки.Дата);
	
	ТаблицаТоваров = РезультатЗапроса[1].Выгрузить();
	ТаблицаТоваров.Колонки.Добавить("Характеристика");
	ТаблицаТоваров.Колонки.Добавить("Упаковка");
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		СтрокаТаблицы.Сопоставление = СтруктураДляСопоставленияНоменклатурыЭД(
			СтрокаТаблицы.Номенклатура, СтрокаТаблицы.СтавкаНДС);
	КонецЦикла;
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДокумента, ТаблицаТоваров, "Товары");
	СтрокаТаблицаТоваров = ДеревоДокумента.Строки.Найти("Товары", "ПолныйПуть");
	Для Каждого Товар Из СтрокаТаблицаТоваров.Строки Цикл
		
		ИндексСтроки = Число(Товар.Значение) - 1;
		Покупатель = ТаблицаТоваров[ИндексСтроки].ПокупательСсылка;
		ЗаполнитьДанныеУчастника(Товар, ПолучитьДанныеЮрФизЛица(Покупатель), "Товары.НомерСтроки.Покупатель", "Юр", РеквизитыШапки.Дата);
		
		ДатаРеализации = ТаблицаТоваров[ИндексСтроки].ДатаРеализации;
		СтрокаДопДанных = Товар.Строки.Найти("Товары.НомерСтроки.ДопДанныеПодписанные", "ПолныйПуть", Истина);
		ЭлектронноеВзаимодействие.ДобавитьДопДанныеВДерево(СтрокаДопДанных, Новый Структура("ДатаРеализации", ДатаРеализации));
		
	КонецЦикла;
	
	ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(РеквизитыШапки.Организация, РеквизитыШапки.Дата);
	СтрокаДопДанных = ДеревоДокумента.Строки.Найти("ДопДанные", "ПолныйПуть", Истина);
	ЭлектронноеВзаимодействие.ДобавитьДопДанныеВДерево(СтрокаДопДанных, Новый Структура("ПлательщикНДС", ПлательщикНДС), Истина);
	ЭлектронноеВзаимодействие.ДобавитьДопДанныеВДерево(СтрокаДопДанных, Новый Структура("УдержатьВознаграждение", РеквизитыШапки.УдержатьВознаграждение), Истина);
	
	// Табличная часть "Денежные средства"
	Если Не РезультатЗапроса[2].Пустой() Тогда
		
		ТаблицаДС = Новый ТаблицаЗначений;
		ТаблицаДС.Колонки.Добавить("ВидОтчетаПоПлатежам");
		ТаблицаДС.Колонки.Добавить("Сумма");
		ТаблицаДС.Колонки.Добавить("СтавкаНДС");
		ТаблицаДС.Колонки.Добавить("СуммаНДС");
		ТаблицаДС.Колонки.Добавить("Покупатель");
		ТаблицаДС.Колонки.Добавить("ДатаСобытия");
		
		СоответствиеСтавокНДС = Новый Соответствие;
		ЗаполнитьСоответствиеСтавокНДС(СоответствиеСтавокНДС);
		ВыборкаДС = РезультатЗапроса[2].Выбрать();
		Пока ВыборкаДС.Следующий() Цикл
			
			СтрокаДС = ТаблицаДС.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДС, ВыборкаДС,, "ВидОтчетаПоПлатежам, СтавкаНДС, Покупатель");
			Если ВыборкаДС.ВидОтчетаПоПлатежам = Перечисления.ВидыОтчетовПоПлатежамКомиссия.Аванс Тогда
				СтрокаДС.ВидОтчетаПоПлатежам = "Аванс";
			ИначеЕсли ВыборкаДС.ВидОтчетаПоПлатежам = Перечисления.ВидыОтчетовПоПлатежамКомиссия.Оплата Тогда
				СтрокаДС.ВидОтчетаПоПлатежам = "Оплата";
			ИначеЕсли ВыборкаДС.ВидОтчетаПоПлатежам = Перечисления.ВидыОтчетовПоПлатежамКомиссия.ЗачетАванса Тогда
				СтрокаДС.ВидОтчетаПоПлатежам = "ЗачетАванса";
			КонецЕсли;
			Если ЗначениеЗаполнено(ВыборкаДС.СтавкаНДС) Тогда
				СтрокаДС.СтавкаНДС = СоответствиеСтавокНДС[ВыборкаДС.СтавкаНДС];
			КонецЕсли;
			Если ЗначениеЗаполнено(ВыборкаДС.Покупатель) Тогда
				СтрокаДС.Покупатель = ДанныеКонтрагентаСтруктурой(ВыборкаДС.Покупатель, РеквизитыШапки.Дата);
			КонецЕсли;
			
		КонецЦикла;
		
		// Добавляем данные табличной части "Денежные средства" в доп. данные электронного документа
		ДанныеДС = ОбщегоНазначения.ЗначениеВСтрокуXML(ТаблицаДС);
		ЭлектронноеВзаимодействие.ДобавитьДопДанныеВДерево(СтрокаДопДанных, Новый Структура("ДенежныеСредства", ДанныеДС), Истина);
		
	КонецЕсли;
	
	Если РеквизитыШапки.СуммаДокумента < 0 Тогда
		ТекстИтоговаяСтрока = Нстр("ru = 'Всего возвращено наименований'");
	Иначе
		ТекстИтоговаяСтрока = Нстр("ru = 'Всего продано наименований'");
	КонецЕсли;
	
	ИтоговаяСтрока = ТекстИтоговаяСтрока
		+ " "
		+ ТаблицаТоваров.Количество()
		+ Нстр("ru = ', на сумму'")
		+ " "
		+ ОбщегоНазначенияБПВызовСервера.ФорматСумм(РеквизитыШапки.СуммаДокумента, РеквизитыШапки.ВалютаДокумента);
	
	СуммаПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(РеквизитыШапки.СуммаДокумента, РеквизитыШапки.ВалютаДокумента);
		
	Если РеквизитыШапки.СуммаВознаграждения <> 0 Тогда
		СуммаВознаграждения = Нстр("ru = 'Сумма комиссионного вознаграждения составила'")
			+ " "
			+ ?(РеквизитыШапки.СуммаВознаграждения < 0, Нстр("ru = 'минус'") + " ", "")
			+ РаботаСКурсамиВалют.СформироватьСуммуПрописью(РеквизитыШапки.СуммаВознаграждения, РеквизитыШапки.ВалютаДокумента);
	КонецЕсли;
	
	ИтоговаяСтрока = ИтоговаяСтрока + Символы.ПС + СуммаПрописью + Символы.ПС + СуммаВознаграждения;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДокумента, 
			"ИтогиПрописью", 
			ИтоговаяСтрока);
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеДляАктОРасхождениях()
//
Процедура ЗаполнитьДанныеДляАктОРасхождениях(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	МассивОбъектов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СсылкаНаОбъект);
	
	ДанныеДляФормированияЭД = Документы.АктОРасхождениях.ПолучитьДанныеДляПечатнойФормыТОРГ2(МассивОбъектов);
	
	ВыборкаШапка = ДанныеДляФормированияЭД.ДанныеПечати.Выбрать();
	Если Не ВыборкаШапка.Следующий() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Наименование документа.
	НаименованиеДокумента = НСтр("ru = 'Акт об установленном расхождении по количеству и качеству при приемке товарно-материальных ценностей'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НаименованиеДокумента", НаименованиеДокумента);
	
	// Обстоятельства составления.
	Если ВыборкаШапка.ВидОперации = Перечисления.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке Тогда
		ОбстоятельстваСоставления = "1";
	Иначе
		ОбстоятельстваСоставления = "2";
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных, "ОбстоятельстваСоставленияДокумента", ОбстоятельстваСоставления);
	
	// Номер документа.
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных, "НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаШапка.НомерДокумента, Истина, Ложь));

	// Дата документа.
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных, "ДатаДокумента", ВыборкаШапка.ДатаДокумента);
	
	// Покупатель.
	СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(ВыборкаШапка.Организация, ВыборкаШапка.ДатаДокумента);
	ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПокупателе, "Покупатель", "Юр", ВыборкаШапка.ДатаДокумента);
	
	// Грузополучатель.
	Если ЗначениеЗаполнено(ВыборкаШапка.Грузополучатель) Тогда
		СведенияОГрузополучателе = ПолучитьДанныеЮрФизЛица(ВыборкаШапка.Грузополучатель, ВыборкаШапка.ДатаДокумента);
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОГрузополучателе, "Грузополучатель", "Факт", ВыборкаШапка.ДатаДокумента);
	КонецЕсли;
		
	// Составитель.
	СоставительДокументаНаименование = СведенияОПокупателе.ПолноеНаименование
		+ ?(ЗначениеЗаполнено(СведенияОПокупателе.КПП),
		СтрШаблон(НСтр("ru = ', ИНН/КПП %1/%2'"), СведенияОПокупателе.ИНН, СведенияОПокупателе.КПП),
		СтрШаблон(НСтр("ru = ', ИНН %1'"), СведенияОПокупателе.ИНН));
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных, "СоставительДокументаНаименование", СоставительДокументаНаименование);
		
	// Поставщик.
	СведенияОПоставщике  = ПолучитьДанныеЮрФизЛица(ВыборкаШапка.Поставщик, ВыборкаШапка.ДатаДокумента);
	ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПоставщике, "Продавец", "Юр", ВыборкаШапка.ДатаДокумента);
	
	// Грузоотправитель.
	Если ЗначениеЗаполнено(ВыборкаШапка.Грузоотправитель) Тогда
		СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(ВыборкаШапка.Грузоотправитель, ВыборкаШапка.ДатаДокумента);
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОГрузоотправителе, "Грузополучатель", "Факт", ВыборкаШапка.ДатаДокумента);
	КонецЕсли;
	
	// Сведения по транспортным документам.
	СведенияПоТранспортнымДокументам = ЭлектронноеВзаимодействиеСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
		ДеревоДанных, "СведенияПоТранспортнымДокументам");
	ВыборкаТовары = ДанныеДляФормированияЭД.ДанныеТовары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		Если Не ЗначениеЗаполнено(ВыборкаТовары.КоличествоПоДокументам) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = СведенияПоТранспортнымДокументам.Добавить();
		НоваяСтрока.ВидУпаковки                  = "";
		НоваяСтрока.Количество                   = ВыборкаТовары.КоличествоПоДокументам;
		НоваяСтрока.НаименованиеГруза            = ВыборкаТовары.НоменклатураНаименование;
		НоваяСтрока.ЕдиницаИзмеренияКод          = ВыборкаТовары.КодПоОКЕИ;
		НоваяСтрока.ЕдиницаИзмеренияНаименование = ВыборкаТовары.ЕдиницаИзмеренияНаименование;
	КонецЦикла;
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, СведенияПоТранспортнымДокументам, "СведенияПоТранспортнымДокументам");
	
	// Сопроводительный документ.
	ВыборкаДанныеСчетовФактур = ДанныеДляФормированияЭД.ДанныеСчетовФактур.Выбрать();
	Если ВыборкаДанныеСчетовФактур.Следующий() Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, "СведенияОбОсмотреГруза.СопроводительныйДокумент.Наименование", "Счет-фактура");
	Иначе
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, "СведенияОбОсмотреГруза.СопроводительныйДокумент.Наименование", "Товарная накладная");
	КонецЕсли;
	ТекстОшибки = НСтр("ru = 'В документе поступления не заполнен номер входящего документа.'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных, "СведенияОбОсмотреГруза.СопроводительныйДокумент.Номер",
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаШапка.НомерВходящегоДокумента, Истина, Ложь), ТекстОшибки);
	ТекстОшибки = НСтр("ru = 'В документе поступления не заполнена дата входящего документа.'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, "СведенияОбОсмотреГруза.СопроводительныйДокумент.Дата", ВыборкаШапка.ДатаВходящегоДокумента, ТекстОшибки);
	
	// Сведения о лице, принявшем товар.
	Кладовщик = ОтветственныеЛицаБП.ОтветственноеЛицоНаСкладе(ВыборкаШапка.Склад, ВыборкаШапка.ДатаДокумента);
	Если ЗначениеЗаполнено(Кладовщик) Тогда
		ОбщийПуть = "СведенияОЛицеПринявшемТовар.РаботникОрганизацииПокупателя.";
		ДанныеКладовщика = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Кладовщик, "Фамилия, Имя, Отчество");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, ОбщийПуть + "Фамилия", ДанныеКладовщика.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, ОбщийПуть + "Имя", ДанныеКладовщика.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, ОбщийПуть + "Отчество", ДанныеКладовщика.Отчество);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, ОбщийПуть + "ОснованиеПолномочий", НСтр("ru = 'Должностные обязанности'"));
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, ОбщийПуть + "Должность", НСтр("ru = 'Кладовщик'"));
	КонецЕсли;
	
	// Краткое описание события.
	ОписаниеСобытия = НСтр("ru = 'При приемке указанных в документе ценностей (результатов работ) установлены расхождения с сопроводительными документами.'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных, "КраткоеОписаниеСобытия", ОписаниеСобытия);
	
	// Результаты приемки.
	РезультатыПриемки = ЭлектронноеВзаимодействиеСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
		ДеревоДанных, "РезультатыПриемки");
		
	ВыборкаТовары.Сбросить();
	ТаблицаКодовМаркировки     = ДанныеДляФормированияЭД.ШтрихкодыУпаковок.Выгрузить();
	ТаблицаУпаковкиРасхождения = ДанныеДляФормированияЭД.УпаковкиРасхождения.Выгрузить();
	ТаблицаУпаковкиИзлишек     = ТаблицаУпаковкиРасхождения.Скопировать(Новый Структура("ТипРасхождения", Перечисления.ТипыРасхождений.Излишек));
	ТаблицаУпаковкиНедостача   = ТаблицаУпаковкиРасхождения.Скопировать(Новый Структура("ТипРасхождения", Перечисления.ТипыРасхождений.Недостача));
	РасхожденияВСуммеИКоличестве = Ложь;
	Пока ВыборкаТовары.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаТовары.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = РезультатыПриемки.Добавить();
		НоваяСтрока.ЕдиницаИзмеренияНаименование = ВыборкаТовары.ЕдиницаИзмеренияНаименование;
		НоваяСтрока.ЕдиницаИзмеренияКод          = ВыборкаТовары.КодПоОКЕИ;
		
		НоваяСтрока.Товар = Новый Структура;
		НоваяСтрока.Товар.Вставить("Наименование", ВыборкаТовары.НоменклатураНаименование);
		НоваяСтрока.Товар.Вставить("Артикул"     , ВыборкаТовары.НоменклатураПоставщикаАртикул);
		НоваяСтрока.Товар.Вставить("Код"         , ВыборкаТовары.Код);
		
		СтавкаНДСЧислом = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(ВыборкаТовары.СтавкаНДС);
		
		// По документу.
		НоваяСтрока.ПоДокументу = Новый Структура;
		НоваяСтрока.ПоДокументу.Вставить("Количество", ВыборкаТовары.КоличествоПоДокументам);
		НоваяСтрока.ПоДокументу.Вставить("Цена",
			УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(ВыборкаТовары.Цена, ВыборкаТовары.ЦенаВключаетНДСПоДокументам, Ложь, СтавкаНДСЧислом));
		НоваяСтрока.ПоДокументу.Вставить("СтавкаНДС" , ВыборкаТовары.СтавкаНДС);
		НоваяСтрока.ПоДокументу.Вставить("СуммаНДС"  , ВыборкаТовары.СуммаНДСПоДокументам);
		НоваяСтрока.ПоДокументу.Вставить("СуммаСНДС" ,
			?(ВыборкаТовары.ЦенаВключаетНДСПоДокументам, ВыборкаТовары.СуммаПоДокументам, ВыборкаТовары.СуммаПоДокументам + ВыборкаТовары.СуммаНДСПоДокументам));
		
		// По факту.
		НоваяСтрока.ПоФакту = Новый Структура;
		НоваяСтрока.ПоФакту.Вставить("Количество", ВыборкаТовары.КоличествоПоФакту);
		НоваяСтрока.ПоФакту.Вставить("Цена",
			УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(ВыборкаТовары.ЦенаПоФакту, ВыборкаТовары.ЦенаВключаетНДСПоФакту, Ложь, СтавкаНДСЧислом));
		НоваяСтрока.ПоФакту.Вставить("СтавкаНДС" , ВыборкаТовары.СтавкаНДС);
		НоваяСтрока.ПоФакту.Вставить("СуммаНДС"  , ВыборкаТовары.СуммаНДСПоФакту);
		НоваяСтрока.ПоФакту.Вставить("СуммаСНДС" ,
			?(ВыборкаТовары.ЦенаВключаетНДСПоФакту, ВыборкаТовары.СуммаПоФакту, ВыборкаТовары.СуммаПоФакту + ВыборкаТовары.СуммаНДСПоФакту));
		ЗаполнитьСведенияОМаркировкеВАктеОРасхождениях(НоваяСтрока.ПоФакту, ВыборкаТовары, ТаблицаКодовМаркировки);
		
		Если Не РасхожденияВСуммеИКоличестве Тогда
			РасхожденияВСуммеИКоличестве = Не (НоваяСтрока.ПоДокументу.Количество = НоваяСтрока.ПоФакту.Количество
				И НоваяСтрока.ПоДокументу.СуммаСНДС = НоваяСтрока.ПоФакту.СуммаСНДС);
		КонецЕсли;
		
		// Излишки и недостача.
		НоваяСтрока.Излишки = Новый Структура;
		НоваяСтрока.Недостача = Новый Структура;
		Если ВыборкаТовары.КоличествоПоФакту > ВыборкаТовары.КоличествоПоДокументам Тогда
			НоваяСтрока.Излишки.Вставить("Количество", ВыборкаТовары.КоличествоПоФакту - ВыборкаТовары.КоличествоПоДокументам);
			ЗаполнитьСведенияОМаркировкеВАктеОРасхождениях(НоваяСтрока.Излишки, ВыборкаТовары, ТаблицаУпаковкиИзлишек);
		ИначеЕсли ВыборкаТовары.КоличествоПоФакту < ВыборкаТовары.КоличествоПоДокументам Тогда
			НоваяСтрока.Недостача.Вставить("Количество", ВыборкаТовары.КоличествоПоДокументам - ВыборкаТовары.КоличествоПоФакту);
			ЗаполнитьСведенияОМаркировкеВАктеОРасхождениях(НоваяСтрока.Недостача, ВыборкаТовары, ТаблицаУпаковкиНедостача);
		КонецЕсли;
		Если ВыборкаТовары.СуммаНДСПоФакту > ВыборкаТовары.СуммаНДСПоДокументам Тогда
			НоваяСтрока.Излишки.Вставить("СуммаНДС", ВыборкаТовары.СуммаНДСПоФакту - ВыборкаТовары.СуммаНДСПоДокументам);
		ИначеЕсли ВыборкаТовары.СуммаНДСПоФакту < ВыборкаТовары.СуммаНДСПоДокументам Тогда
			НоваяСтрока.Недостача.Вставить("СуммаНДС", ВыборкаТовары.СуммаНДСПоДокументам - ВыборкаТовары.СуммаНДСПоФакту);
		КонецЕсли;
		Если ВыборкаТовары.СуммаПоФакту > ВыборкаТовары.СуммаПоДокументам Тогда
			НоваяСтрока.Излишки.Вставить("СуммаСНДС", ВыборкаТовары.СуммаПоФакту - ВыборкаТовары.СуммаПоДокументам);
		ИначеЕсли ВыборкаТовары.СуммаПоФакту < ВыборкаТовары.СуммаПоДокументам Тогда
			НоваяСтрока.Недостача.Вставить("СуммаСНДС", ВыборкаТовары.СуммаПоДокументам - ВыборкаТовары.СуммаПоФакту);
		КонецЕсли;
		
		// Сопоставление.
		НоваяСтрока.Сопоставление = СтруктураДляСопоставленияНоменклатурыЭД(
			ВыборкаТовары.Номенклатура, ВыборкаТовары.СтавкаНДС, ВыборкаТовары.НоменклатураНаименование, ВыборкаШапка.Поставщик);
		
	КонецЦикла;
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, РезультатыПриемки, "РезультатыПриемки");
	
	// Обстоятельства использования.
	Если РасхожденияВСуммеИКоличестве Тогда
		ОбстоятельстваИспользования = "3000";
	Иначе
		ОбстоятельстваИспользования = "4000";
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных, "ОбстоятельстваИспользованияДокумента", ОбстоятельстваИспользования);
	
КонецПроцедуры

#КонецОбласти

#Область РаспаковкаЭД

// Сохраняет данные из электронного документа в объект ИБ.
//
// Параметры:
//  СтрокаДляЗагрузки - Строка - параметры для загрузки.
//  ДеревоРазбора     - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - ДокументСсылка - владелец электронного документа.
//  Записывать - Булево - если Истина, то объект будет записан.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//  НайденныйОбъект - Произвольный - созданный объект.
//
// Возвращаемое значение:
//  НайденныйОбъект - ссылка на объект.
//
Процедура СохранитьДанныеОбъектаВБД(СтрокаДляЗагрузки, ДеревоРазбора, ПараметрыОбработки, НайденныйОбъект = Неопределено) Экспорт
	
	Если СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ТОРГ12
		ИЛИ СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец
		ИЛИ СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
		ИЛИ СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда
		
		НайденныйОбъект = НайтиСоздатьПоступлениеТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора, ПараметрыОбработки.СсылкаНаВладельца);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
		НайденныйОбъект = НайтиСоздатьКорректировкуПоступления(СтрокаДляЗагрузки, ДеревоРазбора, ПараметрыОбработки.СсылкаНаВладельца);
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.СчетФактура
		ИЛИ СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		
		НайденныйОбъект = НайтиСоздатьСчетФактуру(СтрокаДляЗагрузки, ДеревоРазбора, ПараметрыОбработки.СсылкаНаВладельца);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭД = Перечисления.ВидыЭД.СчетНаОплату Тогда
		
		НайденныйОбъект = НайтиСоздатьСчетНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора, ПараметрыОбработки.СсылкаНаВладельца);
	ИначеЕсли ВРег(СтрокаДляЗагрузки.ВидЭД) = ВРег("РеквизитыОрганизации") Тогда
		
		НайденныйОбъект = ЭлектронноеВзаимодействиеБП.НовыйКонтрагент(СтрокаДляЗагрузки, ДеревоРазбора, ПараметрыОбработки.СсылкаНаВладельца);
		
	ИначеЕсли СтрокаДляЗагрузки.ВидЭд = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара Тогда
		
		НайденныйОбъект = НайтиСоздатьОтчетКомиссионера(СтрокаДляЗагрузки, ДеревоРазбора, ПараметрыОбработки.СсылкаНаВладельца);	
		
	КонецЕсли;
	
КонецПроцедуры

// Поиск и создание документа передачи товаров.
//
// Параметры:
//  ДеревоДанных		 - ДеревоЗначений - дерево данных электронного документа.
//  СсылкаНаВладельца	 - ДокументСсылка - ссылка на документ учета.
//  Записывать			 - Булево - признак записи документа.
//  СпособОбработки		 - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьДокументПередачаТоваров(ДеревоДанных, СсылкаНаВладельца, Записывать = Истина, СпособОбработки = "") Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		ДанныеОбъекта = ПодготовитьСтруктуруДляПоступленияТоваровУслугПоПередачеТоваров(ДеревоДанных);
		Если ДанныеОбъекта.Шапка.Исправление Тогда
			ЗаполнитьДокументКорректировкиПоступления(СсылкаНаВладельца, ДанныеОбъекта);
		Иначе
			ЗаполнитьДокументПоступленияТоваровУслуг(СсылкаНаВладельца, ДанныеОбъекта);
		КонецЕсли;

		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Поиск и создание документа передачи результатов работ.
//
// Параметры:
//  ДеревоДанных		 - ДеревоЗначений - дерево данных электронного документа.
//  СсылкаНаВладельца	 - ДокументСсылка - ссылка на документ учета.
//  Записывать			 - Булево - признак записи документа.
//  СпособОбработки		 - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьДокументПередачаРезультатовРабот(ДеревоДанных, СсылкаНаВладельца, Записывать = Истина, СпособОбработки = "") Экспорт
	
	НачатьТранзакцию();
	Попытка

		УстановитьПривилегированныйРежим(Истина);
	
		Если ЭтоПоступлениеДопРасходов(СпособОбработки) Тогда
			ДанныеДляЗаполнения = ПодготовитьСтруктуруДляПоступленияДопРасходовПоПередачаРабот(ДеревоДанных);
			ЗаполнитьДокументПоступленияДопРасходов(СсылкаНаВладельца, ДанныеДляЗаполнения);
		Иначе
			ДанныеДляЗаполнения = ПодготовитьСтруктуруДляПоступленияТоваровУслугПоПередачаРабот(ДеревоДанных);
			Если ДанныеДляЗаполнения.Шапка.Исправление Тогда
				ЗаполнитьДокументКорректировкиПоступления(СсылкаНаВладельца, ДанныеДляЗаполнения);
			Иначе
				ЗаполнитьДокументПоступленияТоваровУслуг(СсылкаНаВладельца, ДанныеДляЗаполнения);
			КонецЕсли;
		КонецЕсли;
	
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
		
	КонецПопытки;

КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкиНаВладельцев - Массив - документы информационной базы, созданные ранее по входящему электронному документу.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Структура - способы сохранения данных в информационной базе.
//     * ПервичныйДокумент - Строка - способ сохранения первичного документа.
//     * СчетФактура       - Строка - способ сохранения счета-фактуры.
//
Процедура НайтиСоздатьУниверсальныйПередаточныйДокумент(ДеревоДанных, СсылкиНаВладельцев = Неопределено,
	Записывать = Истина, СпособОбработки = Неопределено) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		ПервичныйДокумент = Неопределено;
		СчетФактура = Неопределено;
		Если СсылкиНаВладельцев <> Неопределено Тогда
			Для каждого Ссылка Из СсылкиНаВладельцев Цикл
				Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
					СчетФактура = Ссылка;
				Иначе
					ПервичныйДокумент = Ссылка;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ДокументыУчета = Новый Массив;
		
		УстановитьПривилегированныйРежим(Истина);
	
		ДанныеУПД = ПодготовитьСтруктуруДляПоступленияТоваровУслугУПД(ДеревоДанных);
		Если ДанныеУПД.Шапка.Исправление Тогда
			ЗаполнитьДокументКорректировкиПоступления(ПервичныйДокумент, ДанныеУПД);
		Иначе
			ЗаполнитьДокументПоступленияТоваровУслуг(ПервичныйДокумент, ДанныеУПД);
		КонецЕсли;
		ДокументыУчета.Добавить(ПервичныйДокумент);
		
		ДокументыОснованияСчетаФактуры = Новый Массив;
		ДокументыОснованияСчетаФактуры.Добавить(ПервичныйДокумент);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", ДокументыОснованияСчетаФактуры);
		
		ДанныеУПД = ПодготовитьСтруктуруДляСчетаФактурыУПД(ДеревоДанных);
		ЗаполнитьДокументСчетФактураПолученный(СчетФактура, ДанныеУПД);
		
		ДокументыУчета.Добавить(СчетФактура);
		
		СсылкиНаВладельцев = ДокументыУчета;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУПДДокументОПередаче(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	НачатьТранзакцию();
	Попытка
	
		УстановитьПривилегированныйРежим(Истина);
	
		ДанныеУПД = ПодготовитьСтруктуруДляПоступленияТоваровУслугУПД(ДеревоДанных);
		
		Если ДанныеУПД.Шапка.Исправление Тогда
			ЗаполнитьДокументКорректировкиПоступления(СсылкаНаВладельца, ДанныеУПД);
		Иначе
			ЗаполнитьДокументПоступленияТоваровУслуг(СсылкаНаВладельца, ДанныеУПД);
		КонецЕсли;
	
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУПДСчетФактуру(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	НачатьТранзакцию();
	Попытка

		УстановитьПривилегированныйРежим(Истина);
	
		ДанныеУПД = ПодготовитьСтруктуруДляСчетаФактурыУПД(ДеревоДанных);
		ЗаполнитьДокументСчетФактураПолученный(СсылкаНаВладельца, ДанныеУПД);
	
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкиНаВладельцев - Массив - документы информационной базы, созданные ранее по входящему электронному документу.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Структура - способы сохранения данных в информационной базе.
//     * ПервичныйДокумент - Строка - способ сохранения первичного документа.
//     * СчетФактура       - Строка - способ сохранения счета-фактуры.
//
Процедура НайтиСоздатьУниверсальныйКорректировочныйДокумент(ДеревоДанных, СсылкиНаВладельцев = Неопределено,
	Записывать = Истина, СпособОбработки  = Неопределено) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		ПервичныйДокумент = Неопределено;
		СчетФактура = Неопределено;
		Если СсылкиНаВладельцев <> Неопределено Тогда
			Для каждого Ссылка Из СсылкиНаВладельцев Цикл
				Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
					СчетФактура = Ссылка;
				Иначе
					ПервичныйДокумент = Ссылка;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ДокументыУчета = Новый Массив;
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если НужноСоздатьДокументВозвратТоваровПоставщику(СпособОбработки, ДеревоДанных) Тогда
			ДанныеУКД = ПодготовитьСтруктуруДляВозвратаТоваровПоставщикуУКД(ДеревоДанных);
			ЗаполнитьДокументВозвратТоваровПоставщику(ПервичныйДокумент, ДанныеУКД);
		Иначе
			ДанныеУКД = ПодготовитьСтруктуруДляКорректировкиПоступленияУКД(ДеревоДанных);
			ЗаполнитьДокументКорректировкиПоступления(ПервичныйДокумент, ДанныеУКД);
		КонецЕсли;
		ДокументыУчета.Добавить(ПервичныйДокумент);
		
		ДокументыОснованияСчетаФактуры = Новый Массив;
		ДокументыОснованияСчетаФактуры.Добавить(ПервичныйДокумент);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", ДокументыОснованияСчетаФактуры);
		
		ДанныеУКД = ПодготовитьСтруктуруДляСчетаФактурыУКД(ДеревоДанных);
		ЗаполнитьДокументСчетФактураПолученный(СчетФактура, ДанныеУКД);
		ДокументыУчета.Добавить(СчетФактура);
		
		СсылкиНаВладельцев = ДокументыУчета;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУКДДокументОбИзмененииСтоимости(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	НачатьТранзакцию();
	Попытка
	
		УстановитьПривилегированныйРежим(Истина);
		
		Если НужноСоздатьДокументВозвратТоваровПоставщику(СпособОбработки, ДеревоДанных) Тогда
			ДанныеУКД = ПодготовитьСтруктуруДляВозвратаТоваровПоставщикуУКД(ДеревоДанных);
			ЗаполнитьДокументВозвратТоваровПоставщику(СсылкаНаВладельца, ДанныеУКД);
		Иначе
			ДанныеУКД = ПодготовитьСтруктуруДляКорректировкиПоступленияУКД(ДеревоДанных);
			ЗаполнитьДокументКорректировкиПоступления(СсылкаНаВладельца, ДанныеУКД);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУКДСчетФактуру(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	НачатьТранзакцию();
	Попытка

		УстановитьПривилегированныйРежим(Истина);
		
		ДанныеУКД = ПодготовитьСтруктуруДляСчетаФактурыУКД(ДеревоДанных);
		ЗаполнитьДокументСчетФактураПолученный(СсылкаНаВладельца, ДанныеУКД);
	
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.НайтиСоздатьАктОРасхождениях_ФНС_2019();
//
Процедура НайтиСоздатьАктОРасхождениях_ФНС_2019(ДеревоДанных, ДокументУчета = Неопределено, СпособОбработки = "", ОписаниеОшибки = "") Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		НайтиСоздатьДокументАктОРасхожденияхПолученнный(ДеревоДанных, ДокументУчета);

		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область УПД_2019

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляУПДИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ЭтоКомиссионнаяТорговля  = Ложь;
	ЭтоСчетФактураНаАванс    = Ложь;
	ОснованиеСФУчаствуетВЭДО = Истина;
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		СчетФактура = СсылкаНаОбъект;
		ЭтоКомиссионнаяТорговля  = ЭтоКомиссионнаяТорговля(СчетФактура);
		ЭтоСчетФактураНаАванс    = ЭтоСчетФактураНаАванс(СчетФактура);
		ОснованиеСФУчаствуетВЭДО = ОснованиеСФУчаствуетВЭДО(СчетФактура);
			
		Если ЭтоКомиссионнаяТорговля
				ИЛИ ЭтоСчетФактураНаАванс
				ИЛИ Не ОснованиеСФУчаствуетВЭДО Тогда // если основание СФ не участвует в ЭДО, тогда формируем ЭД СФ вместо УПД
			СтруктураЭД.Функция = "СЧФ";
			СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.СЧФУПД;
			ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
			Возврат;
		КонецЕсли;
	Иначе
		Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ДоговорКонтрагента.ВидДоговора")
				= Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
			
			СтруктураЭД.Вставить("ВидОперацииЭД", Перечисления.ВидыОперацийЭД.Комиссия);
			ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
			
			Возврат;
			
		КонецЕсли;
		
		СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(СсылкаНаОбъект);
		
		// Если счета-фактуры не требуется, то формируем УПД со статусом 2.
		Если Не ЗначениеЗаполнено(СчетФактура) И СчетФактураНеТребуется(СсылкаНаОбъект) Тогда
			СтруктураЭД.Функция = "ДОП";
			ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	МассивСчетовФактур = Новый Массив();
	МассивСчетовФактур.Добавить(СчетФактура);
	Если ЭтоКомиссионнаяТорговля
		Или ЭтоСчетФактураНаАванс
		Или Не ОснованиеСФУчаствуетВЭДО Тогда
		
		ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиСчетаФактуры1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур());
	Иначе
		ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиСчетаФактуры1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур(Истина, Истина), Истина, Истина);
	КонецЕсли;
	Если ТаблицаСчетовФактур = Неопределено
			ИЛИ ТаблицаСчетовФактур.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЭД.Вставить("ЭтоСчетФактураНаАванс", ЭтоСчетФактураНаАванс);
	ДанныеДляФормированияЭД = ТаблицаСчетовФактур[0];
	ЗаполнитьОсновнуюЧастьУПД_2019(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	
	Если Не (ЭтоСчетФактураНаАванс
		ИЛИ ЭтоКомиссионнаяТорговля
		ИЛИ Не ОснованиеСФУчаствуетВЭДО) Тогда
		
		ЗаполнитьРасширеннуюЧастьУПД_2019(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//
Процедура ЗаполнитьДанныеДляДОПИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	Если ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры") = "Авансовый" Тогда
		Возврат;
	КонецЕсли;
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить(СсылкаНаОбъект);
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаДанных = УчетНДС.ПолучитьДанныеДляПечатиУниверсальногоПередаточногоДокумента(
		МассивДокументов, Документы[СсылкаНаОбъект.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов(), Истина);
	УстановитьПривилегированныйРежим(Ложь);
	
	ДанныеДляФормированияЭД = ТаблицаДанных[0];
	
	Если Не СтруктураЭД.Свойство("ВидОперацииЭД") 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ДоговорКонтрагента.ВидДоговора")= Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
		
		СтруктураЭД.Вставить("ВидОперацииЭД", Перечисления.ВидыОперацийЭД.Комиссия);
		
	КонецЕсли;
	
	СтруктураЭД.Вставить("ЭтоСчетФактураНаАванс", Ложь);
	ЗаполнитьОсновнуюЧастьУПД_2019(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	ЗаполнитьРасширеннуюЧастьУПД_2019(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация покупателя).
//
// Параметры:
//  СсылкаНаЭД - Ссылка - ссылка на ЭД, по которому необходимо сформировать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляУПДИнформацииПокупателяФНС_2019(СсылкаНаЭД, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ДанныеОтветногоТитула = Новый Структура;
	ДанныеОтветногоТитула.Вставить("КодИтога"                    , "1"); // товары, работы, услуги принял без претензий
	ДанныеОтветногоТитула.Вставить("АктОРасхожденияхНаименование", НСтр("ru = 'Акт о расхождениях'"));
	ДанныеОтветногоТитула.Вставить("АктОРасхожденияхНомер"       , "");
	ДанныеОтветногоТитула.Вставить("АктОРасхожденияхДата"        , Неопределено);
	ДанныеОтветногоТитула.Вставить("АктОРасхожденияхВид"         , "2"); // документ о приемке с расхождениями
	ДанныеОтветногоТитула.Вставить("СодержаниеОперации"          , "");
	ДанныеОтветногоТитула.Вставить("СоставСодержания"            , Новый Массив);
	ДанныеОтветногоТитула.Вставить("СоставительДокумента"        , "-");
	ДанныеОтветногоТитула.Вставить("ДатаПолученияТоваров"        , Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВладелецЭД", СтруктураЭД.ВладелецЭД);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭлектронныйДокументВходящийДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЭлектронныйДокументВходящийДокументыОснования.ДокументОснование) = ТИП(Документ.ПоступлениеТоваровУслуг)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Порядок
	|ПОМЕСТИТЬ ВТОснование
	|ИЗ
	|	Документ.ЭлектронныйДокументВходящий.ДокументыОснования КАК ЭлектронныйДокументВходящийДокументыОснования
	|ГДЕ
	|	ЭлектронныйДокументВходящийДокументыОснования.Ссылка = &ВладелецЭД
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Дата КАК Дата,
	|	ПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ПоступлениеТоваровУслуг.Товары.(
	|		НомерСтроки КАК НомерСтроки
	|	) КАК Товары,
	|	ПоступлениеТоваровУслуг.Услуги.(
	|		НомерСтроки КАК НомерСтроки
	|	) КАК Услуги,
	|	ПоступлениеТоваровУслуг.АгентскиеУслуги.(
	|		НомерСтроки КАК НомерСтроки
	|	) КАК АгентскиеУслуги
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОснование КАК ВТОснование
	|		ПО ПоступлениеТоваровУслуг.Ссылка = ВТОснование.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураПолученный.Дата КАК Дата,
	|	СчетФактураПолученный.Организация КАК Организация
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОснование КАК ВТОснование
	|		ПО СчетФактураПолученный.Ссылка = ВТОснование.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	АктОРасхождениях.Номер КАК Номер,
	|	АктОРасхождениях.Дата КАК Дата
	|ИЗ
	|	Документ.АктОРасхождениях КАК АктОРасхождениях
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОснование КАК ВТОснование
	|		ПО АктОРасхождениях.ДокументПоступления = ВТОснование.ДокументОснование
	|			И (АктОРасхождениях.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке))
	|			И (НЕ АктОРасхождениях.ПометкаУдаления)";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ДанныеОтветногоТитула.ДатаПолученияТоваров = ТекущаяДатаСеанса();
	Организация = Неопределено;
	
	Если Не РезультатЗапроса[3].Пустой() Тогда
		ВыборкаАктОРасхождениях = РезультатЗапроса[3].Выбрать();
		ВыборкаАктОРасхождениях.Следующий();
		ДанныеОтветногоТитула.КодИтога              = "2"; // товары, работы, услуги приняты с расхождениями
		ДанныеОтветногоТитула.АктОРасхожденияхНомер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаАктОРасхождениях.Номер, Истина, Ложь);
		ДанныеОтветногоТитула.АктОРасхожденияхДата  = НачалоДня(ВыборкаАктОРасхождениях.Дата);
	КонецЕсли;
	
	Если Не РезультатЗапроса[1].Пустой() Тогда // В основаниях есть документ поступления
		Выборка = РезультатЗапроса[1].Выбрать();
		Выборка.Следующий();
		Если Не Выборка.Товары.Пустой() Тогда
			Если ДанныеОтветногоТитула.КодИтога = "1" Тогда
				ДанныеОтветногоТитула.СоставСодержания.Добавить(НСтр("ru = 'Товары принял без претензий.'"));
			ИначеЕсли ДанныеОтветногоТитула.КодИтога = "2" Тогда
				ДанныеОтветногоТитула.СоставСодержания.Добавить(НСтр("ru = 'Товары приняты с расхождениями (претензиями).'"));
			КонецЕсли;
		КонецЕсли;
		Если Не (Выборка.Услуги.Пустой() И Выборка.АгентскиеУслуги.Пустой()) Тогда
			Если ДанныеОтветногоТитула.КодИтога = "1" Тогда
				ДанныеОтветногоТитула.СоставСодержания.Добавить(НСтр("ru = 'Услуги получены, претензий нет.'"));
			ИначеЕсли ДанныеОтветногоТитула.КодИтога = "2" Тогда
				ДанныеОтветногоТитула.СоставСодержания.Добавить(НСтр("ru = 'Услуги приняты с расхождениями (претензиями).'"));
			КонецЕсли;
		КонецЕсли;
		ДанныеОтветногоТитула.СодержаниеОперации   = СтрСоединить(ДанныеОтветногоТитула.СоставСодержания, " ");
		ДанныеОтветногоТитула.ДатаПолученияТоваров = Выборка.Дата;
		Организация = Выборка.Организация;
	ИначеЕсли Не РезультатЗапроса[2].Пустой() Тогда // основание - СФ
		Выборка = РезультатЗапроса[2].Выбрать();
		Выборка.Следующий();
		Организация = Выборка.Организация;
	КонецЕсли;
	
	Если Организация <> Неопределено Тогда
		ДанныеОтветногоТитула.СоставительДокумента = СоставительДокумента(
			ПолучитьДанныеЮрФизЛица(Организация, ДанныеОтветногоТитула.ДатаПолученияТоваров));
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СоставительДокументаНаименование", ДанныеОтветногоТитула.СоставительДокумента);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияОПринятииТоваров.ДатаПолученияТоваров", ДанныеОтветногоТитула.ДатаПолученияТоваров);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияОПринятииТоваров.СодержаниеОперации", ДанныеОтветногоТитула.СодержаниеОперации);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СведенияОПринятииТоваров.КодИтога", ДанныеОтветногоТитула.КодИтога);
	
	// Создан акт о расхождениях.
	Если ДанныеОтветногоТитула.КодИтога = "2" Тогда
		Путь = "СведенияОПринятииТоваров.ДокументОРасхождениях";
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			Путь + ".Наименование", ДанныеОтветногоТитула.АктОРасхожденияхНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			Путь + ".Номер", ДанныеОтветногоТитула.АктОРасхожденияхНомер);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			Путь + ".Дата", ДанныеОтветногоТитула.АктОРасхожденияхДата);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			Путь + ".Вид", ДанныеОтветногоТитула.АктОРасхожденияхВид);
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа УПД (информация продавца).
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на объект информационной базы, по которому необходимо 
//                                    создать электронный документ.
//  СтруктураЭД - Структура - структура данных для формирования электронного документа.
//  ДеревоДанных - ДеревоЗначений - дерево данных заполнения электронного документа.
//  Отказ - Булево - если нужно отказаться от создания электронного документа, необходимо установить значение в Истина.
//                   После этого дальнейшие действия по формированию документа производиться не будут, поэтому
//                   нужно сформировать сообщения пользователю при необходимости самостоятельно.
//
Процедура ЗаполнитьДанныеДляСЧФИнформацияПродавцаФНС_2019(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	Если СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.СЧФДОПУПД
		И СсылкаНаОбъект.ВидОперации = Перечисления.ВидыОперацийЭД.Комиссия Тогда
		Возврат;
	КонецЕсли;
	
	СчетФактура = СсылкаНаОбъект;
	ЭтоКомиссионнаяТорговля  = ЭтоКомиссионнаяТорговля(СчетФактура);
	ЭтоСчетФактураНаАванс    = ЭтоСчетФактураНаАванс(СчетФактура);
	ОснованиеСФУчаствуетВЭДО = ОснованиеСФУчаствуетВЭДО(СчетФактура);
	
	МассивСчетовФактур = Новый Массив();
	МассивСчетовФактур.Добавить(СчетФактура);
	Если ЭтоКомиссионнаяТорговля
		Или ЭтоСчетФактураНаАванс
		Или Не ОснованиеСФУчаствуетВЭДО Тогда
		
		ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиСчетаФактуры1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур());
	Иначе
		ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиСчетаФактуры1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур(Истина, Истина), Истина);
	КонецЕсли;
	Если ТаблицаСчетовФактур = Неопределено
			ИЛИ ТаблицаСчетовФактур.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЭД.Вставить("ЭтоСчетФактураНаАванс", ЭтоСчетФактураНаАванс);
	ДанныеДляФормированияЭД = ТаблицаСчетовФактур[0];
	ЗаполнитьОсновнуюЧастьУПД_2019(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкиНаВладельцев - Массив - документы информационной базы, созданные ранее по входящему электронному документу.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Структура - способы сохранения данных в информационной базе.
//     * ПервичныйДокумент - Строка - способ сохранения первичного документа.
//     * СчетФактура       - Строка - способ сохранения счета-фактуры.
//
Процедура НайтиСоздатьУниверсальныйПередаточныйДокумент_2019(ДеревоДанных, СсылкиНаВладельцев = Неопределено, СпособОбработки = Неопределено) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		ПервичныйДокумент = Неопределено;
		СчетФактура = Неопределено;
		Если СсылкиНаВладельцев <> Неопределено Тогда
			Для каждого Ссылка Из СсылкиНаВладельцев Цикл
				Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
					СчетФактура = Ссылка;
				Иначе
					ПервичныйДокумент = Ссылка;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ДокументыУчета = Новый Массив;
		
		УстановитьПривилегированныйРежим(Истина);
		
		// Заполнение первичного документа.
		Если ЭтоИсправлениеУПД(ДеревоДанных) Тогда // корректировка поступления с видом операции "Исправление в первичных документах"
			ДанныеДляЗаполнения = ПодготовитьСтруктуруДляПоступленияТоваровУслугУПД_2019(ДеревоДанных);
			ЗаполнитьДокументКорректировкиПоступления(ПервичныйДокумент, ДанныеДляЗаполнения);
		ИначеЕсли ЭтоПоступлениеДопРасходов(СпособОбработки) Тогда // поступление доп. расходов
			ДанныеДляЗаполнения = ПодготовитьСтруктуруДляПоступленияДопРасходовУПД_2019(ДеревоДанных);
			ЗаполнитьДокументПоступленияДопРасходов(ПервичныйДокумент, ДанныеДляЗаполнения);
		Иначе // поступление товаров и услуг
			ДанныеДляЗаполнения = ПодготовитьСтруктуруДляПоступленияТоваровУслугУПД_2019(ДеревоДанных);
			ЗаполнитьДокументПоступленияТоваровУслуг(ПервичныйДокумент, ДанныеДляЗаполнения);
		КонецЕсли;
		ДокументыУчета.Добавить(ПервичныйДокумент);
		
		// Заполнение счета-фактуры.
		ДокументыОснованияСчетаФактуры = Новый Массив;
		ДокументыОснованияСчетаФактуры.Добавить(ПервичныйДокумент);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", ДокументыОснованияСчетаФактуры);
		ДанныеДляЗаполнения = ПодготовитьСтруктуруДляСчетаФактурыУПД_2019(ДеревоДанных);
		ЗаполнитьДокументСчетФактураПолученный(СчетФактура, ДанныеДляЗаполнения);
		
		ДокументыУчета.Добавить(СчетФактура);
		
		СсылкиНаВладельцев = ДокументыУчета;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУПДДокументОПередаче_2019(ДеревоДанных, СсылкаНаВладельца = Неопределено, Записывать = Истина, СпособОбработки = "") Экспорт
	
	НачатьТранзакцию();
	Попытка
	
		УстановитьПривилегированныйРежим(Истина);
		
		Если ЭтоИсправлениеУПД(ДеревоДанных) Тогда // корректировка поступления с видом операции "Исправление в первичных документах"
			ДанныеДляЗаполнения = ПодготовитьСтруктуруДляПоступленияТоваровУслугУПД_2019(ДеревоДанных);
			ЗаполнитьДокументКорректировкиПоступления(СсылкаНаВладельца, ДанныеДляЗаполнения);
		ИначеЕсли ЭтоПоступлениеДопРасходов(СпособОбработки) Тогда // поступление доп. расходов
			ДанныеДляЗаполнения = ПодготовитьСтруктуруДляПоступленияДопРасходовУПД_2019(ДеревоДанных);
			ЗаполнитьДокументПоступленияДопРасходов(СсылкаНаВладельца, ДанныеДляЗаполнения);
		Иначе // поступление товаров и услуг
			ДанныеДляЗаполнения = ПодготовитьСтруктуруДляПоступленияТоваровУслугУПД_2019(ДеревоДанных);
			ЗаполнитьДокументПоступленияТоваровУслуг(СсылкаНаВладельца, ДанныеДляЗаполнения);
		КонецЕсли;
	
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Сохраняет данные из электронного документа в объекты ИБ.
//
// Параметры:
//  ДеревоДанных - ДеревоЗначений - структура параметров документа ИБ.
//  СсылкаНаВладельца - Ссылка - ссылка на объект ИБ, владельца электронного документа.
//  Записывать - Булево - признак необходимости записывать объект ИБ.
//  СпособОбработки - Строка - способ сохранения данных в информационной базе.
//
Процедура НайтиСоздатьУПДСчетФактуру_2019(ДеревоДанных, СсылкаНаВладельца = Неопределено, СпособОбработки = "") Экспорт
	
	НачатьТранзакцию();
	Попытка

		УстановитьПривилегированныйРежим(Истина);
	
		ДанныеДляЗаполнения = ПодготовитьСтруктуруДляСчетаФактурыУПД_2019(ДеревоДанных);
		ЗаполнитьДокументСчетФактураПолученный(СсылкаНаВладельца, ДанныеДляЗаполнения);
	
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область УКД_2020

Процедура ЗаполнитьДанныеУКДИнформацияПродавцаФНС_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ) Экспорт
	
	ПроверитьГотовностьОснованияУКД(СсылкаНаОбъект, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.КСЧФДИСУКД Тогда
		ЗаполнитьУКД_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
	ИначеЕсли СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ДИСУКД Тогда
		ЗаполнитьДИС_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
	Иначе
		ЗаполнитьКСЧ_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура НайтиСоздатьУКД_2020(ТипЭлементаВерсииЭД, ДеревоДанных, СсылкиНаВладельцев, Записывать, СпособОбработки) Экспорт
	
	Если ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.КСЧФДИСУКД Тогда
		НачатьТранзакцию();
		Попытка
			
			ПервичныйДокумент = Неопределено;
			СчетФактура = Неопределено;
			Если СсылкиНаВладельцев <> Неопределено Тогда
				Для каждого Ссылка Из СсылкиНаВладельцев Цикл
					Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
						СчетФактура = Ссылка;
					Иначе
						ПервичныйДокумент = Ссылка;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			ДокументыУчета = Новый Массив;
			
			УстановитьПривилегированныйРежим(Истина);
			
			Если НужноСоздатьДокументВозвратТоваровПоставщику(СпособОбработки, ДеревоДанных) Тогда
				ДанныеУКД = ПодготовитьСтруктуруДляВозвратаТоваровПоставщикуУКД(ДеревоДанных);
				ЗаполнитьДокументВозвратТоваровПоставщику(ПервичныйДокумент, ДанныеУКД);
			Иначе
				ДанныеУКД = ПодготовитьСтруктуруДляКорректировкиПоступленияУКД_2020(ДеревоДанных);
				ЗаполнитьДокументКорректировкиПоступления(ПервичныйДокумент, ДанныеУКД);
			КонецЕсли;
			ДокументыУчета.Добавить(ПервичныйДокумент);
			
			ДокументыОснованияСчетаФактуры = Новый Массив;
			ДокументыОснованияСчетаФактуры.Добавить(ПервичныйДокумент);
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", ДокументыОснованияСчетаФактуры);
			
			ДанныеУКД = ПодготовитьСтруктуруДляСчетаФактурыУКД_2020(ДеревоДанных);
			ЗаполнитьДокументСчетФактураПолученный(СчетФактура, ДанныеУКД);
			ДокументыУчета.Добавить(СчетФактура);
			
			СсылкиНаВладельцев = ДокументыУчета;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				,
				,
				ТекстСообщения);
			ВызватьИсключение;
		КонецПопытки;
	ИначеЕсли ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ДИСУКД Тогда
		НачатьТранзакцию();
		Попытка
			УстановитьПривилегированныйРежим(Истина);
			
			СсылкаНаВладельца = Неопределено;
			Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
				СсылкаНаВладельца = СсылкиНаВладельцев;
			КонецЕсли;
			
			Если НужноСоздатьДокументВозвратТоваровПоставщику(СпособОбработки, ДеревоДанных) Тогда
				ДанныеУКД = ПодготовитьСтруктуруДляВозвратаТоваровПоставщикуУКД(ДеревоДанных);
				ЗаполнитьДокументВозвратТоваровПоставщику(СсылкаНаВладельца, ДанныеУКД);
			Иначе
				ДанныеУКД = ПодготовитьСтруктуруДляКорректировкиПоступленияУКД_2020(ДеревоДанных);
				ЗаполнитьДокументКорректировкиПоступления(СсылкаНаВладельца, ДанныеУКД);
			КонецЕсли;
			Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
				СсылкиНаВладельцев = СсылкаНаВладельца;
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				,
				,
				ТекстСообщения);
			ВызватьИсключение;
		КонецПопытки;
	Иначе
		НачатьТранзакцию();
		Попытка
			УстановитьПривилегированныйРежим(Истина);
			
			СсылкаНаВладельца = Неопределено;
			Если ЗначениеЗаполнено(СсылкиНаВладельцев) Тогда
				СсылкаНаВладельца = СсылкиНаВладельцев;
			КонецЕсли;
			ДанныеУКД = ПодготовитьСтруктуруДляСчетаФактурыУКД_2020(ДеревоДанных);
			ЗаполнитьДокументСчетФактураПолученный(СсылкаНаВладельца, ДанныеУКД);
			
			Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
				СсылкиНаВладельцев = СсылкаНаВладельца;
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				,
				,
				ТекстСообщения);
			ВызватьИсключение;
		КонецПопытки;
	КонецЕсли; // КСЧФУКД
	
КонецПроцедуры

#КонецОбласти

#Область СопоставлениеНоменклатуры

// См. ОбменСКонтрагентамиПереопределяемый.ПриЗаполненииФормыНоменклатурыПоДаннымКонтрагента
//
Процедура ПриЗаполненииФормыНоменклатурыПоДаннымКонтрагента(НоменклатураКонтрагента, Форма) Экспорт
	
	Объект = Форма.Объект;
	Объект.Наименование       = НоменклатураКонтрагента.Наименование;
	Объект.НаименованиеПолное = НоменклатураКонтрагента.Наименование;
	Объект.Артикул            = НоменклатураКонтрагента.Артикул;
	Объект.ЕдиницаИзмерения   = ЕдиницаИзмеренияПоДаннымКонтрагента(
		НоменклатураКонтрагента.ЕдиницаИзмеренияКод, НоменклатураКонтрагента.ЕдиницаИзмерения);
	Объект.ВидСтавкиНДС       = ВидСтавкиНДСПоДаннымКонтрагента(НоменклатураКонтрагента.СтавкаНДС);
	
	Форма.Штрихкоды           = НоменклатураКонтрагента.ШтрихкодыНоменклатуры;
	
КонецПроцедуры

#КонецОбласти

#Область СтатусыДокументов

Процедура УстановитьСтатусДокументПодписан(МассивДокументов) Экспорт
	
	Для Каждого Документ Из МассивДокументов Цикл
		Если Не ЗначениеЗаполнено(Документ) Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
				Или ТипЗнч(Документ) = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг") Тогда
			СтатусыДокумента = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
			СтатусыДокумента.Статус = Перечисления.СтатусыДокументовРеализации.Подписан;
			РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(Документ, СтатусыДокумента);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаполнитьПараметрыЭДПоИсточнику(Источник, ПараметрыЭД, ФорматCML) Экспорт
	
	ТипИсточника = ТипЗнч(Источник);
	
	Если ТипИсточника = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.СчетНаОплату;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.СчетНаОплатуПокупателю") Тогда
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.СчетНаОплату;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.СчетНаОплату;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли  ТипИсточника = Тип("ДокументОбъект.СчетНаОплатуПоставщика") Тогда
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.СчетНаОплату;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		Если ФорматCML Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12;
		Иначе
			ПараметрыЭД.ВидЭД = ВидЭлектронногоДокументаРеализации(Источник);
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
		
		Если ФорматCML Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12;
		Иначе
			ПараметрыЭД.ВидЭД = ВидЭлектронногоДокументаРеализации(Источник);
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
			
		Если Не ЗначениеЗаполнено(ПараметрыЭД.ВидЭД) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.УПД;
		КонецЕсли;
		
		Если ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик Тогда
			
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		Иначе
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий;
		КонецЕсли;
		
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.ПоступлениеТоваровУслуг") Тогда
		
		Если Не ЗначениеЗаполнено(ПараметрыЭД.ВидЭД) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.УПД;
		КонецЕсли;
		
		Если ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
			ИЛИ ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик Тогда
			
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Исходящий;
		Иначе
			ПараметрыЭД.НаправлениеЭД = Перечисления.НаправленияЭД.Входящий;
		КонецЕсли;
		
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента, ДокументОснование");
		
		Если ЭтоКорректировочныйДокумент(Источник) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
		Иначе
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
		ДокументОснование = ЗначенияРеквизитов.ДокументОснование;
		Если ЗначениеЗаполнено(ДокументОснование)
			И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
			
			РеквизитыОснования = Новый Структура("Контрагент");
			Если ДокументОснование.Метаданные().Реквизиты.Найти("Договор") <> Неопределено Тогда
				РеквизитыОснования.Вставить("Договор"); 
			КонецЕсли;
			ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, РеквизитыОснования);
			ПараметрыЭД.Контрагент = ДанныеДокумента.Контрагент;
			Если ДанныеДокумента.Свойство("Договор") Тогда
				ПараметрыЭД.ДоговорКонтрагента = ДанныеДокумента.Договор;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.СчетФактураВыданный") Тогда
		
		Если ЭтоКорректировочныйДокумент(Источник) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
		Иначе
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
		ДокументОснование = Источник.ДокументОснование;
		Если ЗначениеЗаполнено(ДокументОснование)
			И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
			
			РеквизитыОснования = Новый Структура("Контрагент");
			Если ДокументОснование.Метаданные().Реквизиты.Найти("Договор") <> Неопределено Тогда
				РеквизитыОснования.Вставить("Договор"); 
			КонецЕсли;
			ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, РеквизитыОснования);
			ПараметрыЭД.Контрагент = ДанныеДокумента.Контрагент;
			Если ДанныеДокумента.Свойство("Договор") Тогда
				ПараметрыЭД.ДоговорКонтрагента = ДанныеДокумента.Договор;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		Если ЭтоКорректировочныйДокумент(Источник) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
		Иначе
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.СчетФактураПолученный") Тогда
		
		Если ЭтоКорректировочныйДокумент(Источник) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.КорректировочныйСчетФактура;
		Иначе
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.СчетФактура;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		ПараметрыЭД.ВидЭД = ВидЭлектронногоДокументаКорректировки(Источник);
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.КорректировкаРеализации") Тогда
		
		ПараметрыЭД.ВидЭД = ВидЭлектронногоДокументаКорректировки(Источник);
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;

	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.ВозвратТоваровОтПокупателя") Тогда
		
		ПараметрыЭД.ВидЭД = ВидЭлектронногоДокументаКорректировки(Источник);
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
	
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		ПараметрыЭД.ВидЭД = ВидЭлектронногоДокументаКорректировки(Источник);
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
	
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
			
		Если Не ЗначениеЗаполнено(ПараметрыЭД.ВидЭД) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.УКД;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
	
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.КорректировкаПоступления") Тогда
		
		Если Не ЗначениеЗаполнено(ПараметрыЭД.ВидЭД) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.УКД;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
			
		Если Не ЗначениеЗаполнено(ПараметрыЭД.ВидЭД) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.УКД;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
	
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
		
		Если Не ЗначениеЗаполнено(ПараметрыЭД.ВидЭД) Тогда
			ПараметрыЭД.ВидЭД = Перечисления.ВидыЭД.УКД;
		КонецЕсли;
		
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.ОтчетКомитентуОПродажах") Тогда
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.ОтчетКомиссионераОПродажах") Тогда
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.АктОРасхождениях") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.АктОРасхождениях;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.АктОРасхождениях") Тогда
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.АктОРасхождениях;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.АктОРасхожденияхПолученный") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.АктОРасхождениях;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.АктОРасхожденияхПолученный") Тогда
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.АктОРасхождениях;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ПоступлениеДопРасходов") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.АктЗаказчик;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент			 = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.ПоступлениеДопРасходов") Тогда
		
		ПараметрыЭД.ВидЭД				 = Перечисления.ВидыЭД.АктЗаказчик;
		ПараметрыЭД.НаправлениеЭД		 = Перечисления.НаправленияЭД.Входящий;
		ПараметрыЭД.Организация			 = Источник.Организация;
		ПараметрыЭД.Контрагент			 = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента	 = Источник.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Источник,
			"Организация, Контрагент, ДоговорКонтрагента");

		ПараметрыЭД.ВидЭД              = Перечисления.ВидыЭД.АктИсполнитель;
		ПараметрыЭД.НаправлениеЭД      = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация        = ЗначенияРеквизитов.Организация;
		ПараметрыЭД.Контрагент         = ЗначенияРеквизитов.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента = ЗначенияРеквизитов.ДоговорКонтрагента;
		
	ИначеЕсли ТипИсточника = Тип("ДокументОбъект.АктОбОказанииПроизводственныхУслуг") Тогда
		
		ПараметрыЭД.ВидЭД              = Перечисления.ВидыЭД.АктИсполнитель;
		ПараметрыЭД.НаправлениеЭД      = Перечисления.НаправленияЭД.Исходящий;
		ПараметрыЭД.Организация        = Источник.Организация;
		ПараметрыЭД.Контрагент         = Источник.Контрагент;
		ПараметрыЭД.ДоговорКонтрагента = Источник.ДоговорКонтрагента;

	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСоответствиеСтавокНДС(Соответствие) Экспорт
	
	Соответствие.Вставить("0",       Перечисления.СтавкиНДС.НДС0);
	Соответствие.Вставить("10",      Перечисления.СтавкиНДС.НДС10);
	Соответствие.Вставить("18",      Перечисления.СтавкиНДС.НДС18);
	Соответствие.Вставить("20",      Перечисления.СтавкиНДС.НДС20);
	Соответствие.Вставить("10/110",  Перечисления.СтавкиНДС.НДС10_110);
	Соответствие.Вставить("18/118",  Перечисления.СтавкиНДС.НДС18_118);
	Соответствие.Вставить("20/120",  Перечисления.СтавкиНДС.НДС20_120);
	Соответствие.Вставить("без НДС", Перечисления.СтавкиНДС.БезНДС);
	
	СтавкаНДСИсчисляетсяНалоговымАгентом = СтавкаНДСИсчисляетсяНалоговымАгентом();
	Соответствие.Вставить(СтавкаНДСИсчисляетсяНалоговымАгентом, СтавкаНДСИсчисляетсяНалоговымАгентом);
	
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС0,      "0");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС10,     "10");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС18,     "18");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС20,     "20");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС10_110, "10/110");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС18_118, "18/118");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС20_120, "20/120");
	Соответствие.Вставить(Перечисления.СтавкиНДС.БезНДС,    "без НДС");

КонецПроцедуры

Функция ПолучитьДанныеЮрФизЛица(ЮрФизЛицо, ДатаСведений = Неопределено, БанковскийСчет = Неопределено) Экспорт
	
	Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		ОбъектПолученияСведений = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЮрФизЛицо, "Владелец");
	Иначе
		ОбъектПолученияСведений = ЮрФизЛицо;
	КонецЕсли;
	
	ДанныеЮрФизЛица = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ОбъектПолученияСведений, ДатаСведений, БанковскийСчет);
	ТипЮрФизЛица = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектПолученияСведений, "ЮридическоеФизическоеЛицо");
	
	Если ТипЮрФизЛица = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		
		ДанныеЮрФизЛица.Вставить("Фамилия",  "");
		ДанныеЮрФизЛица.Вставить("Имя",      "");
		ДанныеЮрФизЛица.Вставить("Отчество", "");
		
		Если ТипЗнч(ОбъектПолученияСведений)=Тип("СправочникСсылка.Организации") Тогда
			НаименованияОрганизации = Справочники.Организации.НаименованияНаДату(ОбъектПолученияСведений, ДатаСведений);
			ДанныеЮрФизЛица.Вставить("Фамилия", НаименованияОрганизации.ФИО.Фамилия);
			ДанныеЮрФизЛица.Вставить("Имя", НаименованияОрганизации.ФИО.Имя);
			ДанныеЮрФизЛица.Вставить("Отчество", НаименованияОрганизации.ФИО.Отчество);
			ДанныеЮрФизЛица.ПолноеНаименование = НаименованияОрганизации.ФИО.Представление;
		Иначе
			ФИО = ДанныеЮрФизЛица.ПолноеНаименование;
			Если ВРЕГ(Лев(ФИО,2))="ИП" Тогда
				ФИО = Прав(ФИО, СтрДлина(ФИО)-2);
			ИначеЕсли ВРЕГ(Лев(ФИО, СтрДлина("Индивидуальный предприниматель")))="ИНДИВИДУАЛЬНЫЙ ПРЕДПРИНИМАТЕЛЬ" Тогда
				ФИО = Прав(ФИО, СтрДлина(ФИО)-СтрДлина("Индивидуальный предприниматель"));
			КонецЕсли;
			ФИО = СтрЗаменить(ФИО, """","");
			ФИО = СтрЗаменить(ФИО, "'","");
			ФИО = СокрЛП(ФИО);
			ЗаполнитьФИОиДолжность(ДанныеЮрФизЛица, ФИО);
			ДанныеЮрФизЛица.ПолноеНаименование  = ФИО;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(ЮрФизЛицо) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЮрФизЛицо, "КПП, НаименованиеПолное");
		ДанныеЮрФизЛица.Вставить("ПолноеНаименование", Реквизиты.НаименованиеПолное);
		Если ТипЮрФизЛица = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
			ДанныеЮрФизЛица.Вставить("КПП", Реквизиты.КПП);
		КонецЕсли;
		ДанныеЮрФизЛица.Вставить("ОбособленноеПодразделениеОрганизации", ЮрФизЛицо);
	КонецЕсли;
	
	ДанныеЮрФизЛица.Вставить("ОфициальноеНаименование", ДанныеЮрФизЛица.ПолноеНаименование);
	ДанныеЮрФизЛица.Вставить("ТипЮрФизЛица", ТипЮрФизЛица);
	ДанныеЮрФизЛица.Вставить("ЮрФизЛицоСсылка", ОбъектПолученияСведений.Ссылка);
	
	Возврат ДанныеЮрФизЛица;
	
КонецФункции

// Создает объект в ИБ по дереву параметров и помещает ссылку на него в "НовыйЭлемент".
//
// Параметры:
//  СтрокаОбъекта - Структура - параметры записываемого объекта.
//  ДеревоРазбора - ДеревоЗначений - результат разбора электронного документа.
//  НовыйЭлемент - СправочникСсылка - в этот параметр необходимо вернуть ссылку на созданный элемент справочника.
//
Процедура СоздатьОбъектВБД(СтрокаОбъекта, ДеревоРазбора, НовыйЭлемент) Экспорт
	
КонецПроцедуры

Функция ПолучитьСтавкуНДСПеречислением(СтавкаНДС, ОбратнаяСтавка = Ложь) Экспорт
	
	ЗначениеНДС = Неопределено;
	
	Если ТипЗнч(СтавкаНДС) = Тип("Строка") Тогда
		ПредставлениеСтавкиНДС = СтавкаНДС;
	ИначеЕсли ТипЗнч(СтавкаНДС) = Тип("Число") Тогда 
		ПредставлениеСтавкиНДС = Строка(СтавкаНДС);
	Иначе // неправильный тип
		ПредставлениеСтавкиНДС = Неопределено;
	КонецЕсли;
	
	Если ПредставлениеСтавкиНДС = Неопределено
			ИЛИ ВРЕГ(ПредставлениеСтавкиНДС) = "БЕЗ НДС"
			ИЛИ Не ЗначениеЗаполнено(ПредставлениеСтавкиНДС) Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.БезНДС;
		
	ИначеЕсли ПредставлениеСтавкиНДС = "0" ИЛИ ПредставлениеСтавкиНДС = "0%" Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС0;
		
	ИначеЕсли Найти("10#0.1#0,1#0.10#0,10#10%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС10;
		
	ИначеЕсли Найти("20#0.2#0,2#0.20#0,20#20%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС20;
		
	ИначеЕсли Найти("18#0.18#0,18#0.18#0,18#18%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС18;
		
	ИначеЕсли Найти("10/110#10% / 110%#10%/110%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС10_110;
		
	ИначеЕсли Найти("18/118#18% / 118%#18%/118%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС18_118;
		
	ИначеЕсли Найти("20/120#20% / 120%#20%/120%", ПредставлениеСтавкиНДС) > 0 Тогда
		ЗначениеНДС = Перечисления.СтавкиНДС.НДС20_120;
		
	КонецЕсли;
	
	Возврат ЗначениеНДС;
	
КонецФункции

Функция НайтиСсылкуНаОбъект(ТипОбъекта, ИдОбъекта = "", ДополнительныеРеквизиты = Неопределено) Экспорт
	
	Результат = Неопределено;

	Если ТипОбъекта = "Валюты" ИЛИ ТипОбъекта = "Банки" Тогда
		Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Код", ИдОбъекта);
	ИначеЕсли ТипОбъекта = "ЕдиницыИзмерения" Тогда
		Результат = НайтиСсылкуНаОбъектПоРеквизиту("КлассификаторЕдиницИзмерения", "Код", ИдОбъекта);
	ИначеЕсли (ТипОбъекта = "Контрагенты" ИЛИ ТипОбъекта = "Организации") И ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
		ПараметрПоиска = "";
		ИНН = "";
		КПП = "";
		Если ДополнительныеРеквизиты.Свойство("ИНН")
			И ЗначениеЗаполнено(ДополнительныеРеквизиты.ИНН) Тогда 
			ИНН = ДополнительныеРеквизиты.ИНН;
		КонецЕсли;
		Если ДополнительныеРеквизиты.Свойство("КПП")
			И ЗначениеЗаполнено(ДополнительныеРеквизиты.КПП) Тогда 
			КПП = ДополнительныеРеквизиты.КПП;
		КонецЕсли;
		Если ЗначениеЗаполнено(ИНН) Тогда
			Результат = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП(ТипОбъекта, ИНН, КПП); 
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Результат) И ДополнительныеРеквизиты.Свойство("Наименование", ПараметрПоиска) Тогда
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Наименование", ПараметрПоиска); 
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "НоменклатураПоставщиков" И ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
		Контрагент = "";
		ПараметрПоиска = "";
		Если ДополнительныеРеквизиты.Свойство("Идентификатор", ПараметрПоиска)
			И ДополнительныеРеквизиты.Свойство("Владелец", Контрагент) Тогда // есть Идентификатор
			Результат = ЭлектронноеВзаимодействиеБП.НайтиСсылкуНаНоменклатуруПоставщикаПоИдентификатору(
				ПараметрПоиска, Контрагент, "НоменклатураПоставщика");
		КонецЕсли;
	ИначеЕсли ТипОбъекта = "Номенклатура" И ЗначениеЗаполнено(ДополнительныеРеквизиты) Тогда
		ПараметрПоиска = "";
		Контрагент = "";
		Если ДополнительныеРеквизиты.Свойство("Идентификатор", ПараметрПоиска)
			И ДополнительныеРеквизиты.Свойство("Владелец", Контрагент) Тогда // по Идентификатору
			Результат = ЭлектронноеВзаимодействиеБП.НайтиСсылкуНаНоменклатуруПоставщикаПоИдентификатору(
				ПараметрПоиска, Контрагент);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Результат) И ДополнительныеРеквизиты.Свойство("Артикул", ПараметрПоиска) Тогда // по Коду номенклатуры своей компании
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ТипОбъекта, "Код", ПараметрПоиска);
		КонецЕсли;
	ИначеЕсли ТипОбъекта = "ВидыКонтактнойИнформации" Тогда
		Попытка 
			Результат = Справочники.ВидыКонтактнойИнформации[ИдОбъекта];
		Исключение
			Результат = Неопределено;
		КонецПопытки;
	ИначеЕсли (ТипОбъекта = "БанковскиеСчетаОрганизаций" Или ТипОбъекта = "БанковскиеСчетаКонтрагентов") И ЗначениеЗаполнено(ДополнительныеРеквизиты)Тогда
		Владелец = "";
		Если ДополнительныеРеквизиты.Свойство("Владелец", Владелец) Тогда
			ИмяПрикладногоСправочника = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ИмяПрикладногоСправочника(ТипОбъекта);
			Результат = НайтиСсылкуНаОбъектПоРеквизиту(ИмяПрикладногоСправочника, "НомерСчета", ИдОбъекта, Владелец);
		КонецЕсли;
		
	ИначеЕсли ТипОбъекта = "СтраныМира" Тогда
		Результат = НайтиСсылкуНаОбъектПоРеквизиту("СтраныМира", "Код", ИдОбъекта);
		Если НЕ ЗначениеЗаполнено(Результат) Тогда
			Результат = УправлениеКонтактнойИнформацией.СтранаМираПоКодуИлиНаименованию(ИдОбъекта);
		КонецЕсли;
	ИначеЕсли ТипОбъекта = "НомерТД" Тогда
		Результат = НайтиСсылкуНаОбъектПоРеквизиту("НомераГТД", "Код", ИдОбъекта);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьИДКонтрагента(Контрагент, ВидКонтрагента) Экспорт
	
	ИдКонтрагента = "";
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат ИдКонтрагента;
	КонецЕсли;
	
	Если ВРег(ВидКонтрагента) = ВРег("Организация") Тогда
		ИдКонтрагента = Контрагент.ИНН + "_" + Контрагент.КПП;
	ИначеЕсли ВРег(ВидКонтрагента) = ВРег("Контрагент") Тогда
		ИдКонтрагента = Контрагент.ИНН + "_" + Контрагент.КПП;
	КонецЕсли;
	
	Возврат ИдКонтрагента;
	
КонецФункции

Процедура КомандыЭДО_ФормаСписка(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	ПараметрыПриСозданииНаСервере = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаСписка();
	ПараметрыПриСозданииНаСервере.Форма                 = Форма;
	ПараметрыПриСозданииНаСервере.МестоРазмещенияКоманд = Элементы.КомандыЭДО;
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаСписка(ПараметрыПриСозданииНаСервере);
	
КонецПроцедуры

Процедура КомандыЭДО_ФормаДокумента(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	ПараметрыЭДОПриСоздании= ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаДокумента();
	ПараметрыЭДОПриСоздании.Форма                 = Форма;
	ПараметрыЭДОПриСоздании.ДокументСсылка        = Объект.Ссылка;
	ПараметрыЭДОПриСоздании.ДекорацияСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыЭДОПриСоздании.ГруппаСостояниеЭДО    = Элементы.ГруппаСостояниеЭДО;
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаДокумента(ПараметрыЭДОПриСоздании);
	
КонецПроцедуры

// Создание контрагента в информационной базе по реквизитам.
//
// Параметры:
//   РеквизитыКонтрагента - Структура - реквизиты необходимые для создания контрагента.
//    * ИНН - Строка - ИНН контрагента.
//    * КПП - Строка - КПП контрагента.
//    * Наименование - Строка - наименование контрагента.
//   Контрагент - СправочникСсылка - ссылка на созданного контрагента.
//   Отказ - Булево - признак ошибки.
//
Процедура СоздатьКонтрагентаПоРеквизитам(Знач РеквизитыКонтрагента, Контрагент, Отказ = Ложь) Экспорт
	
	ИНН                     = СокрЛП(РеквизитыКонтрагента.ИНН);
	КПП                     = СокрЛП(РеквизитыКонтрагента.КПП);
	НаименованиеКонтрагента = СокрЛП(РеквизитыКонтрагента.Наименование);
	
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось создать контрагента, т.к. во входящем электронном документе не заполнен ИНН.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		Возврат;
	КонецЕсли;
	
	КонтрагентОбъект = Справочники.Контрагенты.СоздатьЭлемент();
	Если СтрДлина(ИНН) > 10 Тогда
		КонтрагентОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	Иначе
		КонтрагентОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	КонецЕсли;
	КонтрагентОбъект.ОбособленноеПодразделение = Ложь;
	
	ЭтоЮрЛицо = КонтрагентОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	Если ЭтоЮрЛицо Тогда
		СведенияОКонтрагенте = РаботаСКонтрагентами.СведенияОЮридическомЛицеПоИНН(ИНН);
		Если ЗначениеЗаполнено(СведенияОКонтрагенте.ЕГРЮЛ) Тогда
			РеквизитыКонтрагента = СведенияОКонтрагенте.ЕГРЮЛ;
		Иначе
			РеквизитыКонтрагента = Новый Структура;
			ШаблонОписанияОшибки = НСтр("ru = 'Не удалось найти данные для заполнения реквизитов по ИНН %1.'");
			РеквизитыКонтрагента.Вставить("ОписаниеОшибки", СтрШаблон(ШаблонОписанияОшибки, ИНН));
		КонецЕсли;
		РеквизитыКонтрагента.Вставить("ИНН", ИНН);
		Руководитель = Новый Структура("Фамилия, Имя, Отчество, Представление, ИНН, ДатаЗаписи, Должность");
		Если Не ЗначениеЗаполнено(РеквизитыКонтрагента.ОписаниеОшибки) Тогда
			Если ЗначениеЗаполнено(РеквизитыКонтрагента.Руководители) Тогда
				ЗаполнитьЗначенияСвойств(Руководитель, РеквизитыКонтрагента.Руководители[0]);
			Иначе
				Руководитель = Неопределено;
			КонецЕсли;
			РеквизитыКонтрагента.Вставить("Руководитель", Руководитель);
			РеквизитыКонтрагента.Вставить("КодОКВЭД",
				?(ЗначениеЗаполнено(РеквизитыКонтрагента.ВидДеятельности), РеквизитыКонтрагента.ВидДеятельности.Код, Неопределено));
			РеквизитыКонтрагента.Вставить("ЭтоОКВЭД2",  
				?(ЗначениеЗаполнено(РеквизитыКонтрагента.ВидДеятельности) И РеквизитыКонтрагента.ВидДеятельности.Классификатор = "ОКВЭД2", Истина, Ложь));
		КонецЕсли;
	Иначе
		РеквизитыКонтрагента = РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(ИНН);
		РеквизитыКонтрагента.Вставить("КодОКВЭД",
			?(ЗначениеЗаполнено(РеквизитыКонтрагента.ВидДеятельности), РеквизитыКонтрагента.ВидДеятельности.Код, Неопределено));
		РеквизитыКонтрагента.Вставить("ЭтоОКВЭД2",  
			?(ЗначениеЗаполнено(РеквизитыКонтрагента.ВидДеятельности) И РеквизитыКонтрагента.ВидДеятельности.Классификатор = "ОКВЭД2", Истина, Ложь));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыКонтрагента.ОписаниеОшибки) Тогда // не удалось получить данные контрагента из ЕГРН
		КонтрагентОбъект.Наименование = НаименованиеКонтрагента;
		КонтрагентОбъект.ИНН = ИНН;
		КонтрагентОбъект.КПП = КПП;
	Иначе
		ЗаполнитьЗначенияСвойств(КонтрагентОбъект, РеквизитыКонтрагента);
		КонтрагентОбъект.НаименованиеПолное = РеквизитыКонтрагента.НаименованиеСокращенное;
		Если ЭтоЮрЛицо Тогда
			ТаблицаКИ = УправлениеКонтактнойИнформацией.НоваяКонтактнаяИнформация(Ложь);
			Если РеквизитыКонтрагента.ЮридическийАдрес <> Неопределено Тогда
				СтрокаКИ = ТаблицаКИ.Добавить();
				СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
				СтрокаКИ.Значение = РеквизитыКонтрагента.ЮридическийАдрес.КонтактнаяИнформация;
				
				СтрокаКИ = ТаблицаКИ.Добавить();
				СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
				СтрокаКИ.Значение = РеквизитыКонтрагента.ЮридическийАдрес.КонтактнаяИнформация;
				
				СтрокаКИ = ТаблицаКИ.Добавить();
				СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента;
				СтрокаКИ.Значение = РеквизитыКонтрагента.ЮридическийАдрес.КонтактнаяИнформация;
			КонецЕсли;
			
			Если ТаблицаКИ.Количество() > 0 Тогда
				УправлениеКонтактнойИнформацией.УстановитьКонтактнуюИнформациюОбъекта(КонтрагентОбъект, ТаблицаКИ);
			КонецЕсли;
			
			Если Не ПустаяСтрока(КПП) Тогда
				КонтрагентОбъект.КПП = КПП;
			КонецЕсли;
		Иначе
			КонтрагентОбъект.КПП = "";
			СвидетельствоОРегистрации = РеквизитыКонтрагента.СвидетельствоОРегистрации;
			Если СвидетельствоОРегистрации <> Неопределено Тогда
				КонтрагентОбъект.СвидетельствоСерияНомер = "" + СвидетельствоОРегистрации.Серия + " " + СвидетельствоОРегистрации.Номер;
				КонтрагентОбъект.СвидетельствоДатаВыдачи = СвидетельствоОРегистрации.Дата;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Попытка
		КонтрагентОбъект.Записать();
	Исключение
		ТекстСообщения = НСтр("ru = 'Возникла ошибка при записи нового элемента справочника ""Контрагенты"".
			|Подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Создание контрагента'"),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Контрагенты,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	Контрагент = КонтрагентОбъект.Ссылка;

КонецПроцедуры

// См. ОбменСКонтрагентамиПереопределяемый.ПолучитьЗапросКонструктораДополнительныхПолейШапки.
Процедура ПолучитьЗапросКонструктораДополнительныхПолейШапки(Параметры, ТекстЗапроса) Экспорт
	
	Если (Параметры.ИспользоватьУПД И Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.СчетФактура)
		ИЛИ Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.УПД
		ИЛИ Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.УКД
		ИЛИ (Параметры.ИспользоватьУКД И Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.КорректировочныйСчетФактура) Тогда
		
		ТекстЗапроса = ЗапросКонструктораДополнительныхПолейШапкиУПД();
	
	ИначеЕсли Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.СчетФактура
		ИЛИ Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		
		ТекстЗапроса = ЗапросКонструктораДополнительныхПолейШапкиСчетаФактуры();
		
	ИначеЕсли Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.ТОРГ12Продавец
		ИЛИ Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.АктИсполнитель
		ИЛИ Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.АктНаПередачуПрав
		ИЛИ Параметры.ВидЭлектронногоДокумента = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
		
		ТекстЗапроса = ЗапросКонструктораДополнительныхПолейШапкиПервичногоДокумента();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеЭД

Процедура ЗаполнитьОсновнуюЧастьУПД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных)
	
	ДанныеШапки = ДанныеДляФормированияЭД.ДанныеШапки;
	ЭтоСчетФактураНаАванс = СтруктураЭД.ЭтоСчетФактураНаАванс;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры",
		?(ЭтоСчетФактураНаАванс, "Авансовый", "Реализация"));
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СсылкаСчетаФактуры", ДанныеДляФормированияЭД.СчетФактура);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента", ДанныеШапки.Номер);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента",  ДанныеШапки.Дата);
	Если ДанныеШапки.Исправление Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления", ДанныеШапки.НомерИсправления);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления",  ДанныеШапки.ДатаИсправления);
	КонецЕсли;
	
	ВидОперацииЭД = Неопределено;
	СтруктураЭД.Свойство("ВидОперацииЭД", ВидОперацииЭД);
	Если ВидОперацииЭД = Перечисления.ВидыОперацийЭД.Комиссия Тогда
		
		ДокументРеализации = ДанныеДляФормированияЭД.Ссылка;
		ДанныеПокупателяПродавца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРеализации, "Организация, Контрагент");
		СведенияОПокупателе  = ПолучитьДанныеЮрФизЛица(ДанныеПокупателяПродавца.Контрагент, ДанныеШапки.Дата);
		СведенияОПоставщике  = ПолучитьДанныеЮрФизЛица(ДанныеПокупателяПродавца.Организация, ДанныеШапки.Дата);
		
	Иначе
		
		СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Покупатель, ДанныеШапки.Дата);
		СведенияОПоставщике  = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Поставщик, ДанныеШапки.Дата);
		СведенияОПокупателе.ИНН = ДанныеШапки.ИННпокупателя;
		СведенияОПокупателе.КПП = ДанныеШапки.КППпокупателя;
		СведенияОПоставщике.ИНН = ДанныеШапки.ИННпоставщика;
		СведенияОПоставщике.КПП = ДанныеШапки.КППпоставщика;
		
	КонецЕсли;
	
	ДополнитьДаннымиОПодразделенииОрганизации(СведенияОПоставщике, ДанныеДляФормированияЭД);
	ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПоставщике, "СведенияОПродавце", "Юр", ДанныеШапки.Дата);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СоставительДокументаНаименование", СоставительДокумента(СведенияОПоставщике));
	
	ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПокупателе, "СведенияОПокупателе", "Юр", ДанныеШапки.Дата);
	
	ПолучитьКомиссионераДляПеревыставленногоСчетаФактуры(ДанныеШапки, ДанныеДляФормированияЭД);
	
	Если ДанныеШапки.Свойство("Комиссионер")
		И ЗначениеЗаполнено(ДанныеШапки.Комиссионер) Тогда
		
		СведенияОКомиссионере = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Комиссионер, ДанныеШапки.Дата);
		
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОКомиссионере, "СведенияОКомиссионере", "Юр", ДанныеШапки.Дата);
		
	КонецЕсли;
	
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить код валюты документа. Проверьте, что:
	|	- в документе указана валюта,
	|	- для нее заполнен код по Общероссийскому классификатору валют.'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", ДанныеШапки.ВалютаКод, ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ВалютаНаименование", ДанныеШапки.ВалютаНаименованиеПолное);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ВалютаКурс", ДанныеШапки.ВалютаКурс);
	
	Если ЗначениеЗаполнено(ДанныеШапки.ИдентификаторГосКонтракта) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта", ДанныеШапки.ИдентификаторГосКонтракта);
	КонецЕсли;
	
	ДокументыОснования = Новый Массив;
	Для Каждого ДокументОснование Из ДанныеДляФормированияЭД.ДокументыОснования Цикл
		ДокументыОснования.Добавить(ДокументОснование);
	КонецЦикла;
	
	Если ДанныеШапки.Исправление
		И ЗначениеЗаполнено(ДанныеДляФормированияЭД.СчетФактура) Тогда
		
		ИсправляемыйСчетФактура = ИсправляемыйСчетФактураВыданный(ДанныеДляФормированияЭД.СчетФактура);
		Если ЗначениеЗаполнено(ИсправляемыйСчетФактура) Тогда
			ДокументыОснования.Добавить(ИсправляемыйСчетФактура);
		КонецЕсли;
		
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", ДокументыОснования);
	
	ПлатежныеДокументы = Новый ТаблицаЗначений();
	ПлатежныеДокументы.Колонки.Добавить("ДатаПРД");
	ПлатежныеДокументы.Колонки.Добавить("НомерПРД");
	
	Если ДанныеШапки.ТаблицаПлатежныхДокументов <> Неопределено Тогда
		Для Каждого ПлатежныйДокумент ИЗ ДанныеШапки.ТаблицаПлатежныхДокументов Цикл
			НовыйПлатежныйДокумент = ПлатежныеДокументы.Добавить();
			НовыйПлатежныйДокумент.ДатаПРД  = ПлатежныйДокумент.ДатаДокумента;
			НовыйПлатежныйДокумент.НомерПРД = ПлатежныйДокумент.НомерДокумента;
		КонецЦикла;
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ПлатежныеДокументы, "ПлатежноРасчетныеДокументы");
	
	Если ЭтоСчетФактураНаАванс Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
			НСтр("ru = 'Регистрация счет-фактуры на аванс'"));
		
	КонецЕсли;
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки");
	ТаблицаТоваров.Колонки.Добавить("ТоварНаименование");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияКод");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	ТаблицаТоваров.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("ЦенаЗаЕдиницуИзмерения", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("НалоговаяСтавка");
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогом", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаАкциза", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СведенияОТаможеннойДекларации", Новый ОписаниеТипов("ТаблицаЗначений"));
	ТаблицаТоваров.Колонки.Добавить("СтранаПроисхожденияНаименование", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТаблицаТоваров.Колонки.Добавить("Признак", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1)));
	ТаблицаТоваров.Колонки.Добавить("ТоварКод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТаблицаТоваров.Колонки.Добавить("ТоварИдентификатор", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	
	НомерСтроки = 1;
	ТолькоУслуги = Истина;
	УчетАгентскогоНДСПокупателем = Ложь; // используется для реализации металлолома, сырых шкур животных.
	Если ДокументыОснования.Количество() > 0
		И (ТипЗнч(ДокументыОснования[0]) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		ИЛИ ТипЗнч(ДокументыОснования[0]) = Тип("ДокументСсылка.КорректировкаРеализации")
		ИЛИ ТипЗнч(ДокументыОснования[0]) = Тип("ДокументСсылка.СчетФактураВыданный")) Тогда
		
		УчетАгентскогоНДСПокупателем = (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ДокументыОснования[0],
			"ДоговорКонтрагента.УчетАгентскогоНДСПокупателем") = Истина);
			
	КонецЕсли;
	
	Для каждого Строка Из ДанныеДляФормированияЭД.ТаблицаДокумента Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		
		НоваяСтрока.НомерСтроки         = НомерСтроки;
		НоваяСтрока.ТоварНаименование   = Строка.ТоварНаименование;
		
		Если Не ЭтоСчетФактураНаАванс Тогда
			Если ПустаяСтрока(Строка.ЕдиницаИзмеренияКод) Тогда
				ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Товар, "ЕдиницаИзмерения");
				Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда // берем значение ед. измерения из карточки номенклатуры
					РеквизитыЕдиницыИзмерения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЕдиницаИзмерения, "Наименование, Код");
					НоваяСтрока.ЕдиницаИзмеренияКод          = СокрЛП(РеквизитыЕдиницыИзмерения.Код);
					НоваяСтрока.ЕдиницаИзмеренияНаименование = СокрЛП(РеквизитыЕдиницыИзмерения.Наименование);
				КонецЕсли;
			Иначе
				НоваяСтрока.ЕдиницаИзмеренияКод          = СокрЛП(Строка.ЕдиницаИзмеренияКод);
				НоваяСтрока.ЕдиницаИзмеренияНаименование = СокрЛП(Строка.ЕдиницаИзмеренияНаименование);
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока.Количество                = Строка.Количество;
		НоваяСтрока.ЦенаЗаЕдиницуИзмерения    = Строка.Цена;
		НоваяСтрока.СтоимостьТоваровБезНалога = ?(ЭтоСчетФактураНаАванс И Не УчетАгентскогоНДСПокупателем, 0, Строка.Стоимость);
		НоваяСтрока.НалоговаяСтавка           = ?(УчетАгентскогоНДСПокупателем, СтавкаНДСИсчисляетсяНалоговымАгентом(), Строка.СтавкаНДС);
		НоваяСтрока.СуммаНалога               = Строка.СуммаНДС;
		НоваяСтрока.СтоимостьТоваровСНалогом  = ?(УчетАгентскогоНДСПокупателем, 0, Строка.Всего);
		
		Если ЗначениеЗаполнено(Строка.НомерГТД) Тогда
			НоваяСтрока.СведенияОТаможеннойДекларации = СведенияОТаможеннойДекларации();
			Декларация = НоваяСтрока.СведенияОТаможеннойДекларации.Добавить();
			Декларация.СтранаПроисхожденияКод    = ПолучитьКорректныйКодСтраны(Строка.СтранаПроисхожденияКод);
			Декларация.ТаможеннаяДекларацияНомер = ?(ЗначениеЗаполнено(Строка.НомерГТД), СокрЛП(Строка.НомерГТД), "");
			НоваяСтрока.СтранаПроисхожденияНаименование = Строка.СтранаПроисхождения;
		КонецЕсли;
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(НоваяСтрока.СведенияОТаможеннойДекларации, "СтранаПроисхожденияКод",,,
			НСтр("ru = 'Не заполнен код страны происхождения'"));
		
		НоваяСтрока.Признак = ПризнакТовараЕдиногоДокумента(Строка.Товар);
		НоваяСтрока.ТоварКод           = Строка.ТоварКод;
		НоваяСтрока.ТоварИдентификатор = Строка.Товар.УникальныйИдентификатор();
		
		Если ТолькоУслуги
			И НоваяСтрока.Признак = "1" Тогда
			ТолькоУслуги = Ложь;
		КонецЕсли;
		
		НоваяСтрока.Сопоставление = СтруктураДляСопоставленияНоменклатурыЭД(
			Строка.Товар, НоваяСтрока.НалоговаяСтавка, Строка.ТоварНаименование);
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	ДобавитьОбработкуОшибокЗаполненияТабличнойЧастиЭД(ТаблицаТоваров);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСтоимостьТоваровБезНалога", ?(ЭтоСчетФактураНаАванс И Не УчетАгентскогоНДСПокупателем, 0, ТаблицаТоваров.Итог("СтоимостьТоваровБезНалога")));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом", ТаблицаТоваров.Итог("СтоимостьТоваровСНалогом"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСуммаНалога", ТаблицаТоваров.Итог("СуммаНалога"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоКоличество", ТаблицаТоваров.Итог("Количество"));
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "СведенияОТоварах");
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", ТолькоУслуги);
	
	// Не выводим данные Грузоотправителя и Грузополучателя, если в документе только услуги или это счет-фактура на аванс.
	Если Не (ТолькоУслуги Или ЭтоСчетФактураНаАванс) Тогда
		
		Если НЕ ЗначениеЗаполнено(ДанныеШапки.Грузоотправитель)ИЛИ (ДанныеШапки.Грузоотправитель = "он же") Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
				"СведенияОГрузоотправителе.ОнЖе", Истина);
		Иначе
			СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Грузоотправитель, ДанныеШапки.Дата);
			ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОГрузоотправителе, "СведенияОГрузоотправителе.Грузоотправитель", "Факт", ДанныеШапки.Дата);
		КонецЕсли;
		
		Грузополучатель = ?(НЕ ЗначениеЗаполнено(ДанныеШапки.Грузополучатель) ИЛИ (ДанныеШапки.Грузополучатель = "он же"),
			СтруктураЭД.Контрагент, ДанныеШапки.Грузополучатель);
		СведенияОГрузополучателе = ПолучитьДанныеЮрФизЛица(Грузополучатель, ДанныеШапки.Дата);
		
		// Получаем адрес доставки из документа реализации
		ДокументРеализации = Неопределено;
		Если ТипЗнч(ДанныеДляФормированияЭД.ДокументыОснования) = Тип("Массив") Тогда
			Для Каждого Документ Из ДанныеДляФормированияЭД.ДокументыОснования Цикл
				Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
					Или ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
					
					ДокументРеализации = Документ;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		АдресДоставки = Неопределено;
		Если ЗначениеЗаполнено(ДокументРеализации) Тогда
			АдресДоставки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРеализации, "АдресДоставки");
		КонецЕсли;
		Если ЗначениеЗаполнено(АдресДоставки) Тогда
			СведенияОГрузополучателе.Вставить("АдресДоставки", АдресДоставки);
		КонецЕсли;
		
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОГрузополучателе, "СведенияОГрузополучателе", "Факт", ДанныеШапки.Дата);
		
	КонецЕсли;

КонецПроцедуры

Процедура ПолучитьКомиссионераДляПеревыставленногоСчетаФактуры(ДанныеШапки, ДанныеДляФормированияЭД)

	ДокументОснование = Неопределено;
	Если ТипЗнч(ДанныеДляФормированияЭД.ДокументыОснования) = Тип("Массив")
		И ДанныеДляФормированияЭД.ДокументыОснования.Количество() > 0 Тогда
		
		ДокументОснование = ДанныеДляФормированияЭД.ДокументыОснования[0];
	КонецЕсли;
	
	Если Не (ЗначениеЗаполнено(ДокументОснование)
		И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")) Тогда
		
		Возврат;
	КонецЕсли;
	
	СчетФактура = ДанныеДляФормированияЭД.СчетФактура;
	Если Не ЗначениеЗаполнено(СчетФактура) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажахПокупатели.Ссылка.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажахПокупатели
	|ГДЕ
	|	ОтчетКомиссионераОПродажахПокупатели.Ссылка = &ОтчетКомиссионераОПродажах
	|	И ОтчетКомиссионераОПродажахПокупатели.СчетФактура = &СчетФактура
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетКомиссионераОПродажахПокупатели.Ссылка.Контрагент";
	Запрос.УстановитьПараметр("ОтчетКомиссионераОПродажах", ДокументОснование);
	Запрос.УстановитьПараметр("СчетФактура", СчетФактура);
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	Если Выборка.Следующий() Тогда
		ДанныеШапки.Вставить("Комиссионер", Выборка.Контрагент);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьРасширеннуюЧастьУПД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных)
	
	ДанныеШапки = ДанныеДляФормированияЭД.ДанныеШапки;
	
	Если ДанныеШапки.Исправление Тогда
		ВидОперацииЭД = Перечисления.ВидыОперацийЭД.Исправление;
	ИначеЕсли СтруктураЭД.Свойство("ВидОперацииЭД") Тогда
		ВидОперацииЭД = СтруктураЭД.ВидОперацииЭД;
	Иначе
		ВидОперацииЭД = Перечисления.ВидыОперацийЭД.ПродажаКомиссия;
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации", ВидОперацииЭД);
	
	ЕстьТовары = Ложь;
	ЕстьУслуги = Ложь;
	Для Каждого Строка Из ДанныеДляФормированияЭД.ТаблицаДокумента Цикл
		ПризнакТовара = ПризнакТовараЕдиногоДокумента(Строка.Товар);
		Если ПризнакТовара = "1" Тогда
			ЕстьТовары = Истина;
		Иначе
			ЕстьУслуги = Истина;
		КонецЕсли;
	КонецЦикла;
	
	СоставСодержания = Новый Массив;
	Если ЕстьТовары Тогда
		СоставСодержания.Добавить(НСтр("ru = 'Товары переданы.'"));
	КонецЕсли;
	Если ЕстьУслуги Тогда
		СоставСодержания.Добавить(НСтр("ru = 'Услуги оказаны в полном объеме.'"));
	КонецЕсли;
	СодержаниеОперации = СтрСоединить(СоставСодержания, " ");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", СодержаниеОперации);
	
	// Дата отгрузки должна быть равна дате документа-реализации товаров и услуг.
	Если ДанныеДляФормированияЭД.ДокументыОснования.Количество() > 0 Тогда
		ДатаОтгрузкиПередачи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеДляФормированияЭД.ДокументыОснования[0], "Дата");
	Иначе
		ДатаОтгрузкиПередачи = ДанныеШапки.Дата;
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаОтгрузкиТоваров", ДатаОтгрузкиПередачи);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияДокументаОтгрузки", ДанныеДляФормированияЭД.ДокументыОснования);
	
	ОснованиеОтгрузкиТоваров = ПодготовитьТаблицуДанныхДоговораУПД(
		ДанныеШапки.Основание,
		ДанныеШапки.ОснованиеНомер,
		ДанныеШапки.ОснованиеДата);
		
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ОснованиеОтгрузкиТоваров, "ДокументДата",,,
		НСтр("ru = 'Необходимо указать дату договора.'"));
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ОснованиеОтгрузкиТоваров, "ОснованиеОтгрузкиТоваров");
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОТранспортировке", ДанныеШапки.ДанныеТранспортнаяНакладная);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ИныеСведенияОбОтгрузке", ДанныеШапки.СопроводительныеДокументы);
	
	ТранспортнаяНакладная = Новый ТаблицаЗначений;
	ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяНомер");
	ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяДата");
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТранспортнаяНакладная, "ТранспортнаяНакладная");
	
	Если ЗначениеЗаполнено(ДанныеШапки.Перевозчик) Тогда
		СведенияОПеревозчике = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Перевозчик);
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПеревозчике, "СведенияОПеревозчике",, ДанныеШапки.Дата);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеШапки.ДолжностьКладовщика)
		И ЗначениеЗаполнено(ДанныеШапки.Кладовщик) Тогда
		
		// Кладовщик работает в организации
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Должность", Строка(ДанныеШапки.ДолжностьКладовщика));
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Фамилия", ДанныеШапки.КладовщикФИО.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Имя", ДанныеШапки.КладовщикФИО.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Отчество", ДанныеШапки.КладовщикФИО.Отчество);
			
	ИначеЕсли ЗначениеЗаполнено(ДанныеШапки.Кладовщик) Тогда
		// Кладовщик не работает в организации
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ.Фамилия", ДанныеШапки.КладовщикФИО.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ.Имя", ДанныеШапки.КладовщикФИО.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ.Отчество", ДанныеШапки.КладовщикФИО.Отчество);
	КонецЕсли;
	
	Если ДанныеШапки.ОснованиеВид = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
		
		ТекстоваяИнформация = Новый ТаблицаЗначений;
		ТекстоваяИнформация.Колонки.Добавить("Идентификатор");
		ТекстоваяИнформация.Колонки.Добавить("Значение");
		
		НоваяСтрока = ТекстоваяИнформация.Добавить();
		НоваяСтрока.Идентификатор = "ПередачаТовараКомитентом";
		НоваяСтрока.Значение = "Истина";
		
		ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТекстоваяИнформация, "ДопДанныеДокументаОтгрузки.ТекстоваяИнформация");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьОсновнуюЧастьУКД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных)
	
	ДанныеШапки = ДанныеДляФормированияЭД.ДанныеШапки;
	
	Если СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.КСЧФДИСУКД
		ИЛИ СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.КСЧФУКД Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СсылкаКорректировочногоСчетаФактуры", ДанныеДляФормированияЭД.СчетФактура);
		
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидДокумента"  , ВидУКД(ДанныеДляФормированияЭД.Ссылка));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента", ДанныеШапки.Номер);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента" ,  ДанныеШапки.Дата);
	Если ДанныеШапки.Исправление И СтруктураЭД.ТипЭлементаВерсииЭД <> Перечисления.ТипыЭлементовВерсииЭД.ДИСУКД Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления",
			ДанныеШапки.НомерИсправленияКорректировочного);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления",
			ДанныеШапки.ДатаИсправленияКорректировочного);
	КонецЕсли;
	
	Если ДанныеШапки.ТаблицаРеквизитовОснований.Количество() > 0 Тогда
		Основание = ДанныеШапки.ТаблицаРеквизитовОснований[0];
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента", Основание.НомерСчетаФактуры);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента", Основание.ДатаСчетаФактуры);
		Если Основание.УчитыватьИсправлениеИсходногоДокумента Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправленияИсходногоДокумента",
				Основание.НомерИсправления);
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправленияИсходногоДокумента",
				Основание.ДатаИсправления);
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ДИСУКД Тогда
		ДанныеОснования = ДанныеОснованияКорректировкиВозврата(ДанныеДляФормированияЭД.Ссылка);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента",
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеОснования.Номер, Истина, Ложь));
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента",
			ДанныеОснования.Дата);
	КонецЕсли;
	
	СчетФактура = Неопределено;
	Если ЭтоДокументСДоговоромНаКомиссионнуюТорговлю(ДанныеДляФормированияЭД.Ссылка)
			И ЭтоКорректировкаПеревыставленногоСчетФактуры(ДанныеДляФормированияЭД.Ссылка, СчетФактура) Тогда
		ДанныеПокупателяПродавца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактура, "Организация, Контрагент, Продавец");
		СведенияОПокупателе   = ПолучитьДанныеЮрФизЛица(ДанныеПокупателяПродавца.Контрагент, ДанныеШапки.Дата);
		СведенияОПоставщике   = ПолучитьДанныеЮрФизЛица(ДанныеПокупателяПродавца.Продавец, ДанныеШапки.Дата);
		СведенияОКомиссионере = ПолучитьДанныеЮрФизЛица(ДанныеПокупателяПродавца.Организация, ДанныеШапки.Дата);
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОКомиссионере, "СведенияОКомиссионере", "Юр", ДанныеШапки.Дата);
	Иначе
		СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Покупатель, ДанныеШапки.Дата);
		СведенияОПоставщике = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Поставщик, ДанныеШапки.Дата, БанковскийСчетПродавца(ДанныеШапки));
		СведенияОПокупателе.ИНН = ДанныеШапки.ИННпокупателя;
		СведенияОПокупателе.КПП = ДанныеШапки.КППпокупателя;
		СведенияОПоставщике.ИНН = ДанныеШапки.ИННпродавца;
		СведенияОПоставщике.КПП = ДанныеШапки.КППпродавца;
	КонецЕсли;
	
	ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПоставщике, "СведенияОПродавце", "Юр", ДанныеШапки.Дата);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СоставительДокументаНаименование", СоставительДокумента(СведенияОПоставщике));
		
	ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПокупателе,      "СведенияОПокупателе", "Юр", ДанныеШапки.Дата);
		
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить код валюты документа. Проверьте, что:
	|	- в документе указана валюта,
	|	- для нее заполнен код по Общероссийскому классификатору валют.'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", ДанныеШапки.ВалютаКод, ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ВалютаНаименование", ДанныеШапки.ВалютаНаименованиеПолное);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ВалютаКурс", ДанныеШапки.ВалютаКурс);
	
	Если ЗначениеЗаполнено(ДанныеШапки.ИдентификаторГосКонтракта) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта", ДанныеШапки.ИдентификаторГосКонтракта);
	КонецЕсли;
	
	ДокументыОснования = ОснованияУКД(ДанныеДляФормированияЭД.Ссылка);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных, "ДокументыОснованияСчетаФактуры", ДокументыОснования);
	
	Если СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.КСЧФУКД Тогда // УКД СФ без первичного документа
		
		ОснованиеКорректировки = ПодготовитьТаблицуДанныхДоговораУПД(
			ДанныеШапки.Основание,
			ДанныеШапки.ОснованиеНомер,
			ДанныеШапки.ОснованиеДата);
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ОснованиеКорректировки, "ДокументДата",,,
			НСтр("ru = 'Необходимо указать дату договора.'"));
		ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ОснованиеКорректировки, "ОснованиеКорректировки");
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", "Предлагаю изменить стоимость");
		
	КонецЕсли;
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки");
	ТаблицаТоваров.Колонки.Добавить("ТоварКод");
	ТаблицаТоваров.Колонки.Добавить("ТоварНаименование");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияКодДоКорректировки");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияКод");
	ТаблицаТоваров.Колонки.Добавить("КоличествоДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("ЦенаЗаЕдиницуИзмеренияДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("ЦенаЗаЕдиницуИзмерения", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалогаДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалогаУвеличение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалогаУменьшение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("НалоговаяСтавкаДоКорректировки");
	ТаблицаТоваров.Колонки.Добавить("НалоговаяСтавка");
	ТаблицаТоваров.Колонки.Добавить("СуммаНалогаДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНалогаУвеличение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНалогаУменьшение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаАкцизаДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаАкциза", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаАкцизаУвеличение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаАкцизаУменьшение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогомДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогом", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогомУвеличение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогомУменьшение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("ТоварИдентификатор", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТаблицаТоваров.Колонки.Добавить("СведенияОМаркировкеДо");
	ТаблицаТоваров.Колонки.Добавить("СведенияОМаркировкеПосле");
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	
	УчетАгентскогоНДСПокупателем = Ложь; // используется для реализации металлолома, сырых шкур животных.
	Если ТипЗнч(ДанныеДляФормированияЭД.Ссылка) = Тип("ДокументСсылка.КорректировкаРеализации")
			ИЛИ ТипЗнч(ДанныеДляФормированияЭД.Ссылка) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		УчетАгентскогоНДСПокупателем = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ДанныеДляФормированияЭД.Ссылка,
			"ДоговорКонтрагента.УчетАгентскогоНДСПокупателем");
	КонецЕсли;
	
	// Штрихкоды упаковок.
	ТаблицаКодовМаркировкиДо = Неопределено;
	ТаблицаКодовМаркировкиПосле = Неопределено;
	Если ДанныеДляФормированияЭД.Владелец().Колонки.Найти("ШтрихкодыУпаковок") <> Неопределено Тогда
		Для Каждого ДокументОснование Из ДокументыОснования Цикл
			Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				ШтрихкодыУпаковокДо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ШтрихкодыУпаковок").Выгрузить();
				ШтрихкодыУпаковокДо.Колонки.ШтрихкодУпаковки.Имя = "Штрихкод";
				ТаблицаКодовМаркировкиДо = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковокДо);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ТаблицаКодовМаркировкиПосле = ДанныеДляФормированияЭД.ШтрихкодыУпаковок;
	КонецЕсли;
	
	НомерСтроки = 1;
	ТолькоУслуги = Истина;
	Для Каждого Строка Из ДанныеДляФормированияЭД.ТаблицаДокумента Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		
		НаименованиеНоменклатуры = ?(ЗначениеЗаполнено(Строка.НаименованиеНоменклатуры), Строка.НаименованиеНоменклатуры,
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Товар, "НаименованиеПолное"));
		
		НоваяСтрока.НомерСтроки              = НомерСтроки;
		НоваяСтрока.ТоварКод                 = Строка.ТоварКод;
		НоваяСтрока.ТоварНаименование        = НаименованиеНоменклатуры;
		НоваяСтрока.ЕдиницаИзмеренияКодДоКорректировки = СокрЛП(Строка.ЕдиницаИзмеренияКод);
		НоваяСтрока.ЕдиницаИзмеренияКод                = СокрЛП(Строка.ЕдиницаИзмеренияКод);
		НоваяСтрока.КоличествоДоКорректировки = Строка.КоличествоДоИзменения;
		НоваяСтрока.Количество                = Строка.КоличествоПослеИзменения;
		НоваяСтрока.ЦенаЗаЕдиницуИзмеренияДоКорректировки    = Строка.ЦенаДоИзменения;
		НоваяСтрока.ЦенаЗаЕдиницуИзмерения                   = Строка.ЦенаПослеИзменения;
		НоваяСтрока.СтоимостьТоваровБезНалогаДоКорректировки = Строка.СтоимостьБезНДСДоИзменения;
		НоваяСтрока.СтоимостьТоваровБезНалога                = Строка.СтоимостьБезНДСПослеИзменения;
		НоваяСтрока.СтоимостьТоваровБезНалогаУвеличение      = Строка.РазницаБезНДСУвеличение;
		НоваяСтрока.СтоимостьТоваровБезНалогаУменьшение      = Строка.РазницаБезНДСУменьшение;
		
		СтавкаНДС = ?(УчетАгентскогоНДСПокупателем = Истина, СтавкаНДСИсчисляетсяНалоговымАгентом(), Строка.СтавкаНДС);
		НоваяСтрока.НалоговаяСтавкаДоКорректировки = СтавкаНДС;
		НоваяСтрока.НалоговаяСтавка                = СтавкаНДС;
		НоваяСтрока.СуммаНалогаДоКорректировки = Строка.СуммаНДСДоИзменения;
		НоваяСтрока.СуммаНалога                = Строка.СуммаНДСПослеИзменения;
		НоваяСтрока.СуммаНалогаУвеличение      = Строка.РазницаНДСУвеличение;
		НоваяСтрока.СуммаНалогаУменьшение      = Строка.РазницаНДСУменьшение;
		НоваяСтрока.СтоимостьТоваровСНалогомДоКорректировки = ?(УчетАгентскогоНДСПокупателем = Истина, 0, Строка.СтоимостьСНДСДоИзменения);
		НоваяСтрока.СтоимостьТоваровСНалогом                = ?(УчетАгентскогоНДСПокупателем = Истина, 0, Строка.СтоимостьСНДСПослеИзменения);
		НоваяСтрока.СтоимостьТоваровСНалогомУвеличение      = ?(УчетАгентскогоНДСПокупателем = Истина, 0, Строка.РазницаСНДСУвеличение);
		НоваяСтрока.СтоимостьТоваровСНалогомУменьшение      = ?(УчетАгентскогоНДСПокупателем = Истина, 0, Строка.РазницаСНДСУменьшение);
		НоваяСтрока.ТоварИдентификатор = Строка.Товар.УникальныйИдентификатор();
		
		Если ТолькоУслуги
			И ПризнакТовараЕдиногоДокумента(Строка.Товар) = "1" Тогда
			ТолькоУслуги = Ложь;
		КонецЕсли;
		
		ЗаполнитьСведенияОМаркировкеВУКД(
			НоваяСтрока.СведенияОМаркировкеДо, Строка, ТаблицаКодовМаркировкиДо, Строка.КоличествоДоИзменения);
			
		ЗаполнитьСведенияОМаркировкеВУКД(
			НоваяСтрока.СведенияОМаркировкеПосле, Строка, ТаблицаКодовМаркировкиПосле, Строка.КоличествоПослеИзменения);
		
		НоваяСтрока.Сопоставление = СтруктураДляСопоставленияНоменклатурыЭД(
			Строка.Товар, НоваяСтрока.НалоговаяСтавка, НаименованиеНоменклатуры);
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	ДобавитьОбработкуОшибокЗаполненияТабличнойЧастиЭД(ТаблицаТоваров);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровБезНалогаУвеличение", ТаблицаТоваров.Итог("СтоимостьТоваровБезНалогаУвеличение"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровСНалогомУвеличение", ТаблицаТоваров.Итог("СтоимостьТоваровСНалогомУвеличение"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСуммаНалогаУвеличение", ТаблицаТоваров.Итог("СуммаНалогаУвеличение"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровБезНалогаУменьшение", ТаблицаТоваров.Итог("СтоимостьТоваровБезНалогаУменьшение"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровСНалогомУменьшение", ТаблицаТоваров.Итог("СтоимостьТоваровСНалогомУменьшение"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСуммаНалогаУменьшение", ТаблицаТоваров.Итог("СуммаНалогаУменьшение"));
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "СведенияОТоварах");
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", ТолькоУслуги);
	
КонецПроцедуры

Процедура ЗаполнитьРасширеннуюЧастьУКД(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных)
	
	ДанныеШапки = ДанныеДляФормированияЭД.ДанныеШапки;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", "Предлагаю изменить стоимость");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаНаправленияНаСогласование", ДанныеШапки.Дата);
	
	ОснованиеКорректировки = ПодготовитьТаблицуДанныхДоговораУПД(
			ДанныеШапки.Основание,
			ДанныеШапки.ОснованиеНомер,
			ДанныеШапки.ОснованиеДата);
			
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ОснованиеКорректировки, "ДокументДата",,,
			НСтр("ru = 'Необходимо указать дату договора.'"));
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ОснованиеКорректировки, "ОснованиеКорректировки");
	
	МассивОснований = Новый Массив;
	
	Для Каждого Основание ИЗ ДанныеШапки.ТаблицаРеквизитовОснований Цикл
		Если Основание.УчитыватьИсправлениеИсходногоДокумента Тогда
			ОписаниеОснования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = '№ %1 от %2, с учетом исправления № %3 от %4'"),
									Основание.НомерСчетаФактуры, Формат(Основание.ДатаСчетаФактуры, "ДЛФ=D"),
									Основание.НомерИсправления, Формат(Основание.ДатаИсправления, "ДЛФ=D"));
		Иначе
			ОписаниеОснования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = '№ %1 от %2'"),
									Основание.НомерСчетаФактуры, Формат(Основание.ДатаСчетаФактуры, "ДЛФ=D"));
		КонецЕсли;
		МассивОснований.Добавить(ОписаниеОснования);
	КонецЦикла;
	
	Если МассивОснований.Количество() > 0 Тогда
		ОписаниеОснований = СтрСоединить(МассивОснований, ", ");
		ОписаниеОснований = НСтр("ru = 'Универсальный передаточный документ'") + " " + ОписаниеОснований;
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПередаточныхДокументов",
																	ОписаниеОснований);
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ИныеСведенияОбИзмененииСтоимости",
																	ДанныеШапки.ИныеСведения);
	
КонецПроцедуры

Процедура ЗаполнитьДокументКорректировкиПоступления(Документ, ДанныеДокумента)
	
	ДанныеЗаполнения = ДанныеДокумента.Шапка;
	РежимЗаписи      = РежимЗаписиДокумента.Запись;
	Если ЗначениеЗаполнено(Документ) Тогда // получены изменения по существующему документу
		ДокументОбъект = Документ.ПолучитьОбъект();
		Если Документ.Проведен Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		КонецЕсли;
	Иначе // создаем новый
		
		ДокументОбъект = Документы.КорректировкаПоступления.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ЗаполнениеДокументов.Заполнить(ДокументОбъект, ДанныеЗаполнения);
		
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.ВидОперацииЭД = Перечисления.ВидыОперацийЭД.Исправление Тогда
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки;
	Иначе
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение;
	КонецЕсли;
	
	ДокументОбъект.ЗаполнитьСвойстваШапки();
	
	Если ДанныеЗаполнения.ВидОперацииЭД = Перечисления.ВидыОперацийЭД.Исправление Тогда
		ДокументОбъект.НомерИсправления = ДанныеЗаполнения.НомерИсправления;
		ДокументОбъект.ДатаИсправления = ДанныеЗаполнения.ДатаИсправления;
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("НомерИсходногоДокумента")
		И ЗначениеЗаполнено(ДанныеЗаполнения.НомерИсходногоДокумента) Тогда
		ДокументОбъект.НомерИсходногоДокумента = ДанныеЗаполнения.НомерИсходногоДокумента;
		ДокументОбъект.ДатаИсходногоДокумента  = ДанныеЗаполнения.ДатаИсходногоДокумента;
		Если ДанныеЗаполнения.УчитыватьИсправлениеИсходногоДокумента Тогда
			ДокументОбъект.НомерИсправленияИсходногоДокумента = ДанныеЗаполнения.НомерИсправленияИсходногоДокумента;
			ДокументОбъект.ДатаИсправленияИсходногоДокумента  = ДанныеЗаполнения.ДатаИсправленияИсходногоДокумента;
		КонецЕсли;
	КонецЕсли;
	
	ДокументПоступления = ДокументОбъект.ДокументПоступления;
	Если Не ЗначениеЗаполнено(ДокументПоступления) И ДанныеЗаполнения.Свойство("Основание") Тогда
		ДокументПоступления = ДанныеЗаполнения.Основание;
	КонецЕсли;
	
	// Заполненим корректировку поступления данными основания
	Если ЗначениеЗаполнено(ДокументПоступления) Тогда
		ДокументОбъект.Заполнить(ДокументПоступления);
	КонецЕсли;
	
	// Заполним табличные части данными корректировки
	ТабличныеЧастиДляЗаполения = Новый Структура("Товары, Услуги");
	СтрокиТабличнойЧастиДляПерезаполнения = Новый Структура("Товары, Услуги",
			"Количество, Цена, Сумма, СуммаНДС, НомерГТД, СтранаПроисхождения", 
			"Количество, Цена, Сумма, СуммаНДС, Содержание");
	Если ДанныеЗаполнения.ВидОперацииЭД = Перечисления.ВидыОперацийЭД.Исправление Тогда
		СтрокиТабличнойЧастиДляПерезаполненияНового = Новый Структура("Товары, Услуги", "", "");
	Иначе
		СтрокиТабличнойЧастиДляПерезаполненияНового = Новый Структура("Товары, Услуги",
				"КоличествоДоИзменения, ЦенаДоИзменения, СуммаДоИзменения, СуммаНДСДоИзменения, НомерГТДДоИзменения, СтранаПроисхожденияДоИзменения", 
				"КоличествоДоИзменения, ЦенаДоИзменения, СуммаДоИзменения, СуммаНДСДоИзменения, СодержаниеДоИзменения");
	КонецЕсли;
	Для Каждого ТабличнаяЧасть Из ТабличныеЧастиДляЗаполения Цикл
		
		ИмяТЧ = ТабличнаяЧасть.Ключ;
		
		СтруктураПоиска = Новый Структура("Номенклатура, СтавкаНДС");
		
		Для Каждого СтрокаДокумента Из ДокументОбъект[ИмяТЧ] Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаДокумента);
			РезультатПоискаДанныхЗаполнения = ДанныеДокумента[ИмяТЧ].НайтиСтроки(СтруктураПоиска);
			
			Если РезультатПоискаДанныхЗаполнения.Количество() > 0 Тогда
				
				СтрокаДанныхЗаполнения = РезультатПоискаДанныхЗаполнения[0];
				ЗаполнитьЗначенияСвойств(СтрокаДокумента, СтрокаДанныхЗаполнения, СтрокиТабличнойЧастиДляПерезаполнения[ИмяТЧ]);
				
				// Удалим обработанную строку из данных заполнения
				ДанныеДокумента[ИмяТЧ].Удалить(СтрокаДанныхЗаполнения);
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Добавим новые строки в документ
		Если ДанныеДокумента[ИмяТЧ].Количество() > 0 Тогда
			
			Для Каждого СтрокаДанныхЗаполнения Из ДанныеДокумента[ИмяТЧ] Цикл
				
				НоваяСтрока = ДокументОбъект[ИмяТЧ].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанныхЗаполнения, , СтрокиТабличнойЧастиДляПерезаполненияНового[ИмяТЧ]);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДанныеДокумента.Свойство("ШтрихкодыУпаковок") Тогда 
		ДокументОбъект.ШтрихкодыУпаковок.Загрузить(ДанныеДокумента.ШтрихкодыУпаковок);
	КонецЕсли;
	
	ЗаписатьДокумент(ДокументОбъект, РежимЗаписи);
	
	Если НЕ ЗначениеЗаполнено(Документ) Тогда
		Документ = ДокументОбъект.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументВозвратТоваровПоставщику(Документ, ДанныеДокумента)
	
	ДанныеШапки = ДанныеДокумента.Шапка;
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	Если ЗначениеЗаполнено(Документ) Тогда // получены изменения по существующему документу
		ДокументОбъект = Документ.ПолучитьОбъект();
		Если ДокументОбъект.Проведен Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		КонецЕсли;
	Иначе
		// Пользователем может быть уже создан документ "Возврат товаров поставщику",
		// поэтому перед созданием поищем его по основанию, номенклатуре и количеству.
		СозданныйПользователемДокумент = СозданныйПользователемВозвратПоставщику(ДанныеДокумента);
		Если СозданныйПользователемДокумент = Неопределено Тогда
			ДокументОбъект = Документы.ВозвратТоваровПоставщику.СоздатьДокумент();
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			ЗаполнениеДокументов.Заполнить(ДокументОбъект, ДанныеШапки, Истина);
			
			ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеШапки);
		Иначе
			ДокументОбъект = СозданныйПользователемДокумент.ПолучитьОбъект();
			Если ДокументОбъект.Проведен Тогда 
				РежимЗаписи = РежимЗаписиДокумента.Проведение;
			КонецЕсли
		КонецЕсли;
	КонецЕсли;
	
	ДанныеДокументаПоступления = Новый Структура("ДоговорКонтрагента, Склад");
	Если Не ЗначениеЗаполнено(ДокументОбъект.Сделка) И ДанныеШапки.Свойство("Основание") Тогда
		ДокументОбъект.Сделка = ДанныеШапки.Основание;
		Если ТипЗнч(ДанныеШапки.Основание) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
			ДанныеДокументаПоступления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.Сделка, "ДоговорКонтрагента, Склад");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокументаПоступления);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ДокументОбъект.ДоговорКонтрагента) Тогда
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДокументОбъект.ДоговорКонтрагента,
			ДокументОбъект.Контрагент, ДокументОбъект.Организация);
	КонецЕсли;
	ДокументОбъект.КорректировочныйСчетФактураПолученОтПоставщика = Истина;
	
	ДокументОбъект.Товары.Очистить();
	Для Каждого Строка Из ДанныеДокумента.Товары Цикл
		СтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТовары, Строка);
	КонецЦикла;
	СчетаУчетаВДокументах.Заполнить(ДокументОбъект, Новый Структура());
	ДокументОбъект.НДСВключенВСтоимость = Ложь;
	ДокументОбъект.ДокументБезНДС       = Ложь;
	
	ЗаписатьДокумент(ДокументОбъект, РежимЗаписи);
	
	Если НЕ ЗначениеЗаполнено(Документ) Тогда
		Документ = ДокументОбъект.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоступленияТоваровУслуг(Документ, ДанныеДокумента)
	
	ДанныеШапки = ДанныеДокумента.Шапка;
	
	ПередачаТовараКомитентом = ДанныеШапки.ПередачаТовараКомитентом;
	
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	Если ЗначениеЗаполнено(Документ) Тогда // получены изменения по существующему документу
		ДокументОбъект = Документ.ПолучитьОбъект();
		Если ДокументОбъект.Проведен Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		КонецЕсли;
	Иначе
		ДокументОбъект = Документы.ПоступлениеТоваровУслуг.СоздатьДокумент();
		ЗагрузкаИзExcel = Ложь;
		Если ДанныеШапки.Свойство("ЗагрузкаИзФайлаExcel", ЗагрузкаИзExcel) И Булево(ЗагрузкаИзExcel) 
			И ЗначениеЗаполнено(ДанныеШапки.Дата) Тогда
			
			ДокументОбъект.Дата = ДанныеШапки.Дата;
			
		Иначе
			
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			
		КонецЕсли;
		
		ЗаполнениеДокументов.Заполнить(ДокументОбъект, ДанныеШапки);
		
		ОтборПоВалюте       = Новый Структура("ЗначениеОтбора", ДокументОбъект.ВалютаДокумента);
		СтруктураПараметров = Новый Структура("ВалютаВзаиморасчетов", ОтборПоВалюте);
		
		СписокВидовДоговоров = Новый СписокЗначений;
		Если ПередачаТовараКомитентом Тогда
			
			СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
			
		ИначеЕсли ДанныеДокумента.Свойство("НДСИсчисляетсяНалоговымАгентом")
			И ДанныеДокумента.НДСИсчисляетсяНалоговымАгентом Тогда
			
			СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
			СтруктураПараметров.Вставить("УчетАгентскогоНДС", Новый Структура("ЗначениеОтбора", Истина));
			СтруктураПараметров.Вставить("ВидАгентскогоДоговора",
				Новый Структура("ЗначениеОтбора", Перечисления.ВидыАгентскихДоговоров.РеализацияТоваров));
			
		Иначе
			
			СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
			СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СФакторинговойКомпанией);
			СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
			СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
			
		КонецЕсли;
		
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДокументОбъект.ДоговорКонтрагента,
						ДокументОбъект.Контрагент, ДокументОбъект.Организация, СписокВидовДоговоров, СтруктураПараметров);
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЗаполнитьСчетаУчетаПередЗаписью", Истина);
		
	КонецЕсли;
	
	// вручную переопределим, если требуется
	ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеШапки);
	Если ПередачаТовараКомитентом Или ДанныеДокумента.ВозвратнаяТара.Количество() > 0 Тогда
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия;
	Иначе
		Если ДанныеДокумента.Товары.Количество()=0 Тогда
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги;
		ИначеЕсли ДанныеДокумента.Услуги.Количество()=0 Тогда
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары;
		Иначе
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия;
		КонецЕсли;
	КонецЕсли;
	
	ТипСклада   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.Склад, "ТипСклада");
	ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.ДоговорКонтрагента, "ВидДоговора");
	УчетВПродажныхЦенах = УчетнаяПолитика.СпособОценкиТоваровВРознице(ДокументОбъект.Организация, ДокументОбъект.Дата) = Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;
	РассчитыватьСуммаВРознице = ТипСклада <> Перечисления.ТипыСкладов.ОптовыйСклад
								И УчетВПродажныхЦенах
								И (ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
									ИЛИ ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары)
								И ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;

	НТТ = ТипСклада = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка;
	СвойстваСчета4112 = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(
		ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ);
	РазделениеПоСтавкамВРознице = СвойстваСчета4112.ВидСубконто1 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС
		ИЛИ СвойстваСчета4112.ВидСубконто2 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС
		ИЛИ СвойстваСчета4112.ВидСубконто3 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС;
	
	ЗаполнятьСтавкуНДСВРознице = НТТ И УчетВПродажныхЦенах И РазделениеПоСтавкамВРознице;
	
	ПлательщикНДС	 = УчетнаяПолитика.ПлательщикНДС(ДокументОбъект.Организация, ДокументОбъект.Дата);
	ЭтоКомиссия		 = ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
	ОтражениеВУСН	 = ПоступлениеТоваровУслугФормыКлиентСервер.ОтражениеВУСН(ДокументОбъект.ВидОперации, ЭтоКомиссия);
	
	ДокументОбъект.Товары.Очистить();
	Для Каждого СтрокаНакладной Из ДанныеДокумента.Товары Цикл
		
		СтрокаТЧ = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаНакладной);
		
		Если РассчитыватьСуммаВРознице Тогда
			ОбработкаТабличныхЧастей.ЗаполнитьЦенуВРозницеТабЧасти(СтрокаТЧ, ДокументОбъект, ДокументОбъект.Метаданные());
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуВРозницеТабЧасти(СтрокаТЧ);
		КонецЕсли;
		
		Если ЗаполнятьСтавкуНДСВРознице Тогда
			СтрокаТЧ.СтавкаНДСВРознице = ?(ПлательщикНДС, СтрокаТЧ.СтавкаНДС, Перечисления.СтавкиНДС.БезНДС);
		КонецЕсли;
		
		СтрокаТЧ.ОтражениеВУСН = ОтражениеВУСН;
		
		Если ДокументОбъект.НДСНеВыделять И ЗначениеЗаполнено(СтрокаТЧ.СуммаНДС) Тогда
			ДокументОбъект.НДСНеВыделять = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	ДокументОбъект.Услуги.Очистить();
	Для Каждого СтрокаНакладной Из ДанныеДокумента.Услуги Цикл
		СтрокаТЧ = ДокументОбъект.Услуги.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаНакладной);
		
		СтрокаТЧ.ОтражениеВУСН = ОтражениеВУСН;
		
		Если ДокументОбъект.НДСНеВыделять И ЗначениеЗаполнено(СтрокаТЧ.СуммаНДС) Тогда
			ДокументОбъект.НДСНеВыделять = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	ДокументОбъект.ВозвратнаяТара.Очистить();
	Для Каждого СтрокаНакладной Из ДанныеДокумента.ВозвратнаяТара Цикл
		
		СтрокаТЧ = ДокументОбъект.ВозвратнаяТара.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаНакладной);
		
	КонецЦикла;
	
	Если ДанныеДокумента.Свойство("ШтрихкодыУпаковок") Тогда 
		ДокументОбъект.ШтрихкодыУпаковок.Загрузить(ДанныеДокумента.ШтрихкодыУпаковок);
	КонецЕсли;
	СчетаУчетаВДокументах.Заполнить(ДокументОбъект, Новый Структура());
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("СтатусДокумента", Перечисления.СтатусыДокументовПоступления.ОригиналПолучен);
	
	ЗаписатьДокумент(ДокументОбъект, РежимЗаписи);
	
	Если НЕ ЗначениеЗаполнено(Документ) Тогда
		Документ = ДокументОбъект.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументСчетФактураПолученный(Документ, ДанныеДокумента)
	
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	Если ЗначениеЗаполнено(Документ) Тогда // получены изменения по существующему документу
		ДокументОбъект = Документ.ПолучитьОбъект();
		Если ДокументОбъект.Проведен Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		КонецЕсли;
	Иначе// создаем новый
		ДокументОбъект = Документы.СчетФактураПолученный.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		// вручную переопределим, если требуется
		ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеДокумента);
		
		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.Заполнить(ДокументОбъект);
		ДокументОбъект.ВидСчетаФактуры = ДанныеДокумента.ВидСчетаФактуры;
		ДокументОбъект.КодВидаОперации = ДанныеДокумента.КодВидаОперации;
	КонецЕсли;
	
	ДокументОбъект.ДокументыОснования.Очистить();
	Для Каждого ДокументОснование Из ДанныеДокумента.ДокументыОснования Цикл
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный")
				Или (ДокументОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный 
					И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")) Тогда
			Продолжить;
		КонецЕсли;
		ДокументОбъект.ДокументыОснования.Добавить().ДокументОснование = ДокументОснование;
	КонецЦикла;
	
	ДокументОбъект.ДокументОснование = ?(ДокументОбъект.ДокументыОснования.Количество()>0, ДокументОбъект.ДокументыОснования[0].ДокументОснование, Неопределено);
	Если ЗначениеЗаполнено(ДокументОбъект.ДокументОснование)
			И ТипЗнч(ДокументОбъект.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеДопРасходов") Тогда
		СтатусыДокумента = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
		СтатусыДокумента.СтатусОригиналаСФ = Перечисления.СтатусыДокументовПоступления.ОригиналПолучен;
		РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(ДокументОбъект.ДокументОснование, СтатусыДокумента);
	КонецЕсли;
	ДокументОбъект.ДополнительныеСвойства.Вставить("СтатусДокумента", Перечисления.СтатусыДокументовПоступления.ОригиналПолучен);
	
	Если ТипЗнч(ДокументОбъект.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
		
		ДокументОбъект.ДоговорКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.ДокументОснование, "ДоговорКонтрагента");
		
	КонецЕсли;
	
	ДокументОбъект.КодСпособаПолучения = 2;
	ДокументОбъект.НомерВходящегоДокумента = ДанныеДокумента.НомерВходящегоДокумента;
	ДокументОбъект.ДатаВходящегоДокумента  = ДанныеДокумента.ДатаВходящегоДокумента;
	ДокументОбъект.НомерИсходногоДокумента = ДанныеДокумента.НомерИсходногоДокумента;
	ДокументОбъект.ДатаИсходногоДокумента  = ДанныеДокумента.ДатаИсходногоДокумента;
	Если ДанныеДокумента.Свойство("Исправление") Тогда
		ДокументОбъект.Исправление       = ДанныеДокумента.Исправление;
		ДокументОбъект.НомерИсправления  = ДанныеДокумента.НомерИсправления;
		ДокументОбъект.ДатаИсправления   = ДанныеДокумента.ДатаИсправления;
	КонецЕсли;
	
	Если ДокументОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
		Если ДокументОбъект.ДокументыОснования.Количество()>0 Тогда
			ОснованиеСчетаФактуры = ДокументОбъект.ДокументыОснования[0];
			ОснованиеСчетаФактуры.НомерИсходногоДокумента = ДанныеДокумента.НомерИсходногоДокумента;
			ОснованиеСчетаФактуры.ДатаИсходногоДокумента  = ДанныеДокумента.ДатаИсходногоДокумента;
			ОснованиеСчетаФактуры.УчитыватьИсправлениеИсходногоДокумента = ДанныеДокумента.Свойство("УчитыватьИсправлениеИсходногоДокумента") И ДанныеДокумента.УчитыватьИсправлениеИсходногоДокумента;
			ОснованиеСчетаФактуры.СуммаУвеличение    = ДанныеДокумента.СуммаУвеличение;
			ОснованиеСчетаФактуры.СуммаУменьшение    = ДанныеДокумента.СуммаУменьшение;
			ОснованиеСчетаФактуры.СуммаНДСУвеличение = ДанныеДокумента.СуммаНДСУвеличение;
			ОснованиеСчетаФактуры.СуммаНДСУменьшение = ДанныеДокумента.СуммаНДСУменьшение;
			
			Если ДанныеДокумента.Свойство("УчитыватьИсправлениеИсходногоДокумента")
				И ДанныеДокумента.УчитыватьИсправлениеИсходногоДокумента Тогда
				ОснованиеСчетаФактуры.НомерИсправленияИсходногоДокумента = ДанныеДокумента.НомерИсправленияИсходногоДокумента;
				ОснованиеСчетаФактуры.ДатаИсправленияИсходногоДокумента  = ДанныеДокумента.ДатаИсправленияИсходногоДокумента;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Отражение вычета НДС по умолчанию
	СтруктураПараметров	= Новый Структура("Дата, Организация, ВидСчетаФактуры, ДокументОснование, Исправление");
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, ДокументОбъект);
	ДокументОбъект.НДСПредъявленКВычету = Документы.СчетФактураПолученный.ПолучитьПорядокОтраженияВычетаПоУмолчанию(СтруктураПараметров);
	
	ДокументОбъект.СуммаУвеличение    = ДанныеДокумента.СуммаУвеличение;
	ДокументОбъект.СуммаУменьшение    = ДанныеДокумента.СуммаУменьшение;
	ДокументОбъект.СуммаНДСУвеличение = ДанныеДокумента.СуммаНДСУвеличение;
	ДокументОбъект.СуммаНДСУменьшение = ДанныеДокумента.СуммаНДСУменьшение;
	Если ДанныеДокумента.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
		ДокументОбъект.СуммаДокумента     = ?(ДанныеДокумента.СуммаУвеличение = Неопределено, 0, ДанныеДокумента.СуммаУвеличение)
											- ?(ДанныеДокумента.СуммаУменьшение = Неопределено, 0, ДанныеДокумента.СуммаУменьшение);
		ДокументОбъект.СуммаНДСДокумента     = ?(ДанныеДокумента.СуммаНДСУвеличение = Неопределено, 0, ДанныеДокумента.СуммаНДСУвеличение)
											- ?(ДанныеДокумента.СуммаНДСУменьшение = Неопределено, 0, ДанныеДокумента.СуммаНДСУменьшение);
	Иначе
		ДокументОбъект.СуммаДокумента     = ДанныеДокумента.СуммаДокумента;
		ДокументОбъект.СуммаНДСДокумента  = ДанныеДокумента.СуммаНДСДокумента;
	КонецЕсли;
	
	Если ДанныеДокумента.Свойство("Авансы") Тогда
		ДокументОбъект.Авансы.Загрузить(ДанныеДокумента.Авансы);
	КонецЕсли;
	
	ЗаполнитьСчетаФактурыВыданныеПокупателям(ДокументОбъект);
	
	ЗаписатьДокумент(ДокументОбъект, РежимЗаписи);
	
	Если НЕ ЗначениеЗаполнено(Документ) Тогда
		Документ = ДокументОбъект.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСчетаФактурыВыданныеПокупателям(ДокументОбъект)

	Если Не (ЗначениеЗаполнено(ДокументОбъект.ДокументОснование)
		И ТипЗнч(ДокументОбъект.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах")) Тогда
		
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Документы.СчетФактураПолученный.НовыйПараметрыЗапросаСчетовФактурВыданныхПокупателям();
	ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ДокументОбъект,, "Комитент, ДоговорКомитента");
	ПараметрыЗапроса.Комитент         = ДокументОбъект.Контрагент;
	ПараметрыЗапроса.ДоговорКомитента = ДокументОбъект.ДоговорКонтрагента;
	
	ТаблицаСчетаФактурыВыданныеПокупателям = Документы.СчетФактураПолученный.СчетаФактурыВыданныеПокупателям(ПараметрыЗапроса);
	ДокументОбъект.СчетаФактурыВыданныеПокупателям.Загрузить(ТаблицаСчетаФактурыВыданныеПокупателям);
	
	КоличествоСФВыданныхПокупателю = ДокументОбъект.СчетаФактурыВыданныеПокупателям.Количество();
	Если КоличествоСФВыданныхПокупателю > 0 Тогда
		ДокументОбъект.СчетФактураВыданныйПокупателю = ДокументОбъект.СчетаФактурыВыданныеПокупателям[0].СчетФактура;
		Если КоличествоСФВыданныхПокупателю > 1 Тогда
			ДокументОбъект.КодВидаОперации = "27";
		КонецЕсли;
	Иначе
		ДокументОбъект.СчетФактураВыданныйПокупателю = Документы.СчетФактураВыданный.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПерезаполнениеЗначенийРеквизитовШапки(ТекущийОбъект, ДанныеЗаполнения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Строка Из ДанныеЗаполнения Цикл
		
		Если ЗначениеЗаполнено(Строка.Ключ) И ТекущийОбъект.Метаданные().Реквизиты.Найти(Строка.Ключ) <> Неопределено Тогда
			Если ТекущийОбъект[Строка.Ключ] <> Строка.Значение Тогда
				ТекущийОбъект[Строка.Ключ] = Строка.Значение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДанныеСтрокиТЧ(ДанныеЗаполнения, ДеревоРазбора, ЦенаВключаетНДС = Ложь)
	
	ДанныеДляЗаполненияСтрокиТЧ = Новый Структура();
	
	ЕстьРеквизитСопоставление = Ложь;
	СтрокаДереваНоменклатура  = Неопределено;
	Для Каждого ТекСтрока Из ДанныеЗаполнения Цикл
		ИмяРеквизитаВБД = ТекСтрока.Реквизит;
		Если ВРег(ИмяРеквизитаВБД) = ВРег("Мест") Тогда
			ИмяРеквизитаВБД = "КоличествоМест";
		ИначеЕсли ВРег(ИмяРеквизитаВБД) = ВРег("Описание") Тогда
			ИмяРеквизитаВБД = "Содержание";
		ИначеЕсли Найти(ИмяРеквизитаВБД, "ДоКорректировки") <> 0 Тогда
			ИмяРеквизитаВБД = СтрЗаменить(ИмяРеквизитаВБД, "ДоКорректировки", "ДоИзменения");
		КонецЕсли;
		НайденноеЗначение = ПолучитьЗначениеРеквизита(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
		ДанныеДляЗаполненияСтрокиТЧ.Вставить(ИмяРеквизитаВБД, НайденноеЗначение);
		
		Если ВРег(ИмяРеквизитаВБД) = ВРег("Сопоставление") Тогда
			ЕстьРеквизитСопоставление = Истина;
			Сопоставление = ПолучитьЗначениеРеквизита(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
			Если ТипЗнч(Сопоставление) <> Тип("Структура") Тогда
				Продолжить;
			ИначеЕсли Сопоставление.Свойство("НоменклатураИБ") Тогда
				Если ЗначениеЗаполнено(Сопоставление.НоменклатураИБ) Тогда
					ДанныеДляЗаполненияСтрокиТЧ.Вставить("Номенклатура", Сопоставление.НоменклатураИБ);
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ВРег(ИмяРеквизитаВБД) = ВРег("Номенклатура") Тогда
			СтрокаДереваНоменклатура = ТекСтрока;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьРеквизитСопоставление И СтрокаДереваНоменклатура <> Неопределено Тогда
		ПолучитьНоменклатуруПоКонтрагентуИИдентификатору(ДанныеДляЗаполненияСтрокиТЧ, СтрокаДереваНоменклатура, ДеревоРазбора);
	КонецЕсли;
	
	Если ДанныеДляЗаполненияСтрокиТЧ.Свойство("СуммаСНДС") И ЦенаВключаетНДС Тогда
		ДанныеДляЗаполненияСтрокиТЧ.Сумма = ДанныеДляЗаполненияСтрокиТЧ.СуммаСНДС;
	КонецЕсли;
	
	ЗаполняемСтавкуНДС = 0;
	ДанныеДляРасчетаСтавки = Новый Структура("СтавкаНДС, СуммаНДС, Сумма");
	ЗаполнитьЗначенияСвойств(ДанныеДляРасчетаСтавки, ДанныеДляЗаполненияСтрокиТЧ);
	
	Если НЕ ЗначениеЗаполнено(ДанныеДляРасчетаСтавки.СтавкаНДС) Тогда
		Если ЗначениеЗаполнено(ДанныеДляРасчетаСтавки.СуммаНДС) 
			И ЗначениеЗаполнено(ДанныеДляРасчетаСтавки.Сумма) Тогда
			//Определим ставку НДС расчетным путем
			ЗначениеСтавки = ОКР(ДанныеДляРасчетаСтавки.СуммаНДС / ДанныеДляРасчетаСтавки.Сумма,2);
			ДанныеДляЗаполненияСтрокиТЧ.Вставить("СтавкаНДС", ПолучитьСтавкуНДСПеречислением(ЗначениеСтавки));
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ДанныеДляЗаполненияСтрокиТЧ.Свойство("Содержание") И ДанныеДляЗаполненияСтрокиТЧ.Свойство("Наименование") Тогда
		ДанныеДляЗаполненияСтрокиТЧ.Вставить("Содержание", ДанныеДляЗаполненияСтрокиТЧ.Наименование);
	КонецЕсли;
	
	Возврат ДанныеДляЗаполненияСтрокиТЧ;
	
КонецФункции

Процедура ПолучитьНоменклатуруПоКонтрагентуИИдентификатору(ДанныеДляЗаполненияСтрокиТЧ, СтрокаДереваНоменклатура, ДеревоРазбора)
	
	СтрокаНоменклатура = ДеревоРазбора.Строки.Найти(СтрокаДереваНоменклатура.ЗначениеРеквизита, "ИндексСтроки", Истина);
	ИдентификаторНоменклатуры = СтрокаНоменклатура.ИД;
	СтрокаКонтрагент = ДеревоРазбора.Строки.Найти("Контрагент", "Реквизит" ,Истина);
	Контрагент = ПолучитьЗначениеРеквизита(СтрокаКонтрагент, "Контрагент", Истина, ДеревоРазбора);
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Номенклатура = НоменклатураПоИдентификаторуИКонтрагенту(ИдентификаторНоменклатуры, Контрагент);
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			ДанныеДляЗаполненияСтрокиТЧ.Вставить("Номенклатура", Номенклатура);
		Иначе
			ИдентификаторНоменклатуры = ВРег(СтрЗаменить(ДанныеДляЗаполненияСтрокиТЧ.Наименование, " ", "")
				+ "##" + ДанныеДляЗаполненияСтрокиТЧ.ЕдиницаИзмеренияНаименование);
			Номенклатура = НоменклатураПоИдентификаторуИКонтрагенту(ИдентификаторНоменклатуры, Контрагент);
			Если ЗначениеЗаполнено(Номенклатура) Тогда
				ДанныеДляЗаполненияСтрокиТЧ.Вставить("Номенклатура", Номенклатура);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция НоменклатураПоИдентификаторуИКонтрагенту(Идентификатор, Контрагент)
	
	Результат = Неопределено;
	СтруктураПоискаНоменклатуры = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(
		Контрагент, Идентификатор);
			
	МассивНайденнойНоменклатуры = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(
		Новый Структура("НоменклатураКонтрагента", СтруктураПоискаНоменклатуры), Истина);
		
	НайденныеДанные = ?(МассивНайденнойНоменклатуры.Количество() > 0,
		МассивНайденнойНоменклатуры[0].НоменклатураИБ, Неопределено);
		
	Если ТипЗнч(НайденныеДанные) = Тип("Структура") Тогда
		Результат = НайденныеДанные.Номенклатура;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СкопироватьЗначениеСтруктуры(Структура, ПолеИсточник, НовоеПоле)
	
	Если Структура.Свойство(ПолеИсточник) И Не Структура.Свойство(НовоеПоле) Тогда
		Структура.Вставить(НовоеПоле, Структура[ПолеИсточник]);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьАдресВДеревеУПД(ДеревоДанных, АдресУчастника, ТипАдреса, ВидУчастника)
	
	Если ТипАдреса = "АдресРФ" Тогда
			АдресУчастника.Удалить("АдресРФ");
			АдресУчастника.Удалить("КодСтр");
			АдресУчастника.Удалить("КодСтраны");
			АдресУчастника.Удалить("АдрТекст");
			АдресУчастника.Удалить("АдресТекст");
			АдресУчастника.Удалить("Регион");
			АдресУчастника.Удалить("КодРегион");
			АдресУчастника.Удалить("НаселПункт");
			АдресУчастника.Удалить("Кварт");
			АдресУчастника.Удалить("КодГАР");
			
		Для Каждого Элемент Из АдресУчастника Цикл
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Адрес." + ТипАдреса + "." + Элемент.Ключ,
									Элемент.Значение);
		КонецЦикла;
	ИначеЕсли ТипАдреса = "АдресИнформация" Тогда
			АдресУчастника.Удалить("АдресРФ");
			АдресУчастника.Удалить("ПочтовыйИндекс");
			АдресУчастника.Удалить("Индекс");
			АдресУчастника.Удалить("Регион");
			АдресУчастника.Удалить("КодРегиона");
			АдресУчастника.Удалить("КодРегион");
			АдресУчастника.Удалить("Район");
			АдресУчастника.Удалить("Город");
			АдресУчастника.Удалить("НаселенныйПункт");
			АдресУчастника.Удалить("НаселПункт");
			АдресУчастника.Удалить("Улица");
			АдресУчастника.Удалить("Дом");
			АдресУчастника.Удалить("Корпус");
			АдресУчастника.Удалить("Квартира");
			АдресУчастника.Удалить("Кварт");
			АдресУчастника.Удалить("КодСтр");
			АдресУчастника.Удалить("АдрТекст");
			АдресУчастника.Удалить("КодГАР");
	
		Для Каждого Элемент Из АдресУчастника Цикл
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Адрес." + ТипАдреса + "." + Элемент.Ключ,
									Элемент.Значение);
		КонецЦикла;
	ИначеЕсли ТипАдреса = "КодГАР" Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Адрес.КодГАР",
									Элемент.Значение);
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоСчетФактураНаАванс(СчетФактура)
	
	Если Не (ЗначениеЗаполнено(СчетФактура) И ТипЗнч(СчетФактура) = Тип("ДокументСсылка.СчетФактураВыданный")) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидСчетаФактуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетФактура, "ВидСчетаФактуры");
	
	Возврат ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАванс
		ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитента
		ИЛИ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаАвансКомитентаНаЗакупку;
	
КонецФункции

Функция ЭтоКомиссионнаяТорговля(СчетФактура)
	
	Если Не (ЗначениеЗаполнено(СчетФактура) И ТипЗнч(СчетФактура) = Тип("ДокументСсылка.СчетФактураВыданный")) Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетФактура, "ДокументОснование");
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТипЗначенияДокументаОснования = ТипЗнч(ДокументОснование);
	Возврат (ТипЗначенияДокументаОснования = Тип("ДокументСсылка.ОтчетКомитентуОПродажах")
			ИЛИ ТипЗначенияДокументаОснования = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
	
КонецФункции

Функция ОснованиеСФУчаствуетВЭДО(СчетФактура)
	
	Если Не ЗначениеЗаполнено(СчетФактура) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не (ТипЗнч(СчетФактура) = Тип("ДокументСсылка.СчетФактураВыданный")
			ИЛИ ТипЗнч(СчетФактура) = Тип("ДокументОбъект.СчетФактураВыданный")) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетФактура, "ДокументОснование");
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТипЗначенияДокументаОснования = ТипЗнч(ДокументОснование);
	Для каждого Тип Из Метаданные.ОпределяемыеТипы.ОснованияЭлектронныхДокументов.Тип.Типы() Цикл
		Если ТипЗначенияДокументаОснования = Тип Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;

КонецФункции

Функция НазначениеПлатежаДляСчетаНаОплату(Знач Наименование, Знач Номер, Знач Дата)
	
	Если ДоговорУказан(Наименование) Тогда
 		НазначениеПлатежаМассив = Новый Массив;
		НазначениеПлатежаМассив.Добавить(СокрЛП(Наименование));
		Если ЗначениеЗаполнено(Номер) Или ЗначениеЗаполнено(Дата) Тогда
			НазначениеПлатежаМассив.Добавить(" ");
			НазначениеПлатежаМассив.Добавить("(");
			Если ЗначениеЗаполнено(Номер) Тогда
				НазначениеПлатежаМассив.Добавить(НСтр("ru = '№'"));
				НазначениеПлатежаМассив.Добавить(" ");
				НазначениеПлатежаМассив.Добавить(СокрЛП(Номер));
			КонецЕсли;
			Если ЗначениеЗаполнено(Дата) Тогда
				Если ЗначениеЗаполнено(Номер) Тогда
					НазначениеПлатежаМассив.Добавить(" ");
				КонецЕсли;
				НазначениеПлатежаМассив.Добавить(НСтр("ru = 'от'"));
				НазначениеПлатежаМассив.Добавить(" ");
				НазначениеПлатежаМассив.Добавить(Формат(Дата, "ДЛФ=D"));
			КонецЕсли;
			НазначениеПлатежаМассив.Добавить(")");
		КонецЕсли;
		НазначениеПлатежа = СтрСоединить(НазначениеПлатежаМассив);
	Иначе
		НазначениеПлатежа = "";
	КонецЕсли;
	
	Возврат НазначениеПлатежа;
	
КонецФункции

Функция ПодготовитьТаблицуДанныхДоговора(Знач Наименование, Знач Номер, Знач Дата)
	
	ТаблицаДанныхДоговора = Новый ТаблицаЗначений;
	ТаблицаДанныхДоговора.Колонки.Добавить("ДокОснованиеНаименование");
	ТаблицаДанныхДоговора.Колонки.Добавить("ДокОснованиеНомер");
	ТаблицаДанныхДоговора.Колонки.Добавить("ДокОснованиеДата");
	
	Если Не ДоговорУказан(Наименование) Тогда
		Возврат ТаблицаДанныхДоговора; // возвращаем пустую таблицу
	КонецЕсли;
	
	НоваяСтрока = ТаблицаДанныхДоговора.Добавить();
	НоваяСтрока.ДокОснованиеНаименование = Наименование;
	НоваяСтрока.ДокОснованиеНомер        = Номер;
	НоваяСтрока.ДокОснованиеДата         = Дата;
	
	Возврат ТаблицаДанныхДоговора;
	
КонецФункции

Функция ПодготовитьТаблицуДанныхДоговораУПД(Знач Наименование, Знач Номер, Знач Дата)
	
	ТаблицаДанныхДоговора = Новый ТаблицаЗначений;
	ТаблицаДанныхДоговора.Колонки.Добавить("ДокументНаименование");
	ТаблицаДанныхДоговора.Колонки.Добавить("ДокументНомер");
	ТаблицаДанныхДоговора.Колонки.Добавить("ДокументДата");
	ТаблицаДанныхДоговора.Колонки.Добавить("ДокументДопСведения");
	
	Если Не ДоговорУказан(Наименование) Тогда
		Возврат ТаблицаДанныхДоговора; // возвращаем пустую таблицу
	КонецЕсли;
	
	НоваяСтрока = ТаблицаДанныхДоговора.Добавить();
	НоваяСтрока.ДокументНаименование = Наименование;
	НоваяСтрока.ДокументНомер        = Номер;
	НоваяСтрока.ДокументДата         = Дата;
	
	Возврат ТаблицаДанныхДоговора;
	
КонецФункции

Функция ДоговорУказан(НаименованиеДоговора)
	
	Возврат (ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам")
		И СтрНайти(ВРег(НаименованиеДоговора), "БЕЗ ДОГОВОРА") = 0);
	
КонецФункции

Процедура СообщитьОбОшибкеПриФормированииЭД(Документ, ТекстОшибки, Отказ)

	ШаблонСообщения = НСтр("ru = 'Не удалось сформировать электронный документ на основании документа ""%1"". %2'");
	
	ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ШаблонСообщения, Документ, ТекстОшибки),,,, Отказ);

КонецПроцедуры

Функция СозданныйПользователемВозвратПоставщику(ДанныеДокумента)

	Если Не ЗначениеЗаполнено(ДанныеДокумента.Шапка.Основание) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сделка", ДанныеДокумента.Шапка.Основание);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВозвратТоваровПоставщикуТовары.Ссылка КАК Ссылка,
	|	ВозвратТоваровПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ВозвратТоваровПоставщикуТовары.Количество) КАК Количество
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК ВозвратТоваровПоставщикуТовары
	|ГДЕ
	|	ВозвратТоваровПоставщикуТовары.Ссылка.Сделка = &Сделка
	|	И НЕ ВозвратТоваровПоставщикуТовары.Ссылка.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровПоставщикуТовары.Ссылка,
	|	ВозвратТоваровПоставщикуТовары.Номенклатура
	|ИТОГИ ПО
	|	Ссылка";
	
	ТаблицаТовары = ДанныеДокумента.Товары.Скопировать();
	ТаблицаТовары.Свернуть("Номенклатура", "Количество");
	
	ТаблицаТоварыДокумента = ТаблицаТовары.Скопировать();
	ВыборкаПоДокументам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоДокументам.Следующий() Цикл
		Выборка = ВыборкаПоДокументам.Выбрать();
		ТаблицаТоварыДокумента.Очистить();
		Пока Выборка.Следующий() Цикл
			СтрокаТоварыДокумента = ТаблицаТоварыДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоварыДокумента, Выборка);
		КонецЦикла;
		Если ОбщегоНазначения.КоллекцииИдентичны(ТаблицаТовары, ТаблицаТоварыДокумента) Тогда
			Возврат ВыборкаПоДокументам.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область СозданиеДокументовИБ

Функция НайтиСоздатьСчетНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляСчетаНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора);
	
	Если ДанныеДляЗагрузки.Свойство("Шапка") Тогда 
		ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	КонецЕсли;
	
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
		ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Если ДокументОбъект.Проведен Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		КонецЕсли;
	Иначе // создаем новый 
		ДокументОбъект = Документы.СчетНаОплатуПоставщика.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		
		ЗаполнениеДокументов.Заполнить(ДокументОбъект, ДанныеЗаполнения);
		
		СписокВидовДоговоров = Новый СписокЗначений;
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
		ОтборПоВалюте       = Новый Структура("ЗначениеОтбора", ДокументОбъект.ВалютаДокумента);
		СтруктураПараметров = Новый Структура("ВалютаВзаиморасчетов", ОтборПоВалюте);
		
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДокументОбъект.ДоговорКонтрагента,
						ДокументОбъект.Контрагент, ДокументОбъект.Организация, СписокВидовДоговоров, СтруктураПараметров);

	КонецЕсли;
	
	// вручную переопределим, если требуется
	ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
	
	ДокументОбъект.Товары.Загрузить(ДанныеДляЗагрузки.Товары);
	
	ЗаписатьДокумент(ДокументОбъект, РежимЗаписи);
	
	Возврат ДокументОбъект.Ссылка;
	
КонецФункции

Функция НайтиСоздатьКорректировкуПоступления(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляКорректировкиПоступления(СтрокаДляЗагрузки, ДеревоРазбора);
	
	Документ = СсылкаНаВладельца;
	ЗаполнитьДокументКорректировкиПоступления(Документ, ДанныеДляЗагрузки);
	
	Возврат Документ;
	
КонецФункции

Функция НайтиСоздатьСчетФактуру(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляСчетФактуры(СтрокаДляЗагрузки, ДеревоРазбора);
	
	Документ = СсылкаНаВладельца;
	ЗаполнитьДокументСчетФактураПолученный(Документ, ДанныеДляЗагрузки);
	
	Возврат Документ;
	
КонецФункции

Функция НайтиСоздатьОтчетКомиссионера(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Текст = "";
	ДанныеДляЗагрузки = ПодготовитьСтруктуруДляОтчетаКомиссионера(СтрокаДляЗагрузки, ДеревоРазбора);
	
	Если ДанныеДляЗагрузки.Свойство("Шапка") Тогда 
		ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	КонецЕсли; 
	
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда // получены изменения по существующему документу
		ДокументОбъект = СсылкаНаВладельца.ПолучитьОбъект();
		Если ДокументОбъект.Проведен Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			Текст = НСтр("ru = 'Операция возможна только для непроведенных документов!'");
		КонецЕсли;
	Иначе  // создаем новый
		
		ДокументОбъект = Документы.ОтчетКомиссионераОПродажах.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ЗаполнениеДокументов.Заполнить(ДокументОбъект, ДанныеЗаполнения);
		
		СписокВидовДоговоров = Новый СписокЗначений;
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
		ОтборПоВалюте			= Новый Структура("ЗначениеОтбора", ДокументОбъект.ВалютаДокумента);
		СтруктураПараметров		= Новый Структура("ВалютаВзаиморасчетов", ОтборПоВалюте);
		
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДокументОбъект.ДоговорКонтрагента,
						ДокументОбъект.Контрагент, ДокументОбъект.Организация, СписокВидовДоговоров, СтруктураПараметров);		
		// Заполнение реквизитов, специфичных для документа:		
		Документы.ОтчетКомиссионераОПродажах.ЗаполнитьСчетаУчетаРасчетов(,, ДокументОбъект);	
		
		Если ЗначениеЗаполнено(ДокументОбъект.ДоговорКонтрагента) Тогда
			
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.ДоговорКонтрагента, "СпособРасчетаКомиссионногоВознаграждения, ПроцентКомиссионногоВознаграждения");
			ДокументОбъект.СпособРасчетаКомиссионногоВознаграждения = ЗначенияРеквизитов.СпособРасчетаКомиссионногоВознаграждения;
			ДокументОбъект.ПроцентКомиссионногоВознаграждения = ЗначенияРеквизитов.ПроцентКомиссионногоВознаграждения;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// установка счета и статей затрат
	Если Не ЗначениеЗаполнено(ДокументОбъект.СчетУчетаЗатрат) Тогда
		
		ДокументОбъект.СчетУчетаЗатрат = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("ПланСчетов.Хозрасчетный.ИздержкиОбращения");
		СвойстваСчетаЗатрат = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ДокументОбъект.СчетУчетаЗатрат);
		СтатьяЗатрат = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтатьиЗатрат.УслугиКомиссионеров");
		Для Индекс = 1 По СвойстваСчетаЗатрат.КоличествоСубконто Цикл
			Если СвойстваСчетаЗатрат["ВидСубконто" + Индекс] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат Тогда
				Если НЕ ЗначениеЗаполнено(ДокументОбъект["Субконто" + Индекс]) Тогда
					ДокументОбъект["Субконто" + Индекс] = СтатьяЗатрат;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
	
	КонецЕсли;	
	ДокументОбъект.ОбменДанными.Загрузка = Истина;
	
	// вручную переопределим, если требуется
	ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);
	
	ДокументОбъект.Товары.Загрузить(ДанныеДляЗагрузки.Товары);
	ДокументОбъект.Услуги.Загрузить(ДанныеДляЗагрузки.Услуги);
	ДокументОбъект.Покупатели.Загрузить(ДанныеДляЗагрузки.Покупатели);
	ДокументОбъект.ДенежныеСредства.Загрузить(ДанныеДляЗагрузки.ДенежныеСредства);
	Документы.ОтчетКомиссионераОПродажах.ЗаполнитьСчетаУчетаВТабличнойЧасти(ДокументОбъект, "Товары");
	Документы.ОтчетКомиссионераОПродажах.ЗаполнитьСчетаУчетаВТабличнойЧасти(ДокументОбъект, "Услуги");
	
	ЗаписатьДокумент(ДокументОбъект, РежимЗаписи);
	
	Возврат ДокументОбъект.Ссылка;
	
КонецФункции

Функция НайтиСоздатьПоступлениеТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора, СсылкаНаВладельца = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВидОперации = ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ВидОперации", Истина, ДеревоРазбора);	
	Если ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		
		ДанныеДляЗагрузки = ПодготовитьСтруктуруДляКорректировкиПоступления(СтрокаДляЗагрузки, ДеревоРазбора);
		
		Документ = СсылкаНаВладельца;
		ЗаполнитьДокументКорректировкиПоступления(Документ, ДанныеДляЗагрузки);
		
	Иначе
		
		ДанныеДляЗагрузки = ПодготовитьСтруктуруДляПоступленияТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора);
		
		
		Документ = СсылкаНаВладельца;
		ЗаполнитьДокументПоступленияТоваровУслуг(Документ, ДанныеДляЗагрузки);
		
	КонецЕсли;
	
	Возврат Документ;
	
КонецФункции

Функция НайтиСоздатьДокументАктОРасхожденияхПолученнный(ДеревоДанных, ДокументУчета)
	
	ДанныеЭД = ДанныеВходящегоЭД(ДеревоДанных);
	
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	Если ЗначениеЗаполнено(ДокументУчета) Тогда
		ДокументОбъект = ДокументУчета.ПолучитьОбъект();
		Если ДокументОбъект.Проведен Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		КонецЕсли;
	Иначе
		ДокументОбъект = Документы.АктОРасхожденияхПолученный.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	// Шапка.
	ДокументОбъект.НомерВходящегоДокумента = ДанныеЭД.НомерДокумента;
	ДокументОбъект.ДатаВходящегоДокумента  = ДанныеЭД.ДатаДокумента;
	
	ДанныеОбъекта = Новый Структура("ДатаВходящегоДокумента", ДокументОбъект.ДатаВходящегоДокумента);
	НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "Продавец");
	ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеОбъекта);
	ДокументОбъект.Контрагент  = КонтрагентПоДаннымЭД(ДеревоДанных , "Покупатель");
	
	ДокументОснование = Неопределено;
	СопроводительныйДокумент = ДанныеЭД.СведенияОбОсмотреГруза.СопроводительныйДокумент;
	Если ЗначениеЗаполнено(СопроводительныйДокумент.Номер) Тогда
		ДокументОснование = НайтиРеализациюТоваровУслуг(ДокументОбъект.Организация, СопроводительныйДокумент.Номер, СопроводительныйДокумент.Дата);
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			ДокументОбъект.ДокументРеализации = ДокументОснование;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОбъект.ДокументРеализации) Тогда
		ДокументОбъект.СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.ДокументРеализации, "СуммаВключаетНДС");
	Иначе
		ДокументОбъект.СуммаВключаетНДС = Истина;
	КонецЕсли;

	// Товары.
	ЗначенияШтрихкодов = Новый Массив;
	ДокументОбъект.Товары.Очистить();
	Для Каждого СтрокаПриемки Из ДанныеЭД.РезультатыПриемки Цикл
		Номенклатура = Неопределено;
		Если ЗначениеЗаполнено(СтрокаПриемки.Сопоставление.НоменклатураИБ) Тогда
			Номенклатура = СтрокаПриемки.Сопоставление.НоменклатураИБ;
		КонецЕсли;
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			ЭтоУслуга = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "Услуга");
		КонецЕсли;
		
		Если ЭтоУслуга Тогда
			СтрокаТовара = ДокументОбъект.Услуги.Добавить();
		Иначе
			СтрокаТовара = ДокументОбъект.Товары.Добавить();
		КонецЕсли;
		СтрокаТовара.Номенклатура = Номенклатура;
		
		СтрокаТовара.СтавкаНДС = СтрокаПриемки.ПоДокументу.СтавкаНДС;
		
		СтавкаНДСЧислом = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаПриемки.ПоДокументу.СтавкаНДС);
		
		СтрокаТовара.КоличествоПоДокументу = СтрокаПриемки.ПоДокументу.Количество;
		СтрокаТовара.СуммаПоДокументу      = ?(ДокументОбъект.СуммаВключаетНДС, СтрокаПриемки.ПоДокументу.СуммаСНДС,
			СтрокаПриемки.ПоДокументу.СуммаСНДС - СтрокаПриемки.ПоДокументу.СуммаНДС);
		СтрокаТовара.СуммаНДСПоДокументу   = СтрокаПриемки.ПоДокументу.СуммаНДС;
		СтрокаТовара.ЦенаПоДокументу       = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
			СтрокаПриемки.ПоДокументу.Цена, Ложь, ДокументОбъект.СуммаВключаетНДС, СтавкаНДСЧислом);
		
		СтрокаТовара.Количество = СтрокаПриемки.ПоФакту.Количество;
		СтрокаТовара.Сумма      = ?(ДокументОбъект.СуммаВключаетНДС, СтрокаПриемки.ПоФакту.СуммаСНДС, 
			СтрокаПриемки.ПоФакту.СуммаСНДС - СтрокаПриемки.ПоФакту.СуммаНДС);
		СтрокаТовара.СуммаНДС   = СтрокаПриемки.ПоФакту.СуммаНДС;
		СтрокаТовара.Цена       = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
			СтрокаПриемки.ПоФакту.Цена, Ложь, ДокументОбъект.СуммаВключаетНДС, СтавкаНДСЧислом);
		
		ПолучитьЗначенияШтрихкодов(ЗначенияШтрихкодов, СтрокаПриемки.ПоФакту);
		
		СтрокаТовара.ЕстьВДокументеРеализации = Истина;
	КонецЦикла;
	
	ЗаполнитьШтрихкодыУпаковок(ДокументОбъект.ШтрихкодыУпаковок, ЗначенияШтрихкодов);
	
	ЗаписатьДокумент(ДокументОбъект, РежимЗаписи);
	
	Если НЕ ЗначениеЗаполнено(ДокументУчета) Тогда
		ДокументУчета = ДокументОбъект.Ссылка;
	КонецЕсли;
	
КонецФункции

Функция ПодготовитьСтруктуруДляПоступленияТоваровУслуг(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	
	Товары = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	ВозвратнаяТара = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().ВозвратнаяТара.ВыгрузитьКолонки();
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		Если СтрокаРеквизита.Реквизит = "СписокОписаний" Тогда
			СтрокаРеквизитаОписанийРабот = ПолучитьЗначениеРеквизита(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			Для Каждого СтрокаРеквизитаОписания Из СтрокаРеквизитаОписанийРабот.Строки Цикл
				Если СтрокаРеквизитаОписания.Строки.Количество() = 0 Тогда
					Реквизит = ПолучитьЗначениеРеквизита(СтрокаРеквизитаОписания, СтрокаРеквизитаОписания.Реквизит, Истина, ДеревоРазбора);
					Если ЗначениеЗаполнено(Реквизит) Тогда
						ДанныеЗаполненияШапки.Вставить(СтрокаРеквизитаОписания.Реквизит, Реквизит);
					КонецЕсли;
				Иначе
					ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизитаОписания.Строки, ДеревоРазбора);
					ЗаполнитьЗначенияСвойств(Услуги.Добавить(), ДанныеДляЗаполненияСтрокиТЧ);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Если СтрокаРеквизита.Строки.Количество() = 0 Тогда
				Реквизит = ПолучитьЗначениеРеквизита(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
				Если ЗначениеЗаполнено(Реквизит) Тогда
					ДанныеЗаполненияШапки.Вставить(СтрокаРеквизита.Реквизит, Реквизит);
				КонецЕсли;
			Иначе
				ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
				
				ЭтоУслуга = Ложь;
				Если ДанныеДляЗаполненияСтрокиТЧ.Свойство("Номенклатура")
					И ЗначениеЗаполнено(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура) Тогда
					
					ЭтоУслуга = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеДляЗаполненияСтрокиТЧ.Номенклатура, "Услуга");
				Иначе
					ЭтоУслуга = ДанныеДляЗаполненияСтрокиТЧ.Свойство("Услуга") И ДанныеДляЗаполненияСтрокиТЧ.Услуга = Истина;
				КонецЕсли;
				
				Если ЭтоУслуга Тогда
					СтрокаТаблицы = Услуги.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеДляЗаполненияСтрокиТЧ);
					Если Не ЗначениеЗаполнено(ДанныеДляЗаполненияСтрокиТЧ.Количество)
							И ЗначениеЗаполнено(ДанныеДляЗаполненияСтрокиТЧ.МассаНетто)
							И ДанныеДляЗаполненияСтрокиТЧ.МассаНетто > 0 Тогда
							
							СтрокаТаблицы.Количество = ДанныеДляЗаполненияСтрокиТЧ.МассаНетто;
							
					КонецЕсли;
				Иначе
					
					Если ДанныеДляЗаполненияСтрокиТЧ.Свойство("ТаблицаДокумента")
						И ДанныеДляЗаполненияСтрокиТЧ.ТаблицаДокумента = "ВозвратнаяТара" Тогда
						
						СтрокаТаблицы = ВозвратнаяТара.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеДляЗаполненияСтрокиТЧ);
						
					Иначе
						
						СтрокаТаблицы = Товары.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеДляЗаполненияСтрокиТЧ);
						ЗаполнитьНомерТДИСтрануПроисхождения(СтрокаТаблицы, ДанныеДляЗаполненияСтрокиТЧ);
						Если Не ЗначениеЗаполнено(ДанныеДляЗаполненияСтрокиТЧ.Количество)
							И ЗначениеЗаполнено(ДанныеДляЗаполненияСтрокиТЧ.МассаНетто)
							И ДанныеДляЗаполненияСтрокиТЧ.МассаНетто > 0 Тогда
							
							СтрокаТаблицы.Количество = ДанныеДляЗаполненияСтрокиТЧ.МассаНетто;
							
						КонецЕсли;
						
					КонецЕсли;
						
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СкопироватьЗначениеСтруктуры(ДанныеЗаполненияШапки, "Валюта", "ВалютаДокумента");
	СкопироватьЗначениеСтруктуры(ДанныеЗаполненияШапки, "Курс", "КурсВзаиморасчетов");
	Если НЕ ДанныеЗаполненияШапки.Свойство("КратностьВзаиморасчетов") Тогда
		ДанныеЗаполненияШапки.Вставить("КратностьВзаиморасчетов", 1);
	КонецЕсли;
	
	Организация = ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Организация", Истина, ДеревоРазбора);
	Если Организация = Неопределено Тогда
		
		ИндексСтроки = ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Организация", Истина);
		НайденнаяСтрока = ДеревоРазбора.Строки.Найти(ИндексСтроки, "ИндексСтроки", Истина);
		ИННОрганизации = ПолучитьЗначениеРеквизита(НайденнаяСтрока, "ИНН", Истина);
		КППОрганизации = ПолучитьЗначениеРеквизита(НайденнаяСтрока, "КПП", Истина);
		
		Если ИННОрганизации <> Неопределено Тогда
			
			Организация = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Организации", ИННОрганизации, КППОрганизации);
			Если Организация = Неопределено Тогда
				
				Организация = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Организации", ИННОрганизации);
				
			КонецЕсли;
			Если Организация <> Неопределено Тогда
				
				ДанныеЗаполненияШапки.Вставить("Организация", Организация);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеЗаполненияШапки.Вставить("СчетНаОплатуПоставщика",  ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Основание", Истина, ДеревоРазбора));
	
	ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Номер"));
	ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента",  ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Дата"));
	
	НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ПередачаТовараКомитентом", Истина);
	Если НайденноеЗначение <> Неопределено Тогда
		ДанныеЗаполненияШапки.Вставить("ПередачаТовараКомитентом", Булево(НайденноеЗначение));
	Иначе
		ДанныеЗаполненияШапки.Вставить("ПередачаТовараКомитентом", Ложь);
	КонецЕсли;
	
	ДанныеЗаполненияШапки.Удалить("ВидОперации");
	ДанныеЗаполненияШапки.Вставить("СуммаВключаетНДС", Ложь);
	
	ДанныеДляОбъекта.Вставить("Шапка", ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	ДанныеДляОбъекта.Вставить("Услуги", Услуги);
	ДанныеДляОбъекта.Вставить("ВозвратнаяТара", ВозвратнаяТара);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляСчетаНаОплатуПоставщика(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	
	Товары = Документы.СчетНаОплатуПоставщика.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	
	ЦенаВключаетНДС = ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ЦенаВключаетНДС", Истина, ДеревоРазбора);
	Если ЦенаВключаетНДС = Неопределено Тогда
		ЦенаВключаетНДС = Ложь;
	КонецЕсли;
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Строки.Количество() = 0 Тогда
			
			Реквизит = ПолучитьЗначениеРеквизита(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			Если ЗначениеЗаполнено(Реквизит) Тогда
				ДанныеЗаполненияШапки.Вставить(СтрокаРеквизита.Реквизит, Реквизит);
			КонецЕсли;
			
		Иначе
			
			ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора, ЦенаВключаетНДС);
			ЗаполнитьЗначенияСвойств(Товары.Добавить(), ДанныеДляЗаполненияСтрокиТЧ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СкопироватьЗначениеСтруктуры(ДанныеЗаполненияШапки, "Валюта", "ВалютаДокумента");
	СкопироватьЗначениеСтруктуры(ДанныеЗаполненияШапки, "Курс", "КурсВзаиморасчетов");
	
	СкопироватьЗначениеСтруктуры(ДанныеЗаполненияШапки, "ЦенаВключаетНДС", "СуммаВключаетНДС");
	
	Если НЕ ДанныеЗаполненияШапки.Свойство("КратностьВзаиморасчетов") Тогда
		ДанныеЗаполненияШапки.Вставить("КратностьВзаиморасчетов", 1);
	КонецЕсли;
	
	ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Номер"));
	ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Дата"));
	
	// Если по ИНН и КПП организация не найдена, то возможно:
	//  - у организации изменился КПП (нужно проверить историю изменений КПП)
	//  - организация является обособленным подразделением в справочнике "Подразделения организаций"
	//    (необходимо найти головную организацию и подразделение, и подставить в документ учета).
	Если Не (ДанныеЗаполненияШапки.Свойство("Организация")
		И ЗначениеЗаполнено(ДанныеЗаполненияШапки.Организация)) Тогда
		
		ДанныеОрганизации = ДеревоРазбора.Строки.Найти("Организации", "ТипОбъекта", Истина);
		ИННОрганизации = ПолучитьЗначениеРеквизита(ДанныеОрганизации, "ИНН", Истина);
		КППОрганизации = ПолучитьЗначениеРеквизита(ДанныеОрганизации, "КПП", Истина);
		НайтиОрганизациюПоДаннымЭДCML(ДанныеЗаполненияШапки, ИННОрганизации, КППОрганизации);
	КонецЕсли;
	
	ДанныеДляОбъекта.Вставить("Шапка", ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляПоступленияТоваровУслугУПД(ДеревоДанных)
	
	ДанныеОбъекта = Новый Структура;
	
	Товары         = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги         = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	ВозвратнаяТара = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().ВозвратнаяТара.ВыгрузитьКолонки();
	
	Валюта = НайтиСсылкуНаОбъект("Валюты", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод"));
	ДанныеОбъекта.Вставить("ВалютаДокумента", Валюта);
	КурсВзаиморасчетов = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ВалютаКурс");
	ДанныеОбъекта.Вставить("КурсВзаиморасчетов", ?(ЗначениеЗаполнено(КурсВзаиморасчетов), КурсВзаиморасчетов, 1));
	ДанныеОбъекта.Вставить("КратностьВзаиморасчетов", 1);
	
	ДанныеОбъекта.Вставить("ВидОперацииЭД", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации"));
	
	ДанныеОбъекта.Вставить("СуммаВключаетНДС", Ложь);
	ДанныеОбъекта.Вставить("Корректировка",    Ложь);
	
	ДанныеОбъекта.Вставить("НомерВходящегоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента"));
	ДанныеОбъекта.Вставить("ДатаВходящегоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента"));
	
	Если ЗначениеЗаполнено(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления")) Тогда
		ДанныеОбъекта.Вставить("Исправление", Истина);
		ДанныеОбъекта.Вставить("НомерИсправления", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления"));
		ДанныеОбъекта.Вставить("ДатаИсправления",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления"));
	Иначе
		ДанныеОбъекта.Вставить("Исправление", Ложь);
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("УчитыватьИсправлениеИсходногоДокумента", Ложь);
	
	НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "СведенияОПокупателе");
	
	ДанныеОбъекта.Вставить("Контрагент",  КонтрагентПоДаннымЭД(ДеревоДанных, "СведенияОПродавце"));
	
	ДанныеОбъекта.Вставить("ПередачаТовараКомитентом", Ложь);
	
	ОснованиеСчетаФактуры = Неопределено;
	ДокументыОснованияСчетаФактуры = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
	Если ТипЗнч(ДокументыОснованияСчетаФактуры) = Тип("Массив") Тогда
		Для Каждого ДокументОснование Из ДокументыОснованияСчетаФактуры Цикл
			Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				ОснованиеСчетаФактуры = ДокументОснование;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(ДокументыОснованияСчетаФактуры) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ОснованиеСчетаФактуры = ДокументыОснованияСчетаФактуры;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОснованиеСчетаФактуры) Тогда
		ДокументОснованиеСчетаФактуры = ДокументОснованиеСчетаФактурыПолученного(ОснованиеСчетаФактуры);
		Если ЗначениеЗаполнено(ДокументОснованиеСчетаФактуры) Тогда
			ДанныеОбъекта.Вставить("Основание", ДокументОснованиеСчетаФактуры);
		КонецЕсли;
	КонецЕсли;
	
	ТекстоваяИнформация = ДеревоДанных.Строки.Найти("ДопДанныеДокументаОтгрузки.ТекстоваяИнформация", "ПолныйПуть", Истина);
	Если ТекстоваяИнформация <> Неопределено Тогда
		Для Каждого СтрокаТекстовойИнформации Из ТекстоваяИнформация.Строки Цикл
			Если ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаТекстовойИнформации, 
						"ДопДанныеДокументаОтгрузки.ТекстоваяИнформация.НомерСтроки.Идентификатор") = "ПередачаТовараКомитентом" Тогда
				ДанныеОбъекта.ПередачаТовараКомитентом = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаТекстовойИнформации,
						"ДопДанныеДокументаОтгрузки.ТекстоваяИнформация.НомерСтроки.Значение") = "Истина";
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ИспользуетсяОбратноеНачислениеНДС = БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
	НДСИсчисляетсяНалоговымАгентом = Ложь;
	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		
		Номенклатура = НоменклатураИБ(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Сопоставление"); 
		
		Признак = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Признак");
		ЭтоУслуга = Ложь;
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			ЭтоУслуга = ?(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "Услуга"), Истина, Ложь);
		ИначеЕсли ЗначениеЗаполнено(Признак) Тогда
			ЭтоУслуга = ?(Признак = "1", Ложь, Истина);
		Иначе
			ЭтоУслуга = Истина;
		КонецЕсли;
		
		Если Не ЭтоУслуга Тогда
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.ЕдиницаИзмерения = НайтиСсылкуНаОбъект("ЕдиницыИзмерения",
				ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЕдиницаИзмеренияКод"));
		Иначе
			НоваяСтрока = Услуги.Добавить();
			НоваяСтрока.Содержание = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ТоварНаименование");
		КонецЕсли;
			
		// Обязательные реквизиты:
		// Ставка "НДС исчисляется налоговым агентом" используется при продаже металлолома, сырых шкур животных
		СтавкаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
		Если СтавкаНДС = СтавкаНДСИсчисляетсяНалоговымАгентом() Тогда
			
			НДСИсчисляетсяНалоговымАгентом = Истина;
			Если ИспользуетсяОбратноеНачислениеНДС Тогда
				
				НоваяСтрока.СтавкаНДС = УчетНДСКлиентСервер.ОбщаяСтавкаНДС(
					ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента"));
				// Вычисляем НДС сверху по общей ставке
				НоваяСтрока.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
					ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалога"),
					Ложь,
					УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
					
			Иначе
				НоваяСтрока.СтавкаНДС = Неопределено;
				НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалога");
			КонецЕсли;
			
		Иначе
			НоваяСтрока.СтавкаНДС = СтавкаНДС;
			НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалога");
		КонецЕсли;
		
		// Необязательные реквизиты:
		НоваяСтрока.Количество = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Количество");
		НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЦенаЗаЕдиницуИзмерения");
		НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалога");
		
		НоваяСтрока.Номенклатура = Номенклатура;
		
		Если Не ЭтоУслуга Тогда
		
			СведенияОТаможеннойДекларации = СведенияОТоваре.Строки.Найти(
				"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации", "ПолныйПуть", Истина);
			
			Если СведенияОТаможеннойДекларации <> Неопределено
				И СведенияОТаможеннойДекларации.Строки.Количество() > 0 Тогда
				
				ДанныеГТД = Новый Структура("НомерТД, КодСтраныПроисхождения", "", "");
				ДанныеГТД.НомерТД = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТаможеннойДекларации.Строки[0],
					"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации.НомерСтроки.ТаможеннаяДекларацияНомер");
				ДанныеГТД.КодСтраныПроисхождения = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТаможеннойДекларации.Строки[0],
					"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации.НомерСтроки.СтранаПроисхожденияКод");
				
				ЗаполнитьНомерТДИСтрануПроисхождения(НоваяСтрока, ДанныеГТД, Ложь);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДляЗаполнения = Новый Структура();
	ДанныеДляЗаполнения.Вставить("Шапка", ДанныеОбъекта);
	ДанныеДляЗаполнения.Вставить("Товары", Товары);
	ДанныеДляЗаполнения.Вставить("Услуги", Услуги);
	ДанныеДляЗаполнения.Вставить("ВозвратнаяТара", ВозвратнаяТара);
	ДанныеДляЗаполнения.Вставить("НДСИсчисляетсяНалоговымАгентом", НДСИсчисляетсяНалоговымАгентом);
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

Функция ПодготовитьСтруктуруДляКорректировкиПоступленияУКД(ДеревоДанных)
	
	ДанныеОбъекта = Новый Структура;
	
	Товары         = Документы.КорректировкаПоступления.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги         = Документы.КорректировкаПоступления.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Валюта = НайтиСсылкуНаОбъект("Валюты", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод"));
	ДанныеОбъекта.Вставить("ВалютаДокумента", Валюта);
	КурсВзаиморасчетов = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ВалютаКурс");
	ДанныеОбъекта.Вставить("КурсВзаиморасчетов", ?(ЗначениеЗаполнено(КурсВзаиморасчетов), КурсВзаиморасчетов, 1));
	ДанныеОбъекта.Вставить("КратностьВзаиморасчетов", 1);
	
	ДанныеОбъекта.Вставить("СуммаВключаетНДС", Ложь);
	
	ДанныеОбъекта.Вставить("НомерВходящегоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента"));
	ДанныеОбъекта.Вставить("ДатаВходящегоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента"));
	
	ДанныеОбъекта.Вставить("НомерИсходногоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента"));
	ДанныеОбъекта.Вставить("ДатаИсходногоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента"));
	
	Если ЗначениеЗаполнено(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправленияИсходногоДокумента")) Тогда
		ДанныеОбъекта.Вставить("УчитыватьИсправлениеИсходногоДокумента", Истина);
		ДанныеОбъекта.Вставить("НомерИсправленияИсходногоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправленияИсходногоДокумента"));
		ДанныеОбъекта.Вставить("ДатаИсправленияИсходногоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправленияИсходногоДокумента"));
	Иначе
		ДанныеОбъекта.Вставить("УчитыватьИсправлениеИсходногоДокумента", Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления")) Тогда
		ДанныеОбъекта.Вставить("ВидОперацииЭД", Перечисления.ВидыОперацийЭД.Исправление);
		ДанныеОбъекта.Вставить("Исправление", Истина);
		ДанныеОбъекта.Вставить("НомерИсправления", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления"));
		ДанныеОбъекта.Вставить("ДатаИсправления",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления"));
	Иначе
		ДанныеОбъекта.Вставить("ВидОперацииЭД", Перечисления.ВидыОперацийЭД.Корректировка);
		ДанныеОбъекта.Вставить("Исправление", Ложь);
	КонецЕсли;
	
	ДокументОснование = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
	Если ТипЗнч(ДокументОснование) = Тип("Массив") И ДокументОснование.Количество() > 0 Тогда
		ДокументОснование = ДокументОснование[0];
	КонецЕсли;
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ДокументОснование");
	КонецЕсли;
	ДанныеОбъекта.Вставить("Основание", ДокументОснование);
	
	НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "СведенияОПокупателе");
	ДанныеОбъекта.Вставить("Контрагент",  КонтрагентПоДаннымЭД(ДеревоДанных, "СведенияОПродавце"));
	
	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
	ИспользуетсяОбратноеНачислениеНДС = БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
	ШтрихкодыУпаковок = ЭлектронноеВзаимодействиеИСМП.НоваяТаблицаШтрихкодыУпаковок();
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		
		Номенклатура = НоменклатураИБ(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Сопоставление");
		
		Если ЗначениеЗаполнено(Номенклатура)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "Услуга") Тогда
			НоваяСтрока = Услуги.Добавить();
			НоваяСтрока.СодержаниеДоИзменения = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ТоварНаименование");
			НоваяСтрока.Содержание            = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ТоварНаименование");
		Иначе
			НоваяСтрока = Товары.Добавить();
		КонецЕсли;
		
		НоваяСтрока.Номенклатура = Номенклатура;
		
		// Ставка "НДС исчисляется налоговым агентом" используется при продаже металлолома, сырых шкур животных
		СтавкаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
		Если СтавкаНДС = СтавкаНДСИсчисляетсяНалоговымАгентом() Тогда
			
			Если ИспользуетсяОбратноеНачислениеНДС Тогда
				
				НоваяСтрока.СтавкаНДС = УчетНДСКлиентСервер.ОбщаяСтавкаНДС(
					ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента"));
				НоваяСтрока.СуммаНДСДоКорректировки = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
					ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалогаДоКорректировки"),
					Ложь,
					УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
					
				НоваяСтрока.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
					ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалога"),
					Ложь,
					УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
					
			Иначе
				НоваяСтрока.СтавкаНДС = Неопределено;
				НоваяСтрока.СуммаНДСДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалогаДоКорректировки");
				НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалога");
			КонецЕсли;
			
		Иначе
			НоваяСтрока.СтавкаНДС = СтавкаНДС;
			НоваяСтрока.СуммаНДСДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалогаДоКорректировки");
			НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалога");
		КонецЕсли;
		
		НоваяСтрока.КоличествоДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.КоличествоДоКорректировки");
		НоваяСтрока.Количество = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Количество");
		НоваяСтрока.ЦенаДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЦенаЗаЕдиницуИзмеренияДоКорректировки");
		НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЦенаЗаЕдиницуИзмерения");
		НоваяСтрока.СуммаДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалогаДоКорректировки");
		НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалога");
		
		ДобавитьШтрихкодыТаблицуШтрихкодовУпаковокУКД(ШтрихкодыУпаковок, СведенияОТоваре);
		
	КонецЦикла;
	
	ЭлектронноеВзаимодействиеИСМП.СвернутьТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок);
	
	ДанныеДляЗаполнения = Новый Структура();
	ДанныеДляЗаполнения.Вставить("Шапка"            , ДанныеОбъекта);
	ДанныеДляЗаполнения.Вставить("Товары"           , Товары);
	ДанныеДляЗаполнения.Вставить("Услуги"           , Услуги);
	ДанныеДляЗаполнения.Вставить("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

Функция ПодготовитьСтруктуруДляСчетФактуры(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеЗаполненияШапки = Новый Структура;
	ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Номер"));
	ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Дата"));
	
	ДанныеЗаполненияШапки.Вставить("НомерИсходногоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерСчетаФактуры"));
	ДанныеЗаполненияШапки.Вставить("ДатаИсходногоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ДатаСчетаФактуры"));
	
	КодВидаОперации = "01";
	Комиссионер = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ДанныеКомиссионера");
	Если СтрокаДляЗагрузки.ОписаниеОбъекта = "Корректировочный" Тогда
		ДанныеЗаполненияШапки.Вставить("ВидСчетаФактуры",Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
		Если ЗначениеЗаполнено (ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерИсправления")) Тогда
			ДанныеЗаполненияШапки.Вставить("Исправление", Истина);
			ДанныеЗаполненияШапки.Вставить("НомерИсправления", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерИсправления"));
			ДанныеЗаполненияШапки.Вставить("ДатаИсправления", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ДатаИсправления"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерИсправленияСчетаФактуры")) Тогда
			ДанныеЗаполненияШапки.Вставить("УчитыватьИсправлениеИсходногоДокумента", Истина);
			ДанныеЗаполненияШапки.Вставить("НомерИсправленияИсходногоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерИсправленияСчетаФактуры"));
			ДанныеЗаполненияШапки.Вставить("ДатаИсправленияИсходногоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ДатаИсправленияСчетаФактуры"));
		КонецЕсли;
		Если ЗначениеЗаполнено(Комиссионер) Тогда
			
			КодВидаОперации = "04";
			
		КонецЕсли;
	Иначе
		
		// Если счет-фактура на аванс.
		СчетФактураНаАванс = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ДеревоРазбора, "СчетФактураНаАванс", Истина);
		Если ЗначениеЗаполнено(СчетФактураНаАванс) И Булево(СчетФактураНаАванс) Тогда
			
			ДанныеЗаполненияШапки.Вставить("ВидСчетаФактуры",Перечисления.ВидСчетаФактурыПолученного.НаАванс);
			КодВидаОперации = "02";
			
		Иначе
			
			ДанныеЗаполненияШапки.Вставить("ВидСчетаФактуры",Перечисления.ВидСчетаФактурыПолученного.НаПоступление);
			Если ЗначениеЗаполнено(Комиссионер) Тогда
				КодВидаОперации = "04";
			КонецЕсли;
			
		КонецЕсли;
			
		Если ЗначениеЗаполнено (ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерИсправления")) Тогда
			ДанныеЗаполненияШапки.Вставить("Исправление", Истина);
			ДанныеЗаполненияШапки.Вставить("НомерИсправления", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерИсправления"));
			ДанныеЗаполненияШапки.Вставить("ДатаИсправления", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ДатаИсправления"));
		КонецЕсли;
	КонецЕсли;	
	ДанныеЗаполненияШапки.Вставить("КодВидаОперации", КодВидаОперации);
	
	//Заполняем документы-основания
	МассивДокументовОснований = Новый Массив();
	НайденнаяСтрока = СтрокаДляЗагрузки.Строки.Найти("ДокументыОснования", "Реквизит");
	Если НайденнаяСтрока <> Неопределено Тогда 
		Для Каждого Строка Из НайденнаяСтрока.Строки Цикл
			МассивДокументовОснований.Добавить(Строка.СсылкаНаОбъект);
		КонецЦикла;
	КонецЕсли;
	ДанныеЗаполненияШапки.Вставить("ДокументыОснования", МассивДокументовОснований);
	
	ВалКод = ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ВалКод");
	Если НЕ  ВалКод="643" Тогда
		ДанныеЗаполненияШапки.Вставить("ВалютаДокумента", НайтиСсылкуНаОбъект("Валюты", ВалКод));
	Иначе
		ДанныеЗаполненияШапки.Вставить("ВалютаДокумента", Константы.ВалютаРегламентированногоУчета.Получить());
	КонецЕсли;
	
	// Cчет-фактура на поступление
	ДанныеЗаполненияШапки.Вставить("Организация", ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Организация", Истина, ДеревоРазбора));
	Контрагент = ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Контрагент", Истина, ДеревоРазбора);
	ДанныеЗаполненияШапки.Вставить("Контрагент", Контрагент); 
	
	ДанныеЗаполненияШапки.Вставить("СуммаДокумента",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "СтТовУчНал"));
	ДанныеЗаполненияШапки.Вставить("СуммаНДСДокумента",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "СумНДС"));
	
	ДанныеЗаполненияШапки.Вставить("СуммаУменьшение",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "СтТовУчНалВсегоУм"));
	ДанныеЗаполненияШапки.Вставить("СуммаНДСУменьшение",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "СумНДСУм"));
	
	ДанныеЗаполненияШапки.Вставить("СуммаУвеличение",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "СтТовУчНалВсегоУвел"));
	ДанныеЗаполненияШапки.Вставить("СуммаНДСУвеличение",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "СумНДСУвел"));
	
	Возврат ДанныеЗаполненияШапки;
	
КонецФункции

Функция ПодготовитьСтруктуруДляСчетаФактурыУКД(ДеревоДанных)
	
	ДанныеОбъекта = Новый Структура;
	ДанныеОбъекта.Вставить("НомерВходящегоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента"));
	ДанныеОбъекта.Вставить("ДатаВходящегоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента"));
	
	НомерИсправления = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления");
	ДатаИсправления  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления");
	
	Если ЗначениеЗаполнено(НомерИсправления) Тогда
		ДанныеОбъекта.Вставить("Исправление", Истина);
		ДанныеОбъекта.Вставить("НомерИсправления", НомерИсправления);
		ДанныеОбъекта.Вставить("ДатаИсправления",  ДатаИсправления);
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("НомерИсходногоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента"));
	ДанныеОбъекта.Вставить("ДатаИсходногоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента"));
	
	НомерИсправленияИсходногоДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправленияИсходногоДокумента");
	ДатаИсправленияИсходногоДокумента  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправленияИсходногоДокумента");
	
	Если ЗначениеЗаполнено(НомерИсправленияИсходногоДокумента) Тогда
		ДанныеОбъекта.Вставить("УчитыватьИсправлениеИсходногоДокумента", Истина);
		ДанныеОбъекта.Вставить("НомерИсправленияИсходногоДокумента", НомерИсправленияИсходногоДокумента);
		ДанныеОбъекта.Вставить("ДатаИсправленияИсходногоДокумента",  ДатаИсправленияИсходногоДокумента);
	Иначе
		ДанныеОбъекта.Вставить("УчитыватьИсправлениеИсходногоДокумента", Ложь);
	КонецЕсли;
	
	НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "СведенияОПокупателе");
	ДанныеОбъекта.Вставить("Контрагент",  КонтрагентПоДаннымЭД(ДеревоДанных, "СведенияОПродавце"));
	ДанныеОбъекта.Вставить("Комиссионер", Справочники.Контрагенты.ПустаяСсылка());
	
	// Если счет-фактура на аванс.
	ДанныеОбъекта.Вставить("ВидСчетаФактуры",Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
	ДанныеОбъекта.Вставить("КодВидаОперации", "01");
	
	//Заполняем документы-основания
	МассивДокументовОснований = Новый Массив();
	НайденнаяСтрока = ДеревоДанных.Строки.Найти("ДокументыОснованияСчетаФактуры", "ПолныйПуть");
	Если Не (НайденнаяСтрока = Неопределено Или НайденнаяСтрока.Значение = Неопределено) Тогда // нет данных о документе-основании
		МассивДокументовОснований = НайденнаяСтрока.Значение;
	КонецЕсли;
	ДанныеОбъекта.Вставить("ДокументыОснования", МассивДокументовОснований);
	
	КодВалюты = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод");
	ДанныеОбъекта.Вставить("ВалютаДокумента", НайтиСсылкуНаОбъект("Валюты", КодВалюты));
	
	ДанныеОбъекта.Вставить("СуммаДокумента",    0);
	ДанныеОбъекта.Вставить("СуммаНДСДокумента", 0);
	
	ДанныеОбъекта.Вставить("СуммаУменьшение",    ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровСНалогомУменьшение"));
	ДанныеОбъекта.Вставить("СуммаНДСУменьшение", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоИзмененияСтоимости.ВсегоСуммаНалогаУменьшение"));
	ДанныеОбъекта.Вставить("СуммаУвеличение",    ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровСНалогомУвеличение"));
	ДанныеОбъекта.Вставить("СуммаНДСУвеличение", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоИзмененияСтоимости.ВсегоСуммаНалогаУвеличение"));
	
	Возврат ДанныеОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляСчетаФактурыУПД(ДеревоДанных)
	
	ДанныеОбъекта = Новый Структура;
	ДанныеОбъекта.Вставить("НомерВходящегоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента"));
	ДанныеОбъекта.Вставить("ДатаВходящегоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента"));
	
	ДанныеОбъекта.Вставить("НомерИсходногоДокумента", "");
	ДанныеОбъекта.Вставить("ДатаИсходногоДокумента",  '00010101');
	
	НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "СведенияОПокупателе");
	ДанныеОбъекта.Вставить("Контрагент",  КонтрагентПоДаннымЭД(ДеревоДанных, "СведенияОПродавце"));
	ДанныеОбъекта.Вставить("Комиссионер", КонтрагентПоДаннымЭД(ДеревоДанных, "СведенияОКомиссионере"));
	
	// Если счет-фактура на аванс.
	ВидСчетаФактуры = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры");
	
	Если ВидСчетаФактуры = "Авансовый" Тогда
		ДанныеОбъекта.Вставить("ВидСчетаФактуры",Перечисления.ВидСчетаФактурыПолученного.НаАванс);
		КодВидаОперации = "02";
	Иначе
		ДанныеОбъекта.Вставить("ВидСчетаФактуры",Перечисления.ВидСчетаФактурыПолученного.НаПоступление);
		Если ЗначениеЗаполнено(ДанныеОбъекта.Комиссионер) Тогда
			КодВидаОперации = "04";
		Иначе
			КодВидаОперации = "01";
		КонецЕсли;
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("КодВидаОперации", КодВидаОперации);
	
	НомерИсправления = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления");
	ДатаИсправления  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления");
	
	Если ЗначениеЗаполнено(НомерИсправления) Тогда
		ДанныеОбъекта.Вставить("Исправление", Истина);
		ДанныеОбъекта.Вставить("НомерИсправления", НомерИсправления);
		ДанныеОбъекта.Вставить("ДатаИсправления",  ДатаИсправления);
	КонецЕсли;
	
	//Заполняем документы-основания
	НайденнаяСтрока = ДеревоДанных.Строки.Найти("ДокументыОснованияСчетаФактуры", "ПолныйПуть");
	Если НайденнаяСтрока <> Неопределено Тогда
		ТаблицаОснований = Новый ТаблицаЗначений;
		ТаблицаОснований.Колонки.Добавить("Основание");
		Если ТипЗнч(НайденнаяСтрока.Значение) = Тип("Массив") Тогда
			Для Каждого Элемент Из НайденнаяСтрока.Значение Цикл
				
				СтрокаТаблицы = ТаблицаОснований.Добавить();
				СтрокаТаблицы.Основание = Элемент;
				
			КонецЦикла;
			ТаблицаОснований.Свернуть("Основание");
		Иначе
			СтрокаТаблицы = ТаблицаОснований.Добавить();
			СтрокаТаблицы.Основание = НайденнаяСтрока.Значение;
		КонецЕсли;
	КонецЕсли;
	ДанныеОбъекта.Вставить("ДокументыОснования", ТаблицаОснований.ВыгрузитьКолонку("Основание"));
	
	КодВалюты = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод");
	ДанныеОбъекта.Вставить("ВалютаДокумента", НайтиСсылкуНаОбъект("Валюты", КодВалюты));
	
	ДанныеОбъекта.Вставить("СуммаДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом"));
	ДанныеОбъекта.Вставить("СуммаНДСДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоКОплате.ВсегоСуммаНалога"));
	
	ДанныеОбъекта.Вставить("СуммаУменьшение",    0);
	ДанныеОбъекта.Вставить("СуммаНДСУменьшение", 0);
	ДанныеОбъекта.Вставить("СуммаУвеличение",    0);
	ДанныеОбъекта.Вставить("СуммаНДСУвеличение", 0);
	
	// Авансы
	Если ДанныеОбъекта.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс Тогда
		
		СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
		ТаблицаАвансы = Новый ТаблицаЗначений;
		ТаблицаАвансы.Колонки.Добавить("Сумма");
		ТаблицаАвансы.Колонки.Добавить("СуммаНДС");
		ТаблицаАвансы.Колонки.Добавить("СтавкаНДС");
		Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
			СтрокаАвансы = ТаблицаАвансы.Добавить();
			СтавкаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
				СведенияОТоваре,
				"СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
			Если СтавкаНДС = СтавкаНДСИсчисляетсяНалоговымАгентом() Тогда
				СуммаБезНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
					СведенияОТоваре,
					"СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалога");
				СтрокаАвансы.СтавкаНДС = УчетНДСКлиентСервер.РасчетнаяСтавкаНДСПоУмолчанию(ДанныеОбъекта.ДатаВходящегоДокумента);
				СтрокаАвансы.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
					СуммаБезНДС, Ложь, УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаАвансы.СтавкаНДС));
				СтрокаАвансы.Сумма = СуммаБезНДС + СтрокаАвансы.СуммаНДС;
			Иначе
				СтрокаАвансы.СтавкаНДС = СтавкаНДС;
				СтрокаАвансы.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
					СведенияОТоваре,
					"СведенияОТоварах.НомерСтроки.СтоимостьТоваровСНалогом");
				СтрокаАвансы.СуммаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
					СведенияОТоваре,
					"СведенияОТоварах.НомерСтроки.СуммаНалога");
			КонецЕсли;
		КонецЦикла;
		Если ТаблицаАвансы.Количество() > 0 Тогда
			ДанныеОбъекта.Вставить("Авансы", ТаблицаАвансы);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДанныеОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляОтчетаКомиссионера(СтрокаДляЗагрузки, ДеревоРазбора)

	ДанныеДляОбъекта = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	ТаблицаТоваров = Документы.ОтчетКомиссионераОПродажах.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	ТаблицаТоваров.Колонки.Добавить("ДатаСФ");
	ТаблицаТоваров.Колонки.Добавить("Покупатель");
	ТаблицаУслуг = Документы.ОтчетКомиссионераОПродажах.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	ТаблицаУслуг.Колонки.Добавить("ДатаСФ");
	ТаблицаУслуг.Колонки.Добавить("Покупатель");	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		Если СтрокаРеквизита.Строки.Количество()=0 Тогда // примитивный тип
			Реквизит = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			Если ЗначениеЗаполнено(Реквизит) Тогда
				ДанныеЗаполненияШапки.Вставить(СтрокаРеквизита.Реквизит, Реквизит);
			КонецЕсли;
		ИначеЕсли СтрокаРеквизита.Реквизит = "СтрокаТЧ" Тогда // добавим строку ТЧ
			
			ЭтоУслуга = Ложь;
			Сопоставление = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, "Сопоставление", Истина, ДеревоРазбора);
			Если ТипЗнч(Сопоставление) = Тип("Структура")
				И Сопоставление.Свойство("НоменклатураИБ") Тогда
				
				Номенклатура = Сопоставление.НоменклатураИБ;
				ЭтоУслуга = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидНоменклатуры.Услуга");
				Если ЭтоУслуга Тогда
					
					НоваяСтрока = ТаблицаУслуг.Добавить();
					КолонкиТаблицы = ТаблицаУслуг.Колонки;
					
				Иначе
					
					НоваяСтрока = ТаблицаТоваров.Добавить();
					КолонкиТаблицы = ТаблицаТоваров.Колонки;
					
				КонецЕсли;
				
			Иначе
				
				НоваяСтрока = ТаблицаТоваров.Добавить();
				КолонкиТаблицы = ТаблицаУслуг.Колонки;
				
			КонецЕсли;
			ДанныеПокупателя = Новый Структура("НаименованиеПолное, ИНН, КПП, ЮрАдрес, ФактАдрес");	
			Для Каждого ТекСтрока Из СтрокаРеквизита.Строки Цикл
				
				ИмяРеквизитаВБД = ТекСтрока.Реквизит;				
				Если ИмяРеквизитаВБД = "ПокупательНаименование" Тогда	
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					ДанныеПокупателя.НаименованиеПолное = НайденноеЗначение;
					
				ИначеЕсли ИмяРеквизитаВБД = "ПокупательИНН" Тогда	
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					ДанныеПокупателя.ИНН = НайденноеЗначение;
					
				ИначеЕсли ИмяРеквизитаВБД = "ПокупательКПП" Тогда	
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					ДанныеПокупателя.КПП = НайденноеЗначение;
					
				ИначеЕсли ИмяРеквизитаВБД = "ПокупательЮрАдрес" Тогда	
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					ДанныеПокупателя.ЮрАдрес = НайденноеЗначение;
					
				ИначеЕсли ИмяРеквизитаВБД = "ПокупательФактАдрес" Тогда	
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					ДанныеПокупателя.ФактАдрес = НайденноеЗначение;
					
				ИначеЕсли ИмяРеквизитаВБД = "Цена" И Не ЭтоУслуга Тогда
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					НоваяСтрока["ЦенаПередачи"] = НайденноеЗначение;
					
				ИначеЕсли ИмяРеквизитаВБД = "Сумма" И Не ЭтоУслуга Тогда
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					НоваяСтрока["СуммаПередачи"] = НайденноеЗначение;
					
				ИначеЕсли ИмяРеквизитаВБД = "ЦенаПродажи" Тогда
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					НоваяСтрока["Цена"] = НайденноеЗначение;
					
				ИначеЕсли ИмяРеквизитаВБД = "СуммаПродажи" Тогда				
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					НоваяСтрока["Сумма"] = НайденноеЗначение;					
					
				ИначеЕсли ИмяРеквизитаВБД = "ДатаРеализации" Тогда				
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					ДатаРеализации = СтрЗаменить(НайденноеЗначение, "-", "");
					ДатаРеализации = СтрЗаменить(ДатаРеализации, "T", "");
					ДатаРеализации = СтрЗаменить(ДатаРеализации, ":", "");
					НоваяСтрока["ДатаСФ"] = ?(ЗначениеЗаполнено(ДатаРеализации), Дата(ДатаРеализации), Неопределено);
					СтроковыеФункцииКлиентСервер.СтрокаВДату(НайденноеЗначение);
				ИначеЕсли ИмяРеквизитаВБД = "Номенклатура" Тогда	
					
					НоваяСтрока["Номенклатура"] = Номенклатура;
					Если ЭтоУслуга Тогда
						НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина);
						Если НайденноеЗначение <> Неопределено Тогда
							ОписаниеОбъекта = ДеревоРазбора.Строки.Найти(НайденноеЗначение, "ИндексСтроки", Истина).ОписаниеОбъекта;	
							НоваяСтрока["Содержание"] = ОписаниеОбъекта;
						КонецЕсли;
					КонецЕсли;
					
				ИначеЕсли КолонкиТаблицы.Найти(ИмяРеквизитаВБД) <> Неопределено Тогда
					
					НайденноеЗначение = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ТекСтрока, ТекСтрока.Реквизит, Истина, ДеревоРазбора);
					НоваяСтрока[ИмяРеквизитаВБД] = НайденноеЗначение;
					
				КонецЕсли;
				
			КонецЦикла;
			Покупатель = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ДанныеПокупателя.ИНН, ДанныеПокупателя.КПП);
			Если НЕ ЗначениеЗаполнено(Покупатель) Тогда
				
				ИндексСтроки = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(СтрокаРеквизита, "Покупатель");
				// В электронном документе в строке может быть не указаны данные контрагента,
				// поэтому заполним покупателя только если они есть. Если нет - пользователю нужно будет заполнить контрагента вручную.
				Если ИндексСтроки <> Неопределено Тогда
					СтрокаКонтрагента = ДеревоРазбора.Строки.Найти(ИндексСтроки, "ИндексСтроки", Истина);
					Если СтрокаКонтрагента <> Неопределено Тогда
						
						Покупатель = ЭлектронноеВзаимодействиеБП.НовыйКонтрагент(СтрокаКонтрагента, ДеревоРазбора, Неопределено);
						
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			НоваяСтрока.Покупатель = Покупатель;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента",ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Номер"));
	ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента", ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "Дата"));
	ДанныеЗаполненияШапки.Вставить("СуммаВключаетНДС",ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "ЦенаВключаетНДС"));
	ДанныеЗаполненияШапки.Вставить("СтавкаНДСВознаграждения", ПолучитьСтавкуНДСПеречислением(ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(СтрокаДляЗагрузки, "СтавкаНДСВознаграждения")));
	
	НомерСтрокиДокумента = 0;
	Для Каждого ТекСтрока Из ТаблицаТоваров Цикл 
		// заполним Количество с учетом единиц измерения
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Количество) Тогда
			Если ЗначениеЗаполнено(ТекСтрока.Упаковка) Тогда
				ТекКоэффициент = ТекСтрока.Упаковка.Коэффициент;
			Иначе
				ТекКоэффициент = 1;
			КонецЕсли;
			ТекСтрока.Количество = ТекСтрока.КоличествоУпаковок*ТекКоэффициент;
		КонецЕсли;
		
	КонецЦикла;
	
	// получение данных о покупателях
	ТаблицаПокупателей = Документы.ОтчетКомиссионераОПродажах.ПустаяСсылка().Покупатели.ВыгрузитьКолонки();		
	Для Каждого СтрокаТЗ Из ТаблицаТоваров Цикл
		
		СтрокаПокупателя = ТаблицаПокупателей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПокупателя, СтрокаТЗ);
			
	КонецЦикла;
	Для Каждого СтрокаТЗ Из ТаблицаУслуг Цикл
		
		СтрокаПокупателя = ТаблицаПокупателей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПокупателя, СтрокаТЗ);
			
	КонецЦикла;
	
	// установка ключа строки	
	ТаблицаПокупателей.Свернуть("Покупатель, ДатаСФ", "ВыставленСФ, КлючСтроки");
	Счетчик = 0;
	Для Каждого СтрокаПокупателя Из ТаблицаПокупателей Цикл
		
		Счетчик = Счетчик + 1;
		СтрокаПокупателя.КлючСтроки = Счетчик;
		
	КонецЦикла;
	Для Каждого СтрокаТЗ Из ТаблицаТоваров Цикл
		
		СтруктураПоиска = Новый Структура("Покупатель, ДатаСФ", СтрокаТЗ.Покупатель, СтрокаТЗ.ДатаСФ);
		НайденныеСтроки = ТаблицаПокупателей.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			СтрокаТЗ.КлючСтроки = НайденныеСтроки[0].КлючСтроки;
			
		КонецЕсли;
		
	КонецЦикла;
	Для Каждого СтрокаТЗ Из ТаблицаУслуг Цикл
		
		СтруктураПоиска = Новый Структура("Покупатель, ДатаСФ", СтрокаТЗ.Покупатель, СтрокаТЗ.ДатаСФ);
		НайденныеСтроки = ТаблицаПокупателей.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			СтрокаТЗ.КлючСтроки = НайденныеСтроки[0].КлючСтроки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// определяем, нужно ли перевыставлять счета-фактуры
	ПлательщикНДС = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ДеревоРазбора, "ПлательщикНДС", Истина);
	Если ЗначениеЗаполнено(ПлательщикНДС) И Булево(ПлательщикНДС) Тогда
		
		ТаблицаПокупателей.ЗаполнитьЗначения(Истина, "ВыставленСФ");
		
	КонецЕсли;
	
	// Заполняем табличную часть "Денежные средства"
	ТаблицаДС = Новый ТаблицаЗначений;
	ТаблицаДС.Колонки.Добавить("ВидОтчетаПоПлатежам");
	ТаблицаДС.Колонки.Добавить("Сумма");
	ТаблицаДС.Колонки.Добавить("СтавкаНДС");
	ТаблицаДС.Колонки.Добавить("СуммаНДС");
	ТаблицаДС.Колонки.Добавить("Покупатель");
	ТаблицаДС.Колонки.Добавить("ДатаСобытия");
	ДанныеДССтрокой = ЭлектронноеВзаимодействиеБП.ПолучитьЗначениеРеквизитаДерева(ДеревоРазбора, "ДенежныеСредства", Истина);
	Если ЗначениеЗаполнено(ДанныеДССтрокой) Тогда
		
		ДенежныеСредстваДанные = ОбщегоНазначения.ЗначениеИзСтрокиXML(ДанныеДССтрокой);
		СоответствиеСтавокНДС = Новый Соответствие;
		ЗаполнитьСоответствиеСтавокНДС(СоответствиеСтавокНДС);
		Для Каждого СтрокаДанных Из ДенежныеСредстваДанные Цикл
			
			СтрокаДС = ТаблицаДС.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДС, СтрокаДанных,, "ВидОтчетаПоПлатежам, СтавкаНДС, Покупатель, Сумма");
			
			// Необходимо просуммировать, т.к. в отчете комитенту поле Сумма содержит сумму без НДС,
			// а в отчете комиссионера поле Сумма содержит сумму с НДС.
			СтрокаДС.Сумма = СтрокаДанных.Сумма + СтрокаДанных.СуммаНДС;
			Если СтрокаДанных.ВидОтчетаПоПлатежам = "Аванс" Тогда
				СтрокаДС.ВидОтчетаПоПлатежам = Перечисления.ВидыОтчетовПоПлатежамКомиссия.Аванс;
			ИначеЕсли СтрокаДанных.ВидОтчетаПоПлатежам = "Оплата" Тогда
				СтрокаДС.ВидОтчетаПоПлатежам = Перечисления.ВидыОтчетовПоПлатежамКомиссия.Оплата;
			ИначеЕсли СтрокаДанных.ВидОтчетаПоПлатежам = "ЗачетАванса" Тогда
				СтрокаДС.ВидОтчетаПоПлатежам = Перечисления.ВидыОтчетовПоПлатежамКомиссия.ЗачетАванса;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаДанных.СтавкаНДС) Тогда
				СтрокаДС.СтавкаНДС = СоответствиеСтавокНДС[СтрокаДанных.СтавкаНДС];
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаДанных.Покупатель) Тогда
				
				СтрокаДС.Покупатель = ПолучитьСоздатьКонтрагента(СтрокаДанных.Покупатель);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Если по ИНН и КПП организация не найдена, то возможно:
	//  - у организации изменился КПП (нужно проверить историю изменений КПП)
	//  - организация является обособленным подразделением в справочнике "Подразделения организаций"
	//    (необходимо найти головную организацию и подразделение, и подставить в документ учета).
	Если Не (ДанныеЗаполненияШапки.Свойство("Организация")
		И ЗначениеЗаполнено(ДанныеЗаполненияШапки.Организация)) Тогда
		
		ДанныеОрганизации = ДеревоРазбора.Строки.Найти("Организации", "ТипОбъекта", Истина);
		ИННОрганизации = ПолучитьЗначениеРеквизита(ДанныеОрганизации, "ИНН", Истина);
		КППОрганизации = ПолучитьЗначениеРеквизита(ДанныеОрганизации, "КПП", Истина);
		НайтиОрганизациюПоДаннымЭДCML(ДанныеЗаполненияШапки, ИННОрганизации, КППОрганизации);
	КонецЕсли;
	
	ДанныеДляОбъекта.Вставить("Шапка",  ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", ТаблицаТоваров);
	ДанныеДляОбъекта.Вставить("Услуги", ТаблицаУслуг);
	ДанныеДляОбъекта.Вставить("Покупатели", ТаблицаПокупателей);
	ДанныеДляОбъекта.Вставить("ДенежныеСредства", ТаблицаДС);
	
	Возврат ДанныеДляОбъекта;

КонецФункции

Функция ПодготовитьСтруктуруДляПоступленияТоваровУслугПоПередачеТоваров(ДеревоДанных)
	
	ДанныеОбъекта = Новый Структура;
	
	ЭтоИсправление = ЗначениеЗаполнено(
		ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления"));
	
	Товары         = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги         = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	ВозвратнаяТара = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().ВозвратнаяТара.ВыгрузитьКолонки();
	
	Валюта = НайтиСсылкуНаОбъект("Валюты", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод"));
	ДанныеОбъекта.Вставить("ВалютаДокумента", Валюта);
	
	КурсВзаиморасчетов = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
				ДеревоДанных, "ДопДанные.ДопДанныеПодписанные.КурсВзаиморасчетов", Ложь);
	ДанныеОбъекта.Вставить("КурсВзаиморасчетов", ?(ЗначениеЗаполнено(КурсВзаиморасчетов), КурсВзаиморасчетов, 1));
	ДанныеОбъекта.Вставить("КратностьВзаиморасчетов", 1);
	
	ДанныеОбъекта.Вставить("ВидОперацииЭД", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации"));
	
	ДанныеОбъекта.Вставить("СуммаВключаетНДС", Ложь);
	ДанныеОбъекта.Вставить("Корректировка",    Ложь);
	
	ДокументыОснования = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования");
	Если ЗначениеЗаполнено(ДокументыОснования) Тогда
		
		Если ТипЗнч(ДокументыОснования) = Тип("Массив") И ДокументыОснования.Количество() > 0 Тогда
			ДокументОснование = ДокументыОснования[0];
		Иначе
			ДокументОснование = ДокументыОснования;
		КонецЕсли;
		
		Если ЭтоИсправление Тогда
			ДанныеОбъекта.Вставить("Основание", ДокументОснование);
		Иначе
			ДанныеОбъекта.Вставить("СчетНаОплатуПоставщика", ДокументОснование);
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("НомерВходящегоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерТоварнойНакладной"));
	ДанныеОбъекта.Вставить("ДатаВходящегоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаТоварнойНакладной"));
	
	Если ЭтоИсправление Тогда
		ДанныеОбъекта.Вставить("Исправление", Истина);
		ДанныеОбъекта.Вставить("НомерИсправления", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления"));
		ДанныеОбъекта.Вставить("ДатаИсправления",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления"));
	Иначе
		ДанныеОбъекта.Вставить("Исправление", Ложь);
	КонецЕсли;
	
	НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "Плательщик");
	ДанныеОбъекта.Вставить("Контрагент",  КонтрагентПоДаннымЭД(ДеревоДанных, "Поставщик"));
	
	ПередачаТовараКомитентом = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
		ДеревоДанных, "ДопДанные.Подписанные.ПередачаТовараКомитентом", Ложь);
	Если ЗначениеЗаполнено(ПередачаТовараКомитентом) Тогда
		ДанныеОбъекта.Вставить("ПередачаТовараКомитентом", Булево(ПередачаТовараКомитентом));
	Иначе
		ДанныеОбъекта.Вставить("ПередачаТовараКомитентом", Ложь);
	КонецЕсли;
	
	ИспользуетсяОбратноеНачислениеНДС = БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
	НДСИсчисляетсяНалоговымАгентом = Ложь;
	СведенияОТоварах = ДеревоДанных.Строки.Найти("ТаблицаТоваров", "ПолныйПуть");
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		
		НаименованиеНоменклатуры  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
				"ТаблицаТоваров.НомерСтроки.НаименованиеНоменклатуры");
		Номенклатура = НоменклатураИБ(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.Сопоставление");
		
		ВидТаблицыДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.ДопДанныеПодписанные.ТаблицаДокумента", Ложь);
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидНоменклатуры.Услуга") Тогда
				ВидТаблицыДокумента = "Услуги";
			ИначеЕсли ВидТаблицыДокумента <> "ВозвратнаяТара" Тогда
				ВидТаблицыДокумента = "Товары";
			КонецЕсли;
		КонецЕсли;
			
		Если ВидТаблицыДокумента = "Товары" Тогда
			НоваяСтрока = Товары.Добавить();
		ИначеЕсли ВидТаблицыДокумента = "Услуги" Тогда
			НоваяСтрока = Услуги.Добавить();
		ИначеЕсли ВидТаблицыДокумента = "ВозвратнаяТара" Тогда
			НоваяСтрока = ВозвратнаяТара.Добавить();
		Иначе
			ВидТаблицыДокумента = "Товары";
			НоваяСтрока = Товары.Добавить();
		КонецЕсли;
		НоваяСтрока.Номенклатура = Номенклатура;
		
		Если ВидТаблицыДокумента = "Товары" Тогда
			НоваяСтрока.ЕдиницаИзмерения = НайтиСсылкуНаОбъект("ЕдиницыИзмерения",
				ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.БазоваяЕдиницаКод"));
		КонецЕсли;
		
		Если ВидТаблицыДокумента = "Услуги" Тогда
			НоваяСтрока.Содержание = НаименованиеНоменклатуры;
		КонецЕсли;
		
		Если ВидТаблицыДокумента <> "ВозвратнаяТара" Тогда
			// Обязательные реквизиты:
			СтавкаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.СтавкаНДС");
			Если СтавкаНДС = СтавкаНДСИсчисляетсяНалоговымАгентом() Тогда
				
				НДСИсчисляетсяНалоговымАгентом = Истина;
				Если ИспользуетсяОбратноеНачислениеНДС Тогда
					
					НоваяСтрока.СтавкаНДС = УчетНДСКлиентСервер.ОбщаяСтавкаНДС(
						ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаТоварнойНакладной"));
					// Вычисляем НДС сверху по общей ставке
					НоваяСтрока.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
						ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.СуммаБезНДС"),
						Ложь,
						УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
						
				Иначе
					НоваяСтрока.СтавкаНДС = Неопределено;
					НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.СуммаНДС");
				КонецЕсли;
				
			Иначе
				НоваяСтрока.СтавкаНДС = СтавкаНДС;
				НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.СуммаНДС");
			КонецЕсли;
		КонецЕсли;
		
		// Необязательные реквизиты:
		МассаНетто = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.МассаНетто");
		КоличествоМест = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.КоличествоМест");
		НоваяСтрока.Количество = ?(ЗначениеЗаполнено(МассаНетто), МассаНетто, КоличествоМест);
		НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.Цена");
		НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.СуммаБезНДС");
		
		Если ВидТаблицыДокумента = "Товары" Тогда
			НомерТД = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
				СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.ДопДанныеПодписанные.НомерТД", Ложь);
			КодСтраныПроисхождения = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
				СведенияОТоваре, "ТаблицаТоваров.НомерСтроки.ДопДанныеПодписанные.КодСтраныПроисхождения", Ложь);
			ДанныеГТД = Новый Структура;
			Если ЗначениеЗаполнено(НомерТД) Тогда
				ДанныеГТД.Вставить("НомерТД", НомерТД);
			КонецЕсли;
			Если ЗначениеЗаполнено(КодСтраныПроисхождения) Тогда
				ДанныеГТД.Вставить("КодСтраныПроисхождения", КодСтраныПроисхождения);
			КонецЕсли;
			ЗаполнитьНомерТДИСтрануПроисхождения(НоваяСтрока, ДанныеГТД);
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеДляЗаполнения = Новый Структура();
	ДанныеДляЗаполнения.Вставить("Шапка", ДанныеОбъекта);
	ДанныеДляЗаполнения.Вставить("Товары", Товары);
	ДанныеДляЗаполнения.Вставить("Услуги", Услуги);
	ДанныеДляЗаполнения.Вставить("ВозвратнаяТара", ВозвратнаяТара);
	ДанныеДляЗаполнения.Вставить("НДСИсчисляетсяНалоговымАгентом", НДСИсчисляетсяНалоговымАгентом);
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

Функция ПодготовитьСтруктуруДляКорректировкиПоступления(СтрокаДляЗагрузки, ДеревоРазбора)
	
	ДанныеДляОбъекта      = Новый Структура;
	ДанныеЗаполненияШапки = Новый Структура;
	
	Товары = Документы.КорректировкаПоступления.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги = Документы.КорректировкаПоступления.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Для Каждого СтрокаРеквизита Из СтрокаДляЗагрузки.Строки Цикл
		
		Если СтрокаРеквизита.Реквизит = "СписокОписаний" Тогда
			 
			СтрокаРеквизитаОписанийРабот = ПолучитьЗначениеРеквизита(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
			
			Для Каждого СтрокаРеквизитаОписания Из СтрокаРеквизитаОписанийРабот.Строки Цикл
				
				Если СтрокаРеквизитаОписания.Строки.Количество() = 0 Тогда
					ЗначениеРеквизита = ПолучитьЗначениеРеквизита(СтрокаРеквизитаОписания, СтрокаРеквизитаОписания.Реквизит, Истина, ДеревоРазбора);
					Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
						ДанныеЗаполненияШапки.Вставить(СтрокаРеквизитаОписания.Реквизит, ЗначениеРеквизита);
					КонецЕсли;
				Иначе
					ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизитаОписания.Строки, ДеревоРазбора);
					ЗаполнитьЗначенияСвойств(Услуги.Добавить(), ДанныеДляЗаполненияСтрокиТЧ);
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Если СтрокаРеквизита.Строки.Количество() = 0 Тогда 
				
				// Заполним реквизит шапки
				ЗначениеРеквизита = ПолучитьЗначениеРеквизита(СтрокаРеквизита, СтрокаРеквизита.Реквизит, Истина, ДеревоРазбора);
				
				Если СтрокаРеквизита.Реквизит = "ДатаПоДаннымКлиента" Тогда
					ИмяРеквизита = "ДатаПоДаннымПокупателя";
				ИначеЕсли СтрокаРеквизита.Реквизит = "НомерПоДаннымКлиента" Тогда
					ИмяРеквизита = "НомерПоДаннымПокупателя";
				ИначеЕсли СтрокаРеквизита.Реквизит = "Валюта" Тогда
					ИмяРеквизита = "ВалютаДокумента";
				ИначеЕсли СтрокаРеквизита.Реквизит = "ДокументОснования" Тогда
					ИмяРеквизита = "ДокументПоступления";
				Иначе
					ИмяРеквизита = СтрокаРеквизита.Реквизит;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
					ДанныеЗаполненияШапки.Вставить(ИмяРеквизита, ЗначениеРеквизита);
				КонецЕсли;
				
			Иначе 
				
				// Добавим строку табличной части
				ДанныеДляЗаполненияСтрокиТЧ = ПолучитьДанныеСтрокиТЧ(СтрокаРеквизита.Строки, ДеревоРазбора);
				
				Если ДанныеДляЗаполненияСтрокиТЧ.Свойство("Услуга")
					И ДанныеДляЗаполненияСтрокиТЧ.Услуга = Истина Тогда
					ЗаполнитьЗначенияСвойств(Услуги.Добавить(), ДанныеДляЗаполненияСтрокиТЧ);
					
				Иначе
					СтрокаТаблицы = Товары.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеДляЗаполненияСтрокиТЧ);
					ЗаполнитьНомерТДИСтрануПроисхождения(СтрокаТаблицы, ДанныеДляЗаполненияСтрокиТЧ, Истина);
					Если Не ЗначениеЗаполнено(ДанныеДляЗаполненияСтрокиТЧ.Количество)
							И ЗначениеЗаполнено(ДанныеДляЗаполненияСтрокиТЧ.МассаНетто)
							И ДанныеДляЗаполненияСтрокиТЧ.МассаНетто > 0 Тогда
							
							СтрокаТаблицы.Количество = ДанныеДляЗаполненияСтрокиТЧ.МассаНетто;
							
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеЗаполненияШапки.Вставить("ВидОперацииЭД", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ВидОперации", Истина, ДеревоРазбора));
	
	// спец. значения 
	Если ЗначениеЗаполнено(ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерСчетаФактуры")) Тогда // указана счет-фактура
		ДанныеЗаполненияШапки.Вставить("НомерСчетаФактуры",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерСчетаФактуры"));
		ДанныеЗаполненияШапки.Вставить("ДатаСчетаФактуры", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ДатаСчетаФактуры"));
	КонецЕсли;
	
	Если ДанныеЗаполненияШапки.ВидОперации = Перечисления.ВидыОперацийЭД.Исправление Тогда
		ДанныеЗаполненияШапки.Вставить("НомерИсправления",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерИсправления"));
		ДанныеЗаполненияШапки.Вставить("ДатаИсправления", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ДатаИсправления"));
	КонецЕсли;
	
	ДанныеЗаполненияШапки.Вставить("НомерИсходногоДокумента",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерИсходногоДокумента"));
	ДанныеЗаполненияШапки.Вставить("ДатаИсходногоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ДатаИсходногоДокумента"));
	Если ЗначениеЗаполнено(ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерИсправленияИсходногоДокумента")) Тогда
		ДанныеЗаполненияШапки.Вставить("УчитыватьИсправлениеИсходногоДокумента", Истина);
		ДанныеЗаполненияШапки.Вставить("НомерИсправленияИсходногоДокумента",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "НомерИсправленияИсходногоДокумента"));
		ДанныеЗаполненияШапки.Вставить("ДатаИсправленияИсходногоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "ДатаИсправленияИсходногоДокумента"));
	Иначе
		ДанныеЗаполненияШапки.Вставить("УчитыватьИсправлениеИсходногоДокумента", Ложь);
	КонецЕсли;
	
	ДанныеЗаполненияШапки.Вставить("НомерВходящегоДокумента",ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Номер"));
	ДанныеЗаполненияШапки.Вставить("ДатаВходящегоДокумента", ПолучитьЗначениеРеквизита(СтрокаДляЗагрузки, "Дата"));
	
	ДанныеДляОбъекта.Вставить("Шапка",  ДанныеЗаполненияШапки);
	ДанныеДляОбъекта.Вставить("Товары", Товары);
	ДанныеДляОбъекта.Вставить("Услуги", Услуги);
	
	Возврат ДанныеДляОбъекта;
	
КонецФункции

Функция ПодготовитьСтруктуруДляПоступленияТоваровУслугПоПередачаРабот(ДеревоДанных)
	
	ДанныеОбъекта = Новый Структура;
	
	ЭтоИсправление = ЗначениеЗаполнено(
		ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления"));
		
	Услуги = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	Товары = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	ВозвратнаяТара = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().ВозвратнаяТара.ВыгрузитьКолонки();
	
	Валюта = НайтиСсылкуНаОбъект("Валюты", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод"));
	ДанныеОбъекта.Вставить("ВалютаДокумента", Валюта);
	
	КурсВзаиморасчетов = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
				ДеревоДанных, "ДопДанные.ДопДанныеПодписанные.КурсВзаиморасчетов", Ложь);
	ДанныеОбъекта.Вставить("КурсВзаиморасчетов", ?(ЗначениеЗаполнено(КурсВзаиморасчетов), КурсВзаиморасчетов, 1));
	ДанныеОбъекта.Вставить("КратностьВзаиморасчетов", 1);
	
	ДанныеОбъекта.Вставить("ВидОперацииЭД", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации"));
	
	ДанныеОбъекта.Вставить("СуммаВключаетНДС", Ложь);
	ДанныеОбъекта.Вставить("Корректировка",    Ложь);
	
	ДокументыОснования = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования");
	Если ЗначениеЗаполнено(ДокументыОснования) Тогда
		
		Если ТипЗнч(ДокументыОснования) = Тип("Массив") И ДокументыОснования.Количество() > 0 Тогда
			ДокументОснование = ДокументыОснования[0];
		Иначе
			ДокументОснование = ДокументыОснования;
		КонецЕсли;
		
		Если ЭтоИсправление Тогда
			ДанныеОбъекта.Вставить("Основание", ДокументОснование);
		Иначе
			ДанныеОбъекта.Вставить("СчетНаОплатуПоставщика", ДокументОснование);
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("НомерВходящегоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерАкта"));
	ДанныеОбъекта.Вставить("ДатаВходящегоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаАкта"));
	
	Если ЭтоИсправление Тогда
		ДанныеОбъекта.Вставить("Исправление", Истина);
		ДанныеОбъекта.Вставить("НомерИсправления", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления"));
		ДанныеОбъекта.Вставить("ДатаИсправления",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления"));
	Иначе
		ДанныеОбъекта.Вставить("Исправление", Ложь);
	КонецЕсли;
	
	НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "Исполнитель");
	ДанныеОбъекта.Вставить("Контрагент",  КонтрагентПоДаннымЭД(ДеревоДанных, "Заказчик"));
	
	ДанныеОбъекта.Вставить("ПередачаТовараКомитентом", Ложь);
	
	ДокументОснование = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснования");
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		
		ДанныеОбъекта.Вставить("СчетНаОплатуПоставщика", ДокументОснование);
		
	КонецЕсли;
	
	СведенияОТоварах = ДеревоДанных.Строки.Найти("ТаблицаУслуг", "ПолныйПуть");
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		
		НоваяСтрока = Услуги.Добавить();
		
		// Обязательные реквизиты:
		НоваяСтрока.СтавкаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаУслуг.НомерСтроки.СтавкаНДС");
		НоваяСтрока.СуммаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,  "ТаблицаУслуг.НомерСтроки.СуммаНДС");
		
		// Необязательные реквизиты:
		НоваяСтрока.Количество = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаУслуг.НомерСтроки.Количество");
		НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаУслуг.НомерСтроки.Цена");
		НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаУслуг.НомерСтроки.СуммаБезНДС");
		НоваяСтрока.Содержание = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "ТаблицаУслуг.НомерСтроки.Описание");

		НоваяСтрока.Номенклатура = НоменклатураИБ(СведенияОТоваре, "ТаблицаУслуг.НомерСтроки.Сопоставление");
		
	КонецЦикла;
	
	ДанныеДляЗаполнения = Новый Структура();
	ДанныеДляЗаполнения.Вставить("Шапка", ДанныеОбъекта);
	ДанныеДляЗаполнения.Вставить("Услуги", Услуги);
	ДанныеДляЗаполнения.Вставить("Товары", Товары);
	ДанныеДляЗаполнения.Вставить("ВозвратнаяТара", ВозвратнаяТара);
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

Функция ПодготовитьСтруктуруДляВозвратаТоваровПоставщикуУКД(ДеревоДанных)
	
	ДанныеОбъекта = Новый Структура;
	
	ДанныеОбъекта.Вставить("ВидОперации", Перечисления.ВидыОперацийВозвратТоваровПоставщику.ПокупкаКомиссия);
	Товары = Документы.КорректировкаПоступления.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	
	Валюта = НайтиСсылкуНаОбъект("Валюты", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод"));
	ДанныеОбъекта.Вставить("ВалютаДокумента", Валюта);
	КурсВзаиморасчетов = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ВалютаКурс");
	ДанныеОбъекта.Вставить("КурсВзаиморасчетов", ?(ЗначениеЗаполнено(КурсВзаиморасчетов), КурсВзаиморасчетов, 1));
	ДанныеОбъекта.Вставить("КратностьВзаиморасчетов", 1);
	
	ДанныеОбъекта.Вставить("СуммаВключаетНДС", Ложь);
	
	ДокументОснование = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
	Если ТипЗнч(ДокументОснование) = Тип("Массив") И ДокументОснование.Количество() > 0 Тогда
		ДокументОснование = ДокументОснование[0];
	КонецЕсли;
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ДокументОснование");
	КонецЕсли;
	ДанныеОбъекта.Вставить("Основание", ДокументОснование);
	
	НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "СведенияОПокупателе");
	ДанныеОбъекта.Вставить("Контрагент",  КонтрагентПоДаннымЭД(ДеревоДанных, "СведенияОПродавце"));
	
	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		
		Номенклатура = НоменклатураИБ(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Сопоставление");
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Номенклатура = Номенклатура;
		
		НоваяСтрока.СтавкаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
		НоваяСтрока.СуммаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалогаУменьшение");
		
		КоличествоДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.КоличествоДоКорректировки");
		КоличествоПослеКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Количество");
		НоваяСтрока.Количество = КоличествоДоКорректировки - КоличествоПослеКорректировки;
		
		НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЦенаЗаЕдиницуИзмерения");
		НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалогаУменьшение");
		
	КонецЦикла;
	ДанныеДляЗаполнения = Новый Структура();
	ДанныеДляЗаполнения.Вставить("Шапка" , ДанныеОбъекта);
	ДанныеДляЗаполнения.Вставить("Товары", Товары);
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

Функция ПодготовитьСтруктуруДляПоступленияДопРасходовУПД_2019(ДеревоДанных)
	
	ДанныеЭД = ДанныеВходящегоЭД(ДеревоДанных);
	
	Шапка = Новый Структура;
	
	Шапка.Вставить("НомерВходящегоДокумента", ДанныеЭД.НомерДокумента);
	Шапка.Вставить("ДатаВходящегоДокумента" , ДанныеЭД.ДатаДокумента);
	
	Валюта = НайтиСсылкуНаОбъект("Валюты", ДанныеЭД.ВалютаКод);
	Шапка.Вставить("ВалютаДокумента", Валюта);
	
	КурсВзаиморасчетов = ДанныеЭД.ДополнительныеСведенияОбУчастниках.ВалютаКурс;
	Шапка.Вставить("КурсВзаиморасчетов"     ,?(ЗначениеЗаполнено(КурсВзаиморасчетов), КурсВзаиморасчетов, 1));
	Шапка.Вставить("КратностьВзаиморасчетов", 1);
	Шапка.Вставить("СуммаВключаетНДС"       , Ложь);
	Шапка.Вставить("ВидОперацииЭД"          , ДанныеЭД.ВидОперации);
	
	Если ЗначениеЗаполнено(ДанныеЭД.НомерИсправления) Тогда
		Шапка.Вставить("Исправление"     , Истина);
		Шапка.Вставить("НомерИсправления", ДанныеЭД.НомерИсправления);
		Шапка.Вставить("ДатаИсправления" , ДанныеЭД.ДатаИсправления);
		
		// Вид операции ЭД может быть не указан, т.к. реквизит не является обязательным.
		Если Не ЗначениеЗаполнено(Шапка.ВидОперацииЭД) Тогда
			Шапка.ВидОперацииЭД = Перечисления.ВидыОперацийЭД.Исправление;
		КонецЕсли;
	Иначе
		Шапка.Вставить("Исправление", Ложь);
	КонецЕсли;
	
	ОпределитьУчастниковПоДаннымЭД_2019(ДеревоДанных, Шапка);
	
	ОснованиеСчетаФактуры = Неопределено;
	Если ТипЗнч(ДанныеЭД.ДокументыОснованияСчетаФактуры) = Тип("Массив") Тогда
		Для Каждого ДокументОснование Из ДанныеЭД.ДокументыОснованияСчетаФактуры Цикл
			Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				ОснованиеСчетаФактуры = ДокументОснование;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(ДанныеЭД.ДокументыОснованияСчетаФактуры) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ОснованиеСчетаФактуры = ДанныеЭД.ДокументыОснованияСчетаФактуры;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОснованиеСчетаФактуры) Тогда
		ДокументОснованиеСчетаФактуры = ДокументОснованиеСчетаФактурыПолученного(ОснованиеСчетаФактуры);
		Если ЗначениеЗаполнено(ДокументОснованиеСчетаФактуры) Тогда
			Шапка.Вставить("Основание", ДокументОснованиеСчетаФактуры);
		КонецЕсли;
	ИначеЕсли Шапка.Исправление Тогда
		ДокументПоступления = Неопределено;
		Если ТипЗнч(ДанныеЭД.ДокументыОснованияДокументаОтгрузки) = Тип("Массив") Тогда
			Для Каждого ДокументОснование Из ДанныеЭД.ДокументыОснованияДокументаОтгрузки Цикл
				Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
					ДокументПоступления = ДокументОснование;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Если ТипЗнч(ДанныеЭД.ДокументыОснованияДокументаОтгрузки) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
					ДокументПоступления = ДанныеЭД.ДокументыОснованияДокументаОтгрузки;
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(ДокументПоступления) Тогда
			Шапка.Вставить("Основание", ДокументПоступления);
		КонецЕсли;
	КонецЕсли;
	
	МассивНаименованийУслуг = Новый Массив;
	ПерваяИтерация          = Истина;
	СтавкаНДС               = Неопределено;
	Для Каждого СведенияОТоваре Из ДанныеЭД.СведенияОТоварах Цикл
		МассивНаименованийУслуг.Добавить(СведенияОТоваре.ТоварНаименование);
		Если ПерваяИтерация Тогда
			СтавкаНДС = СведенияОТоваре.НалоговаяСтавка;
			ПерваяИтерация = Ложь;
		КонецЕсли;
		
		// Если строки таблицы документа имеют разные ставки НДС, то ставку НДС в документе "Поступление доп. расходов" не заполняем.
		Если СтавкаНДС <> СведенияОТоваре.НалоговаяСтавка Тогда
			СтавкаНДС = Неопределено;
		КонецЕсли;
	КонецЦикла;
	Шапка.Вставить("Содержание"     , СтрСоединить(МассивНаименованийУслуг, ";" + " "));
	Шапка.Вставить("СтавкаНДС"      , СтавкаНДС);
	Шапка.Вставить("СуммаНДС"       , ДанныеЭД.СведенияОТоварах.Итог("СуммаНалога"));
	Шапка.Вставить("Сумма"          , ДанныеЭД.СведенияОТоварах.Итог("СтоимостьТоваровБезНалога"));
	Шапка.Вставить("СуммаДокумента" , ДанныеЭД.СведенияОТоварах.Итог("СтоимостьТоваровСНалогом"));
	
	ДаныеДляЗаполнения = Новый Структура();
	ДаныеДляЗаполнения.Вставить("Шапка", Шапка);
	
	Возврат ДаныеДляЗаполнения;
	
КонецФункции

Функция ПодготовитьСтруктуруДляПоступленияДопРасходовПоПередачаРабот(ДеревоДанных)
	
	ДанныеЭД = ДанныеВходящегоЭД(ДеревоДанных);
	
	Шапка = Новый Структура;
	
	Шапка.Вставить("НомерВходящегоДокумента", ДанныеЭД.НомерАкта);
	Шапка.Вставить("ДатаВходящегоДокумента" , ДанныеЭД.ДатаАкта);
	
	Валюта = НайтиСсылкуНаОбъект("Валюты", ДанныеЭД.ВалютаКод);
	Шапка.Вставить("ВалютаДокумента", Валюта);
	
	КурсВзаиморасчетов = Неопределено;
	ДанныеЭД.ДопДанные.Подписанные.Свойство("КурсВзаиморасчетов", КурсВзаиморасчетов);
	Шапка.Вставить("КурсВзаиморасчетов"     ,?(ЗначениеЗаполнено(КурсВзаиморасчетов), КурсВзаиморасчетов, 1));
	Шапка.Вставить("КратностьВзаиморасчетов", 1);
	Шапка.Вставить("СуммаВключаетНДС"       , Ложь);
	Шапка.Вставить("ВидОперацииЭД"          , ДанныеЭД.ВидОперации);
	
	Если ЗначениеЗаполнено(ДанныеЭД.НомерИсправления) Тогда
		Шапка.Вставить("Исправление"     , Истина);
		Шапка.Вставить("НомерИсправления", ДанныеЭД.НомерИсправления);
		Шапка.Вставить("ДатаИсправления" , ДанныеЭД.ДатаИсправления);
		
		// Вид операции ЭД может быть не указан, т.к. реквизит не является обязательным.
		Если Не ЗначениеЗаполнено(Шапка.ВидОперацииЭД) Тогда
			Шапка.ВидОперацииЭД = Перечисления.ВидыОперацийЭД.Исправление;
		КонецЕсли;
	Иначе
		Шапка.Вставить("Исправление", Ложь);
	КонецЕсли;
	
	НайтиОрганизациюПоДаннымЭД(Шапка, ДеревоДанных, "Исполнитель");
	Шапка.Вставить("Контрагент",  КонтрагентПоДаннымЭД(ДеревоДанных, "Заказчик"));
	
	МассивНаименованийУслуг = Новый Массив;
	ПерваяИтерация          = Истина;
	СтавкаНДС               = Неопределено;
	Для Каждого СтрокаТаблицы Из ДанныеЭД.ТаблицаУслуг Цикл
		МассивНаименованийУслуг.Добавить(СтрокаТаблицы.Описание);
		Если ПерваяИтерация Тогда
			СтавкаНДС = СтрокаТаблицы.СтавкаНДС;
			ПерваяИтерация = Ложь;
		КонецЕсли;
		
		// Если строки таблицы документа имеют разные ставки НДС, то ставку НДС в документе "Поступление доп. расходов" не заполняем.
		Если СтавкаНДС <> СтрокаТаблицы.СтавкаНДС Тогда
			СтавкаНДС = Неопределено;
		КонецЕсли;
	КонецЦикла;
	Шапка.Вставить("Содержание"     , СтрСоединить(МассивНаименованийУслуг, ";" + " "));
	Шапка.Вставить("СтавкаНДС"      , СтавкаНДС);
	Шапка.Вставить("СуммаНДС"       , ДанныеЭД.ТаблицаУслуг.Итог("СуммаНДС"));
	Шапка.Вставить("Сумма"          , ДанныеЭД.ТаблицаУслуг.Итог("СуммаБезНДС"));
	Шапка.Вставить("СуммаДокумента" , ДанныеЭД.ТаблицаУслуг.Итог("СуммаСНДС"));
	
	ДаныеДляЗаполнения = Новый Структура();
	ДаныеДляЗаполнения.Вставить("Шапка", Шапка);
	
	Возврат ДаныеДляЗаполнения;
	
КонецФункции

Процедура ЗаполнитьДокументПоступленияДопРасходов(Документ, ДанныеДляЗаполнения)
	
	Шапка = ДанныеДляЗаполнения.Шапка;
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	Если ЗначениеЗаполнено(Документ) Тогда // получены изменения по существующему документу
		ДокументОбъект = Документ.ПолучитьОбъект();
		Если ДокументОбъект.Проведен Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		КонецЕсли;
	Иначе
		ДокументОбъект = Документы.ПоступлениеДопРасходов.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ДокументОбъект, Шапка, Истина);
	
	ОтборПоВалюте       = Новый Структура("ЗначениеОтбора", ДокументОбъект.ВалютаДокумента);
	СтруктураПараметров = Новый Структура("ВалютаВзаиморасчетов", ОтборПоВалюте);
	
	СписокВидовДоговоров = Новый СписокЗначений;
	СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДокументОбъект.ДоговорКонтрагента,
		ДокументОбъект.Контрагент, ДокументОбъект.Организация, СписокВидовДоговоров, СтруктураПараметров);
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, Шапка);
	
	ДокументОбъект.СпособРаспределения = Перечисления.СпособыРаспределенияДопРасходов.ПоСумме;
	ДокументОбъект.НДСВключенВСтоимость = Ложь;
	ДокументОбъект.НДСНеВыделять = Ложь;
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("СтатусДокумента", Перечисления.СтатусыДокументовПоступления.ОригиналПолучен);
	
	ЗаписатьДокумент(ДокументОбъект, РежимЗаписи);
	Если НЕ ЗначениеЗаполнено(Документ) Тогда
		Документ = ДокументОбъект.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УПД_2019

Процедура ЗаполнитьОсновнуюЧастьУПД_2019(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных)
	
	ДанныеШапки = ДанныеДляФормированияЭД.ДанныеШапки;
	ЭтоСчетФактураНаАванс = СтруктураЭД.ЭтоСчетФактураНаАванс;
	
	Если ЭтоСчетФактураНаАванс Тогда
		ВидСчетаФактуры = "Авансовый";
		ОбстоятельстваФормированияСФ = "2";
	Иначе
		ВидСчетаФактуры = "Реализация";
		ОбстоятельстваФормированияСФ = "1";
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидСчетаФактуры", ВидСчетаФактуры);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ОбстоятельстваФормированияСФ", ОбстоятельстваФормированияСФ);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СсылкаСчетаФактуры", ДанныеДляФормированияЭД.СчетФактура);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента", ДанныеШапки.Номер);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента",  ДанныеШапки.Дата);
	Если ДанныеШапки.Исправление Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления", ДанныеШапки.НомерИсправления);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления",  ДанныеШапки.ДатаИсправления);
	КонецЕсли;
	
	// Сведения о  продавце и покупателе
	ВидОперацииЭД = Неопределено;
	СтруктураЭД.Свойство("ВидОперацииЭД", ВидОперацииЭД);
	Если ВидОперацииЭД = Перечисления.ВидыОперацийЭД.Комиссия Тогда
		ДокументРеализации = ДанныеДляФормированияЭД.Ссылка;
		ДанныеПокупателяПродавца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРеализации, "Организация, Контрагент");
		СведенияОПокупателе  = ПолучитьДанныеЮрФизЛица(ДанныеПокупателяПродавца.Контрагент, ДанныеШапки.Дата);
		СведенияОПоставщике  = ПолучитьДанныеЮрФизЛица(ДанныеПокупателяПродавца.Организация, ДанныеШапки.Дата, БанковскийСчетПродавца(ДанныеШапки));
	Иначе
		СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Покупатель, ДанныеШапки.Дата);
		СведенияОПоставщике  = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Поставщик, ДанныеШапки.Дата, БанковскийСчетПродавца(ДанныеШапки));
		СведенияОПокупателе.ИНН = ДанныеШапки.ИННпокупателя;
		СведенияОПокупателе.КПП = ДанныеШапки.КППпокупателя;
		СведенияОПоставщике.ИНН = ДанныеШапки.ИННпоставщика;
		СведенияОПоставщике.КПП = ДанныеШапки.КППпоставщика;
	КонецЕсли;
	
	ДополнитьДаннымиОПодразделенииОрганизации(СведенияОПоставщике, ДанныеДляФормированияЭД);
	ЗаполнитьДанныеУчастниковУПД_2019(
		ДеревоДанных,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СведенияОПоставщике),
		"СведенияОПродавце",
		"Юр",
		ДанныеШапки.Дата);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СоставительДокументаНаименование", СоставительДокумента(СведенияОПоставщике));
	
	ЗаполнитьДанныеУчастниковУПД_2019(
		ДеревоДанных,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СведенияОПокупателе),
		"СведенияОПокупателе",
		"Юр",
		ДанныеШапки.Дата);
	
	ПолучитьКомиссионераДляПеревыставленногоСчетаФактуры(ДанныеШапки, ДанныеДляФормированияЭД);
	
	// Сведения о комиссионере
	Если ДанныеШапки.Свойство("Комиссионер")
		И ЗначениеЗаполнено(ДанныеШапки.Комиссионер) Тогда
		
		СведенияОКомиссионере = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Комиссионер, ДанныеШапки.Дата);
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОКомиссионере, "СведенияОКомиссионере", "Юр", ДанныеШапки.Дата);
	КонецЕсли;
	
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить код валюты документа. Проверьте, что:
	|	- в документе указана валюта,
	|	- для нее заполнен код по Общероссийскому классификатору валют.'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", ДанныеШапки.ВалютаКод, ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ВалютаНаименование", ДанныеШапки.ВалютаНаименованиеПолное);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ВалютаКурс", ДанныеШапки.ВалютаКурс);
	
	Если ЗначениеЗаполнено(ДанныеШапки.ИдентификаторГосКонтракта) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта", ДанныеШапки.ИдентификаторГосКонтракта);
	КонецЕсли;
	
	ДокументыОснования = Новый Массив;
	Для Каждого ДокументОснование Из ДанныеДляФормированияЭД.ДокументыОснования Цикл
		ДокументыОснования.Добавить(ДокументОснование);
	КонецЦикла;
	
	Если ДанныеШапки.Исправление Тогда
		Если ЗначениеЗаполнено(ДанныеДляФормированияЭД.СчетФактура) Тогда
			ИсправляемыйСчетФактура = ИсправляемыйСчетФактураВыданный(ДанныеДляФормированияЭД.СчетФактура);
			Если ЗначениеЗаполнено(ИсправляемыйСчетФактура) Тогда
				ДокументыОснования.Добавить(ИсправляемыйСчетФактура);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры", ДокументыОснования);
	
	ПлатежныеДокументы = Новый ТаблицаЗначений();
	ПлатежныеДокументы.Колонки.Добавить("ДатаПРД");
	ПлатежныеДокументы.Колонки.Добавить("НомерПРД");
	ПлатежныеДокументы.Колонки.Добавить("Сумма");
	Если ДанныеШапки.ТаблицаПлатежныхДокументов <> Неопределено Тогда
		Для Каждого ПлатежныйДокумент ИЗ ДанныеШапки.ТаблицаПлатежныхДокументов Цикл
			НовыйПлатежныйДокумент = ПлатежныеДокументы.Добавить();
			НовыйПлатежныйДокумент.ДатаПРД  = ПлатежныйДокумент.ДатаДокумента;
			НовыйПлатежныйДокумент.НомерПРД = ПлатежныйДокумент.НомерДокумента;
		КонецЦикла;
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ПлатежныеДокументы, "ПлатежноРасчетныеДокументы");
	
	Если ЭтоСчетФактураНаАванс Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", 
			НСтр("ru = 'Регистрация счет-фактуры на аванс'"));
	КонецЕсли;
	
	ДанныеПервичногоДокумента = ДанныеПервичногоДокументаУПД(ДанныеДляФормированияЭД.Ссылка);
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки");
	ТаблицаТоваров.Колонки.Добавить("ТоварНаименование");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияКод");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	ТаблицаТоваров.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("ЦенаЗаЕдиницуИзмерения", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("НалоговаяСтавка");
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогом", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаАкциза", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СведенияОТаможеннойДекларации", Новый ОписаниеТипов("ТаблицаЗначений"));
	ТаблицаТоваров.Колонки.Добавить("СтранаПроисхожденияНаименование", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТаблицаТоваров.Колонки.Добавить("Признак", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1)));
	ТаблицаТоваров.Колонки.Добавить("ТоварКод", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТаблицаТоваров.Колонки.Добавить("КодВидаТовара", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТаблицаТоваров.Колонки.Добавить("ТоварИдентификатор", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТаблицаТоваров.Колонки.Добавить("СведенияОМаркировке");
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	
	НомерСтроки = 1;
	ТолькоУслуги = Истина;
	УчетАгентскогоНДСПокупателем = Ложь; // используется для реализации металлолома, сырых шкур животных.
	Если ДокументыОснования.Количество() > 0
		И (ТипЗнч(ДокументыОснования[0]) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		ИЛИ ТипЗнч(ДокументыОснования[0]) = Тип("ДокументСсылка.КорректировкаРеализации")
		ИЛИ ТипЗнч(ДокументыОснования[0]) = Тип("ДокументСсылка.СчетФактураВыданный")) Тогда
		
		УчетАгентскогоНДСПокупателем = (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ДокументыОснования[0],
			"ДоговорКонтрагента.УчетАгентскогоНДСПокупателем") = Истина);
			
	КонецЕсли;
	
	ТаблицаКодовМаркировки = Неопределено;
	Если ДанныеДляФормированияЭД.Владелец().Колонки.Найти("ШтрихкодыУпаковок") <> Неопределено Тогда
		ТаблицаКодовМаркировки = ДанныеДляФормированияЭД.ШтрихкодыУпаковок;
	КонецЕсли;
	
	ОбъектыСОграничениемДлины = Новый Массив;
	
	Для каждого Строка Из ДанныеДляФормированияЭД.ТаблицаДокумента Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		
		НаименованиеНоменклатуры = ?(ЗначениеЗаполнено(Строка.ТоварНаименование), Строка.ТоварНаименование,
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Товар, "НаименованиеПолное"));
		
		НоваяСтрока.НомерСтроки         = НомерСтроки;
		НоваяСтрока.ТоварНаименование   = НаименованиеНоменклатуры;
		
		Если НеобходимоУказатьЕдиницуИзмерения(Строка, ЭтоСчетФактураНаАванс) Тогда
			Если ПустаяСтрока(Строка.ЕдиницаИзмеренияКод) И ТипЗнч(Строка.Товар) = Тип("СправочникСсылка.Номенклатура") Тогда
				ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Товар, "ЕдиницаИзмерения");
				Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда // берем значение ед. измерения из карточки номенклатуры
					РеквизитыЕдиницыИзмерения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЕдиницаИзмерения, "Наименование, Код");
					НоваяСтрока.ЕдиницаИзмеренияКод          = СокрЛП(РеквизитыЕдиницыИзмерения.Код);
					НоваяСтрока.ЕдиницаИзмеренияНаименование = СокрЛП(РеквизитыЕдиницыИзмерения.Наименование);
				КонецЕсли;
			Иначе
				НоваяСтрока.ЕдиницаИзмеренияКод          = СокрЛП(Строка.ЕдиницаИзмеренияКод);
				НоваяСтрока.ЕдиницаИзмеренияНаименование = СокрЛП(Строка.ЕдиницаИзмеренияНаименование);
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока.Количество                = Строка.Количество;
		НоваяСтрока.ЦенаЗаЕдиницуИзмерения    = Строка.Цена;
		НоваяСтрока.СтоимостьТоваровБезНалога = ?(ЭтоСчетФактураНаАванс И Не УчетАгентскогоНДСПокупателем, 0, Строка.Стоимость);
		НоваяСтрока.НалоговаяСтавка           = ?(УчетАгентскогоНДСПокупателем, СтавкаНДСИсчисляетсяНалоговымАгентом(), Строка.СтавкаНДС);
		НоваяСтрока.СуммаНалога               = Строка.СуммаНДС;
		НоваяСтрока.СтоимостьТоваровСНалогом  = ?(УчетАгентскогоНДСПокупателем, 0, Строка.Всего);
		
		Если ЗначениеЗаполнено(Строка.НомерГТД)
			Или ЗначениеЗаполнено(Строка.СтранаПроисхожденияКод) Тогда
			
			НоваяСтрока.СведенияОТаможеннойДекларации = СведенияОТаможеннойДекларации();
			Декларация = НоваяСтрока.СведенияОТаможеннойДекларации.Добавить();
			Декларация.СтранаПроисхожденияКод    = ПолучитьКорректныйКодСтраны(Строка.СтранаПроисхожденияКод);
			Декларация.ТаможеннаяДекларацияНомер = ?(ЗначениеЗаполнено(Строка.НомерГТД), СокрЛП(Строка.НомерГТД), "");
			НоваяСтрока.СтранаПроисхожденияНаименование = Строка.СтранаПроисхождения;
		КонецЕсли;
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(НоваяСтрока.СведенияОТаможеннойДекларации, "СтранаПроисхожденияКод",,,
			НСтр("ru = 'Не заполнен код страны происхождения'"));
		
		НоваяСтрока.Признак = ПризнакТовараЕдиногоДокумента(Строка.Товар);
		НоваяСтрока.ТоварКод           = СокрЛП(Строка.ТоварКод);
		НоваяСтрока.ТоварИдентификатор = Строка.Товар.УникальныйИдентификатор();
		
		ЗаполнитьСведенияОМаркировкеВУПД(НоваяСтрока, ДанныеПервичногоДокумента.Ссылка, Строка, ТаблицаКодовМаркировки);
		
		Если ТолькоУслуги
			И НоваяСтрока.Признак = "1" Тогда
			ТолькоУслуги = Ложь;
		КонецЕсли;
		
		Если ТипЗнч(Строка.Товар) = Тип("СправочникСсылка.Номенклатура") Тогда
			НоваяСтрока.Сопоставление = СтруктураДляСопоставленияНоменклатурыЭД(
				Строка.Товар, НоваяСтрока.НалоговаяСтавка, НаименованиеНоменклатуры);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.ТоварКодТНВЭД) И ТипЗнч(Строка.ТоварКодТНВЭД) = Тип("СправочникСсылка.КлассификаторТНВЭД") Тогда
			НоваяСтрока.КодВидаТовара = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.ТоварКодТНВЭД, "Код");
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	ДобавитьОбработкуОшибокЗаполненияТабличнойЧастиЭД(ТаблицаТоваров);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСтоимостьТоваровБезНалога", ?(ЭтоСчетФактураНаАванс И Не УчетАгентскогоНДСПокупателем, 0, ТаблицаТоваров.Итог("СтоимостьТоваровБезНалога")));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом", ТаблицаТоваров.Итог("СтоимостьТоваровСНалогом"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоСуммаНалога", ТаблицаТоваров.Итог("СуммаНалога"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоКОплате.ВсегоКоличество", ТаблицаТоваров.Итог("Количество"));
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "СведенияОТоварах");
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", ТолькоУслуги);
	
	// Не выводим данные Грузоотправителя и Грузополучателя, если в документе только услуги или это счет-фактура на аванс.
	Если Не (ТолькоУслуги Или ЭтоСчетФактураНаАванс) Тогда
		
		// Сведения о грузоотправителе
		Если НЕ ЗначениеЗаполнено(ДанныеШапки.Грузоотправитель)ИЛИ (ДанныеШапки.Грузоотправитель = "он же") Тогда
			ТаблицаГрузоотправителей = ЭлектронноеВзаимодействиеСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
				ДеревоДанных, "СведенияОГрузоотправителе");
			ТаблицаГрузоотправителей.Колонки.Добавить("СведенияОбУчастнике");
			ТаблицаГрузоотправителей.Добавить().ОнЖе = Истина;
			ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаГрузоотправителей, "СведенияОГрузоотправителе");
		Иначе
			СведенияОГрузоотправителе = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Грузоотправитель, ДанныеШапки.Дата);
			ЗаполнитьДанныеУчастниковУПД_2019(
				ДеревоДанных,
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СведенияОГрузоотправителе),
				"СведенияОГрузоотправителе",
				"Факт",
				ДанныеШапки.Дата);
		КонецЕсли;
		
		// Сведения о грузополучателе
		Грузополучатель = ?(НЕ ЗначениеЗаполнено(ДанныеШапки.Грузополучатель) ИЛИ (ДанныеШапки.Грузополучатель = "он же"),
			СтруктураЭД.Контрагент, ДанныеШапки.Грузополучатель);
		СведенияОГрузополучателе = ПолучитьДанныеЮрФизЛица(Грузополучатель, ДанныеШапки.Дата);
		
		// Получаем адрес доставки из документа реализации
		ДокументРеализации = Неопределено;
		Если ТипЗнч(ДанныеДляФормированияЭД.ДокументыОснования) = Тип("Массив") Тогда
			Для Каждого Документ Из ДанныеДляФормированияЭД.ДокументыОснования Цикл
				Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
					Или ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
					
					ДокументРеализации = Документ;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		АдресДоставки = Неопределено;
		Если ЗначениеЗаполнено(ДокументРеализации) Тогда
			АдресДоставки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРеализации, "АдресДоставки");
		КонецЕсли;
		Если ЗначениеЗаполнено(АдресДоставки) Тогда
			СведенияОГрузополучателе.Вставить("АдресДоставки", АдресДоставки);
		КонецЕсли;
		
		ЗаполнитьДанныеУчастниковУПД_2019(
			ДеревоДанных,
 			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СведенияОГрузополучателе),
			"СведенияОГрузополучателе",
			"Факт",               
			ДанныеШапки.Дата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРасширеннуюЧастьУПД_2019(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных)
	
	ДанныеШапки = ДанныеДляФормированияЭД.ДанныеШапки;
	
	Если ДанныеШапки.Исправление Тогда
		ВидОперацииЭД = Перечисления.ВидыОперацийЭД.Исправление;
	ИначеЕсли СтруктураЭД.Свойство("ВидОперацииЭД") Тогда
		ВидОперацииЭД = СтруктураЭД.ВидОперацииЭД;
	Иначе
		ВидОперацииЭД = Перечисления.ВидыОперацийЭД.ПродажаКомиссия;
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации", ВидОперацииЭД);
	
	ЕстьТовары = Ложь;
	ЕстьУслуги = Ложь;
	Для Каждого Строка Из ДанныеДляФормированияЭД.ТаблицаДокумента Цикл
		ПризнакТовара = ПризнакТовараЕдиногоДокумента(Строка.Товар);
		Если ПризнакТовара = "1" Тогда
			ЕстьТовары = Истина;
		Иначе
			ЕстьУслуги = Истина;
		КонецЕсли;
	КонецЦикла;
	
	СоставСодержания = Новый Массив;
	Если ЕстьТовары Тогда
		СоставСодержания.Добавить(НСтр("ru = 'Товары переданы.'"));
	КонецЕсли;
	Если ЕстьУслуги Тогда
		СоставСодержания.Добавить(НСтр("ru = 'Услуги оказаны в полном объеме.'"));
	КонецЕсли;
	СодержаниеОперации = СтрСоединить(СоставСодержания, " ");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", СодержаниеОперации);
	
	ДанныеПервичногоДокумента = ДанныеПервичногоДокументаУПД(ДанныеДляФормированияЭД.Ссылка);
	
	// Дата отгрузки должна быть равна дате документа-реализации товаров и услуг.
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных, "ДатаОтгрузкиТоваров", ДанныеПервичногоДокумента.ДатаОтгрузки);
		
	// Данные документа подтверждения отгрузки (данные документа "Реализация товаров и услуг")
	ДокументыПодтвержденияОтгрузки = Новый ТаблицаЗначений;
	ДокументыПодтвержденияОтгрузки.Колонки.Добавить("Наименование");
	ДокументыПодтвержденияОтгрузки.Колонки.Добавить("Номер");
	ДокументыПодтвержденияОтгрузки.Колонки.Добавить("Дата");
	СтрокаДокументыПодтвержденияОтгрузки = ДокументыПодтвержденияОтгрузки.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаДокументыПодтвержденияОтгрузки, ДанныеПервичногоДокумента);
	СтрокаДокументыПодтвержденияОтгрузки.Наименование = "Реализация (акт, накладная, УПД)";
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(
		ДеревоДанных, ДокументыПодтвержденияОтгрузки, "ДокументыПодтвержденияОтгрузки");
	
	// Данные транспортной накладной (данные документа "Реализация товаров и услуг")
	ТранспортнаяНакладная = Новый ТаблицаЗначений;
	ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяНомер");
	ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяДата");
	СтрокаТранспортнаяНакладная = ТранспортнаяНакладная.Добавить();
	СтрокаТранспортнаяНакладная.ТранспортнаяНакладнаяНомер = ДанныеПервичногоДокумента.Номер;
	СтрокаТранспортнаяНакладная.ТранспортнаяНакладнаяДата = ДанныеПервичногоДокумента.Дата;
	
	Для Каждого ДокументОснование Из ДанныеДляФормированияЭД.ДокументыОснования Цикл
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			ДокументРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ДокументРеализации");
			Если ЗначениеЗаполнено(ДокументРеализации) Тогда
				ДанныеДляФормированияЭД.ДокументыОснования.Добавить(ДокументРеализации);
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияДокументаОтгрузки", ДанныеДляФормированияЭД.ДокументыОснования);
	
	ОснованиеОтгрузкиТоваров = ПодготовитьТаблицуДанныхДоговораУПД(
		ДанныеШапки.Основание,
		ДанныеШапки.ОснованиеНомер,
		ДанныеШапки.ОснованиеДата);
		
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ОснованиеОтгрузкиТоваров, "ДокументДата",,,
		НСтр("ru = 'Необходимо указать дату договора.'"));
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ОснованиеОтгрузкиТоваров, "ОснованиеОтгрузкиТоваров");
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СведенияОТранспортировке", ДанныеШапки.ДанныеТранспортнаяНакладная);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ИныеСведенияОбОтгрузке", ДанныеШапки.СопроводительныеДокументы);
	
	ТранспортнаяНакладная = Новый ТаблицаЗначений;
	ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяНомер");
	ТранспортнаяНакладная.Колонки.Добавить("ТранспортнаяНакладнаяДата");
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТранспортнаяНакладная, "ТранспортнаяНакладная");
	
	Если ЗначениеЗаполнено(ДанныеШапки.Перевозчик) Тогда
		СведенияОПеревозчике = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Перевозчик);
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПеревозчике, "СведенияОПеревозчике",, ДанныеШапки.Дата);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеШапки.ДолжностьКладовщика)
		И ЗначениеЗаполнено(ДанныеШапки.Кладовщик) Тогда
		
		// Кладовщик работает в организации
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Должность", Строка(ДанныеШапки.ДолжностьКладовщика));
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Фамилия", ДанныеШапки.КладовщикФИО.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Имя", ДанныеШапки.КладовщикФИО.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.РаботникОрганизацииПродавца.Отчество", ДанныеШапки.КладовщикФИО.Отчество);
			
	ИначеЕсли ЗначениеЗаполнено(ДанныеШапки.Кладовщик) Тогда
		// Кладовщик не работает в организации
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ.Фамилия", ДанныеШапки.КладовщикФИО.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ.Имя", ДанныеШапки.КладовщикФИО.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"СведенияОЛицеПередавшемТовары.ИноеЛицо.ФЛ.Отчество", ДанныеШапки.КладовщикФИО.Отчество);
	КонецЕсли;
	
	Если ДанныеШапки.ОснованиеВид = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
		
		ТекстоваяИнформация = Новый ТаблицаЗначений;
		ТекстоваяИнформация.Колонки.Добавить("Идентификатор");
		ТекстоваяИнформация.Колонки.Добавить("Значение");
		
		НоваяСтрока = ТекстоваяИнформация.Добавить();
		НоваяСтрока.Идентификатор = "ПередачаТовараКомитентом";
		НоваяСтрока.Значение = "Истина";
		
		ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТекстоваяИнформация, "ДопДанныеДокументаОтгрузки.ТекстоваяИнформация");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеУчастниковУПД_2019(ДеревоДанных, СведенияОбУчастниках, ВидУчастника, ВидАдреса = "АдресРФ", ДатаСведений = '00010101')
	
		ТаблицаУчастников = ЭлектронноеВзаимодействиеСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(
			ДеревоДанных, ВидУчастника);
		ТаблицаУчастников.Колонки.Добавить("СведенияОбУчастнике");
		
		Для Каждого СведенияОбУчастнике Из СведенияОбУчастниках Цикл
			СтрокаУчастника = ТаблицаУчастников.Добавить();
			ДанныеУчастника = ДанныеУчастникаУПД(СведенияОбУчастнике, ВидУчастника, ВидАдреса, ДатаСведений);
			Если ВРег(ВидУчастника) = ВРег("СведенияОГрузоотправителе") Тогда
				СтрокаУчастника.Грузоотправитель = ДанныеУчастника;
				СтрокаУчастника.ОнЖе = Ложь;
				ЗаполнитьЗначенияСвойств(СтрокаУчастника, ДанныеУчастника, "СведенияОбУчастнике");
			Иначе
				ЗаполнитьЗначенияСвойств(СтрокаУчастника, ДанныеУчастника);
			КонецЕсли;
		КонецЦикла;
		
		ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаУчастников, ВидУчастника);
		
КонецПроцедуры

Функция ДанныеУчастникаУПД(СведенияОбУчастнике, ВидУчастника, ВидАдреса, ДатаСведений)
	
	Данные = Новый Структура("СведенияОбУчастнике", СведенияОбУчастнике);
	
	// Наименование, ИНН, КПП участника
	Данные.Вставить("ТипУчастника", Новый Структура);
	Если СведенияОбУчастнике.СтранаРегистрации <> Справочники.СтраныМира.Россия Тогда
		Данные.ТипУчастника.Вставить("ИЛ", Новый Структура);
		Данные.ТипУчастника.ИЛ.Вставить("НаименованиеОрганизации", СведенияОбУчастнике.ПолноеНаименование);
	ИначеЕсли СведенияОбУчастнике.ТипЮрФизЛица = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		Данные.ТипУчастника.Вставить("ЮЛ", Новый Структура);
		Данные.ТипУчастника.ЮЛ.Вставить("НаименованиеОрганизации", СведенияОбУчастнике.ПолноеНаименование);
		Данные.ТипУчастника.ЮЛ.Вставить("ИНН",                     СведенияОбУчастнике.ИНН);
		Данные.ТипУчастника.ЮЛ.Вставить("КПП",                     СведенияОбУчастнике.КПП);
	Иначе
		ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(СведенияОбУчастнике.ПолноеНаименование);

		Данные.ТипУчастника.Вставить("ИП", Новый Структура);
		Данные.ТипУчастника.ИП.Вставить("ИНН",      СведенияОбУчастнике.ИНН);
		Данные.ТипУчастника.ИП.Вставить("Фамилия",  ФИО.Фамилия);
		Данные.ТипУчастника.ИП.Вставить("Имя",      ФИО.Имя);
		Данные.ТипУчастника.ИП.Вставить("Отчество", ФИО.Отчество);
		
		Если ЗначениеЗаполнено(СведенияОбУчастнике.СвидетельствоСерияНомер) Тогда
			Свидетельство = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Свидетельство №%1 от %2'"),
				СведенияОбУчастнике.СвидетельствоСерияНомер,
				Формат(СведенияОбУчастнике.СвидетельствоДатаВыдачи, "ДЛФ=D"));
				
			Данные.ТипУчастника.ИП.Вставить("СвидетельствоОГосРегистрации", Свидетельство);
		КонецЕсли;
	КонецЕсли;
	
	// Контактная информация участника
	Данные.Вставить("Адрес", Новый Структура);
	Данные.Адрес.Вставить("АдресИнформация", Новый Структура("КодСтраны, АдресТекст"));
	
	Если СведенияОбУчастнике.Свойство("ОбособленноеПодразделениеОрганизации")
		И ЗначениеЗаполнено(СведенияОбУчастнике.ОбособленноеПодразделениеОрганизации) Тогда
		
		Компания = СведенияОбУчастнике.ОбособленноеПодразделениеОрганизации;
	Иначе
		Компания = СведенияОбУчастнике.ЮрФизЛицоСсылка;
	КонецЕсли;
	АдресУчастника = Новый Структура;
	ПолучитьАдресСтруктурой(АдресУчастника, Компания, ВидАдреса, ДатаСведений);
	
	Если СведенияОбУчастнике.Свойство("АдресДоставки") Тогда
		АдресУчастника.АдресТекст = СведенияОбУчастнике.АдресДоставки;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(Данные.Адрес.АдресИнформация, АдресУчастника);
	
	Данные.Вставить("КонтактныеСведения", Новый Структура);
	
	Телефон = "";
	Если СведенияОбУчастнике.Свойство("Телефоны", Телефон) И ЗначениеЗаполнено(Телефон) Тогда
		Данные.КонтактныеСведения.Вставить("Телефон", Телефон);
	КонецЕсли;
	
	ЭлектроннаяПочта = "";
	Если СведенияОбУчастнике.Свойство("Email", ЭлектроннаяПочта) И ЗначениеЗаполнено(ЭлектроннаяПочта) Тогда
		Данные.КонтактныеСведения.Вставить("ЭлектроннаяПочта", ЭлектроннаяПочта);
	КонецЕсли;
	
	// Банковские реквизиты
	Данные.Вставить("БанковскиеРеквизиты", Новый Структура);
	НомерСчета = "";
	Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
		Данные.БанковскиеРеквизиты.Вставить("НомерСчета", НомерСчета);		
		Банк =     "";
		БИК =      "";
		КоррСчет = "";
		Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
			Данные.БанковскиеРеквизиты.Вставить("НаименованиеБанка", Банк);
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
			Данные.БанковскиеРеквизиты.Вставить("БИКБанка", БИК);
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("КоррСчет", КоррСчет) И ЗначениеЗаполнено(КоррСчет) Тогда
			Данные.БанковскиеРеквизиты.Вставить("КорреспондентскийСчетБанка", КоррСчет);
		КонецЕсли;
	КонецЕсли;
	
	// ОКПО
	КодПоОКПО = "";
	Если СведенияОбУчастнике.Свойство("КодПоОКПО", КодПоОКПО) И ЗначениеЗаполнено(КодПоОКПО) Тогда
		Данные.Вставить("КодОКПО", КодПоОКПО);
	КонецЕсли;
	
	Если СведенияОбУчастнике.Свойство("ПодразделениеОрганизации") Тогда
		Данные.Вставить("СтруктурноеПодразделение", СведенияОбУчастнике.ПодразделениеОрганизации);
	КонецЕсли;
	
	Возврат Данные;

КонецФункции

Функция ПодготовитьСтруктуруДляПоступленияТоваровУслугУПД_2019(ДеревоДанных)
	
	ДанныеОбъекта = Новый Структура;
	
	Товары = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Товары.Колонки.Добавить("ШтрихкодыУпаковок");
	ШтрихкодыУпаковок = ЭлектронноеВзаимодействиеИСМП.НоваяТаблицаШтрихкодыУпаковок();
	
	Услуги         = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	ВозвратнаяТара = Документы.ПоступлениеТоваровУслуг.ПустаяСсылка().ВозвратнаяТара.ВыгрузитьКолонки();
	
	Валюта = НайтиСсылкуНаОбъект("Валюты", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод"));
	ДанныеОбъекта.Вставить("ВалютаДокумента", Валюта);
	КурсВзаиморасчетов = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ВалютаКурс");
	ДанныеОбъекта.Вставить("КурсВзаиморасчетов", ?(ЗначениеЗаполнено(КурсВзаиморасчетов), КурсВзаиморасчетов, 1));
	ДанныеОбъекта.Вставить("КратностьВзаиморасчетов", 1);
	
	ДанныеОбъекта.Вставить("ВидОперацииЭД", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидОперации"));
	
	ДанныеОбъекта.Вставить("СуммаВключаетНДС", Ложь);
	ДанныеОбъекта.Вставить("Корректировка",    Ложь);
	
	ДанныеОбъекта.Вставить("НомерВходящегоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента"));
	ДанныеОбъекта.Вставить("ДатаВходящегоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента"));
	
	Если ЗначениеЗаполнено(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления")) Тогда
		ДанныеОбъекта.Вставить("Исправление", Истина);
		ДанныеОбъекта.Вставить("НомерИсправления", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления"));
		ДанныеОбъекта.Вставить("ДатаИсправления",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления"));
		
		// Вид операции ЭД может быть не указан, т.к. реквизит не является обязательным.
		Если Не ЗначениеЗаполнено(ДанныеОбъекта.ВидОперацииЭД) Тогда
			ДанныеОбъекта.ВидОперацииЭД = Перечисления.ВидыОперацийЭД.Исправление;
		КонецЕсли;
	Иначе
		ДанныеОбъекта.Вставить("Исправление", Ложь);
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("УчитыватьИсправлениеИсходногоДокумента", Ложь);
	
	ОпределитьУчастниковПоДаннымЭД_2019(ДеревоДанных, ДанныеОбъекта);
	
	ДанныеОбъекта.Вставить("ПередачаТовараКомитентом", Ложь);
	
	ОснованиеСчетаФактуры = Неопределено;
	ДокументыОснованияСчетаФактуры = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
	Если ТипЗнч(ДокументыОснованияСчетаФактуры) = Тип("Массив") Тогда
		Для Каждого ДокументОснование Из ДокументыОснованияСчетаФактуры Цикл
			Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				ОснованиеСчетаФактуры = ДокументОснование;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(ДокументыОснованияСчетаФактуры) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ОснованиеСчетаФактуры = ДокументыОснованияСчетаФактуры;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОснованиеСчетаФактуры) Тогда
		ДокументОснованиеСчетаФактуры = ДокументОснованиеСчетаФактурыПолученного(ОснованиеСчетаФактуры);
		Если ЗначениеЗаполнено(ДокументОснованиеСчетаФактуры) Тогда
			ДанныеОбъекта.Вставить("Основание", ДокументОснованиеСчетаФактуры);
		КонецЕсли;
	ИначеЕсли ДанныеОбъекта.Исправление Тогда
		ДокументыОснованияДокументаОтгрузки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияДокументаОтгрузки");
		ДокументПоступления = Неопределено;
		Если ТипЗнч(ДокументыОснованияДокументаОтгрузки) = Тип("Массив") Тогда
			Для Каждого ДокументОснование Из ДокументыОснованияДокументаОтгрузки Цикл
				Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
					ДокументПоступления = ДокументОснование;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Если ТипЗнч(ДокументыОснованияДокументаОтгрузки) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
					ДокументПоступления = ДокументыОснованияДокументаОтгрузки;
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(ДокументПоступления) Тогда
			ДанныеОбъекта.Вставить("Основание", ДокументПоступления);
		КонецЕсли;
	КонецЕсли;
	
	ТекстоваяИнформация = ДеревоДанных.Строки.Найти("ДопДанныеДокументаОтгрузки.ТекстоваяИнформация", "ПолныйПуть", Истина);
	Если ТекстоваяИнформация <> Неопределено Тогда
		Для Каждого СтрокаТекстовойИнформации Из ТекстоваяИнформация.Строки Цикл
			Если ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаТекстовойИнформации, 
						"ДопДанныеДокументаОтгрузки.ТекстоваяИнформация.НомерСтроки.Идентификатор") = "ПередачаТовараКомитентом" Тогда
				ДанныеОбъекта.ПередачаТовараКомитентом = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаТекстовойИнформации,
						"ДопДанныеДокументаОтгрузки.ТекстоваяИнформация.НомерСтроки.Значение") = "Истина";
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ИспользуетсяОбратноеНачислениеНДС = БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
	НДСИсчисляетсяНалоговымАгентом = Ложь;
	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		
		Номенклатура = НоменклатураИБ(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Сопоставление"); 
		
		Признак = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Признак");
		ЭтоУслуга = Ложь;
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			ЭтоУслуга = ?(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "Услуга"), Истина, Ложь);
		ИначеЕсли ЗначениеЗаполнено(Признак) Тогда
			ЭтоУслуга = ?(Признак = "1", Ложь, Истина);
		Иначе
			ЭтоУслуга = Истина;
		КонецЕсли;
		
		Если Не ЭтоУслуга Тогда
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.ЕдиницаИзмерения = НайтиСсылкуНаОбъект("ЕдиницыИзмерения",
				ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЕдиницаИзмеренияКод"));
		Иначе
			НоваяСтрока = Услуги.Добавить();
			НоваяСтрока.Содержание = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ТоварНаименование");
		КонецЕсли;
			
		// Обязательные реквизиты:
		// Ставка "НДС исчисляется налоговым агентом" используется при продаже металлолома, сырых шкур животных
		СтавкаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
		Если СтавкаНДС = СтавкаНДСИсчисляетсяНалоговымАгентом() Тогда
			
			НДСИсчисляетсяНалоговымАгентом = Истина;
			Если ИспользуетсяОбратноеНачислениеНДС Тогда
				
				НоваяСтрока.СтавкаНДС = УчетНДСКлиентСервер.ОбщаяСтавкаНДС(
					ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента"));
				// Вычисляем НДС сверху по общей ставке
				НоваяСтрока.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
					ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалога"),
					Ложь,
					УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
					
			Иначе
				НоваяСтрока.СтавкаНДС = Неопределено;
				НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалога");
			КонецЕсли;
			
		Иначе
			НоваяСтрока.СтавкаНДС = СтавкаНДС;
			НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалога");
		КонецЕсли;
		
		// Необязательные реквизиты:
		НоваяСтрока.Количество = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Количество");
		НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЦенаЗаЕдиницуИзмерения");
		НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалога");
		
		НоваяСтрока.Номенклатура = Номенклатура;
		
		Если Не ЭтоУслуга Тогда
		
			СведенияОТаможеннойДекларации = СведенияОТоваре.Строки.Найти(
				"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации", "ПолныйПуть", Истина);
			
			Если СведенияОТаможеннойДекларации <> Неопределено
				И СведенияОТаможеннойДекларации.Строки.Количество() > 0 Тогда
				
				ДанныеГТД = Новый Структура("НомерТД, КодСтраныПроисхождения", "", "");
				ДанныеГТД.НомерТД = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТаможеннойДекларации.Строки[0],
					"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации.НомерСтроки.ТаможеннаяДекларацияНомер");
				ДанныеГТД.КодСтраныПроисхождения = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТаможеннойДекларации.Строки[0],
					"СведенияОТоварах.НомерСтроки.СведенияОТаможеннойДекларации.НомерСтроки.СтранаПроисхожденияКод");
				
				ЗаполнитьНомерТДИСтрануПроисхождения(НоваяСтрока, ДанныеГТД, Ложь);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ЭлектронноеВзаимодействиеИСМП.ДобавитьШтрихкодыТаблицуШтрихкодовУпаковок_2019(ШтрихкодыУпаковок, СведенияОТоваре);
	КонецЦикла;
	
	ЭлектронноеВзаимодействиеИСМП.СвернутьТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок);
	
	ДанныеДляЗаполнения = Новый Структура();
	ДанныеДляЗаполнения.Вставить("Шапка",             ДанныеОбъекта);
	ДанныеДляЗаполнения.Вставить("Товары",            Товары);
	ДанныеДляЗаполнения.Вставить("Услуги",            Услуги);
	ДанныеДляЗаполнения.Вставить("ВозвратнаяТара",    ВозвратнаяТара);
	ДанныеДляЗаполнения.Вставить("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
	ДанныеДляЗаполнения.Вставить("НДСИсчисляетсяНалоговымАгентом", НДСИсчисляетсяНалоговымАгентом);
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

Функция ПодготовитьСтруктуруДляСчетаФактурыУПД_2019(ДеревоДанных)
	
	ДанныеЭД = ДанныеВходящегоЭД(ДеревоДанных);
	
	ДанныеОбъекта = Новый Структура;
	ДанныеОбъекта.Вставить("НомерВходящегоДокумента", ДанныеЭД.НомерДокумента);
	ДанныеОбъекта.Вставить("ДатаВходящегоДокумента",  ДанныеЭД.ДатаДокумента);
	
	ДанныеОбъекта.Вставить("НомерИсходногоДокумента", "");
	ДанныеОбъекта.Вставить("ДатаИсходногоДокумента",  '00010101');
	
	ОпределитьУчастниковПоДаннымЭД_2019(ДеревоДанных, ДанныеОбъекта);
	
	// Если есть сведения о комиссионере, значит нужно создать перевыставленную счет-фактуру.
	Если ДанныеЭД.СведенияОКомиссионере.ТипУчастника.Количество() > 0 Тогда
		НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "СведенияОКомиссионере");
	КонецЕсли;
	
	Если ЭтоВходящаяСчетФактураНаАванс(ДанныеЭД) Тогда
		ДанныеОбъекта.Вставить("ВидСчетаФактуры",Перечисления.ВидСчетаФактурыПолученного.НаАванс);
		КодВидаОперации = "02";
	Иначе
		ДанныеОбъекта.Вставить("ВидСчетаФактуры",Перечисления.ВидСчетаФактурыПолученного.НаПоступление);
		КодВидаОперации = "01";
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("КодВидаОперации", КодВидаОперации);
	
	НомерИсправления = ДанныеЭД.НомерИсправления;
	ДатаИсправления  = ДанныеЭД.ДатаИсправления;
	
	Если ЗначениеЗаполнено(НомерИсправления) Тогда
		ДанныеОбъекта.Вставить("Исправление", Истина);
		ДанныеОбъекта.Вставить("НомерИсправления", НомерИсправления);
		ДанныеОбъекта.Вставить("ДатаИсправления",  ДатаИсправления);
	КонецЕсли;
	
	//Заполняем документы-основания.
	ОснованияСФ = Новый Массив;
	Если ДанныеЭД.ДокументыОснованияСчетаФактуры <> Неопределено Тогда
		Если ТипЗнч(ДанныеЭД.ДокументыОснованияСчетаФактуры) = Тип("Массив") Тогда
			Для Каждого Элемент Из ДанныеЭД.ДокументыОснованияСчетаФактуры Цикл
				ОснованияСФ.Добавить(Элемент);
			КонецЦикла;
			ОснованияСФ = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОснованияСФ);
		Иначе
			ОснованияСФ.Добавить(ДанныеЭД.ДокументыОснованияСчетаФактуры);
		КонецЕсли;
	КонецЕсли;
	ДанныеОбъекта.Вставить("ДокументыОснования", ОснованияСФ);
	
	КодВалюты = ДанныеЭД.ВалютаКод;
	ДанныеОбъекта.Вставить("ВалютаДокумента", НайтиСсылкуНаОбъект("Валюты", КодВалюты));
	
	ДанныеОбъекта.Вставить("СуммаДокумента"   , ДанныеЭД.ВсегоКОплате.ВсегоСтоимостьТоваровСНалогом);
	ДанныеОбъекта.Вставить("СуммаНДСДокумента", ДанныеЭД.ВсегоКОплате.ВсегоСуммаНалога);
	
	ДанныеОбъекта.Вставить("СуммаУменьшение"   , 0);
	ДанныеОбъекта.Вставить("СуммаНДСУменьшение", 0);
	ДанныеОбъекта.Вставить("СуммаУвеличение"   , 0);
	ДанныеОбъекта.Вставить("СуммаНДСУвеличение", 0);
	
	// Авансы
	Если ДанныеОбъекта.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс Тогда
		СведенияОТоварах = ДанныеЭД.СведенияОТоварах;
		ТаблицаАвансы = Новый ТаблицаЗначений;
		ТаблицаАвансы.Колонки.Добавить("Сумма");
		ТаблицаАвансы.Колонки.Добавить("СуммаНДС");
		ТаблицаАвансы.Колонки.Добавить("СтавкаНДС");
		Для Каждого СведенияОТоваре Из СведенияОТоварах Цикл
			СтрокаАвансы = ТаблицаАвансы.Добавить();
			СтрокаАвансы.СтавкаНДС = СведенияОТоваре.НалоговаяСтавка;
			СтрокаАвансы.Сумма     = СведенияОТоваре.СтоимостьТоваровСНалогом;
			СтрокаАвансы.СуммаНДС  = СведенияОТоваре.СуммаНалога;
		КонецЦикла;
		Если ТаблицаАвансы.Количество() > 0 Тогда
			ДанныеОбъекта.Вставить("Авансы", ТаблицаАвансы);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеОбъекта;
	
КонецФункции

Процедура ОпределитьУчастниковПоДаннымЭД_2019(ДеревоДанных, ДанныеОбъекта)
	
	СведенияОПродавце = ДеревоДанных.Строки.Найти("СведенияОПродавце", "ПолныйПуть");
	СведенияОПокупателе = ДеревоДанных.Строки.Найти("СведенияОПокупателе", "ПолныйПуть");
	Если СведенияОПродавце.Строки.Количество() > 1
		ИЛИ СведенияОПокупателе.Строки.Количество() > 1 Тогда
		ТекстИсключения = СтрШаблон(НСтр("ru = 'Ошибка загрузки электронной товарной накладной %1 от %2.'"), 
			ДанныеОбъекта.НомерЭД, Формат(ДанныеОбъекта.ДатаЭД,"ДЛФ=D")) + Символы.ПС
			+ НСтр("ru = 'Загрузка сводных накладных не поддерживается.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ОсновнаяСтрокаПродавца = Неопределено;
	Для каждого СтрокаПродавца Из СведенияОПродавце.Строки Цикл
		ДанныеОбъекта.Вставить("Контрагент", КонтрагентПоДаннымЭД(СтрокаПродавца, "СведенияОПродавце.НомерСтроки"));
		ОсновнаяСтрокаПродавца = СтрокаПродавца;
		Прервать;
	КонецЦикла;
	Для каждого СтрокаПокупателя Из СведенияОПокупателе.Строки Цикл
		НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, СтрокаПокупателя, "СведенияОПокупателе.НомерСтроки");
		Прервать;
	КонецЦикла;
	
	СведенияОГрузоотправителе = ДеревоДанных.Строки.Найти("СведенияОГрузоотправителе", "ПолныйПуть");
	Для каждого СтрокаГрузоотправителя Из СведенияОГрузоотправителе.Строки Цикл
		ГрузоотправительОнЖе = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СтрокаГрузоотправителя, "СведенияОГрузоотправителе.НомерСтроки.ОнЖе");
		Если ГрузоотправительОнЖе = Истина Тогда
			ДанныеОбъекта.Вставить("Грузоотправитель", ДанныеОбъекта.Контрагент);
		Иначе
			ДанныеОбъекта.Вставить("Грузоотправитель",
				КонтрагентПоДаннымЭД(СтрокаГрузоотправителя, "СведенияОГрузоотправителе.НомерСтроки.Грузоотправитель"));
		КонецЕсли;
		Прервать;
	КонецЦикла;
	
	СведенияОГрузополучателе = ДеревоДанных.Строки.Найти("СведенияОГрузополучателе", "ПолныйПуть");
	Для каждого СтрокаГрузополучателя Из СведенияОГрузополучателе.Строки Цикл
		ДанныеОбъекта.Вставить("Грузополучатель", КонтрагентПоДаннымЭД(СтрокаГрузополучателя, "СведенияОГрузополучателе.НомерСтроки"));
		Прервать;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьШтрихкодыТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок, СведенияОТоваре) Экспорт
	
	ШтрихкодыУпаковокСтрокиТЧ = Новый Массив;
	КодыУпаковок = СведенияОТоваре.Строки.Найти(
		"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.ИндивидуальныеУпаковки", "ПолныйПуть", Истина);
	Если КодыУпаковок <> Неопределено И ЗначениеЗаполнено(КодыУпаковок.Значение) Тогда
		Для Каждого СтрокаКодаУпаковки Из КодыУпаковок.Строки Цикл
			ШтрихкодыУпаковокСтрокиТЧ.Добавить(
				ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
					СтрокаКодаУпаковки,
					"СведенияОТоварах.НомерСтроки.СведенияОМаркировке.ИндивидуальныеУпаковки.НомерСтроки.Код"));
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ЗначениеШтрихкода Из ШтрихкодыУпаковокСтрокиТЧ Цикл
		
		НоваяСтрокаТЧШтрихкодыУпаковок = ШтрихкодыУпаковок.Добавить();
		НоваяСтрокаТЧШтрихкодыУпаковок.ЗначениеШтрихкода = ЗначениеШтрихкода;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ШтрихкодыНоменклатуры(Номенклатура)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ 
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Номенклатура = &Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Штрихкод";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Результат = ОбщегоНазначения.ВыгрузитьКолонку(РезультатЗапроса.Выгрузить(), "Штрихкод");
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции 

Функция СтруктураДляСопоставленияНоменклатурыЭД(Номенклатура, СтавкаНДС, Содержание = "", Владелец = Неопределено)

	Результат = Новый Структура("НоменклатураИБ, Наименование, Артикул, СтавкаНДС, ЕдиницаИзмерения, ЕдиницаИзмеренияКод,"
		+ "ШтрихкодКомбинации, ШтрихкодыНоменклатуры, Идентификатор");
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ЗначенияРеквизитовНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура,
		"НаименованиеПолное, Артикул, ВидСтавкиНДС, Услуга, ЕдиницаИзмерения, ЕдиницаИзмерения.Наименование, ЕдиницаИзмерения.Код");
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		Отбор = Новый Структура;
		НоменклатураИБ = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы(
			Номенклатура,, ЗначенияРеквизитовНоменклатуры.ЕдиницаИзмерения);
		Отбор.Вставить("НоменклатураИБ", НоменклатураИБ);
		Отбор.Вставить("Владелец"      , Владелец);
		СоответствиеНоменклатуры = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(Отбор, Истина);
		Если СоответствиеНоменклатуры.Количество() > 0 Тогда
			Результат.Идентификатор = СоответствиеНоменклатуры[0].НоменклатураКонтрагента.Идентификатор;
		КонецЕсли;
	КонецЕсли;
	
	Результат.НоменклатураИБ = Номенклатура;
	Если ЗначенияРеквизитовНоменклатуры.Услуга И ЗначениеЗаполнено(Содержание) Тогда
		Результат.Наименование = Содержание;
	Иначе
		Результат.Наименование = ЗначенияРеквизитовНоменклатуры.НаименованиеПолное;
	КонецЕсли;
	Результат.Артикул = ЗначенияРеквизитовНоменклатуры.Артикул;
	
	Если ЗначениеЗаполнено(СтавкаНДС) И ТипЗнч(СтавкаНДС) = Тип("ПеречислениеСсылка.СтавкиНДС") Тогда
		Результат.СтавкаНДС = СтавкаНДС;
	ИначеЕсли ЗначениеЗаполнено(ЗначенияРеквизитовНоменклатуры.ВидСтавкиНДС) Тогда
		Результат.СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(ЗначенияРеквизитовНоменклатуры.ВидСтавкиНДС,
			ТекущаяДатаСеанса());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначенияРеквизитовНоменклатуры.ЕдиницаИзмерения) Тогда
		Результат.ЕдиницаИзмерения = ЗначенияРеквизитовНоменклатуры.ЕдиницаИзмеренияНаименование;
		Результат.ЕдиницаИзмеренияКод = ЗначенияРеквизитовНоменклатуры.ЕдиницаИзмеренияКод;
	КонецЕсли;
	
	Штрихкоды = ШтрихкодыНоменклатуры(Номенклатура);
	Если ЗначениеЗаполнено(Штрихкоды) Тогда
		Результат.ШтрихкодКомбинации = Штрихкоды[0];
		Результат.ШтрихкодыНоменклатуры = Штрихкоды;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоВходящаяСчетФактураНаАванс(ДанныеЭД)
	
	Результат = Ложь;
	ОбстоятельстваФормированияСФ = "";
	Если ДанныеЭД.ДополнительныеСведенияОбУчастниках.Свойство("ОбстоятельстваФормированияСФ", ОбстоятельстваФормированияСФ)
			И ЗначениеЗаполнено(ОбстоятельстваФормированияСФ) Тогда
		Если ОбстоятельстваФормированияСФ = "2" Тогда
			Результат = Истина;
		Иначе
			Результат = Ложь;
		КонецЕсли;
	Иначе
		// Тег ОбстоятельстваФормированияСФ в документах из роуминга может быть не заполнен,
		// поэтому определяем вид счета-фактуры по косвенным признакам.
		Если ДанныеЭД.ВсегоКОплате.ВсегоСтоимостьТоваровБезНалога = 0 Тогда
			Результат = Истина;
		Иначе
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НеобходимоУказатьЕдиницуИзмерения(Строка, ЭтоСчетФактураНаАванс)
	
	Если ЭтоСчетФактураНаАванс Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Для услуг единицу измерения указывать необязательно, если не указано количество.
	Если ЗначениеЗаполнено(Строка.Товар)
			И Строка.Количество = 0
			И ТипЗнч(Строка.Товар) = Тип("СправочникСсылка.Номенклатура")
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Товар, "Услуга") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область УКД_2020

Процедура ЗаполнитьУКД_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ)
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		СчетФактура = СсылкаНаОбъект;
	Иначе
		СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(СсылкаНаОбъект);
		// Если счета-фактуры не требуется, то формируем УКД со статусом 2.
		Если Не ЗначениеЗаполнено(СчетФактура) И СчетФактураНеТребуется(СсылкаНаОбъект) Тогда
			СтруктураЭД.Функция = "ДИС";
			СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ДИСУКД;
			ЗаполнитьДИС_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СчетФактура) Тогда
		Возврат;
	КонецЕсли;
	
	МассивСчетовФактур = Новый Массив();
	МассивСчетовФактур.Добавить(СчетФактура);
	ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиКорректировочныхСчетовФактур1137(
	МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьКорректировочныхСчетовФактур(Истина, Истина, Истина), Истина);
	Если ТаблицаСчетовФактур = Неопределено
		ИЛИ ТаблицаСчетовФактур.Количество()=0 Тогда
		
		СообщитьОбОшибкеПриФормированииЭД(
		СсылкаНаОбъект, НСтр("ru = 'Возможно, не была произведена корректировка табличной части документа.'"), Отказ);
		Возврат;
	КонецЕсли;
	
	ДанныеДляФормированияЭД = ТаблицаСчетовФактур[0];
	
	ЗаполнитьОсновнуюЧастьУКД_2020(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	ЗаполнитьРасширеннуюЧастьУКД_2020(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);

КонецПроцедуры

Процедура ЗаполнитьДИС_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ)
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить(СсылкаНаОбъект);
	ТаблицаДанных = УчетНДС.ПолучитьДанныеДляПечатиУниверсальногоКорректировочногоДокумента(
		МассивДокументов, Документы[СсылкаНаОбъект.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов());
	Если ТаблицаДанных.Количество() = 0 Тогда
		СообщитьОбОшибкеПриФормированииЭД(
			СсылкаНаОбъект, НСтр("ru = 'Возможно, не была произведена корректировка табличной части документа.'"), Отказ);
		Возврат;
	КонецЕсли;
	ДанныеДляФормированияЭД = ТаблицаДанных[0];
	
	ЗаполнитьОсновнуюЧастьУКД_2020(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);
	ЗаполнитьРасширеннуюЧастьУКД_2020(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);

КонецПроцедуры

Процедура ЗаполнитьКСЧ_2020(СсылкаНаОбъект, СтруктураЭД, ДеревоДанных, Отказ)
	
	МассивСчетовФактур = Новый Массив();
	МассивСчетовФактур.Добавить(СсылкаНаОбъект);
	ТаблицаСчетовФактур = УчетНДС.ПолучитьДанныеДляПечатиКорректировочныхСчетовФактур1137(
			МассивСчетовФактур, Документы.СчетФактураВыданный.ТекстЗапросаПечатьКорректировочныхСчетовФактур(Истина, Истина, Истина), Истина);
	Если ТаблицаСчетовФактур = Неопределено
			ИЛИ ТаблицаСчетовФактур.Количество()=0 Тогда
		
		СообщитьОбОшибкеПриФормированииЭД(
			СсылкаНаОбъект, НСтр("ru = 'Возможно, не была произведена корректировка табличной части документа-основания.'"), Отказ);
		Возврат;
	КонецЕсли;
	
	ДанныеДляФормированияЭД = ТаблицаСчетовФактур[0];
	
	ЗаполнитьОсновнуюЧастьУКД_2020(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных);

КонецПроцедуры

Процедура ЗаполнитьОсновнуюЧастьУКД_2020(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных)
	
	ДанныеШапки = ДанныеДляФормированияЭД.ДанныеШапки;
	
	Если СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.КСЧФДИСУКД
		ИЛИ СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.КСЧФУКД Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СсылкаКорректировочногоСчетаФактуры", ДанныеДляФормированияЭД.СчетФактура);
		
	КонецЕсли;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВидДокумента"  , ВидУКД(ДанныеДляФормированияЭД.Ссылка));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента", ДанныеШапки.Номер);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента" , ДанныеШапки.Дата);
	Если ДанныеШапки.Исправление И СтруктураЭД.ТипЭлементаВерсииЭД <> Перечисления.ТипыЭлементовВерсииЭД.ДИСУКД Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления",
			ДанныеШапки.НомерИсправленияКорректировочного);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления",
			ДанныеШапки.ДатаИсправленияКорректировочного);
	КонецЕсли;
	
	Если ДанныеШапки.ТаблицаРеквизитовОснований.Количество() > 0 Тогда
		Основание = ДанныеШапки.ТаблицаРеквизитовОснований[0];
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента", Основание.НомерСчетаФактуры);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента", Основание.ДатаСчетаФактуры);
		Если Основание.УчитыватьИсправлениеИсходногоДокумента Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправленияИсходногоДокумента",
				Основание.НомерИсправления);
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправленияИсходногоДокумента",
				Основание.ДатаИсправления);
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.ДИСУКД Тогда
		ДанныеОснования = ДанныеОснованияКорректировкиВозврата(ДанныеДляФормированияЭД.Ссылка);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента",
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеОснования.Номер, Истина, Ложь));
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента",
			ДанныеОснования.Дата);
	КонецЕсли;
	
	СчетФактура = Неопределено;
	Если ЭтоДокументСДоговоромНаКомиссионнуюТорговлю(ДанныеДляФормированияЭД.Ссылка)
			И ЭтоКорректировкаПеревыставленногоСчетФактуры(ДанныеДляФормированияЭД.Ссылка, СчетФактура) Тогда
		ДанныеПокупателяПродавца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактура, "Организация, Контрагент, Продавец");
		СведенияОПокупателе   = ПолучитьДанныеЮрФизЛица(ДанныеПокупателяПродавца.Контрагент, ДанныеШапки.Дата);
		СведенияОПоставщике   = ПолучитьДанныеЮрФизЛица(ДанныеПокупателяПродавца.Продавец, ДанныеШапки.Дата);
		СведенияОКомиссионере = ПолучитьДанныеЮрФизЛица(ДанныеПокупателяПродавца.Организация, ДанныеШапки.Дата);
		ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОКомиссионере, "СведенияОКомиссионере", "Юр", ДанныеШапки.Дата);
	Иначе
		СведенияОПокупателе = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Покупатель, ДанныеШапки.Дата);
		СведенияОПоставщике = ПолучитьДанныеЮрФизЛица(ДанныеШапки.Поставщик, ДанныеШапки.Дата, БанковскийСчетПродавца(ДанныеШапки));
		СведенияОПокупателе.ИНН = ДанныеШапки.ИННпокупателя;
		СведенияОПокупателе.КПП = ДанныеШапки.КППпокупателя;
		СведенияОПоставщике.ИНН = ДанныеШапки.ИННпродавца;
		СведенияОПоставщике.КПП = ДанныеШапки.КППпродавца;
	КонецЕсли;
	
	ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПоставщике, "СведенияОПродавце", "Юр", ДанныеШапки.Дата);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"СоставительДокументаНаименование", СоставительДокумента(СведенияОПоставщике));
		
	ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОПокупателе,      "СведенияОПокупателе", "Юр", ДанныеШапки.Дата);
	
	ТекстОшибки = НСтр("ru = 'Не удалось заполнить код валюты документа. Проверьте, что:
	|	- в документе указана валюта,
	|	- для нее заполнен код по Общероссийскому классификатору валют.'");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод", ДанныеШапки.ВалютаКод, ТекстОшибки);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ВалютаНаименование", ДанныеШапки.ВалютаНаименованиеПолное);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДополнительныеСведенияОбУчастниках.ВалютаКурс", ДанныеШапки.ВалютаКурс);
	
	Если ЗначениеЗаполнено(ДанныеШапки.ИдентификаторГосКонтракта) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
			"ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта", ДанныеШапки.ИдентификаторГосКонтракта);
	КонецЕсли;
	
	ДокументыОснования = ОснованияУКД(ДанныеДляФормированияЭД.Ссылка);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
		ДеревоДанных, "ДокументыОснованияСчетаФактуры", ДокументыОснования);
	
	Если СтруктураЭД.ТипЭлементаВерсииЭД = Перечисления.ТипыЭлементовВерсииЭД.КСЧФУКД Тогда // УКД СФ без первичного документа
		
		ОснованиеКорректировки = ПодготовитьТаблицуДанныхДоговораУПД(
			ДанныеШапки.Основание,
			ДанныеШапки.ОснованиеНомер,
			ДанныеШапки.ОснованиеДата);
		ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ОснованиеКорректировки, "ДокументДата",,,
			НСтр("ru = 'Необходимо указать дату договора.'"));
		ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ОснованиеКорректировки, "ОснованиеКорректировки");
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", "Предлагаю изменить стоимость");
		
	КонецЕсли;
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки");
	ТаблицаТоваров.Колонки.Добавить("ТоварКод");
	ТаблицаТоваров.Колонки.Добавить("ТоварНаименование");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияКодДоКорректировки");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияКод");
	ТаблицаТоваров.Колонки.Добавить("НаименованиеЕдиницыИзмеренияДоКорректировки");
	ТаблицаТоваров.Колонки.Добавить("НаименованиеЕдиницыИзмеренияПослеКорректировки");
	ТаблицаТоваров.Колонки.Добавить("КоличествоДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("ЦенаЗаЕдиницуИзмеренияДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("ЦенаЗаЕдиницуИзмерения", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(26,11)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалогаДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалогаУвеличение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровБезНалогаУменьшение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("НалоговаяСтавкаДоКорректировки");
	ТаблицаТоваров.Колонки.Добавить("НалоговаяСтавка");
	ТаблицаТоваров.Колонки.Добавить("СуммаНалогаДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНалога", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНалогаУвеличение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаНалогаУменьшение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаАкцизаДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаАкциза", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаАкцизаУвеличение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СуммаАкцизаУменьшение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогомДоКорректировки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогом", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогомУвеличение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("СтоимостьТоваровСНалогомУменьшение", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(19,2)));
	ТаблицаТоваров.Колонки.Добавить("ТоварИдентификатор", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТаблицаТоваров.Колонки.Добавить("КодВидаТовара", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
	ТаблицаТоваров.Колонки.Добавить("СведенияОМаркировкеДо");
	ТаблицаТоваров.Колонки.Добавить("СведенияОМаркировкеПосле");
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	
	УчетАгентскогоНДСПокупателем = Ложь; // используется для реализации металлолома, сырых шкур животных.
	Если ТипЗнч(ДанныеДляФормированияЭД.Ссылка) = Тип("ДокументСсылка.КорректировкаРеализации")
			ИЛИ ТипЗнч(ДанныеДляФормированияЭД.Ссылка) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		УчетАгентскогоНДСПокупателем = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ДанныеДляФормированияЭД.Ссылка,
			"ДоговорКонтрагента.УчетАгентскогоНДСПокупателем");
	КонецЕсли;
	
	// Штрихкоды упаковок.
	ТаблицаКодовМаркировкиДо = Неопределено;
	ТаблицаКодовМаркировкиПосле = Неопределено;
	Если ДанныеДляФормированияЭД.Владелец().Колонки.Найти("ШтрихкодыУпаковок") <> Неопределено Тогда
		Для Каждого ДокументОснование Из ДокументыОснования Цикл
			Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				ШтрихкодыУпаковокДо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ШтрихкодыУпаковок").Выгрузить();
				ШтрихкодыУпаковокДо.Колонки.ШтрихкодУпаковки.Имя = "Штрихкод";
				ТаблицаКодовМаркировкиДо = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковокДо);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ТаблицаКодовМаркировкиПосле = ДанныеДляФормированияЭД.ШтрихкодыУпаковок;
	КонецЕсли;
	
	НомерСтроки = 1;
	ТолькоУслуги = Истина;
	Для Каждого Строка Из ДанныеДляФормированияЭД.ТаблицаДокумента Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		
		НоваяСтрока.НомерСтроки              = НомерСтроки;
		НоваяСтрока.ТоварКод                 = Строка.ТоварКод;
		
		НаименованиеНоменклатуры = ?(ЗначениеЗаполнено(Строка.НаименованиеНоменклатуры), Строка.НаименованиеНоменклатуры,
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Товар, "НаименованиеПолное"));
		
		НоваяСтрока.ТоварНаименование                              = НаименованиеНоменклатуры;
		НоваяСтрока.ЕдиницаИзмеренияКодДоКорректировки             = СокрЛП(Строка.ЕдиницаИзмеренияКод);
		НоваяСтрока.ЕдиницаИзмеренияКод                            = СокрЛП(Строка.ЕдиницаИзмеренияКод);
		НоваяСтрока.НаименованиеЕдиницыИзмеренияДоКорректировки    = Строка.ЕдиницаИзмеренияНаименование;
		НоваяСтрока.НаименованиеЕдиницыИзмеренияПослеКорректировки = Строка.ЕдиницаИзмеренияНаименование;
		НоваяСтрока.КодВидаТовара                                  =
			?(СтрДлина(Строка.ТоварКодТНВЭД) < 10, "", Строка.ТоварКодТНВЭД);
		
		НоваяСтрока.КоличествоДоКорректировки                = Строка.КоличествоДоИзменения;
		НоваяСтрока.Количество                               = Строка.КоличествоПослеИзменения;
		НоваяСтрока.ЦенаЗаЕдиницуИзмеренияДоКорректировки    = Строка.ЦенаДоИзменения;
		НоваяСтрока.ЦенаЗаЕдиницуИзмерения                   = Строка.ЦенаПослеИзменения;
		НоваяСтрока.СтоимостьТоваровБезНалогаДоКорректировки = Строка.СтоимостьБезНДСДоИзменения;
		НоваяСтрока.СтоимостьТоваровБезНалога                = Строка.СтоимостьБезНДСПослеИзменения;
		НоваяСтрока.СтоимостьТоваровБезНалогаУвеличение      = Строка.РазницаБезНДСУвеличение;
		НоваяСтрока.СтоимостьТоваровБезНалогаУменьшение      = Строка.РазницаБезНДСУменьшение;
		
		СтавкаНДС = ?(УчетАгентскогоНДСПокупателем = Истина, СтавкаНДСИсчисляетсяНалоговымАгентом(), Строка.СтавкаНДС);
		НоваяСтрока.НалоговаяСтавкаДоКорректировки = СтавкаНДС;
		НоваяСтрока.НалоговаяСтавка                = СтавкаНДС;
		НоваяСтрока.СуммаНалогаДоКорректировки = Строка.СуммаНДСДоИзменения;
		НоваяСтрока.СуммаНалога                = Строка.СуммаНДСПослеИзменения;
		НоваяСтрока.СуммаНалогаУвеличение      = Строка.РазницаНДСУвеличение;
		НоваяСтрока.СуммаНалогаУменьшение      = Строка.РазницаНДСУменьшение;
		НоваяСтрока.СтоимостьТоваровСНалогомДоКорректировки = ?(УчетАгентскогоНДСПокупателем = Истина, 0, Строка.СтоимостьСНДСДоИзменения);
		НоваяСтрока.СтоимостьТоваровСНалогом                = ?(УчетАгентскогоНДСПокупателем = Истина, 0, Строка.СтоимостьСНДСПослеИзменения);
		НоваяСтрока.СтоимостьТоваровСНалогомУвеличение      = ?(УчетАгентскогоНДСПокупателем = Истина, 0, Строка.РазницаСНДСУвеличение);
		НоваяСтрока.СтоимостьТоваровСНалогомУменьшение      = ?(УчетАгентскогоНДСПокупателем = Истина, 0, Строка.РазницаСНДСУменьшение);
		НоваяСтрока.ТоварИдентификатор = Строка.Товар.УникальныйИдентификатор();
		
		Если ТолькоУслуги
			И ПризнакТовараЕдиногоДокумента(Строка.Товар) = "1" Тогда
			ТолькоУслуги = Ложь;
		КонецЕсли;
		
		ЗаполнитьСведенияОМаркировкеВУКД_2020(
			НоваяСтрока.СведенияОМаркировкеДо, Строка, ТаблицаКодовМаркировкиДо, Строка.КоличествоДоИзменения);
			
		ЗаполнитьСведенияОМаркировкеВУКД_2020(
			НоваяСтрока.СведенияОМаркировкеПосле, Строка, ТаблицаКодовМаркировкиПосле, Строка.КоличествоПослеИзменения);
		
		НоваяСтрока.Сопоставление = СтруктураДляСопоставленияНоменклатурыЭД(
			Строка.Товар, НоваяСтрока.НалоговаяСтавка, НаименованиеНоменклатуры);
		
		Если ЗначениеЗаполнено(Строка.ТоварКодТНВЭД) И ТипЗнч(Строка.ТоварКодТНВЭД) = Тип("СправочникСсылка.КлассификаторТНВЭД") Тогда
			НоваяСтрока.КодВидаТовара = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.ТоварКодТНВЭД, "Код");
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	ДобавитьОбработкуОшибокЗаполненияТабличнойЧастиЭД(ТаблицаТоваров);
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровБезНалогаУвеличение", ТаблицаТоваров.Итог("СтоимостьТоваровБезНалогаУвеличение"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровСНалогомУвеличение", ТаблицаТоваров.Итог("СтоимостьТоваровСНалогомУвеличение"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСуммаНалогаУвеличение", ТаблицаТоваров.Итог("СуммаНалогаУвеличение"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровБезНалогаУменьшение", ТаблицаТоваров.Итог("СтоимостьТоваровБезНалогаУменьшение"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровСНалогомУменьшение", ТаблицаТоваров.Итог("СтоимостьТоваровСНалогомУменьшение"));
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных,
		"ВсегоИзмененияСтоимости.ВсегоСуммаНалогаУменьшение", ТаблицаТоваров.Итог("СуммаНалогаУменьшение"));
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаТоваров, "СведенияОТоварах");
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ТолькоУслуги", ТолькоУслуги);
	
КонецПроцедуры

Процедура ЗаполнитьРасширеннуюЧастьУКД_2020(ДанныеДляФормированияЭД, СтруктураЭД, ДеревоДанных)
	
	ДанныеШапки = ДанныеДляФормированияЭД.ДанныеШапки;
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "СодержаниеОперации", "Предлагаю изменить стоимость");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаНаправленияНаСогласование", ДанныеШапки.Дата);
	
	ОснованиеКорректировки = ПодготовитьТаблицуДанныхДоговораУПД(
			ДанныеШапки.Основание,
			ДанныеШапки.ОснованиеНомер,
			ДанныеШапки.ОснованиеДата);
			
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ОснованиеКорректировки, "ДокументДата",,,
			НСтр("ru = 'Необходимо указать дату договора.'"));
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ОснованиеКорректировки, "ОснованиеКорректировки");
	
	ТаблицаОснований = Новый ТаблицаЗначений;
	ТаблицаОснований.Колонки.Добавить("ДокументНаименование");
	ТаблицаОснований.Колонки.Добавить("ДокументНомер");
	ТаблицаОснований.Колонки.Добавить("ДокументДата");
	Для Каждого Основание ИЗ ДанныеШапки.ТаблицаРеквизитовОснований Цикл
		Если Основание.УчитыватьИсправлениеИсходногоДокумента Тогда
			ОписаниеОснования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = '№ %1 от %2, с учетом исправления № %3 от %4'"),
									Основание.НомерСчетаФактуры, Формат(Основание.ДатаСчетаФактуры, "ДЛФ=D"),
									Основание.НомерИсправления, Формат(Основание.ДатаИсправления, "ДЛФ=D"));
		Иначе
			ОписаниеОснования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = '№ %1 от %2'"),
									Основание.НомерСчетаФактуры, Формат(Основание.ДатаСчетаФактуры, "ДЛФ=D"));
		КонецЕсли;
		СтрокаТаблицыОснований = ТаблицаОснований.Добавить();
		СтрокаТаблицыОснований.ДокументНаименование = НСтр("ru = 'Универсальный передаточный документ'") + " " + ОписаниеОснования;
		СтрокаТаблицыОснований.ДокументНомер = Основание.НомерСчетаФактуры;
		СтрокаТаблицыОснований.ДокументДата = Основание.ДатаСчетаФактуры;
	КонецЦикла;
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаОснований, "ПередаточныйДокумент");
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, "ИныеСведенияОбИзмененииСтоимости",
																	ДанныеШапки.ИныеСведения);
	
КонецПроцедуры

Функция ПодготовитьСтруктуруДляКорректировкиПоступленияУКД_2020(ДеревоДанных)
	
	ДанныеОбъекта = Новый Структура;
	
	Товары         = Документы.КорректировкаПоступления.ПустаяСсылка().Товары.ВыгрузитьКолонки();
	Услуги         = Документы.КорректировкаПоступления.ПустаяСсылка().Услуги.ВыгрузитьКолонки();
	
	Валюта = НайтиСсылкуНаОбъект("Валюты", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод"));
	ДанныеОбъекта.Вставить("ВалютаДокумента", Валюта);
	КурсВзаиморасчетов = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДополнительныеСведенияОбУчастниках.ВалютаКурс");
	ДанныеОбъекта.Вставить("КурсВзаиморасчетов", ?(ЗначениеЗаполнено(КурсВзаиморасчетов), КурсВзаиморасчетов, 1));
	ДанныеОбъекта.Вставить("КратностьВзаиморасчетов", 1);
	
	ДанныеОбъекта.Вставить("СуммаВключаетНДС", Ложь);
	
	ДанныеОбъекта.Вставить("НомерВходящегоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента"));
	ДанныеОбъекта.Вставить("ДатаВходящегоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента"));
	
	ДанныеОбъекта.Вставить("НомерИсходногоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента"));
	ДанныеОбъекта.Вставить("ДатаИсходногоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента"));
	
	Если ЗначениеЗаполнено(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправленияИсходногоДокумента")) Тогда
		ДанныеОбъекта.Вставить("УчитыватьИсправлениеИсходногоДокумента", Истина);
		ДанныеОбъекта.Вставить("НомерИсправленияИсходногоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправленияИсходногоДокумента"));
		ДанныеОбъекта.Вставить("ДатаИсправленияИсходногоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправленияИсходногоДокумента"));
	Иначе
		ДанныеОбъекта.Вставить("УчитыватьИсправлениеИсходногоДокумента", Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления")) Тогда
		ДанныеОбъекта.Вставить("ВидОперацииЭД", Перечисления.ВидыОперацийЭД.Исправление);
		ДанныеОбъекта.Вставить("Исправление", Истина);
		ДанныеОбъекта.Вставить("НомерИсправления", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления"));
		ДанныеОбъекта.Вставить("ДатаИсправления",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления"));
	Иначе
		ДанныеОбъекта.Вставить("ВидОперацииЭД", Перечисления.ВидыОперацийЭД.Корректировка);
		ДанныеОбъекта.Вставить("Исправление", Ложь);
	КонецЕсли;
	
	ДокументОснование = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДокументыОснованияСчетаФактуры");
	Если ТипЗнч(ДокументОснование) = Тип("Массив") И ДокументОснование.Количество() > 0 Тогда
		ДокументОснование = ДокументОснование[0];
	КонецЕсли;
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ДокументОснование");
	КонецЕсли;
	ДанныеОбъекта.Вставить("Основание", ДокументОснование);
	
	НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "СведенияОПокупателе");
	ДанныеОбъекта.Вставить("Контрагент",  КонтрагентПоДаннымЭД(ДеревоДанных, "СведенияОПродавце"));
	
	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
	ИспользуетсяОбратноеНачислениеНДС = БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
	ШтрихкодыУпаковок = ЭлектронноеВзаимодействиеИСМП.НоваяТаблицаШтрихкодыУпаковок();
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		
		Номенклатура = НоменклатураИБ(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Сопоставление");
		
		Если ЗначениеЗаполнено(Номенклатура)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "Услуга") Тогда
			НоваяСтрока = Услуги.Добавить();
			НоваяСтрока.СодержаниеДоИзменения = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ТоварНаименование");
			НоваяСтрока.Содержание            = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ТоварНаименование");
		Иначе
			НоваяСтрока = Товары.Добавить();
		КонецЕсли;
		
		НоваяСтрока.Номенклатура = Номенклатура;
		
		// Ставка "НДС исчисляется налоговым агентом" используется при продаже металлолома, сырых шкур животных
		СтавкаНДС = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
		Если СтавкаНДС = СтавкаНДСИсчисляетсяНалоговымАгентом() Тогда
			
			Если ИспользуетсяОбратноеНачислениеНДС Тогда
				
				НоваяСтрока.СтавкаНДС = УчетНДСКлиентСервер.ОбщаяСтавкаНДС(
					ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента"));
				НоваяСтрока.СуммаНДСДоКорректировки = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
					ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалогаДоКорректировки"),
					Ложь,
					УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
					
				НоваяСтрока.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
					ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалога"),
					Ложь,
					УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
					
			Иначе
				НоваяСтрока.СтавкаНДС = Неопределено;
				НоваяСтрока.СуммаНДСДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалогаДоКорректировки");
				НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалога");
			КонецЕсли;
			
		Иначе
			НоваяСтрока.СтавкаНДС = СтавкаНДС;
			НоваяСтрока.СуммаНДСДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалогаДоКорректировки");
			НоваяСтрока.СуммаНДС  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СуммаНалога");
		КонецЕсли;
		
		НоваяСтрока.КоличествоДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.КоличествоДоКорректировки");
		НоваяСтрока.Количество = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Количество");
		НоваяСтрока.ЦенаДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЦенаЗаЕдиницуИзмеренияДоКорректировки");
		НоваяСтрока.Цена = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЦенаЗаЕдиницуИзмерения");
		НоваяСтрока.СуммаДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалогаДоКорректировки");
		НоваяСтрока.Сумма = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре, "СведенияОТоварах.НомерСтроки.СтоимостьТоваровБезНалога");
		
		ДобавитьШтрихкодыТаблицуШтрихкодовУпаковокУКД_2020(ШтрихкодыУпаковок, СведенияОТоваре);
		
	КонецЦикла;
	
	ЭлектронноеВзаимодействиеИСМП.СвернутьТаблицуШтрихкодовУпаковок(ШтрихкодыУпаковок);
	
	ДанныеДляЗаполнения = Новый Структура();
	ДанныеДляЗаполнения.Вставить("Шапка"            , ДанныеОбъекта);
	ДанныеДляЗаполнения.Вставить("Товары"           , Товары);
	ДанныеДляЗаполнения.Вставить("Услуги"           , Услуги);
	ДанныеДляЗаполнения.Вставить("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

Функция ПодготовитьСтруктуруДляСчетаФактурыУКД_2020(ДеревоДанных)
	
	ДанныеОбъекта = Новый Структура;
	ДанныеОбъекта.Вставить("НомерВходящегоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерДокумента"));
	ДанныеОбъекта.Вставить("ДатаВходящегоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента"));
	
	НомерИсправления = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления");
	ДатаИсправления  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправления");
	
	Если ЗначениеЗаполнено(НомерИсправления) Тогда
		ДанныеОбъекта.Вставить("Исправление", Истина);
		ДанныеОбъекта.Вставить("НомерИсправления", НомерИсправления);
		ДанныеОбъекта.Вставить("ДатаИсправления",  ДатаИсправления);
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("НомерИсходногоДокумента", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсходногоДокумента"));
	ДанныеОбъекта.Вставить("ДатаИсходногоДокумента",  ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента"));
	
	НомерИсправленияИсходногоДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправленияИсходногоДокумента");
	ДатаИсправленияИсходногоДокумента  = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсправленияИсходногоДокумента");
	
	Если ЗначениеЗаполнено(НомерИсправленияИсходногоДокумента) Тогда
		ДанныеОбъекта.Вставить("УчитыватьИсправлениеИсходногоДокумента", Истина);
		ДанныеОбъекта.Вставить("НомерИсправленияИсходногоДокумента", НомерИсправленияИсходногоДокумента);
		ДанныеОбъекта.Вставить("ДатаИсправленияИсходногоДокумента",  ДатаИсправленияИсходногоДокумента);
	Иначе
		ДанныеОбъекта.Вставить("УчитыватьИсправлениеИсходногоДокумента", Ложь);
	КонецЕсли;
	
	НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, "СведенияОПокупателе");
	ДанныеОбъекта.Вставить("Контрагент",  КонтрагентПоДаннымЭД(ДеревоДанных, "СведенияОПродавце"));
	ДанныеОбъекта.Вставить("Комиссионер", Справочники.Контрагенты.ПустаяСсылка());
	
	// Если счет-фактура на аванс.
	ДанныеОбъекта.Вставить("ВидСчетаФактуры",Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
	ДанныеОбъекта.Вставить("КодВидаОперации", "01");
	
	//Заполняем документы-основания
	МассивДокументовОснований = Новый Массив();
	НайденнаяСтрока = ДеревоДанных.Строки.Найти("ДокументыОснованияСчетаФактуры", "ПолныйПуть");
	Если Не (НайденнаяСтрока = Неопределено Или НайденнаяСтрока.Значение = Неопределено) Тогда // нет данных о документе-основании
		МассивДокументовОснований = НайденнаяСтрока.Значение;
	КонецЕсли;
	ДанныеОбъекта.Вставить("ДокументыОснования", МассивДокументовОснований);
	
	КодВалюты = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВалютаКод");
	ДанныеОбъекта.Вставить("ВалютаДокумента", НайтиСсылкуНаОбъект("Валюты", КодВалюты));
	
	ДанныеОбъекта.Вставить("СуммаДокумента",    0);
	ДанныеОбъекта.Вставить("СуммаНДСДокумента", 0);
	
	ДанныеОбъекта.Вставить("СуммаУменьшение",    ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровСНалогомУменьшение"));
	ДанныеОбъекта.Вставить("СуммаНДСУменьшение", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоИзмененияСтоимости.ВсегоСуммаНалогаУменьшение"));
	ДанныеОбъекта.Вставить("СуммаУвеличение",    ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоИзмененияСтоимости.ВсегоСтоимостьТоваровСНалогомУвеличение"));
	ДанныеОбъекта.Вставить("СуммаНДСУвеличение", ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВсегоИзмененияСтоимости.ВсегоСуммаНалогаУвеличение"));
	
	Возврат ДанныеОбъекта;
	
КонецФункции

#КонецОбласти

#Область ЗапросыНастройкиДополнительныхПолей

Функция ЗапросКонструктораДополнительныхПолейШапкиУПД()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК Ссылка,
	|	ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.РеализацияТоваровУслуг) КАК ДокументОснование
	|ПОМЕСТИТЬ ВТДокументыОснования
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВТДокументыОснования.Ссылка.Номер КАК СсылкаНомер,
	|	ВТДокументыОснования.Ссылка.Дата КАК СсылкаДата,
	|	ВТДокументыОснования.Ссылка.ВидСчетаФактуры КАК СсылкаВидСчетаФактуры,
	|	ВТДокументыОснования.Ссылка.Организация КАК СсылкаОрганизация,
	|	ВТДокументыОснования.Ссылка.Контрагент КАК СсылкаКонтрагент,
	|	ВТДокументыОснования.Ссылка.ДоговорКонтрагента КАК СсылкаДоговорКонтрагента,
	|	ВТДокументыОснования.Ссылка.НомерИсправления КАК СсылкаНомерИсправления,
	|	ВТДокументыОснования.Ссылка.НомерИсправляемогоКорректировочногоДокумента КАК СсылкаНомерИсправляемогоКорректировочногоДокумента,
	|	ВТДокументыОснования.Ссылка.ДатаИсправляемогоКорректировочногоДокумента КАК СсылкаДатаИсправляемогоКорректировочногоДокумента,
	|	ВТДокументыОснования.Ссылка.НомерИсходногоДокумента КАК СсылкаНомерИсходногоДокумента,
	|	ВТДокументыОснования.Ссылка.ДатаИсходногоДокумента КАК СсылкаДатаИсходногоДокумента,
	|	ВТДокументыОснования.Ссылка.Продавец КАК СсылкаПродавец,
	|	ВТДокументыОснования.Ссылка.ДатаПлатежноРасчетногоДокумента КАК СсылкаДатаПлатежноРасчетногоДокумента,
	|	ВТДокументыОснования.Ссылка.НомерПлатежноРасчетногоДокумента КАК СсылкаНомерПлатежноРасчетногоДокумента,
	|	ВТДокументыОснования.Ссылка.ДатаДокументаАвансаКомитента КАК СсылкаДатаДокументаАвансаКомитента,
	|	ВТДокументыОснования.Ссылка.НомерДокументаАвансаКомитента КАК СсылкаНомерДокументаАвансаКомитента,
	|	ВТДокументыОснования.Ссылка.ДатаВыставления КАК СсылкаДатаВыставления,
	|	ВТДокументыОснования.Ссылка.Сумма КАК СсылкаСумма,
	|	ВТДокументыОснования.Ссылка.СуммаНДС КАК СсылкаСуммаНДС,
	|	ВТДокументыОснования.Ссылка.СуммаДокумента КАК СсылкаСуммаДокумента,
	|	ВТДокументыОснования.Ссылка.СуммаУвеличение КАК СсылкаСуммаУвеличение,
	|	ВТДокументыОснования.Ссылка.СуммаУменьшение КАК СсылкаСуммаУменьшение,
	|	ВТДокументыОснования.Ссылка.СуммаНДСДокумента КАК СсылкаСуммаНДСДокумента,
	|	ВТДокументыОснования.Ссылка.СуммаНДСУвеличение КАК СсылкаСуммаНДСУвеличение,
	|	ВТДокументыОснования.Ссылка.СуммаНДСУменьшение КАК СсылкаСуммаНДСУменьшение,
	|	ВТДокументыОснования.Ссылка.Ответственный КАК СсылкаОтветственный,
	|	ВТДокументыОснования.Ссылка.Комментарий КАК СсылкаКомментарий,
	|	ВТДокументыОснования.Ссылка.Руководитель КАК СсылкаРуководитель,
	|	ВТДокументыОснования.Ссылка.ГлавныйБухгалтер КАК СсылкаГлавныйБухгалтер,
	|	ВТДокументыОснования.Ссылка.СуммаДокументаКомиссия КАК СсылкаСуммаДокументаКомиссия,
	|	ВТДокументыОснования.Ссылка.СуммаНДСДокументаКомиссия КАК СсылкаСуммаНДСДокументаКомиссия,
	|	ВТДокументыОснования.Ссылка.СуммаУвеличениеКомиссия КАК СсылкаСуммаУвеличениеКомиссия,
	|	ВТДокументыОснования.Ссылка.СуммаУменьшениеКомиссия КАК СсылкаСуммаУменьшениеКомиссия,
	|	ВТДокументыОснования.Ссылка.СуммаНДСУвеличениеКомиссия КАК СсылкаСуммаНДСУвеличениеКомиссия,
	|	ВТДокументыОснования.Ссылка.СуммаНДСУменьшениеКомиссия КАК СсылкаСуммаНДСУменьшениеКомиссия,
	|	ВТДокументыОснования.ДокументОснование.Номер КАК ДокументОснованиеНомер,
	|	ВТДокументыОснования.ДокументОснование.Дата КАК ДокументОснованиеДата,
	|	ВТДокументыОснования.ДокументОснование.ВидОперации КАК ДокументОснованиеВидОперации,
	|	ВТДокументыОснования.ДокументОснование.Организация КАК ДокументОснованиеОрганизация,
	|	ВТДокументыОснования.ДокументОснование.Склад КАК ДокументОснованиеСклад,
	|	ВТДокументыОснования.ДокументОснование.ПодразделениеОрганизации КАК ДокументОснованиеПодразделениеОрганизации,
	|	ВТДокументыОснования.ДокументОснование.Контрагент КАК ДокументОснованиеКонтрагент,
	|	ВТДокументыОснования.ДокументОснование.ДоговорКонтрагента КАК ДокументОснованиеДоговорКонтрагента,
	|	ВТДокументыОснования.ДокументОснование.ВалютаДокумента КАК ДокументОснованиеВалютаДокумента,
	|	ВТДокументыОснования.ДокументОснование.Грузоотправитель КАК ДокументОснованиеГрузоотправитель,
	|	ВТДокументыОснования.ДокументОснование.Грузополучатель КАК ДокументОснованиеГрузополучатель,
	|	ВТДокументыОснования.ДокументОснование.АдресДоставки КАК ДокументОснованиеАдресДоставки,
	|	ВТДокументыОснования.ДокументОснование.СуммаДокумента КАК ДокументОснованиеСуммаДокумента,
	|	ВТДокументыОснования.ДокументОснование.Ответственный КАК ДокументОснованиеОтветственный,
	|	ВТДокументыОснования.ДокументОснование.Комментарий КАК ДокументОснованиеКомментарий,
	|	ВТДокументыОснования.ДокументОснование.Руководитель КАК ДокументОснованиеРуководитель,
	|	ВТДокументыОснования.ДокументОснование.ГлавныйБухгалтер КАК ДокументОснованиеГлавныйБухгалтер,
	|	ВТДокументыОснования.ДокументОснование.ОтпускПроизвел КАК ДокументОснованиеОтпускПроизвел,
	|	ВТДокументыОснования.ДокументОснование.Перевозчик КАК ДокументОснованиеПеревозчик,
	|	ВТДокументыОснования.ДокументОснование.МаркаАвтомобиля КАК ДокументОснованиеМаркаАвтомобиля,
	|	ВТДокументыОснования.ДокументОснование.РегистрационныйЗнакАвтомобиля КАК ДокументОснованиеРегистрационныйЗнакАвтомобиля,
	|	ВТДокументыОснования.ДокументОснование.Водитель КАК ДокументОснованиеВодитель,
	|	ВТДокументыОснования.ДокументОснование.ВодительскоеУдостоверение КАК ДокументОснованиеВодительскоеУдостоверение,
	|	ВТДокументыОснования.ДокументОснование.КраткоеНаименованиеГруза КАК ДокументОснованиеКраткоеНаименованиеГруза,
	|	ВТДокументыОснования.ДокументОснование.СопроводительныеДокументы КАК ДокументОснованиеСопроводительныеДокументы,
	|	ВТДокументыОснования.ДокументОснование.ДеятельностьНаТорговомСборе КАК ДокументОснованиеДеятельностьНаТорговомСборе,
	|	ВТДокументыОснования.ДокументОснование.ОтветственныйЗаОформление КАК ДокументОснованиеОтветственныйЗаОформление,
	|	ВТДокументыОснования.ДокументОснование.СведенияОТранспортировкеИГрузе КАК ДокументОснованиеСведенияОТранспортировкеИГрузе,
	|	ВТДокументыОснования.ДокументОснование.ПеревозкаАвтотранспортом КАК ДокументОснованиеПеревозкаАвтотранспортом,
	|	ВТДокументыОснования.ДокументОснование.ЕстьМаркируемаяПродукцияГИСМ КАК ДокументОснованиеЕстьМаркируемаяПродукцияГИСМ,
	|	ВТДокументыОснования.ДокументОснование.НомерЧекаККМ КАК ДокументОснованиеНомерЧекаККМ
	|ИЗ
	|	ВТДокументыОснования КАК ВТДокументыОснования";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ЗапросКонструктораДополнительныхПолейШапкиСчетаФактуры()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СчетФактураВыданный.Номер КАК Номер,
	|	СчетФактураВыданный.Дата КАК Дата,
	|	СчетФактураВыданный.ВидСчетаФактуры КАК ВидСчетаФактуры,
	|	СчетФактураВыданный.Организация КАК Организация,
	|	СчетФактураВыданный.Контрагент КАК Контрагент,
	|	СчетФактураВыданный.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СчетФактураВыданный.НомерИсправления КАК НомерИсправления,
	|	СчетФактураВыданный.НомерИсправляемогоКорректировочногоДокумента КАК НомерИсправляемогоКорректировочногоДокумента,
	|	СчетФактураВыданный.ДатаИсправляемогоКорректировочногоДокумента КАК ДатаИсправляемогоКорректировочногоДокумента,
	|	СчетФактураВыданный.НомерИсходногоДокумента КАК НомерИсходногоДокумента,
	|	СчетФактураВыданный.ДатаИсходногоДокумента КАК ДатаИсходногоДокумента,
	|	СчетФактураВыданный.Комитент КАК Комитент,
	|	СчетФактураВыданный.Продавец КАК Продавец,
	|	СчетФактураВыданный.ДатаПлатежноРасчетногоДокумента КАК ДатаПлатежноРасчетногоДокумента,
	|	СчетФактураВыданный.НомерПлатежноРасчетногоДокумента КАК НомерПлатежноРасчетногоДокумента,
	|	СчетФактураВыданный.ДатаДокументаАвансаКомитента КАК ДатаДокументаАвансаКомитента,
	|	СчетФактураВыданный.НомерДокументаАвансаКомитента КАК НомерДокументаАвансаКомитента,
	|	СчетФактураВыданный.ДатаВыставления КАК ДатаВыставления,
	|	СчетФактураВыданный.Сумма КАК Сумма,
	|	СчетФактураВыданный.СуммаНДС КАК СуммаНДС,
	|	СчетФактураВыданный.СуммаДокумента КАК СуммаДокумента,
	|	СчетФактураВыданный.СуммаУвеличение КАК СуммаУвеличение,
	|	СчетФактураВыданный.СуммаУменьшение КАК СуммаУменьшение,
	|	СчетФактураВыданный.СуммаНДСДокумента КАК СуммаНДСДокумента,
	|	СчетФактураВыданный.СуммаНДСУвеличение КАК СуммаНДСУвеличение,
	|	СчетФактураВыданный.СуммаНДСУменьшение КАК СуммаНДСУменьшение,
	|	СчетФактураВыданный.ВалютаДокумента КАК ВалютаДокумента,
	|	СчетФактураВыданный.Руководитель КАК Руководитель,
	|	СчетФактураВыданный.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	СчетФактураВыданный.ИдентификаторГосКонтракта КАК ИдентификаторГосКонтракта
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ЗапросКонструктораДополнительныхПолейШапкиПервичногоДокумента()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.ВидОперации КАК ВидОперации,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РеализацияТоваровУслуг.Склад КАК Склад,
	|	РеализацияТоваровУслуг.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	РеализацияТоваровУслуг.Патент КАК Патент,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияТоваровУслуг.Грузоотправитель КАК Грузоотправитель,
	|	РеализацияТоваровУслуг.Грузополучатель КАК Грузополучатель,
	|	РеализацияТоваровУслуг.АдресДоставки КАК АдресДоставки,
	|	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	|	РеализацияТоваровУслуг.Руководитель КАК Руководитель,
	|	РеализацияТоваровУслуг.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	РеализацияТоваровУслуг.Перевозчик КАК Перевозчик,
	|	РеализацияТоваровУслуг.МаркаАвтомобиля КАК МаркаАвтомобиля,
	|	РеализацияТоваровУслуг.РегистрационныйЗнакАвтомобиля КАК РегистрационныйЗнакАвтомобиля,
	|	РеализацияТоваровУслуг.Водитель КАК Водитель,
	|	РеализацияТоваровУслуг.ВодительскоеУдостоверение КАК ВодительскоеУдостоверение,
	|	РеализацияТоваровУслуг.КраткоеНаименованиеГруза КАК КраткоеНаименованиеГруза,
	|	РеализацияТоваровУслуг.СопроводительныеДокументы КАК СопроводительныеДокументы,
	|	РеализацияТоваровУслуг.ДеятельностьНаТорговомСборе КАК ДеятельностьНаТорговомСборе,
	|	РеализацияТоваровУслуг.ОтветственныйЗаОформление КАК ОтветственныйЗаОформление,
	|	РеализацияТоваровУслуг.СведенияОТранспортировкеИГрузе КАК СведенияОТранспортировкеИГрузе,
	|	РеализацияТоваровУслуг.ПеревозкаАвтотранспортом КАК ПеревозкаАвтотранспортом
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ЭтоКорректировочныйДокумент(СсылкаНаОбъект)
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		Возврат СсылкаНаОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.Корректировочный;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		Возврат СсылкаНаОбъект.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция СтавкаНДСИсчисляетсяНалоговымАгентом()
	
	Возврат "НДС исчисляется налоговым агентом";
	
КонецФункции

Функция ВидЭлектронногоДокументаРеализации(Источник)
	
	Если ТипЗнч(Источник) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ВидЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "ВидЭлектронногоДокумента");
		Если Не ЗначениеЗаполнено(ВидЭД) Тогда
			ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец;
		КонецЕсли;
		Возврат ВидЭД;
	Иначе
		Возврат Источник.ВидЭлектронногоДокумента;
	КонецЕсли;
	
КонецФункции

Функция ВидЭлектронногоДокументаКорректировки(Источник)
	
	ВидЭД = Неопределено;
	Если ЭтоКорректировкаПоСогласованиюСторон(Источник)
			Или ЭтоВозвратТоваровОтПокупателя(Источник) Тогда
		ВидЭД = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель; // будет сформирован УКД
	ИначеЕсли ЭтоИсправлениеВПервичныхДокументах(Источник) Тогда
		ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец; // будет сформирован исправительный документ.
	КонецЕсли;
	
	Возврат ВидЭД;
	
КонецФункции

Функция ВидОперацииКорректировки(Источник)
	
	ВидОперации = Неопределено;
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.КорректировкаРеализации") Тогда
		ВидОперации = Источник.ВидОперации;
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументСсылка.КорректировкаРеализации")
			И ЗначениеЗаполнено(Источник) Тогда
		ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ВидОперации");
	КонецЕсли;
	
	Возврат ВидОперации;
	
КонецФункции

Функция ЭтоКорректировкаПоСогласованиюСторон(Источник)
	
	Возврат (ВидОперацииКорректировки(Источник)
		= Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение);
	
КонецФункции

Функция ЭтоИсправлениеВПервичныхДокументах(Источник)
	
	Возврат (ВидОперацииКорректировки(Источник)
		= Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки);
	
КонецФункции

Функция ЭтоВозвратТоваровОтПокупателя(Источник)

	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровОтПокупателя")
		Или ТипЗнч(Источник) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

Функция ВидУКД(Знач Ссылка)
	
	ДокументОснование = Ссылка;
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ДокументОснование");
	КонецЕсли;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		Возврат "Возврат";
	Иначе
		Возврат "Корректировка"
	КонецЕсли;
	
КонецФункции

Функция СоставительДокумента(СведенияОбОрганизации) 
	
	Результат = СведенияОбОрганизации.СокращенноеНаименование;
	Если СведенияОбОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		Результат = Результат + ", " + НСтр("ru = 'ИНН/КПП'")
			+ " " + СведенияОбОрганизации.ИНН + "/" + СведенияОбОрганизации.КПП;
	Иначе
		Результат = Результат + ", " + НСтр("ru = 'ИНН'")
			+ " " + СведенияОбОрганизации.ИНН;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИсправляемыйСчетФактураВыданный(СчетФактура)
	
	ДокументОснованиеСчетаФактуры = ДокументОснованиеСчетаФактурыВыданного(СчетФактура);
	
	Если ЗначениеЗаполнено(ДокументОснованиеСчетаФактуры) Тогда
		ОснованиеДокумента = Неопределено;
		Если ТипЗнч(ДокументОснованиеСчетаФактуры) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			ОснованиеДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснованиеСчетаФактуры, "ДокументРеализации");
		ИначеЕсли ТипЗнч(ДокументОснованиеСчетаФактуры) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
			ОснованиеДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснованиеСчетаФактуры, "Сделка");
		КонецЕсли;
		Если ЗначениеЗаполнено(ОснованиеДокумента) Тогда
			Возврат УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(ОснованиеДокумента);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ЗаполнитьФИОИДолжность(СтруктураПриемник, ИсточникДанных, Должность = Неопределено)
	
	ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(ИсточникДанных);
	
	СтруктураПриемник.Вставить("Фамилия", ФИО.Фамилия);
	СтруктураПриемник.Вставить("Имя", ФИО.Имя);
	СтруктураПриемник.Вставить("Отчество", ФИО.Отчество);
	Если Должность <> Неопределено Тогда
		СтруктураПриемник.Вставить("Должность", Должность);
	КонецЕсли;
	
КонецПроцедуры

Функция СведенияОТаможеннойДекларации()
	
	ТаможеннаяДекларация = Новый ТаблицаЗначений;
	ТаможеннаяДекларация.Колонки.Добавить("СтранаПроисхожденияКод");
	ТаможеннаяДекларация.Колонки.Добавить("ТаможеннаяДекларацияНомер");
	Возврат ТаможеннаяДекларация;
	
КонецФункции

Функция ПризнакТовараЕдиногоДокумента(Товар)
	
	Если ТипЗнч(Товар) = Тип("СправочникСсылка.Номенклатура") Тогда
		Если ЗначениеЗаполнено(Товар)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Товар, "Услуга") Тогда
			Возврат "3"; // Услуга
		Иначе
			Возврат "1"; // Имущество
		КонецЕсли;
	ИначеЕсли ТипЗнч(Товар) = Тип("СправочникСсылка.ОсновныеСредства") Тогда
		Возврат "1"; // Имущество
	ИначеЕсли ТипЗнч(Товар) = Тип("СправочникСсылка.НематериальныеАктивы") Тогда
		Возврат "4"; // Имущественные права;
	Иначе
		Возврат "5"; // Иное
	КонецЕсли;
	
КонецФункции

Функция ДатаПолученияТовараПриемкиРабот(ВладелецЭД)

	Дата = ТекущаяДатаСеанса();
	ДокументыОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецЭД, "ДокументыОснования").Выгрузить();
	Если ДокументыОснования.Количество() > 0 Тогда
		ДокументОснование = ДокументыОснования[0].ДокументОснование;
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
			Дата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Дата");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Дата;
	
КонецФункции

Процедура ЗаполнитьДанныеУчастника(ДеревоДанных, СведенияОбУчастнике, ВидУчастника, ВидАдреса = "Структурированный", ДатаСведений = '00010101')
	
	Если СведенияОбУчастнике.СтранаРегистрации <> Справочники.СтраныМира.Россия
		И ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ИностраннаяОрганизация") Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИностраннаяОрганизация.НаименованиеОрганизации",
									СведенияОбУчастнике.ПолноеНаименование);
									
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИностраннаяОрганизация.Страна",
									Строка(СведенияОбУчастнике.СтранаРегистрации));
	
	ИначеЕсли СведенияОбУчастнике.ТипЮрФизЛица = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		
		ПутьКРеквизитуДерева = ВидУчастника + ".ТипУчастника.ЮЛ";
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ПутьКРеквизитуДерева + ".ИНН",
									СведенияОбУчастнике.ИНН);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ПутьКРеквизитуДерева + ".КПП",
									СведенияОбУчастнике.КПП);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ПутьКРеквизитуДерева + ".НаименованиеОрганизации",
									СведенияОбУчастнике.ПолноеНаименование);
									
	Иначе
		
		ПутьКРеквизитуДерева = ВидУчастника + ".ТипУчастника";
		Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ПутьКРеквизитуДерева + ".ИП") Тогда
			ПутьКРеквизитуДерева = ПутьКРеквизитуДерева + ".ИП";
		Иначе
			ПутьКРеквизитуДерева = ПутьКРеквизитуДерева + ".ФЛ";
		КонецЕсли;
		
		Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(
			ДеревоДанных,
			ПутьКРеквизитуДерева + ".ПолноеНаименование") Тогда
			
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ПутьКРеквизитуДерева + ".ПолноеНаименование",
				СведенияОбУчастнике.НаименованиеДляПечатныхФорм);
				
		КонецЕсли;
			
		Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(
			ДеревоДанных,
			ПутьКРеквизитуДерева + ".СвидетельствоОГосРегистрации")
			И ЗначениеЗаполнено(СведенияОбУчастнике.Свидетельство) Тогда
			
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ПутьКРеквизитуДерева + ".СвидетельствоОГосРегистрации",
				СведенияОбУчастнике.Свидетельство);
				
		КонецЕсли;
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ПутьКРеквизитуДерева + ".ИНН",
									СведенияОбУчастнике.ИНН);
		
		ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(СведенияОбУчастнике.ПолноеНаименование);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ПутьКРеквизитуДерева + ".Фамилия",
									ФИО.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ПутьКРеквизитуДерева + ".Имя",
									ФИО.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ПутьКРеквизитуДерева + ".Отчество",
									ФИО.Отчество);
									
	КонецЕсли;
	
	АдресУчастника = Новый Структура;
	ПолучитьАдресСтруктурой(АдресУчастника, СведенияОбУчастнике.ЮрФизЛицоСсылка, ВидАдреса, ДатаСведений);
	
	Если ЗначениеЗаполнено(АдресУчастника) Тогда
		ПолныйПуть = ВидУчастника + ".Адрес.Иностранный";
		Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ПолныйПуть) Тогда
			ТипАдреса = "Иностранный";
		Иначе
			ТипАдреса = "Произвольный";
		КонецЕсли;
		
		Если СведенияОбУчастнике.Свойство("АдресДоставки") Тогда
			АдресУчастника.АдресТекст = СведенияОбУчастнике.АдресДоставки;
		КонецЕсли;
		ЗаполнитьАдресВДереве(ДеревоДанных, АдресУчастника, ТипАдреса, ВидУчастника);
		
	КонецЕсли;
	
	ПолныйПуть = ВидУчастника + ".Контакт.Телефон";
	Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ПолныйПуть) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								ВидУчастника + ".Контакт.Телефон",
								СведенияОбУчастнике.Телефоны);
	КонецЕсли;
	
	ПолныйПуть = ВидУчастника + ".БанковскийСчет";
	НомерСчета = "";
	Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета)
		И ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ПолныйПуть) Тогда
		Банк = "";
		БИК = "";
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".БанковскийСчет.НомерСчета",
				НомерСчета);
		Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".БанковскийСчет.НаимБанк",
										Банк);
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".БанковскийСчет.БИК",
										БИК);
		КонецЕсли;
	КонецЕсли;
	
	ПолныйПуть = ВидУчастника + ".Руководитель";
	Руководитель = Неопределено;
	Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ПолныйПуть)
		И СведенияОбУчастнике.Свойство("Руководитель", Руководитель)
		И ЗначениеЗаполнено(Руководитель) Тогда
		
		ФИОРуководителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Руководитель, "Фамилия, Имя, Отчество");
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть + ".Фамилия",  ФИОРуководителя.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть + ".Имя",      ФИОРуководителя.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть + ".Отчество", ФИОРуководителя.Отчество);
		
		Если СведенияОбУчастнике.Свойство("ДолжностьРуководителяПредставление")
			И ЗначениеЗаполнено(СведенияОбУчастнике.ДолжностьРуководителяПредставление) Тогда
			
			ДолжностьРуководителя = СведенияОбУчастнике.ДолжностьРуководителяПредставление;
		Иначе
			ДолжностьРуководителя = НРег("ru = 'Директор'");
		КонецЕсли;
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть + ".Должность", ДолжностьРуководителя);
		
	КонецЕсли;
	
	ПолныйПуть = ВидУчастника + ".Комментарий";
	Комментарий = "";
	Если ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(ДеревоДанных, ПолныйПуть)
		И СведенияОбУчастнике.Свойство("Комментарий", Комментарий) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть, Комментарий);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьАдресВДереве(ДеревоДанных, АдресУчастника, ТипАдреса, ВидУчастника)
	
	Если ТипАдреса = "Произвольный" Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".Адрес.Произвольный",
									АдресУчастника.АдрТекст);
	Иначе
		Если ТипАдреса = "Структурированный" Тогда
			АдресУчастника.Удалить("АдресРФ");
			АдресУчастника.Удалить("КодСтр");
			АдресУчастника.Удалить("КодРегиона");
			АдресУчастника.Удалить("КодСтраны");
			АдресУчастника.Удалить("НаселенныйПункт");
			АдресУчастника.Удалить("АдрТекст");
			АдресУчастника.Удалить("АдресТекст");
			АдресУчастника.Удалить("Квартира");
			АдресУчастника.Удалить("КодГАР");
		Иначе
			АдресУчастника.Удалить("АдресРФ");
			АдресУчастника.Удалить("ПочтовыйИндекс");
			АдресУчастника.Удалить("Индекс");
			АдресУчастника.Удалить("Регион");
			АдресУчастника.Удалить("КодРегион");
			АдресУчастника.Удалить("КодРегиона");
			АдресУчастника.Удалить("Район");
			АдресУчастника.Удалить("Город");
			АдресУчастника.Удалить("НаселенныйПункт");
			АдресУчастника.Удалить("НаселПункт");
			АдресУчастника.Удалить("Улица");
			АдресУчастника.Удалить("Дом");
			АдресУчастника.Удалить("Корпус");
			АдресУчастника.Удалить("Квартира");
			АдресУчастника.Удалить("Кварт");
			АдресУчастника.Удалить("КодГАР");
		КонецЕсли;
		Для Каждого Элемент Из АдресУчастника Цикл
			
			ИмяПоля = ВидУчастника + ".Адрес." + ТипАдреса + "." + Элемент.Ключ;
			
			Если Не ДеревоДанных.Строки.Найти(ИмяПоля, "ПолныйПуть", Истина) = Неопределено Тогда
				
				ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
								ДеревоДанных,
								ИмяПоля,
								Элемент.Значение);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция РассчитатьИтоговыеСуммыДокумента(ТаблицаДокумента)
	
	ИтоговыеСуммы = ИтоговыеСуммыДокумента();
	
	Для Каждого Строка ИЗ ТаблицаДокумента Цикл
		
		ИтоговыеСуммы.ИтогоМест        = ИтоговыеСуммы.ИтогоМест        + ?(ЗначениеЗаполнено(Строка.КоличествоМест),Строка.КоличествоМест, 0);
		ИтоговыеСуммы.ИтогоСуммаБезНДС = ИтоговыеСуммы.ИтогоСуммаБезНДС + Строка.СуммаБезНДС;
		ИтоговыеСуммы.ИтогоНДС         = ИтоговыеСуммы.ИтогоНДС         + Строка.СуммаНДС;
		ИтоговыеСуммы.ИтогоСуммаСНДС   = ИтоговыеСуммы.ИтогоСуммаСНДС   + Строка.СуммаБезНДС + Строка.СуммаНДС;
		
	КонецЦикла;
	
	ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписей = ТаблицаДокумента.Количество();
	
	Возврат ИтоговыеСуммы;
	
КонецФункции

Функция ПолучитьИДНоменклатуры(Номенклатура, Характеристика, Упаковка) 
	
	ИДТовара = Номенклатура.УникальныйИдентификатор();
	ИДХарактеристики = ?(ЗначениеЗаполнено(Характеристика), Характеристика.УникальныйИдентификатор(), "");
	ИДУпаковки = ?(ЗначениеЗаполнено(Упаковка), Упаковка.УникальныйИдентификатор(), "");
	
	Возврат Строка(ИДТовара) + "#" + Строка(ИДХарактеристики) + "#" + Строка(ИДУпаковки);
	
КонецФункции

Процедура ЗаполнитьДанныеФизическогоЛица(ДеревоДанных, ТипЛица, СтруктураФИО, НаименованиеДолжности = "")
	
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Фамилия", СтруктураФИО.Фамилия);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Имя", СтруктураФИО.Имя);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Отчество", СтруктураФИО.Отчество);
	Если ЗначениеЗаполнено(НаименованиеДолжности) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДанных, ТипЛица + ".Должность", НаименованиеДолжности);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначениеРеквизита(СтрокаДерева, ИмяРеквизита, ВключатьПодчиненные = Ложь, ДеревоРазбора = Неопределено)
	
	Результат = Неопределено;
	
	Если СтрокаДерева.Строки.Количество()>0 Тогда
		НайденнаяСтрока = СтрокаДерева.Строки.Найти(ИмяРеквизита, "Реквизит", ВключатьПодчиненные);
	Иначе
		НайденнаяСтрока = СтрокаДерева;
	КонецЕсли;
	
	Если НайденнаяСтрока <> Неопределено Тогда
		Результат = НайденнаяСтрока.ЗначениеРеквизита;
		// Если реквизит ссылочного типа (передали реквизит ДеревоРазбора),
		// тогда нашли всего лишь индекс строки
		Если ЗначениеЗаполнено(ДеревоРазбора) Тогда 
			НайденнаяСтрока = ДеревоРазбора.Строки.Найти(Результат, "ИндексСтроки", Истина);
			Если НайденнаяСтрока <> Неопределено Тогда
				Результат = НайденнаяСтрока.СсылкаНаОбъект;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДокументОснованиеСчетаФактурыВыданного(СчетФактура)
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("СчетФактура", СчетФактура);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СчетФактураВыданныйДокументыОснования.ДокументОснование
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|ГДЕ
	|	СчетФактураВыданныйДокументыОснования.Ссылка = &СчетФактура
	|	И СчетФактураВыданныйДокументыОснования.НомерСтроки = 1";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ДокументОснование;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ДокументОснованиеСчетаФактурыПолученного(СчетФактура)
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("СчетФактура", СчетФактура);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СчетФактураПолученныйДокументыОснования.ДокументОснование
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|ГДЕ
	|	СчетФактураПолученныйДокументыОснования.Ссылка = &СчетФактура
	|	И СчетФактураПолученныйДокументыОснования.НомерСтроки = 1";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ДокументОснование;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ЗаполнитьНомерТДИСтрануПроисхождения(СтрокаТаблицы, Данные, ЭтоКорректировка = Ложь)
	
	Если Данные.Свойство("НомерТД") Тогда
		СтрокаТаблицы.НомерГТД = НомерТДПолучитьСсылку(Данные.НомерТД);
	КонецЕсли;
	
	Если Данные.Свойство("КодСтраныПроисхождения") Тогда
		СтрокаТаблицы.СтранаПроисхождения = СтранаПроисхожденияПолучитьСсылку(Данные.КодСтраныПроисхождения);
	КонецЕсли;
	
	Если ЭтоКорректировка И Данные.Свойство("НомерТДДоИзменения") Тогда
		СтрокаТаблицы.НомерГТДДоИзменения = НомерТДПолучитьСсылку(Данные.НомерТДДоИзменения);
	КонецЕсли;
	
	Если ЭтоКорректировка И Данные.Свойство("КодСтраныПроисхожденияДоИзменения") Тогда
		СтрокаТаблицы.СтранаПроисхожденияДоИзменения = СтранаПроисхожденияПолучитьСсылку(Данные.КодСтраныПроисхожденияДоИзменения);
	КонецЕсли; 

КонецПроцедуры

Функция НомерТДПолучитьСсылку(НомерТД)
	
	Если Не ЗначениеЗаполнено(НомерТД) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НомерТД = СокрЛП(НомерТД);
	НомерТДСсылка = Справочники.НомераГТД.НайтиПоКоду(НомерТД);
	Если Не ЗначениеЗаполнено(НомерТДСсылка) Тогда
		
		НомерТДОбъект = Справочники.НомераГТД.СоздатьЭлемент();
		НомерТДОбъект.Код = НомерТД;
		НомерТДОбъект.Записать();
		НомерТДСсылка = НомерТДОбъект.Ссылка;
		
	КонецЕсли; 
	
	Возврат НомерТДСсылка;
	
КонецФункции

Функция СтранаПроисхожденияПолучитьСсылку(КодСтраныПроисхождения)
	
	Если Не ЗначениеЗаполнено(КодСтраныПроисхождения) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		СтранаПроисхожденияСсылка = УправлениеКонтактнойИнформацией.СтранаМираПоКодуИлиНаименованию(СокрЛП(КодСтраныПроисхождения));
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось найти страну происхождения в классификаторе стран мира.");
		Возврат Неопределено;
	КонецПопытки;
	
	Если ЗначениеЗаполнено(СтранаПроисхожденияСсылка) Тогда
		Возврат СтранаПроисхожденияСсылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // СтранаПроисхожденияПолучитьСсылку()

// Подготавливает структуру данных контрагента к отправке в составе ЭД, оставляет в структуре данных только простые типы.
//
// Параметры:
//  Контрагент	 - СправочникСсылка.Контрагенты - контрагент, по данные которого нужно подготовить
//  ДатаСведений - Дата - дата на которую нужно получить данные
// 
// Возвращаемое значение:
//  Структура - структура данных контрагента, подготовленный к отправке
//
Функция ДанныеКонтрагентаСтруктурой(Контрагент, ДатаСведений = Неопределено)
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ДанныеКонтрагента = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Контрагент, ДатаСведений);
	Результат = Новый Структура;
	Если ДанныеКонтрагента.Свойство("ЮридическоеФизическоеЛицо")
		И ДанныеКонтрагента.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		
		Результат.Вставить("ЮридическоеФизическоеЛицо", "ФЛ");
	Иначе
		Результат.Вставить("ЮридическоеФизическоеЛицо", "ЮЛ");
	КонецЕсли;
	
	Если ДанныеКонтрагента.Свойство("СтранаРегистрации")
		И ЗначениеЗаполнено(ДанныеКонтрагента.СтранаРегистрации) Тогда
		
		Результат.Вставить("КодСтраныРегистрации",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеКонтрагента.СтранаРегистрации, "Код"));
			
	КонецЕсли;
	
	// оставим только простые типы к передаче в составе ЭД
	Для Каждого ЭлементСтруктуры Из ДанныеКонтрагента Цикл
		Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Строка")
			ИЛИ ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Число")
			ИЛИ ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Булево")
			ИЛИ ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Дата") Тогда
			
			Результат.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Производит поиск контрагента по ИНН и КПП. Создает нового контрагента, если не находит.
//
// Параметры:
//  ДанныеКонтрагента	 - Структура - данные контрагента
// 
// Возвращаемое значение:
//  СправочникСсылка.Контрагенты - ссылка на найденного или созданного контрагента
//
Функция ПолучитьСоздатьКонтрагента(ДанныеКонтрагента)
	
	Если ТипЗнч(ДанныеКонтрагента) <> Тип("Структура") Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ИНН = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "ИНН");
	КПП = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "КПП");
	
	КонтрагентСсылка = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ИНН, КПП);
	Если ЗначениеЗаполнено(КонтрагентСсылка) Тогда // если в ИБ уже есть такой контрагент, не создаем новый и не редактируем существующий
		
		Возврат КонтрагентСсылка;
		
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
	
		КонтрагентОбъект = Справочники.Контрагенты.СоздатьЭлемент();
		Если ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "ЮридическоеФизическоеЛицо") = "ФЛ" Тогда
			КонтрагентОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		Иначе
			КонтрагентОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		КонецЕсли;
		
		КонтрагентОбъект.Наименование			 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "СокращенноеНаименование");
		КонтрагентОбъект.НаименованиеПолное		 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "ПолноеНаименование");
		КонтрагентОбъект.ИНН					 = ИНН;
		КонтрагентОбъект.КПП					 = КПП;
		КонтрагентОбъект.РегистрационныйНомер	 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "ОГРН");
		
		// ИП
		КонтрагентОбъект.СвидетельствоСерияНомер		 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "СвидетельствоСерияНомер");
		КонтрагентОбъект.СвидетельствоДатаВыдачи		 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "СвидетельствоДатаВыдачи");
		КонтрагентОбъект.ДокументУдостоверяющийЛичность = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "ДокументУдостоверяющийЛичность");
		
		// Страна регистрации
		КодСтраныРегистрации = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "КодСтраныРегистрации");
		Если ЗначениеЗаполнено(КодСтраныРегистрации) Тогда
			
			КонтрагентОбъект.СтранаРегистрации = УправлениеКонтактнойИнформацией.СтранаМираПоКодуИлиНаименованию(
				КодСтраныРегистрации);
			
		КонецЕсли;
		
		// Контактная информация
		ЮрАдрес			 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "ЗначениеЮридическийАдрес");
		ФактАдрес		 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "ЗначениеФактическийАдрес");
		ПочтовыйАдрес	 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "ЗначениеПочтовыйАдрес");
		Телефон			 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "Телефоны");
		АдресЭП			 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "Email");
		Если ЗначениеЗаполнено(ЮрАдрес) Тогда
			
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(КонтрагентОбъект,
				ЮрАдрес, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
			
		КонецЕсли;
		Если ЗначениеЗаполнено(ФактАдрес) Тогда
			
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(КонтрагентОбъект,
				ФактАдрес, Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
			
		КонецЕсли;
		Если ЗначениеЗаполнено(ПочтовыйАдрес) Тогда
			
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(КонтрагентОбъект,
				ПочтовыйАдрес, Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента);
			
		КонецЕсли;
		Если ЗначениеЗаполнено(Телефон) Тогда
			
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(КонтрагентОбъект,
				Телефон, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
			
		КонецЕсли;
		Если ЗначениеЗаполнено(АдресЭП) Тогда
			
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(КонтрагентОбъект,
				АдресЭП, Справочники.ВидыКонтактнойИнформации.EmailКонтрагенты);
			
		КонецЕсли;
		
		// Банковский счет
		БИК				 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "БИК");
		НомерСчета		 = ПолучитьСвойствоСтруктуры(ДанныеКонтрагента, "НомерСчета");
		БанковскийСчет	 = Неопределено;
		Если ЗначениеЗаполнено(НомерСчета) И ЗначениеЗаполнено(БИК) Тогда
			
			Банк = Справочники.Банки.СсылкаНаБанк(БИК);
			Если Не ЗначениеЗаполнено(Банк) Тогда
				
				БанкИзКлассификатора = РаботаСБанкамиБП.СсылкаПоКлассификатору(БИК);
				Если ЗначениеЗаполнено(БанкИзКлассификатора) Тогда
					
					РаботаСБанкамиБП.ПодобратьБанкИзКлассификатора(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(БанкИзКлассификатора));
					
					// РаботаСБанкамиБП.ПодобратьБанкИзКлассификатора() возвращает массив, содержащий кроме ссылок на банк группы банков,
					// поэтому проще использовать Справочники.Банки.СсылкаНаБанк()
					Банк = Справочники.Банки.СсылкаНаБанк(БИК);
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Банк) Тогда
				
				КонтрагентОбъект.ОсновнойБанковскийСчет = Справочники.БанковскиеСчета.ПолучитьСсылку();
				БанковскийСчет = Справочники.БанковскиеСчета.СоздатьЭлемент();
				БанковскийСчет.УстановитьСсылкуНового(КонтрагентОбъект.ОсновнойБанковскийСчет);
				БанковскийСчет.Банк			 = Банк;
				БанковскийСчет.НомерСчета	 = НомерСчета;
				БанковскийСчет.Наименование	 = БанковскиеСчетаФормыКлиентСервер.НаименованиеБанковскогоСчета(БанковскийСчет, 
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Наименование"));
				Если БанковскиеПравила.ЭтоРублевыйСчет(НомерСчета) Тогда
					
					БанковскийСчет.ВалютаДенежныхСредств = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
					БанковскийСчет.Валютный = Ложь;
					
				Иначе
					
					КодВалюты = БанковскиеПравила.КодВалютыБанковскогоСчета(НомерСчета);
					БанковскийСчет.ВалютаДенежныхСредств = БанковскиеСчетаВызовСервера.ПолучитьВалютуПоКоду(КодВалюты);
					БанковскийСчет.Валютный = Истина;
					
				КонецЕсли;

			КонецЕсли;
			
		КонецЕсли;
		
		КонтрагентОбъект.Записать();
		КонтрагентСсылка = КонтрагентОбъект.Ссылка;
		Если БанковскийСчет <> Неопределено Тогда
			
			БанковскийСчет.Владелец = КонтрагентСсылка;
			БанковскийСчет.Записать();
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
		Возврат КонтрагентСсылка;
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстСообщения);
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

Функция ПолучитьНаименованиеНоменклатуры(Знач Наименование, МаксДлина = 250)
	
	Наименование = СокрЛП(Наименование);
	Если СтрДлина(Наименование) > МаксДлина Тогда
		
		Возврат Лев(Наименование, МаксДлина);
		
	Иначе
		
		Возврат Наименование;
		
	КонецЕсли;
	
КонецФункции

Процедура ЗаписатьДокумент(ДокументОбъект, РежимЗаписи)
	
	ОписаниеОшибки = "";
	Если ДатыЗапретаИзменения.ИзменениеЗапрещено(ДокументОбъект,, ОписаниеОшибки) Тогда
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	// Документ записывается дважды:
	// Первый раз запись происходит с установкой признака ОбменДанными.Загрузка = Истина
	// Второй раз запись выполняется без установленного признака ОбменДанными.Загрузка
	
	Попытка
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ЕстьСоглашение", Истина);
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать();
		
	Исключение
		
		ЭлектронноеВзаимодействиеБПВызовСервера.ОбработатьИсключениеПоЭДНаСервере(
						НСтр("ru = 'Запись документа ИБ'"), 
						ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	Попытка
		
		ДокументОбъект.ОбменДанными.Загрузка = Ложь;
		ДокументОбъект.Записать(РежимЗаписи);
		
	Исключение
		
		ЭлектронноеВзаимодействиеБПВызовСервера.ОбработатьИсключениеПоЭДНаСервере(
						НСтр("ru = 'Запись документа ИБ'"), 
						ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
КонецПроцедуры

Функция ИтоговыеСуммыДокумента()
	
	Структура = Новый Структура;
	
	// Инициализация итогов по документу.
	Структура.Вставить("ИтогоМест", 0);
	Структура.Вставить("ИтогоСуммаСНДС", 0);
	Структура.Вставить("ИтогоСуммаБезНДС", 0);
	Структура.Вставить("ИтогоНДС", 0);
	Структура.Вставить("ИтогоСуммаСНДСДоКорректировки", 0);
	Структура.Вставить("ИтогоСуммаДоКорректировки", 0);
	Структура.Вставить("ИтогоНДСДоКорректировки", 0);
	Структура.Вставить("ИтогоМассаБрутто", 0);
	Структура.Вставить("ИтогоМассаНетто", 0);
	
	Структура.Вставить("КоличествоПорядковыхНомеровЗаписей", 0);
	Структура.Вставить("СуммаПрописью", "");
	
	Возврат Структура;
	
КонецФункции

Функция ПолучитьКорректныйКодСтраны(КодСтраны)
	
	Если КодСтраны = NULL Тогда
		Возврат "";
	КонецЕсли;
	
	Для К=1 По СтрДлина(КодСтраны) Цикл
		
		СимволКода = Сред(КодСтраны, К, 1);
		Если Найти("0123456789", СимволКода)=0 Тогда
			//Код страны в электронном документе должен содеражать только цифры
			Возврат "";
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат КодСтраны;
	
КонецФункции

// Поиск организации при загрузке электронных документов, регламентированных ФНС.
Процедура НайтиОрганизациюПоДаннымЭД(ДанныеОбъекта, ДеревоДанных, ВидУчастника)
	
	Организация = Неопределено;
	ПодразделениеОрганизации = Неопределено;
	
	ТипУчастника = СокрЛП(ВРег(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника")));
	ИНН = "";
	КПП = "";
	ДатаВходящегоДокумента = Неопределено;
	ДанныеОбъекта.Свойство("ДатаВходящегоДокумента", ДатаВходящегоДокумента);
	Если ТипУчастника = "ЮЛ" Тогда
		
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ЮЛ.ИНН");
		КПП = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ЮЛ.КПП");
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Организации.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.ИНН = &ИНН
			|	И Организации.КПП = &КПП
			|	И Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
			|	И НЕ Организации.ПометкаУдаления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Организации.Ссылка КАК Ссылка,
			|	Организации.КПП КАК КПП
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.ИНН = &ИНН
			|	И Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
			|	И НЕ Организации.ПометкаУдаления";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Запрос.УстановитьПараметр("КПП", КПП);
		
		Результат = Запрос.ВыполнитьПакет();
		ВыборкаПоИННКПП = Результат[0].Выбрать();
		Если ВыборкаПоИННКПП.Следующий() Тогда // найдено точное соответствие
			Организация = ВыборкаПоИННКПП.Ссылка;
		Иначе // нет точного соответствия, возможно у организации изменился КПП, или это обособленное подразделение
			ВыборкаПоИНН = Результат[1].Выбрать();
			Если ЗначениеЗаполнено(КПП) Тогда
				ЭтоКППОбособленногоПодразделения = ЭтоКППОбособленногоПодразделения(КПП);
				Пока ВыборкаПоИНН.Следующий() Цикл
					КППНаДату = Справочники.Организации.КППНаДату(ВыборкаПоИНН.Ссылка, ДатаВходящегоДокумента);
					Если КППНаДату = КПП Тогда
						Организация = ВыборкаПоИНН.Ссылка;
					ИначеЕсли ЭтоКППОбособленногоПодразделения
							И ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеПодразделения") Тогда
						ПодразделениеОрганизации = ОбособленноеПодразделениеОрганизацииПоКПП(ВыборкаПоИНН.Ссылка, КПП);
						Если ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
							Организация = ВыборкаПоИНН.Ссылка;
						КонецЕсли;
					КонецЕсли;
					Если ЗначениеЗаполнено(Организация) Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе // в ЭД не указан КПП организации
				Если ВыборкаПоИНН.Следующий() Тогда
					Организация = ВыборкаПоИНН.Ссылка;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипУчастника = "ИП" Тогда
		
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ИП.ИНН");
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Организации.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.ИНН = &ИНН
			|	И Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
			|	И НЕ Организации.ПометкаУдаления";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Организация = Выборка.Ссылка;
		КонецЕсли;
		
	ИначеЕсли ТипУчастника = "ФЛ" Тогда
		
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ФЛ.ИНН");
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Организации.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.ИНН = &ИНН
			|	И Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
			|	И НЕ Организации.ПометкаУдаления";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Организация = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	// Если организация не найдена, то вызываем исключение и не создаем документ ИБ на основании ЭД.
	Если Не ЗначениеЗаполнено(Организация) Тогда
		
		Если ЗначениеЗаполнено(КПП) Тогда
			Шаблон = НСтр("ru = 'Не удалось создать документ, т.к. организация с ИНН/КПП %1/%2 не найдена'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, ИНН, КПП);
		Иначе
			Шаблон = НСтр("ru = 'Не удалось создать документ, т.к. организация с ИНН %1 не найдена'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, ИНН);
		КонецЕсли;
		
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("Организация", Организация);
	Если ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
		ДанныеОбъекта.Вставить("ПодразделениеОрганизации", ПодразделениеОрганизации);
	КонецЕсли;
	
КонецПроцедуры

// Поиск организации при загрузке электронных документов формата CML.
Процедура НайтиОрганизациюПоДаннымЭДCML(ДанныеОбъекта, ИНН, КПП)
	
	Организация = Неопределено;
	ПодразделениеОрганизации = Неопределено;
	
	ДатаВходящегоДокумента = Неопределено;
	ДанныеОбъекта.Свойство("ДатаВходящегоДокумента", ДатаВходящегоДокумента);
	
	Если ЗначениеЗаполнено(КПП) Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Организации.Ссылка КАК Ссылка,
			|	Организации.КПП КАК КПП
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.ИНН = &ИНН
			|	И Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
			|	И НЕ Организации.ПометкаУдаления";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			ЭтоКППОбособленногоПодразделения = ЭтоКППОбособленногоПодразделения(КПП);
			Пока Выборка.Следующий() Цикл
				КППНаДату = Справочники.Организации.КППНаДату(Выборка.Ссылка, ДатаВходящегоДокумента);
				Если КППНаДату = КПП Тогда
					Организация = Выборка.Ссылка;
				ИначеЕсли ЭтоКППОбособленногоПодразделения
					И ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеПодразделения") Тогда
					
					ПодразделениеОрганизации = ОбособленноеПодразделениеОрганизацииПоКПП(Выборка.Ссылка, КПП);
					Если ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
						Организация = Выборка.Ссылка;
					КонецЕсли;
				КонецЕсли;
				Если ЗначениеЗаполнено(Организация) Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	// Если организация не найдена, то вызываем исключение и не создаем документ ИБ на основании ЭД.
	Если Не ЗначениеЗаполнено(Организация) Тогда
		
		Если ЗначениеЗаполнено(КПП) Тогда
			Шаблон = НСтр("ru = 'Не удалось создать документ, т.к. организация с ИНН/КПП %1/%2 не найдена'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, ИНН, КПП);
		Иначе
			Шаблон = НСтр("ru = 'Не удалось создать документ, т.к. организация с ИНН %1 не найдена'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, ИНН);
		КонецЕсли;
		
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
	ДанныеОбъекта.Вставить("Организация", Организация);
	Если ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
		ДанныеОбъекта.Вставить("ПодразделениеОрганизации", ПодразделениеОрганизации);
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоКППОбособленногоПодразделения(КПП)

	КодПричиныПостановкиНаУчет = ИдентификационныеНомераНалогоплательщиков.КодПричиныПостановкиНаУчет(КПП);
	Возврат Не ИдентификационныеНомераНалогоплательщиков.ЭтоПричинаПостановкиНаУчетОрганизацииВЦелом(КодПричиныПостановкиНаУчет);

КонецФункции

Функция ОбособленноеПодразделениеОрганизацииПоКПП(Организация, КПП)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПодразделенияОрганизаций.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Организация
	|	И ПодразделенияОрганизаций.ОбособленноеПодразделение
	|	И ПодразделенияОрганизаций.КПП = &КПП";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("КПП", КПП);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция КонтрагентПоДаннымЭД(ДеревоДанных, ВидУчастника)
	
	ТипУчастника = СокрЛП(ВРег(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника")));
	Если ТипУчастника = "ЮЛ" Тогда
		
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ЮЛ.ИНН");
		КПП = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ЮЛ.КПП");
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Контрагенты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.ИНН = &ИНН
			|	И Контрагенты.КПП = &КПП
			|	И Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
			|	И НЕ Контрагенты.ПометкаУдаления";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Запрос.УстановитьПараметр("КПП", КПП);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
		
	ИначеЕсли ТипУчастника = "ИП" Тогда
		
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ИП.ИНН");
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Контрагенты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.ИНН = &ИНН
			|	И Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
			|	И НЕ Контрагенты.ПометкаУдаления";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
		
	ИначеЕсли ТипУчастника = "ФЛ"  Тогда
		
		ИНН = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ВидУчастника + ".ТипУчастника.ФЛ.ИНН");
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Контрагенты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.ИНН = &ИНН
			|	И НЕ Контрагенты.ПометкаУдаления
			|	И Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)";
		Запрос.УстановитьПараметр("ИНН", ИНН);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
		
	ИначеЕсли ТипУчастника = "ИЛ" Тогда
		
		Наименование = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			ДеревоДанных,
			ВидУчастника + ".ТипУчастника.ИЛ.НаименованиеОрганизации");
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Контрагенты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	НЕ Контрагенты.ПометкаУдаления
			|	И Контрагенты.НаименованиеПолное = &НаименованиеПолное";
		Запрос.УстановитьПараметр("НаименованиеПолное", Наименование);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Справочники.Контрагенты.ПустаяСсылка();
	
КонецФункции

Функция НоменклатураИБ(СведенияОТоваре, ПутьКДаннымСопоставления)
	
	Сопоставление = СведенияОТоваре.Строки.Найти(
			ПутьКДаннымСопоставления, "ПолныйПуть", Истина);
	
	Если Сопоставление <> Неопределено Тогда
		Номенклатура = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
			ПутьКДаннымСопоставления + ".НоменклатураИБ");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат Номенклатура;
	Иначе
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСвойствоСтруктуры(ДанныеСтруктуры, СвойствоСтруктуры)
	
	Если ДанныеСтруктуры.Свойство(СвойствоСтруктуры) Тогда
		
		Возврат ДанныеСтруктуры[СвойствоСтруктуры];
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция СобытиеЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Загрузка электронных документов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
КонецФункции

Процедура ЗаполнитьДанныеУчастникаУПД(ДеревоДанных, СведенияОбУчастнике, ВидУчастника, ВидАдреса = "АдресРФ", ДатаСведений = '00010101')
	
	Если СведенияОбУчастнике.СтранаРегистрации <> Справочники.СтраныМира.Россия Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИЛ.НаименованиеОрганизации",
									СведенияОбУчастнике.ПолноеНаименование);
		
	ИначеЕсли СведенияОбУчастнике.ТипЮрФизЛица = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.НаименованиеОрганизации",
									СведенияОбУчастнике.ПолноеНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.ИНН",
									СведенияОбУчастнике.ИНН);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ЮЛ.КПП",
									СведенияОбУчастнике.КПП);
		
	Иначе
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИП.ИНН",
									СведенияОбУчастнике.ИНН);
		
		Если ЗначениеЗаполнено(СведенияОбУчастнике.СвидетельствоСерияНомер) Тогда
			Свидетельство = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Свидетельство №%1 от %2'"),
							СведенияОбУчастнике.СвидетельствоСерияНомер,
							Формат(СведенияОбУчастнике.СвидетельствоДатаВыдачи, "ДЛФ=D"));
			
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".ТипУчастника.ИП.СвидетельствоОГосРегистрации",
										Свидетельство);
		КонецЕсли;
		
		ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(СведенияОбУчастнике.ПолноеНаименование);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИП.Фамилия",
									ФИО.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИП.Имя",
									ФИО.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".ТипУчастника.ИП.Отчество",
									ФИО.Отчество);
	КонецЕсли;
	
	Если СведенияОбУчастнике.Свойство("ОбособленноеПодразделениеОрганизации")
		И ЗначениеЗаполнено(СведенияОбУчастнике.ОбособленноеПодразделениеОрганизации) Тогда
		
		Компания = СведенияОбУчастнике.ОбособленноеПодразделениеОрганизации;
	Иначе
		Компания = СведенияОбУчастнике.ЮрФизЛицоСсылка;
	КонецЕсли;
	АдресУчастника = Новый Структура;
	ПолучитьАдресСтруктурой(АдресУчастника, Компания, ВидАдреса, ДатаСведений);
	
	Если СведенияОбУчастнике.Свойство("АдресДоставки") Тогда
		АдресУчастника.АдресТекст = СведенияОбУчастнике.АдресДоставки;
	КонецЕсли;
	ЗаполнитьАдресВДеревеУПД(ДеревоДанных, АдресУчастника, "АдресИнформация", ВидУчастника);
	
	Если ЗначениеЗаполнено(СведенияОбУчастнике.Телефоны) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".КонтактныеСведения.Телефон",
									СведенияОбУчастнике.Телефоны);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СведенияОбУчастнике.Email) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".КонтактныеСведения.ЭлектроннаяПочта",
									СведенияОбУчастнике.Email);
	КонецЕсли;
	
	НомерСчета = "";
	Если СведенияОбУчастнике.Свойство("НомерСчета", НомерСчета) И ЗначениеЗаполнено(НомерСчета) Тогда
		Банк = "";
		БИК = "";
		КоррСчет = "";
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
				ДеревоДанных,
				ВидУчастника + ".БанковскиеРеквизиты.НомерСчета",
				НомерСчета);
				
		Если СведенияОбУчастнике.Свойство("Банк", Банк) И ЗначениеЗаполнено(Банк) Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".БанковскиеРеквизиты.НаименованиеБанка",
										Банк);
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("БИК", БИК) И ЗначениеЗаполнено(БИК) Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".БанковскиеРеквизиты.БИКБанка",
										БИК);
		КонецЕсли;
		Если СведенияОбУчастнике.Свойство("КоррСчет", КоррСчет) И ЗначениеЗаполнено(КоррСчет) Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
										ДеревоДанных,
										ВидУчастника + ".БанковскиеРеквизиты.КорреспондентскийСчетБанка",
										КоррСчет);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СведенияОбУчастнике.КодПоОКПО) Тогда
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
			ДеревоДанных, ВидУчастника + ".КодОКПО", СведенияОбУчастнике.КодПоОКПО);
	КонецЕсли;
	
	Если СведенияОбУчастнике.Свойство("ПодразделениеОрганизации") Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(
									ДеревоДанных,
									ВидУчастника + ".СтруктурноеПодразделение",
									СведенияОбУчастнике.ПодразделениеОрганизации);
		
	КонецЕсли;
	
КонецПроцедуры

// Находит ссылку на справочник по переданному реквизиту.
//
// Параметры:
//  ИмяСправочника - Строка, имя справочника, объект которого надо найти,
//  ИмяРеквизита - Строка, имя реквизита, по которому будет проведен поиск,
//  ЗначРеквизита - произвольное значение, значение реквизита, по которому будет проведен поиск,
//  Владелец - Ссылка на владельца для поиска в иерархическом справочнике.
//
Функция НайтиСсылкуНаОбъектПоРеквизиту(ИмяСправочника, ИмяРеквизита, ЗначРеквизита, Владелец = Неопределено)
	
	Результат = Неопределено;
	
	ОбъектМетаданных = Метаданные.Справочники[ИмяСправочника];
	Если НЕ ОбщегоНазначения.ЭтоСтандартныйРеквизит(ОбъектМетаданных.СтандартныеРеквизиты, ИмяРеквизита)
		И НЕ ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита) <> Неопределено Тогда
		
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИскСправочник.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник." + ИмяСправочника + " КАК ИскСправочник
	|ГДЕ
	|	ИскСправочник." + ИмяРеквизита + " = &ЗначРеквизита
	|	И НЕ ИскСправочник.ПометкаУдаления";
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		Запрос.Текст = Запрос.Текст + " И ИскСправочник.Владелец = &Владелец";
		Запрос.УстановитьПараметр("Владелец", Владелец);
	КонецЕсли;
	Запрос.УстановитьПараметр("ЗначРеквизита", ЗначРеквизита);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПолучитьАдресСтруктурой(СтруктураАдреса, Ссылка, ВидАдреса = "Юр", ДатаСведений = '00010101')
	
	СтруктураАдреса.Вставить("АдресРФ",         Истина);
	СтруктураАдреса.Вставить("КодСтр",          "");
	СтруктураАдреса.Вставить("КодСтраны",       "");
	СтруктураАдреса.Вставить("Индекс",          "");
	СтруктураАдреса.Вставить("КодРегион",       "");
	СтруктураАдреса.Вставить("КодРегиона",      "");
	СтруктураАдреса.Вставить("Район",           "");
	СтруктураАдреса.Вставить("Город",           "");
	СтруктураАдреса.Вставить("НаселПункт",      "");
	СтруктураАдреса.Вставить("НаселенныйПункт", "");
	СтруктураАдреса.Вставить("Улица",           "");
	СтруктураАдреса.Вставить("Дом",             "");
	СтруктураАдреса.Вставить("Корпус",          "");
	СтруктураАдреса.Вставить("Кварт",           "");
	СтруктураАдреса.Вставить("Квартира",        "");
	СтруктураАдреса.Вставить("АдрТекст",        "");
	СтруктураАдреса.Вставить("АдресТекст",      "");
	СтруктураАдреса.Вставить("КодГАР",          "");	
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Организации") Тогда
		ВидКонтактнойИнформации = ?(ВидАдреса = "Юр", Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации, Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ФактическийАдресПодразделенияОрганизаций;
	Иначе
		ВидКонтактнойИнформации = ?(ВидАдреса = "Юр", Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента, Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
	КонецЕсли;
	
	СведенияОбАдресе = УправлениеКонтактнойИнформациейБП.АдресСтруктурой(Ссылка, ВидКонтактнойИнформации, ДатаСведений);
	
	СтруктураАдреса.АдрТекст   = СведенияОбАдресе.Представление;
	СтруктураАдреса.АдресТекст = СведенияОбАдресе.Представление;
	
	СтруктураАдреса.КодСтраны  = СведенияОбАдресе.КодСтраны;
	СтруктураАдреса.КодСтр     = СведенияОбАдресе.КодСтраны;
	СтруктураАдреса.АдресРФ    = СведенияОбАдресе.АдресРФ;
	СтруктураАдреса.КодРегион  = СведенияОбАдресе.КодРегиона;
	СтруктураАдреса.КодРегиона = СведенияОбАдресе.КодРегиона;
	СтруктураАдреса.Индекс     = СведенияОбАдресе.Индекс;
	СтруктураАдреса.Район      = СведенияОбАдресе.Район;
	СтруктураАдреса.Город      = СведенияОбАдресе.Город;
	СтруктураАдреса.НаселПункт = СведенияОбАдресе.НаселенныйПункт;
	СтруктураАдреса.НаселенныйПункт = СведенияОбАдресе.НаселенныйПункт;
	СтруктураАдреса.Улица      = СведенияОбАдресе.Улица;
	
	СтруктураАдреса.Дом        = ?(ЗначениеЗаполнено(СведенияОбАдресе.ТипДома), НРег(Строка(СведенияОбАдресе.ТипДома)) + " ", "")
		+ СведенияОбАдресе.Дом;
	
	СтруктураАдреса.Корпус     = ?(ЗначениеЗаполнено(СведенияОбАдресе.ТипКорпуса), НРег(Строка(СведенияОбАдресе.ТипКорпуса)) + " ", "")
		+ СведенияОбАдресе.Корпус;
	
	СтруктураАдреса.Кварт      = ?(ЗначениеЗаполнено(СведенияОбАдресе.ТипКвартиры), НРег(Строка(СведенияОбАдресе.ТипКвартиры)) + " ", "")
		+ СведенияОбАдресе.Квартира;
	
	СтруктураАдреса.Квартира   = ?(ЗначениеЗаполнено(СведенияОбАдресе.ТипКвартиры), НРег(Строка(СведенияОбАдресе.ТипКвартиры)) + " ", "")
		+ СведенияОбАдресе.Квартира;
		
КонецПроцедуры

Процедура ДополнитьДаннымиОПодразделенииОрганизации(СведенияОбОрганизации, ДанныеДляФормированияЭД)
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям") Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОснование = ?(ДанныеДляФормированияЭД.ДокументыОснования.Количество() > 0,
		ДанныеДляФормированияЭД.ДокументыОснования[0],
		Неопределено);
	
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.КорректировкаРеализации")) Тогда
		
		Возврат 
	КонецЕсли;
	
	НаименованиеПодразделения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ПодразделениеОрганизации.Наименование");
	Если Не ЗначениеЗаполнено(НаименованиеПодразделения) Тогда
		Возврат;
	КонецЕсли;
	
	СведенияОбОрганизации.Вставить("ПодразделениеОрганизации", СокрЛП(НаименованиеПодразделения));
	
КонецПроцедуры

Процедура ДобавитьОбработкуОшибокЗаполненияТабличнойЧастиЭД(ТаблицаТоваров)

	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров, "ТоварНаименование",,,
		НСтр("ru = 'Не указано полное наименование товара (услуги) в справочнике ""Номенклатура"".'"));
		
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров, "ЕдиницаИзмеренияНаименование",,,
		НСтр("ru = 'Не указано наименование единицы измерения в справочнике ""Номенклатура"".'"));
		
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров, "ЕдиницаИзмеренияКод",,,
		НСтр("ru = 'Не указан код единицы измерения в справочнике ""Номенклатура"".'"));
		
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров, "Количество",,,
		НСтр("ru = 'Не заполнено количество в табличной части документа.'"));
		
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров, "БазоваяЕдиницаКод",,,
		НСтр("ru = 'Не указан код единицы измерения в справочнике ""Номенклатура"".'"));

	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров, "ЕдиницаИзмеренияКодДоКорректировки",,,
		НСтр("ru = 'Не указан код единицы измерения в справочнике ""Номенклатура"".'"));
	
	ЭлектронноеВзаимодействие.ДобавитьВТаблицуОбработкуОшибкиЧерезСообщениеПользователю(ТаблицаТоваров, "КоличествоДоКорректировки",,,
		НСтр("ru = 'Не указано количество товара в табличной части'"));

КонецПроцедуры

Функция НайтиРеализациюТоваровУслуг(Организация, НомерДокумента, ДатаДокумента)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеПервичныхДокументов.Документ) = ТИП(Документ.СчетФактураВыданный)
	|			ТОГДА ДанныеПервичныхДокументов.Документ.ДокументОснование
	|		ИНАЧЕ ДанныеПервичныхДокументов.Документ
	|	КОНЕЦ КАК Документ
	|ИЗ
	|	РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|ГДЕ
	|	ДанныеПервичныхДокументов.Организация = &Организация
	|	И ДанныеПервичныхДокументов.Номер = &НомерДокумента
	|	И НАЧАЛОПЕРИОДА(ДанныеПервичныхДокументов.Дата, ДЕНЬ) = &ДатаДокумента
	|	И (ТИПЗНАЧЕНИЯ(ДанныеПервичныхДокументов.Документ) = ТИП(Документ.РеализацияТоваровУслуг)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДанныеПервичныхДокументов.Документ) = ТИП(Документ.СчетФактураВыданный))";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НомерДокумента", НомерДокумента);
	Запрос.УстановитьПараметр("ДатаДокумента", НачалоДня(ДатаДокумента));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Документ;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция БанковскийСчетПродавца(ДанныеШапки)

	Если ТипЗнч(ДанныеШапки) = Тип("СтрокаТаблицыЗначений") Тогда
		Владелец = ДанныеШапки.Владелец();
		Если Владелец.Колонки.Найти("БанковскийСчетПродавца") <> Неопределено Тогда
			Возврат ДанныеШапки.БанковскийСчетПродавца;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДанныеШапки) = Тип("Структура") И ДанныеШапки.Свойство("БанковскийСчетПродавца") Тогда
		Возврат ДанныеШапки.БанковскийСчетПродавца;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

Функция ДанныеОснованияКорректировкиВозврата(СсылкаНаДокумент)

	Результат = Новый Структура("Ссылка, Номер, Дата");
	
	Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		ДанныеОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаДокумент, "Сделка, Сделка.Номер, Сделка.Дата");
		Результат.Ссылка = ДанныеОснования.Сделка;
		Результат.Номер  = ДанныеОснования.СделкаНомер;
		Результат.Дата   = ДанныеОснования.СделкаДата;
	ИначеЕсли ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		ДанныеОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаДокумент, "ДокументРеализации, ДокументРеализации.Номер, ДокументРеализации.Дата");
		Результат.Ссылка = ДанныеОснования.ДокументРеализации;
		Результат.Номер  = ДанныеОснования.ДокументРеализацииНомер;
		Результат.Дата   = ДанныеОснования.ДокументРеализацииДата;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция НужноСоздатьДокументВозвратТоваровПоставщику(СпособОбработки, ДеревоДанных)

	СпособОбработкиПервичногоДокумента = "";
	Если ТипЗнч(СпособОбработки) = Тип("Структура")
		И СпособОбработки.Свойство("ПервичныйДокумент") Тогда
		
		СпособОбработкиПервичногоДокумента = СпособОбработки.ПервичныйДокумент;
	ИначеЕсли ТипЗнч(СпособОбработки) = Тип("Строка") Тогда
		СпособОбработкиПервичногоДокумента = СпособОбработки;
	КонецЕсли;
	
	Если СпособОбработкиПервичногоДокумента = "ОпределяетсяПрограммой" Тогда
		
		// Если корректируемый документ был создан в прошлом году и прошло больше квартала до даты корректировки,
		// то будет создан документ "КорректировкаПоступления". 
		ВидОперации = ВидОперацииУКД(ДеревоДанных);
		
		Если Не КорректируемыйДокументСозданВПрошлыхГодах(ДеревоДанных)
			И (ВидОперации = "Возврат"
				Или (ВидОперации = Неопределено И ВоВсехСтрокахПроизводитсяУменьшениеКоличества(ДеревоДанных))) Тогда
			
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция КорректируемыйДокументСозданВПрошлыхГодах(ДеревоДанных)
	
	// Проверяем, что корректируемый документ создан в прошлом году
	// и между датой корректируемого документа и датой документа
	// корректировки прошло больше квартала.
	ДатаДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаДокумента");
	ДатаКорректируемогоДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ДатаИсходногоДокумента");
	РазницаВГодах = Год(ДатаДокумента) - Год(ДатаКорректируемогоДокумента);
	Если РазницаВГодах > 1 Тогда
		Возврат Истина;
	ИначеЕсли РазницаВГодах = 1
		И КонецКвартала(ДатаКорректируемогоДокумента) <> НачалоКвартала(ДатаДокумента) - 1 Тогда
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция ВоВсехСтрокахПроизводитсяУменьшениеКоличества(ДеревоДанных)
	
	// Если есть строки, где количество не уменьшается или производится корректировка только суммы,
	// то это не возврат, а корректировка поступления.
	СведенияОТоварах = ДеревоДанных.Строки.Найти("СведенияОТоварах", "ПолныйПуть");
	Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
		КоличествоДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СведенияОТоваре, "СведенияОТоварах.НомерСтроки.КоличествоДоКорректировки");
		КоличествоПослеКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СведенияОТоваре, "СведенияОТоварах.НомерСтроки.Количество");
		Если КоличествоДоКорректировки <= КоличествоПослеКорректировки Тогда
			Возврат Ложь;
		КонецЕсли;
		
		// Если производится корректировка ставки НДС или единицы измерения, то это корректировка поступления,
		// т.к. с помощью документа "Возврат от покупателя" нельзя корректировать эти данные.
		НалоговаяСтавкаДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СведенияОТоваре, "СведенияОТоварах.НомерСтроки.НалоговаяСтавкаДоКорректировки");
		НалоговаяСтавкаПослеКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СведенияОТоваре, "СведенияОТоварах.НомерСтроки.НалоговаяСтавка");
		Если НалоговаяСтавкаДоКорректировки <> НалоговаяСтавкаПослеКорректировки Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ЕдиницаИзмеренияКодДоКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЕдиницаИзмеренияКодДоКорректировки");
		ЕдиницаИзмеренияКодПослеКорректировки = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
			СведенияОТоваре, "СведенияОТоварах.НомерСтроки.ЕдиницаИзмеренияКод");
		Если ЕдиницаИзмеренияКодДоКорректировки <> ЕдиницаИзмеренияКодПослеКорректировки Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;

КонецФункции

Функция ВидОперацииУКД(ДеревоДанных)
	
	ВидДокумента = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "ВидДокумента");
	Если ЗначениеЗаполнено(ВидДокумента) Тогда
		Возврат ВидДокумента;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ЭтоПоступлениеДопРасходов(СпособОбработки)
	
	Результат = Ложь;
	СпособОбработкиПоступлениеДопРасходов = ВРег("ПоступлениеДопРасходов");
	Если ТипЗнч(СпособОбработки) = Тип("Структура") Тогда
		Результат = (ВРег(СпособОбработки.ПервичныйДокумент) = СпособОбработкиПоступлениеДопРасходов);
	Иначе
		Результат = (ВРег(СпособОбработки) = СпособОбработкиПоступлениеДопРасходов);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоИсправлениеУПД(ДеревоДанных)
	
	Возврат ЗначениеЗаполнено(ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, "НомерИсправления"));
	
КонецФункции

Функция ДанныеВходящегоЭД(ДеревоДанных)
	
	Результат = Новый Структура;
	Для каждого СтрокаДерева Из ДеревоДанных.Строки Цикл
		Путь = СтрокаДерева.ПолныйПуть;
		ДанныеСтроки = ЭлектронноеВзаимодействиеСлужебный.ДанныеЭлементаДереваЭлектронногоДокумента(ДеревоДанных, Путь);
		Результат.Вставить(Путь, ДанныеСтроки);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СчетФактураНеТребуется(СсылкаНаДокумент)
	
	Если ЭтоДокументСДоговоромНаКомиссионнуюТорговлю(СсылкаНаДокумент) Тогда
		Возврат Истина;
	КонецЕсли;
	
	МассивДокументов = Документы[СсылкаНаДокумент.Метаданные().Имя].СчетаФактурыНеТребуются(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СсылкаНаДокумент));
	
	Возврат МассивДокументов.Количество() > 0;
	
КонецФункции

Функция ОснованияУКД(Знач ДокументСсылка)
	
	ДокументыОснования = Новый Массив;
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		ДокументСсылка = ДокументОснованиеСчетаФактурыВыданного(ДокументСсылка);
		ДокументыОснования.Добавить(ДокументСсылка);
	КонецЕсли;
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		ПервичныйДокумент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Сделка");
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		ПервичныйДокумент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ДокументРеализации");
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		ПервичныйДокумент = ДокументОснованиеСчетаФактурыВыданного(ДокументСсылка);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПервичныйДокумент) Тогда
		ДокументыОснования.Добавить(ПервичныйДокумент);
		СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(ПервичныйДокумент);
		Если ЗначениеЗаполнено(СчетФактура) Тогда
			ДокументыОснования.Добавить(СчетФактура);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДокументыОснования;
	
КонецФункции

Функция ДанныеПервичногоДокументаУПД(Знач ДокументСсылка)
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		ДокументСсылка = ДокументОснованиеСчетаФактурыВыданного(ДокументСсылка);
	КонецЕсли;
	
	Результат = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Дата, Номер, Ссылка");
	Результат.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Результат.Номер, Истина, Ложь);
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		// При исправлении в первичных документах дата отгрузки должна быть равна дате документа реализации.
		Результат.Вставить("ДатаОтгрузки",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ДокументРеализации.Дата"));
	Иначе
		Результат.Вставить("ДатаОтгрузки", Результат.Дата);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПроверитьГотовностьОснованияУКД(СсылкаНаОбъект, Отказ)
	
	ПервичныйДокумент = Неопределено;
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		ПервичныйДокумент = ДокументОснованиеСчетаФактурыВыданного(СсылкаНаОбъект);
	Иначе
		ПервичныйДокумент = СсылкаНаОбъект;
	КонецЕсли;
	
	Если Не ЭтоВозвратТоваровОтПокупателя(ПервичныйДокумент)
			Или СчетФактураНеТребуется(ПервичныйДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	ПокупателюВыставляетсяКорректировочныйСчетФактура = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ПервичныйДокумент, "ПокупателюВыставляетсяКорректировочныйСчетФактура");
	Если ПокупателюВыставляетсяКорректировочныйСчетФактура <> Истина Тогда
		ШаблонСообщения = НСтр("ru = 'Для документа ""%1"" не удалось сформировать электронный документ. Во вкладке ""НДС"" формы документа должен быть установлен вариант ""Корректировочный счет-фактура выданный покупателю (рекомендуется)"".'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон(ШаблонСообщения, ПервичныйДокумент));
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоДокументСДоговоромНаКомиссионнуюТорговлю(СсылкаНаОбъект)
	
	Если Не ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ЕстьРеквизитОбъекта("ДоговорКонтрагента", СсылкаНаОбъект.Метаданные()) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ДоговорКонтрагента.ВидДоговора");
	
	Возврат (ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером
		Или ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку
		Или ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом
		Или ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку);
	
КонецФункции

Функция ЭтоКорректировкаПеревыставленногоСчетФактуры(СсылкаНаОбъект, СчетФактура)
	
	Если Не ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		СчетФактура = СсылкаНаОбъект;
	Иначе
		СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(СсылкаНаОбъект);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СчетФактура) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеСФ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетФактура, "Организация, Продавец");
	
	Возврат ДанныеСФ.Организация <> ДанныеСФ.Продавец;
	
КонецФункции

#КонецОбласти

#Область СопоставлениеНоменклатуры

Функция ЕдиницаИзмеренияПоДаннымКонтрагента(ЕдиницаИзмеренияКод, ЕдиницаИзмеренияНаименование)

	Если ЗначениеЗаполнено(ЕдиницаИзмеренияКод) Тогда
		ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(
			ЕдиницаИзмеренияКод, Ложь);
	ИначеЕсли ЗначениеЗаполнено(ЕдиницаИзмеренияНаименование) Тогда
		ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию(
			ЕдиницаИзмеренияНаименование, Истина);
	Иначе
		ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ЕдиницаИзмерения;

КонецФункции

Функция ВидСтавкиНДСПоДаннымКонтрагента(СтавкаНДССтрока)

	СтавкаНДС = ПолучитьСтавкуНДСПеречислением(СтавкаНДССтрока);
	ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.ВидСтавки(СтавкаНДС);
	
	Возврат ВидСтавкиНДС;

КонецФункции

#КонецОбласти

#Область Маркировка

Процедура ЗаполнитьСведенияОМаркировкеВАктеОРасхождениях(Приемник, Источник, ДанныеШтрихкодовУпаковок)
	
	Если ДанныеШтрихкодовУпаковок = Неопределено
		Или ДанныеШтрихкодовУпаковок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИндивидуальныеУпаковки = Новый ТаблицаЗначений;
	ИндивидуальныеУпаковки.Колонки.Добавить("Код");
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("Номенклатура", Источник.Номенклатура);
	ПараметрыПоиска.Вставить("Обработан"   , Ложь);
	
	СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	Количество = Приемник.Количество;
	Для Каждого СтрокаУпаковки Из СтрокиУпаковок Цикл
		
		Если (Количество <= 0) Тогда
			Прервать;
		ИначеЕсли СтрокаУпаковки.Обработан Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ИндивидуальныеУпаковки.Добавить();
		НоваяСтрока.Код = СтрокаУпаковки.ЗначениеШтрихКода;
		СтрокаУпаковки.Обработан = Истина;
		Количество = Количество - 1;
	КонецЦикла;
	
	СведенияОМаркировке = Новый Структура;
	Если ИндивидуальныеУпаковки.Количество() Тогда
		СведенияОМаркировке.Вставить("ИндивидуальныеУпаковки", ИндивидуальныеУпаковки);
	КонецЕсли;
	Приемник.Вставить("Маркировка", СведенияОМаркировке);
	
КонецПроцедуры

Процедура ПолучитьЗначенияШтрихкодов(Приемник, Источник)
	
	Маркировка = Неопределено;
	Если Не Источник.Свойство("Маркировка", Маркировка) Тогда
		Возврат;
	КонецЕсли;
	
	Если Маркировка.Свойство("ИндивидуальныеУпаковки")
			И ТипЗнч(Маркировка.ИндивидуальныеУпаковки) = Тип("ТаблицаЗначений") Тогда
		Для Каждого СтрокаТаблицы  Из Маркировка.ИндивидуальныеУпаковки Цикл
			Приемник.Добавить(СтрокаТаблицы.Код);
		КонецЦикла;
	КонецЕсли;
	
	Если Маркировка.Свойство("КонтрольныеИдентификационныеЗнаки")
			И ТипЗнч(Маркировка.КонтрольныеИдентификационныеЗнаки) = Тип("ТаблицаЗначений") Тогда
		Для Каждого СтрокаТаблицы  Из Маркировка.КонтрольныеИдентификационныеЗнаки Цикл
			Приемник.Добавить(СтрокаТаблицы.Код);
		КонецЦикла;
	КонецЕсли;
	
	Если Маркировка.Свойство("ТранспортныеУпаковки")
			И ТипЗнч(Маркировка.ТранспортныеУпаковки) = Тип("ТаблицаЗначений") Тогда
		Для Каждого СтрокаТаблицы  Из Маркировка.ТранспортныеУпаковки Цикл
			Приемник.Добавить(СтрокаТаблицы.Код);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьШтрихкодыУпаковок(ТаблицаШтрихкодыУпаковок, ЗначенияШтрихкодов)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШтрихкодыУпаковокТоваров.Ссылка КАК ШтрихкодУпаковки
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|ГДЕ
	|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода В(&ЗначенияШтрихкодов)";
	
	Запрос.УстановитьПараметр("ЗначенияШтрихкодов", ЗначенияШтрихкодов);
	ТаблицаШтрихкодыУпаковок.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьСведенияОМаркировкеВУКД(Приемник, Источник, ДанныеШтрихкодовУпаковок, Количество) Экспорт
	
	Если ДанныеШтрихкодовУпаковок = Неопределено
		Или ДанныеШтрихкодовУпаковок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКДаннымДляОшибки = "Объект";
	
	ТаблицаКодов = Новый ТаблицаЗначений;
	ТаблицаКодов.Колонки.Добавить("Код");
	
	СведенияОМаркировке = Новый Структура;
	СведенияОМаркировке.Вставить("КонтрольныеИдентификационныеЗнаки", ТаблицаКодов);
	Приемник = СведенияОМаркировке;
	
	ПараметрыПоиска = Новый Структура("Номенклатура, Обработан");
	ПараметрыПоиска.Номенклатура = Источник.Товар;
	ПараметрыПоиска.Обработан    = Ложь;
	СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаУпаковки Из СтрокиУпаковок Цикл
		Если (Количество <= 0) Тогда
			Прервать;
		КонецЕсли;
		НоваяСтрока = ТаблицаКодов.Добавить();
		НоваяСтрока.Код = СтрокаУпаковки.ЗначениеШтрихКода;
		СтрокаУпаковки.Обработан = Истина;
		Количество = Количество - 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияОМаркировкеВУКД_2020(Приемник, Источник, ДанныеШтрихкодовУпаковок, Количество) Экспорт
	
	Если ДанныеШтрихкодовУпаковок = Неопределено
		Или ДанныеШтрихкодовУпаковок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКДаннымДляОшибки = "Объект";
	
	ТаблицаКодов = Новый ТаблицаЗначений;
	ТаблицаКодов.Колонки.Добавить("Код");
	
	СведенияОМаркировке = Новый Структура;
	СведенияОМаркировке.Вставить("ИндивидуальныеУпаковки", ТаблицаКодов);
	Приемник = СведенияОМаркировке;
	
	ПараметрыПоиска = Новый Структура("Номенклатура, Обработан");
	ПараметрыПоиска.Номенклатура = Источник.Товар;
	ПараметрыПоиска.Обработан    = Ложь;
	СтрокиУпаковок = ДанныеШтрихкодовУпаковок.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаУпаковки Из СтрокиУпаковок Цикл
		Если (Количество <= 0) Тогда
			Прервать;
		КонецЕсли;
		НоваяСтрока = ТаблицаКодов.Добавить();
		НоваяСтрока.Код = СтрокаУпаковки.ЗначениеШтрихКода;
		СтрокаУпаковки.Обработан = Истина;
		Количество = Количество - 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьШтрихкодыТаблицуШтрихкодовУпаковокУКД(ШтрихкодыУпаковок, СведенияОТоваре)
	
	ШтрихкодыУпаковокСтрокиТЧ = Новый Массив;
	КодыУпаковок = СведенияОТоваре.Строки.Найти(
		"СведенияОТоварах.НомерСтроки.СведенияОМаркировкеПосле.КонтрольныеИдентификационныеЗнаки", "ПолныйПуть", Истина);
	Если КодыУпаковок <> Неопределено И ЗначениеЗаполнено(КодыУпаковок.Значение) Тогда
		Для Каждого СтрокаКодаУпаковки Из КодыУпаковок.Строки Цикл
			ШтрихкодыУпаковокСтрокиТЧ.Добавить(
				ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
					СтрокаКодаУпаковки,
					"СведенияОТоварах.НомерСтроки.СведенияОМаркировкеПосле.КонтрольныеИдентификационныеЗнаки.НомерСтроки.Код"));
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ЗначениеШтрихкода Из ШтрихкодыУпаковокСтрокиТЧ Цикл
		
		НоваяСтрокаТЧШтрихкодыУпаковок = ШтрихкодыУпаковок.Добавить();
		НоваяСтрокаТЧШтрихкодыУпаковок.ЗначениеШтрихкода = ЗначениеШтрихкода;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьШтрихкодыТаблицуШтрихкодовУпаковокУКД_2020(ШтрихкодыУпаковок, СведенияОТоваре)
	
	ШтрихкодыУпаковокСтрокиТЧ = Новый Массив;
	КодыУпаковок = СведенияОТоваре.Строки.Найти(
		"СведенияОТоварах.НомерСтроки.СведенияОМаркировкеПосле.ИндивидуальныеУпаковки", "ПолныйПуть", Истина);
	Если КодыУпаковок <> Неопределено И ЗначениеЗаполнено(КодыУпаковок.Значение) Тогда
		Для Каждого СтрокаКодаУпаковки Из КодыУпаковок.Строки Цикл
			ШтрихкодыУпаковокСтрокиТЧ.Добавить(
				ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
					СтрокаКодаУпаковки,
					"СведенияОТоварах.НомерСтроки.СведенияОМаркировкеПосле.ИндивидуальныеУпаковки.НомерСтроки.Код"));
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ЗначениеШтрихкода Из ШтрихкодыУпаковокСтрокиТЧ Цикл
		
		НоваяСтрокаТЧШтрихкодыУпаковок = ШтрихкодыУпаковок.Добавить();
		НоваяСтрокаТЧШтрихкодыУпаковок.ЗначениеШтрихкода = ЗначениеШтрихкода;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияОМаркировкеВУПД(Приемник, ПервичныйДокумент, СведенияОТоваре, ДанныеШтрихкодовУпаковок) Экспорт
	
	Источник = Новый Структура("Ссылка, Номенклатура, Количество");
	Источник.Ссылка       = ПервичныйДокумент;
	Источник.Номенклатура = СведенияОТоваре.Товар;
	Источник.Количество   = СведенияОТоваре.Количество;
	
	ЭлектронноеВзаимодействиеИСМП.ЗаполнитьСведенияОМаркировке_2019(Приемник, Источник, ДанныеШтрихкодовУпаковок);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти