#Область СлужебныйПрограммныйИнтерфейс

#Область БазоваяФункциональностьПроверокЗаполнения

// Сообщает об ошибке заполнения в реквизите объекта и включает флажок Отказ.
//
// Параметры:
//   Отказ        - Булево        - Флажок отказа, который включается в данной процедуре.
//   Объект       - ЛюбаяСсылка,
//                  ЛюбойОбъект   - Объект, в котором обнаружена ошибка.
//   ИмяРеквизита - Строка        - Имя реквизита, в котором обнаружена ошибка.
//   Текст        - Строка        - Текст ошибки.
//
Процедура СообщитьОбОшибкеВОбъекте(Отказ, Объект, ИмяРеквизита, Текст) Экспорт
	Отказ = Истина;
	СообщениеОбъекта(Текст, Объект, ИмяРеквизита).Сообщить();
КонецПроцедуры

// Сообщает об ошибке заполнения в реквизите объекта и включает флажок Отказ.
//
// Параметры:
//   Отказ        - Булево                 - Флажок отказа, который включается в данной процедуре.
//   Объект       - ЛюбаяСсылка,
//                  ЛюбойОбъект            - Объект, в котором обнаружена ошибка.
//   ИмяТаблицы   - Строка                 - Имя табличной части.
//   Строка       - Строка табличной части - Строка табличной части.
//   ИмяРеквизита - Строка                 - Имя реквизита строки табличной части.
//   Текст        - Строка                 - Текст ошибки. Если не указан, то будет сформирован стандартный текст ошибки.
//
Процедура СообщитьОбОшибкеВСтрокеТаблицы(Отказ, Объект, ИмяТаблицы, Строка, ИмяРеквизита, Текст) Экспорт
	Отказ = Истина;
	ПутьКРеквизиту = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицы, Строка.НомерСтроки, ИмяРеквизита);
	СообщениеОбъекта(Текст, Объект, ПутьКРеквизиту).Сообщить();
КонецПроцедуры

// Проверяет заполнение реквизита, в случае незаполненности включает флажок Отказ и сообщает об ошибке пользователю.
//
// Параметры:
//   Отказ        - Булево      - Флажок отказа, который включается в случае незаполненности реквизита.
//   Объект       - ЛюбойОбъект - Проверяемый объект.
//   ИмяРеквизита - Строка      - Проверяемый реквизит.
//   Текст        - Строка      - Текст ошибки. Если не указан, то будет сформирован стандартный текст ошибки.
//
Процедура ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, Объект, ИмяРеквизита, Знач Текст = "") Экспорт
	Если Не ЗначениеЗаполнено(Объект[ИмяРеквизита]) Тогда
		Если Текст = "" Тогда
			Текст = СтрШаблон(
				НСтр("ru = 'Не заполнено поле ""%1"".'"),
				Объект.Метаданные().Реквизиты[ИмяРеквизита].Представление());
		КонецЕсли;
		СообщитьОбОшибкеВОбъекте(Отказ, Объект, ИмяРеквизита, Текст);
	КонецЕсли;
КонецПроцедуры

// Проверяет заполнение реквизита, в случае незаполненности включает флажок Отказ и сообщает об ошибке пользователю.
//
// Параметры:
//   Отказ        - Булево      - Флажок отказа, который включается в случае незаполненности реквизита.
//   Объект       - ЛюбойОбъект - Проверяемый объект.
//   ИмяРеквизита - Строка      - Проверяемый реквизит типа "Адрес электронной почты".
//
Процедура ПроверитьЗаполнениеЭлектроннойПочтыОбъекта(Отказ, Объект, ИмяРеквизита) Экспорт
	Если Не ЗначениеЗаполнено(Объект[ИмяРеквизита]) Тогда
		Текст = СтрШаблон(
			НСтр("ru = 'Не заполнен %1.'"),
			Объект.Метаданные().Реквизиты[ИмяРеквизита].Представление());
		СообщитьОбОшибкеВОбъекте(Отказ, Объект, ИмяРеквизита, Текст);
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Объект[ИмяРеквизита], Истина) Тогда
		Текст = СтрШаблон(
			НСтр("ru = '%1 заполнен неверно.'"),
			Объект.Метаданные().Реквизиты[ИмяРеквизита].Представление());
		СообщитьОбОшибкеВОбъекте(Отказ, Объект, ИмяРеквизита, Текст);
	КонецЕсли;
КонецПроцедуры

// Проверяет заполнение реквизита, в случае незаполненности включает флажок Отказ и сообщает об ошибке пользователю.
//
// Параметры:
//   Отказ        - Булево      - Флажок отказа, который включается в случае незаполненности реквизита.
//   Объект       - ЛюбойОбъект            - Проверяемый объект.
//   Строка       - Строка табличной части - Проверяемая строка табличной части.
//   ИмяТаблицы   - Строка                 - Имя табличной части.
//   Строка       - Строка табличной части - Строка табличной части.
//   ИмяРеквизита - Строка                 - Имя реквизита строки табличной части.
//   Текст        - Строка                 - Текст ошибки. Если не указан, то будет сформирован стандартный текст ошибки.
//
Процедура ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, Объект, ИмяТаблицы, Строка, ИмяРеквизита, Знач Текст = "") Экспорт
	Если Не ЗначениеЗаполнено(Строка[ИмяРеквизита]) Тогда
		Если Текст = "" Тогда
			Текст = СтрШаблон(
				НСтр("ru = 'Не заполнено поле ""%1"".'"),
				Объект.Метаданные().ТабличныеЧасти[ИмяТаблицы].Реквизиты[ИмяРеквизита].Представление());
		КонецЕсли;
		СообщитьОбОшибкеВСтрокеТаблицы(Отказ, Объект, ИмяТаблицы, Строка, ИмяРеквизита, Текст);
	КонецЕсли;
КонецПроцедуры

// Возвращает сообщение об ошибке заполнения в реквизите объекта.
//
// Параметры:
//   Текст  - Строка      - Текст сообщения.
//   Объект - ЛюбаяСсылка,
//            ЛюбойОбъект - Объект, в котором обнаружена ошибка.
//   Поле   - Строка      - Полное имя реквизита, в котором обнаружена ошибка.
//
// Возвращаемое значение:
//   СообщениеПользователю - Сообщение об ошибке.
//
Функция СообщениеОбъекта(Текст, Объект, Поле = "") Экспорт
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Поле = Поле;
	
	ТипДанныхXML = XMLТипЗнч(Объект);
	Если ТипДанныхXML <> Неопределено И СтрНайти(ТипДанныхXML.ИмяТипа, "Object.") > 0 Тогда
		Сообщение.УстановитьДанные(Объект);
	Иначе
		Сообщение.КлючДанных = Объект;
	КонецЕсли;
	
	Возврат Сообщение;
КонецФункции

// Проверяет корректность заполнения начала и окончание периода в строке таблицы.
//
// Параметры:
//   Отказ - Булево - Флажок отказа, который будет включен в случае ошибки.
//   Объект - ЛюбаяСсылка, ЛюбойОбъект - Проверяемый объект.
//   ИмяТаблицы - Строка - Имя табличной части.
//   Строка - Строка табличной части - Строка таблицы.
//   ИмяПоляДатыНачала - Строка - Имя реквизита строки табличной части, в котором хранится дата начала периода.
//   ИмяПоляДатыОкончания - Строка - Имя реквизита строки табличной части, в котором хранится дата окончания.
//   ПредставлениеВРодительномПадеже - Строка - Представление (заголовок) периода в родительном падеже.
//
// Возвращаемое значение:
//   Булево - Признак того, что поля успешно прошли проверку.
//       Возвращает Ложь если была выявлена ошибка.
//
Функция ПроверитьПериодВСтрокеТаблицы(Отказ, Объект, ИмяТаблицы, Строка, ИмяПоляДатыНачала, ИмяПоляДатыОкончания, ПредставлениеВРодительномПадеже) Экспорт
	Дата1 = Строка[ИмяПоляДатыНачала];
	Дата2 = Строка[ИмяПоляДатыОкончания];
	Если Не ЗначениеЗаполнено(Дата1) Тогда
		Текст = СтрШаблон(НСтр("ru = 'Не заполнена дата начала %1.'"), ПредставлениеВРодительномПадеже);
		СообщитьОбОшибкеВСтрокеТаблицы(Отказ, Объект, ИмяТаблицы, Строка, ИмяПоляДатыНачала, Текст);
		Возврат Ложь;
	ИначеЕсли Не ЗначениеЗаполнено(Дата2) Тогда
		Текст = СтрШаблон(НСтр("ru = 'Не заполнена дата окончания %1.'"), ПредставлениеВРодительномПадеже);
		СообщитьОбОшибкеВСтрокеТаблицы(Отказ, Объект, ИмяТаблицы, Строка, ИмяПоляДатыОкончания, Текст);
		Возврат Ложь;
	ИначеЕсли Дата1 > Дата2 Тогда
		Текст = СтрШаблон(НСтр("ru = 'Дата начала %1 больше даты окончания.'"), ПредставлениеВРодительномПадеже);
		СообщитьОбОшибкеВСтрокеТаблицы(Отказ, Объект, ИмяТаблицы, Строка, ИмяПоляДатыНачала, Текст);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

#КонецОбласти

#КонецОбласти

