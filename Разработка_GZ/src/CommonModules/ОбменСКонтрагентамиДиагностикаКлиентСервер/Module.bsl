////////////////////////////////////////////////////////////////////////////////
// ОбменСКонтрагентамиДиагностикаКлиентСервер: механизм диагностики обмена электронными документами.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Формирует параметры выполнения диагностики.
// 
// Возвращаемое значение:
//  Структура - со свойствами:
//     * ВидыДиагностики        - Массив из Строка - виды диагностики, которые нужно выполнить. Если не заполнен, то выполняются
//                                  все виды диагностики. См. область ВидыДиагностики общего
//                                  модуля ОбменСКонтрагентамиДиагностикаКлиентСервер.
//     * ОповещениеОЗавершении  - ОписаниеОповещения - Если не задан, будет выполняться стандартная
//                                  обработка - открытие формы мастера диагностики электронного документооборота.
//                                  Содержит описание процедуры, которая будет вызвана после завершения диагностики
//                                  со следующими параметрами:
//          ** РезультатДиагностики - Структура - см. ОбменСКонтрагентамиДиагностика.НовыйРезультатДиагностики.
//          ** ДополнительныеПараметры - Произвольный - значение, которое было указано при создании
//                                  объекта ОписаниеОповещения.
//     * Отбор  - Структура - со свойствами:
//        * Сертификат - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификаты,
//                                по которым нужно выполнить диагностику.
//        * УчетнаяЗапись - Массив из Строка - идентификаторы учетных записей, по которым нужно выполнить диагностику.
//     * ПояснениеФормыВводаПароляСертификата - Строка - выводится в форму ввода пароля сертификата.
//     * ДополнительныеСертификатыДляПроверки - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - заполняется,
//                                              если необходимо дополнительно проверить сертификаты, непривязанные к учетным записям.
//
Функция НовыеПараметрыВыполненияДиагностики() Экспорт
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("ВидыДиагностики", Новый Массив);
	ПараметрыВыполнения.Вставить("ОповещениеОЗавершении", Неопределено);
	ПараметрыВыполнения.Вставить("ПояснениеФормыВводаПароляСертификата", "");
	ПараметрыВыполнения.Вставить("ДополнительныеСертификатыДляПроверки", Новый Массив);
	
	Отбор = Новый Структура;
	Отбор.Вставить("Сертификат", Новый Массив);
	Отбор.Вставить("УчетнаяЗапись", Новый Массив);
	
	ПараметрыВыполнения.Вставить("Отбор", Отбор);
	
	ДобавитьСлужебныеПараметрыВыполненияДиагностики(ПараметрыВыполнения);
	
	Возврат ПараметрыВыполнения;
	
КонецФункции

Функция ЕстьОшибки(КонтекстОперации) Экспорт
	
	Если КонтекстОперации = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат КонтекстОперации.Диагностика.Ошибки.Количество() > 0;
	
КонецФункции

#Область ВидыДиагностики

// Возвращает идентификатор вида диагностики интернет-соединения.
// 
// Возвращаемое значение:
//  Строка - вид диагностики.
//
Функция ВидДиагностикиИнтернетСоединение() Экспорт
	
	Возврат "ИнтернетСоединение";
	
КонецФункции

// Возвращает идентификатор вида диагностики криптографии.
// 
// Возвращаемое значение:
//  Строка - вид диагностики.
//
Функция ВидДиагностикиКриптография() Экспорт
	
	Возврат "Криптография";
	
КонецФункции

// Возвращает идентификатор вида диагностики интернет-поддержки.
// 
// Возвращаемое значение:
//  Строка - вид диагностики.
//
Функция ВидДиагностикиИнтернетПоддержка() Экспорт
	
	Возврат "ИнтернетПоддержка";
	
КонецФункции

// Возвращает идентификатор вида диагностики работы с файлами.
// 
// Возвращаемое значение:
//  Строка - вид диагностики.
//
Функция ВидДиагностикиРаботаСФайлами() Экспорт
	
	Возврат "РаботаСФайлами";
	
КонецФункции

// Возвращает идентификатор вида диагностики теста аутентификации.
// 
// Возвращаемое значение:
//  Строка - вид диагностики.
//
Функция ВидДиагностикиТестАутентификации() Экспорт
	
	Возврат "ТестАутентификации";
	
КонецФункции

#КонецОбласти

#Область ВидыОшибок

// Возвращает идентификатор вида ошибки интернет-соединения.
// 
// Возвращаемое значение:
//  Строка - вид ошибки.
//
Функция ВидОшибкиИнтернетСоединение() Экспорт
	
	Возврат "ИнтернетСоединение";
	
КонецФункции

// Возвращает идентификатор вида ошибки криптографии.
// 
// Возвращаемое значение:
//  Строка - вид ошибки.
//
Функция ВидОшибкиКриптография() Экспорт
	
	Возврат "Криптография";
	
КонецФункции

// Возвращает идентификатор вида ошибки интернет-поддержки.
// 
// Возвращаемое значение:
//  Строка - вид ошибки.
//
Функция ВидОшибкиИнтернетПоддержка() Экспорт
	
	Возврат "ИнтернетПоддержка";
	
КонецФункции

// Возвращает идентификатор вида ошибки работы с файлами.
// 
// Возвращаемое значение:
//  Строка - вид ошибки.
//
Функция ВидОшибкиРаботаСФайлами() Экспорт
	
	Возврат "РаботаСФайлами";
	
КонецФункции

Функция ВидОшибкиОбновлениеВерсииЭДЗакрытПринудительно() Экспорт
	
	Возврат "ОбновлениеВерсииЭДЗакрытПринудительно";
	
КонецФункции

Функция ВидОшибкиОбновлениеВерсииЭДТребуетсяАннулирование() Экспорт
	
	Возврат "ОбновлениеВерсииЭДТребуетсяАннулирование";
	
КонецФункции

Функция ВидОшибкиПовторноеПолучениеПакета() Экспорт
	
	Возврат "ПовторноеПолучениеПакета";
	
КонецФункции

Функция ВидОшибкиИсходящийЭДОтветногоТитула() Экспорт
	
	Возврат "ДляОтветногоТитулаНеНайденИсходящийЭлектронныйДокумент";
	
КонецФункции

Функция ВидОшибкиЕстьНеполученныеЭД() Экспорт
	
	Возврат "ЕстьНеполученныеЭД";
	
КонецФункции

Функция ВидОшибкиЕстьНеполученныеПриглашения() Экспорт
	
	Возврат "ЕстьНеполученныеПриглашения";
	
КонецФункции

Функция ВидОшибкиВидДокументаНеПоддерживается() Экспорт
	
	Возврат "ВидДокументаНеПоддерживается";
	
КонецФункции 

Функция ВидОшибкиНеВключеноИспользованиеОбменаЭД() Экспорт
	
	Возврат "НеВключеноИспользованиеОбменаЭД";
	
КонецФункции

Функция ВидОшибкиЕстьУчетныеЗаписиБезСертификатов() Экспорт
	
	Возврат "ЕстьУчетныеЗаписиБезСертификатов";
	
КонецФункции

Функция ВидОшибкиОтправкиИПолученияНетДоступныхСертификатов() Экспорт
	
	Возврат "НетДоступныхСертификатов";
	
КонецФункции

Функция ВидОшибкиМаршрутПодписанияПодписиУжеУстановлены() Экспорт
	
	Возврат "МаршрутПодписанияПодписиУжеУстановлены";
	
КонецФункции

// Возвращает идентификатор ошибки для ситуации, когда для сертификата
// в списке ""Подписываемые виды документов"" нет данного вида документа.
// 
// Возвращаемое значение:
//  Строка - вид ошибки.
//
Функция ВидОшибкиДляСертификатаНетПодписываемогоВидаДокумента() Экспорт
	
	Возврат "ДляСертификатаНетПодписываемогоВидаДокумента";
	
КонецФункции

Функция ВидОшибкиОтсутствуетСертификатДляПодписания() Экспорт
	Возврат "НеНайденСертификатДляПодписания";
КонецФункции

Функция ВидОшибкиАвтоматическоеСозданиеКонтрагента() Экспорт
	
	Возврат "АвтоматическоеСозданиеКонтрагента";
	
КонецФункции

Функция ВидОшибкиНетПравНаОбработкуДокументов() Экспорт
	Возврат "НетПравНаОбработкуДокументов";
КонецФункции

Функция ВидОшибкиНеПринятоПриглашение() Экспорт
	
	Возврат "НеПринятоПриглашение";
	
КонецФункции

Функция ВидОшибкиОжидаетсяСогласиеНаПриглашение() Экспорт
	
	Возврат "СогласиеНаПриглашение";
	
КонецФункции

Функция ВидОшибкиТребуетсяОтправкаПриглашения() Экспорт
	
	Возврат "ТребуетсяОтправкаПриглашения";
	
КонецФункции

Функция ВидОшибкиОшибкаПриглашения() Экспорт
	
	Возврат "ОшибкаПриглашения";
	
КонецФункции

Функция ВидОшибкиНетПриглашения() Экспорт
	
	Возврат "НетПриглашения";
	
КонецФункции

Функция ВидОшибкиНеУказанОбратныйАдресКонтрагента() Экспорт
	Возврат "НеУказанОбратныйАдресКонтрагента";
КонецФункции

Функция ВидОшибкиНетПравНаИзменениеИнформацииПоУчетнойЗаписи() Экспорт
	
	Возврат "НетПравНаИзменениеИнформацииПоУчетнойЗаписи";
	
КонецФункции

Функция ВидОшибкиПодписьНеверна() Экспорт
	
	Возврат "ПодписьНеверна";
	
КонецФункции 

Функция ВидОшибкиТребуетсяОтправкаПриглашенияОтправкаДокументов() Экспорт
	
	Возврат "ТребуетсяОтправкаПриглашенияОтправкаДокументов";
	
КонецФункции

Функция ВидОшибкиТребуетсяСогласиеНаПриглашениеОтправкаДокументов() Экспорт
	
	Возврат "ТребуетсяСогласиеНаПриглашениеОтправкаДокументов";
	
КонецФункции

Функция ВидОшибкиОжидаемСогласияНаПриглашениеОтправкаДокументов() Экспорт
	
	Возврат "ОжидаемСогласияНаПриглашениеОтправкаДокументов";
	
КонецФункции

Функция ВидОшибкиОшибкаПриглашенияОтправкаДокументов() Экспорт
	
	Возврат "ОшибкаПриглашенияОтправкаДокументов";
	
КонецФункции

Функция ВидОшибкиНедействительныйПользователь() Экспорт
	Возврат "НедействительныйПользователь";
КонецФункции

// Возвращает идентификатор неизвестной ошибки.
// 
// Возвращаемое значение:
//  Строка - вид ошибки.
//
Функция ВидОшибкиНеизвестнаяОшибка() Экспорт
	
	Возврат "НеизвестнаяОшибка";
	
КонецФункции

Функция ПояснениеОшибкиНенайденногоИсходящегоЭДДляОтветногоТитула() Экспорт
	
	Возврат НСтр("ru = 'Возможные причины:
						|- первичный титул был удален из текущей базы;
						|- полученный пакет не предназначен для текущей ИБ (первичный титул был сформирован в другой базе/копии текущей базы).'");
	
КонецФункции

Функция ПояснениеОшибкиПодписиУжеУстановлены() Экспорт
	
	Возврат НСтр("ru = 'Для подписания документа есть доступные сертификаты, но подписи по ним уже установлены.'");
	
КонецФункции

Функция НовыеДополнительныеСвойстваОшибки(ВидОшибки) Экспорт
	
	ДополнительныеСвойства = Новый Структура;
	ДополнительныеСвойства.Вставить("ДанныеДляФормированияФайловДляТехподдержки", Новый Массив);
	ДополнительныеСвойства.Вставить("ТекстСообщения",                             "");
	ДополнительныеСвойства.Вставить("Приглашение",                                Неопределено);
	ДополнительныеСвойства.Вставить("ИдентификаторДокумента",                     "");
	ДополнительныеСвойства.Вставить("ПредставлениеНенастроеннойСвязи",            "");
	
	Возврат ДополнительныеСвойства;
	
КонецФункции

#КонецОбласти

#Область ОбработкаРезультатовОтправкиПолучения

Функция НовоеОписаниеОбработаннойУчетнойЗаписи(Идентификатор = "") Экспорт
	
	ОбработаннаяУчетнаяЗапись = Новый Структура;
	ОбработаннаяУчетнаяЗапись.Вставить("Идентификатор", Идентификатор);
	ОбработаннаяУчетнаяЗапись.Вставить("ОтправкаПолучениеВыполнялись", Ложь);
	ОбработаннаяУчетнаяЗапись.Вставить("ПользовательОтказалсяОтОперации", Ложь);
	ОбработаннаяУчетнаяЗапись.Вставить("ИнформацияОбОшибке", НоваяИнформацияОбОшибке());
	
	Возврат ОбработаннаяУчетнаяЗапись;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Функция ОшибкиДиагностируются(Ошибки, ВидыДиагностики) Экспорт
	
	ВидыОшибок = ЭлектронноеВзаимодействиеОбработкаОшибокКлиентСервер.ЗначенияСвойствОшибок(Ошибки, "ВидОшибки");
	Если ВидыОшибок.Найти(ВидОшибкиИнтернетСоединение()) <> Неопределено Тогда
		ВидыДиагностики.Добавить(ВидДиагностикиИнтернетСоединение());
	КонецЕсли;
	Если ВидыОшибок.Найти(ВидОшибкиКриптография()) <> Неопределено Тогда
		ВидыДиагностики.Добавить(ВидДиагностикиКриптография());
	КонецЕсли;
	Если ВидыОшибок.Найти(ВидОшибкиИнтернетПоддержка()) <> Неопределено Тогда
		ВидыДиагностики.Добавить(ВидДиагностикиИнтернетПоддержка());
	КонецЕсли;
	Если ВидыОшибок.Найти(ВидОшибкиРаботаСФайлами()) <> Неопределено Тогда
		ВидыДиагностики.Добавить(ВидДиагностикиРаботаСФайлами());
	КонецЕсли;
	
	Возврат ВидыДиагностики.Количество() > 0;
	
КонецФункции

Функция ОписаниеМенеджераКриптографииWindows() Экспорт
	
	Описание = Новый Структура; 
	Описание.Вставить("ИмяПрограммы", "Microsoft Enhanced Cryptographic Provider v1.0");
	Описание.Вставить("ТипПрограммы", 1);
	Описание.Вставить("ПутьКПрограмме", "");
	
	Возврат Описание;
	
КонецФункции

Функция ДобавитьСлужебныеПараметрыВыполненияДиагностики(ОсновныеПараметры)
	
	СлужебныеПараметры = Новый Структура;
	СлужебныеПараметры.Вставить("Ошибки", Новый Массив);
	СлужебныеПараметры.Вставить("РезультатДиагностики", Неопределено);
	СлужебныеПараметры.Вставить("ПараметрыОткрытияМастера", Неопределено);
	СлужебныеПараметры.Вставить("ПараметрыФормы", Новый Структура);
	СлужебныеПараметры.Вставить("ОткрыватьОкноДиагностики", Ложь);
	СлужебныеПараметры.Вставить("ДлительнаяОперация", Неопределено);
	СлужебныеПараметры.Вставить("МассивОтпечатковСертификатовКлиент", Новый Массив);
	СлужебныеПараметры.Вставить("ЕстьКриптографияНаСервере", Ложь);
	СлужебныеПараметры.Вставить("ЕстьКриптографияНаКлиенте", Ложь);
	СлужебныеПараметры.Вставить("ЕстьМенеджерКриптографииНаКлиенте", Ложь);
	СлужебныеПараметры.Вставить("ЕстьМенеджерКриптографииНаСервере", Ложь);
	СлужебныеПараметры.Вставить("СертификатыСУстановленнымиПаролями", Новый Массив);
	СлужебныеПараметры.Вставить("ВидыПроверок", Новый Массив);
	СлужебныеПараметры.Вставить("ИнформационнаяБазаФайловая", Неопределено);
	СлужебныеПараметры.Вставить("ИмяКомпьютераКлиент", "");
	СлужебныеПараметры.Вставить("ИмяКомпьютераСервер", "");
	СлужебныеПараметры.Вставить("ДиагностикаРаботыСФайламиВРежимеОтображенияРезультата", Ложь);
	СлужебныеПараметры.Вставить("ЗамерДиагностикиНаКлиенте", Неопределено);
	// В веб-клиенте проверки сертификатов выполняются только при запуске диагностики через общую команду "Диагностика ЭДО"
	// или при выполнении повторной диагностики (кнопка "Повторить диагностику" в форме ДиагностикаЭДО).
	СлужебныеПараметры.Вставить("ВыполнятьПроверкиСертификатовВВебКлиенте", Ложь);
	СлужебныеПараметры.Вставить("СертификатыДляПроверки", Новый Массив);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ОсновныеПараметры, СлужебныеПараметры, Ложь);
	
	Возврат ОсновныеПараметры;
	
КонецФункции

Функция ЕстьВидДиагностики(ВидыДиагностики, Вид) Экспорт 
	
	Возврат ВидыДиагностики.Количество() = 0 Или ВидыДиагностики.Найти(Вид) <> Неопределено;
	
КонецФункции

Функция ПредставлениеОшибок(Ошибки) Экспорт
	
	ПредставленияОшибок = Новый Массив;
	Для каждого Ошибка Из Ошибки Цикл
		ПредставлениеОшибки = ПолучитьСклонение(Ошибка.ВидОшибки, "Творительный");
		Если ПредставленияОшибок.Найти(ПредставлениеОшибки) = Неопределено Тогда
			ПредставленияОшибок.Добавить(ПредставлениеОшибки);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(ПредставленияОшибок, ", ");
	
КонецФункции

Процедура ДобавитьСклонение(Склонения, Строка, Падеж, Значение) 
	
	Склонение = Склонения.Получить(Строка);
	Если Склонение = Неопределено Тогда
		Склонение = Новый Структура;
	КонецЕсли;
	Склонение.Вставить(Падеж, Значение);
	Склонения.Вставить(Строка, Склонение);
	
КонецПроцедуры

Функция РезультатВидаДиагностики(РезультатДиагностики, ВидДиагностики) Экспорт
	
	РезультатВидаДиагностики = Неопределено; 
	РезультатДиагностики.Результаты.Свойство(ВидДиагностики, РезультатВидаДиагностики);
	
	Возврат РезультатВидаДиагностики; 
	
КонецФункции

Функция ПолучитьСклонение(Строка, Падеж) Экспорт
	
	Склонения = Новый Соответствие;
	ДобавитьСклонение(Склонения, ВидДиагностикиИнтернетСоединение(), "Творительный", НСтр("ru = 'интернет-соединением'"));
	ДобавитьСклонение(Склонения, ВидДиагностикиИнтернетСоединение(), "Родительный", НСтр("ru = 'интернет-соединения'"));
	
	ДобавитьСклонение(Склонения, ВидДиагностикиКриптография(), "Творительный", НСтр("ru = 'криптографией'"));
	ДобавитьСклонение(Склонения, ВидДиагностикиКриптография(), "Родительный", НСтр("ru = 'криптографии'"));
	
	ДобавитьСклонение(Склонения, ВидДиагностикиИнтернетПоддержка(), "Творительный", НСтр("ru = 'интернет-поддержкой'"));
	ДобавитьСклонение(Склонения, ВидДиагностикиИнтернетПоддержка(), "Родительный", НСтр("ru = 'интернет-поддержки'"));
	
	ДобавитьСклонение(Склонения, ВидДиагностикиРаботаСФайлами(), "Творительный", НСтр("ru = 'работой с файлами'"));
	ДобавитьСклонение(Склонения, ВидДиагностикиРаботаСФайлами(), "Родительный", НСтр("ru = 'работы с файлами'"));
	
	ДобавитьСклонение(Склонения, ВидДиагностикиТестАутентификации(), "Творительный", НСтр("ru = 'тестом аутентификации'"));
	ДобавитьСклонение(Склонения, ВидДиагностикиТестАутентификации(), "Родительный", НСтр("ru = 'теста аутентификации'"));
	
	Склонение = Склонения.Получить(Строка);
	Если Склонение = Неопределено Тогда
		Возврат Неопределено; 
	КонецЕсли;
	
	Значение = Неопределено;
	Склонение.Свойство(Падеж, Значение);
	
	Возврат Значение;
	
КонецФункции

Функция НоваяИнформацияОбОшибке()
	
	ИнформацияОбОшибке = Новый Структура;
	ИнформацияОбОшибке.Вставить("ВидОшибки", ВидОшибкиНеизвестнаяОшибка());
	ИнформацияОбОшибке.Вставить("КраткоеПредставление", "");
	
	ПодробноеПредставление = Новый Структура;
	ПодробноеПредставление.Вставить("Текст", "");
	ПодробноеПредставление.Вставить("ОбработчикиНажатия", Новый Соответствие);
	ИнформацияОбОшибке.Вставить("ПодробноеПредставление", ПодробноеПредставление);
	
	Возврат ИнформацияОбОшибке;
	
КонецФункции

Функция СкрыватьРезультатТестаАутентификации(РезультатДиагностики) Экспорт
	
	Возврат ТолькоУчетныеЗаписиПрямогоОбмена(РезультатДиагностики);
	
КонецФункции 

Функция ТолькоУчетныеЗаписиПрямогоОбмена(РезультатДиагностики)
	
	Результат = Истина;
	Для каждого ДанныеУчетнойЗаписи Из РезультатДиагностики.ДанныеУчетныхЗаписей Цикл
		Если Не ДанныеУчетнойЗаписи.Значение.ЭтоПрямойОбмен Тогда
			Результат = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция РазделительПараметровМетодикиУстраненияОшибок() Экспорт
	
	Возврат "a650ea8b-e581-4bcf-9019-55d541316628";
	
КонецФункции 

Процедура ЗаполнитьРезультатПроверкиКорневыхСертификатовГУЦ(НаличиеКорневогоСертификатаГУЦ) Экспорт
	
	ВсеКорневыеСертификатыУстановлены = Истина;
	Для каждого ОписаниеСертификата Из НаличиеКорневогоСертификатаГУЦ.Сертификаты Цикл
		Если ОписаниеСертификата.Результат <> Истина Тогда
			ВсеКорневыеСертификатыУстановлены = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НаличиеКорневогоСертификатаГУЦ.Сертификаты.Количество() Тогда
		НаличиеКорневогоСертификатаГУЦ.Результат = ВсеКорневыеСертификатыУстановлены;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
