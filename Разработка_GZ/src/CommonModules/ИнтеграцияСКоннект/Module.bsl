///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ИнтеграцияСКоннект".
// ОбщийМодуль.ИнтеграцияСКоннект.
//
// Серверные процедуры интеграции с сервисом 1С-Коннект:
//  - сохранение и получение настроек интеграции;
//  - обработка событий БСП;
//  - начальное заполнение информационной базы.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет пользовательские настройки интеграции с 1С-Коннект.
//
// Возвращаемое значение:
//  Структура - настройки интеграции текущего пользователя:
//   *ОтображатьКнопкуЗапуска - Булево - признак необходимости отображения кнопки интеграции;
//   *РасположениеФайла - Строка - расположение файла приложения 1С-Коннект.
//
Функция НастройкиИнтеграции() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ИдентификаторКлиента = СистемнаяИнформация.ИдентификаторКлиента;
	
	НастройкиПользователя = ОписаниеНастроекПользователя();
	
	НастройкиХранилище = ХранилищеОбщихНастроек.Загрузить(
		"НастройкиПользователяКоннект",
		"НастройкиПользователяКоннект");
	
	Если НастройкиХранилище <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(
			НастройкиПользователя,
			НастройкиХранилище);
		
	Иначе
		
		// Поддержка обратной совместимости при переходе на новую реализацию
		// интеграции с 1С-Коннект.
		
		НастройкиПользователя.РасположениеФайла = ХранилищеОбщихНастроек.Загрузить(
			"ПутиИсполняемыхФайловВызовОнлайнПоддержки",
			ИдентификаторКлиента);
		
		НастройкиХранилище = ХранилищеОбщихНастроек.Загрузить(
			"УчетныеЗаписиПользователейВызовОнлайнПоддержки",
			"НастройкиУчетныхДанных");
		
		Если НастройкиХранилище <> Неопределено
			И ТипЗнч(НастройкиХранилище) = Тип("Структура") Тогда
			
			Если НастройкиХранилище.Свойство("ВидимостьКнопки1СБухфон") Тогда
				НастройкиПользователя.ОтображатьКнопкуЗапуска = НастройкиХранилище.ВидимостьКнопки1СБухфон;
			ИначеЕсли НастройкиХранилище.Свойство("ВидимостьКнопкиВызовОнлайнПоддержки") Тогда
				НастройкиПользователя.ОтображатьКнопкуЗапуска = НастройкиХранилище.ВидимостьКнопкиВызовОнлайнПоддержки;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Кнопка запуска не должна отображаться, если отключена работа
	// сервисом 1С-Коннект.
	УстановитьПривилегированныйРежим(Истина);
	Если НастройкиПользователя.ОтображатьКнопкуЗапуска Тогда
		НастройкиПользователя.ОтображатьКнопкуЗапуска = Константы.ИспользоватьКоннект.Получить();
	КонецЕсли;
	
	Возврат НастройкиПользователя;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область БазоваяФункциональностьБСП

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
// 
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Процедура = "ИнтеграцияСКоннект.НачальноеЗаполнение";
	
КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииПереименованийОбъектовМетаданных.
//
Процедура ПриДобавленииПереименованийОбъектовМетаданных(Итог) Экспорт
	
	ОбщегоНазначения.ДобавитьПереименование(
		Итог,
		"2.4.1.1",
		"Роль.Интеграция1СБухфон",
		"Роль.ВызовОнлайнПоддержки",
		"СтандартныеПодсистемы");
	
	ОбщегоНазначения.ДобавитьПереименование(
		Итог,
		"2.4.2.20",
		"Роль.ВызовОнлайнПоддержки",
		"Роль.ИнтеграцияСКоннект",
		"ИнтернетПоддержкаПользователей");
	
	// Временная мера. Некоторые библиотеки используют
	// наименование роли для настройки профиля доступа.
	ОбщегоНазначения.ДобавитьПереименование(
		Итог,
		"2.4.2.32",
		"Роль.ИнтеграцияСКоннект",
		"Роль.ВызовОнлайнПоддержки",
		"ИнтернетПоддержкаПользователей");
	
КонецПроцедуры

#КонецОбласти

#Область БСПНастройкиПрограммы

// Вызывается из обработчика ПриСозданииНаСервере() панели администрирования
// БСП. Выполняется настройку отображения элементов управления для подсистем
// библиотеки ИПП.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма панели управления.
//
Процедура ИнтернетПоддержкаИСервисы_ПриСозданииНаСервере(Форма) Экспорт
	
	Если ДоступнаИнтеграцияСКоннект()
		И ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.НастройкаПодключенияКоннект) Тогда
		
		Форма.БИПИнтеграцияСКоннект = Константы.ИспользоватьКоннект.Получить();
		Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
			Форма.Элементы.БИПИнтеграцияСКоннект.Доступность = Ложь;
		КонецЕсли;
		
		Если Не Форма.БИПИнтеграцияСКоннект Тогда
			Форма.Элементы.БИПНастройкаИнтеграцииСКоннект.Доступность = Ложь;
		КонецЕсли;
		
	Иначе
		Форма.Элементы.БИПГруппаИнтеграцияСКоннект.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настройка подсистемы при создании новой информационной базы.
//
Процедура НачальноеЗаполнение() Экспорт
	
	Константы.ИспользоватьКоннект.Установить(Истина);
	
КонецПроцедуры

// Обновляет пользовательские настройки интеграции с 1С-Коннект.
//
// Параметры:
//  РасположениеФайла - Строка - расположение файла приложения 1С-Коннект;
//  ОтображатьКнопкуЗапуска - Булево - признак необходимости отображения кнопки интеграции.
//
Процедура СохранитьНастройкиИнтеграции(
		РасположениеФайла,
		ОтображатьКнопкуЗапуска) Экспорт
		
	НастройкиПользователя = ОписаниеНастроекПользователя();
	НастройкиПользователя.РасположениеФайла       = РасположениеФайла;
	НастройкиПользователя.ОтображатьКнопкуЗапуска = ОтображатьКнопкуЗапуска;
	
	ХранилищеОбщихНастроек.Сохранить(
		"НастройкиПользователяКоннект",
		"НастройкиПользователяКоннект",
		НастройкиПользователя);
	
КонецПроцедуры

// Формирует описание пользовательских настроек интеграции с 1С-Коннект.
//
// Возвращаемое значение:
//  Структура - пользовательские настройки интеграции с 1С-Коннект
//
Функция ОписаниеНастроекПользователя()
	
	НастройкиПользователя = Новый Структура();
	НастройкиПользователя.Вставить("РасположениеФайла",       "");
	НастройкиПользователя.Вставить("ОтображатьКнопкуЗапуска", Ложь);
	
	Возврат НастройкиПользователя;
	
КонецФункции

// Обновляет настройку использования интеграции с сервисом 1С-Коннект.
//
// Параметры:
//  Значение - Булево - новое значение настройки использования.
//
Процедура УстановитьИспользованиеИнтеграции(Значение) Экспорт
	
	Если Не ПравоДоступа("Изменение", Метаданные.Константы.ИспользоватьКоннект) Тогда
		ВызватьИсключение НСтр("ru = ''Недостаточно прав доступа для включения или отключения настройки интеграции с сервисом ""1С-Коннект"".'");
	КонецЕсли;
	
	Константы.ИспользоватьКоннект.Установить(Значение);
	
КонецПроцедуры

// Определяет доступность интеграции с 1С-Коннект.
//
// Возвращаемое значение:
//  Булево - если Истина, интеграция с 1С-Коннект доступна.
//
Функция ДоступнаИнтеграцияСКоннект() Экспорт
	
	Возврат ОбщегоНазначения.ЭтоWindowsКлиент();
	
КонецФункции

#КонецОбласти
