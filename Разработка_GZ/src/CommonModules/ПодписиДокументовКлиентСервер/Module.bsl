
#Область СлужебныйПрограммныйИнтерфейс

// АПК:581-выкл используется в расширенной версии библиотеки.

// Формирует описание формы документа для обращения к организации.
// 
// Возвращаемое значение:
//  Структура - описание формы документа.
//
Функция ОписаниеФормыОбъектаДляОрганизации() Экспорт
	
	ОписаниеФормыОбъекта = Новый Структура;
	
	ОписаниеФормыОбъекта.Вставить("ИмяРеквизитаОрганизация");
	ОписаниеФормыОбъекта.Вставить("ПрефиксФормыОбъект");
	ОписаниеФормыОбъекта.Вставить("ИмяПосадочнойГруппы");
	
	Возврат ОписаниеФормыОбъекта;
	
КонецФункции

// АПК:581-вкл

// Формирует описание формы документа для обращения к организации.
// 
// Возвращаемое значение:
//  Структура - см. ОписаниеФормыОбъектаДляОрганизации.
//
Функция ОписаниеФормыОбъектаДляОрганизацииПоУмолчанию() Экспорт
	
	ОписаниеФормыОбъекта = ОписаниеФормыОбъектаДляОрганизации();
	
	ОписаниеФормыОбъекта.ПрефиксФормыОбъект = "Объект.";
	ОписаниеФормыОбъекта.ИмяРеквизитаОрганизация = "Организация";
	ОписаниеФормыОбъекта.ИмяПосадочнойГруппы = ИмяПосадочнойГруппыПоОрганизации(ОписаниеФормыОбъекта.ИмяРеквизитаОрганизация);
	
	Возврат ОписаниеФормыОбъекта;
	
КонецФункции

// АПК:559-выкл используется на сервере в расширенной версии библиотеки.

// Заполняет основания полномочий в форме документа с подписями.
//
// Параметры:
//  Форма					 - УправляемаяФорма - форма документа.
//  СтруктураРеквизитов		 - Структура - описание подписи, см. ПодписиДокументов.ОписаниеРеквизитовПодписанта.
//  ДанныеФизическогоЛица	 - Структура - описание данных физического лица.
//
Процедура ЗаполнитьОснованияПолномочийВФорме(Форма, СтруктураРеквизитов, ДанныеФизическогоЛица) Экспорт 

	// Очищаем поля
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
		Форма,
		СтруктураРеквизитов.Должность,
		ПредопределенноеЗначение("Справочник.Должности.ПустаяСсылка"));
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
		Форма,
		СтруктураРеквизитов.ОснованиеПодписи,
		"");
	
	// Получаем данные
	Если ДанныеФизическогоЛица = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Устанавливаем
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
		Форма,
		СтруктураРеквизитов.Должность,
		ДанныеФизическогоЛица.Должность);
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
		Форма,
		СтруктураРеквизитов.ОснованиеПодписи,
		ДанныеФизическогоЛица.ОснованиеПодписи);

КонецПроцедуры

// АПК:559-вкл

Процедура УстановитьПредставлениеПодписей(Форма, ОписаниеФормыОбъекта = Неопределено) Экспорт

	Если Не СозданыРеквизитыПодписей(Форма) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОписаниеФормыОбъекта = Неопределено Тогда
		ОписаниеФормыОбъекта = ОписаниеФормыОбъектаДляОрганизацииПоУмолчанию();
	КонецЕсли;
	
	ОписанияПолейПодписантов = СоответствиеОписанийПодписейДокументаДляОрганизации(Форма, ОписаниеФормыОбъекта);
	
	Для Каждого ОписаниеПолейПодписанта Из ОписанияПолейПодписантов Цикл
		ИмяРасширеннойПодсказки = ИмяРасширеннойПодсказкиЭлементаФормыПоРолиПодписанта(
									ОписаниеПолейПодписанта.Ключ, ОписаниеФормыОбъекта.ИмяРеквизитаОрганизация);
		УстановитьПредставлениеПодписи(Форма, ИмяРасширеннойПодсказки, ОписаниеПолейПодписанта.Значение);
	КонецЦикла; 

КонецПроцедуры

Процедура ДополнитьТекстОснованиемПодписи(ДополняемыйТекст, ОснованиеПодписи) Экспорт

	Если ЗначениеЗаполнено(ОснованиеПодписи) И ПодписиДокументовВызовСервера.ИспользоватьОснованияПолномочий() Тогда
		ДополняемыйТекст = ДополняемыйТекст + " /" + ОснованиеПодписи + "/";
	КонецЕсли;

КонецПроцедуры

Функция ИмяЭлементаФормыПоРолиПодписанта(РольПодписанта, ИмяРеквизитаОрганизация = "Организация") Экспорт
	Возврат ИмяРеквизитаОрганизация + "Подписи" + РольПодписанта;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СозданыРеквизитыПодписей(Форма) Экспорт
	Возврат ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ОписаниеПодписейДокумента");
КонецФункции

Функция ЗаголовокСвернутогоОтображенияГруппы(ПредставляемыеЗначения)
	
	Если ПредставляемыеЗначения.Количество() = 0 Тогда
		Возврат НСтр("ru = 'Подписи не указаны'");
	Иначе
		ПредставлениеЗначений = СтрСоединить(ПредставляемыеЗначения, ", ");
		Возврат НСтр("ru = 'Подписи'") + ": " + ПредставлениеЗначений;
	КонецЕсли;	
	
КонецФункции

Функция ИмяРасширеннойПодсказкиЭлементаФормыПоРолиПодписанта(РольПодписанта, ИмяРеквизитаОрганизация = "Организация")
	Возврат ИмяРеквизитаОрганизация + "Подписи" + РольПодписанта + "РасширеннаяПодсказка";
КонецФункции

#Область ОписаниеПодписантовОбъекта

Функция СоответствиеОписанийПодписейДокументаДляОрганизации(Форма, ОписаниеФормыОбъекта, ДобавлятьПрефиксОбъекта = Истина) Экспорт
	
	МассивОписанийДляОрганизации = МассивОписанийИменРеквизитовДляОрганизации(Форма, ОписаниеФормыОбъекта.ИмяРеквизитаОрганизация);
		
	ПрефиксИменРеквизитов = ?(ДобавлятьПрефиксОбъекта, ОписаниеФормыОбъекта.ПрефиксФормыОбъект, "");
	
	Возврат СоответствиеОписанийИзКоллекцииОписаний(МассивОписанийДляОрганизации, ПрефиксИменРеквизитов);
	
КонецФункции

Функция МассивОписанийИменРеквизитовДляОрганизации(Форма, ИмяРеквизитаОрганизация) Экспорт
	
	ВозвращаемыйМассив = Новый Массив;
	
	МассивОписаний = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "ОписаниеПодписейДокумента");
	Для Каждого СтруктураИменРеквизитов Из МассивОписаний Цикл
		Если СтруктураИменРеквизитов.Организация <> ИмяРеквизитаОрганизация Тогда
			Продолжить;
		КонецЕсли;
		
		ВозвращаемыйМассив.Добавить(СтруктураИменРеквизитов);
	КонецЦикла; 
	
	Возврат ВозвращаемыйМассив;
	
КонецФункции

#КонецОбласти

Процедура ЗапомнитьПодписиДокументовВФорме(Форма, ОписаниеФормыОбъекта, СменаПодписиВДокументе = Ложь) Экспорт

	Если СменаПодписиВДокументе Тогда
		СменаПодписантовВФорме = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "СменаПодписантов");
		Если ТипЗнч(СменаПодписантовВФорме) = Тип("ФиксированноеСоответствие") Тогда
			СменаПодписантов = Новый Соответствие(СменаПодписантовВФорме);
			СменаПодписантовПоОрганизации = СменаПодписантов.Получить(ОписаниеФормыОбъекта.ИмяРеквизитаОрганизация);
		Иначе
			СменаПодписантовПоОрганизации = Новый Соответствие;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеПолейПодписантов = СоответствиеОписанийПодписейДокументаДляОрганизации(Форма, ОписаниеФормыОбъекта);
	
	СведенияОбОтветственныхРаботникахВФорме = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "СведенияОбОтветственныхРаботниках");
	Если ТипЗнч(СведенияОбОтветственныхРаботникахВФорме) = Тип("ФиксированноеСоответствие") Тогда
		СведенияОбОтветственныхРаботниках = Новый Соответствие(СведенияОбОтветственныхРаботникахВФорме);
	Иначе
		СведенияОбОтветственныхРаботниках = Новый Соответствие;
	КонецЕсли;
	
	ДанныеФормыПоОрганизации = Новый Структура;
	Для Каждого КлючИЗначение Из ОписаниеПолейПодписантов Цикл
		СтруктураОписанияПодписанта = КлючИЗначение.Значение;
		ФизическоеЛицо = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, СтруктураОписанияПодписанта.ФизическоеЛицо);
		
		Если СменаПодписиВДокументе Тогда
			Если СменаПодписантовПоОрганизации.Получить(КлючИЗначение.Ключ) = НастройкиСменыПодписи().НеЗапоминать Тогда
				Возврат;
			Иначе
				ДанныеФормыПоОрганизации.Вставить(КлючИЗначение.Ключ, ФизическоеЛицо);
			КонецЕсли;
		Иначе
			ДанныеФормыПоОрганизации.Вставить(КлючИЗначение.Ключ, ФизическоеЛицо);
		КонецЕсли;
	
	КонецЦикла;
	
	СведенияОбОтветственныхРаботниках.Вставить(ОписаниеФормыОбъекта.ИмяРеквизитаОрганизация, ДанныеФормыПоОрганизации);
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма, "СведенияОбОтветственныхРаботниках", Новый ФиксированноеСоответствие(СведенияОбОтветственныхРаботниках));
	
КонецПроцедуры

Процедура УстановитьЗаголовокГруппеПодписей(Форма, ОписаниеФормыОбъекта = Неопределено) Экспорт

	Если ОписаниеФормыОбъекта = Неопределено Тогда
		ОписаниеФормыОбъекта = ОписаниеФормыОбъектаДляОрганизацииПоУмолчанию();
	КонецЕсли;
	
	ОписаниеПодписейДокумента = МассивОписанийИменРеквизитовДляОрганизации(Форма, ОписаниеФормыОбъекта.ИмяРеквизитаОрганизация);
	
	ИОФамилии = Новый Массив;
	Для Каждого ОписаниеПодписанта Из ОписаниеПодписейДокумента Цикл
		
		ФизическоеЛицо = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
			Форма,
			ОписаниеФормыОбъекта.ПрефиксФормыОбъект + ОписаниеПодписанта.ФизическоеЛицо);
			
		Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
			Продолжить;
		КонецЕсли;
		
		ИОФамилии.Добавить(ФизическиеЛицаЗарплатаКадрыКлиентСервер.ФамилияИнициалы(
			ПодписиДокументовВызовСервера.ПолноеИмяФизическогоЛица(ФизическоеЛицо)));
		
	КонецЦикла;		
	
	Если ИОФамилии.Количество() > 0 Тогда
		ТекстЗаголовка = ЗаголовокСвернутогоОтображенияГруппы(ИОФамилии);
	Иначе	
		ТекстЗаголовка = НСтр("ru = 'Подписи не указаны'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		ОписаниеФормыОбъекта.ИмяПосадочнойГруппы,
		"ЗаголовокСвернутогоОтображения",
		ТекстЗаголовка);

КонецПроцедуры

// Заполняет расширенную подсказку элементов подписей.
//
Процедура УстановитьПредставлениеПодписи(Форма, ИмяЭлемента, СтруктураИменРеквизитов) Экспорт
	
	ФизическоеЛицо = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
		Форма, СтруктураИменРеквизитов.ФизическоеЛицо);
		
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Должность = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
			Форма, СтруктураИменРеквизитов.Должность);
		ОснованиеПодписи = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
			Форма, СтруктураИменРеквизитов.ОснованиеПодписи);
			
		ТекстПредставления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 %2'"),
				?(ЗначениеЗаполнено(Должность), Должность, НСтр("ru = '<должность не указана>'")));
				
		ДополнитьТекстОснованиемПодписи(ТекстПредставления, ОснованиеПодписи);
	Иначе
		ТекстПредставления = " ";
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		ИмяЭлемента,
		"Гиперссылка",
		ЗначениеЗаполнено(ФизическоеЛицо) И Форма.ЕстьПравоИзмененияОснованийПолномочий);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		ИмяЭлемента,
		"Заголовок",
		ТекстПредставления);
	
КонецПроцедуры

Функция ИмяПосадочнойГруппыПоОрганизации(ИмяРеквизитаОрганизация) Экспорт 
	
	Если ИмяРеквизитаОрганизация = "Организация" Тогда
		Возврат "ПодписиГруппа";
	Иначе
		Возврат ИмяРеквизитаОрганизация + "ПодписиГруппа";
	КонецЕсли;
	
КонецФункции

Функция СоответствиеОписанийИзКоллекцииОписаний(КоллекцияОписанийРеквизитов, ПрефиксИменРеквизитов = "") Экспорт

	СоответствиеИменРеквизитов = Новый Соответствие;
	
	Для Каждого СтруктураИменРеквизитов Из КоллекцияОписанийРеквизитов Цикл
		ДополненнаяСтруктура = Новый Структура;
		Для Каждого КлючИЗначение Из СтруктураИменРеквизитов Цикл
			Если КлючИЗначение.Ключ = "Ключ" Или КлючИЗначение.Ключ = "Организация" Тогда
				Продолжить;
			КонецЕсли;
			ДополненнаяСтруктура.Вставить(КлючИЗначение.Ключ, ПрефиксИменРеквизитов + КлючИЗначение.Значение);
		КонецЦикла; 
		СоответствиеИменРеквизитов.Вставить(СтруктураИменРеквизитов.РольПодписанта, ДополненнаяСтруктура);
	КонецЦикла; 
	
	Возврат СоответствиеИменРеквизитов;
	
КонецФункции

Функция НастройкиСменыПодписи() Экспорт
	
	СтруктураНастройки = Новый Структура;
	СтруктураНастройки.Вставить("ВызыватьДиалог", "ВызыватьДиалог");
	СтруктураНастройки.Вставить("ЗапоминатьДляВсехДокументов", "ЗапоминатьДляВсехДокументов");
	СтруктураНастройки.Вставить("ЗапоминатьПоВидамДокументов", "ЗапоминатьПоВидамДокументов");
	СтруктураНастройки.Вставить("НеЗапоминать", "НеЗапоминать");
	
	Возврат СтруктураНастройки;
	
КонецФункции

#КонецОбласти