////////////////////////////////////////////////////////////////////////////////
// ПоступлениеНаРасчетныйСчетФормыВызовСервера: серверные процедуры и функции,
// вызываемые из форм документа "Поступление на расчетный счет".
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция СвойстваСтрокРасшифровкиПлатежаСервер(Знач ПараметрыПлатежа, Знач ПолучатьДоговор) Экспорт
	
	Возврат ПоступлениеНаРасчетныйСчетФормы.СвойстваСтрокРасшифровкиПлатежаСервер(ПараметрыПлатежа, ПолучатьДоговор);
	
КонецФункции

Процедура ЗаполнитьОтражениеСтрокиВУСННаСервере(СтрокаПлатеж, Знач ПараметрыУСН) Экспорт
	
	НалоговыйУчетУСН.ЗаполнитьОтражениеВУСНСтрокиРасшифровкиПлатежа(СтрокаПлатеж, ПараметрыУСН);
	
КонецПроцедуры

Процедура УстановитьДоговорКонтрагента(ДоговорКонтрагента, Знач Контрагент, Знач Организация, Знач СписокВидовДоговоров, Знач Отбор = Неопределено) Экспорт
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
		ДоговорКонтрагента, Контрагент, Организация, СписокВидовДоговоров, Отбор);
	
КонецПроцедуры

Процедура ОбработатьИзмененияВОрганизации(Знач Организация, ИспользоватьНесколькоБанковскихСчетовОрганизации, ОсновнойБанковскийСчетОрганизацииЗаполнен, СчетОрганизации) Экспорт
	
	ИспользоватьНесколькоБанковскихСчетовОрганизации =
		Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Организация);
	
	ОсновнойБанковскийСчетОрганизацииЗаполнен =
		ПроверкаРеквизитовОрганизации.ОсновнойБанковскийСчетОрганизацииЗаполнен(Организация);
	
	Если
		НЕ ИспользоватьНесколькоБанковскихСчетовОрганизации
		И ОсновнойБанковскийСчетОрганизацииЗаполнен
		И НЕ ЗначениеЗаполнено(СчетОрганизации) Тогда
		СчетОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ОсновнойБанковскийСчет");
	КонецЕсли;
	
КонецПроцедуры

Функция НастроитьСтатьяДДСДляОперации(СтатьяДДС, КонтекстОперации) Экспорт
	
	Если Не ПравоДоступа("Изменение", Метаданные.Справочники.СтатьиДвиженияДенежныхСредств) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Не БанкИКассаФормы.СтатьяДДСДляОперацииНастроена(СтатьяДДС, КонтекстОперации);
	
КонецФункции

Процедура УстановитьСтатьюДДСПоУмолчанию(РезультатВыполнения) Экспорт
	
	СтатьяДДСОбъект = РезультатВыполнения.СтатьяДДС.ПолучитьОбъект();
	ЗаполнитьЗначенияСвойств(СтатьяДДСОбъект, РезультатВыполнения);
	СтатьяДДСОбъект.Записать();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

Процедура УстановитьДоговорУмолчанию(Договор) Экспорт
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьОсновнойДоговорКонтрагента(Договор);
	
КонецПроцедуры

Функция НастроитьОсновнойДоговор(ПараметрыПлатежа) Экспорт
	
	Если Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ОсновныеДоговорыКонтрагента) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Не БанкИКассаФормы.ЕстьОсновнойДоговор(ПараметрыПлатежа);
	
КонецФункции

#КонецОбласти
