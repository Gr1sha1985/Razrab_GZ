// Механизм расчета статусов оформления документов ВЕТИС.
//

#Область СлужебныйПрограммныйИнтерфейс

// Позволяет переопределить имена реквизитов документа-основания для документа ВЕТИС.
//
// Параметры:
//	МетаданныеДокументаОснования - ОбъектМетаданных:Документ - метаданные документа из ОпределяемыйТип.Основание<Имя документа ВЕТИС>
//	МетаданныеДокументаВЕТИС 	 - ОбъектМетаданных:Документ - метаданные документа из ОпределяемыйТип.ДокументыВЕТИСПоддерживающиеСтатусыОформления
//	Реквизиты 					 - Структура - имена реквизитов
//		Ключ 	 - служебное имя реквизита в ВЕТИС
//		Значение - имя реквизита документа-основания, которое при необходимости надо переопределить
//			Подробнее см. описание РасчетСтатусовОформленияВЕТИС.СтруктураРеквизитовДляРасчетаСтатусаОформленияДокументовВЕТИС	
//
Процедура ПриОпределенииИменРеквизитовДокументаДляРасчетаСтатусаОформленияДокументаВЕТИС(
			Знач МетаданныеДокументаОснования, Знач МетаданныеДокументаВЕТИС, Реквизиты) Экспорт
	
	// Зададим имена реквизитов по умолчанию.
	Реквизиты.Контрагент                        = "Организация";
	Реквизиты.ТорговыйИлиПроизводственныйОбъект = "Склад";
	Реквизиты.Ответственный                     = "Ответственный";
		
	// Уточним имена реквизитов для конкретных документов.
	Если МетаданныеДокументаОснования = Метаданные.Документы.ПеремещениеТоваров Тогда
		
		Если МетаданныеДокументаВЕТИС = Метаданные.Документы.ИсходящаяТранспортнаяОперацияВЕТИС Тогда
			
			Реквизиты.Контрагент = "Организация";
			Реквизиты.ТорговыйИлиПроизводственныйОбъект = "СкладОтправитель";
			
		ИначеЕсли МетаданныеДокументаВЕТИС = Метаданные.Документы.ВходящаяТранспортнаяОперацияВЕТИС Тогда
			
			Реквизиты.Контрагент = "Организация";
			Реквизиты.ТорговыйИлиПроизводственныйОбъект = "СкладПолучатель";
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОтчетПроизводстваЗаСмену Тогда
		
		Реквизиты.ТорговыйИлиПроизводственныйОбъект = "ПодразделениеЗатрат";
		
	КонецЕсли;
	
КонецПроцедуры

// Позволяет переопределить текст запроса выборки данных из документов-основания для расчета статуса оформления.
//
// Параметры:
//	МетаданныеДокументаОснования - ОбъектМетаданных:Документ - метаданные документа из ОпределяемыйТип.Основание<Имя документа ВЕТИС>
//	МетаданныеДокументаВЕТИС 	 - ОбъектМетаданных:Документ - метаданные документа из ОпределяемыйТип.ДокументыВЕТИСПоддерживающиеСтатусыОформления
//	ТекстЗапроса 				 - Строка - текст запроса выборки данных, который надо переопределить
//		Требования к тексту запроса:
//		- результат запроса обязательно должен содержать следующие поля:
//			- Ссылка 			- ОпределяемыйТип.Основание<Имя документа ВЕТИС> - ссылка на документ-основание
//			- ЭтоДвижениеПриход - Булево - вид движения ТМЦ (Истина - приход, Ложь - расход)
//			- Номенклатура 		- ОпределяемыйТип.Номенклатура - номенклатура
//			- Характеристика 	- ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика номенклатуры
//			- Серия 			- ОпределяемыйТип.СерияНоменклатуры - серия номенклатуры
//			- Количество 		- Число - количество номенклатуры в ее основной единице измерения
//		- в результат запроса нужно включать только подконтрольную номенклатуру ВЕТИС
//		- для отбора документов-основания в запросе нужно использовать отбор "В (&МассивДокументов)"
//		- выбранные данные необходимо поместить во временную таблицу,
//			см. РасчетСтатусовОформленияВЕТИС.ИмяВременнойТаблицыДляВыборкиДанныхДокумента
//		Если данные из документа выбирать не требуется, то данному параметру надо присвоить значение "" (пустая строка).
//	ДополнительныеПараметрыЗапроса - Структура - дополнительные параметры запроса,
//	 	требуемые для выполнения запроса конкретного документа; при необходимости можно дополнить данную структуру
//		- Ключ 	   - имя параметры
//		- Значение - значение параметра
//
Процедура ПриОпределенииТекстаЗапросаДляРасчетаСтатусаОформленияДокументаВЕТИС(
			Знач МетаданныеДокументаОснования, Знач МетаданныеДокументаВЕТИС, ТекстЗапроса, ДополнительныеПараметрыЗапроса) Экспорт
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.ОтчетПроизводстваЗаСмену Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ОтчетПроизводстваЗаСмену.Продукция КАК ТаблицаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
		|
		|ГДЕ
		|	ТаблицаТовары.Ссылка В (&МассивДокументов)
		|	И СправочникНоменклатура.ПодконтрольнаяПродукцияВЕТИС
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ИЗ
		|	Документ.ОтчетПроизводстваЗаСмену.Материалы КАК ТаблицаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
		|
		|ГДЕ
		|	ТаблицаТовары.Ссылка В (&МассивДокументов)
		|	И СправочникНоменклатура.ПодконтрольнаяПродукцияВЕТИС
		|";
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.СписаниеТоваров
		ИЛИ МетаданныеДокументаОснования = Метаданные.Документы.РеализацияТоваровУслуг
		ИЛИ МетаданныеДокументаОснования = Метаданные.Документы.ВозвратТоваровПоставщику
		ИЛИ (МетаданныеДокументаОснования = Метаданные.Документы.ПеремещениеТоваров
		И МетаданныеДокументаВЕТИС = Метаданные.Документы.ИсходящаяТранспортнаяОперацияВЕТИС) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЛОЖЬ КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.СписаниеТоваров.Товары КАК ТаблицаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
		|
		|ГДЕ
		|	ТаблицаТовары.Ссылка В (&МассивДокументов)
		|	И СправочникНоменклатура.ПодконтрольнаяПродукцияВЕТИС
		|";
		
		ИмяОснования         = МетаданныеДокументаОснования.Имя;
		ТекстЗапроса         = СтрЗаменить(ТекстЗапроса, "СписаниеТоваров", ИмяОснования);
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОприходованиеТоваров
		ИЛИ МетаданныеДокументаОснования = Метаданные.Документы.ПоступлениеТоваровУслуг Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЭтоДвижениеПриход,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	"""" КАК Характеристика,
		|	"""" КАК Серия,
		|	ТаблицаТовары.Количество КАК Количество
		|ПОМЕСТИТЬ %1
		|ИЗ
		|	Документ.ОприходованиеТоваров.Товары КАК ТаблицаТовары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО СправочникНоменклатура.Ссылка = ТаблицаТовары.Номенклатура
		|
		|ГДЕ
		|	ТаблицаТовары.Ссылка В (&МассивДокументов)
		|	И СправочникНоменклатура.ПодконтрольнаяПродукцияВЕТИС
		|";
		
		ИмяОснования         = МетаданныеДокументаОснования.Имя;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ОприходованиеТоваров", ИмяОснования);
		
	Иначе
		
		ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 0
		|	ЛОЖЬ		 КАК ЭтоДвижениеПриход,
		|	НЕОПРЕДЕЛЕНО КАК Ссылка,
		|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Характеристика,
		|	НЕОПРЕДЕЛЕНО КАК Серия,
		|	0 			 КАК Количество
		|ПОМЕСТИТЬ %1";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
