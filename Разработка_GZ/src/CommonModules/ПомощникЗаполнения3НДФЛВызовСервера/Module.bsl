#Область ПрограммныйИнтерфейс

#Область ЗаполнениеПоИНН

Функция ДанныеЗаполненияПоИНН(Знач ИНН) Экспорт
	
	// Заполнение по ИНН в помощнике 3-НДФЛ доступно только для юридических лиц
	ЭтоЮрЛицо = Истина;
	
	ДанныеЗаполнения = Новый Структура("ОписаниеОшибки", "");
	
	ИНН = СокрП(ИНН);
	
	РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(ИНН, ЭтоЮрЛицо);
	
	Если РезультатПроверки.СоответствуетТребованиям Тогда
		СведенияОКонтрагенте = РаботаСКонтрагентами.СведенияОЮридическомЛицеПоИНН(ИНН);
		Если ЗначениеЗаполнено(СведенияОКонтрагенте.ЕГРЮЛ) Тогда
			РеквизитыКонтрагента = СведенияОКонтрагенте.ЕГРЮЛ;
		Иначе
			РеквизитыКонтрагента = Новый Структура;
			ШаблонОписанияОшибки = НСтр("ru = 'Не удалось найти данные для заполнения реквизитов по ИНН %1.'");
			РеквизитыКонтрагента.Вставить("ОписаниеОшибки", СтрШаблон(ШаблонОписанияОшибки, ИНН));
		КонецЕсли;
		РеквизитыКонтрагента = СведенияОКонтрагенте.ЕГРЮЛ;
		РеквизитыКонтрагента.Вставить("ИНН", ИНН);
		Руководитель = Новый Структура("Фамилия, Имя, Отчество, Представление, ИНН, ДатаЗаписи, Должность");
		Если Не ЗначениеЗаполнено(РеквизитыКонтрагента.ОписаниеОшибки) Тогда
			Если ЗначениеЗаполнено(РеквизитыКонтрагента.Руководители) Тогда
				ЗаполнитьЗначенияСвойств(Руководитель, РеквизитыКонтрагента.Руководители[0]);
			Иначе
				Руководитель = Неопределено;
			КонецЕсли;
			РеквизитыКонтрагента.Вставить("Руководитель", Руководитель);
			РеквизитыКонтрагента.Вставить("КодОКВЭД",
				?(ЗначениеЗаполнено(РеквизитыКонтрагента.ВидДеятельности), РеквизитыКонтрагента.ВидДеятельности.Код, Неопределено));
			РеквизитыКонтрагента.Вставить("ЭтоОКВЭД2",  
				?(ЗначениеЗаполнено(РеквизитыКонтрагента.ВидДеятельности) И РеквизитыКонтрагента.ВидДеятельности.Классификатор = "ОКВЭД2", Истина, Ложь));
		КонецЕсли;
		
		РеквизитыКонтрагента.Вставить("ЮридическоеФизическоеЛицо", Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
		
		Если ЗначениеЗаполнено(РеквизитыКонтрагента.ОписаниеОшибки) Тогда
			ДанныеЗаполнения.ОписаниеОшибки = РеквизитыКонтрагента.ОписаниеОшибки;
		Иначе
			
			ОКТМО = "";
			Если ЗначениеЗаполнено(РеквизитыКонтрагента.ЮридическийАдрес)
				И ТипЗнч(РеквизитыКонтрагента.ЮридическийАдрес) = Тип("Структура")
				И РеквизитыКонтрагента.ЮридическийАдрес.Свойство("КонтактнаяИнформация")
				И ЗначениеЗаполнено(РеквизитыКонтрагента.ЮридическийАдрес.КонтактнаяИнформация) Тогда
				
				ЗначениеАдресаJSON = РеквизитыКонтрагента.ЮридическийАдрес.КонтактнаяИнформация;
				СведенияОНалоговомОрганеПоАдресу = АдресныйКлассификатор.КодыАдреса(ЗначениеАдресаJSON, "Сервис1С");
				ОКТМО = СведенияОНалоговомОрганеПоАдресу.ОКТМО;
				
			КонецЕсли;
			
			ДанныеЗаполнения.Вставить("Наименование", РеквизитыКонтрагента.НаименованиеСокращенное);
			ДанныеЗаполнения.Вставить("ИНН",   РеквизитыКонтрагента.ИНН);
			ДанныеЗаполнения.Вставить("КПП",   РеквизитыКонтрагента.КПП);
			ДанныеЗаполнения.Вставить("ОКТМО", ОКТМО);
			
		КонецЕсли;
	Иначе
		ДанныеЗаполнения.ОписаниеОшибки = РезультатПроверки.ОписаниеОшибки;
	КонецЕсли;
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

#КонецОбласти

#КонецОбласти