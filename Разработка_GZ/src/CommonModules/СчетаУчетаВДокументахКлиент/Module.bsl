Функция ЗаполнитьПриИзменении(ПараметрыЗаполнения) Экспорт
	
	Результат = Новый Структура;
	
	Если Не ПараметрыЗаполнения.Заполнять Тогда
		// В форме не должно быть реквизитов, зависимых от счетов. Поэтому допускается вернуть пустой результат.
		Возврат Результат; 
	КонецЕсли;
	
	Если ПараметрыЗаполнения.КЗаполнению.Количество() = 0 Тогда
		// Нечего делать
		Возврат Результат;
	КонецЕсли;
	
	// Передадим данные на сервер
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения.Контейнер.Объект, ПараметрыЗаполнения.ДанныеФормы.Объект);
	
	// Можем заполнять только одну строку или только шапку.
	// Определим, что именно заполняем, по первому заполняемому реквизиту.
	// Все остальные должны быть такими же.
	ПолноеИмяРеквизита = "";
	Для Каждого КлючИЗначение Из ПараметрыЗаполнения.КЗаполнению Цикл
		ПолноеИмяРеквизита = КлючИЗначение.Ключ;
		Прервать;
	КонецЦикла;
	ИмяРеквизитаДетально = ОбщегоНазначенияБПКлиентСервер.РазложитьПолноеИмяРеквизита(ПолноеИмяРеквизита);
	Если Не ПустаяСтрока(ИмяРеквизитаДетально.ТабличнаяЧасть) Тогда
		
		// Заполняем строку.
		// Поэтому могут понадобиться данные строки
		
		ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения.Контейнер.Строка, ПараметрыЗаполнения.ДанныеФормы.Строка);
		
		РезультатЗаполнения = СчетаУчетаВДокументахВызовСервера.ЗаполнитьРеквизитыПриИзменении(
			ПараметрыЗаполнения.ИмяДокумента,
			ПараметрыЗаполнения.КЗаполнению,
			ПараметрыЗаполнения.Контейнер.Объект,
			ИмяРеквизитаДетально.ТабличнаяЧасть,
			ПараметрыЗаполнения.Контейнер.Строка);
			
		ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения.ДанныеФормы.Строка, РезультатЗаполнения);
		
	Иначе
		
		// Заполняем объект
		РезультатЗаполнения = СчетаУчетаВДокументахВызовСервера.ЗаполнитьРеквизитыПриИзменении(
			ПараметрыЗаполнения.ИмяДокумента,
			ПараметрыЗаполнения.КЗаполнению,
			ПараметрыЗаполнения.Контейнер.Объект);
			
		ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения.ДанныеФормы.Объект, РезультатЗаполнения);
		
	КонецЕсли;
		
	Возврат РезультатЗаполнения;
	
КонецФункции

// Стандартный обработчик события НачалоВыбора в полях форм, связанных с субконто к счету затрат.
//
// Параметры:
//  Форма				 - ФормаКлиентскогоПриложения - форма, которую требуется настроить
//  Элемент				 - ЭлементФормы - элемент, в котором начат выбор
//  СтандартнаяОбработка - Булево - возвращаемый; признак выполнения стандартной (системной) обработки
//  ОписаниеСубконто	 - см. СчетаУчетаВДокументахКлиентСервер.НовыйОписаниеСубконтоЗатрат()
Процедура НачалоВыбораЗначенияСубконтоЗатрат(Форма, Элемент, СтандартнаяОбработка, ОписаниеСубконто) Экспорт
	
	ШаблонПоляСубконто = ОписаниеСубконто.ИмяПоляСубконто + "%Индекс%";
	
	ПараметрыВыбора = СчетаУчетаВДокументахКлиентСервер.ПараметрыВыбораСубконтоЗатрат(Форма, ШаблонПоляСубконто, ОписаниеСубконто.ИмяСчета);
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(Форма, Элемент, СтандартнаяОбработка, ПараметрыВыбора);
	
КонецПроцедуры

