#Область ПрограммныйИнтерфейс

// Показывает на форме предупреждение о некорректной настройке налогов и отчетов.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - Форма, на которой следует показать предупреждение. Форма должна содержать:
//		Реквизит "ПредупреждениеНастройкиНалоговИОтчетов" (ФорматированнаяСтрока)
//		Элемент "ПредупреждениеНастройкиНалоговИОтчетов", отображающий данные соответствующего реквизита
//	Организация - СправочникОрганизации.Ссылка - Организация, настройки налогов и отчетов которой проверяются
//	Период - Дата - Дата, по состоянию на которую проверяются настройки налогов и отчетов
//	ПроверятьПатент - Булево - Признак проверки заполнения сведений о патенте
//
Процедура ОтобразитьПредупреждение(Форма, Организация, Период, ПроверятьПатент) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ВыводитьПредупреждение = Ложь;
	ТекстПредупреждения = "";
	
	ПравоРедактированияНастроекСистемыНалогообложения = ПравоДоступа("Изменение",
		Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения);
	
	Если Период >= УчетЕНВДКлиентСервер.ДатаОтменыЕНВД()
		И УчетнаяПолитика.ТолькоЕНВДПоДаннымУчетнойПолитики(Организация, Период) Тогда
		
		ВыводитьПредупреждение = Истина;
		
		Если ПравоРедактированияНастроекСистемыНалогообложения Тогда
			ДополнительныйТекст = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Измените настройки '"),
				Новый ФорматированнаяСтрока(НСтр("ru = 'системы налогообложения'"),,,, "НастройкаСистемыНалогообложения")
			);
		Иначе
			ДополнительныйТекст = НСтр("ru = 'Обратитесь к сотруднику, имеющему право настройки системы налогообложения.'");
		КонецЕсли;
		ТекстПредупреждения = Новый ФорматированнаяСтрока(
			НСтр("ru = 'С 01.01.2021 ЕНВД не применяется. '"),
			ДополнительныйТекст
		);
		
	ИначеЕсли ПроверятьПатент
		И УчетнаяПолитика.ПрименяетсяТолькоУСНПатент(Организация, Период)
		И Справочники.Патенты.ПатентыОрганизации(Организация, Период).Количество() = 0 Тогда
		
		ПравоРедактированияПатентов = ПравоДоступа("Изменение", Метаданные.Справочники.Патенты);
		
		ВыводитьПредупреждение = Истина;
		
		Если ПравоРедактированияПатентов Тогда
			ТекстПредупреждения = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Укажите '"),
				Новый ФорматированнаяСтрока(
					НСтр("ru = 'сведения о патенте'"),,,,
					УчетПСНКлиентСервер.НавигационнаяСсылкаНеобходимоЗаполнитьПатент()
				)
			);
		Иначе
			ТекстПредупреждения = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Обратитесь к сотруднику, имеющему право указывать сведения о патенте.'")
			);
		КонецЕсли;
		
	ИначеЕсли Не УчетнаяПолитика.СуществуетСистемаНалогообложения(Организация, Период, Ложь) Тогда
		
		ВыводитьПредупреждение = Истина;
		
		Если ПравоРедактированияНастроекСистемыНалогообложения Тогда
			ТекстПредупреждения = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Заполните настройки '"),
				Новый ФорматированнаяСтрока(НСтр("ru = 'системы налогообложения'"),,,, "НастройкаСистемыНалогообложения")
			);
		Иначе
			ТекстПредупреждения = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Обратитесь к сотруднику, имеющему право настройки системы налогообложения.'")
			);
		КонецЕсли;
		
	КонецЕсли;
	
	ЭлементФормы = Форма.Элементы.Найти("ПредупреждениеНастройкиНалоговИОтчетов");
	Если ЭлементФормы <> Неопределено Тогда
		Форма.Элементы.ПредупреждениеНастройкиНалоговИОтчетов.Видимость = ВыводитьПредупреждение;
		Форма.ПредупреждениеНастройкиНалоговИОтчетов = ТекстПредупреждения;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик проверки заполнения формы.
//
//	Форма - ФормаКлиентскогоПриложения - Форма, проверка заполенения которой выполняется. Форма должна содержать:
//		Реквизит "ПредупреждениеНастройкиНалоговИОтчетов" (ФорматированнаяСтрока)
//		Элемент "ПредупреждениеНастройкиНалоговИОтчетов", отображающий данные соответствующего реквизита
//	Организация - СправочникОрганизации.Ссылка - Организация, настройки налогов и отчетов которой проверяются
//	Период - Дата - Дата, по состоянию на которую проверяются настройки налогов и отчетов
//	ПроверятьПатент - Булево - Признак проверки заполнения сведений о патенте
//	Отказ - Булево - Отказ от продолжения работы после выполнения проверки заполнения
//
Процедура ПроверкаЗаполнения(Форма, Организация, Период, ПроверятьПатент, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если Период >= УчетЕНВДКлиентСервер.ДатаОтменыЕНВД()
		И УчетнаяПолитика.ТолькоЕНВДПоДаннымУчетнойПолитики(Организация, Период) Тогда
		
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Необходимо изменить настройки системы налогообложения'"),,
			"ПредупреждениеНастройкиНалоговИОтчетов",, Отказ);
		
	ИначеЕсли ПроверятьПатент
		И УчетнаяПолитика.ПрименяетсяТолькоУСНПатент(Организация, Период)
		И Справочники.Патенты.ПатентыОрганизации(Организация, Период).Количество() = 0 Тогда
		
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Необходимо указать сведения о патенте'"),,
			"ПредупреждениеНастройкиНалоговИОтчетов",, Отказ);
		
	ИначеЕсли Не УчетнаяПолитика.СуществуетСистемаНалогообложения(Организация, Период, Ложь) Тогда
		
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Необходимо заполнить настройки системы налогообложения'"),,
			"ПредупреждениеНастройкиНалоговИОтчетов",, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти






