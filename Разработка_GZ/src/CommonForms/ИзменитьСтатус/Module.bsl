#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоДокументов = Параметры.СписокДокументов.Количество();
	Если КоличествоДокументов = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	СписокДокументов = Параметры.СписокДокументов;
	
	КоллекцияСтатусов = РегистрыСведений.СтатусыДокументов.ПолучитьСтатусыДокументов(СписокДокументов.ВыгрузитьЗначения());
	
	СтатусыДокумента = КоллекцияСтатусов[СписокДокументов[0].Значение];
	Если ТипЗнч(СтатусыДокумента.Статус) = Тип("ПеречислениеСсылка.СтатусыДокументовПоступления") Тогда
		КлючНазначенияИспользования = "Поступления";
		Элементы.СтатусДокументаРеализации.Видимость = Ложь;
	ИначеЕсли ТипЗнч(СтатусыДокумента.Статус) = Тип("ПеречислениеСсылка.СтатусыДокументовРеализации") Тогда
		КлючНазначенияИспользования = "Реализации";
		Элементы.СтатусДокументаПоступления.Видимость = Ложь;
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ЭтотОбъект["СтатусДокумента" + КлючНазначенияИспользования] = СтатусыДокумента.Статус;
	
	// Проверим, совпадают ли статусы всех выбранных документов
	Для Индекс = 1 По КоличествоДокументов - 1 Цикл
		СтатусыДокумента = КоллекцияСтатусов[СписокДокументов[Индекс].Значение];
		Если ЭтотОбъект["СтатусДокумента" + КлючНазначенияИспользования] <> СтатусыДокумента.Статус Тогда
			ЭтотОбъект["СтатусДокумента" + КлючНазначенияИспользования] = Неопределено;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если КоличествоДокументов = 1 Тогда
		
		Заголовок = Строка(СписокДокументов[0].Значение);
		
		ПозицияСкобки = СтрНайти(Заголовок, "(");
		Если ПозицияСкобки <> 0 Тогда // уберем лишнюю уточняющую информацию
			Заголовок = Лев(Заголовок, ПозицияСкобки - 1) + Сред(Заголовок, СтрНайти(Заголовок, ")") + 1);
		КонецЕсли;
		
	Иначе
		
		Заголовок = СтрШаблон(НСтр("ru = 'Изменить статусы документов (%1)'"), КоличествоДокументов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ЗначениеЗаполнено(ЭтотОбъект["СтатусДокумента" + КлючНазначенияИспользования]) Тогда
		
		СтатусИзменен = ОбщегоНазначенияБПВызовСервера.УстановитьСтатусыДокументов(
			СписокДокументов, ЭтотОбъект["СтатусДокумента" + КлючНазначенияИспользования]);
		Если СтатусИзменен Тогда
			ОбщегоНазначенияБПКлиент.ОповеститьОбИзмененииСтатусовДокументов(
				ВладелецФормы, СписокДокументов, ЭтотОбъект["СтатусДокумента" + КлючНазначенияИспользования]);
		КонецЕсли;
		
	КонецЕсли;

	Закрыть();
	
КонецПроцедуры

#КонецОбласти
