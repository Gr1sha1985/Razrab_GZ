
// Хранит значения, часть из которых может существовать только на клиенте.
&НаКлиенте
Перем ПараметрыОткрытияКлиент Экспорт;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если Параметры.ПереопределенаНаСервере Тогда
		
		// Часть параметром открытия имеют типы, доступные только на клиенте. Поэтому перемещаем их в переменную формы.
		ПараметрыОткрытияКлиент = Параметры.ПараметрыОткрытия;
		Параметры.ПараметрыОткрытия = Неопределено;
		
		// Дозаполним те параметры, которые не были известны на сервере.
		// См. ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием()
		ПараметрыОткрытияКлиент.Владелец                    = ВладелецФормы;
		ПараметрыОткрытияКлиент.Уникальность                = КлючУникальности;
		ПараметрыОткрытияКлиент.Окно                        = Окно;
		ПараметрыОткрытияКлиент.НавигационнаяСсылка         = НавигационнаяСсылка;
		ПараметрыОткрытияКлиент.ОписаниеОповещенияОЗакрытии = ОписаниеОповещенияОЗакрытии;
		ПараметрыОткрытияКлиент.РежимОткрытияОкна           = РежимОткрытияОкна;

	КонецЕсли;
			
	ПодключитьОбработчикОжидания("Подключаемый_ОткрытьДлительнуюФорму", 0.5, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьДлительнуюФорму()
	
	ОбщегоНазначенияБПКлиент.ОткрытьОсновнуюФорму(Истина, ПараметрыОткрытияКлиент, Параметры.ПараметрыОткрываемойФормы);

	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры
