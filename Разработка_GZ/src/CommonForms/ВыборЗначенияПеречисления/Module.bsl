#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ПустаяСтрока(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	ВыбранноеЗначение = Параметры.ВыбранноеЗначение;
	
	КолонкаПодробноеПредставлениеЗаполнена = Ложь;
	
	Значения = ПолучитьИзВременногоХранилища(Параметры.АдресХранилищаЗначений);
	Для каждого Значение Из Значения Цикл
		НоваяСтрока = ЗначенияПеречисления.Добавить();
		НоваяСтрока.ЗначениеПеречисления = Значение;
		НоваяСтрока.Идентификатор = Значение.Идентификатор;
		НоваяСтрока.КраткоеПредставление = Значение.КраткоеПредставление;
		НоваяСтрока.ПодробноеПредставление = Значение.ПодробноеПредставление;
		
		Если ЗначениеЗаполнено(Значение.ПодробноеПредставление) Тогда
			КолонкаПодробноеПредставлениеЗаполнена = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.СписокПодробноеПредставление.Видимость = КолонкаПодробноеПредставлениеЗаполнена;
	
	ОбновитьСписок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(Элементы.Список.ТекущиеДанные.ЗначениеПеречисления);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	ОбновитьСписок();
	
	ТекущийЭлемент = Элементы.Список;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаОчистка(Элемент, СтандартнаяОбработка)
	
	ОбновитьСписок();
	
	ТекущийЭлемент = Элементы.Список;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ВыбранноеЗначение = ?(Элемент.ТекущиеДанные <> Неопределено,
		Элемент.ТекущиеДанные.Идентификатор,
		Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Элемент.ТекущиеДанные.ЗначениеПеречисления);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписок()
	
	Список.Очистить();
	
	Для каждого ЗначениеПеречисления Из ЗначенияПеречисления Цикл
		Если ПустаяСтрока(СтрокаПоиска)
		ИЛИ СтрНайти(НРег(ЗначениеПеречисления.КраткоеПредставление), НРег(СтрокаПоиска)) > 0
		ИЛИ СтрНайти(НРег(ЗначениеПеречисления.ПодробноеПредставление), НРег(СтрокаПоиска)) > 0 Тогда
			НоваяСтрока = Список.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначениеПеречисления);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", ВыбранноеЗначение);
		НайденныеСтроки = Список.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Элементы.Список.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
