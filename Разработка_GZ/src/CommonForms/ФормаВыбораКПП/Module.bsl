////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.Контрагент) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СвойстваКонтрагента	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Контрагент, "КПП, ЮридическоеФизическоеЛицо");
	
	Если СвойстваКонтрагента.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru ='КПП для физических лиц не задается'"), , , , Отказ);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДатаСведений   = Параметры.ДатаСведений;
	
	КППКонтрагента = Параметры.КППКонтрагента;
	ЗаполнитьСписокВыбораКППКонтрагента(СписокВыбораКПП, Параметры.Контрагент, ДатаСведений);
	Если ПустаяСтрока(КППКонтрагента) И СписокВыбораКПП.Количество() > 0 Тогда
		КППКонтрагента	= СписокВыбораКПП[0].Значение;
	КонецЕсли;
	
	ТекстПодсказки = Новый Массив;
	Если Параметры.РольКонтрагента = "Поставщик" Тогда
		Заголовок = НСтр("ru = 'КПП поставщика'");
		ТекстПодсказки.Добавить(НСтр("ru = 'При приобретении товаров и услуг через обособленные подразделения контрагентов,'"));
		ТекстПодсказки.Добавить(НСтр("ru = 'в строке 2б ""ИНН/КПП продавца"" счета-фактуры указывается КПП соответствующего'"));
		ТекстПодсказки.Добавить(НСтр("ru = 'обособленного подразделения'"));
	ИначеЕсли Параметры.РольКонтрагента = "Покупатель" Тогда
		Заголовок = НСтр("ru = 'КПП покупателя'");
		ТекстПодсказки.Добавить(НСтр("ru = 'При реализации товаров и услуг обособленным подразделениям контрагентов,'"));
		ТекстПодсказки.Добавить(НСтр("ru = 'в строке 6б ""ИНН/КПП покупателя"" счета-фактуры указывается КПП соответствующего'"));
		ТекстПодсказки.Добавить(НСтр("ru = 'обособленного подразделения'"));
	Иначе
		Заголовок = НСтр("ru = 'КПП контрагента'");
		Элементы.КППКонтрагента.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	Элементы.КППКонтрагента.РасширеннаяПодсказка.Заголовок = СтрСоединить(ТекстПодсказки, " ");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ВопросСохраненияДанныхЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КППКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ДанныеВыбора = СписокВыбораКПП;
			
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	
	СохранитьИзменения();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораКППКонтрагента(СписокВыбораКПП, ГоловнойКонтрагент, ДатаСведений)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГоловнойКонтрагент", ГоловнойКонтрагент);
	Запрос.УстановитьПараметр("ДатаСведений", ?(ЗначениеЗаполнено(ДатаСведений), ДатаСведений, ТекущаяДатаСеанса()));

	// Первый элемент - всегда КПП головного контрагента, даже, если не задан
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контрагенты.Ссылка КАК Контрагент,
	|	ИСТИНА КАК ГоловнойКонтрагент
	|ПОМЕСТИТЬ СсылкиНаКонтрагентов
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &ГоловнойКонтрагент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка,
	|	ЛОЖЬ
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.Ссылка = &ГоловнойКонтрагент
	|	И Контрагенты.ГоловнойКонтрагент = &ГоловнойКонтрагент
	|	И Контрагенты.ОбособленноеПодразделение
	|	И НЕ Контрагенты.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СсылкиНаКонтрагентов.Контрагент,
	|	СсылкиНаКонтрагентов.ГоловнойКонтрагент,
	|	МАКСИМУМ(КонтрагентыИсторияКПП.Период) КАК Период
	|ПОМЕСТИТЬ ИсторияКППКонтрагентов
	|ИЗ
	|	СсылкиНаКонтрагентов КАК СсылкиНаКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты.ИсторияКПП КАК КонтрагентыИсторияКПП
	|		ПО СсылкиНаКонтрагентов.Контрагент = КонтрагентыИсторияКПП.Ссылка
	|			И (КонтрагентыИсторияКПП.Период <= &ДатаСведений)
	|
	|СГРУППИРОВАТЬ ПО
	|	СсылкиНаКонтрагентов.Контрагент,
	|	СсылкиНаКонтрагентов.ГоловнойКонтрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияКППКонтрагентов.Контрагент.Представление КАК ПредставлениеКонтрагента,
	|	ЕСТЬNULL(КонтрагентыИсторияКПП.КПП, ИсторияКППКонтрагентов.Контрагент.КПП) КАК КПП
	|ИЗ
	|	ИсторияКППКонтрагентов КАК ИсторияКППКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты.ИсторияКПП КАК КонтрагентыИсторияКПП
	|		ПО ИсторияКППКонтрагентов.Контрагент = КонтрагентыИсторияКПП.Ссылка
	|			И ИсторияКППКонтрагентов.Период = КонтрагентыИсторияКПП.Период
	|ГДЕ
	|	ИсторияКППКонтрагентов.ГоловнойКонтрагент
	|	ИЛИ ЕСТЬNULL(КонтрагентыИсторияКПП.КПП, ИсторияКППКонтрагентов.Контрагент.КПП) <> """"";
	
	СписокВыбораКПП.Очистить();
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПредставлениеКПП	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 (%2)", ?(ПустаяСтрока(Выборка.КПП), "<не задан>", Выборка.КПП), Выборка.ПредставлениеКонтрагента);
		СписокВыбораКПП.Добавить(Выборка.КПП, ПредставлениеКПП);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения()
	
	Если ПроверитьЗаполнение() Тогда
		Модифицированность	= Ложь;
		
		ЗначениеВыбора	= Новый Структура("КППКонтрагента", КППКонтрагента);
			
		ОповеститьОВыборе(ЗначениеВыбора);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохраненияДанныхЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьИзменения();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры
