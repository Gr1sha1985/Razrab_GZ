#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ШаблоныПисьма = Параметры.ШаблоныПисьма;
	Если ШаблоныПисьма.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Не заданы шаблоны для этого письма'");
	КонецЕсли;
	
	ТекущийИндекс = 0;
	Для каждого ШаблонПисьма Из ШаблоныПисьма Цикл
		Элементы.ТемаПисьма.СписокВыбора.Добавить(ШаблонПисьма.Представление);
		Если ШаблонПисьма.Представление = Параметры.ТемаПисьма Тогда
			ТекущийИндекс = Элементы.ТемаПисьма.СписокВыбора.Количество() - 1;
		КонецЕсли;
	КонецЦикла;
	
	ТемаПисьма = ШаблоныПисьма[ТекущийИндекс].Представление;
	УстановитьТелоПисьма(ШаблоныПисьма[ТекущийИндекс].Значение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТемаПисьмаПриИзменении(Элемент)
	
	Для каждого ШаблонПисьма Из ШаблоныПисьма Цикл
		Если ТемаПисьма = ШаблонПисьма.Представление Тогда
			УстановитьТелоПисьма(ШаблонПисьма.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТемаПисьмаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьШаблон(Команда)
	
	Для каждого ШаблонПисьма Из ШаблоныПисьма Цикл
		Если ТемаПисьма = ШаблонПисьма.Представление Тогда
			ОповеститьОВыборе(ШаблонПисьма);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТелоПисьма(ТекстПисьма)

	ТелоПисьма.УстановитьHTML(ТекстВHTML(ТекстПисьма), Новый Структура);

КонецПроцедуры

&НаСервере
Функция ТекстВHTML(Текст)
	
	Если СтрНайти(НРег(Текст), "</html>", НаправлениеПоиска.СКонца) > 0 Тогда
		Возврат Текст;
	КонецЕсли;
	
	ДокументHTML = Новый ДокументHTML;
	
	ЭлементТело = ДокументHTML.СоздатьЭлемент("body");
	ДокументHTML.Тело = ЭлементТело;
	
	Для НомерСтроки = 1 По СтрЧислоСтрок(Текст) Цикл
		Строка = СтрПолучитьСтроку(Текст, НомерСтроки);
		
		ЭлементБлок = ДокументHTML.СоздатьЭлемент("p");
		ЭлементТело.ДобавитьДочерний(ЭлементБлок);
		
		ЭлементТекст = ДокументHTML.СоздатьТекстовыйУзел(Строка);
		ЭлементБлок.ДобавитьДочерний(ЭлементТекст);
	КонецЦикла;
	
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьHTML = Новый ЗаписьHTML;
	ЗаписьHTML.УстановитьСтроку();
	ЗаписьDOM.Записать(ДокументHTML, ЗаписьHTML);
	Результат = ЗаписьHTML.Закрыть();
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
