////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для каждого ОтветственноеЛицо Из Перечисления.ОтветственныеЛицаОрганизаций Цикл
		ВидыОтветственныхЛиц.Добавить(ОтветственноеЛицо);
	КонецЦикла;
	
	Если Параметры.Отбор.Свойство("СтруктурнаяЕдиница") Тогда
		ОтборСтруктурнаяЕдиница = Параметры.Отбор.СтруктурнаяЕдиница;
		ЭлементОтбораСтруктурнаяЕдиница = ОтборыСписковКлиентСервер.ЭлементОтбораСпискаПоИмени(Список, "СтруктурнаяЕдиница");
		Если ЭлементОтбораСтруктурнаяЕдиница <> Неопределено Тогда
			ЭлементОтбораСтруктурнаяЕдиница.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		КонецЕсли;
		Элементы.ГруппаОтборСтруктурнаяЕдиница.Видимость = Истина;
		Если ТипЗнч(ОтборСтруктурнаяЕдиница) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
			Элементы.ОтборСтруктурнаяЕдиница.Заголовок = НСтр("ru='Подразделение'");
		КонецЕсли;
	Иначе
		ОбщегоНазначенияБПВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтаФорма,, "СтруктурнаяЕдиница");
		Элементы.ГруппаОтборСтруктурнаяЕдиница.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ОтветственноеЛицо") Тогда
		НайденноеЗначение	= ВидыОтветственныхЛиц.НайтиПоЗначению(Параметры.Отбор.ОтветственноеЛицо);
		Если НЕ НайденноеЗначение = Неопределено Тогда
			Элементы.ВидыОтветственныхЛиц.ТекущаяСтрока	= НайденноеЗначение.ПолучитьИдентификатор();
		КонецЕсли;
		Параметры.Отбор.Удалить("ОтветственноеЛицо");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ВидыОтветственныхЛицПриАктивизацииСтроки(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ОтветственноеЛицо", Элемент.ТекущиеДанные.Значение, Истина);
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)

	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "СтруктурнаяЕдиница");

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	
	Оповестить("ИзменениеОтветственныхЛиц", ОтборСтруктурнаяЕдиница);
	Оповестить("ИзмененСписокОтветственныхЛиц", ОтборСтруктурнаяЕдиница);
	
КонецПроцедуры


